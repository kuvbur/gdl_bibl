
!---------------!
! Battered Wall !
!---------------!

material gs_mat

if ac_refSide then
	dir=-1
else
	dir=1
endif

group "battered wall"
	if ac_wall_radius<eps then
	!! Straight Wall
		addx -over_beg
		rotx 90
		roty 90

		prism 4, ac_wall_length+over_beg+over_end,
			0,yl,
			0,yh,
			xh*dir,yh,
			xl*dir,yl

		del 3
	else
	!! Curved Wall

		resol ac_wall_resol

		roty -90
		rotx 180
		addy -ac_wall_radius
		rotx -overang_1

		Revolve 5, 180*ac_wall_length/ac_wall_radius/pi+overang_1+overang_2, 4+8+16+32,
				yl,ac_wall_radius,      2,
				yh,ac_wall_radius,      2,
				yh,ac_wall_radius-xh*dir,      2,
				yl,ac_wall_radius-xl*dir,      2,
				yl,ac_wall_radius,      2

		del 4
	endif
endgroup
if gs_gdl_debug then
	model wire
	placegroup "battered wall"
	model solid
endif

!!----------------!!!----------------!!!----------------!!
!! Cutting Bodies !!! Cutting Bodies !!! Cutting Bodies !!
!!----------------!!!----------------!!!----------------!!

group "cutting body"
	dim cut_set[5]
	if uwbp then cut_set[1]=1 else cut_set[1]=0	! Lower Cut
	if uwtp then cut_set[2]=1 else cut_set[2]=0	! Upper Cut
	cut_set[3]=1	! End Cuts
	cut_set[4]=1	! Window/Door
	cut_set[5]=1	! Connecting Walls

	call "Wall Cutting Body" parameters ref_thickness=ref_thickness, gs_mat=gs_mat, cut_set=cut_set,
										ac_accessory_debug=ac_accessory_debug, gs_gdl_debug=gs_gdl_debug,
										ac_wall_height=ac_wall_height, ac_wall_radius=ac_wall_radius, ac_wall_length=ac_wall_length,
										ac_wall_resol=ac_wall_resol, ac_refside=ac_refside, ac_angles=ac_angles, ac_side_poly=ac_side_poly,
										ac_top_poly=ac_top_poly, ac_bot_poly=ac_bot_poly,
										ac_wd_poly=ac_wd_poly, ac_curved_wall=ac_curved_wall, ac_cont_pen=ac_cont_pen,
										bot_ext=bot_ext, top_ext=top_ext
endgroup
if gs_gdl_debug then
!	model wire
	placegroup "cutting body"
!	model solid
endif

!!------------------!!
!! Solid Operations !!
!!------------------!!

wall_1=subgroup("battered wall","cutting body")
placegroup wall_1

base
vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1
coor 2, -1, -2, -3, -4
body 1


end
