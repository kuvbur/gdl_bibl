call "Элемент" parameters all
krat_txt_m = krat * 1
endd=0
dim null1[]

values "corner_xtch" RANGE (0,ax_vn)
values "corner_ytch" RANGE (0,by_vn)
if corner_xtch = 10/1000 then PARAMETERS corner_xtch = ax_vn/2
if corner_ytch = 10/1000 then PARAMETERS corner_ytch = by_vn/2

if corner_xvn = 0 then PARAMETERS corner_xvn = ax_vn/2
if corner_yvn = 0 then PARAMETERS corner_yvn = by_vn/2

values "marka_bet" "B10","B15","B20","B25","B30","B35","B40","B45","B50","B55","B60"
values "n1" 2.5,2
values "vid_arm" "Растянутая", "Сжатая"
if vid_arm="Растянутая" then
	values "alpha" 1,1.2
else
	values "alpha" 0.9,0.75
endif

if not(otobr_gran) then hideparameter 'p_gran', 'lin_gran'

if marka_bet="B10" then Rbt=0.56
if marka_bet="B15" then Rbt=0.75
if marka_bet="B20" then Rbt=0.9
if marka_bet="B25" then Rbt=1.05
if marka_bet="B30" then Rbt=1.15
if marka_bet="B35" then Rbt=1.3
if marka_bet="B40" then Rbt=1.4
if marka_bet="B45" then Rbt=1.5
if marka_bet="B50" then Rbt=1.6
if marka_bet="B55" then Rbt=1.7
if marka_bet="B60" then Rbt=1.8

if strstr(marka,"240")<>0 then Rs=215
if strstr(marka,"400")<>0 then Rs=355
if strstr(marka,"500")<>0 then Rs=435
l_nahl_r=kisp*alpha*(Rs*diam_arm)/(n1*n2*Rbt*us)
PARAMETERS lnahlauto=round_int(l_nahl_r*200)/200

krat = 1
!=======================Настройка типа и режима==========================================
parameters n_tip_el=2
if n_tip_el=2 then values "redakt" "Границ и защитных слоёв", "Расположения стержней","Печать"
if redakt="Границ и защитных слоёв" then parameters mode_red=1
if redakt="Расположения стержней" then parameters mode_red=2
if redakt="Печать" then parameters mode_red=5
VALUES "gs_detlevel_3D" 	`Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 	2, 1, 0
IF GLOB_MODPAR_NAME = "gs_detlevel_3D" THEN
	IF gs_detlevel_3D = `Откл.` THEN PARAMETERS gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN PARAMETERS gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Детальный` THEN PARAMETERS gs_detlevel_3D_m = 2
ELSE
	IF gs_detlevel_3D_m = 0 THEN PARAMETERS gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN PARAMETERS gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN PARAMETERS gs_detlevel_3D = `Детальный`
ENDIF
!==================Допустимые границы защитных слоёв======================================
if ankorzash then
	if uselauto then
		PARAMETERS lankmun = lnahlauto
		HIDEPARAMETER 'lankmun'
	endif
	if shag_osn="По Y" then PARAMETERS zash_x_1 = -lankmun, zash_x_2 = -lankmun, zash_y_1 = 0, zash_y_2 = 0
	if shag_osn="По X" then PARAMETERS zash_y_1 = -lankmun, zash_y_2 = -lankmun, zash_x_1 = 0, zash_x_2 = 0
	if shag_osn="По Z" then PARAMETERS zash_z_1 = -lankmun, zash_z_2 = -lankmun
else
	HIDEPARAMETER 'uselauto','lankmun'
	if odin_zash_tor=0 then
		values "zash_x_1" RANGE (0,ogr_x-zash_x_2) STEP 0,0.001	
		values "zash_y_1" RANGE (0,ogr_y-zash_y_2) STEP 0,0.001	
		values "zash_x_2" RANGE (0,ogr_x-zash_x_1) STEP 0,0.001	
		values "zash_y_2" RANGE (0,ogr_x-zash_x_1) STEP 0,0.001 	
	else
		values "zash_x_1" RANGE (0,ogr_x-zash_x_2) STEP 0,0.001	
		PARAMETERS zash_y_1=zash_x_1,zash_y_2=zash_x_1,zash_x_2=zash_x_1
	endif
	values "zash_z_1" RANGE (0,ogr_z-zash_z_2) STEP 0,0.001
	values "zash_z_2" RANGE (0,ogr_z-zash_z_1) STEP 0,0.001
endif
!==================Допустимые границы огриничивающей области==============================
values "ogr_x" RANGE (0,] STEP 0,krat_txt
values "ogr_y" RANGE (0,] STEP 0,krat_txt
values "ogr_z" RANGE (0,] STEP 0,krat_txt
PARAMETERS coord_vnsh=coord_vnsh, coord_vn=coord_vn
!---------Размеры ограничивающей области
PARAMETERS coord_ogr=coord_ogr
PARAMETERS ax_vnsh=-coord_vnsh[1][1]+coord_vnsh[4][1]
PARAMETERS by_vnsh=-coord_vnsh[1][2]+coord_vnsh[2][2]
PARAMETERS hz_vnsh=-coord_vnsh[1][3]+coord_vnsh[5][3]
!---------Размеры ограничивающей области по защитным слоям
PARAMETERS ax_vn=-coord_vn[1][1]+coord_vn[4][1]
PARAMETERS by_vn=-coord_vn[1][2]+coord_vn[2][2]
PARAMETERS hz_vn=-coord_vn[1][3]+coord_vn[5][3]
!==================Выбор основоного и доп. шага==========================================
values "shag_osn" "По X","По Y","По Z"
if shag_osn="По X" then values "shag_dop" "По X","По Z"
if shag_osn="По Y" then values "shag_dop" "По Y","По Z"
if shag_osn="По Z" then values "shag_dop" "По X","По Y"
PARAMETERS min_rast_svet_diam=min_rast_svet+diam_arm !Минимальное расстояние между стержнями в осях
if perem_shag=1 then HIDEPARAMETER 'shag_x_dop','shag_y_dop','shag_z_dop'
if perem_shag=0 then HIDEPARAMETER 'shag_x_dopm','shag_y_dopm','shag_z_dopm'

if odin_shag=1 then 
	PARAMETERS shag_x_osn=ax_vn
	PARAMETERS shag_y_osn=by_vn
	PARAMETERS shag_z_osn=hz_vn
	HIDEPARAMETER 'shag_x_osn','shag_y_osn','shag_z_osn'
max1 = max (min_rast_svet_diam,ax_vn) : min1 = min(min_rast_svet_diam,ax_vn)
	values 'shag_x_dop' shag_x_osn,range [min1,max1] !!STEP min_rast_svet_diam,krat
max1 = max (min_rast_svet_diam,by_vn) : min1 = min(min_rast_svet_diam,by_vn)
	values 'shag_y_dop' shag_y_osn,range [min1,max1] !!STEP min_rast_svet_diam,krat
max1 = max (min_rast_svet_diam,hz_vn) : min1 = min(min_rast_svet_diam,hz_vn)
	values 'shag_z_dop' shag_z_osn,range [min1,max1] !STEP min_rast_svet_diam,krat
else
max1 = max (min_rast_svet_diam*2,ax_vn) : min1 = min(min_rast_svet_diam*2,ax_vn)
	values "shag_x_osn" ax_vn,range [min1,max1] STEP 0,krat_txt
	values 'shag_x_dop' shag_x_osn,range [min1,max1] !!STEP min_rast_svet_diam,krat
max1 = max (min_rast_svet_diam*2,by_vn) : min1 = min(min_rast_svet_diam*2,by_vn)
	values "shag_y_osn" by_vn,range [min1,max1] STEP 0,krat_txt
	values 'shag_y_dop' shag_y_osn,range [min1,max1] !!STEP min_rast_svet_diam,krat
max1 = max (min_rast_svet_diam*2,hz_vn) : min1 = min(min_rast_svet_diam*2,hz_vn)
	values "shag_z_osn" hz_vn,range [min1,max1] STEP 0,krat_txt
	values 'shag_z_dop' shag_z_osn,range [min1,max1] !STEP min_rast_svet_diam,krat

endif

if GLOB_MODPAR_NAME="odin_shag" then gosub "regen"

!==================Для прямолинейного стержня =============================
PARAMETERS rX=(shag_osn="По X" or shag_dop="По X")
PARAMETERS rY=(shag_osn="По Y" or shag_dop="По Y")
PARAMETERS rZ= (shag_dop="По Z")
if odin_shag=1 then 
	values 'shag_ved' shag_x_dop, shag_y_dop, shag_z_osn
else
	values 'shag_ved' shag_x_osn, shag_y_osn, shag_z_osn
endif


if rX=0 then HIDEPARAMETER 'shag_x_dop','shag_x_dopm','shag_x_osn'
if rY=0 then HIDEPARAMETER 'shag_y_dop','shag_y_dopm','shag_y_osn'
if rZ=0 then HIDEPARAMETER 'shag_z_dop','shag_z_dopm','shag_z_osn'
if shag_osn="По X" then
	PARAMETERS xRot=-90
	PARAMETERS yRot=0
	!PARAMETERS zRot=0
	LOCK 'yRot','xRot','yRot'
	PARAMETERS dlin_st_model=by_vn
	PARAMETERS kol_vo_model=kol_vo_x*kol_vo_z
endif
if shag_osn="По Y" then
	PARAMETERS yRot=90
	PARAMETERS xRot=-90
	!PARAMETERS zRot=0
	LOCK 'yRot','xRot','yRot'
	PARAMETERS dlin_st_model=ax_vn
	PARAMETERS kol_vo_model=kol_vo_y*kol_vo_z
endif
	if shag_osn="По Z" then
	PARAMETERS yRot=0
	PARAMETERS xRot=0
	PARAMETERS zRot=0
	LOCK 'yRot','xRot','yRot'
	HIDEPARAMETER 'shag_z_dop','shag_z_dopm','shag_z_osn'
	PARAMETERS dlin_st_model=hz_vn
	PARAMETERS kol_vo_model=kol_vo_y*kol_vo_x
	PARAMETERS rX=1
	PARAMETERS rY=1
	PARAMETERS kol_vo_z=2
endif
!==================Показ дополнительных стержней==========================================
values "pokaz_osn_txt" "Да","   ","    ","Нет"
if pokaz_osn_txt="Нет" then parameters pokaz_osn=0
if pokaz_osn_txt="Да" then parameters pokaz_osn=1
!==================Настройки регенерации================================
if GLOB_MODPAR_NAME="perv_isp" then gosub "regen"
if GLOB_MODPAR_NAME="shag_osn" or GLOB_MODPAR_NAME="shag_dop" then gosub "regen" !Изменении основного направления
if GLOB_MODPAR_NAME="shag_x_osn" or GLOB_MODPAR_NAME="ogr_x" then gosub "perestr_X"
if GLOB_MODPAR_NAME="shag_y_osn" or GLOB_MODPAR_NAME="ogr_y" then gosub "perestr_Y"
if GLOB_MODPAR_NAME="shag_z_osn" or GLOB_MODPAR_NAME="ogr_z" then gosub "perestr_Z"
!====================При редактировании положения=====================
!--------------При перетаскивании основных стержней по X
if GLOB_MODPAR_NAME="shag_x_m" then 
	i=1
	parameters koord_x_all=koord_x_all
	parameters koord_x=koord_x
	parameters kol_vo_x=kol_vo_x
	for i=1 to n_x
		shag_1=shag_x_m[i]
		shag_2=shag_x_m[i]
		koord=koord_x[i]
		if i>1 and i<n_x then
			if koord>=coord_vn[3][1]-min_rast_svet_diam then shag_2=shag_1-koord+coord_vn[3][1]-min_rast_svet_diam !За границы нельзя
			if shag_1<min_rast_svet_diam then shag_2=min_rast_svet_diam
			shag_3 = round_int(shag_2 * krat_txt_m) / krat_txt_m
		else
			shag_3 = round_int(shag_2 * krat_txt_m) / krat_txt_m
		endif
		parameters shag_x_m[i]=shag_3
	next i
endif
if GLOB_MODPAR_NAME="shag_x_dopm" then 
	i=1
	parameters koord_x_all=koord_x_all
	parameters koord_x=koord_x
	parameters kol_vo_x=kol_vo_x
	for i=1 to n_x
		shag_3=shag_x_dopm[i]
		if shag_x_dopm[i]>shag_x_m[i] then shag_3=shag_x_m[i]
		if shag_x_dopm[i]<0 then shag_3=shag_x_m[i]/2
		parameters shag_x_dopm[i]= round_int(shag_3 * krat) / krat
	next i
endif
!--------------При перетаскивании основных стержней по Y
if GLOB_MODPAR_NAME="shag_y_m" then 
	i=1
	for i=1 to n_y
		shag_1=shag_y_m[i]
		shag_2=shag_y_m[i]
		koord=koord_y[i]
		if i>1 and i<n_y then
			if koord>=coord_vn[3][2]-min_rast_svet_diam then shag_2=shag_1-koord+coord_vn[3][2]-min_rast_svet_diam !За границы нельзя
			if shag_1<min_rast_svet_diam then shag_2=min_rast_svet_diam
			shag_3 = round_int(shag_2 * krat_txt) / krat_txt
		else
			shag_3 = round_int(shag_2 * krat_txt) / krat_txt
		endif
		parameters shag_y_m[i]=shag_3
	next i
endif
if GLOB_MODPAR_NAME="shag_y_dopm" then 
	i=1
	parameters koord_y_all=koord_y_all
	parameters koord_y=koord_y
	parameters kol_vo_y=kol_vo_y
	for i=1 to n_y
		shag_3=shag_y_dopm[i]
		if shag_y_dopm[i]>shag_y_m[i] then shag_3=shag_y_m[i]
		if shag_y_dopm[i]<0 then shag_3=shag_y_m[i]/2
		parameters shag_y_dopm[i]=round_int(shag_3 * krat) / krat
	next i
endif
!--------------При перетаскивании основных стержней по Z
if GLOB_MODPAR_NAME="shag_z_m" then 
	i=1
	for i=1 to n_z
		shag_1=shag_z_m[i]
		shag_2=shag_z_m[i]
		koord=koord_z[i]
		if i>1 and i<n_z then
			if koord>=coord_vn[5][3]-min_rast_svet_diam then shag_2=shag_1-koord+coord_vn[5][3]-min_rast_svet_diam !За границы нельзя
			if shag_1<min_rast_svet_diam then shag_2=min_rast_svet_diam
			shag_3 = round_int(shag_2 * krat_txt) / krat_txt
		else
			shag_3 = round_int(shag_2 * krat_txt) / krat_txt
		endif
		parameters shag_z_m[i]=shag_2
	next i
endif
if GLOB_MODPAR_NAME="shag_z_dopm" then 
	i=1
	parameters koord_z_all=koord_z_all
	parameters koord_z=koord_z
	parameters kol_vo_z=kol_vo_z
	for i=1 to n_z
		shag_3=shag_z_dopm[i]
		if shag_z_dopm[i]>shag_z_m[i] then shag_3=shag_z_m[i]
		if shag_z_dopm[i]<0 then shag_3=shag_z_m[i]/2
		parameters shag_z_dopm[i]=round_int(shag_3 * krat) / krat
	next i
endif
values "vid0" "тело", "стержень","участок","Откл"
values "vid1" "тело", "стержень","участок","Откл"
values "vid2" "тело", "стержень","участок","Откл"
!-----------Вычисления весов и длин-----------
parameters koord_x=koord_x
parameters koord_y=koord_y
parameters koord_z=koord_z

endd=1
if endd=0 then
	text2 0,0,"!!!!"
	"regen":
	parameters n_x=1
	parameters n_y=1
	parameters n_z=1
	gosub "perestr_X"
	gosub "perestr_Y"
	gosub "perestr_Z"
	parameters perv_isp=0
	return
endif

if endd=0 then
	!--------------Перестройка основных по Z
	!-------Заполняем значения основного шага
	"perestr_Z":
	parameters n_z=round_int(hz_vn/shag_z_osn)*(rZ<>0)+1
	i=1
	for i=1 to n_z
	parameters shag_z_m[i]=shag_z_osn !Рабочий массив
	parameters shag_z_dopm[i]=shag_z_osn/2
	if shag_z_m[i]*i>=coord_vn[7][3] then shag_z_m[i]=coord_vn[7][3]-r_arm-shag_z_m[i]*i !За границы нельзя
	next i
	return
endif
if endd=0 then
	!--------------Перестройка основных по Х
	!-------Заполняем значения основного шага
	"perestr_X":
	parameters n_x=round_int(ax_vn/shag_x_osn)*(rX<>0)+1
	i=1
	for i=1 to n_x
	parameters shag_x_m[i]=shag_x_osn !Рабочий массив
	!parameters shag_x_dopm[i]=shag_x_osn/2
	if shag_x_m[i]*i>=coord_vn[3][1] then shag_x_m[i]=coord_vn[3][1]-r_arm-shag_x_m[i]*i !За границы нельзя
	next i
	return
endif
if endd=0 then
	!--------------Перестройка основных по Y
	!-------Заполняем значения основного шага
	"perestr_Y":
	parameters n_y=round_int(by_vn/shag_y_osn)*(rY<>0)+1
	i=1
	for i=1 to n_y
	parameters shag_y_m[i]=shag_y_osn !Рабочий массив
	if shag_y_m[i]*i>=coord_vn[3][2] then shag_y_m[i]=coord_vn[3][2]-r_arm-shag_y_m[i]*i !За границы нельзя
	next i
	return
endif


