!------------------------
! Reinforcement
! 2012, Eptar Kft.
!------------------------

add2 addrot[1][1], addrot[1][2]
rot2 addrot[1][3]
unid=1
bnullhotspot=0
boffset=0
bshowraster=0
call "ironGlobal" parameters bislisting=bislisting,
							d=d,
							irontype=iron_type_stirrup,
							penironstirrup=penironstirrup,
							bcustom2d=bcustom2d,
							returned_parameters ipen
pen ipen
if istirrupsubtype<>type_stirrup_normal then
	call "ironMinMax" parameters bcomplex=0,
					_pointcoords=stirruppointcoords,
					_arccenter=_arccenter,
					_arcpoints=stirruparcpoints,
					_arcangle=_arcangle,
					_radiusdistance=_radiusdistance,
					minpolyx=minpolyx,
					maxpolyx=maxpolyx,
					minpolyy=minpolyy,
					maxpolyy=maxpolyy,
					returned_parameters minpolyx,maxpolyx,minpolyy,maxpolyy
					centerx=minpolyx+(maxpolyx-minpolyx)/2
					centery=minpolyy+(maxpolyy-minpolyy)/2
else
	centerx=hoopx/2
	centery=hoopy/2
	minpolyx=0
	minpolyy=0
	maxpolyx=hoopx
	maxpolyy=hoopy
endif
if glob_context=6 then
	del top
!	rot2 -SYMB_ROTANGLE
	if SYMB_MIRRORED then mul2 -1,1
	k=1.3
	mul2 k,k
	if stirrupSubType = ststirrupsubtype[1] then gosub "type1"
	if stirrupSubType = ststirrupsubtype[2] then gosub "type2"
	if stirrupSubType = ststirrupsubtype[3] then gosub "type3"
	if stirrupSubType = ststirrupsubtype[4] then gosub "type4"
	if stirrupSubType = ststirrupsubtype[5] then gosub "type5"
	if stirrupSubType = ststirrupsubtype[6] then gosub "type6"
	if stirrupSubType = ststirrupsubtype[7] then gosub "type7"
	if stirrupSubType = ststirrupsubtype[8] then gosub "type8"
	if stirrupSubType = ststirrupsubtype[9] then gosub "type9"
	if stirrupSubType = ststirrupsubtype[10] then gosub "type10"
	if stirrupSubType = ststirrupsubtype[12] then
		bshowdimtext=1
		heightfont=1.5
		iview=view_section
		benablehsedit=0
		gosub "SectionView"
	endif
	end
endif
if bsectionelevationcontext then
	ntrans=0
	if iposition=pos_ver then
		if bshowsection then
			gosub "SectionView"
		else
			minx=minpolyx
			maxx=maxpolyx
			miny=minpolyy
			maxy=maxpolyy
			cx=centerx
			cy=centery
			gosub "SideView"
		endif
	else
		rot2 90
		ntrans=1
		if bshoweditview then
			minx=minpolyy
			maxx=maxpolyy
			miny=minpolyx
			maxy=maxpolyx
			cx=centery
			cy=centerx
		else
			minx=minpolyx
			maxx=maxpolyx
			miny=minpolyy
			maxy=maxpolyy
			cx=centerx
			cy=centery
		endif
		gosub "SideView"
	endif
	if isigntype then
		if iposition=pos_ver then
			bshowraster=1
			gosub "DrawLabel"
		else
			gosub "DrawLabel"
		endif
	endif
	del ntrans
	end
endif
symbzpos=symb_pos_z
zoffset=1
if iposition=pos_ver then
	gosub "FloorView"
else
	gosub "SectionView"
endif
if isigntype then
	if iposition=pos_ver then
		bshowraster=1
		add2 0,centerx+stirrupoffsetx
		gosub "DrawLabel"
		del 1
	else
		if bshoweditview then
			bshowraster=1
			add2 centerx,zoffset
			gosub "EditViewHotspots"
			rot2 90+anglesection
			gosub "DrawLabel"
			del 3
		else
			bshowraster=0
			gosub "DrawLabel"
		endif
	endif
endif
if bshoweditview then
	if iposition=pos_ver then
		add2 0,-zoffset
		gosub "EditViewHotspots"
		rot2 anglesection
		gosub "SectionView"
		del 3
	else
		add2 centerx,zoffset
		add2 possection[1][1],possection[1][2]
		rot2 90+anglesection
		mul2 1,-1
		add2 0,-centerx
		gosub "FloorView"
		del 5
	endif
endif
del top
end

"EditViewHotspots":
	hotspot2 0,possection[1][2],unid,possection[1][1],1+128:unid=unid+1
	hotspot2 possection[1][1],possection[1][2],unid,possection[1][1],2:unid=unid+1
	hotspot2-1,possection[1][2],unid,possection[1][1],3:unid=unid+1
	hotspot2 possection[1][1],0,unid,possection[1][2],1+128:unid=unid+1
	hotspot2 possection[1][1],possection[1][2],unid,possection[1][2],2:unid=unid+1
	hotspot2 possection[1][1],-1,unid,possection[1][2],3:unid=unid+1
	add2 possection[1][1],possection[1][2]
	if abs(centerx)<eps then
	if abs(maxpolyx-minpolyx)<eps then
	hotspot2 0,0,unid,anglesection,6:unid=unid+1
	hotspot2 0,maxpolyy/2,unid,anglesection,4+128:unid=unid+1
	hotspot2 maxpolyy/2*sin(anglesection),maxpolyy/2*cos(anglesection),unid,anglesection,5:unid=unid+1
	else
	hotspot2 0,0,unid,anglesection,6:unid=unid+1
	hotspot2 maxpolyx,0,unid,anglesection,4+128:unid=unid+1
	hotspot2 maxpolyx*cos(anglesection),maxpolyx*sin(anglesection),unid,anglesection,5:unid=unid+1
	endif
	else
	if iposition=pos_ver then
	hotspot2 0,0,unid,anglesection,6:unid=unid+1
	hotspot2 centerx,0,unid,anglesection,4+128:unid=unid+1
	hotspot2 centerx*cos(anglesection),centerx*sin(anglesection),unid,anglesection,5:unid=unid+1
	else
	hotspot2 0,0,unid,anglesection,6:unid=unid+1
	hotspot2 centerx,0,unid,anglesection,4+128:unid=unid+1
	hotspot2 centerx*cos(anglesection),centerx*sin(anglesection),unid,anglesection,5:unid=unid+1
	endif
	endif
return
"SectionView":
	bnullhotspot=(istirrupsubtype<>type_stirrup_polygon)
	hotspot2 centerx,centery,unid:unid=unid+1
	if(istirrupsubtype=type_stirrup_normal)|\
	(istirrupsubtype=type_stirrup_type_2)|\
	(istirrupsubtype=type_stirrup_type_5)|\
	(istirrupsubtype=type_stirrup_type_6)|\
	(istirrupsubtype=type_stirrup_type_7) then
		hotspot2 0,0,unid,hoopx,1+128:unid=unid+1
		hotspot2 hoopx,0,unid,hoopx,2:unid=unid+1
		hotspot2-1,0,unid,hoopx,3:unid=unid+1
		hotspot2 0,hoopy,unid,hoopx,1+256:unid=unid+1
		hotspot2 hoopx,hoopy,unid,hoopx,2:unid=unid+1
		hotspot2-1,hoopy,unid,hoopx,3:unid=unid+1
		hotspot2 0,0,unid,hoopy,1+128:unid=unid+1
		hotspot2 0,hoopy,unid,hoopy,2:unid=unid+1
		hotspot2 0,-1,unid,hoopy,3:unid=unid+1
		hotspot2 hoopx,0,unid,hoopy,1+256:unid=unid+1
		hotspot2 hoopx,hoopy,unid,hoopy,2:unid=unid+1
		hotspot2 hoopx,-1,unid,hoopy,3:unid=unid+1
		if benabledist then
			hotspot2 0,hoopy,unid,rebardistdiag,1+128:unid=unid+1
			hotspot2-cos(45),hoopy+sin(45),unid,rebardistdiag,3:unid=unid+1
			hotspot2 rebardistdiag*cos(45),hoopy-rebardistdiag*sin(45),unid,rebardistdiag,2,rebardist:unid=unid+1
			hotline2 rebardist,hoopy-rebardist,hoopx-rebardist,hoopy-rebardist
			hotline2 rebardist,rebardist,hoopx-rebardist,rebardist
			hotline2 rebardist,rebardist,rebardist,hoopy-rebardist
			hotline2 hoopx-rebardist,rebardist,hoopx-rebardist,hoopy-rebardist
			hotline2 hoopx/2,hoopy-rebardist,hoopx/2,rebardist
			hotline2 rebardist,hoopy/2,hoopx-rebardist,hoopy/2
		endif
	endif
	if(istirrupsubtype=type_stirrup_type_8)|\
	(istirrupsubtype=type_stirrup_type_9) then
		hotspot2 0,0,unid,hoopx,1+128:unid=unid+1
		hotspot2-hoopx,0,unid,hoopx,2:unid=unid+1
		hotspot2 1,0,unid,hoopx,3:unid=unid+1
		hotspot2 0,hoopy,unid,hoopx,1+256:unid=unid+1
		hotspot2-hoopx,hoopy,unid,hoopx,2:unid=unid+1
		hotspot2 1,hoopy,unid,hoopx,3:unid=unid+1
		hotspot2 0,0,unid,hoopy,1+128:unid=unid+1
		hotspot2 0,hoopy,unid,hoopy,2:unid=unid+1
		hotspot2 0,-1,unid,hoopy,3:unid=unid+1
		hotspot2-hoopx,0,unid,hoopy,1+256:unid=unid+1
		hotspot2-hoopx,hoopy,unid,hoopy,2:unid=unid+1
		hotspot2-hoopx,-1,unid,hoopy,3:unid=unid+1
		if benabledist then
			hotline2-hoopx,hoopy-rebardist,-rebardist,hoopy-rebardist
			hotline2-rebardist,rebardist,-rebardist,hoopy-rebardist
			hotline2-hoopx,rebardist,-rebardist,rebardist
		endif
	endif
	if(istirrupsubtype=type_stirrup_type_3)|\
	(istirrupsubtype=type_stirrup_type_4) then
		hotspot2 0,0,unid,hoopy,1+256:unid=unid+1
		hotspot2 0,hoopy,unid,hoopy,2:unid=unid+1
		hotspot2 0,-1,unid,hoopy,3:unid=unid+1
	endif
	if(istirrupsubtype=type_stirrup_type_10) then
		hotspot2 0,0,unid,hoopx,1+128:unid=unid+1
		hotspot2-hoopx,0,unid,hoopx,2:unid=unid+1
		hotspot2 1,0,unid,hoopx,3:unid=unid+1
		hotspot2 0,0,unid,hoopy,1+128:unid=unid+1
		hotspot2 0,-hoopy,unid,hoopy,2:unid=unid+1
		hotspot2 0,1,unid,hoopy,3:unid=unid+1
		if benabledist then
			hotline2-hoopx,-rebardist,-rebardist,-rebardist
			hotline2-rebardist,-rebardist,-rebardist,-hoopy
		endif
	endif
	if(istirrupsubtype=type_stirrup_circle) then
		hotspot2 0,0,unid,hoopcircle2,1+128:unid=unid+1
		hotspot2 hoopcircle2,0,unid,hoopcircle2,2,hoopcircle:unid=unid+1
		hotspot2-1,0,unid,hoopcircle2,3:unid=unid+1
		if benabledist then
			hotarc2 0,0,hoopcircle/2-rebardist,0,360
		endif
	endif
	if(istirrupsubtype=type_stirrup_polygon) then
		boffset=1
	endif
	iview=view_section
	if iPosition = 1 and bShowDimText = 0 then
		project2 3,270,2
	else
		if iPosition = 1 then project2 3,270,2
		gosub "drawIron"
	endif
return
"FloorView":
	if n>1 then
	if iposition=pos_ver then
	_offset=0
	else
	_offset=centerx
	endif
	if iedittype<>editby_dist_n then
		hotspot2 startdist,_offset,unid,fulllength,1+256:unid=unid+1
		hotspot2 startdist+fulllength,_offset,unid,fulllength,2:unid=unid+1
		hotspot2 startdist-1,_offset,unid,fulllength,3:unid=unid+1
	else
		unid=unid+3
	endif
	if iedittype<>editby_l_n then
		hotspot2 startdist,_offset,unid,disthoops,1:unid=unid+1
		hotspot2 startdist+disthoops,_offset,unid,disthoops,2:unid=unid+1
		hotspot2 startdist-1,_offset,unid,disthoops,3:unid=unid+1
	else
	unid=unid+3
	endif
	endif
	add2 0,stirrupoffsetx*(iposition=pos_ver)
	if n>1 then
		hotspot2 0,maxpolyx-rebardist*benabledist,unid,startdist,1:unid=unid+1
		hotspot2 startdist,maxpolyx-rebardist*benabledist,unid,startdist,2:unid=unid+1
		hotspot2-1,maxpolyx-rebardist*benabledist,unid,startdist,3:unid=unid+1
		hotspot2 0,minpolyx+rebardist*benabledist,unid,startdist,1:unid=unid+1
		hotspot2 startdist,minpolyx+rebardist*benabledist,unid,startdist,2:unid=unid+1
		hotspot2-1,minpolyx+rebardist*benabledist,unid,startdist,3:unid=unid+1
		hotspot2 startdist+fulllength,maxpolyx-rebardist*benabledist,unid,enddist,1+128:unid=unid+1
		hotspot2 startdist+fulllength+enddist,maxpolyx-rebardist*benabledist,unid,enddist,2:unid=unid+1
		hotspot2 startdist+fulllength-1,maxpolyx-rebardist*benabledist,unid,enddist,3:unid=unid+1
		hotspot2 startdist+fulllength,minpolyx+rebardist*benabledist,unid,enddist,1+128:unid=unid+1
		hotspot2 startdist+fulllength+enddist,minpolyx+rebardist*benabledist,unid,enddist,2:unid=unid+1
		hotspot2 startdist+fulllength-1,minpolyx+rebardist*benabledist,unid,enddist,3:unid=unid+1
		hotline2 startdist,minpolyx+rebardist*benabledist,startdist+fulllength+enddist,minpolyx+rebardist*benabledist
		hotline2 startdist,maxpolyx-rebardist*benabledist,startdist+fulllength+enddist,maxpolyx-rebardist*benabledist
	endif
	pen ipen
	line_type ltiron
	if iDetLevel3D = 2 then
		type = (istirrupsubtype=type_stirrup_type_5)+(istirrupsubtype=type_stirrup_type_10)
		if type and view_part<>view_part_m[3] then
			if view_part=view_part_m[1] then
				if istirrupsubtype=type_stirrup_type_5 then circle2 0,0,r_arm
				if istirrupsubtype=type_stirrup_type_10 then 
					circle2 0,0,r_arm
				endif
			else
				if istirrupsubtype=type_stirrup_type_5 then circle2 0,otstup,r_arm
			endif
		else
			project2 3,270,2
		endif
		if istirrupsubtype=type_stirrup_type_5 then
			hotspot2 0,otstup
			hotspot2 0,0
		endif
	else
		add2 startdist,0
		for i=1 to n
			line2 0,minpolyx,0,maxpolyx		
			hotline2 0,minpolyx,0,maxpolyx
			add2 disthoops,0
		next i
		del n+2
	endif
return
"SideView":
	add2 0,stirrupoffsety
	if iedittype<>editby_dist_n then
		hotspot2 startdist,cy,unid,fulllength,1+256:unid=unid+1
		hotspot2 startdist+fulllength,cy,unid,fulllength,2:unid=unid+1
		hotspot2 startdist-1,cy,unid,fulllength,3:unid=unid+1
	else
		unid=unid+3
	endif
	if iedittype<>editby_l_n then
		hotspot2 startdist,cy,unid,disthoops,1:unid=unid+1
		hotspot2 startdist+disthoops,cy,unid,disthoops,2:unid=unid+1
		hotspot2 startdist-1,cy,unid,disthoops,3:unid=unid+1
	else
		unid=unid+3
	endif
	hotspot2 0,maxy-rebardist,unid,startdist,1:unid=unid+1
	hotspot2 startdist,maxy-rebardist,unid,startdist,2:unid=unid+1
	hotspot2-1,maxy-rebardist,unid,startdist,3:unid=unid+1
	hotspot2 0,minx+rebardist,unid,startdist,1:unid=unid+1
	hotspot2 startdist,minx+rebardist,unid,startdist,2:unid=unid+1
	hotspot2-1,minx+rebardist,unid,startdist,3:unid=unid+1
	hotspot2 startdist+fulllength,maxy-rebardist,unid,enddist,1+128:unid=unid+1
	hotspot2 startdist+fulllength+enddist,maxy-rebardist,unid,enddist,2:unid=unid+1
	hotspot2 startdist+fulllength-1,maxy-rebardist,unid,enddist,3:unid=unid+1
	hotspot2 startdist+fulllength,minx+rebardist,unid,enddist,1+128:unid=unid+1
	hotspot2 startdist+fulllength+enddist,minx+rebardist,unid,enddist,2:unid=unid+1
	hotspot2 startdist+fulllength-1,minx+rebardist,unid,enddist,3:unid=unid+1
	pen ipen
	line_type ltiron
	add2 startdist,0
	for i=1 to n
		if iDetLevel3D = 2 then
			rot2 rot_st
			add2 -d/2,0
			line2 0,miny,0,maxy
			add2 d,0
			line2 0,miny,0,maxy
			del 2
			line2 -d/2,miny,d/2,miny
			line2 -d/2,maxy,d/2,maxy
			del 1			
		endif
		if iDetLevel3D = 1 then line2 0,miny,0,maxy
		hotline2 0,miny,0,maxy
		add2 disthoops,0
	next i
	del n+2
return
"DrawLabel":
	pen penfont
	line_type ltiron
	if bshowraster then
		add2 fulllength/2+startdist,0
	else
		add2 maxpolyx,centery
	endif
	hotspot2 0,postextfloorplan[1][2],unid,postextfloorplan[1][1],1+128:unid=unid+1
	hotspot2 postextfloorplan[1][1],postextfloorplan[1][2],unid,postextfloorplan[1][1],2:unid=unid+1
	hotspot2-1,postextfloorplan[1][2],unid,postextfloorplan[1][1],3:unid=unid+1
	hotspot2 postextfloorplan[1][1],0,unid,postextfloorplan[1][2],1+128:unid=unid+1
	hotspot2 postextfloorplan[1][1],postextfloorplan[1][2],unid,postextfloorplan[1][2],2:unid=unid+1
	hotspot2 postextfloorplan[1][1],-1,unid,postextfloorplan[1][2],3:unid=unid+1
	if bshowraster then
		line2-fulllength/2,postextfloorplan[1][2],fulllength/2,postextfloorplan[1][2]
		add2-fulllength/2,postextfloorplan[1][2]
		_arrowl=0.03
		for i=1 to n
			_actpos=(i-1)*disthoops
			if _actpos>(fulllength/2+postextfloorplan[1][1]) then
				line2 _actpos,0,_actpos-_arrowl,_arrowl
				line2 _actpos,0,_actpos-_arrowl,-_arrowl
			else
				line2 _actpos,0,_actpos+_arrowl,_arrowl
				line2 _actpos,0,_actpos+_arrowl,-_arrowl
			endif
		next i
		del 1
	endif
	add2 postextfloorplan[1][1],postextfloorplan[1][2]
	if iposition=pos_hor&bshowraster then rot2-90
	call "ironLabel" parameters strid=strironid,
								labeltext1=strtext1,
								labeltext2=strtext2,
								penfont=penfont,
								labelpos=labelpos,
								labelangle=labelangle,
								labelmirrored=imirrored,
								stylefont=stylefont,
								heightfont=heightfont,
								bonlysign=bonlysign,
								unidstart=100000,
								isigntype=isigntype
	del 2+(iposition=pos_hor&bshowraster)
return
"drawIron":
	call "ironDrawer" parameters buseirondata=0,
								benablehsedit=benablehsedit,
								bnullhotspot=bnullhotspot,
								boffset=boffset,
								peniron=ipen,
								penfont=penfont,
								heightfontdim=heightfont,
								stylefont=stylefont,
								ltiron=ltiron,
								bshowdimtext=bshowdimtext,
								unid=unid,
								iview=iview,
								nstirrup=n,
								stirrupdist=disthoops,
								irontype=irontype,
								d=d,
								bendradius=bendradius,
								jointlength=jointlength,
								rebardist=rebardist,
								rebardistdiag=rebardistdiag,
								bdetailedarc=1,
								fulllength=fulllength,
								hoopx=hoopx,
								hoopy=hoopy,
								nstirruppoint=nstirruppoint,
								stirruppointcoords=stirruppointcoords,
								stirrupprevcoords=stirrupprevcoords,
								stirruparcpoints=stirruparcpoints,
								stirrupnewpoint=stirrupnewpoint,
								bclosedpolygon=bclosedpolygon,
								bstirrupback1=bstirrupback1,
								bstirrupback2=bstirrupback2,
								stirrupang1=stirrupang1,
								stirrupang2=stirrupang2,
	returned_parameters unid
return

"type1":
!Хомут 1
	razm_1 = str("%.0mm", hoopx - diam_arm)
	razm_2 = str("%.0mm", hoopy - diam_arm)
	lapka = str("%.0mm", jointLength + bendRadius)
	r = str("%.0mm", rebarDist)
	call "Хомут_1", parameters razm_1=razm_1,razm_2=razm_2,lapka=lapka,r=r
return

"type2":
	razm_1 = 1
return

"type3":
	razm_1 = 1
return

"type4":
	razm_1 = 1
return

"type5":
	razm_1 = str("%.0mm", t_perekr)
	razm_2 = str("%.0mm", h_vipusk)
	razm_3 = str("%.0mm", h_et+h_vipusk+t_perekr)
	razm_4 = str("%.0mm", otstup)
	call "Выпуск_колонны", parameters razm_1=razm_1,razm_2=razm_2,razm_3=razm_3,razm_4=razm_4
return

"type6":
	razm_1 = 1
return

"type7":
	razm_1 = 1
return

"type8":
	razm_1 = 1
return

"type9":
	razm_1 = 1
return

"type10":
!Г-образный
	razm_1 = str("%.0mm", hoopx - diam_arm)
	razm_2 = str("%.0mm", hoopy - diam_arm)
	r = str("%.0mm", rebarDist)
	call "Г-образный", parameters razm_1=razm_1,razm_2=razm_2, r=r
return