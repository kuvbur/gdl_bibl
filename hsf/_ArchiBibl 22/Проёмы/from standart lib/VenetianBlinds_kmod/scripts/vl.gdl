
if gs_sunShade_iType = SUNSHADE_VENETIAN then
	gosub "paramSettingsForInLintel"
else
	lock	"gs_shutLam2_boxHeight",
			"gs_shutLam2_boxWidth",
			"gs_shutLam2_distFromWindow",
			"gs_shutLam2_distFromHead",
			"gs_shutLam2_axisFromHead",
			"gs_shutLam2_segmentWidth",
			"gs_shutLam2_rotation"

	hideparameter	"gs_shutLam2_boxHeight",
			"gs_shutLam2_boxWidth",
			"gs_shutLam2_distFromWindow",
			"gs_shutLam2_distFromHead",
			"gs_shutLam2_axisFromHead",
			"gs_shutLam2_segmentWidth",
			"gs_shutLam2_rotation"
endif


if gs_sunShade_iType = SUNSHADE_EXTVENETIAN then
	gosub "paramSettingsForExtVenetian"
else
	lock	"gs_shutLam_placement",
			"gs_shutLam_iPlacement",
			"gs_shutLam_boxHeight",
			"gs_shutLam_boxWidth",
			"gs_shutLam_bOpenBox",
			"gs_shutLam_distFromWall",
			"gs_shutLam_coverWidth",
			"gs_shutLam_distFromHead",
			"gs_shutLam_cutWallBehind",
			"gs_shutLam_segmentWidth",
			"gs_shutLam_rotation",
			"gs_shutLam_bRail",
			"gs_shutLam_railPos"

	hideparameter	"gs_shutLam_placement",
			"gs_shutLam_boxHeight",
			"gs_shutLam_boxWidth",
			"gs_shutLam_bOpenBox",
			"gs_shutLam_distFromWall",
			"gs_shutLam_coverWidth",
			"gs_shutLam_distFromHead",
			"gs_shutLam_cutWallBehind",
			"gs_shutLam_segmentWidth",
			"gs_shutLam_rotation",
			"gs_shutLam_bRail",
			"gs_shutLam_railPos"

endif

if not((gs_shutLam_iPlacement = SHUTLAM_BEHIND) & gs_shutLam_cutWallBehind) then
	lock	"gs_sunShade_insFillType",
			"gs_sunShade_insFillPen",
			"gs_sunShade_insFillBackPen"

	hideparameter	"gs_sunShade_insFillType",
					"gs_sunShade_insFillPen",
					"gs_sunShade_insFillBackPen"
endif

values "gs_shutter_opHeight" range [0, sunShadeHeight3D + gs_shutLam_distFromHead]
values "gs_shutter_closedHeight" range [0, sunShadeHeight3D + gs_shutLam_distFromHead]

if GLOB_MODPAR_NAME = "gs_shutter_opHeight" then
	gs_shutter_closedHeight = sunShadeHeight3D + gs_shutLam_distFromHead - gs_shutter_opHeight
	parameters gs_shutter_closedHeight = gs_shutter_closedHeight
else
	gs_shutter_opHeight = max (0, sunShadeHeight3D + gs_shutLam_distFromHead - gs_shutter_closedHeight)
	parameters gs_shutter_opHeight = gs_shutter_opHeight
endif


! ------------------------------------------------------------------------------
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ------------------------------------------------------------------------------


! ==============================================================================
"paramSettingsForExtVenetian":
! ==============================================================================

	dim enabledVenetianConst[]
	dim iEnabledVenetianconst[]
	nEnabledTypes = 0

	for i = 1 to vardim1(stShutLamPlacement)
		if bittest (gs_shutLam_placementEnable, i-1) then
			nEnabledTypes = nEnabledTypes + 1
			enabledVenetianConst[nEnabledTypes] = stShutLamPlacement[i]
			iEnabledVenetianconst[nEnabledTypes] = i
		endif
	next i

	values "gs_shutLam_placement" enabledVenetianConst
	values "gs_shutLam_iPlacement" iEnabledVenetianconst

	if GLOB_MODPAR_NAME = "gs_shutLam_placement" then
		gs_shutLam_iPlacement = SHUTLAM_ONFACADE
		if gs_shutLam_placement = stShutLamPlacement[2] then gs_shutLam_iPlacement = SHUTLAM_TOWINDOW
		if gs_shutLam_placement = stShutLamPlacement[3] then gs_shutLam_iPlacement = SHUTLAM_BEHIND
		parameters gs_shutLam_iPlacement = gs_shutLam_iPlacement
	else
		parameters gs_shutLam_placement = stShutLamPlacement[gs_shutLam_iPlacement]
	endif

	values "gs_shutLam_rotation" range [0, 85]


	! -----------------------------------------------------------------------------
	! Defaults by Placement
	! -----------------------------------------------------------------------------

	if GLOB_MODPAR_NAME = "gs_shutLam_placement" | GLOB_MODPAR_NAME = "gs_shutLam_iPlacement" then
		if gs_shutLam_iPlacement = SHUTROLL_ONFACADE then
			gs_shutLam_distFromHead = 0
			parameters gs_shutLam_distFromHead = gs_shutLam_distFromHead
		endif
		if gs_shutLam_iPlacement = SHUTROLL_TOWINDOW then
			gs_shutLam_distFromHead = -gs_shutLam_boxHeight
			parameters gs_shutLam_distFromHead = gs_shutLam_distFromHead
		endif
		if gs_shutLam_iPlacement = SHUTROLL_BEHIND then
			gs_shutLam_distFromHead = gs_reveal_top
			parameters gs_shutLam_distFromHead = gs_shutLam_distFromHead
		endif
	endif

	! -----------------------------------------------------------------------------
	! Rail Position
	! -----------------------------------------------------------------------------

	values "gs_shutLam_railPos" stShutLamRailPos
	values "gs_shutLam_iRailPos" SHUTLAM_ONCASING, SHUTLAM_ONWALL

	if GLOB_MODPAR_NAME = "gs_shutLam_railPos" then
		gs_shutLam_iRailPos =  SHUTLAM_ONCASING
		if gs_shutLam_railPos = stShutLamRailPos[2] then gs_shutLam_iRailPos =  SHUTLAM_ONWALL
		parameters gs_shutLam_iRailPos = gs_shutLam_iRailPos
	else
		gs_shutLam_railPos = stShutLamRailPos[1]
		if gs_shutLam_iRailPos =  SHUTLAM_ONWALL then gs_shutLam_railPos = stShutLamRailPos[2]
		parameters gs_shutLam_railPos = gs_shutLam_railPos
	endif

	if not(gs_shutLam_bRail) then
		lock "gs_shutLam_railPos", "gs_shutLam_iRailPos"
	endif

	if gs_shutLam_iPlacement = SHUTLAM_ONFACADE then
		lock "gs_shutLam_railPos", "gs_shutLam_iRailPos", "gs_shutLam_showCasingBehind"
		hideparameter "gs_shutLam_railPos", "gs_shutLam_iRailPos", "gs_shutLam_showCasingBehind"
	endif

return


! ==============================================================================
"paramSettingsForInLintel":
! ==============================================================================

	if GLOB_MODPAR_NAME = "gs_shutLam2_axisFromHead" then
		gs_shutLam2_distFromHead = gs_shutLam2_axisFromHead - gs_shutLam2_boxHeight/2
		parameters gs_shutLam2_distFromHead = gs_shutLam2_distFromHead
	else
		gs_shutLam2_axisFromHead = gs_shutLam2_distFromHead + gs_shutLam2_boxHeight/2
		parameters gs_shutLam2_axisFromHead = gs_shutLam2_axisFromHead
	endif

	values "gs_shutLam2_rotation" range [0, 85]

return

