
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "A1FAC075-48BB-4F1F-8560-330D152D611A"
	_startID	= "B8B22D7B-B159-4697-8848-D20D50E0A2A1"
	_endID		= "AC96E5DC-2828-45F6-BEA0-0D6EF1B7D821"
gosub "newparameter_FWM"

	_startID	= "AC96E5DC-2828-45F6-BEA0-0D6EF1B7D821"
	_endID		= "CEDED6AC-8BB9-491B-A3A3-A10F544352E8"
gosub "repairHeight_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! Newparameter: dis_wall
! ==============================================================================
"newparameter_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		
		bSuccess  =	STORED_PAR_VALUE ("gs_gutter_dd", _gs_gutter_dd) &\
					STORED_PAR_VALUE ("gs_gutter_bb", _gs_gutter_bb) &\
					STORED_PAR_VALUE ("gs_gutter_ds_profile_num", _gs_gutter_ds_profile_num)

		if bSuccess then
			if _gs_gutter_ds_profile_num = 1 then 	!	if Downspout Profile is round
				dis_wall = _gs_gutter_dd
			else	!	if Downspout Profile is rectangular or corrugated
				dis_wall = _gs_gutter_bb
			endif

			parameters	dis_wall = dis_wall
		endif

		actualGuid = _endID
	endif
return

! ==============================================================================
! Head leader could be placed incorrrectly in 19
! ==============================================================================
"repairHeight_FWM":
	if actualGuid = _startID then

		bSuccess  =	STORED_PAR_VALUE ("ZZYZX", ZZYZX) &\
					STORED_PAR_VALUE ("gs_elbow", gs_elbow) &\
					STORED_PAR_VALUE ("zzyzx_elbow", zzyzx_elbow) &\
					STORED_PAR_VALUE ("gs_gutter_ds_lgth", gs_gutter_ds_lgth) &\
					STORED_PAR_VALUE ("gs_downspout_offset", gs_downspout_offset) &\
					STORED_PAR_VALUE ("DsOffsetLength", DsOffsetLength) &\
					STORED_PAR_VALUE ("gs_gutter_os_u_lgth", gs_gutter_os_u_lgth) &\
					STORED_PAR_VALUE ("gs_head_leader", gs_head_leader) &\
					STORED_PAR_VALUE ("HL_Height", HL_Height) &\
					STORED_PAR_VALUE ("gs_gutter_ds_ang", gs_gutter_ds_ang)

		minh_HL = 0.05		!from param script
		if _gs_gutter_ds_profile_num = 1 then minh_offset = _gs_gutter_dd else minh_offset = _gs_gutter_bb	!params set in prev. migration step
		
		!don't migrate angled downspout + offset, different geometry in 19
		if bSuccess &\
			((gs_downspout_offset | gs_head_leader) \
			 & abs(gs_gutter_ds_ang) > EPS \
			) then bSuccess = 0
		
		if bSuccess and gs_downspout_offset and gs_head_leader then
			offtop = gs_elbow * zzyzx_elbow + gs_gutter_ds_lgth + gs_downspout_offset * DsOffsetLength

			if offtop + min_HL > ZZYZX then	!repair offset upper length

				if offtop - gs_gutter_os_u_lgth + minh_offset <= ZZYZX - HL_Height then
					gs_gutter_os_u_lgth = gs_gutter_os_u_lgth - (offtop - (ZZYZX - HL_Height))
					DsOffsetLength = DsOffsetLength - (offtop - (ZZYZX - HL_Height))
					parameters	gs_gutter_os_u_lgth = gs_gutter_os_u_lgth,
								DsOffsetLength = DsOffsetLength
				else
					bSuccess = 0				!keep broken geometry
				endif
			else								!repair HL_Height
				HL_Height = ZZYZX - offtop
				parameters HL_Height = HL_Height
			endif
		endif

		if bSuccess then
			actualGuid = _endID
		else
			actualGuid = ""
		endif
	endif
return
