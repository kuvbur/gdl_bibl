values "min_qty" range[1,36]
MSFront = 1.5
if min_onwall_1 and min_qty<6 then MSFront = MSFront - MSFront * 0.4
if min_onwall_1 and min_qty>=6 then MSFront = MSFront + MSFront * 0.25
PARAMETERS MSFront = MSFront


MSAdditionalLength = 1.5
if min_onwall_2 and min_qty<6 then MSAdditionalLength = MSAdditionalLength - MSAdditionalLength * 0.4
if min_onwall_2 and min_qty>=6 then MSAdditionalLength = MSAdditionalLength + MSAdditionalLength * 0.25
PARAMETERS MSAdditionalLength = MSAdditionalLength

values "A" 0.8,0.9
parameters B = A
parameters ZZYZX = 0.3

values "gs_symbol_type" stSymbolTypes

values "gs_detlevel_3D" `Детальный`,`Простой`,`Черновой`,`Откл.`
values "gs_detlevel_3D_m" 2, 1, 3, 0
values "gs_detlevel_2D" `Зависит от масштаба`,`1:50`,`1:100`,`1:200`
values "gs_resol" RANGE [3,)

if gs_cont_pen<=0 then parameters gs_cont_pen=75
if gs_fill_pen<=0 then parameters gs_fill_pen=19

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Черновой` THEN gs_detlevel_3D_m = 3
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	IF gs_detlevel_3D_m = 3 THEN gs_detlevel_3D = `Черновой`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF


if gs_detlevel_3D_m = 1 then lock "gs_resol"


! Minimal Space
if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif

! --- start of modify to Shaft

IF isMEPEnabled THEN

	IF NOT(ConStatus_1) THEN
		LOCK "gs_con_mat"
		HIDEPARAMETER "gs_con_mat"
	ENDIF

	DIM shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	PARAMETERS MEP_NumberConnections = 1


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Соединение для сточной воды`

	ConDVecX = 0
	ConDVecY = 0
	ConDVecZ = -1

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	IF ConWidth_1 < 0.001 THEN
		ConWidth_1 = 0.001
		PARAMETERS ConWidth_1 = ConWidth_1
	ENDIF
	IF ConWidth_1 > 0.35*(MIN(A/0.90, B/0.90)) THEN
		ConWidth_1 = 0.35*(MIN(A/0.90, B/0.90))
		PARAMETERS ConWidth_1 = ConWidth_1
	ENDIF
	VALUES "ConWidth_1" RANGE (0, 0.35*(MIN(A/0.90, B/0.90))]
	PARAMETERS ConDepth_1 = ConWidth_1
	VALUES "ConLength_1" RANGE [0, ]


	PARAMETERS ConPosX_1 = 0.175*(A/0.90)
	PARAMETERS ConPosY_1 = 0.175*(B/0.90)
	PARAMETERS ConPosZ_1 = 0


	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 1


	PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	PARAMETERS MEP_ConInfo[ConID][2] = ConName
	PARAMETERS MEP_ConInfo[ConID][3] = "0"

	IF gs_ui_current_page = 3 THEN
		ac_mep_connectionpage_active = 1
	ELSE
		ac_mep_connectionpage_active = 0
	ENDIF
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active

	IF SetProgram THEN
		SetProgram = 0
		PARAMETERS SetProgram = SetProgram
	ENDIF
ELSE
	HIDEPARAMETER "gs_connection", "recipe", "gs_con_mat", "useSysMat"

	ac_mep_connectionpage_active = 0
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active
ENDIF

call "ui_tabcontrol" parameters controlType = 1,
								gs_ui_current_page = gs_ui_current_page

call "ui_plumbingfixtures" parameters iObjectType 		= 20,
								gs_ui_current_page = gs_ui_current_page
! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif
END
