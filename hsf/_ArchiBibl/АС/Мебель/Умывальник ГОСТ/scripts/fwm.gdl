actualGuid = FROM_GUID

! ==============================================================================
! New top height parameter /instead of zzyzx/ - (AC14 --> AC15)
! ==============================================================================

if actualGuid = "2D1C78B8-5DF6-4644-B420-086136F9BF79" then
	h_top=zzyzx
	parameters h_top=h_top
	actualGuid="F71DEBD7-64FF-47DE-A049-89BC044ABF77"
endif


! ==============================================================================
! Tap Type - (AC14 --> AC15) same as Basin 01
! ==============================================================================

if actualGuid = "F71DEBD7-64FF-47DE-A049-89BC044ABF77" then
	call "tapMigration_m" parameters iObjectType	= 10,
									gs_tap_type 	= gs_tap_type,
									iMigrationStep 	= 1,
									returned_parameters returnedActID

	if returnedActID = 1 then
		actualGuid = "4297AAE6-A5D8-4A9D-917E-5F2534635841"
	else
		actualGuid = ""
	endif
endif


! ==============================================================================
! M-133: missing and misplaced MEPconnections in some objects  - AC15 --> AC16
! ==============================================================================

if actualGUID = "4297AAE6-A5D8-4A9D-917E-5F2534635841" then

	if bWallmountedTap then
		pos1st_1 = -posTap - widthTap + ConWidth_1/2
		pos2nd_1 = -posTap - ConWidth_1/2

		pos3rd_1 = +posTap + ConWidth_1/2
		pos4th_1 = +posTap + widthTap - ConWidth_1/2

		pos1st_2 = -posTap - widthTap + ConWidth_2/2
		pos2nd_2 = -posTap - ConWidth_2/2

		pos3rd_2 = +posTap + ConWidth_2/2
		pos4th_2 = +posTap + widthTap - ConWidth_2/2

		posLeft = -posTap - widthTap/2
		posRight = posTap + widthTap/2


		if (ConPosX_1 < ConPosX_2) then
			if (ConPosX_1 < pos1st_1) | (ConPosX_1 > pos2nd_1) then
				ConPosX_1 = posLeft
				parameters ConPosX_1 = ConPosX_1
			endif
			if (ConPosX_2 < pos3rd_2) | (ConPosX_2 > pos4th_2) then
				ConPosX_2 = posRight
				parameters ConPosX_2 = ConPosX_2
			endif
		else
			if (ConPosX_1 < pos3rd_1) | (ConPosX_1 > pos4th_1) then
				ConPosX_1 = posRight
				parameters ConPosX_1 = ConPosX_1
			endif
			if (ConPosX_2 < pos1st_2) | (ConPosX_2 > pos2nd_2) then
				ConPosX_2 = posLeft
				parameters ConPosX_2 = ConPosX_2
			endif
		endif
	endif

	actualGUID = "221A8FDF-CB5E-4B63-8835-A41F3F4C2698"
endif


! ==============================================================================
! New integer parameter for Support Types and detlevel 2d - (AC15 --> AC16)
! ==============================================================================

if actualGuid = "221A8FDF-CB5E-4B63-8835-A41F3F4C2698" then
	for iSup = 1 to vardim1(stSupTypeForMigration)
		if suptyp = stSupTypeForMigration[iSup] then iSuptyp = iSup
	next iSup

	parameters iSuptyp = iSuptyp

	call "2dDetailLevel" parameters gs_detlevel_2d	= gs_detlevel_2d,
									iMigrationStep	= 1,
						returned_parameters returnedActID

	if returnedActID = 1 then
		actualGuid = "6E3F253D-C888-4D24-B2D3-5DC797C87755"
	else
		actualGuid = ""
	endif
endif


! ==============================================================================
! Set migration GUID
! ==============================================================================

SETMIGRATIONGUID actualGUID