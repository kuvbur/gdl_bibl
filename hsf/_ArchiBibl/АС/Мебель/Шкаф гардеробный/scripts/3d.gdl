IF gs_detlevel_3D_m=0 then END
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

IF gs_detlevel_3D_m=1 THEN gs_resol = 4
RESOL gs_resol

IF gs_shadow=0 THEN SHADOW OFF

!!!! END PANELS OVERHANG !!!
front_cab = 0
rear_cab = 0
left_cab = 0
right_cab = 0
dob_face = 0
count_th = 0
sink = 0
if rear_panel then rear_cab = rear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02


!! Height Editing
unID=1
hotspot a, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

!! Lower Door Height Editing

hotspot 0, 0, 0, unID, low_door_h, 1+128 : unID=unID+1
hotspot 0, 0, 0-1, unID, low_door_h, 3 : unID=unID+1
hotspot 0, 0, 0+low_door_h, unID, low_door_h, 2 : unID=unID+1

hotspot a, 0, 0, unID, low_door_h, 1+128 : unID=unID+1
hotspot a, 0, 0-1, unID, low_door_h, 3 : unID=unID+1
hotspot a, 0, 0+low_door_h, unID, low_door_h, 2 : unID=unID+1


PEN gs_cont_pen
MATERIAL gs_cabinet_mat


!!!!!****************************************** CALL GENERAL BASE CABINET

IF UpCabinet THEN 	!! Upper Cabinet
	temp_drawer_h=up_door_h-0.02
	temp_door_h=low_door_h-toe_h
ELSE
	temp_drawer_h=0
	temp_door_h=ZZYZX-toe_h
ENDIF

IF UpCabinet THEN
	ADD 0.02,fr_th1,low_door_h-0.01
	MATERIAL gs_ShelfMat
	PRISM_ 5,0.02,
			0,0,15,
			a-0.02*2,0,15,
			a-0.02*2,b-0.02-fr_th1,15,
			0,b-0.02-fr_th1,15,
			0,0,-1
	DEL 1
ENDIF

unID=100
basecab_unID=unID
CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx,
		gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=dob_face, count_th=count_th,
		front_cab=front_cab, rear_cab=rear_cab, left_cab=left_cab,
		right_cab=right_cab, toe_h=toe_h, side_vis_m=side_vis_m+1,
		sink=sink, shelf_num=shelf_num, gs_ShelfMat=gs_ShelfMat,
		gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
		toe_mat=toe_mat, gs_cabinet_mat=gs_cabinet_mat, drawer_h=temp_drawer_h,
		toe_d = toe_d,gs_toe_type_m=gs_toe_type_m,fr_th1=fr_th1,
		basecab_unID=basecab_unID,
		returned_parameters basecab_unID
		unID = basecab_unID

!!!!!****************************************** CALL DOOR PANEL
door_w=(a-left_cab-right_cab)/2
if low_door then
	ADD left_cab,front_cab+fr_th1,toe_h
	GOSUB "lower door"					!*** LOWER LEFT DOOR
	ADDx a-left_cab-right_cab
	mulx -1
	GOSUB "lower door"					!*** LOWER RIGHT DOOR
	del 1
	DEL 2
endif

if up_door and UpCabinet then
	ADD left_cab,front_cab+fr_th1,low_door_h

	if gs_knob_p_m=0 then	! vertical
		if gs_knob_type_m=4 then knob_pl_y_up=knob_pl_y_up+0.113
		if gs_knob_type_m>4 then knob_pl_y_up=knob_pl_y_up+0.1
	endif
	GOSUB "upper door"					!*** UPPER LEFT DOOR
	ADDx a-left_cab-right_cab
	mulx -1
	GOSUB "upper door"					!*** UPPER RIGHT DOOR
	del 1
	DEL 2
endif

!!!!!****************************************** CALL END PANELS

IF rear_panel and dob_face = 0 then
	door_w=(a-left_cab-right_cab)/2: door_h=ZZYZX-toe_h-count_th
	ADD door_w+left_cab,b-rear_cab,toe_h
	ROTZ 180
	GOSUB "end panel"
	ADDX -door_w
	GOSUB "end panel"
	DEL 3
ENDIF

IF left_panel then
	door_w=b-fr_th1-rear_cab: door_h=ZZYZX-toe_h-count_th		!*** LEFT END PANEL
	ADD left_cab,door_w+fr_th1,toe_h
	ROTZ -90
	GOSUB "end panel"
	DEL 2
ENDIF

IF right_panel then
	door_w=b-fr_th1-rear_cab: door_h=ZZYZX-toe_h-count_th
	ADD a-right_cab,fr_th1,toe_h
	ROTZ 90
	GOSUB "end panel"
	DEL 2
ENDIF

END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!! *** RETURN *** !!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! ==============================================================================
"lower door":

	cabdoortyp_unID=unID

	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=gs_full_edit,
		door_w=door_w, door_h=temp_door_h, gs_door_type_m=gs_door_type_m,gs_shadow=gs_shadow,
		doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
		doorglas_mat=doorglas_mat, gs_bevel=gs_bevel,
		vert_gnum=vert_gnum, hor_gnum=hor_gnum,fr_th1=fr_th1,
		doorframe_mat=doorframe_mat, door_ang=door_ang,gs_cust_p=gs_cust_p,
		gs_knob_type_m=gs_knob_type_m,gs_knob_p_m=gs_knob_p_m,knob_size=knob_size,
		knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
		gs_knob_mat=gs_knob_mat,
		cabdoortyp_unID=cabdoortyp_unID,
		gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
		gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
		returned_parameters cabdoortyp_unID

		unID = cabdoortyp_unID

RETURN

! ==============================================================================
"upper door":

	cabdoortyp_unID=unID

	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=gs_full_edit,
		door_w=door_w, door_h=up_door_h, gs_door_type_m=gs_door_type_m,gs_shadow=gs_shadow,
		doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
		doorglas_mat=doorglas_mat,
		vert_gnum=vert_gnum2, hor_gnum=hor_gnum2,fr_th1=fr_th1,
		doorframe_mat=doorframe_mat, door_ang=door_ang, gs_cust_p=gs_cust_p,
		UpperKnob=1,gs_bevel=gs_bevel,
		gs_knob_type_m=gs_knob_type_m,gs_knob_p_m=gs_knob_p_m,knob_size=knob_size,
		knob_pl_x_up=knob_pl_x_up,knob_pl_y_up=knob_pl_y_up,
		gs_knob_mat=gs_knob_mat,
		cabdoortyp_unID=cabdoortyp_unID,
		gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
		gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
		returned_parameters cabdoortyp_unID

		unID = cabdoortyp_unID

RETURN

!!!!!****************************************** END PANELS
"end panel":
	!! on rear panel there is no glas and grilles and knobs!!
	door_open=0
	gs_knob_type_m=0
	If end_panel_type=`Стиль 1` 	then gs_door_type_m=1
	If end_panel_type=`Стиль 2` 	then gs_door_type_m=2
	If end_panel_type=`Стиль 4` 	then gs_door_type_m=11
	If end_panel_type=`Стиль 7` 	then gs_door_type_m=4
	If end_panel_type=`Стиль 9` 	then gs_door_type_m=6
	If end_panel_type=`Спец. панель` 	then gs_door_type_m=10
	gs_full_edit=0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
		door_w=door_w, door_h=temp_door_h, gs_door_type_m=gs_door_type_m,gs_shadow=gs_shadow,
		doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
		doorglas_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
		doorframe_mat=doorframe_mat_end, gs_knob_type_m=gs_knob_type_m,knob_pl_y=low_door_h/2,
		gs_cust_p=gs_cust_p_end,gs_bevel=gs_bevel,
		cabdoortyp_unID=cabdoortyp_unID,
		gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
		gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	IF UpCabinet=1 THEN
		ADDZ low_door_h-toe_h

		cabdoortyp_unID=unID
		CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=door_w, door_h=up_door_h, gs_door_type_m=gs_door_type_m,gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorglas_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorframe_mat=doorframe_mat_end, gs_knob_type_m=gs_knob_type_m,knob_pl_y=low_door_h/2,
			gs_cust_p=gs_cust_p_end,gs_bevel=gs_bevel,
			cabdoortyp_unID=cabdoortyp_unID,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
			returned_parameters cabdoortyp_unID

		unID = cabdoortyp_unID

		DEL 1
	ENDIF

RETURN
