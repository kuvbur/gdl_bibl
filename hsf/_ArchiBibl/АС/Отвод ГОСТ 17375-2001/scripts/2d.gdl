!! Elbow Tube



pen gs_cont_pen
fill gs_fill_type

project2{2} 3, 270, 32+3, gs_back_pen, 0, 0, 90

if abs(angle_1)>=eps and abs(angle_3)>=eps then
	unID=1
	hotspot2 0,0,unID
	end
endif

!------------------!
! Angle of Bending !
!------------------!

arcus=0
if (abs(angle_1-90)<eps or abs(angle_1-270)<eps) and (abs(angle_3-90)>=eps and abs(angle_3-270)>=eps) then
	unID=1
	hotspot2 radius_1,0,unID,angle_2,6+512 : unID=unID+1
	hotspot2 0,0,unID,angle_2,4+256 : unID=unID+1
	hotspot2 (radius_1-radius_1*cos(angle_2)),0,unID,angle_2,5 : unID=unID+1

	line_type gs_DashedLine
	line2 0,radius_2-tube_thk,(radius_1-radius_1*cos(angle_2)),radius_2-tube_thk
	line2 0,-radius_2+tube_thk,(radius_1-radius_1*cos(angle_2)),-radius_2+tube_thk
	hotline2 0,radius_2-tube_thk,(radius_1-radius_1*cos(angle_2)),radius_2-tube_thk
	hotline2 0,-radius_2+tube_thk,(radius_1-radius_1*cos(angle_2)),-radius_2+tube_thk

	if gs_axis then
	pen gs_axis_pen
		line_type gs_axis_line
		line2 0,0,(radius_1-radius_1*cos(angle_2)),0
		hotline2 0,0,(radius_1-radius_1*cos(angle_2)),0
	endif
	pen gs_cont_pen

	arcus=1
else
	mul2 1,cos(angle_1)
endif

if (abs(angle_3-90)<eps or abs(angle_3-270)<eps) and (abs(angle_1-90)>=eps and abs(angle_1-270)>=eps) then
	unID=1
	hotspot2 radius_1,0,unID,angle_2,6+512 : unID=unID+1
	hotspot2 0,0,unID,angle_2,4+256 : unID=unID+1
	hotspot2 0,(radius_1*sin(angle_2)),unID,angle_2,5 : unID=unID+1

	line_type gs_DashedLine
	line2 radius_2-tube_thk,0,radius_2-tube_thk,radius_1*sin(angle_2)
	line2 -radius_2+tube_thk,0,-radius_2+tube_thk,radius_1*sin(angle_2)
	hotline2 radius_2-tube_thk,0,radius_2-tube_thk,radius_1*sin(angle_2)
	hotline2 -radius_2+tube_thk,0,-radius_2+tube_thk,radius_1*sin(angle_2)

	if gs_axis then
		pen gs_axis_pen
		line_type gs_axis_line
		line2 0,0,0,radius_1*sin(angle_2)
		hotline2 0,0,0,radius_1*sin(angle_2)
	endif
	pen gs_cont_pen

	arcus=1
else
	mul2 cos(angle_3),1
endif

if arcus=0 then
	line_type gs_DashedLine
	arc2 radius_1,0,radius_1+radius_2-tube_thk, 180-angle_2, 180
	arc2 radius_1,0,radius_1-radius_2+tube_thk, 180-angle_2, 180
	hotarc2 radius_1,0,radius_1+radius_2-tube_thk, 180-angle_2, 180
	hotarc2 radius_1,0,radius_1-radius_2+tube_thk, 180-angle_2, 180

	if gs_axis then
		pen gs_axis_pen
		line_type gs_axis_line
		arc2 radius_1,0,radius_1, 180-angle_2, 180
		hotarc2 radius_1,0,radius_1, 180-angle_2, 180

		unID=1
		hotspot2 radius_1,0,unID,angle_2,6+512 : unID=unID+1
		hotspot2 0,0,unID,angle_2,4+256 : unID=unID+1
		hotspot2 (radius_1-radius_1*cos(angle_2)),(radius_1*sin(angle_2)),unID,angle_2,5 : unID=unID+1
	endif
	pen gs_cont_pen
endif
del top

if abs(angle_1)<eps and abs(angle_3)<eps then
	for i=1 to 2
		hotspot2 0,0,unID,radius_2,1+128 : unID=unID+1
		hotspot2 -1,0,unID,radius_2,3 : unID=unID+1
		hotspot2 radius_2,0,unID,radius_2,2 : unID=unID+1

		hotspot2 0,0,unID,radius_2,1+128 : unID=unID+1
		hotspot2 1,0,unID,radius_2,3 : unID=unID+1
		hotspot2 -radius_2,0,unID,radius_2,2 : unID=unID+1

		add2 radius_1,0
		rot2 -angle_2
		add2 -radius_1,0
	next i
	del 6
endif

