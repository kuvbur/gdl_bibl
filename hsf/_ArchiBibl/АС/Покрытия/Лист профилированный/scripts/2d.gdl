if GLOB_CONTEXT=6 then
	gosub "showved"
	end
endif
gosub "set_show_2d"
if not(show) or det_lvl_2d=0 then end
pen gs_cont_pen
unID = 1

if bMigrationRotZ then
	add2 0,-waveHeight/2
	mul2 -1,-1
	rot2 90
endif

chStoryDist = GLOB_CH_STORY_DIST
IF gs_StorySensitive = 0 THEN chStoryDist = 0
if chStoryDist > EPS then !"Этажом выше"
	gs_cont_pen = gs_cont_pen_up
	gs_fill_type = gs_fill_type_up
	gs_fill_pen = gs_fill_pen_up
	gs_back_pen = gs_back_pen_up
	gs_line_type = gs_line_type_up
	mark = mark_up
endif
if chStoryDist < EPS then
	if abs(chStoryDist) < EPS then !"Собственный этаж"
	else !"Этажом ниже"
		gs_cont_pen = gs_cont_pen_down
		gs_fill_type = gs_fill_type_down
		gs_fill_pen = gs_fill_pen_down
		gs_back_pen = gs_back_pen_down
		gs_line_type = gs_line_type_down
		mark = mark_down
	endif
endif


!=============================================================================
! Hotspot Editing
!=============================================================================

hotspot2 0,	0, 			unID, width, 1+256 	: unID=unID+1
hotspot2 0,	-0.001, 	unID, width, 3+128	: unID=unID+1
hotspot2 0,	width, 		unID, width, 2		: unID=unID+1

if abs(rotation-90) < eps or abs(rotation+90) < eps then
	hotspot2 -A2,	0, 		unID, width, 1+256		: unID=unID+1
	hotspot2 -A2,	-0.001, unID, width, 3+128		: unID=unID+1
	hotspot2 -A2,	width,  unID, width, 2			: unID=unID+1
else
	hotspot2 A2,	0, 		unID, width, 1+256		: unID=unID+1
	hotspot2 A2,	-0.001, unID, width, 3+128		: unID=unID+1
	hotspot2 A2,	width, 	unID, width, 2			: unID=unID+1
endif


!=============================================================================
! 2D Symbol
!=============================================================================

set fill gs_fill_type
line_type gs_line_type

if abs(abs(rotation)-90)>eps then
	!--- Make Length stretchable
	hotspot2 0, 		0, unID, A2, 1+256	: unID=unID+1
	hotspot2 -0.001, 	0, unID, A2, 3+128	: unID=unID+1
	hotspot2 A2, 		0, unID, A2, 2		: unID=unID+1

	hotspot2 0, 		width, unID, A2, 1+256	: unID=unID+1
	hotspot2 -0.001, 	width, unID, A2, 3+128	: unID=unID+1
	hotspot2 A2, 		width, unID, A2, 2		: unID=unID+1

	line2 0,B/2,0.1,B/2

	if gs_detlevel_2D_m = DETLEVEL_2D_CONTOUR or gs_detlevel_2D_m = DETLEVEL_2D_SIMPLE then		! --- Contour and Simple 2D
		if angCut1 < EPS then cut_width_1 = EPS
		if angCut2 < EPS then cut_width_2 = EPS
		if angCut3 < EPS then cut_width_3 = EPS
		if angCut4 < EPS then cut_width_4 = EPS

		poly2_b 9, 1+2+4, gs_fill_pen, gs_back_pen,
				0,												cut_width_4,			1,
				0,												B-cut_width_3,			1,
				cut_width_3*tan(angCut3) * cos(rotation),		B,						1,
				A2-cut_width_1*tan(angCut1) * cos(rotation),	B,						1,
				A2,												B-cut_width_1,			1,
				A2,												cut_width_2,			1,
				A2-cut_width_2*tan(angCut2) * cos(rotation),	0,						1,
				cut_width_4*tan(angCut4) * cos(rotation),		0,						1,
				0,												cut_width_4,			-1

		if gs_detlevel_2D_m = DETLEVEL_2D_SIMPLE then
			if GLOB_SCALE < 201 then
				waveStep = waveLength*4
				if GLOB_SCALE < 101 then waveStep = waveLength*2
				if GLOB_SCALE < 51 	then waveStep = waveLength

				line_type wave_line_type
				sumw = 0
				for sumw=0 to B step waveStep
					if sumw>0 and sumw<B then
						line2 0, sumw, A2, sumw
					endif
				next sumw
			endif
		endif
	else											! --- Detailed 2D
		pen gs_fill_pen
		line_type gs_line_type
		set fill gs_fill_type
		project2{3} 3, 270, 3+32, 15, gs_back_pen, 0, 0, 90 parameters gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE, gs_resol = 12, bSimple3DProjection = 1

		pen gs_cont_pen
		project2{3} 3, 270, 2+32, 15, gs_back_pen, 0, 0, 90 parameters gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE, gs_resol = 12, bSimple3DProjection = 1

		if iProfiles = 5 then
			waveStep = waveLength

			line_type wave_line_type
			sumw = 0
			for sumw=0 to B step waveStep
				if sumw>0 and sumw<B then
					line2 0, sumw, A2, sumw
				endif
			next sumw
		endif
	endif

else												! --- Vertical Sheet
	line_type gs_line_type
	set fill gs_fill_type

	if gs_detlevel_2D_m = DETLEVEL_2D_CONTOUR or gs_detlevel_2D_m = DETLEVEL_2D_SIMPLE then		! --- Contour and Simple 2D
		project2{3} 3, 270, 32+3, 1+2+4+8, gs_back_pen, 0, 0, 90 parameters gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE, gs_resol = 12, bSimple3DProjection = 1
	else
		project2{3} 3, 270, 32+3, 1+2+4+8, gs_back_pen, 0, 0, 90 parameters gs_detlevel_3D_m = DETLEVEL_3D_DETAILED, gs_resol = 36
	endif
endif


if bMigrationRotZ then del 3


end


"showved":
	pen SYMB_SECT_PEN
	if GLOB_CONTEXT=6 then
		MUL2 A_*0.01, A_*0.01
	endif
	poly2_b{5}      10,      1,      0,      3,      1,      0, 
					   0,            0,            1,            0,            0,            1,            0, 
			1.274845300409, -2.285808895932E-016,      1, 
			1.169405193582, -1.529588720527E-016,      1, 
			1.091999625618, 0.2888815124317,      1, 
			0.8202683249954, 0.2888815124317,      1, 
			0.7428627570311, -6.829965017813E-017,      1, 
			0.5319825433778, -6.829965017813E-017,      1, 
			0.4545769754135, 0.2888815124317,      1, 
			0.182845674791, 0.2888815124317,      1, 
			0.1054401068266,            0,      1, 
					  -0,           -0,      1
return


"set_show_2d":
	show = 1
	call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
	if show=1 then show = show_subpos
	if show then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_metall", show_metall)
		if not(show_metall) then show = 0
	endif
	success = LIBRARYGLOBAL ("Libraryflobals-km", "ignore_id", ignore_id)
	success = LIBRARYGLOBAL ("Libraryflobals-km", "show_ingore", show_ingore)
	show_spec = 0 : IF STRSTR (GLOB_ID, ignore_id, 1) > 0 THEN show_spec = 1
	if not(show_ingore) and show_spec then show = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
	IF gs_detlevel_3D=`Простой` then
		det_lvl_2d = 1
		det_lvl_3d = 1
	endif
	IF gs_detlevel_3D=`Детальный` THEN
		det_lvl_2d = 2
		det_lvl_3d = 2
	endif
	IF gs_detlevel_3D=`Откл` THEN
		det_lvl_2d = 2
		det_lvl_3d = 0
	endif
return
