success = LIBRARYGLOBAL ("Libraryflobals-as", "gs_detlevel_3D_m", gs_detlevel_3D_m)

gosub "set_show_3d"
if not(show) or det_lvl_3d=0 then end

rotx xx
if gs_detlevel_3D_m = DETLEVEL_3D_OFF then end
if not(gs_shadow) then shadow off

if bMigrationRotZ then
	if (GLOB_CONTEXT <> 2 & GLOB_CONTEXT <> 5) then
		addy -waveHeight/2
		muly -1
		mulx -1
		rotz 90
	endif
endif

! --- Vertical Cut Length and Position Adjustment

length_proj = 0
pos_proj = 0

if ABS(rotation) > EPS then
	length_plus = ABS(waveHeight*TAN(rotation))

	if rotation > 0 then
		if iTopCut = 1 then
			length_proj = length_proj + length_plus
			pos_proj    = 0
		endif
	else
		if iBottomCut = 1 then
			length_proj = length_proj + length_plus
			pos_proj    = -length_plus
		endif
	endif
endif


!=============================================================================
! Hotspot Editing
!=============================================================================

unID = 1 + 3

!--- Angle Editing ---
hotspot 0, 						0,	0, 							unID,rotation, 6 		: unID=unID+1
hotspot 1, 						0,	0, 							unID,rotation, 4+128 	: unID=unID+1
hotspot -1, 					0,	0, 							unID,rotation, 7 		: unID=unID+1
hotspot length/2*cos(rotation), 0,	eps+length/2*sin(rotation), unID,rotation, 5 		: unID=unID+1

hotspot 0, 						B,	0, 							unID,rotation, 6 		: unID=unID+1
hotspot 1, 						B,	0, 							unID,rotation, 4+128 	: unID=unID+1
hotspot -1, 					B,	0, 							unID,rotation, 7 		: unID=unID+1
hotspot length/2*cos(rotation), B,	eps+length/2*sin(rotation), unID,rotation, 5 		: unID=unID+1


roty -rotation
!--- Length Editing
hotspot 0, 		0,		0, 	unID, length, 1+128 : unID=unID+1
hotspot -1, 	0,		0, 	unID, length, 3 	: unID=unID+1
hotspot length, 0,		0, 	unID, length, 2 	: unID=unID+1

hotspot 0, 		B/2, 	0,	unID, length, 1+256 : unID=unID+1
hotspot -1, 	B/2, 	0,	unID, length, 3 	: unID=unID+1
hotspot length, B/2, 	0,	unID, length, 2 	: unID=unID+1

hotspot 0, 		B,		0,	unID, length, 1+128 : unID=unID+1
hotspot -1, 	B,		0,	unID, length, 3 	: unID=unID+1
hotspot length, B,		0,	unID, length, 2 	: unID=unID+1

unID=unID+3
!--- Width Editing
hotspot 0,		0, 		0, 	unID, width, 1+256 	: unID=unID+1
hotspot 0,		-0.001, 0, 	unID, width, 3+128	: unID=unID+1
hotspot 0,		width,  0, 	unID, width, 2		: unID=unID+1

hotspot length,	0, 		0,	unID, width, 1+256	: unID=unID+1
hotspot length,	-0.001, 0, 	unID, width, 3+128	: unID=unID+1
hotspot length,	width, 	0, 	unID, width, 2		: unID=unID+1


!=============================================================================
! Feedback Mode
!=============================================================================

if GLOB_CONTEXT > 22 & GLOB_CONTEXT < 29 then
	block length, width, waveHeight

	end
endif

del 1


!=============================================================================
! Model Mode
!=============================================================================

addy B
cutplane -90		!! cutting width B
del 1

pen gs_cont_pen
material mat1

if abs(waveLength) < EPS then end

v = A
sumw = 0

if abs(rotation)>EPS and abs(abs(rotation)-90)>EPS then
	if iBottomCut = 1 then
		rotz 90
		cutplane -90
		del 1
	endif
	if iTopCut = 1 then
		addx A2
		rotz 90
		cutplane 90
		del 2
	endif
endif


roty -rotation
addx length
rotz 90
addy length
rotx 90

if angCut4 > EPS then
	roty 90
	add 0, 0, cut_width_4
	roty angCut4+90
	cutplane
	del 3
endif

if angCut3 > EPS then
	roty 90
	add 0, 0, B-cut_width_3
	roty 90-angCut3
	cutplane
	del 3
endif

if angCut2 > EPS then
	roty 90
	add -length, 0, cut_width_2
	roty -90-angCut2
	cutplane
	del 3
endif

if angCut1 > EPS then
	roty 90
	add -length, 0, B-cut_width_1
	roty angCut1-90
	cutplane
	del 3
endif

!!!	block width, waveHeight, length	!--- check cutting---


!--- Profile 1 ---

if iProfiles = 1 then
	alpha=atn((width_bottom-width_upper)/2/waveHeight)

	if gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE then
		for sumw=0 to B step waveLength
				put sumw,																				0.75*poc1*not(bSimple3DProjection),		bSimple3DProjection
				put sumw+poc1,																			0,										bSimple3DProjection
				put sumw+waveLength/2-width_bottom/2,													0,										0

			if gs_resol > 24 then
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha),								kiem_h1,								0
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha)-kiem*cos(alpha),				kiem_h1+kiem*sin(alpha),				0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha)-kiem*cos(alpha),				kiem_h2+kiem*sin(alpha),				0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha),								kiem_h2,								0
			endif

				put sumw+waveLength/2-width_upper/2,													waveHeight,								0
				put sumw+waveLength/2-poc2,																waveHeight,								bSimple3DProjection
				put sumw+waveLength/2,																	waveHeight-0.75*poc2*not(bSimple3DProjection),	bSimple3DProjection
				put sumw+waveLength/2+poc2,																waveHeight,								bSimple3DProjection
				put sumw+waveLength/2+width_upper/2,													waveHeight,								0

			if gs_resol > 24 then
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha),					kiem_h2,								0
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha)+kiem*cos(alpha),	kiem_h2+kiem*sin(alpha),				0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha)+kiem*cos(alpha),				kiem_h1+kiem*sin(alpha),				0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha),								kiem_h1,								0
			endif

				put sumw+waveLength/2+width_bottom/2,													0,										0
				put sumw+waveLength-poc1,																0,										bSimple3DProjection
				put sumw+waveLength,																	0.75*poc1*not(bSimple3DProjection),		bSimple3DProjection
		next sumw

		addz pos_proj
		extrude NSP/3,0,0,length+length_proj,48,
			get(NSP)
		del 1
	else
		thk_corr   = (width_upper/2-width_bottom/2)*(thickness/waveHeight)

				put sumw-poc1,																			0, thickness/2,							0

		for sumw=0 to B step waveLength
				put sumw,																				0, 0.75*poc1-thickness/2,				0
				put sumw+poc1,																			0, thickness/2,							0
				put sumw+waveLength/2-width_bottom/2-thk_corr,											0, thickness/2,							0

			if gs_resol > 24 then
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha),								0, kiem_h1-thickness/2,					0
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha)-kiem*cos(alpha),				0, kiem_h1+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha)-kiem*cos(alpha),				0, kiem_h2+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha),								0, kiem_h2-thickness/2,					0
			endif

				put sumw+waveLength/2-width_upper/2,													0, waveHeight-thickness/2,				0
				put sumw+waveLength/2-poc2,																0, waveHeight-thickness/2,				0
				put sumw+waveLength/2,																	0, waveHeight-0.75*poc2-thickness/2,		0
				put sumw+waveLength/2+poc2,																0, waveHeight-thickness/2,				0
				put sumw+waveLength/2+width_upper/2,													0, waveHeight-thickness/2,				0

			if gs_resol > 24 then
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha),					0, kiem_h2-thickness/2,					0
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha)+kiem*cos(alpha),	0, kiem_h2+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha)+kiem*cos(alpha),				0, kiem_h1+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha),								0, kiem_h1-thickness/2,					0
			endif

				put sumw+waveLength/2+width_bottom/2+thk_corr,											0, thickness/2,							0
				put sumw+waveLength-poc1,																0, thickness/2,							0
				put sumw+waveLength,																	0, 0.75*poc1-thickness/2,				0
		next sumw

				put sumw+waveLength+1,																	0, thickness/2,							0

		addz pos_proj
		mulz -1
		rotx -90
		addz thickness/2
		tube 4, NSP/4, 1+2+4+8+16+32+0+0,
				0,					0,			0,
				length+length_proj, 0,			0,
				length+length_proj, -thickness,	0,
				0, 					-thickness,	0,

				get(NSP)
		del 4
	endif
endif


!--- Profile 2 ---

if iProfiles = 2 then
	if gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE then
		for sumw=0 to B step waveLength
			put sumw,							waveHeight,	1
			put sumw+width_upper/2,				waveHeight,	0
			put sumw+width_bottom/2,			0,			0
			put sumw+waveLength-width_bottom/2,	0,			0
			put sumw+waveLength-width_upper/2,	waveHeight,	0
			put sumw+waveLength,				waveHeight,	1
		next sumw

		addz pos_proj
		extrude NSP/3,0,0,length+length_proj,48,
				get(NSP)
		del 1
	else
		thk_corr = (width_upper/2-width_bottom/2)*(thickness/waveHeight)

			put -1,											0, waveHeight-thickness/2,	0

		for sumw=0 to B step waveLength
			put sumw,										0, waveHeight-thickness/2,	0
			put sumw+width_upper/2,							0, waveHeight-thickness/2,	0
			put sumw+width_bottom/2+thk_corr,				0, thickness/2,				0
			put sumw+waveLength-width_bottom/2-thk_corr,	0, thickness/2,				0
			put sumw+waveLength-width_upper/2,				0, waveHeight-thickness/2,	0
			put sumw+waveLength,							0, waveHeight-thickness/2,	0
		next sumw

			put sumw+waveLength+1,							0, waveHeight-thickness/2,	0

		addz pos_proj
		mulz -1
		rotx -90
		addz thickness/2
		tube 4, NSP/4, 1+2+4+8+16+32+0+0,
				0,					0,			0,
				length+length_proj, 0,			0,
				length+length_proj, -thickness,	0,
				0, 					-thickness,	0,

				get(NSP)
		del 4
	endif
endif


!--- Profile 3 ---

if iProfiles = 3 then
	if 	bProfiledMigrationStart then	! --- Correct Positioning during Forward Migration
		if gs_detlevel_3D_m = 1 then
			for sumw=0 to B step waveLength
				put sumw,								0,			0
				put sumw+waveLength*3/8-width_upper/2,	waveHeight,	0
				put sumw+waveLength*3/8+width_upper/2,	waveHeight,	0
				put sumw+waveLength*3/8+width_bottom/2,	0,			0
				put sumw+waveLength,					0,			0
			next sumw

			addz pos_proj
			extrude NSP/3,0,0,length+length_proj,48,
					get(NSP)
			del 1
		else
			thk_corr = (width_upper/2-width_bottom/2)*(thickness/waveHeight)

				put sumw-1,										0, thickness/2,				0
				put sumw-eps/10,								0, thickness/2,				0

			for sumw=0 to B step waveLength
				put sumw,										0, thickness/2,				0
				put sumw+waveLength*3/8-width_upper/2,			0, waveHeight-thickness/2,	0
				put sumw+waveLength*3/8+width_upper/2,			0, waveHeight-thickness/2,	0
				put sumw+waveLength*3/8+width_bottom/2+thk_corr,0, thickness/2,				0
				put sumw+waveLength,							0, thickness/2,				0
			next sumw

				put sumw+waveLength+1,							0, thickness/2,				0

			addz pos_proj
			mulz -1
			rotx -90
			addz thickness/2
			tube 4, NSP/4, 1+2+4+8+16+32+0+0,
					0,					0,			0,
					length+length_proj, 0,			0,
					length+length_proj, -thickness,	0,
					0, 					-thickness,	0,

					get(NSP)
			del 4
		endif
	else
		if gs_detlevel_3D_m = 1 then
			for sumw=0 to B step waveLength
				put sumw,								0,			1
				put sumw+waveLength/2-width_bottom/2,	0,			0
				put sumw+waveLength/2-width_upper/2,	waveHeight,	0
				put sumw+waveLength/2+width_upper/2,	waveHeight,	0
				put sumw+waveLength/2+width_bottom/2,	0,			0
				put sumw+waveLength,					0,			1
			next sumw

			addz pos_proj
			extrude NSP/3,0,0,length+length_proj,48,
					get(NSP)
			del 1
		else
			thk_corr = (width_upper/2-width_bottom/2)*(thickness/waveHeight)

				put sumw-1,										0, thickness/2,				0

			for sumw=0 to B step waveLength
				put sumw,										0, thickness/2,				0
				put sumw+waveLength/2-width_bottom/2-thk_corr,	0, thickness/2,				0
				put sumw+waveLength/2-width_upper/2,			0, waveHeight-thickness/2,	0
				put sumw+waveLength/2+width_upper/2,			0, waveHeight-thickness/2,	0
				put sumw+waveLength/2+width_bottom/2+thk_corr,	0, thickness/2,				0
				put sumw+waveLength,							0, thickness/2,				0
			next sumw

				put sumw+waveLength+1,							0, thickness/2,				0

			addz pos_proj
			mulz -1
			rotx -90
			addz thickness/2
			tube 4, NSP/4, 1+2+4+8+16+32+0+0,
					0,					0,			0,
					length+length_proj, 0,			0,
					length+length_proj, -thickness,	0,
					0, 					-thickness,	0,

					get(NSP)
			del 4
		endif
	endif
endif


!--- Profile 4 ---

if iProfiles = 4 then
	alpha=atn((width_bottom-width_upper)/2/waveHeight)

	if gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE then
		for sumw=0 to B step waveLength
				put sumw,																				0,							1
				put sumw+waveLength/2-width_bottom/2,													0,							0

			if gs_resol > 24 then
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha),								kiem_h1,					0
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha)-kiem*cos(alpha),				kiem_h1+kiem*sin(alpha),	0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha)-kiem*cos(alpha),				kiem_h2+kiem*sin(alpha),	0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha),								kiem_h2,					0
			endif

				put sumw+waveLength/2-width_upper/2,													waveHeight,					0
				put sumw+waveLength/2+width_upper/2,													waveHeight,					0

			if gs_resol > 24 then
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha),					kiem_h2,					0
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha)+kiem*cos(alpha),	kiem_h2+kiem*sin(alpha),	0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha)+kiem*cos(alpha),				kiem_h1+kiem*sin(alpha),	0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha),								kiem_h1,					0
			endif

				put sumw+waveLength/2+width_bottom/2,													0,							0
				put sumw+waveLength,																	0,							1
		next sumw

		addz pos_proj
		extrude NSP/3,0,0,length+length_proj,48,
				get(NSP)
		del 1
	else
		thk_corr  = (width_upper/2-width_bottom/2)*(thickness/waveHeight)

				put sumw-1,																				0, thickness/2,							0

		for sumw=0 to B step waveLength
				put sumw,																				0, thickness/2,							0
				put sumw+waveLength/2-width_bottom/2-thk_corr,											0, thickness/2,							0

			if gs_resol >= 24 then
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha),								0, kiem_h1-thickness/2,					0
				put sumw+waveLength/2-width_bottom/2+kiem_h1*tan(alpha)-kiem*cos(alpha),				0, kiem_h1+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha)-kiem*cos(alpha),				0, kiem_h2+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2-width_bottom/2+kiem_h2*tan(alpha),								0, kiem_h2-thickness/2,					0
			endif

				put sumw+waveLength/2-width_upper/2,													0, waveHeight-thickness/2,				0
				put sumw+waveLength/2+width_upper/2,													0, waveHeight-thickness/2,				0

			if gs_resol >= 24 then
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha),					0, kiem_h2-thickness/2,					0
				put sumw+waveLength/2+width_upper/2+(waveHeight-kiem_h2)*tan(alpha)+kiem*cos(alpha),	0, kiem_h2+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha)+kiem*cos(alpha),				0, kiem_h1+kiem*sin(alpha)-thickness/2,		0
				put sumw+waveLength/2+width_bottom/2-kiem_h1*tan(alpha),								0, kiem_h1-thickness/2,					0
			endif

				put sumw+waveLength/2+width_bottom/2+thk_corr,											0, thickness/2,							0
				put sumw+waveLength,																	0, thickness/2,							0
		next sumw

				put sumw+waveLength+1,																	0, thickness/2,							0

		addz pos_proj
		mulz -1
		rotx -90
		addz thickness/2
		tube 4, NSP/4, 1+2+4+8+16+32+0+0,
				0,					0,			0,
				length+length_proj, 0,			0,
				length+length_proj, -thickness,	0,
				0, 					-thickness,	0,

				get(NSP)
		del 4
	endif
endif


!--- Profile 5 ---

if iProfiles = 5 then
	resol gs_resol

	nWaves = INT(width/waveLength) + 1

	if gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE then
		wh     = waveHeight
		dw     = waveLength/4
		wAngle = ATN(wh/2 / dw)*2
		dh     = dw / TAN(wAngle)
		rWave  = dh + wh/2

		sumw = 0
		put 0, wh/2+0.001, 0
		for i=1 to nWaves
			put sumw+0,		-wh/2,		79
			put sumw+0,		 dh,		900
			put rWave,	 	 wAngle,	4079
			put sumw+dw*2,	-dh,		900
			put rWave,		-wAngle*2,	4079
			put sumw+dw*4,	 dh,		900
			put rWave,	 	 wAngle,	4079
			put sumw+dw*4,	-wh/2,		15

			sumw = waveLength*i
		next i
		put sumw, wh/2+0.001, 0

		addz pos_proj

		cutdist = 1
		roty 180
		cutplane
		del 1
		addz length+length_proj
		cutplane
		del 1

		addz -cutdist
		addy waveHeight/2
		prism_ NSP/3,length+length_proj+cutdist*2,
			get(NSP)
		del 2

		cutend
		cutend

		del 1
	else
		wh     = waveHeight - thickness
		dw     = waveLength/4
		wAngle = ATN(wh/2 / dw)*2
		dh     = dw / TAN(wAngle)
		rWave  = dh + wh/2

		sumw = 0
		for i=1 to nWaves
			put sumw+0,				-waveHeight/2,				79
			put sumw+0,				 dh,						900
			put rWave+thickness/2,	 wAngle,					4079
			put sumw+dw*2,			-dh,						900
			put rWave-thickness/2,	-wAngle*2,					4079
			put sumw+dw*4,			 dh,						900
			put rWave+thickness/2,	 wAngle,					4079
			put sumw+dw*4,			-waveHeight/2,				15

			sumw = waveLength*i
		next i

		sumw = nWaves*waveLength
		for i=1 to nWaves
			put sumw-0,				-waveHeight/2+thickness,	79
			put sumw-0,				 dh,						900
			put rWave-thickness/2,	-wAngle,					4079
			put sumw-dw*2,			-dh,						900
			put rWave+thickness/2,	 wAngle*2,					4079
			put sumw-dw*4,			 dh,						900
			put rWave-thickness/2,	-wAngle,					4079
			put sumw-dw*4,			-waveHeight/2+thickness,	15

			sumw = sumw-waveLength
		next i

		addz pos_proj
		addy waveHeight/2
		prism_ NSP/3,length+length_proj,
			get(NSP)
		del 2
	endif
endif


if angCut1 > EPS then cutend
if angCut2 > EPS then cutend
if angCut3 > EPS then cutend
if angCut4 > EPS then cutend

del 5

if abs(rotation)>eps and abs(abs(rotation)-90)>eps then
	if iTopCut = 1 then
		cutend
	endif
	if iBottomCut = 1 then
		cutend
	endif
endif

cutend


if bMigrationRotZ then
	if (GLOB_CONTEXT <> 2 & GLOB_CONTEXT <> 5) then
		del 4
	endif
endif


end

"set_show_3d":
	show = 1
	call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
	if show=1 then show = show_subpos
	if show then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_metall", show_metall)
		if not(show_metall) then show = 0
	endif
	success = LIBRARYGLOBAL ("Libraryflobals-km", "ignore_id", ignore_id)
	success = LIBRARYGLOBAL ("Libraryflobals-km", "show_ingore", show_ingore)
	show_spec = 0 : IF STRSTR (GLOB_ID, ignore_id, 1) > 0 THEN show_spec = 1
	if not(show_ingore) and show_spec then show = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
	IF gs_detlevel_3D=`Простой` then
		det_lvl_2d = 1
		det_lvl_3d = 1
	endif
	IF gs_detlevel_3D=`Детальный` THEN
		det_lvl_2d = 2
		det_lvl_3d = 2
	endif
	IF gs_detlevel_3D=`Откл` THEN
		det_lvl_2d = 2
		det_lvl_3d = 0
	endif
return
