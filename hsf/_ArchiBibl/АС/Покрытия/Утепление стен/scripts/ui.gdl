ui_dialog `Параметры стены с уклоном`,444,266

ui_page 1

arrangement=3
!! 1.Geometry - Figure - General Settings
!! 2.General Settings - Figure - Geometry
!! 3.General Settings - Geometry - Figure

!!----------!!!----------!!!----------!!
!! Geometry !!! Geometry !!! Geometry !!
!!----------!!!----------!!!----------!!

row=25

col_1=95
col_2=50
col_3=20

if arrangement=1 then
	pos_x=0
	pos_y=5
else
	if arrangement=2 then
		pos_x=444-(col_1+col_2+col_3)
		pos_y=5
	else
		pos_x=190
		pos_y=5
	endif
endif

ui_style 2,1
ui_outfield `Геометрия:`,pos_x,pos_y,150,15
ui_style 1,0

par_id=0
pos_y=pos_y+row : par_id=par_id+1 : top_thk_id=par_id
ui_outfield `Толщ. вверху:`,pos_x,pos_y,col_1,15
ui_infield "wain_thk_t",pos_x+col_1,pos_y-1,col_2,15
ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+col_2+5,pos_y,col_3,15

pos_y=pos_y+row : par_id=par_id+1 : top_ext_id=par_id
ui_outfield `Выступ вверху:`,pos_x,pos_y,col_1,15
ui_infield "top_ext",pos_x+col_1,pos_y-1,col_2,15
ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+col_2+5,pos_y,col_3,15

pos_y=pos_y+row : par_id=par_id+1 : ref_hgt_id=par_id
ui_outfield `Базисная высота:`,pos_x,pos_y,col_1,15
ui_infield "rHeight",pos_x+col_1,pos_y-1,col_2,15
ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+col_2+5,pos_y,col_3,15

if rHeight>0 then
	pos_y=pos_y+row : par_id=par_id+1 : ref_thk_id=par_id
	ui_outfield `Базисная толщ.:`,pos_x,pos_y,col_1,15
	ui_infield "wain_thk",pos_x+col_1,pos_y-1,col_2,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+col_2+5,pos_y,col_3,15
endif

pos_y=pos_y+row : par_id=par_id+1
ui_outfield `Угол наклона:`,pos_x,pos_y,col_1,15
ui_infield "bat_angle",pos_x+col_1,pos_y-1,col_2,15
ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+col_2+5,pos_y,col_3,15

pos_y=pos_y+row : par_id=par_id+1 : bot_ext_id=par_id
ui_outfield `Выступ внизу:`,pos_x,pos_y,col_1,15
ui_infield "bot_ext",pos_x+col_1,pos_y-1,col_2,15
ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+col_2+5,pos_y,col_3,15

pos_y=pos_y+row : par_id=par_id+1 : bot_thk_id=par_id
ui_outfield `Толщ. внизу:`,pos_x,pos_y,col_1,15
ui_infield "wain_thk_b",pos_x+col_1,pos_y-1,col_2,15
ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+col_2+5,pos_y,col_3,15

if wain_thk_t<0 or wain_thk<0 or wain_thk_b<0 then
	pos_y=pos_y+row
	ui_style 2,1
	ui_outfield `Внимание: Неправильные параметры!!!`,pos_x,pos_y,175,15
	ui_style 1,0
endif

!!--------!!!--------!!!--------!!
!! Figure !!! Figure !!! Figure !!
!!--------!!!--------!!!--------!!

if arrangement=1 then
	pos_x=180
	pos_y=55
else
	if arrangement=2 then
		pos_x=195
		pos_y=55
	else
		pos_x=365
		pos_y=55
	endif
endif

ui_pict "BattWallSec",pos_x,pos_y, 32, 137, 1
ui_pict "WallSecUI",pos_x+31,pos_y

top_pix=0
if abs(top_ext)>eps then
	if top_ext<0 then
		ui_pict "BattWallTopOver",pos_x,pos_y-27, 32, 30, 1
		call "ui_arrow_02" parameters	dir=0, pixel=26, x=pos_x+65, y=pos_y-26, type=3,
									auto_type=1, num=top_ext_id, num_pos=6, sec_pos=6
		call "ui_arrow_02" parameters	dir=1, pixel=17, x=pos_x+15, y=pos_y-38, type=3,
									auto_type=1, num=top_thk_id, num_pos=2, sec_pos=2
	else
		ui_pict "BattWallTopOverMin",pos_x,pos_y, 32, 28, 1
		call "ui_arrow_02" parameters	dir=0, pixel=26, x=pos_x+65, y=pos_y+1, type=3,
									auto_type=1, num=top_ext_id, num_pos=6, sec_pos=6
		call "ui_arrow_02" parameters	dir=1, pixel=20, x=pos_x+11, y=pos_y+16, type=3,
									auto_type=1, num=top_thk_id, num_pos=2, sec_pos=2
	endif
else
	call "ui_arrow_02" parameters	dir=0, pixel=0, x=pos_x+65, y=pos_y, type=3,
								auto_type=1, num=top_ext_id, num_pos=6, sec_pos=6
	call "ui_arrow_02" parameters	dir=1, pixel=19, x=pos_x+13, y=pos_y-12, type=3,
								auto_type=1, num=top_thk_id, num_pos=2, sec_pos=2
endif

bot_pix=0
if abs(bot_ext)>eps then
	if bot_ext>0 then
		ui_pict "BattWallBotOver",pos_x,pos_y+109, 32, 30, 1
		call "ui_arrow_02" parameters	dir=0, pixel=26, x=pos_x+65, y=pos_y+110, type=3,
									auto_type=1, num=bot_ext_id, num_pos=6, sec_pos=6
		call "ui_arrow_02" parameters	dir=1, pixel=27, x=pos_x+4, y=pos_y+121, type=3,
									auto_type=1, num=bot_thk_id, num_pos=8, sec_pos=8
	else
		ui_pict "BattWallBotOverMin",pos_x,pos_y+136, 32, 30, 1
		call "ui_arrow_02" parameters	dir=0, pixel=28, x=pos_x+65, y=pos_y+137, type=3,
									auto_type=1, num=bot_ext_id, num_pos=6, sec_pos=6
		call "ui_arrow_02" parameters	dir=1, pixel=32, x=pos_x, y=pos_y+177, type=3,
									auto_type=1, num=bot_thk_id, num_pos=8, sec_pos=8
	endif
else
	call "ui_arrow_02" parameters	dir=0, pixel=0, x=pos_x+65, y=pos_y+137, type=3,
								auto_type=1, num=bot_ext_id, num_pos=6, sec_pos=6
	call "ui_arrow_02" parameters	dir=1, pixel=30, x=pos_x+2, y=pos_y+148, type=3,
								auto_type=1, num=bot_thk_id, num_pos=8, sec_pos=8
endif

if rHeight>0 then
	call "ui_arrow_02" parameters	dir=0, pixel=85, x=pos_x+65, y=pos_y+50, type=3,
								auto_type=1, num=ref_hgt_id, num_pos=6, sec_pos=6
	call "ui_arrow_02" parameters	dir=1, pixel=21, x=pos_x+10, y=pos_y+50, type=3,
								auto_type=1, num=ref_thk_id, num_pos=2, sec_pos=2
endif

!!------------------!!!------------------!!!------------------!!
!! General Settings !!! General Settings !!! General Settings !!
!!------------------!!!------------------!!!------------------!!

col_1=95
col_2=20
col_3=60

if arrangement=1 then
	pos_x=444-(col_1+col_2+col_3)
	pos_y=5
else
	if arrangement=2 then
		pos_x=0
		pos_y=5
	else
		pos_x=0
		pos_y=5
	endif
endif

ui_style 2,1
ui_outfield `Общие параметры:`,pos_x,pos_y,col_1+col_2+col_3,15
ui_style 1,0

!! Pictures
pos_y=pos_y+row
if uwbp=0 and uwtp=0 then ui_pict "FollowNone",pos_x+col_1+col_2,pos_y-1, 60, 40, 1
if uwbp=1 and uwtp=0 then ui_pict "FollowBottom",pos_x+col_1+col_2,pos_y-1, 60, 40, 1
if uwbp=0 and uwtp=1 then ui_pict "FollowTop",pos_x+col_1+col_2,pos_y-1, 60, 40, 1
if uwbp=1 and uwtp=1 then ui_pict "FollowBoth",pos_x+col_1+col_2,pos_y-1, 60, 40, 1

ui_outfield `Срез вверху:`,pos_x,pos_y,col_1,15
ui_infield "uwtp",pos_x+col_1,pos_y-1,col_2,15

pos_y=pos_y+row
ui_outfield `Срез внизу:`,pos_x,pos_y,col_1,15
ui_infield "uwbp",pos_x+col_1,pos_y-1,col_2,15

pos_y=pos_y+row+10
ui_style 2,1
ui_outfield `Спец. углы:`,pos_x,pos_y,col_1+col_2,15
ui_style 1,0

pos_y=pos_y+row
ui_outfield `Угол в начале:`,pos_x,pos_y,col_1,15
ui_outfield "(1)",pos_x+col_1,pos_y,col_2,15
ui_infield "cust_bega",pos_x+col_1+col_2,pos_y-1,col_3,15

pos_y=pos_y+row
ui_outfield `Угол в конце:`,pos_x,pos_y,col_1,15
ui_outfield "(2)",pos_x+col_1,pos_y,col_2,15
ui_infield "cust_enda",pos_x+col_1+col_2,pos_y-1,col_3,15

pos_y=pos_y+row
if show_bot then
	ui_pict "batWallplanBot",pos_x,pos_y, 175, 40, 1
else
	ui_pict "batWallplan",pos_x,pos_y, 175, 40, 1
endif
pos_y=pos_y+25

pos_y=pos_y+row
ui_outfield `Толщ. внизу в 2D:`,pos_x,pos_y,col_1+col_2+5+col_3-15,15
ui_infield "show_bot",pos_x+col_1+col_2-15+col_3,pos_y-1,17,15
