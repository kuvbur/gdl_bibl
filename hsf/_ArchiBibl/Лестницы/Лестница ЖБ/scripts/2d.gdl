show_pol = 0 : success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_pol", show_pol)

reqBMinSpace = 0
success = LIBRARYGLOBAL ("LibraryGlobals13", "showBMinSpace", reqBMinSpace)
if success > 0 and reqBMinSpace then
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSFillType", reqMSFillType)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSFillPen", reqMSFillPen)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSFillBackPen", reqMSFillBackPen)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSContLineType", reqMSContLineType)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSContLinePen", reqMSContLinePen)
endif

DEFINE STYLE "MARK" "ISOCPEUR", 3, 2, 0
STYLE "MARK"
unID = 1
if isshowproj then 
	pen 2
	DRAWINDEX 10
	LINE_TYPE line_zhb
	LINE_PROPERTY 1
	project2 3,270,2
endif

LINE_TYPE line_2d
LINE_PROPERTY 2

hotspot2 0,0,unID : unID = unID + 1
hotspot2 0,B,unID : unID = unID + 1
!hotspot2 A,B,unID : unID = unID + 1
!hotspot2 A,0,unID : unID = unID + 1

pen gs_cont_pen
LINE_TYPE line_2d
DRAWINDEX 10

if show_tr = "Площадки" then type_pol_show = tip_pol_pl
if show_tr = "Лестницы" then type_pol_show = tip_pol_l
if show_tr = "Сверху" then type_pol_show = tip_pol_verh
if show_tr = "Снизу" then type_pol_show = tip_pol_niz
if show_tr = "Не показывать" then type_pol_show = ""

if issecondm then
	gosub "niz_m"
	if (t_symb = type_symb_arr[5] or t_symb = type_symb_arr[6] or t_symb = type_symb_arr[7] or t_symb = type_symb_arr[8]) then gosub "plosh"
	mul2 -1,-1
	add2 -(ns_1m - 1) * l_stup, - 2*b_l - b_zazor
	gosub "verh_m"
else
	gosub "niz_m"	
endif

end

"niz_m":
	n_stup = ns_1m
	h_l_t = h_1m 
	l_l_t = l_1m - l_stup + 2*t_pol_l*not(show_pokr)
	if show_pokr then
		startx = 0
	else
		startx = t_pol_l
	endif
	startx = startx-t_pol_l*2*is_eq_bet
	endx = l_l_t - startx

	if t_symb = type_symb_arr[1] then gosub "simb_niz_1"
	if t_symb = type_symb_arr[2] then gosub "simb_niz_2"
	if t_symb = type_symb_arr[3] then gosub "simb_niz_3"
	if t_symb = type_symb_arr[4] then gosub "simb_niz_4"
	if t_symb = type_symb_arr[5] then gosub "simb_niz_1"
	if t_symb = type_symb_arr[6] then gosub "simb_niz_1"
	if t_symb = type_symb_arr[7] then gosub "simb_niz_3"
	if t_symb = type_symb_arr[8] then gosub "simb_niz_3"
	if not(show_pokr) and isshowdl_niz then
		DRAWINDEX 10
		LINE_TYPE line_zhb
		LINE_PROPERTY 2
		if (t_symb = type_symb_arr[1] or t_symb = type_symb_arr[2] or t_symb = type_symb_arr[5] or t_symb = type_symb_arr[6]) and type_pr_niz<>"Сверху на плиту" then gosub "niz_1_zhb"
		if not(issecondm) and (t_symb = type_symb_arr[1] or  t_symb = type_symb_arr[5] or t_symb = type_symb_arr[6]) then gosub "verh_1_zhb"
		pen gs_cont_pen
		LINE_TYPE line_2d
		DRAWINDEX 10
	endif
return

"verh_m":
	n_stup = ns_2m
	h_l_t = h_2m 
	l_l_t = l_2m - l_stup + t_pol_l + t_pol_l*not(show_pokr)
	if show_pokr then
		startx = 0
	else
		startx = t_pol_l
	endif
	endx = l_l_t - startx
	if t_symb = type_symb_arr[5] then gosub "simb_niz_1"
	if t_symb = type_symb_arr[6] then gosub "simb_niz_2"
	if t_symb = type_symb_arr[7] then gosub "simb_niz_2"
	if t_symb = type_symb_arr[8] then gosub "simb_niz_1"
	if t_symb = type_symb_arr[9] then gosub "simb_niz_3"
	if t_symb = type_symb_arr[10] then gosub "simb_niz_1"
	if not(show_pokr) and isshowdl_verh then
		DRAWINDEX 10
		LINE_TYPE line_zhb
		LINE_PROPERTY 2
		if (t_symb = type_symb_arr[5] or t_symb = type_symb_arr[8] or t_symb = type_symb_arr[9] or t_symb = type_symb_arr[10]) then gosub "verh_2_zhb"
		pen gs_cont_pen
		LINE_TYPE line_2d
		DRAWINDEX 10
	else
	endif
return


"simb_niz_1":
	if show_ogr and show_pokr then
		LINE_TYPE line_ogr
		if abs(ogr_left)>EPS then 
			x1 = 0 : y1 = b_l-ogr_left : x2 = l_l_t : y2 = b_l-ogr_left : gosub "hline"
		endif
		if abs(ogr_right)>EPS then 
			x1 = 0 : y1 = ogr_right : x2 = l_l_t : y2 = ogr_right : gosub "hline"
		endif
		LINE_TYPE line_2d
	endif
	xe = startx
	for xs = startx to endx step l_stup
		x1 = xs : y1 = 0 : x2 = xs : y2 = b_l : gosub "hline"
		x1 = xe : y1 = 0 : x2 = xs : y2 = 0 : gosub "hline"
		x1 = xe : y1 = b_l : x2 = xs : y2 = b_l : gosub "hline"
		xe = xs
	next xs
	xv = xe : xn = startx
	line2 xn,b_l/2,xv,b_l/2
	line2 startx,b_l/2,xv,b_l/2
	gosub "strel"
	if reqBMinSpace then
		line_type reqMSContLineType
		pen reqMSContLinePen
		fill reqMSFillType
		POLY2_B 4, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			0,b_l,1,
			0,0,1,
			-b_l,0,1,
			-b_l,b_l,1

		POLY2_B 4, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			l_l_t,b_l,1,
			l_l_t,0,1,
			l_l_t+b_l,0,1,
			l_l_t+b_l,b_l,1
	endif
return


"simb_niz_2":
	HOTSPOT2 0, 0, unID, cut_x_niz_1, 1+128: unID=unID+1
	HOTSPOT2 cut_x_niz_1, 0, unID, cut_x_niz_1, 2: unID=unID+1
	line2 0,0,cut_x_niz_1,0
	line2 0,b_l,cut_x_niz_1+dx_cut,b_l
	line2 cut_x_niz_1,0,cut_x_niz_1+dx_cut,b_l

	if show_ogr and show_pokr then
		LINE_TYPE line_ogr
		if abs(ogr_left)>EPS then 
			x1 = 0 : y1 = b_l-ogr_left : x2 = cut_x_niz_1+dx_cut : y2 = b_l-ogr_left : gosub "hline"
		endif
		if abs(ogr_right)>EPS then 
			x1 = 0 : y1 = ogr_right : x2 = cut_x_niz_1 : y2 = ogr_right : gosub "hline"
		endif
		LINE_TYPE line_2d
	endif

	x1 = cut_x_niz_1 : x2 = cut_x_niz_1 + dx_cut : y1 = 0 : y2 = b_l  !Линия разрыва
	x3 = startx : x4 = l_l_t : y3 = b_l/2 : y4 = y3  !Центральная линия
	gosub "getXY1" :	xv = x : xn = startx
	line2 xn,b_l/2,xv,b_l/2
	gosub "strel"
	for xs = startx to endx step l_stup
		if xs <= cut_x_niz_1 then 
			x1 = xs : y1 = 0 : x2 = xs : y2 = b_l : gosub "hline"
		endif
		if (xs > cut_x_niz_1) and (xs <= cut_x_niz_1+dx_cut) then
			x1 = xs : x2 = xs : y1 = b_l : y2 = 0  !Линия ступени
			x3 = cut_x_niz_1 + dx_cut : x4 = cut_x_niz_1 : y3 = b_l : y4 = 0  !Линия разрыва
			gosub "getXY1"
			x1 = xs : y1 = y : x2 = xs : y2 = b_l : gosub "hline"
		endif
	next xs
	if reqBMinSpace then
		line_type reqMSContLineType
		pen reqMSContLinePen
		fill reqMSFillType
		POLY2_B 4, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			0,b_l,1,
			0,0,1,
			-b_l,0,1,
			-b_l,b_l,1
	endif
return

"simb_niz_3":
	HOTSPOT2 l_l_t, 0, unID, cut_x_niz_2, 1+128: unID=unID+1
	HOTSPOT2 l_l_t - cut_x_niz_2, 0, unID, cut_x_niz_2, 2: unID=unID+1
	line2 l_l_t,0,l_l_t - cut_x_niz_2,0
	line2 l_l_t,b_l,l_l_t - cut_x_niz_2+dx_cut,b_l
	line2 l_l_t-cut_x_niz_2,0,l_l_t-cut_x_niz_2+dx_cut,b_l

	if show_ogr and show_pokr then
		LINE_TYPE line_ogr
		if abs(ogr_left)>EPS then 
			x1 = l_l_t : y1 = b_l-ogr_left : x2 = l_l_t - cut_x_niz_2+dx_cut : y2 = b_l-ogr_left : gosub "hline"
		endif
		if abs(ogr_right)>EPS then 
			x1 = l_l_t : y1 = ogr_right : x2 = l_l_t - cut_x_niz_2 : y2 = ogr_right : gosub "hline"
		endif
		LINE_TYPE line_2d
	endif

	x1 = l_l_t-cut_x_niz_2 : x2 = l_l_t-cut_x_niz_2 + dx_cut : y1 = 0 : y2 = b_l  !Линия разрыва
	x3 = startx : x4 = l_l_t : y3 = b_l/2 : y4 = y3  !Центральная линия
	gosub "getXY1" :	xv = l_l_t : xn = x
	gosub "strel"
	line2 xn, b_l/2, xv, b_l/2
	for xs = startx to endx step l_stup
		if xs > l_l_t - cut_x_niz_2 + dx_cut then 
			x1 = xs : y1 = 0 : x2 = xs : y2 = b_l : gosub "hline"
		endif
		if (xs > l_l_t-cut_x_niz_2) and (xs <=  l_l_t-cut_x_niz_2+dx_cut) then
			x1 = xs : x2 = xs : y1 = b_l : y2 = 0  !Линия ступени
			x3 =  l_l_t-cut_x_niz_2 + dx_cut : x4 =  l_l_t-cut_x_niz_2 : y3 = b_l : y4 = 0  !Линия разрыва
			gosub "getXY1"
			x1 = xs : y1 = y : x2 = xs : y2 = 0 : gosub "hline"
		endif
	next xs
	if reqBMinSpace then
		line_type reqMSContLineType
		pen reqMSContLinePen
		fill reqMSFillType
		POLY2_B 4, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			xs,b_l,1,
			xs,0,1,
			xs+b_l,0,1,
			xs+b_l,b_l,1
	endif
return

"simb_niz_4":
	HOTSPOT2 0, 0, unID, cut_x_niz_1, 1+128: unID=unID+1
	HOTSPOT2 cut_x_niz_1, 0, unID, cut_x_niz_1, 2: unID=unID+1
	HOTSPOT2 l_l_t, 0, unID, cut_x_niz_2, 1+128: unID=unID+1
	HOTSPOT2 l_l_t - cut_x_niz_2, 0, unID, cut_x_niz_2, 2: unID=unID+1

	line2 cut_x_niz_1,0,l_l_t - cut_x_niz_2,0
	line2 cut_x_niz_1 + dx_cut,b_l,l_l_t - cut_x_niz_2 + dx_cut,b_l

	if show_ogr and show_pokr then
		LINE_TYPE line_ogr
		if abs(ogr_right)>EPS then
			x1 =  cut_x_niz_1 : y1 = ogr_right : x2 = l_l_t - cut_x_niz_2 : y2 = ogr_right : gosub "hline"
		endif
		if abs(ogr_left)>EPS then 
			x1 = cut_x_niz_1 + dx_cut : y1 = b_l-ogr_left : x2 = l_l_t - cut_x_niz_2 + dx_cut : y2 = b_l-ogr_left : gosub "hline"
		endif
		LINE_TYPE line_2d
	endif

	line2 l_l_t-cut_x_niz_2,0,l_l_t-cut_x_niz_2+dx_cut,b_l
	line2 cut_x_niz_1,0,cut_x_niz_1+dx_cut,b_l

	x1 = l_l_t-cut_x_niz_2 : x2 = l_l_t-cut_x_niz_2 + dx_cut : y1 = 0 : y2 = b_l  !Линия разрыва 1
	x3 = startx : x4 = l_l_t : y3 = b_l/2 : y4 = y3  !Центральная линия

	gosub "getXY1" :	xv = x

	x1 = cut_x_niz_1 : x2 = cut_x_niz_1 + dx_cut : y1 = 0 : y2 = b_l  !Линия разрыва 2
	x3 = 0 : x4 = l_l_t : y3 = b_l/2 : y4 = y3  !Центральная линия
	gosub "getXY1" :	xn = x
	gosub "strel"
	line2 xn,b_l/2,xv,b_l/2
	for xs = startx to endx step l_stup
		if xs >= cut_x_niz_1 + dx_cut*(cut_x_niz_1>0) and xs <= l_l_t - cut_x_niz_2 then
			x1 = xs : y1 = 0 : x2 = xs : y2 = b_l : gosub "hline"
		endif
		if (xs > cut_x_niz_1) and (xs <= cut_x_niz_1+dx_cut) then
			x1 = xs : x2 = xs : y1 = b_l : y2 = 0  !Линия ступени
			x3 = cut_x_niz_1 + dx_cut : x4 = cut_x_niz_1 : y3 = b_l : y4 = 0  !Линия разрыва
			gosub "getXY1"
			x1 = xs : y1 = y : x2 = xs : y2 = 0 : gosub "hline"
		endif
		if (xs > l_l_t-cut_x_niz_2) and (xs <=  l_l_t-cut_x_niz_2+dx_cut) then
			x1 = xs : x2 = xs : y1 = b_l : y2 = 0  !Линия ступени
			x3 =  l_l_t-cut_x_niz_2 + dx_cut : x4 =  l_l_t-cut_x_niz_2 : y3 = b_l : y4 = 0  !Линия разрыва
			gosub "getXY1"
			x1 = xs : y1 = y : x2 = xs : y2 = b_l : gosub "hline"
		endif
	next xs
return

"getXY1":
    x=(((x1*y2-x2*y1)*(x4-x3)-(x3*y4-x4*y3)*(x2-x1))/((y1-y2)*(x4-x3)-(y3-y4)*(x2-x1)))
    y=(((y3-y4)*x-(x3*y4-x4*y3))/(x4-x3))
	x = - x
!	hotspot2 x,y
!	line2 x1,y1,x2,y2
!	line2 x3,y3,x4,y4
return

"hline":
	line2 x1, y1, x2, y2
	hotline2 x1, y1, x2, y2, unID : unID = unID + 1
	hotspot2 x1, y1, unID : unID = unID + 1
	hotspot2 x2, y2, unID : unID = unID + 1
return

"plosh":
	gosub "show_type_pol"
	x1 = l_l_t - t_pol_l*not(show_pokr) : y1 = 0 : x2 = l_l_t + l_plosh - 2*t_pol_l*not(show_pokr) : y2 = 0 : gosub "hline"
	x1 = l_l_t - t_pol_l*not(show_pokr) : y1 = 2 * b_l + b_zazor : x2 = l_l_t + l_plosh - 2*t_pol_l*not(show_pokr) : y2 = 2 * b_l + b_zazor : gosub "hline"
	x1 = l_l_t + l_plosh - 2*t_pol_l*not(show_pokr) : y1 = 0 : x2 = l_l_t + l_plosh - 2*t_pol_l*not(show_pokr) : y2 = 2 * b_l + b_zazor : gosub "hline"

	if is_eq_bet then
		x1 = l_l_t-t_pol_l-2*t_pol_l*not(show_pokr): y1 = b_l : x2 = l_l_t-t_pol_l-2*t_pol_l*not(show_pokr): y2 = b_l + b_zazor : gosub "hline"

		x1 = l_l_t-t_pol_l*not(show_pokr) : y1 = 0 : x2 = l_l_t-t_pol_l-2*t_pol_l*not(show_pokr)-t_pol_l*show_pokr : y2 = 0 : gosub "hline"
		x1 = l_l_t-t_pol_l*not(show_pokr) : y1 = 2*b_l + b_zazor : x2 = l_l_t-t_pol_l-2*t_pol_l*not(show_pokr)-t_pol_l*show_pokr : y2 = 2*b_l + b_zazor : gosub "hline"


		x1 = l_l_t-t_pol_l-2*t_pol_l*not(show_pokr) : y1 = b_l : x2 = l_l_t-t_pol_l-2*t_pol_l*not(show_pokr)-t_pol_l*show_pokr : y2 = b_l : gosub "hline"
	else
		x1 = l_l_t - 2*t_pol_l*not(show_pokr)+t_pol_l : y1 = b_l : x2 = l_l_t - 2*t_pol_l*not(show_pokr)+t_pol_l-t_pol_l*show_pokr : y2 = b_l : gosub "hline"
		x1 = l_l_t - 2*t_pol_l*not(show_pokr)+t_pol_l : y1 = b_l + b_zazor : x2 = l_l_t - 4*t_pol_l*not(show_pokr)+t_pol_l-t_pol_l*show_pokr : y2 = b_l + b_zazor : gosub "hline"
		x1 = l_l_t - 2*t_pol_l*not(show_pokr)+t_pol_l : y1 = 2*b_l + b_zazor : x2 = l_l_t - 4*t_pol_l*not(show_pokr)+t_pol_l-t_pol_l*show_pokr : y2 = 2*b_l + b_zazor : gosub "hline"
		x1 = l_l_t - 2*t_pol_l*not(show_pokr)+t_pol_l: y1 = b_l : x2 = l_l_t - 2*t_pol_l*not(show_pokr)+t_pol_l: y2 = b_l + b_zazor : gosub "hline"
	endif

return

!"plosh_2":
!	x1 = l_l_t-t_pol_l*(not(show_pokr)) : y1 = 0 : x2 = l_l_t + l_plosh : y2 = 0 : gosub "hline"
!	x1 = l_l_t-t_pol_l*(not(show_pokr)) : y1 = 2 * b_l + b_zazor : x2 = l_l_t + l_plosh : y2 = 2 * b_l + b_zazor : gosub "hline"
!	x1 = l_l_t + l_plosh : y1 = 0 : x2 = l_l_t + l_plosh : y2 = 2 * b_l + b_zazor : gosub "hline"
!	x1 = l_l_t-t_pol_l*(not(show_pokr)) : y1 = 0 : x2 = l_l_t-t_pol_l*(not(show_pokr)) : y2 = 2 * b_l + b_zazor : gosub "hline"
!return

"strel":
	line2 xv, b_l/2, xv - k_str*1.5, b_l/2 + k_str
	line2 xv, b_l/2, xv - k_str*1.5, b_l/2 - k_str
!	circle2 xn, b_l/2,k_str/2
return

"niz_1_zhb":
	x1 = t_pol_l : y1 = 0 : x2 = l_zhb_niz_1m : y2 = y1 : gosub "hline"
	x1 = t_pol_l : y1 = b_l : x2 = x2 : y2 = y1 : gosub "hline"
	x1 = x2: y1 = b_l : x2 = x2 : y2 = 0 : gosub "hline"
return

"verh_1_zhb":
	l_l = l_l_t - 2*t_pol_l
	l_l_t = l_l_t - t_pol_l - t_pol_l*not(show_pokr)
	if isplosh then
		x1 = l_l_t + t_pol_l : y1 = 0 : x2 = l_l_t+l_2_plosh : y2 = y1 : gosub "hline"
		x1 = l_l_t + t_pol_l : y1 = b_2_plosh : x2 = l_l_t+l_2_plosh : y2 = y1 : gosub "hline"
		x1 = l_l_t + t_pol_l : y1 = b_2_plosh : x2 =x1 : y2 = 0 : gosub "hline"
		x1 = l_l_t+l_2_plosh : y1 = 0 : x2 = x1 : y2 = b_2_plosh : gosub "hline"
	else
		x1 = l_l_t + t_pol_l : y1 = 0 : x2 = l_zhb_verh_1m : y2 = y1 : gosub "hline"
		x1 = l_l_t + t_pol_l : y1 = b_l : x2 = x2 : y2 = y1 : gosub "hline"
		x1 = x2: y1 = b_l : x2 = x2 : y2 = 0 : gosub "hline"
	endif
	l_l_t = l_l
return


"verh_2_zhb":
	l_l = l_l_t
	l_l_t = l_l_t - 2*t_pol_l*not(show_pokr)
	if isplosh then
		x1 = l_l_t + t_pol_l : y1 = 0 : x2 = l_l_t+l_2_plosh : y2 = y1 : gosub "hline"
		x1 = l_l_t + t_pol_l : y1 = b_2_plosh : x2 = l_l_t+l_2_plosh : y2 = y1 : gosub "hline"
		x1 = l_l_t + t_pol_l : y1 = b_2_plosh : x2 =x1 : y2 = 0 : gosub "hline"
		x1 = l_l_t+l_2_plosh : y1 = 0 : x2 = x1 : y2 = b_2_plosh : gosub "hline"
	else
		x1 = l_l_t+t_pol_l : y1 = 0 : x2 = l_zhb_verh_2m- 2*t_pol_l : y2 = y1 : gosub "hline"
		x1 = l_l_t+t_pol_l : y1 = b_l : x2 = x2 : y2 = y1 : gosub "hline"
		x1 = x2: y1 = b_l : x2 = x2 : y2 = 0 : gosub "hline"
	endif
	l_l_t = l_l
return

"show_type_pol":
	if show_pol=1 then
		r = request ("View_Rotangle", "", _angleViewRot)
		_totalRotate	= (SYMB_ROTANGLE + _angleViewRot) MOD 360
		rot2 _totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED))
		HOTSPOT2 pos_x1, 0, unID, pos_y1, 1+128: unID=unID+1
		HOTSPOT2 pos_x1, pos_y1, unID, pos_y1, 2: unID=unID+1
		HOTSPOT2 pos_x1, -1, unID, pos_y1, 3: unID=unID+1
		HOTSPOT2 0, pos_y1, unID, pos_x1, 1+128: unID=unID+1
		HOTSPOT2 pos_x1, pos_y1, unID, pos_x1, 2: unID=unID+1
		HOTSPOT2 -1, pos_y1, unID, pos_x1, 3: unID=unID+1
		add2 pos_x1,pos_y1
		mashtab = GLOB_SCALE / 100
		MUL2 mashtab,mashtab
		k = 0.8
		MUL2 k,k
		DRAWINDEX 0
		text2 0.5,0.87/2,type_pol_show
		DRAWINDEX 20
		set fill f
		poly2 3,1+2+4,
		0,0,
		1,0,
		0.5,0.87
		hotspot2 0.5,0.87/2,unID : unID=unID+1
		hotspot2 0.5,0.87,unID : unID=unID+1
		hotspot2 0,0,unID : unID=unID+1
		hotspot2 1,0,unID : unID=unID+1
		del 4
	endif
return

