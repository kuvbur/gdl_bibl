if category_zone <> "" then parameters category_zone = category_zone 
if code_zone <> "" then parameters code_zone = code_zone
if name_zone <> "" then parameters name_zone = name_zone
if n_zon <> "" then parameters n_zon = n_zon

values "s_zap" range[0.05,0.15]
PARAMETERS s_pol_pl = s_pol_pl,
			s_pol_l = s_pol_l,
			s_pol_verh = s_pol_verh,
			s_pol_niz = s_pol_niz,
			perim_pl = perim_pl,
			s_pot_l=s_pot_l,
			s_pol = s_pol
values "show_tr" "Площадки", "Лестницы", "Сверху", "Снизу", "Не показывать"
values "out_type" "Экспликация", "Общестроительная"
if out_type = "Общестроительная" then
	PARAMETERS show_tr = "Не показывать"
	LOCK "show_tr"
endif

values "el_version" 1,2

if el_version = 2 then
	PARAMETERS tip_el = "Лестница"
	dim used_tip[]
	success = LIBRARYGLOBAL ("Libraryflobals-pokr", "used_tip", used_tip)
	values 'tip_pol_pl' used_tip, CUSTOM
	values 'tip_pol_niz' used_tip, CUSTOM
	values 'tip_pol_l' used_tip, CUSTOM
	values 'tip_pol_verh' used_tip, CUSTOM
	values 'tip_pot' "Не задан",used_tip, CUSTOM
	if GLOB_MODPAR_NAME = "tip_pot" then
		if tip_pot<>"Не задан" then
			PARAMETERS is_pot = 1
		else
			PARAMETERS is_pot = 0
		endif
	endif
	if not(is_pot) then LOCK "VOTS"
endif
!1/1000, 5/1000, 10/1000, 50/1000, 100/1000, 500/1000
krat_txt = 5/1000

PARAMETERS l_zhb_niz_1m = l_zhb_niz_1m
PARAMETERS peres_niz_1_1m =peres_niz_1_1m
PARAMETERS peres_niz_2_1m = peres_niz_2_1m
PARAMETERS peres_verh_1m = peres_verh_1m	
PARAMETERS l_zhb_verh_1m = l_zhb_verh_1m
PARAMETERS ns_1m = ns_1m
PARAMETERS l_1m = l_1m
PARAMETERS h_1m = h_1m

values "type_symb_1" type_symb_arr
values "type_symb_2" type_symb_arr

!values "h_stup" range [krat_txt, 1] step krat_txt, krat_txt 
!values "l_stup" range [krat_txt, 1] step krat_txt, krat_txt 
!values "t_pol_niz" range [krat_txt, 1] step krat_txt, krat_txt 
!values "t_pol_verh" range [krat_txt, 1] step krat_txt, krat_txt 
!values "t_pol_plosh" range [krat_txt, 1] step krat_txt, krat_txt 
!values "dl_zhb_niz" range [-1, 10] step krat_txt, krat_txt 
!values "dl_zhb_verh" range [-1, 10] step krat_txt, krat_txt 

values "A" range [krat_txt, 1000] step krat_txt, krat_txt 
values "B" range [krat_txt, 50] step krat_txt, krat_txt
values "ZZYZX" range [h_stup, 100] step h_stup, h_stup 

values "type_pr_niz" type_pr_arr

if isplosh then
	type_pr_verh = "К площадке"
	PARAMETERS dl_zhb_verh = 0
	arr = type_pr_arr : var = type_pr_verh : gosub "getINX"
	PARAMETERS itype_pr_verh = inx, type_pr_verh = type_pr_verh
	values "l_2_plosh" range [0.5, 100] step krat_txt, krat_txt 
	values "b_2_plosh" range [b_l, 100] step krat_txt, krat_txt 
	LOCK "type_pr_verh", "dl_zhb_verh"
else
	values "type_pr_verh" type_pr_arr
	HIDEPARAMETER "l_2_plosh", "b_2_plosh"
endif


if issecondm then
	values "cut_x_niz_1" range [l_stup, int((h_plosh/h_stup)-1)*l_stup - dx_cut]
	values "cut_x_niz_2" range [l_stup, int((h_plosh/h_stup)-1)*l_stup - dx_cut]
	values "h_l" range [h_plosh+h_stup, 10] step h_stup, h_stup 
	values "h_plosh" range [h_stup, h_l-h_stup] step h_stup, h_stup 
	values "l_plosh" range [l_stup, 10] step krat_txt, krat_txt
	PARAMETERS l_zhb_niz_2m = l_zhb_niz_2m
	PARAMETERS peres_niz_1_2m =peres_niz_1_2m
	PARAMETERS peres_niz_2_2m = peres_niz_2_2m
	PARAMETERS peres_verh_2m = peres_verh_2m	
	PARAMETERS l_zhb_verh_2m = l_zhb_verh_2m
	PARAMETERS ns_2m = ns_2m
	PARAMETERS l_2m = l_2m
	PARAMETERS h_2m = h_2m
else
	values "cut_x_niz_1" range [l_stup, int((h_l/h_stup)-1)*l_stup - dx_cut]
	values "cut_x_niz_2" range [l_stup, int((h_l/h_stup)-1)*l_stup - dx_cut]
	values "h_l" range [h_stup, 10] step h_stup, h_stup 
	PARAMETERS l_zhb_niz_2m = 0
	PARAMETERS peres_niz_1_2m = 0
	PARAMETERS peres_niz_2_2m = 0
	PARAMETERS peres_verh_2m = 0	
	PARAMETERS l_zhb_verh_2m = 0
	PARAMETERS ns_2m = 0
	PARAMETERS l_2m = 0
	PARAMETERS h_2m = 0
	HIDEPARAMETER "l_zhb_niz_2m", "peres_niz_1_2m", "peres_niz_2_2m", "peres_verh_2m", "ns_2m", "l_2m", "h_2m", "b_zazor", "l_plosh", "h_plosh"
endif

PARAMETERS ang_l = atn(h_stup/l_stup)

if GLOB_MODPAR_NAME = "h_plosh" then
	if abs(h_plosh - int(h_plosh/h_stup)*h_stup) > EPS then h_plosh = int(h_plosh/h_stup)*h_stup
	PARAMETERS h_plosh = h_plosh
endif

if GLOB_MODPAR_NAME = "h_l" then
	if abs(h_l - int(h_l/h_stup)*h_stup) > EPS then h_l = int(h_l/h_stup)*h_stup
	PARAMETERS h_l = h_l
	PARAMETERS ZZYZX = h_l
	PARAMETERS A = ((h_l/h_stup)-1)*l_stup
	PARAMETERS B = b_l + (b_l + b_zazor) * issecondm
endif

if GLOB_MODPAR_NAME = "ZZYZX" then
	if abs(ZZYZX - int(ZZYZX/h_stup)*h_stup) > EPS then ZZYZX = int(ZZYZX/h_stup)*h_stup
	PARAMETERS h_l = ZZYZX
	PARAMETERS ZZYZX = ZZYZX
	PARAMETERS A = ((ZZYZX/h_stup)-1)*l_stup
endif

if GLOB_MODPAR_NAME = "B" then
	if issecondm then
		if (B - 2 * b_l) < EPS then
			PARAMETERS b_zazor = 0.05
			PARAMETERS B = b_zazor + 2 * b_l
		else
			PARAMETERS b_zazor = B - 2 * b_l
		endif
	else
		PARAMETERS b_l = B
	endif
endif

if GLOB_MODPAR_NAME = "b_zazor" and issecondm then PARAMETERS B = b_zazor + 2 * b_l

if GLOB_MODPAR_NAME = "b_l" then
	if issecondm then
		PARAMETERS B = b_zazor + 2 * b_l
	else
		PARAMETERS B = b_l
	endif
endif

if GLOB_MODPAR_NAME = "issecondm" then
	if issecondm then
		PARAMETERS B = b_zazor + 2 * b_l
	else
		PARAMETERS B = b_l
		PARAMETERS A = (h_l/h_stup)*l_stup
	endif
endif

if GLOB_MODPAR_NAME = "type_pr_niz" then
	arr = type_pr_arr : var = type_pr_niz : gosub "getINX"
	PARAMETERS itype_pr_niz = inx
	if type_pr_niz = "Сверху на плиту" then
		PARAMETERS dl_zhb_niz = 0
		LOCK "dl_zhb_niz"
	endif
endif

if GLOB_MODPAR_NAME = "type_pr_verh" then
	if type_pr_verh = "Сверху на плиту" then type_pr_verh = "Сбоку к плите"
	arr = type_pr_arr : var = type_pr_verh : gosub "getINX"
	PARAMETERS itype_pr_verh = inx, type_pr_verh = type_pr_verh 
endif

zone_param = "-"
if is_pot then 
	if zone_param = "-" then zone_param = ""
	zone_param = zone_param + "@Spl=" + str("%.0",s_pot_l*1000)
endif
parameters zone_param = zone_param


PARAMETERS ac_bottom_level = SYMB_POS_Z
PARAMETERS ac_top_level = SYMB_POS_Z + h_l

end
"getINX":
	inx = 0
	for i = 1 to vardim1(arr)
		if arr[i] = var then inx = i
	next i
return