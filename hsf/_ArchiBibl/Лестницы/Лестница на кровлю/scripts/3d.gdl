success = LIBRARYGLOBAL ("Libraryflobals-as", "gs_detlevel_3D_m", gs_detlevel_3D_m)
IF gs_detlevel_3D_m = 1 or (GLOB_CONTEXT>20 and GLOB_CONTEXT<40) THEN 	!!Simple
	gs_resol=6
	gs_shadow=0
ENDIF

IF gs_detlevel_3D=`Откл.` THEN END
IF gs_shadow=0 THEN SHADOW OFF
RESOL gs_resol

PEN gs_cont_pen
MATERIAL mat_ladder

! --- HOTSPOTS ---

UnID=1
HOTSPOT a/2-4",0,0,UnID 		:UnID=UnID+1
HOTSPOT -a/2+4",0,0,UnID 		:UnID=UnID+1
HOTSPOT a/2-4",0,zzyzx,UnID 	:UnID=UnID+1
HOTSPOT -a/2+4",0,zzyzx,UnID 	:UnID=UnID+1


! --- EDITABLE HOTSPOTS ---

HOTSPOT -a/2+4",-7",zzyzx,UnID,sr_oh,1+128 		:UnID=UnID+1
HOTSPOT -a/2+4",-7",zzyzx-0.1,UnID,sr_oh,3 		:UnID=UnID+1
HOTSPOT -a/2+4",-7",zzyzx+sr_oh,UnID,sr_oh,2 	:UnID=UnID+1

HOTSPOT a/2-4",-7",zzyzx,UnID,sr_oh,1+128 	:UnID=UnID+1
HOTSPOT a/2-4",-7",zzyzx-0.1,UnID,sr_oh,3 	:UnID=UnID+1
HOTSPOT a/2-4",-7",zzyzx+sr_oh,UnID,sr_oh,2 :UnID=UnID+1

HOTSPOT 0,0,zzyzx,UnID,lnd_w,1+128 			:UnID=UnID+1
HOTSPOT 0,-0.1,zzyzx,UnID,lnd_w,3 			:UnID=UnID+1
HOTSPOT 0,lnd_w,zzyzx,UnID,lnd_w,2 			:UnID=UnID+1

HOTSPOT -a/2+4"-sr_w/2,-7"+sr_d/2,zzyzx+sr_oh,UnID,tr_oh,1+128 	:UnID=UnID+1
HOTSPOT -a/2+4"-sr_w/2,-7"+sr_d/2-0.1,zzyzx+sr_oh,UnID,tr_oh,3 	:UnID=UnID+1
HOTSPOT -a/2+4"-sr_w/2,-7"+sr_d/2+tr_oh,zzyzx+sr_oh,UnID,tr_oh,2 	:UnID=UnID+1

HOTSPOT a/2-4"+sr_w/2,-7"+sr_d/2,zzyzx+sr_oh,UnID,tr_oh,1+128 	:UnID=UnID+1
HOTSPOT a/2-4"+sr_w/2,-7"+sr_d/2-0.1,zzyzx+sr_oh,UnID,tr_oh,3 	:UnID=UnID+1
HOTSPOT a/2-4"+sr_w/2,-7"+sr_d/2+tr_oh,zzyzx+sr_oh,UnID,tr_oh,2 	:UnID=UnID+1


! --- SIDE RAIL ---

ADDz 4"
PRISM_ 4,zzyzx-4"+sr_oh,
	a/2-4",-7"+sr_d/2,15,
	a/2-4"+sr_w,-7"+sr_d/2,15,
	a/2-4"+sr_w,-7"-sr_d/2,15,
	a/2-4",-7"-sr_d/2,15

PRISM_ 4,zzyzx-4"+sr_oh,
	-a/2+4",-7"+sr_d/2,15,
	-a/2+4"-sr_w,-7"+sr_d/2,15,
	-a/2+4"-sr_w,-7"-sr_d/2,15,
	-a/2+4",-7"-sr_d/2,15
DEL 1


! --- FOOT Connection ---

IF foot_typ=foot_typ_mtr[1] THEN  !!! TO the FLOOR
	ADD -a/2+4",-7"+sr_d/2,0
	ROTx 90
	PRISM_ 6,sr_d,
		0,h_w,15,
		0,6",15,
		h_w,6",15,
		h_w,0,15,
		-4",0,15,
		-4",h_w,15
	ADDx a-8"
	MULx -1
	PRISM_ 6,sr_d,
		0,h_w,15,
		0,6",15,
		h_w,6",15,
		h_w,0,15,
		-4",0,15,
		-4",h_w,15
	DEL 4
ENDIF

IF foot_typ=foot_typ_mtr[2] THEN !!! TO the WALL
	ADD -a/2+4",-7"+sr_d/2,6"
	block h_w, f_w, sr_d
	ADDx a-8"
	MULx -1
	block h_w, f_w, sr_d
	DEL 3
ENDIF


! --- STEPS ---

ADD -a/2+4",-7"-sr_d/4,0
FOR s=1 to n_s
	ADDz s*dz-rw-0.001
	 BLOCK a-8",sr_d/2,rw
	DEL 1
next s
DEL 1


MATERIAL mat_fence
! --- VERTICAL CAGEs ---

IF zzyzx>=saftyLimit THEN
	ADDz sr_bl
	PRISM_ 4,zzyzx-sr_bl+sr_oh,
		-a/2+h_w,-13",15,
		-a/2+h_w,-13"-vc_d,15,
		-a/2+h_w+vc_w,-13"-vc_d,15,
		-a/2+h_w+vc_w,-13",15

	PRISM_ 4,zzyzx-sr_bl+sr_oh,
		a/2-h_w,-13",15,
		a/2-h_w,-13"-vc_d,15,
		a/2-h_w-vc_w,-13"-vc_d,15,
		a/2-h_w-vc_w,-13",15

	PRISM_ 4,zzyzx-sr_bl+sr_oh,
		-a/2+h_w,-19",15,
		-a/2+h_w,-19"-vc_d,15,
		-a/2+h_w+vc_w,-19"-vc_d,15,
		-a/2+h_w+vc_w,-19",15

	PRISM_ 4,zzyzx-sr_bl+sr_oh,
		a/2-h_w,-19",15,
		a/2-h_w,-19"-vc_d,15,
		a/2-h_w-vc_w,-19"-vc_d,15,
		a/2-h_w-vc_w,-19",15
	DEL 1

	ADDy -2'1/4"-a/2
	TUBE 4,5,1+2+16+32,
		-vc_d/2,0,0,
		-vc_d/2,vc_w,0,
		vc_d/2,vc_w,0,
		vc_d/2,0,0,

		0,0,0,0,
		0,0,sr_bl,0,
		0,5",11',0,
		0,5",zzyzx+sr_oh,0,
		0,5",zzyzx+sr_oh+0.1,0
	DEL 1

	ADDy -2'1/2"
	ROTz 55
	ADDy -a/2+vc_w
	TUBE 4,5,1+2+16+32,
		-vc_d/2,0,0,
		-vc_d/2,vc_w,0,
		vc_d/2,vc_w,0,
		vc_d/2,0,0,

		0,0,0,0,
		0,0,sr_bl,0,
		0,dL,11',alfa,
		0,dL,zzyzx+sr_oh,alfa,
		0,dL,zzyzx+sr_oh+0.1,alfa
	DEL 3

	ADDy -2'
	MULx -1
	ROTz 55
	ADDy -a/2
	TUBE 4,5,1+2+16+32,
		-vc_d/2,0,0,
		-vc_d/2,vc_w,0,
		vc_d/2,vc_w,0,
		vc_d/2,0,0,

		0,0,0,0,
		0,0,sr_bl,0,
		0,dL,11',alfa,
		0,dL,zzyzx+sr_oh,alfa,
		0,dL,zzyzx+sr_oh+0.1,alfa
	DEL 4
ENDIF


! --- HOOP ---

IF zzyzx>=saftyLimit THEN
	ADDz sr_bl
	PRISM_ 20,2",
		a/2-4"-f_w,0,15,
		a/2-4",0,15,
		a/2-4",-7"-sr_d/2,15,
		a/2,-12",15,
		a/2,-3'2"+1'1/2",79,
		-a/2,-3'2"+1'1/2",1079,
		-a/2,-12",15,
		-a/2+4",-7"-sr_d/2,15,
		-a/2+4",0,79,
		-a/2+4"+f_w,0,15,

		-a/2+4"+f_w,-h_w,15,
		-a/2+4"+h_w,-h_w,15,
		-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
		-a/2+h_w,-12"-h_w/2,15,
		-a/2+h_w,-3'2"+1'1/2",79,
		a/2-h_w,-3'2"+1'1/2",1079,
		a/2-h_w,-12"-h_w/2,15,
		a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
		a/2-4"-h_w,-h_w,15,
		a/2-4"-f_w,-h_w,15

	ADDz 4'

	FOR i=1 to n_hoop
		ADDz (i-1)*4'
		PRISM_ 20,2",
			a/2-4"-f_w,hh,15,
			a/2-4",hh,15,
			a/2-4",-7"-sr_d/2,15,
			a/2,-12",15,
			a/2,-2'9"+1'1/2",79,
			-a/2,-2'9"+1'1/2",1079,
			-a/2,-12",15,
			-a/2+4",-7"-sr_d/2,15,
			-a/2+4",hh,79,
			-a/2+4"+f_w,hh,15,

			-a/2+4"+f_w,-h_w,15,
			-a/2+4"+h_w,-h_w,15,
			-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
			-a/2+h_w,-12"-h_w/2,15,
			-a/2+h_w,-2'9"+1'1/2",79,
			a/2-h_w,-2'9"+1'1/2",1079,
			a/2-h_w,-12"-h_w/2,15,
			a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
			a/2-4"-h_w,-h_w,15,
			a/2-4"-f_w,-h_w,15
		DEL 1
	NEXT i
	DEL 2

	ADDz zzyzx-2"
	PRISM_ 20,2",
		a/2-4"-f_w,0,15,
		a/2-4",0,15,
		a/2-4",-7"-sr_d/2,15,
		a/2,-12",15,
		a/2,-2'9"+1'1/2",79,
		-a/2,-2'9"+1'1/2",1079,
		-a/2,-12",15,
		-a/2+4",-7"-sr_d/2,15,
		-a/2+4",0,79,
		-a/2+4"+f_w,0,15,

		-a/2+4"+f_w,-h_w,15,
		-a/2+4"+h_w,-h_w,15,
		-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
		-a/2+h_w,-12"-h_w/2,15,
		-a/2+h_w,-2'9"+1'1/2",79,
		a/2-h_w,-2'9"+1'1/2",1079,
		a/2-h_w,-12"-h_w/2,15,
		a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
		a/2-4"-h_w,-h_w,15,
		a/2-4"-f_w,-h_w,15


	! --- RAIL on the TOP ---

	ADDz sr_oh
	PRISM_ 16,2",
		a/2-4",-7"+sr_d/2,15,
		a/2-4",-7"-sr_d/2,15,
		a/2,-12",15,
		a/2,-2'9"+1'1/2",79,
		-a/2,-2'9"+1'1/2",1079,
		-a/2,-12",15,
		-a/2+4",-7"-sr_d/2,15,
		-a/2+4",-7"+sr_d/2,15,

		-a/2+4"+h_w,-7"+sr_d/2,15,
		-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
		-a/2+h_w,-12"-h_w/2,15,
		-a/2+h_w,-2'9"+1'1/2",79,
		a/2-h_w,-2'9"+1'1/2",1079,
		a/2-h_w,-12"-h_w/2,15,
		a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
		a/2-4"-h_w,-7"+sr_d/2,15
	DEL 2
ENDIF
IF top_rail THEN
	ADD a/2-4",-7"+sr_d/2,zzyzx
	ROTx 90
	ROTy 90
	 PRISM_ 8,sr_w,
		0,0.3,15,
		tr_oh,0.3,15,
		tr_oh,sr_oh,15,
		0,sr_oh,15,
		0,sr_oh-sr_w,15,
		tr_oh-sr_w,sr_oh-sr_w,15,
		tr_oh-sr_w,0.3+sr_w,15,
		0,0.3+sr_w,15

	IF sr_oh-0.3>0.4 THEN
	 ADDy 0.3+(sr_oh-0.4)/2
	  BLOCK  tr_oh-sr_w,sr_w,sr_w
	 DEL 1
	ENDIF

	ADDz -a+4"+sr_w
	 PRISM_ 8,sr_w,
		0,0.3,15,
		tr_oh,0.3,15,
		tr_oh,sr_oh,15,
		0,sr_oh,15,
		0,sr_oh-sr_w,15,
		tr_oh-sr_w,sr_oh-sr_w,15,
		tr_oh-sr_w,0.3+sr_w,15,
		0,0.3+sr_w,15

	IF sr_oh-0.3>0.3 THEN
	 ADDy 0.3+(sr_oh-0.3)/2
	  BLOCK  tr_oh-sr_w,sr_w,sr_w
	 DEL 1
	ENDIF
	DEL 4
ENDIF


! --- LANDING Platform ---

IF landing THEN
	ADDz zzyzx-0.05

	if gs_detlevel_3D_m = 2 then
	PRISM_ 10,0.05,
		-a/2+4",0,15,
		a/2-4",0,15,
		a/2-4",lnd_w,15,
		-a/2+4",lnd_w,15,
		-a/2+4",0,-1,

		-a/2+5",1",15,
		a/2-5",1",15,
		a/2-5",lnd_w-1",15,
		-a/2+5",lnd_w-1",15,
		-a/2+5",1",-1
	ROTx -90
	n_p = INT((lnd_w-2")/0.05)
	n_p2= INT((a-8"-2")/0.05)

	FOR p=1 to n_p+1
		ADDz p*0.05
		POLY 4,
			-a/2+5",0,
			a/2-5",0,
			a/2-5",-0.05,
			-a/2+5",-0.05
		DEL 1
	NEXT p

	ROTy -90
	ADDz -a/2+4"
	FOR p2=1 to n_p2+1
		ADDz p2*0.05
		POLY 4,
			1",0,
			lnd_w-1",0,
			lnd_w-1",-0.05,
			1",-0.05
		DEL 1
	NEXT p2
	DEL 2
	DEL 2
	else
	PRISM_ 5,0.05,
		-a/2+4",0,15,
		a/2-4",0,15,
		a/2-4",lnd_w,15,
		-a/2+4",lnd_w,15,
		-a/2+4",0,-1
	endif
ENDIF