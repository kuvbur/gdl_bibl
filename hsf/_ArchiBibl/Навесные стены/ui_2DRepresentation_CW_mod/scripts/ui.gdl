
! const values for parameter: iCWObjectType
CWTYPE_ACC_ANCHOR				= 1			! Anchor
CWTYPE_ACC_DESIGNCAP			= 2			! Design Cap
CWTYPE_ACC_SUNSHADE				= 3			! Sun Shade

CWTYPE_JUNCTION					= 10		! Junction

CWTYPE_FRAME_CORNER				= 20		! Diagonal Frame
CWTYPE_FRAME					= 21		! Frame
CWTYPE_FRAME_MOD				= 22		! Modular Frame
CWTYPE_FRAME_PROFILED			= 23		! Profiled Frame
CWTYPE_FRAME_PROF_BUTT			= 24		! Profiled Frame Butt-glazed
CWTYPE_FRAME_DIAGONAL			= 25		! Diagonal Frame with Cap
CWTYPE_FRAME_DIAGONAL_BUTT		= 26		! Diagonal Frame Butt-Glazed
CWTYPE_FRAME_REG_DOUBLE			= 27		! Regular Double with Cap
CWTYPE_FRAME_REG_DOUBLE_BUTT	= 28		! Regular Double Butt-Glazed
CWTYPE_FRAME_REG_BLOCK			= 29		! Regular Block with Cap
CWTYPE_FRAME_REG_BLOCK_BUTT		= 30		! Regular Block Butt-Glazed

CWTYPE_PANEL_CORNER				= 40		! CW Corner Panel
CWTYPE_PANEL_BOX				= 41		! CW Panel Box
CWTYPE_PANEL					= 42		! CW Panel
CWTYPE_PANEL_DOOR_REVOLVING		= 43		! CW Door Revolving
CWTYPE_PANEL_DOOR_SLIDING		= 44		! CW Door Sliding
CWTYPE_PANEL_DOOR				= 45		! CW Door
CWTYPE_PANEL_DOOR_DOUBLEASYM	= 46		! CW Double Door Asymmetric
CWTYPE_PANEL_DOOR_DOUBLE		= 47		! CW Double Door
CWTYPE_PANEL_WIN_DOUBLE			= 48		! CW Double Window
CWTYPE_PANEL_WIN				= 49		! CW Window
CWTYPE_PANEL_PROF_EDGE			= 50		! CW Profiled Edge Panel
CWTYPE_PANEL_POLY_WIN			= 51		! CW Polygonal Window
CWTYPE_PANEL_COMPOSITE			= 52		! CW Composite Panel
CWTYPE_PANEL_CUSTOM				= 99		! CW Custom Panel

! const values for parameter: iDetlevel3D
DETLEVEL3D_MVO			= 1
DETLEVEL3D_SCHEMATIC	= 2
DETLEVEL3D_SIMPLE		= 3
DETLEVEL3D_DETAILED		= 4
DETLEVEL3D_LINE			= 5

file_dependence "ui_list_contourPen_hl(1)"
file_dependence "ui_list_cutPen_hl(1)"
file_dependence "ui_list_BuildingMaterial_hl(1)"
file_dependence "ui_list_SurfaceOverride_hl(1)"
file_dependence "ui_list_surface_hl(1)"
file_dependence "ui_list_fillType_hl(1)"
file_dependence "ui_list_fillPen_hl(1)"
file_dependence "ui_list_fillBgPen_hl(1)"
file_dependence "ui_list_contLineType_hl(1)"

_groupAcc =		(iCWObjectType = CWTYPE_ACC_ANCHOR |\
				iCWObjectType = CWTYPE_ACC_DESIGNCAP |\
				iCWObjectType = CWTYPE_ACC_SUNSHADE |\
				iCWObjectType = CWTYPE_JUNCTION)

_groupFrame =	(iCWObjectType = CWTYPE_FRAME_CORNER |\
				iCWObjectType = CWTYPE_FRAME |\
				iCWObjectType = CWTYPE_FRAME_MOD |\
				iCWObjectType = CWTYPE_FRAME_PROFILED	|\
				iCWObjectType = CWTYPE_FRAME_PROF_BUTT|\
				iCWObjectType = CWTYPE_FRAME_DIAGONAL|\
				iCWObjectType = CWTYPE_FRAME_DIAGONAL_BUTT	|\					
				iCWObjectType = CWTYPE_FRAME_REG_DOUBLE |\
				iCWObjectType = CWTYPE_FRAME_REG_DOUBLE_BUTT |\
				iCWObjectType = CWTYPE_FRAME_REG_BLOCK |\
				iCWObjectType = CWTYPE_FRAME_REG_BLOCK_BUTT)

_groupProfileFrame = 	(iCWObjectType = CWTYPE_FRAME_PROFILED	|\
						iCWObjectType = CWTYPE_FRAME_PROF_BUTT)

_groupPanel =	(iCWObjectType = CWTYPE_PANEL_CORNER |\
				iCWObjectType = CWTYPE_PANEL_BOX |\
				iCWObjectType = CWTYPE_PANEL |\
				iCWObjectType = CWTYPE_PANEL_CUSTOM)

_groupOpening = (iCWObjectType >= CWTYPE_PANEL_DOOR_REVOLVING & iCWObjectType <= CWTYPE_PANEL_POLY_WIN )

_itemID = 1

ui_listfield iReprListID, 0, pageStartY, 444, 265

if iCWObjectType = CWTYPE_PANEL_CUSTOM then
	ui_listitem _itemID, iReprListID, "", 	0,	"", `Примечание: Эти Реквизиты действуют только в Схематичном и Упрощенном видах`							: _itemID = _itemID + 1
endif

if 	_groupPanel then
	ui_listitem _itemID, iReprListID, "iDetlevel2D", 		0,	"ui_list_detailLevel2D_hl(1)"			: _itemID = _itemID + 1
	file_dependence "ui_list_detailLevel2D_hl(1)"	
endif

if 	_groupOpening then
	ui_listitem _itemID, iReprListID, "iDWDet2D", 			0,	"ui_list_detailLevel2D_hl(1)"			: _itemID = _itemID + 1
	file_dependence "ui_list_detailLevel2D_hl(1)"	
endif

if bRepresentationPage then		! _groupAcc, _groupFrame, _groupPanel
	if bShowDetlevel3D then
		ui_listitem _itemID, iReprListID, "iDetlevel3D", 		0,	"ui_list_detailLevel3D_hl(1)"		: _itemID = _itemID + 1
		file_dependence "ui_list_detailLevel3D_hl(1)"	

		if	bShowResol 							&\
		 	(iDetlevel3D = DETLEVEL3D_DETAILED 	|\
		 	iDetlevel3D = DETLEVEL3D_MVO)		then

			ui_listitem _itemID, iReprListID, "gs_resol", 		0,	"ui_list_resol_hl(1)"				: _itemID = _itemID + 1
			file_dependence "ui_list_resol_hl(1)"
		endif
		
		ui_listitem _itemID, iReprListID, "gs_shadow", 			0,	"ui_list_shadow_hl(1)"			: _itemID = _itemID + 1
		file_dependence "ui_list_shadow_hl(1)"

		if 	iCWObjectType = CWTYPE_FRAME_REG_DOUBLE |\
			iCWObjectType = CWTYPE_FRAME_REG_DOUBLE_BUTT then

			ui_listitem _itemID, iReprListID, "panelShadow", 			0,	"ui_list_shadow_hl(1)"			: _itemID = _itemID + 1
		endif
	endif
endif

ui_listitem _itemID, iReprListID, "gs_2D_representation", 		1,	""							: _itemID = _itemID + 1


if ( _groupAcc | _groupPanel ) &\
	iCWObjectType <> CWTYPE_PANEL_BOX then
	ui_listitem _itemID, iReprListID, "penUnCut", 		1,	"ui_list_contourPen_hl(1)"	: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penCut", 		1,	"ui_list_cutPen_hl(1)"		: _itemID = _itemID + 1
endif

if _groupFrame then
	ui_listitem _itemID, iReprListID, "penUnCut", 		1,	"ui_list_contourPen_hl(1)"	: _itemID = _itemID + 1
	if not(_groupProfileFrame) then
		ui_listitem _itemID, iReprListID, "ac_CWFrameCutLinePen", 		1,	"ui_list_cutPen_hl(1)"		: _itemID = _itemID + 1
		ui_listitem _itemID, iReprListID, "ac_CWFrameCutLineType", 		1,	"ui_list_contLineType_hl(1)"		: _itemID = _itemID + 1
	endif
endif

if iCWObjectType = CWTYPE_PANEL_BOX then
	ui_listitem _itemID, iReprListID, "penFrameCut", 			1,	"ui_list_cutPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penFrameUnCut", 		1,	"ui_list_contourPen_hl(1)"		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penGlassCut", 			1,	"ui_list_cutPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penGlassUncut", 		1,	"ui_list_contourPen_hl(1)"		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penPanelCut", 			1,	"ui_list_cutPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penPanelUncut", 		1,	"ui_list_contourPen_hl(1)"		: _itemID = _itemID + 1

	ui_listitem _itemID, iReprListID, "fillFrameCover", 		1,	"ui_list_fillType_hl(1)"		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "fillPenFrameCover", 	1,	"ui_list_fillPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "fillPenBgFrameCover", 	1,	"ui_list_fillBgPen_hl(1)"		: _itemID = _itemID + 1

	ui_listitem _itemID, iReprListID, "fillCover", 			1,	"ui_list_fillType_hl(1)"		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "fillPenCover", 			1,	"ui_list_fillPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "fillPenBgCover", 		1,	"ui_list_fillBgPen_hl(1)"		: _itemID = _itemID + 1
endif

if _groupOpening then
	ui_listitem _itemID, iReprListID, "penFrameCut", 			1,	"ui_list_cutPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penFrameUnCut", 		1,	"ui_list_contourPen_hl(1)"		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penSashCut", 			1,	"ui_list_cutPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "penSashUncut", 			1,	"ui_list_contourPen_hl(1)"		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "fillCover", 			1,	"ui_list_fillType_hl(1)"		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "fillPenCover", 			1,	"ui_list_fillPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "fillPenBgCover", 		1,	"ui_list_fillBgPen_hl(1)"		: _itemID = _itemID + 1

	if 	(iCWObjectType = CWTYPE_PANEL_DOOR				|\
		iCWObjectType = CWTYPE_PANEL_DOOR_DOUBLEASYM	|\ 
		iCWObjectType = CWTYPE_PANEL_DOOR_DOUBLE)		&\
		bThreshold										then

		ui_listitem _itemID, iReprListID, "penThreshold", 			1,	"ui_list_contourPen_hl(1)"	: _itemID = _itemID + 1
		ui_listitem _itemID, iReprListID, "fillThreshold", 		1,	"ui_list_fillType_hl(1)"	: _itemID = _itemID + 1
		ui_listitem _itemID, iReprListID, "fillPenThreshold", 		1,	"ui_list_fillPen_hl(1)"		: _itemID = _itemID + 1
		ui_listitem _itemID, iReprListID, "fillPenBgThreshold", 	1,	"ui_list_fillBgPen_hl(1)"	: _itemID = _itemID + 1
	endif
endif

if _groupProfileFrame then
	! --- Attributes for all views ---
	if bRepresentationPage then
		ui_listitem _itemID, iReprListID, "bOverrideSurface", 1, "ui_list_SurfaceOverride_hl(1)" 	: _itemID = _itemID + 1
		if bOverrideSurface and vardim1(surfacesArray) > 0 then
			for _idxItem = 1 to vardim1(surfacesArray)
				ui_listitem _itemID, iReprListID, surfacesArray[_idxItem], 1,	"ui_list_surface_hl(1)" 			: _itemID = _itemID + 1
			next _idxItem
		endif
	endif

	! --- Attributes for Schematic and Simplified views ---
	ui_listitem _itemID, iReprListID, "", 0, "", `Реквизиты для Схематичного и Упрощенного видов`		: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "ac_CWFrameCutLinePen", 		1,	"ui_list_cutPen_hl(1)"			: _itemID = _itemID + 1
	ui_listitem _itemID, iReprListID, "ac_CWFrameCutLineType", 		1,	"ui_list_contLineType_hl(1)"	: _itemID = _itemID + 1

	if bRepresentationPage then
		if vardim1(buildingMatArray) > 0 then
			for _idxItem = 1 to vardim1(buildingMatArray)
				ui_listitem _itemID, iReprListID, buildingMatArray[_idxItem], 1,	"ui_list_BuildingMaterial_hl(1)" 	: _itemID = _itemID + 1
			next _idxItem
		endif
	endif
	
else
	if bRepresentationPage then
		ui_listitem _itemID, iReprListID, "", 0, "", `Материалы и Покрытия`	: _itemID = _itemID + 1
	
		if vardim1(buildingMatArray) > 0 then
			for _idxItem = 1 to vardim1(buildingMatArray)
				ui_listitem _itemID, iReprListID, buildingMatArray[_idxItem], 1,	"ui_list_BuildingMaterial_hl(1)" 	: _itemID = _itemID + 1
			next _idxItem
			ui_listitem _itemID, iReprListID, "bOverrideSurface", 1, "ui_list_SurfaceOverride_hl(1)" 	: _itemID = _itemID + 1
			if bOverrideSurface then
				for _idxItem = 1 to vardim1(surfacesArray)
					ui_listitem _itemID, iReprListID, surfacesArray[_idxItem], 1,	"ui_list_surface_hl(1)" 			: _itemID = _itemID + 1
				next _idxItem
			endif
		endif
	endif
endif

