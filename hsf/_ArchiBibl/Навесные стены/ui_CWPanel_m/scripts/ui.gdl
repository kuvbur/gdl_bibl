

_bPanel		= 0


if iCWObjectType = CWTYPE_PANEL_CORNER |\
	iCWObjectType = CWTYPE_PANEL_BOX |\
	iCWObjectType = CWTYPE_PANEL_CUSTOM |\
	iCWObjectType = CWTYPE_PANEL then

	_bPanel	= 1
endif

! =============================================================================
! Tabpage Structure Definition (inicialize all POSSIBLE index)
! =============================================================================

iUiID_Root 		= -1
_idxTab = 1

! --------------------------------------------------------------------------------
! Panel object group
! --------------------------------------------------------------------------------
if _bPanel then

	iUiID_GeneralSettings  = _idxTab
	iParID_GeneralSettings = iUiID_Root	: _idxTab = _idxTab + 1

	iUiID_DimensionSettings  = _idxTab
	iParID_DimensionSettings = iUiID_Root	: _idxTab = _idxTab + 1

	iUiID_FrameAndGlass  = _idxTab
	iParID_FrameAndGlass	= iUiID_Root	: _idxTab = _idxTab + 1

endif

! --------------------------------------------------------------------------------
! Additional independent pages
! --------------------------------------------------------------------------------
_idxTab = 300

iUiID_ReprAndSurfaces	= _idxTab
iParID_ReprAndSurfaces	= iUiID_Root	: _idxTab = _idxTab + 1


! =============================================================================
! Collect USED page IDs, names, icons
! =============================================================================
dim uiUsedPageIDs[][2]
dim uiUsedPageNames[][2]

idxPage = 1

! --------------------------------------------------------------------------------
! Panel group
! --------------------------------------------------------------------------------
if _bPanel then

	uiUsedPageIDs[idxPage][1]	= iUiID_GeneralSettings			: uiUsedPageIDs[idxPage][2]		= iParID_GeneralSettings
	uiUsedPageNames[idxPage][1]	= `Общие Параметры`			: uiUsedPageNames[idxPage][2]	= "uiTab_CW_panelGeneral_hl(1)"
	idxPage = idxPage + 1
	
	if iCWObjectType = CWTYPE_PANEL_CORNER then
		uiUsedPageIDs[idxPage][1]	= iUiID_DimensionSettings	: uiUsedPageIDs[idxPage][2]		= iParID_DimensionSettings
		uiUsedPageNames[idxPage][1]	= `Размеры`				: uiUsedPageNames[idxPage][2]	= "uiTab_CW_panelDim_hl(1)"
		idxPage = idxPage + 1
	endif

	if iCWObjectType = CWTYPE_PANEL_BOX then
		uiUsedPageIDs[idxPage][1]	= iUiID_FrameAndGlass		: uiUsedPageIDs[idxPage][2]		= iParID_FrameAndGlass
		uiUsedPageNames[idxPage][1]	= `Рама и Остекление`			: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontFrame_hl(1)"
		idxPage = idxPage + 1
	endif

endif

! --------------------------------------------------------------------------------
! Common Description page
! --------------------------------------------------------------------------------

if iCWObjectType = CWTYPE_PANEL_CUSTOM then
	_stTabPageRep	= `Схематичное и Упрощенное Отображение`
else
	_stTabPageRep	= `Отображение и Покрытия`			
endif

uiUsedPageIDs[idxPage][1]	= iUiID_ReprAndSurfaces					: uiUsedPageIDs[idxPage][2]		= iParID_ReprAndSurfaces
uiUsedPageNames[idxPage][1]	= _stTabPageRep							: uiUsedPageNames[idxPage][2]	= "uiTab_2DRepr_hl(1)"
idxPage = idxPage + 1


! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Панели Навесной Стены`
ui_current_page gs_ui_current_page

for _iTabs = 1 to vardim1(uiUsedPageIDs)
	! --------------------------------------------------------------------------------
	! Panel group
	! --------------------------------------------------------------------------------

	if _bPanel then
		if uiUsedPageIDs[_iTabs][1]= iUiID_GeneralSettings then
			ui_page	uiUsedPageIDs[_iTabs][1], uiUsedPageIDs[_iTabs][2], uiUsedPageNames[_iTabs][1], uiUsedPageNames[_iTabs][2]
			if gs_ui_current_page = uiUsedPageIDs[_iTabs][1] then
				gosub "GeneralSettingsPage"
			endif
		endif

		if uiUsedPageIDs[_iTabs][1]= iUiID_DimensionSettings then
			ui_page	uiUsedPageIDs[_iTabs][1], uiUsedPageIDs[_iTabs][2], uiUsedPageNames[_iTabs][1], uiUsedPageNames[_iTabs][2]
			if gs_ui_current_page = uiUsedPageIDs[_iTabs][1] then
				gosub "DimensionSettingsPage"
			endif
		endif

		if uiUsedPageIDs[_iTabs][1]= iUiID_FrameAndGlass then
			ui_page	uiUsedPageIDs[_iTabs][1], uiUsedPageIDs[_iTabs][2], uiUsedPageNames[_iTabs][1], uiUsedPageNames[_iTabs][2]
			if gs_ui_current_page = uiUsedPageIDs[_iTabs][1] then
				gosub "FrameAndGlassPage"
			endif
		endif

	endif

	! --------------------------------------------------------------------------------
	! Common Description page
	! --------------------------------------------------------------------------------

	if uiUsedPageIDs[_iTabs][1]= iUiID_ReprAndSurfaces then
		ui_page	uiUsedPageIDs[_iTabs][1], uiUsedPageIDs[_iTabs][2], uiUsedPageNames[_iTabs][1], uiUsedPageNames[_iTabs][2]
		if gs_ui_current_page = uiUsedPageIDs[_iTabs][1] then
			gosub "pageCallRepresentation"
		endif
	endif
	
next _iTabs


! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence	"uiTab_CW_panelGeneral_hl(1)"
file_dependence	"uiTab_CW_panelDim_hl(1)"
file_dependence	"uiTab_StorefrontFrame_hl(1)"
file_dependence "uiTab_2DRepr_hl(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"GeneralSettingsPage":
! ------------------------------------------------------------------------------

	infieldWidth = 110
	textPopupWidth = 160
	_infHeight = 19
	_outHeight = 15

	x1 = 10
	x2 = 280
	x3 = 440 - textPopupWidth
	x4 = 440 - infieldWidth
	x5 = 440
	xpic = 32
	dy = 24
	y = pageStartY



	if iCWObjectType = CWTYPE_PANEL_BOX then
		ui_pict 7, xpic, pageStartY, 206, 114, 1				!"ui_gen_box_hl(1)"

		y = pageStartY + 165
		ui_style 0, 0
		ui_outfield `Расположение Верха`, x1, y, 135, _outHeight
		ui_infield{3} "gs_updirection",		x1+140, y-4, 100, _infHeight,
			8, "",
			0, 0, 0, 0, 0, 0,
			0, "0°",	0,
			0, "90°",	90,
			0, "180°",	180,
			0, "270°",	270
		y = y + dy
	endif


	if iCWObjectType = CWTYPE_PANEL then

		ui_pict 1, xpic, pageStartY + 20, 206, 65, 1				!"ui_gen_fix_hl(1)"
		ui_separator 272, pageStartY, 272, pageStartY + 100

		ui_separator x1, pageStartY + 105, 440, pageStartY + 105


		yPict = pageStartY + 115
		
		gosub "coatingSetting"

	endif

	if iCWObjectType = CWTYPE_PANEL_CORNER then
		ui_style 0, 0
		ui_outfield `Внутри`, x1, pageStartY +30, 50, _outHeight, 1
		ui_outfield `Наружу`, x1, pageStartY +49, 50, _outHeight, 1
		if iSpecEdgeSetting = SPECEDGE_NOSPECEDGE then
			ui_pict 26, xpic+35, pageStartY +4, 157, 65, 1				!"ui_gen_corner_nospec_hl(1)"26
			y = pageStartY + 85
		else
			if iSpecEdgeSetting = SPECEDGE_ONEMITERED or iSpecEdgeSetting = SPECEDGE_TWOMITERED then
				ui_outfield `Угол`, x1+120, pageStartY, 60, _outHeight, 1
				ui_pict 27, xpic+35, pageStartY+16, 155, 50, 1			!"ui_gen_corner_mittered_hl(1)"
			else
				ui_outfield `Смещение`, x1+193, pageStartY+49, 60, _outHeight
				ui_pict 28, xpic+35, pageStartY+4, 135, 65, 1				!"ui_gen_corner_butted_hl(1)"
			endif
			y = pageStartY + 85
		endif
		x1 = 10
		x2c = 100
		x3c = 210

		x4c = 240
		x5c = 330
		x6c = 440

		y = pageStartY + 80
		dy = 24
		ui_style 0, 1
		UI_INFIELD{3}	"iSpecEdgeSetting",	x1, y-4, 245, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", stSpecEdgeSetting[1], SPECEDGE_NOSPECEDGE,
				"", stSpecEdgeSetting[2], SPECEDGE_ONEMITERED,
				"", stSpecEdgeSetting[3], SPECEDGE_TWOMITERED,
				"", stSpecEdgeSetting[4], SPECEDGE_ONEBUTTED,
				"", stSpecEdgeSetting[5], SPECEDGE_TWOBUTTED
		ui_style 0, 0

		ui_separator 272, pageStartY, 272, pageStartY + 100
		y = y + dy

		if iSpecEdgeSetting <> SPECEDGE_NOSPECEDGE then
			ui_separator x1, y, 440, y
			y = y  + 10
		endif

		if iSpecEdgeSetting <> SPECEDGE_NOSPECEDGE then
			ui_style 0, 1
			ui_outfield `Торец 1`, x1, y, x3c-x1, _outHeight
			ui_style 0, 0
		endif
		if iSpecEdgeSetting = SPECEDGE_TWOMITERED or iSpecEdgeSetting = SPECEDGE_TWOBUTTED then
			ui_style 0, 1
			ui_outfield `Торец 2`, x4c, y, x6c-x4c, _outHeight
			ui_style 0, 0
		endif

		if iSpecEdgeSetting = SPECEDGE_ONEMITERED or iSpecEdgeSetting = SPECEDGE_TWOMITERED then
			y = y + dy

			ui_outfield `Направление:`, x1, y, x2c-x1-3, _outHeight
			ui_infield "aSpecialEdge1", x2c, y-4, x3c-x2c, _infHeight
			y = y + dy
			ui_outfield `Угол`, x1, y, x2c-x1-3, _outHeight
			ui_infield "aMitterAngle1", x2c, y-4, x3c-x2c, _infHeight
			y = y + 2*dy
			ui_style 0, 0
			ui_outfield `Направление:` + ":", x1, y, x2c-x1-3, _outHeight
			ui_style 0, 0
			ui_outfield `Ориентация Торца по линии привязки Навесной Стены`, x2c, y, x6c-x2c-3, _outHeight
		endif

		if iSpecEdgeSetting = SPECEDGE_TWOMITERED then
			y = pageStartY + 80 + 2*dy + 10
			ui_outfield `Направление:`, x4c, y, x5c-x4c-3, _outHeight
			ui_infield "aSpecialEdge2", x5c, y-4, x6c-x5c, _infHeight
			y = y + dy
			ui_outfield `Угол`, x4c, y, x5c-x4c-3, _outHeight
			ui_infield "aMitterAngle2", x5c, y-4, x6c-x5c, _infHeight
		endif

		if iSpecEdgeSetting = SPECEDGE_ONEBUTTED or iSpecEdgeSetting = SPECEDGE_TWOBUTTED then
			y = y + dy

			ui_outfield `Направление:`, x1, y, x2c-x1-3, _outHeight
			ui_infield "aSpecialEdge1", x2c, y-4, x3c-x2c, _infHeight
			y = y + dy
			ui_outfield `Тип Смещения`, x1, y, x2c-x1-3, _outHeight
			ui_infield "iButtedTyp1", x2c, y-4, x3c-x2c, _infHeight
			y = y + dy
			if iButtedTyp1 = BUTTEDTYPE_CUSTOM then
				ui_outfield `Смещение`, x1, y, x2c-x1-3, _outHeight
				ui_infield "lOffset1", x2c, y-4, x3c-x2c, _infHeight
			else
				ui_outfield `Соединение`, x1, y, x2c-x1-3, _outHeight
				ui_infield "aMitterAngle1", x2c, y-4, x3c-x2c, _infHeight
			endif
			y = y + dy
			ui_style 0, 0
			ui_outfield `Направление:` + ":", x1, y, x2c-x1-3, _outHeight
			ui_style 0, 0
			ui_outfield `Ориентация Торца по линии привязки Навесной Стены`, x2c, y, x6c-x2c-3, _outHeight
		endif

		if iSpecEdgeSetting = SPECEDGE_TWOBUTTED then
			y = pageStartY + 80 + 2*dy + 10
			ui_outfield `Направление:`, x4c, y, x5c-x4c-3, _outHeight
			ui_infield "aSpecialEdge2", x5c, y-4, x6c-x5c, _infHeight
			y = y + dy
			ui_outfield `Тип Смещения`, x4c, y, x5c-x4c-3, _outHeight
			ui_infield "iButtedTyp2", x5c, y-4, x6c-x5c, _infHeight
			y = y + dy
			if iButtedTyp2 = BUTTEDTYPE_CUSTOM then
				ui_outfield `Смещение`, x4c, y, x5c-x4c-3, _outHeight
				ui_infield "lOffset2", x5c, y-4, x6c-x5c, _infHeight
			else
				ui_outfield `Соединение`, x4c, y, x5c-x4c-3, _outHeight
				ui_infield "aMitterAngle2", x5c, y-4, x6c-x5c, _infHeight
			endif
			y = y + dy
		endif
	endif

	if 	iCWObjectType <> CWTYPE_PANEL_CUSTOM then
		ui_style 0, 1
		ui_outfield `Уровень Детализации`, x2, pageStartY, x5-x2, _outHeight
		ui_style 0, 0
	
		y = pageStartY + dy
		ui_outfield `2D`, x3, y, x4-x3-2, _outHeight
		ui_infield{3} "iDetlevel2D", x4, y-4, x5-x4, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", sDetlevel2D[1], aDetlevel2D[1],
				"", sDetlevel2D[2], aDetlevel2D[2],
				"", sDetlevel2D[3], aDetlevel2D[3],
				"", sDetlevel2D[4], aDetlevel2D[4],
				"", sDetlevel2D[5], aDetlevel2D[5]
	
		y = y + dy
		ui_outfield `3D`, x3, y, x4-x3-2, _outHeight
		ui_infield{3} "iDetlevel3D", x4, y-4, x5-x4, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", sDetlevel3D[1], aDetlevel3D[1],
				"", sDetlevel3D[2], aDetlevel3D[2],
				"", sDetlevel3D[3], aDetlevel3D[3],
				"", sDetlevel3D[4], aDetlevel3D[4]
		y = y + dy

		ui_style 0, 0
		ui_infield{3} "gs_shadow", x2, y, 160, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Отбрасывание Теней`, 0,
					"", `Отбрасывание Теней`, 1
	endif

	if iCWObjectType = CWTYPE_PANEL_CUSTOM then
		x1		= 0
		x2		= 210
		x3		= x2 + 90
		xEnd	= 444
		_infHeight	= 19
		_outfHeight	= 15
		_yOffset	= 4
		_yCurr	= pageStartY + _yOffset
		_outfFlag1	= 0

		ui_outfield `Имя Панели`, 			x1,	_yCurr,				x2-x1-3,	_outfHeight,	_outfFlag1
		_nCustomPanels = vardim1(gs_pt)
		dim _emptyStrArray []
		for i = 1 to _nCustomPanels
			_emptyStrArray[i] = ""
		next i

		ui_infield{3} "gs_cust_panel",		x2,	_yCurr - _yOffset,	xEnd-x2,	_infHeight,
			8, "",
			0, 0, 0, 0, 0, 0,
			_emptyStrArray, gs_pt, gs_pt

		_yCurr = _yCurr + dy
		ui_outfield `Расположение Верха`, 		x1,	_yCurr,				x2-x1-3,	_outfHeight,	_outfFlag1
		ui_infield{3} "gs_updirection",		x2,	_yCurr - _yOffset,	x3-x2,		_infHeight,
			8, "",
			0, 0, 0, 0, 0, 0,
			0, "0°",	0,
			0, "90°",	90,
			0, "180°",	180,
			0, "270°",	270


		_yCurr = _yCurr + dy
		ui_outfield `Толщина Панели`,		x1,	_yCurr,				x2-x1-3,	_outfHeight,	_outfFlag1
		ui_infield{3} "panelThk",			x2,	_yCurr - _yOffset,	x3-x2,		_infHeight

		_yCurr = _yCurr + dy
		ui_outfield `Смещение Панели`, 		x1,	_yCurr,				x2-x1-3,	_outfHeight,	_outfFlag1
		ui_infield{3} "offsetPanel",		x2,	_yCurr - _yOffset,	x3-x2,		_infHeight

		_yCurr = _yCurr + dy
		ui_infield{3} "bFlipped",			x1, _yCurr,				xEnd - x1,	_infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Развернуть Панель`, 0,
						"", `Развернуть Панель`, 1

		_yCurr = _yCurr + dy
		ui_infield{3} "bExtendedPanel",			x1, _yCurr,				xEnd - x1,	_infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Увеличить Размеры Панели до Размеров Фиксатора`, 0,
						"", `Увеличить Размеры Панели до Размеров Фиксатора`, 1


	endif

return

! ------------------------------------------------------------------------------
"DimensionSettingsPage":
! ------------------------------------------------------------------------------

	dy			= 24
	_outfHeight	= 15
	_yOffset	= 4
	yPict		= pageStartY + 50
	
	gosub "coatingSetting"

return


! ------------------------------------------------------------------------------
"FrameAndGlassPage":
! ------------------------------------------------------------------------------

	dy = 24
	_infHeight	= 19
	_outfHeight	= 15
	_yOffset	= 4
	_infWidth	= 90

	xPict		= 130
	yPict		= pageStartY + 20
	PictWidth	= 172 
	PictHeight	= 188

	ui_pict 47, xPict, yPict, PictWidth, PictHeight, 1

	ui_infield "offsetPanel",	xPict - _infWidth - 3,	yPict + 70,				_infWidth, _infHeight ui_tooltip `Смещение Объема Панели`
	ui_infield "frameThk",		xPict - _infWidth - 3,	yPict + 150,			_infWidth, _infHeight ui_tooltip `Толщина Коробки`
	ui_infield "frameWidth",	xPict - 5,				yPict + PictHeight + 3,	_infWidth, _infHeight ui_tooltip `Ширина Коробки`
	ui_infield "thkGlass",		xPict + 90,				yPict + PictHeight + 3,	_infWidth, _infHeight ui_tooltip `Толщина Остекления`
	ui_infield "panelDepth",	xPict + 175,			yPict + 115,			_infWidth, _infHeight ui_tooltip `Глубина Панели`

return

! ------------------------------------------------------------------------------
"pageCallRepresentation":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	dim buildingMatArray[] 
 	itemID = 1 
	itemIDs = 1
	_actbShowDetlevel3D	= 1
	_actbShowResol		= 0

	if 	iCWObjectType = CWTYPE_PANEL_CORNER |\
		iCWObjectType = CWTYPE_PANEL |\
		iCWObjectType = CWTYPE_PANEL_CUSTOM then
		buildingMatArray[itemID]	= "panelBmat"				: itemID = itemID + 1
		surfacesArray[itemIDs]		= "matPanel"				: itemIDs = itemIDs + 1
	endif

	if 	iCWObjectType = CWTYPE_PANEL_CORNER |\
		iCWObjectType = CWTYPE_PANEL then
		buildingMatArray[itemID]	= "coatingBmat"				: itemID = itemID + 1
		surfacesArray[itemIDs]		= "matCoating"				: itemIDs = itemIDs + 1

	endif

	if iCWObjectType = CWTYPE_PANEL_BOX then
		buildingMatArray[itemID]	= "frameBmat"				: itemID = itemID + 1
		surfacesArray[itemIDs]		= "matFrame"				: itemIDs = itemIDs + 1

		buildingMatArray[itemID]	= "glassBmat"				: itemID = itemID + 1
		surfacesArray[itemIDs]		= "matGlass"				: itemIDs = itemIDs + 1

		buildingMatArray[itemID]	= "panelBmat"				: itemID = itemID + 1
		surfacesArray[itemIDs]		= "matTop"					: itemIDs = itemIDs + 1
		surfacesArray[itemIDs]		= "matBottom"				: itemIDs = itemIDs + 1

	endif

	

	call "ui_2DRepresentation_CW_m" parameters iCWObjectType 		= iCWObjectType,
												iReprListID 		= iUiID_ReprAndSurfaces,
												bRepresentationPage = 1,
												bShowDetlevel3D 	= _actbShowDetlevel3D,
												bShowResol 			= _actbShowResol,
												buildingMatArray	= buildingMatArray,
												bOverrideSurface	= bOverrideSurface,
												surfacesArray		= surfacesArray,
												iDetlevel3D			= iDetlevel3D,
												gs_shadow			= gs_shadow,
												gs_cont_pen			= gs_cont_pen,
												penUnCut			= penUnCut,
												penCut				= penCut,
												pageStartY 			= 0
return

! ------------------------------------------------------------------------------
"coatingSetting":
! ------------------------------------------------------------------------------

	_infHeight	= 19
	_infWidth	= 90
	xPict		= 130
	PictWidth	= 171 
	PictHeight	= 116

	if bFrontCoating | bBackCoating then
		if bFrontCoating & bBackCoating then
			ui_pict 45, xPict, yPict, PictWidth, PictHeight, 1							! ui_panel_frame_hl_full_coating(1)
		else
			if bBackCoating then ui_pict 43, xPict, yPict, PictWidth, PictHeight, 1		! ui_panel_frame_hl_inside_coating(1).
			if bFrontCoating then ui_pict 44, xPict, yPict, PictWidth, PictHeight, 1	! ui_panel_frame_hl_outside_coating(1)
		endif
	else
		ui_pict 46, xPict, yPict, PictWidth, PictHeight, 1								! ui_panel_frame_hl_no_coating(1)
	endif
	ui_style 0, 0

	ui_infield "panelThk",			xPict + 175,	yPict + 55,	_infWidth, _infHeight ui_tooltip `Толщина Панели`
	if bFrontCoating | bBackCoating then
		ui_infield "CoatingThk",		xPict - _infWidth - 3,		yPict + 72,	_infWidth, _infHeight ui_tooltip `Толщина Облицовки`
	endif
	ui_infield "panelBackAngle",	xPict + 60,	yPict + PictHeight + 3,	_infWidth, _infHeight ui_tooltip `Задний Угол Панели`

	ui_infield{3} "bBackCoating", xPict + 175,		yPict + 55 - dy - 21,	444 - xPict - 175, 40,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `Задняя Облицовка`, 0,
				"", `Задняя Облицовка`, 1

	ui_infield{3} "bFrontCoating", xPict + 175,		yPict + 55 + dy,	444 - xPict - 175, 40,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `Передняя Облицовка`, 0,
				"", `Передняя Облицовка`, 1

return

