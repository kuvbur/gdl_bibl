hotspot2 0,0
property = 0 : _stPropertyValue = ""
if property_layer_name = "Поиск свойства по имени" then
	call "macro_get_param" parameters mgp_in_str="%"+property_name+"%",
										mgp_prim1="",
										mgp_prim2="",
										mgp_prim3="",
										mgp_prim4="",
										mgp_prim5="",
										mgp_qty=0,
										mgp_ld="", mgp_ignore_id="", RETURNED_PARAMETERS mgp_out_str
		_stPropertyValue = mgp_out_str	
endif

if property_layer_name = "Поиск IFC свойства по имени" then
	DIM parNamesArray[]
	n = APPLICATION_QUERY ("ParentCustomParameters", "GetParameterNames(IFC.Folder)", parNamesArray)
	ifc_id = ""
	for i=1 to vardim1(parNamesArray) step 3
		if parNamesArray[i+1]=property_name then
			ifc_id = parNamesArray[i]
			i=vardim1(parNamesArray)
		endif
	next i
	if strlen(ifc_id)>0 then
		ifc_id = "GetParameter("+ifc_id+")"
		n = APPLICATION_QUERY ("ParentCustomParameters", ifc_id , _stPropertyValue)
	endif
endif

if property_layer_name ="Выбранное свойство" then
	_stPropertyID = property_layer : gosub "getPropertyValue"	
endif


CALL "macro_qrcode" parameters macro_qr_txt = _stPropertyValue, macro_qr_A = macro_qr_A

end
"getPropertyValue":
	_stPropertyValue = ""
	propInputIds.propertyIds[1].id = _stPropertyID
	if HASKEY(propOutputVals.propertyValues) then t = REMOVEKEY(propOutputVals.propertyValues)
	n = REQUEST ("Property_Values_Of_Parent", propInputIds, propOutputVals)
	if not(haskey(propOutputVals.propertyValues[1].value_status)) then return
	if propOutputVals.propertyValues[1].value_status = 1 then
		_typeValue = propOutputVals.propertyValues[1].type
		_value = 0
		for j = 1 to vardim1(propOutputVals.propertyValues[1].value)
			_value = propOutputVals.propertyValues[1].value[j]
			flag_empty=0
			if vartype(_value)=1 and _typeValue <> 1 then
				if abs(_value)<EPS then
					flag_empty=1
				else
					if _typeValue = 2 then _value = str("%.0", _value)
					if _typeValue = 3 then _value = str("%.2", _value)
					if _typeValue = 5 then _value = str("%.3", _value)
					if _typeValue = 6 then _value = str("%.2", _value)
					if _typeValue = 7 then _value = str("%.2", _value)
				endif
			endif
			if _typeValue = 1 then
				if propOutputVals.propertyValues[1].value[j]>EPS then
					_value = "Да"
				else
					_value = "Нет"
				endif
			endif
			if vartype(_value)=2 then
				if strlen(_value)=0 or _value=" " then
					flag_empty=1
				endif
			endif
			if not(flag_empty) then
				if j > 1 then
					_stPropertyValue = _stPropertyValue + "; " + _value
				else
					_stPropertyValue = _value
				endif
			endif
		next j
	endif
	if HASKEY(propInputIds.propertyIds) then t = REMOVEKEY(propInputIds.propertyIds)
return