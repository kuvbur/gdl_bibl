dim error[ ]
dim position_info[ ][ ]
dim change_info[ ][ ]
dim arr1[ ]
dim arr2[ ][ ]
!------------------------------------------------------------------------------------------------
! Check global existence
!------------------------------------------------------------------------------------------------

dim irtype[] : dim trtype[] : i=0
i=i+1 : trtype[i] = "Обычный" : irtype[i] = 0
i=i+1 : trtype[i] = "Полужирный" : irtype[i] = 1
i=i+1 : trtype[i] = "Курсив" : irtype[i] = 2
i=i+1 : trtype[i] = "Полужирный курсив" : irtype[i] = 3

ROW_PHASE_PART = 2
ROW_DATE = 8
ROW_IZM_N_DOC = 7
ROW_IZM_SUR = 9
FAM_START_CHANGE = 11
FAM_START_LAYUOT = 6

dim GlobIssueScheme[2][]
dim GlobChangeScheme[2][]
dim LayoutRevHistory[][]
dim LayoutChangeHistory[][]

_bEmptyGlobIssues = 0
_bEmptyGlobChanges = 0
_bEmptyLayoutRevHistory = 0
_bEmptyLayoutChHistory = 0
_bfirstchange = 0
_bEmptychange = 0
_bERROR = 0
n_izm = 0
n_err = 0

current_phase = " "
current_part = " "
current_date = " "
surname = " " 
delim = " "

if vardim2(GLOB_ISSUE_SCHEME) > 0 then
	! global exists
	GlobIssueScheme	= GLOB_ISSUE_SCHEME
else
	_bEmptyGlobIssues = 1
endif

if vardim2(GLOB_CHANGE_SCHEME) > 0 then
	! global exists
	GlobChangeScheme = GLOB_CHANGE_SCHEME
else
	_bEmptyGlobChanges = 1
endif

if GLOB_SCRIPT_TYPE = 2 then
	_bLayoutContext = (GLOB_CONTEXT = 8 | GLOB_CONTEXT = 28)
	if _bLayoutContext then
		! Layout context or Layout Editing feedback: use globals from Issue Manager
		LayoutRevHistory = LAYOUT_REVISION_HISTORY
		LayoutChangeHistory	= LAYOUT_CHANGE_HISTORY
		last_change_inx = vardim1(LayoutChangeHistory)
		last_rev_inx = vardim1(LayoutRevHistory)
		if vardim2(LayoutRevHistory) > 1 then
			if STRSTR(LayoutRevHistory[last_rev_inx][3], "Отсутствует в Выпуске") then 
				txt = "Макет добавлен в выпуск?"
				gosub "write_error"
			endif
		else
			txt = "Макет добавлен в выпуск?"
			gosub "write_error"
		endif
	else
		text2 0.05, 0.05,  "!! Работает только на макетах !!"
	endif
endif
if vardim2(LayoutRevHistory) <> vardim2(GlobIssueScheme) then
	! if global exists, must have same number of columns as GLOB_ISSUE_SCHEME
	_bEmptyLayoutRevHistory = 1
endif
if vardim2(LayoutChangeHistory) <> vardim2(GlobChangeScheme) then
	! if global exists, must have same number of columns as GLOB_CHANGE_SCHEME
	_bEmptyLayoutChHistory = 1
endif
if not(_bEmptyLayoutRevHistory) & _bEmptyLayoutChHistory then
	! if has revision data but no change attached, create 1 dummy empty change row
	for _iChangeCol = 1 to vardim2(GLOB_CHANGE_SCHEME)
		if _iChangeCol = 1 then
			LayoutChangeHistory[1][1] = LayoutRevHistory[1][1]
		else
			LayoutChangeHistory[1][_iChangeCol] = ""
		endif
	next _iChangeCol
endif

if not(_bEmptyLayoutRevHistory) then
	!---------------------- Поиск изм. 0 ------------------------	
	zero_change_inx = 0 : flag = 0
	for i = 1 to last_change_inx
		if STRSTR(LayoutChangeHistory[i][ROW_PHASE_PART], "Изм 0") then
			zero_change_inx = i
			if flag = 1 then
				txt = "У макета должен быть только один Изм 0."
				gosub "write_error"
			else
				flag = 1
			endif
		endif
	next i
	!------------------------------------------------------------
	if zero_change_inx then
		phase_part = LayoutChangeHistory[zero_change_inx][ROW_PHASE_PART]
		current_phase = "" : current_part = "" : current_type = "" : current_nizm = 0
		n = SPLIT (phase_part, "%s %s %s %n", current_phase, current_part, current_type, current_nizm)
		if n<>4 then 
			txt = "Проверьте название -" + phase_part
			gosub "write_error"
		else
			if current_type<>"Изм" and current_type<>"Зам" and current_type<>"Нов" then
				txt = "Может быть только Изм/Зам/Нов" + phase_part
				gosub "write_error"
			endif
		endif
		if strlen(current_phase)>2 then
			txt = "Слишком длинное название стадии."
			gosub "write_error"
		endif
		current_date = LayoutChangeHistory[zero_change_inx][ROW_DATE]
	else
		txt = "Добавьте Изм 0"
		gosub "write_error"
	endif
endif
if n_err > 0 then _bERROR = 1
goto 20
"write_error":
	n_err = n_err + 1
return
20:
