paperToModel = GLOB_SCALE/1000

DEFINE STYLE "stGlobal" fontType,	fontSize_mm,	textAlignment,
						gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
DEFINE STYLE "stGlobal_1" fontType,	fontSize_mm,	5,
						gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
SET STYLE "stGlobal_1"

nnn = request ("Height_of_style", "stGlobal", textHeight)
textHeight = textHeight * paperToModel

add2 -SYMB_POS_X, -SYMB_POS_Y

otobr = 0
if annot[1] = 0 then 
	otobr = 1
else
	for i=1 to vardim1(annot)
		if annot[i] = GLOB_SCALE then otobr = 1
	next i
endif
if not(otobr) then end
if GLOB_CONTEXT = 6 then end

! --- Oriented View ------------------------------ [


angleViewRot = 0
rrr = REQUEST ("View_Rotangle", "", angleViewRot)
totalRotate = (SYMB_ROTANGLE + angleViewRot + gs_follow_north*(GLOB_NORTH_DIR - SYMB_ROTANGLE - 90)) MOD 360
if totalRotate < eps then totalRotate = totalRotate + 360

! --- Oriented View ------------------------------ ]

if scaleValues_m = 2 then		! Scale independent (Paper Size)
	B 				= B_mm * paperToModel
	radius_model = B_mm / 2 * paperToModel
	gs_TextPosY = gs_TextPosY_mm * paperToModel
else
	fontSize_mm = fontSize_model / paperToModel
endif
pen gs_FontPen
line2 -textHeight*3,0,textHeight*3, 0
line2 0,-textHeight*3,0, textHeight*3


B = radius_model * 2

if gs_follow_north then
	mul2 1, mirror	!-1/1
	rot2 GLOB_NORTH_DIR + 180*SYMB_MIRRORED - SYMB_ROTANGLE - 90
endif

unID=1
HOTSPOT2 0,0,unID : unID=unID+1

if GLOB_VIEW_TYPE=9 then end

znak1 = ""
abs_otm = ""
az = ""
aaz = pop + GLOB_PROJECT_ALTITUDE
if aaz = 0 then 
	az = "0.00"
else
	az = str("%.2m", abs(aaz))
endif
if aaz < 0 then znak1 = "-"
abs_otm = znak1 + az

if gs_follow_north then mirror = 1
rot2 -totalRotate * mirror ! -1/1


text2 0,-1.3*B/2,"0.000 = " + abs_otm
text2 0,-1.3*B/2-textHeight,"<SHORTDATE> <TIME>"


SET STYLE "stGlobal"
del 1

if GLOB_VIEW_TYPE=4 or GLOB_VIEW_TYPE=5 then end

textAlignment = 8

! ======================================================================
! Symbol Styles
! ======================================================================

form = ""
n = REQUEST ("Model_text_size_unit", "", form)
unit = "mm"
n = SPLIT (form, "%%.%n%s", variable1, variable2, unit)

if symbolStyles_m = 1 or symbolStyles_m = 2 or symbolStyles_m = 3 or \
		symbolStyles_m = 7 or symbolStyles_m = 8 or \
		symbolStyles_m = 9 or symbolStyles_m = 10 or symbolStyles_m = 12 or symbolStyles_m = 13 then	!with circle

	if symbolStyles_m = 12 then radius_model = 0.8 * radius_model	!smaller circle

	drawindex 10
	pen gs_cont_pen
	fill gs_BackFillType
	poly2_b 2, 1+2+4, gs_BackFillPen, gs_BackFillBackPen,
			0, 0, 900,
			radius_model, 360, 4001
	drawindex 30

	if symbolStyles_m = 12 then radius_model = B / 2				!revert to symbol size

	! --- RADIUS HOTSPOT ---
	displayB = B_mm

	if  scaleValues_m = 2 then	! Scale independent (Paper Size)
		if unit = "mm" then
			hotspot2  0, 			0, 	unID, halfB_M, 1+1024+128, 	B_mm		:unID=unID+1
			hotspot2 -1, 			0, 	unID, halfB_M, 3+1024+128, 	B_mm		:unID=unID+1
			hotspot2 radius_model, 	0, 	unID, halfB_M, 2+1024,		B_mm		:unID=unID+1
		
			hotspot2  0, 			0, 	unID, halfB_M, 1+1024+128, 	B_mm		:unID=unID+1
			hotspot2  1, 			0, 	unID, halfB_M, 3+1024+128, 	B_mm		:unID=unID+1
			hotspot2  -radius_model, 0, unID, halfB_M, 2+1024, 		B_mm		:unID=unID+1
		
			hotspot2  0, 0,				unID, halfB_M, 1+1024+128,	B_mm		:unID=unID+1
			hotspot2  0, -1, 			unID, halfB_M, 3+1024+128, 	B_mm		:unID=unID+1
			hotspot2  0, radius_model, 	unID, halfB_M, 2+1024, 		B_mm		:unID=unID+1
		
			hotspot2  0, 0,				unID, halfB_M, 1+1024+128, 	B_mm		:unID=unID+1
			hotspot2  0, 1, 			unID, halfB_M, 3+1024+128, 	B_mm		:unID=unID+1
			hotspot2  0, -radius_model, unID, halfB_M, 2+1024, 		B_mm		:unID=unID+1
		else
			hotspot2  0, 			0, 	unID, halfB_M, 1+1024+128, 	B_pt		:unID=unID+1
			hotspot2 -1, 			0, 	unID, halfB_M, 3+1024+128, 	B_pt		:unID=unID+1
			hotspot2 radius_model, 	0, 	unID, halfB_M, 2+1024,		B_pt		:unID=unID+1
		
			hotspot2  0, 			0, 	unID, halfB_M, 1+1024+128, 	B_pt		:unID=unID+1
			hotspot2  1, 			0, 	unID, halfB_M, 3+1024+128, 	B_pt		:unID=unID+1
			hotspot2  -radius_model, 0, unID, halfB_M, 2+1024, 		B_pt		:unID=unID+1
		
			hotspot2  0, 0,				unID, halfB_M, 1+1024+128,	B_pt		:unID=unID+1
			hotspot2  0, -1, 			unID, halfB_M, 3+1024+128, 	B_pt		:unID=unID+1
			hotspot2  0, radius_model, 	unID, halfB_M, 2+1024, 		B_pt		:unID=unID+1
		
			hotspot2  0, 0,				unID, halfB_M, 1+1024+128, 	B_pt		:unID=unID+1
			hotspot2  0, 1, 			unID, halfB_M, 3+1024+128, 	B_pt		:unID=unID+1
			hotspot2  0, -radius_model, unID, halfB_M, 2+1024, 		B_pt		:unID=unID+1
		endif
	else
		hotspot2  0, 			0, 	unID, radius_model, 1+128, 	B		:unID=unID+1
		hotspot2 -1, 			0, 	unID, radius_model, 3+128, 	B		:unID=unID+1
		hotspot2 radius_model, 	0, 	unID, radius_model, 2,		B		:unID=unID+1
	
		hotspot2  0, 			0, 	unID, radius_model, 1+128, 	B		:unID=unID+1
		hotspot2  1, 			0, 	unID, radius_model, 3+128, 	B		:unID=unID+1
		hotspot2  -radius_model, 0, 	unID, radius_model, 2, 	B		:unID=unID+1
	
		hotspot2  0, 0,				unID, radius_model, 1+128,	B		:unID=unID+1
		hotspot2  0, -1, 			unID, radius_model, 3+128, 	B		:unID=unID+1
		hotspot2  0, radius_model, 	unID, radius_model, 2, 		B		:unID=unID+1
	
		hotspot2  0, 0,				unID, radius_model, 1+128, 	B		:unID=unID+1
		hotspot2  0, 1, 			unID, radius_model, 3+128, 	B		:unID=unID+1
		hotspot2  0, -radius_model, unID, radius_model, 2, 		B		:unID=unID+1
	endif
endif


if symbolStyles_m = 1 then
	alfa1 = acs(((b/2)*0.045)/(b/2))
	line2 0, b/2, 0, -b/2
	line2 b/2, 0, -b/2, 0
	line2 b/2*0.045, 0, b/2*0.045, sin(alfa1)*b/2
endif

if symbolStyles_m = 2 then
	fill		gs_fill_type
	fillpen =	gs_fill_pen
	backpen =	gs_back_pen

	for i = 1 to 2
		POLY2_B 4, 2, fillpen, backpen,
			0,0,1,
			0,b/2,1,
			-b/2,0,1,
			0,0,-1
		mul2 -1, 1
		fill 		gs_fill2_type
		fillpen =	gs_fill2_pen
		backpen =	gs_back2_pen
	next i
	del 2

	line2 0,b/2,0,-b/2
	line2 b/2,0,-b/2,0
	line2 -b/2,0,0,b/2
	line2 0,b/2,b/2,0

endif


if symbolStyles_m = 3 then
	alfa2 = acs(((b/2)*0.49)/(b/2))
	FILL gs_fill_type
	POLY2_B{2}       4,     2, gs_fill_pen, gs_back_pen,
				   0,            0,            0,
		0,b/2,      1,
		-((b/2)*0.49),-(sin (alfa2)*b/2),      1,
		((b/2)*0.49),-(sin (alfa2)*b/2),      1,
		0,b/2,      1

	line2 0,(b/2+b*0.108),0,-(b/2+b*0.108)
	line2 (b/2+b*0.108),0,-(b/2+b*0.108),0
	line2 0,b/2,-((b/2)*0.49),-(sin (alfa2)*b/2)
	line2 0,b/2,((b/2)*0.49),-(sin (alfa2)*b/2)
	line2 -((b/2)*0.49),-(sin (alfa2)*b/2),((b/2)*0.49),-(sin (alfa2)*b/2)
endif


if symbolStyles_m = 4 then
	alfa3=acs(((b/2)*0.45)/((b/2)*1.097))
	PEN gs_cont_pen
	line2 0,b/2,-(((b/2)*0.45)),-((sin (alfa3)*((b/2)*1.096)))
	line2 0,b/2,((b/2)*0.45),-((sin (alfa3)*((b/2)*1.097)))
	line2 -((b/2)*0.45),-((sin (alfa3)*((b/2)*1.097))),0,-((b/2-((b/2)*0.259)))
	line2 (b/2)*0.45,-((sin (alfa3)*((b/2)*1.097))),0,-((b/2-(b/2)*0.259))
	line2 0,b/2,0,-((b/2-((b/2)*0.259)))

	fill		gs_fill_type
	fillpen =	gs_fill_pen
	backpen =	gs_back_pen

	for i = 1 to 2
		POLY2_B{2}       4,     2, fillpen, backpen,
			   0,            0,            0,
			   0, b/2,      1,
			   -(((b/2)*0.45)),-((sin (alfa3)*((b/2)*1.096))),      1,
			   0,-((b/2-((b/2)*0.259))),      1,
			   0, b/2,      1
		mul2 -1, 1
		fill		gs_fill2_type
		fillpen =	gs_fill2_pen
		backpen =	gs_back2_pen
	next i
	del 2


	! RADIUS HOTSPOT
	if  scaleValues_m = 2 then	! Scale independent (Paper Size)
		if unit = "mm" then
			hotspot2  0, 0, 			unID, halfB_M, 1+1024+128, 	B_mm	:unID=unID+1
			hotspot2  0, -1, 			unID, halfB_M, 3+1024+128, 	B_mm	:unID=unID+1
			hotspot2  0, radius_model, 	unID, halfB_M, 2+1024, 		B_mm	:unID=unID+1
		else
			hotspot2  0, 0, 			unID, halfB_M, 1+1024+128, 	B_pt	:unID=unID+1
			hotspot2  0, -1, 			unID, halfB_M, 3+1024+128, 	B_pt	:unID=unID+1
			hotspot2  0, radius_model, 	unID, halfB_M, 2+1024, 		B_pt	:unID=unID+1
		endif
	else
		hotspot2  0, 0, 			unID, radius_model, 1+128, 	B	:unID=unID+1
		hotspot2  0, -1, 			unID, radius_model, 3+128, 	B	:unID=unID+1
		hotspot2  0, radius_model, 	unID, radius_model, 2, 		B	:unID=unID+1
	endif
endif

if symbolStyles_m =  5 then
	textAlignment = 5
	PEN gs_cont_pen
	line2 0,(b/2),0,-b/2
	line2 0,b/2,((b/2)*0.553),((b/2)*0.681)
	line2 0,b/2,-(((b/2)*0.553)),((b/2)*0.681)
	line2 0,(b/2)*0.904,-((b/2)*0.553),((b/2)*0.584)

	! RADIUS HOTSPOT
	if  scaleValues_m = 2 then	! Scale independent (Paper Size)
		if unit = "mm" then
			hotspot2  0, 0, 			unID, halfB_M, 1+1024+128, 	B_mm	:unID=unID+1
			hotspot2  0, -1, 			unID, halfB_M, 3+1024+128, 	B_mm	:unID=unID+1
			hotspot2  0, radius_model, 	unID, halfB_M, 2+1024, 		B_mm	:unID=unID+1
		else
			hotspot2  0, 0, 			unID, halfB_M, 1+1024+128, 	B_pt	:unID=unID+1
			hotspot2  0, -1, 			unID, halfB_M, 3+1024+128, 	B_pt	:unID=unID+1
			hotspot2  0, radius_model, 	unID, halfB_M, 2+1024, 		B_pt	:unID=unID+1
		endif
	else
		hotspot2  0, 0, 			unID, radius_model, 1+128, 	B	:unID=unID+1
		hotspot2  0, -1, 			unID, radius_model, 3+128, 	B	:unID=unID+1
		hotspot2  0, radius_model, 	unID, radius_model, 2, 		B	:unID=unID+1
	endif
endif


if symbolStyles_m = 6 then
	PEN gs_cont_pen
	line2 0,b/2,0,-b/2
	line2 -(b/2)*0.297,0,(b/2)*0.297,0
	line2 0,(b/2)*0.856,(b/2)*0.078,0
	line2 0,(b/2)*0.856,-((b/2)*0.078),0
	line2 0,-(b/2)*0.856,(b/2)*0.078,0
	line2 0,-(b/2)*0.856,-((b/2)*0.078),0

	fill		gs_fill_type
	fillpen =	gs_fill_pen
	backpen =	gs_back_pen

	FOR k=1 to 2
		for i = 1 to 2
			POLY2_B{2}  4,     2, fillpen, backpen,
					0,            0,      0,
				    0,            0,      1,
					0,(b/2)*0.856,1,
					-(((b/2)*0.078)),0,1,
					0,0,1
			rot2 180
		next i
		del 2
		mul2 -1, 1
		fill		gs_fill2_type
		fillpen =	gs_fill2_pen
		backpen =	gs_back2_pen
	NEXT k
	del 2

	! RADIUS HOTSPOT
	if  scaleValues_m = 2 then	! Scale independent (Paper Size)
		if unit = "mm" then
			HOTSPOT2 0, 0, 				unID, halfB_M, 1+1024+128, 	B_mm	:unID=unID+1
			HOTSPOT2 0, -1, 			unID, halfB_M, 3+1024+128, 	B_mm	:unID=unID+1
			HOTSPOT2 0, radius_model, 	unID, halfB_M, 2+1024, 		B_mm	:unID=unID+1
		
			HOTSPOT2 0, 0, 				unID, halfB_M, 1+1024+128, 	B_mm	:unID=unID+1
			HOTSPOT2 0, 1, 				unID, halfB_M, 3+1024+128, 	B_mm	:unID=unID+1
			HOTSPOT2 0, -radius_model, 	unID, halfB_M, 2+1024, 		B_mm	:unID=unID+1
		else
			HOTSPOT2 0, 0, 				unID, halfB_M, 1+1024+128, 	B_pt	:unID=unID+1
			HOTSPOT2 0, -1, 			unID, halfB_M, 3+1024+128, 	B_pt	:unID=unID+1
			HOTSPOT2 0, radius_model, 	unID, halfB_M, 2+1024, 		B_pt	:unID=unID+1
		
			HOTSPOT2 0, 0, 				unID, halfB_M, 1+1024+128, 	B_pt	:unID=unID+1
			HOTSPOT2 0, 1, 				unID, halfB_M, 3+1024+128, 	B_pt	:unID=unID+1
			HOTSPOT2 0, -radius_model, 	unID, halfB_M, 2+1024, 		B_pt	:unID=unID+1
		endif
	else
		HOTSPOT2 0, 0, 				unID, radius_model, 1+128, 	B	:unID=unID+1
		HOTSPOT2 0, -1, 			unID, radius_model, 3+128, 	B	:unID=unID+1
		HOTSPOT2 0, radius_model, 	unID, radius_model, 2, 		B	:unID=unID+1
	
		HOTSPOT2 0, 0, 				unID, radius_model, 1+128, 	B	:unID=unID+1
		HOTSPOT2 0, 1, 				unID, radius_model, 3+128, 	B	:unID=unID+1
		HOTSPOT2 0, -radius_model, 	unID, radius_model, 2, 		B	:unID=unID+1
	endif
endif


if symbolStyles_m = 7 then
	signThk = 0.1*radius_model
	fill gs_fill_type
	for i = 1 to 2
		poly2_b{2} 8, 1+2+4, gs_fill_pen, gs_back_pen,
				0, 0, 0,
				0,          radius_model,      1,
				-radius_model,        0,       1,
				0, 0, 900,
				-radius_model,        -signThk*sqr(2),    3001,
				-signThk/2, radius_model-signThk/2-signThk*sqr(2),   1,
				-signThk/2, -cos(asn(signThk/2/radius_model))*radius_model,    1,
				0,          -radius_model,   3000,
				0,          radius_model,      -1
		mul2 -1, 1
	next i
	del 2
endif


if symbolStyles_m = 8 then
	dy=radius_model*sin(36)
	dx=radius_model*cos(36)

	fill		gs_fill_type
	fillpen =	gs_fill_pen
	backpen =	gs_back_pen

	for i = 1 to 2
		poly2_b 3, 1+2+4, fillpen, backpen,
				dy, -dx, 1,
				0, -dx*2/3, 1,
				0, radius_model, 1
		mul2 -1, 1
		fill		gs_fill2_type
		fillpen =	gs_fill2_pen
		backpen =	gs_back2_pen
	next i
	del 2
endif


if symbolStyles_m = 9 then
	dy=radius_model*cos(36)
	dx=radius_model*sin(36)

	fill		gs_fill2_type
	poly2_b 3, 1+2+4, gs_fill2_pen, gs_back2_pen,
				-dx, -dy, 		 1,
				0, radius_model, 1,
				dx, -dy,		 1
endif


if symbolStyles_m = 10 then
	line2 -radius_model*7/6, 0, radius_model*7/6, 0
	line2 0, -radius_model*7/6, 0, radius_model*7/6
	line2 -radius_model/6, radius_model*4/3, -radius_model/6, radius_model/6
endif

if symbolStyles_m = 11 or symbolStyles_m = 12 or symbolStyles_m = 13 then
	dx1=radius_model*tan(11)

	dx2=radius_model*sin(22)
	dy2=radius_model*cos(22)

	pen			gs_cont_pen
	fill		gs_fill_type
	fillpen =	gs_fill_pen
	backpen =	gs_back_pen

	!arrow upper part
	for i = 1 to 2
		poly2_b 4, 1+2+4, fillpen, backpen,
				0, 0, 1,
				0, radius_model, 1,
				-dx1, 0, 1,
				0, 0, -1
		mul2 -1, 1
		fill		gs_fill2_type
		fillpen =	gs_fill2_pen
		backpen =	gs_back2_pen
	next i
	del 1

	!arrow lower part
	for i = 1 to 2
		poly2_b 5, 1+2+4, fillpen, backpen,
				0, 0, 1,
				dx1, 0, 1,
				dx2, -dy2, 1,
				0, -dy2 * 2/3, 1,
				0, 0, -1
		mul2 -1, 1
		fill		gs_fill_type
		fillpen =	gs_fill_pen
		backpen =	gs_back_pen
	next i
	del 3
	line2 0, -dx2*2/3,0, -radius_model
endif

if symbolStyles_m = 12 or symbolStyles_m = 13 then
	line2 -dx1, 0, -radius_model, 0
	line2 dx1, 0, radius_model, 0
endif
if symbolStyles_m = 11 then
	line2 -dx1, 0, -dx2, 0
	line2 dx1, 0, dx2, 0
endif
if symbolStyles_m = 11 or symbolStyles_m = 12 then
	! RADIUS HOTSPOT
	if  scaleValues_m = 2 then	! Scale independent (Paper Size)
		if unit = "mm" then
			HOTSPOT2 0, 0, 				unID, halfB_M, 1+1024+128, 	B_mm	:unID=unID+1
			HOTSPOT2 0, -1, 			unID, halfB_M, 3+1024+128, 	B_mm	:unID=unID+1
			HOTSPOT2 0, radius_model, 	unID, halfB_M, 2+1024, 		B_mm	:unID=unID+1
		else
			HOTSPOT2 0, 0, 				unID, halfB_M, 1+1024+128, 	B_pt	:unID=unID+1
			HOTSPOT2 0, -1, 			unID, halfB_M, 3+1024+128, 	B_pt	:unID=unID+1
			HOTSPOT2 0, radius_model, 	unID, halfB_M, 2+1024, 		B_pt	:unID=unID+1
		endif
	else
		HOTSPOT2 0, 0, 				unID, radius_model, 1+128, 	B	:unID=unID+1
		HOTSPOT2 0, -1, 			unID, radius_model, 3+128, 	B	:unID=unID+1
		HOTSPOT2 0, radius_model, 	unID, radius_model, 2, 		B	:unID=unID+1
	endif
endif

! ----------------------------------------------------------------------
! Symbol Styles - Text
! ----------------------------------------------------------------------


DRAWINDEX 30
nTrans = 0
PEN gs_FontPen
if symbolStyles_m = 5 then
	ADD2 0, gs_TextPosY
else
	ADD2 0, B/2+gs_TextPosY
endif
nTrans = nTrans + 1

! Text Rotation - Readable / Always Horizontal/ Align with Symbol
! ----------------------------------------------------------------------

if typeTextRotation_m = 2 then								! Readable
	if totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps) then
		rot2 180
		add2 0,-textHeight
		nTrans = nTrans + 2
	endif
else
	if typeTextRotation_m = 1 then							! Horizontal
		if gs_follow_north then mirror = 1
		rot2 -totalRotate * mirror ! -1/1
		nTrans = nTrans + 1
	endif
endif

! ----------------------------------------------------------------------
text2 0,0,txt
del nTrans

if  scaleValues_m = 2 then	! Scale independent (Paper Size)
	if unit = "mm" then
		if symbolStyles_m = 5 then
			hotspot2  0,	0,					unID,	gs_TextPosY_M, 1+1024+128, 	gs_TextPosY_mm 	: unID=unID+1
			hotspot2  0,	-1,					unID,	gs_TextPosY_M, 3+1024, 	gs_TextPosY_mm	: unID=unID+1
			hotspot2  0,	+gs_TextPosY,		unID,	gs_TextPosY_M, 2+1024,  gs_TextPosY_mm	: unID=unID+1
		else
			hotspot2  0,	B/2,				unID,	gs_TextPosY_M, 1+1024+128, gs_TextPosY_mm 	: unID=unID+1
			hotspot2  0,	B/2-1,				unID,	gs_TextPosY_M, 3+1024, gs_TextPosY_mm	: unID=unID+1
			hotspot2  0,	(B/2+gs_TextPosY),	unID,	gs_TextPosY_M, 2+1024, gs_TextPosY_mm	: unID=unID+1
		endif
	else
		if symbolStyles_m = 5 then
			hotspot2  0,	0,					unID,	gs_TextPosY_M,	1+1024+128, gs_TextPosY_pt 	: unID=unID+1
			hotspot2  0,	-1,					unID,	gs_TextPosY_M,	3+1024, gs_TextPosY_pt 		: unID=unID+1
			hotspot2  0,	+gs_TextPosY,		unID,	gs_TextPosY_M,	2+1024, gs_TextPosY_pt 		: unID=unID+1
		else
			hotspot2  0,	B/2,				unID,	gs_TextPosY_M,	1+1024+128, gs_TextPosY_pt 	: unID=unID+1
			hotspot2  0,	B/2-1,				unID,	gs_TextPosY_M,	3+1024, gs_TextPosY_pt 		: unID=unID+1
			hotspot2  0,	(B/2+gs_TextPosY),	unID,	gs_TextPosY_M,	2+1024, gs_TextPosY_pt 		: unID=unID+1
		endif
	endif
else
	if symbolStyles_m = 5 then
		hotspot2  0,	0,					unID,	gs_TextPosY,	1+128 	: unID=unID+1
		hotspot2  0,	-1,					unID,	gs_TextPosY,	3 		: unID=unID+1
		hotspot2  0,	+gs_TextPosY,		unID,	gs_TextPosY,	2 		: unID=unID+1
	else
		hotspot2  0,	B/2,				unID,	gs_TextPosY,	1+128 	: unID=unID+1
		hotspot2  0,	B/2-1,				unID,	gs_TextPosY,	3 		: unID=unID+1
		hotspot2  0,	(B/2+gs_TextPosY),	unID,	gs_TextPosY,	2 		: unID=unID+1
	endif
endif


