CALL "Общий шаблон элемента" parameters all
if not(is_call) then
	VALUES "var_postr" "Отметка по уклону", "Уклон по отметке"
	VALUES "type_obozn" "Пола", "Кровли", "Разреза"
endif
VALUES "type_uklon" "градусы", "проценты", "промилле"
VALUES "dop_option" "Откл", "Отметку низа", "Отметку верха", "Отметку низа и верха"

if GLOB_MODPAR_NAME = "uk_deg" then GOSUB "deg_to_proc"
if GLOB_MODPAR_NAME = "uk_proc" then GOSUB "proc_to_deg"

values "pos_x" RANGE[-200,200] STEP 0,krat_txt
if not(tipe_r = 1 and var_postr = "Отметка по уклону") then values "pos_y" RANGE[-200,200] STEP 0,krat_txt
if tipe_r = 0 then	
	if var_postr <> "Отметка по уклону" then values "pos_z" RANGE[-200,200] STEP 0,krat_txt
	PARAMETERS otm_verh = SYMB_POS_Z
	if GLOB_MODPAR_NAME = "otm_niz" then PARAMETERS pos_z = otm_niz - otm_verh
	if GLOB_MODPAR_NAME = "pos_z" then PARAMETERS otm_niz = pos_z + otm_verh
	PARAMETERS l_skat_3d = SQR(pos_x * pos_x + pos_y * pos_y + pos_z * pos_z)
	PARAMETERS l_skat_2d = SQR(pos_x * pos_x + pos_y * pos_y)
	PARAMETERS A = abs(pos_x), B = abs(pos_y), ZZYZX = abs(pos_z)
else
	HIDEPARAMETER "pos_z"
	PARAMETERS otm_verh = SYMB_POS_Y
	if GLOB_MODPAR_NAME = "otm_niz" then PARAMETERS pos_y = otm_niz - SYMB_POS_Y
	if GLOB_MODPAR_NAME = "pos_y" then PARAMETERS otm_niz = pos_y + SYMB_POS_Y
	PARAMETERS l_skat_2d = abs(pos_x)
	PARAMETERS l_skat_3d = SQR(pos_x * pos_x + pos_y * pos_y)
	PARAMETERS A = abs(pos_x), B = abs(pos_y), ZZYZX = 0
endif

if var_postr = "Отметка по уклону" then
	if tipe_r = 0 then
		PARAMETERS pos_z = (l_skat_2d/100) * uk_proc
		PARAMETERS otm_niz = otm_verh  +  pos_z
	else
		PARAMETERS pos_y = (abs(pos_x)/100) * uk_proc
		PARAMETERS perepad = pos_y
	endif
else
	if tipe_r = 0 then
		PARAMETERS uk_proc = - (pos_z / l_skat_2d) * 100
		GOSUB "proc_to_deg"
	else
		PARAMETERS uk_proc =  (pos_y / abs(pos_x)) * 100
		GOSUB "proc_to_deg"
	endif
endif

endd = 1
if endd = 0 then

"deg_to_proc":
	PARAMETERS uk_proc = TAN(uk_deg) * 100
	GOSUB "upd_prom"
return

"proc_to_deg":
	PARAMETERS uk_deg = ATN (uk_proc/100)
	GOSUB "upd_prom"
return

"upd_prom":
	PARAMETERS uk_prom = uk_proc * 100
return

endif