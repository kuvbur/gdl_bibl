syst_name = "*" : show_el = 1
nd1 = request("Assoclp_parvalue", "syst_name", namindd1, typed1, flagd1, dim1d1, dim2d1, syst_name)

if nd1 then
	if syst_name = "!!!" or strlen(syst_name)<1 then
		syst_name = "!!!"
		DIM SystemStr[][]
		ConToolType_1 = 0
		nd1 = request("Assoclp_parvalue", "ConToolType_1", namindd1, typed1, flagd1, dim1d1, dim2d1, ConToolType_1)
		queryStr = "GetMEPSystems (" + STR(ConToolType_1, 1, 0) + ")"
		isSystemQuery = APPLICATION_QUERY ("MEPSystem", queryStr, SystemStr)
		num = VARDIM2(SystemStr)/2
		dim MEP_StrConnectionData[][]
		nd1 = request("Assoclp_parvalue", "MEP_StrConnectionData", namindd1, typed1, flagd1, dim1d1, dim2d1, MEP_StrConnectionData)
		FOR i = 2 TO num + 1
			IF MEP_StrConnectionData[1][2] = SystemStr[1][(i-1)*2-1] THEN 
				syst_name = SystemStr[1][(i-1)*2]
				i = num + 1
			endif
		NEXT i
		if syst_name = "!!!" then
			queryStr = "GetMEPSystems (" + STR(2, 1, 0) + ")"
			isSystemQuery = APPLICATION_QUERY ("MEPSystem", queryStr, SystemStr)
			num = VARDIM2(SystemStr)/2
			FOR i = 2 TO num + 1
				IF MEP_StrConnectionData[1][2] = SystemStr[1][(i-1)*2-1] THEN 
					syst_name = SystemStr[1][(i-1)*2]
					i = num + 1
				endif
			NEXT i
		endif
	endif
	gosub "show_mep"
endif
if not(show_el) then end
unID=1

ntxt = str("%.0", inx)
symb = label_otm_num[inx]
gosub "otm"


!
!for jjj=1 to min(max_val, vardim1(label_otm_num))
!	ntxt = str("%.0", jjj)
!	symb = label_otm_num[jjj]
!	gosub "otm"
!	if not(multi_otm) then end
!next jjj


end


"otm":
	if not(pl) and otm_auto_z and GLOB_VIEW_TYPE=4 then
		add2  0,-LABEL_POSITION [1][2]+symb
	else
		add2  LABEL_POSITION [2][1]+LABEL_POSITION [3][1]-Ll,LABEL_POSITION [2][2]+LABEL_POSITION [3][2]
	endif

	if multi_otm then
		Ll = Ll_m[jjj]
		IF FixMarkerSize THEN
			orientMarker_sc = orientMarker_fix_m[jjj] /1000*A_
			orientMarker = orientMarker_fix_m[jjj] /1000*A_
			orientMarker_fix = orientMarker_fix_m[jjj] /1000*A_
		ELSE
			orientMarker_fix = orientMarker_fix_m[jjj]
			orientMarker_sc = orientMarker_sc_m[jjj]
			orientMarker = orientMarker_sc_m[jjj]
		ENDIF
	
		IF scalability = "Фиксированная" THEN
			Gap = Gap_fix_m[jjj] /1000*A_
			Gap_fix = Gap_fix_m[jjj] /1000*A_
			LineLength = LineLength_fix_m[jjj] /1000*A_
			LineLength_fix = LineLength_fix_m[jjj] /1000*A_
		else
			Gap = Gap_m[jjj]
			Gap_fix = Gap_fix_m[jjj]
			LineLength = LineLength_m[jjj]
			LineLength_fix = LineLength_fix_m[jjj]
		ENDIF
	else
		IF FixMarkerSize THEN
			orientMarker_sc = orientMarker_fix /1000*A_
			orientMarker = orientMarker_fix /1000*A_
			orientMarker_fix = orientMarker_fix /1000*A_
		ELSE
			orientMarker = orientMarker_sc
		ENDIF
		
		IF scalability = "Фиксированная" THEN
			Gap = Gap_fix /1000*A_
			Gap_fix = Gap_fix /1000*A_
			LineLength = LineLength_fix /1000*A_
			LineLength_fix = LineLength_fix /1000*A_
		ENDIF
	endif

	! ======================================================
	! Сокращения
	! ======================================================
		_sizeArrow   = sizeArrow /1000*A_
		_hFontStyle1 = hFontStyle1 /1000*A_
		_hFontStyle2 = hFontStyle2 /1000*A_
		
		bSerif = 1
		IF iArrowType = 1 OR iArrowType = 3 THEN bSerif = 0

	HOTSPOT2           0, orientMarker + _sizeArrow *bSerif *SGN(orientMarker), unID, Llm, 1+128: unID=unID+1
	HOTSPOT2  -Llm, orientMarker + _sizeArrow *bSerif *SGN(orientMarker), unID, Llm,     2: unID=unID+1
	HOTSPOT2          1, orientMarker + _sizeArrow *bSerif *SGN(orientMarker), unID, Llm,     3: unID=unID+1
	if Llm<EPS then mul2 -1,1
	
	! ======================================================
	! Фоновая штриховка
	! ======================================================
		DRAWINDEX 10
		IF bOpaque THEN
			FILL GLOB_FILL_INDEX_SOLID
			PEN penOpaque
				POLY2_B  5, 2, penOpaque, penOpaque,
						 0, orientMarker, 1,
						-LOpaque, orientMarker, 1,
						-LOpaque, orientMarker + _hFontStyle1, 1,
						 0, orientMarker + _hFontStyle1, 1,
						 0, orientMarker, -1
		ENDIF
		
	! ======================================================
	! Варианты выносной линии и точки на ней
	! ======================================================
		DRAWINDEX 20
		PEN penLine
		
		IF NOT(wLine1) THEN
!			if multi_otm then
!				HOTSPOT2           0, 0, unID, Ll_m[jjj], 1+128: unID=unID+1
!				HOTSPOT2  Ll_m[jjj], 0, unID, Ll_m[jjj],     2: unID=unID+1
!				HOTSPOT2          -1, 0, unID, Ll_m[jjj],     3: unID=unID+1
!			else
!				HOTSPOT2           0, 0, unID, Ll, 1+128: unID=unID+1
!				HOTSPOT2  Ll, 0, unID, Ll,     2: unID=unID+1
!				HOTSPOT2          -1, 0, unID, Ll,     3: unID=unID+1
!			endif
		ENDIF
		
		IF wLine2 THEN
			LINE2 -_sizeArrow, 0, _sizeArrow, 0
		ENDIF
		
		IF wLine3 THEN
				LINE2 -_sizeArrow, 0, LineLength, 0
			IF scalability = "Фиксированная" THEN
				if multi_otm then
					HOTSPOT2           0, 0, unID, LineLength_m[jjj], 1+128, LineLength_fix_m[jjj]: unID=unID+1
					HOTSPOT2  LineLength_m[jjj], 0, unID, LineLength_m[jjj],     2, LineLength_fix_m[jjj]: unID=unID+1
					HOTSPOT2          -1, 0, unID, LineLength_m[jjj],     3, LineLength_fix_m[jjj]: unID=unID+1
				else
					HOTSPOT2           0, 0, unID, LineLength, 1+128, LineLength_fix: unID=unID+1
					HOTSPOT2  LineLength, 0, unID, LineLength,     2, LineLength_fix: unID=unID+1
					HOTSPOT2          -1, 0, unID, LineLength,     3, LineLength_fix: unID=unID+1
				endif
			ELSE
				if multi_otm then
					HOTSPOT2           0, 0, unID, LineLength_m[jjj], 1+128: unID=unID+1
					HOTSPOT2  LineLength_m[jjj], 0, unID, LineLength_m[jjj],     2: unID=unID+1
					HOTSPOT2          -1, 0, unID, LineLength_m[jjj],     3: unID=unID+1
				else
					HOTSPOT2           0, 0, unID, LineLength, 1+128: unID=unID+1
					HOTSPOT2  LineLength, 0, unID, LineLength,     2: unID=unID+1
					HOTSPOT2          -1, 0, unID, LineLength,     3: unID=unID+1
				endif
			ENDIF
		ENDIF
		
		IF wLine4 THEN
			ADD2 Ll, 0
			Gap = 0
				LINE2 MAX(-Gap, -(Ll - _sizeArrow)), 0,  -(Ll + _sizeArrow), 0
			IF scalability = "Фиксированная" THEN
				if multi_otm then
					HOTSPOT2                          0, 0, unID, Gap_m[jjj], 1+128, Gap_fix_m[jjj]: unID=unID+1
					HOTSPOT2  MAX(-Gap_m[jjj], -Ll_m[jjj] + _sizeArrow), 0, unID, Gap_m[jjj],     2, Gap_fix_m[jjj]: unID=unID+1
					HOTSPOT2                          1, 0, unID, Gap_m[jjj],     3, Gap_fix_m[jjj]: unID=unID+1
				else
					HOTSPOT2                          0, 0, unID, Gap, 1+128, Gap_fix: unID=unID+1
					HOTSPOT2  MAX(-Gap, -Ll + _sizeArrow), 0, unID, Gap,     2, Gap_fix: unID=unID+1
					HOTSPOT2                          1, 0, unID, Gap,     3, Gap_fix: unID=unID+1
				endif
			ELSE
				if multi_otm then
					HOTSPOT2                          0, 0, unID, Gap_m[jjj], 1+128: unID=unID+1
					HOTSPOT2  MAX(-Gap_m[jjj], -Ll_m[jjj] + _sizeArrow), 0, unID, Gap_m[jjj],     2: unID=unID+1
					HOTSPOT2                          1, 0, unID, Gap_m[jjj],     3: unID=unID+1
				else
					HOTSPOT2                          0, 0, unID, Gap, 1+128: unID=unID+1
					HOTSPOT2  MAX(-Gap, -Ll + _sizeArrow), 0, unID, Gap,     2: unID=unID+1
					HOTSPOT2                          1, 0, unID, Gap,     3: unID=unID+1
				endif
			ENDIF
			DEL 1
		ENDIF

	! ======================================================
	! Варианты стрелки
	! ======================================================
		DRAWINDEX 50
		IF iArrowType < 6 THEN	PEN penArrow
		
			LINE2 0, 0, _sizeArrow, _sizeArrow *SGN(orientMarker)
			LINE2 _sizeArrow *(iArrowType > 4), _sizeArrow *(iArrowType > 4) *SGN(orientMarker), -_sizeArrow, _sizeArrow *SGN(orientMarker)
		
		fill GLOB_FILL_INDEX_BACKGROUND
		IF iArrowType > 2 AND iArrowType < 6 THEN
			  POLY2_  3, 7 + 32,
					  0, 0, 1,
					  _sizeArrow, _sizeArrow *SGN(orientMarker), 1,
					 -_sizeArrow *(iArrowType < 5), _sizeArrow *SGN(orientMarker), 1
		ENDIF
		
	! ======================================================
	! Звездочка
	! ======================================================
		IF txtOption = "Специальный текст" OR DimOrigin > 0 OR RoundValue > 0.001 THEN
			IF GLOB_CONTEXT > 20 AND GLOB_CONTEXT < 30 THEN
				ADD2 _hFontStyle1 /2, orientMarker + _hFontStyle1 /2
					LINE2  0, -_hFontStyle2 /3, 0, _hFontStyle2 /3
					LINE2 -_hFontStyle2 /3 *COS(30), -_hFontStyle2 /3 *SIN(30), _hFontStyle2 /3 *COS(30),  _hFontStyle2 /3 *SIN(30)
					LINE2 -_hFontStyle2 /3 *COS(30),  _hFontStyle2 /3 *SIN(30), _hFontStyle2 /3 *COS(30), -_hFontStyle2 /3 *SIN(30)
				DEL 1
			ENDIF
		ENDIF

	! ======================================================
	! Полка, линия отступа и точка определения положения (верх/низ)
	! ======================================================
		IF FixMarkerSize THEN
			if multi_otm then
				HOTSPOT2 0,               0, unID, orientMarker_sc_m[jjj], 1+128, orientMarker_fix_m[jjj]: unID=unID+1
				HOTSPOT2 0, orientMarker_sc_m[jjj], unID, orientMarker_sc_m[jjj], 2, orientMarker_fix_m[jjj]: unID=unID+1
				HOTSPOT2 0,              -1, unID, orientMarker_sc_m[jjj], 3, orientMarker_fix_m[jjj]: unID=unID+1
			else
				HOTSPOT2 0,               0, unID, orientMarker_sc, 1+128, orientMarker_fix: unID=unID+1
				HOTSPOT2 0, orientMarker_sc, unID, orientMarker_sc, 2, orientMarker_fix: unID=unID+1
				HOTSPOT2 0,              -1, unID, orientMarker_sc, 3, orientMarker_fix: unID=unID+1
			endif
		ELSE
			if multi_otm then
				HOTSPOT2 0,               0, unID, orientMarker_sc_m[jjj], 1+128: unID=unID+1
				HOTSPOT2 0, orientMarker_sc_m[jjj], unID, orientMarker_sc_m[jjj], 2: unID=unID+1
				HOTSPOT2 0,              -1, unID, orientMarker_sc_m[jjj], 3: unID=unID+1
			else
				HOTSPOT2 0,               0, unID, orientMarker_sc, 1+128: unID=unID+1
				HOTSPOT2 0, orientMarker_sc, unID, orientMarker_sc, 2: unID=unID+1
				HOTSPOT2 0,              -1, unID, orientMarker_sc, 3: unID=unID+1
			endif
		ENDIF
		LINE2 0, 0, 0, orientMarker + _sizeArrow *bSerif *SGN(orientMarker)
		LINE2 _sizeArrow *bSerif, orientMarker, -LShelf, orientMarker

		
	! ======================================================
	! Тексты
	! ======================================================
	
	PAPER_TO_MODEL = GLOB_SCALE / 1000
	sss=request("Height_of_style", "normalText", height, descent, leading)
	dx = -(height/2-descent-leading-sizeFont/2)*PAPER_TO_MODEL
	
	
		PEN penFont
			RICHTEXT2 -_sizeArrow, orientMarker + dx, "UpTextBlock"+ntxt
			RICHTEXT2 -_sizeArrow - 0.3 *_sizeArrow *(SGN(orientMarker) > EPS), orientMarker - _hFontStyle2 + dx, "DownTextBlock"+ntxt
		
	! ======================================================
	del top
return

"show_mep":
var_t=""
show_all_system = 1 : success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_all_system", show_all_system)
show_el = 0

if show_all_system then
	show_el = 1
	goto "end_show_mep"
endif

if strstr(syst_name,"!!!")>EPS then
	show_el = 1
	goto "end_show_mep"
endif

dim show_system[] : show_system[1] = ""
success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_system", show_system)
n_sys=0
dim syst_name_t[] : syst_name_t[1]=""
if strlen(syst_name)>1 then
	if STRSTR(syst_name, ",")>EPS then
		txt_var = syst_name
		max_iter = 1000
		for i=1 to max_iter
			n_t = STRSTR(txt_var, ",")
			if n_t<EPS then 
				i=max_iter
				if STRSUB(txt_var, 1, 1) = " " then txt_var = STRSUB(txt_var, 2, STRLEN(txt_var))
				if strlen(txt_var)>0 then
					n_sys = n_sys + 1
					syst_name_t[n_sys]=txt_var
				endif
			else
				var_t = STRSUB(txt_var, 1, n_t-1)
				if STRSUB(var_t, 1, 1) = " " then var_t = STRSUB(var_t, 2, STRLEN(var_t))
				txt_var = STRSUB(txt_var, n_t+1, strlen(txt_var))
				n_sys = n_sys + 1
				syst_name_t[n_sys]=var_t
			endif
		next i
	else
		n_sys = 1
		syst_name_t[n_sys]=syst_name
	endif
else
	show_el = 1
	goto "end_show_mep"
endif

for i=1 to vardim1(show_system)
	if strlen(show_system[i])>0 and show_system[i]<>" " then
		for k=1 to n_sys
			sname = syst_name_t[k]
			gosub 33
		next k
	endif
next i

"end_show_mep":
return

33:
	usl1 =(show_system[i]=sname)
	if STRSUB(show_system[i], 1, 1) = "*" and not(usl1) then !Если сборка начинается с * - отсекаем, и ищем конец
		findsp = STRSUB(show_system[i], 2, STRLEN(show_system[i]))
		if STRLEN(sname)>=STRLEN(findsp) then !Если имя сборки меньше длины искомого текста - она явно не подходит
			findsp_t = STRSUB(sname,STRLEN(sname)-STRLEN(findsp)+1,STRLEN(findsp))
			usl1 = (findsp_t=findsp)
		else
			usl1 = 0
		endif
	endif
	if STRSUB(show_system[i], STRLEN(show_system[i]), 1) = "*" and not(usl1) then !Если сборка заканчивается на * - отсекаем, и ищем начало
		findsp = STRSUB(show_system[i], 1, STRLEN(show_system[i])-1)
		if STRLEN(sname)>=STRLEN(findsp) then !Если имя сборки меньше длины искомого текста - она явно не подходит
			findsp_t = STRSUB(sname,1,STRLEN(findsp))
			usl1 = (findsp_t=findsp)
		else
			usl1 = 0
		endif
	endif
	if STRSUB(show_system[i], 1, 1) = "*" and STRSUB(show_system[i], STRLEN(show_system[i]), 1) = "*" and not(usl1) then !Ищем вхождение
		findsp = STRSUB(show_system[i], 2, STRLEN(show_system[i])-2)
		usl1 = (STRSTR(sname, findsp, 1)>0)
	endif
	if STRSUB(show_system[i], 1, 1) = "*" and STRLEN(show_system[i])=1 and not(usl1) then !Все сборки
		usl1 = 1
	endif
	if usl1 then 
		show_el = 1
		goto "end_show_mep"
	endif
return

