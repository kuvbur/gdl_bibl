hotspot 0,0,0,900
if GLOB_PREVIEW_MODE=1 or GLOB_PREVIEW_MODE=2 or GLOB_FEEDBACK_MODE=1 then
	show_res=9
else
	if GLOB_SEO_TOOL_MODE = 1 then
		show_res=19
	else
		show_res=res
	endif
endif
SHADOW OFF, OFF
resol show_res
material boltMat
pen gs_cont_pen

!Which Parts to Show for the 2D Symbol
	if not(isProjection) then
		showParts = 7
	endif
	includeHead = bittest(showParts, 0)
	includeNut = bittest(showParts, 1)
	includeShaft = bittest(showParts, 2)
!End of Which Parts to Show for the 2D Symbol

!Adjust Angle
	if not(isProjection) then
		R = shaftLength/2
		if not(iscall) then hotspot 0, 1, 0, iHotspot + 1, incline, 7 + 128
		if not(iscall) then hotspot 0, 0, 0, iHotspot + 2, incline, 6
		if not(iscall) then hotspot R*cos(incline), 0, -R*sin(incline), iHotspot + 3, incline, 5
		if not(iscall) then hotspot R, 0, 0, iHotspot + 4, incline, 4 + 128
		iHotspot = iHotspot + 4
	endif
	roty Incline
	rotx swivel
!End of Adjust Angle

!Hole
	if GLOB_SEO_TOOL_MODE = 1 and show_type<>SHOW_TOP and show_type<>SHOW_SIDE and show_hole and show_res>9 then
		if holePen > 0 then
			pen holePen
		endif
		xform 0, 0, 1, 0,
			0, 1, 0, 0,
			1, 0, 0, 0
		cylind insideLength, holeSizeR
		del 1
		end
	endif
!End of Hole

if ((GLOB_STRUCTURE_DISPLAY = upr_n and upr = 0) or not(show_crew) or show_type = NOT_SHOW) and show_res>9 then
	HOTARC holeSizeR, 0, 360, 9010
	addx insideLength
	hotspot 0,0,0,9011
	HOTARC holeSizeR, 0, 360, 9012
	del 1
	end
endif

!Remove Parts of the Bolt for the 2D Symbol
	xform 1, 0, 0, 0,
		0, 0, 1, 0,
		0, 1, 0, 0
	nCuts = 0
	if not(includeHead) then
		nCuts = nCuts + 1
		cutpoly 4,
			0, -1,
			0, 1,
			-1, 1,
			-1, -1
	endif
	if not(includeShaft) then
		nCuts = nCuts + 1
		cutpoly 4,
			0, -1,
			0, 1,
			insideLength, 1,
			insideLength, -1
	endif
	if not(includeNut) then
		addx insideLength
		nCuts = nCuts + 1
		cutpoly 4,
			0, -1,
			0, 1,
			1, 1,
			1, -1
		del 1
	endif
	del 1
!End of Remove Parts of the Bolt for the 2D Symbol
!Dynamic Hotspots
	if not(iscall) then hotspot -washerThick - headThick, 0, 0
	!Adjust the Shaft Length
		if not(isProjection) then
			addx -washerThick
			if not(iscall) then hotspot 0, 0, 0, iHotspot + 1, shaftLength, 1 + 256
			if not(iscall) then hotspot shaftLength, 0, 0, iHotspot + 2, shaftLength, 2 + 256
			if not(iscall) then hotspot -1, 0, 0, iHotspot + 3, shaftLength, 3 + 256
			iHotspot = iHotspot + 3
			del 1
		endif
	!End of Adjust the Shaft Length
	!Adjust the Inside Length
		if not(isProjection) then
			if not(iscall) then hotspot 0, 0, 0, iHotspot + 1, insideLength, 1 + 256
			if not(iscall) then hotspot insideLength, 0, 0, iHotspot + 2, insideLength, 2 + 256
			if not(iscall) then hotspot -1, 0, 0, iHotspot + 3, insideLength, 3 + 256
			iHotspot = iHotspot + 3
		endif
	!End of Adjust the Inside Length
	!Adjust Threaded Length
		if not(isProjection) then
			addx -washerThick + shaftLength
			if not(iscall) then hotspot 0, 0, 0, iHotspot + 1, threadLength, 1 + 128
			if not(iscall) then hotspot -threadLength, 0, 0, iHotspot + 2, threadLength, 2
			if not(iscall) then hotspot 1, 0, 0, iHotspot + 3, threadLength, 3
			iHotspot = iHotspot + 3
			del 1
		endif
	!End of Adjust Inside Length
!End of Dynamic Hotspots

rad = edgeWidth - flatWidth !RADIUS for ROUNDED EDGES
depth = 0.54127 * pitch

!Trim the Hexagonal Nut and Bolt
	for i = 60 TO 360 step 60
		rotx i
		addy edgeWidth/2
		cutplane -60
		del 2
	next i
!End of Trim the Hexagonal Nut and Bolt

if show_bolt and show_type=SHOW_3D then
	!Head and Shaft
		addx -washerThick
		if showDetail then
			gosub "Thread"
			gosub "Bolt Head"
			revolve	nsp/3, 360, 3,
				get(nsp)
		else
			put shaftLength, 0, 1
			put shaftLength, shaftThick/2, 1
			gosub "Bolt Head - Simple"
			revolve	nsp/3, 360, 3,
				get(nsp)
		endif
		del 1
	!End of Head and Shaft
endif

!Nuts
	if (nNuts and nutThick > 0) and includeNut and show_type=SHOW_3D  then
		for iNut = 1 to nNuts
			addx insideLength + washerThick + (iNut - 1)*nutThick
			gosub "Nut"
			del 1
		next iNut
	endif
!End of Nuts

for i = 1 TO 6
	cutend
next i


for i = 1 to nCuts
	cutend
next i

call "Условный_болт" PARAMETERS ALL show_type=show_type, insideLength=insideLength, flatWidth=flatWidth, amg2d=amg2d
if show_type<>SHOW_3D then end

!Washers
	if showWashers and show_type=SHOW_3D then
		!Washers to Engineers Bolt
			if includeHead and show_bolt then
				addx -washerThick
				if washerType = "Square" then
					gosub "Square Washer"
				else
					gosub "Round Washer"
				endif
				del 1
			endif
			if nNuts and includeNut then
				addx insideLength
				if washerType = "Square" then
					gosub "Square Washer"
				else
					gosub "Round Washer"
				endif	
				del 1
			endif
		!End of Washers to Engineers Bolt
	endif
!End of Washers

end

"Thread":
	R1 = shaftThick/2
	R2 = R1 - depth
	put shaftLength, R1 - 0.075*shaftThick, 0,
		shaftLength - 0.075*shaftThick, R1, 0
	threadX = 0.075*shaftThick
	while threadX + pitch < threadLength do
		put shaftLength - threadX - pitch/2, R2, 0,
			shaftLength - threadX - pitch, R1, 0
		threadX = threadX + pitch
	endwhile
return

"Nut":
	R1 = edgeWidth/2
	R2 = edgeWidth - flatWidth
	if showDetail Then
		put 0, shaftThick/2, 2,
			0, R1 - R2, 1,
			R2, R1 - R2, 901,
			R2, -90, 2001,
			nutThick - R2, R1, 1,
			nutThick - R2, R1 - R2, 901,
			rad, -90, 2001,
			nutThick, shaftThick/2, 2,
			0, shaftThick/2, -1
	else		
		put 0, shaftThick/2, 1,
			0, R1, 1,
			nutThick, R1, 1,
			nutThick, shaftThick/2, 2,
			0, shaftThick/2, -1
	endif
	revolve nsp/3, 360, 0,
		get(nsp)
return

"Bolt Head":
	R1 = edgeWidth/2
	R2 = edgeWidth - flatWidth
	put 0, shaftThick/2, 2,
		0, R1, 2,
		-headThick + R2, R1, 1,
		-headThick + R2, R1 - R2, 901,
		R2, 90, 2002
return

"Bolt Head - Simple":
	put 0, shaftThick/2, 1
	put 0, edgeWidth/2, 1
	put -headThick, edgeWidth/2, 1
	put -headThick, 0, 1
return

"Square Washer":
	roty 90
	W = washerSize/2
	prism_ 7, washerThick,
		-W, W, 15,
		W, W, 15,
		W, -W, 15,
		-W, -W, 15,
		-W, -W, -1,
		0, 0, 915 + 64,
		shaftThick/2 + .0005, 360, 4015 + 64
	del 1
return

"Round Washer":
	roty 90
	W = washerSize/2
	prism_ 3, washerThick,
		0, 0, 915 + 64,
		W, 360, 4015 + 64,
		shaftThick/2 + .0005, 360, 4015 + 64
	del 1
return
