hotspot2 0,0,9001
! Резьбa 
DRAWINDEX 10 
project2  3, 270, 2
if yRot=180 then mul2 -1,1
if yRot=270 then mul2 -1,1
LR=nPos+nThk+wThk
LR2=nPos
IF is_stud THEN
	LR3=POS_2+wThk
	LR4=LR3-nThk-wThk
	if no_nut_right then
		LR3=POS_2+wThk
		LR4=LR3
	endif
ELSE
	IF thread="Complete" THEN
		LR3=0
	ELSE
		LR3=lenB-hit
	ENDIF
ENDIF
if no_nut_left then LR=nPos

if (type_hole=2 or type_hole=3) and yRot_r=90 then
	if abs(coord_hole[1])<abs(coord_hole[2]) then
		max_r=-coord_hole[2]
	else
		max_r=-coord_hole[1]
	endif
	HOTSPOT2 max_r,	0, unID,	ang_hole,	4+128: unID=unID+1
	HOTSPOT2 0,	0,	unID,	ang_hole,	6+512*(yRot=90): unID=unID+1
	HOTSPOT2  max_r*cos(-ang_hole)*0.6,	 max_r*sin(-ang_hole)*0.6,	unID,	ang_hole, 5: unID=unID+1
	rot2 -ang_hole
	if type_hole=2 then
		HOTSPOT2	0,					0,	unID,	coord_hole[1],	1+128: unID=unID+1
		HOTSPOT2	-coord_hole[1],	0,	unID,	coord_hole[1],	2: unID=unID+1
		HOTSPOT2	1,					0,	unID,	coord_hole[1],	3: unID=unID+1
	endif
	HOTSPOT2	0,					0,	unID,	coord_hole[2],	1+128: unID=unID+1
	HOTSPOT2	-coord_hole[2],	0,	unID,	coord_hole[2],	2: unID=unID+1
	HOTSPOT2	1,					0,	unID,	coord_hole[2],	3: unID=unID+1
	del 1
endif

if show_hole2D then
	if yRot_r=90 and show_type<>SHOW_TOP and show_type<>SHOW_3D then
		if type_hole=1 then
			circle2 0,0,holeSizeR
			HOTARC2 0, 0, holeSizeR, 0, 360, 9005
			hotspot2 0,holeSizeR,9006
			hotspot2 0,-holeSizeR,9007
			hotspot2 holeSizeR,0,9008
			hotspot2 -holeSizeR,0,9009
		endif
		if type_hole=2 or type_hole=3 then
			rot2 -ang_hole
			PUT 0,holeSizeR,1
			PUT -coord_hole[1],holeSizeR,1
			PUT -coord_hole[1],0,900
			PUT 0,180,4001
			PUT -coord_hole[1],-holeSizeR,1
			PUT -coord_hole[2],-holeSizeR,1
			if type_hole=2 then
				PUT -coord_hole[2],0,900
				PUT 0,180,4001
				hotspot2 -coord_hole[1]-holeSizeR,0,9010
				hotspot2 -coord_hole[2]+holeSizeR,0,9011
				hotspot2 -coord_hole[1],-holeSizeR,9012
				hotspot2 -coord_hole[1],holeSizeR,9013
				hotspot2 -coord_hole[2],-holeSizeR,9014
				hotspot2 -coord_hole[2],holeSizeR,9015
				HOTARC2 -coord_hole[1], 0, holeSizeR, 0, 360, 9005
				HOTARC2 -coord_hole[2], 0, holeSizeR, 0, 360, 9006
			else
				PUT -coord_hole[2],holeSizeR,1
				hotspot2 -coord_hole[1]-holeSizeR,0
				hotspot2 -coord_hole[1],-holeSizeR
				hotspot2 -coord_hole[1],holeSizeR
				HOTARC2 -coord_hole[1], 0, holeSizeR, 0, 360, 9005
			endif
			PUT 0,holeSizeR,-1
			poly2_ NSP/3, 1+4,get(NSP)
			del 1
		endif
		if show_hole_axis then
			if type_hole=1 then
				line2 0,-holeSizeR-zas_axis,0,holeSizeR+zas_axis
				line2 -holeSizeR-zas_axis,0,holeSizeR+zas_axis,0
			endif
			if type_hole=2 then
				rot2 -ang_hole
				line2 -coord_hole[1]-holeSizeR-zas_axis,0,-coord_hole[2]+holeSizeR+zas_axis,0
				line2 -coord_hole[1],-holeSizeR-zas_axis,-coord_hole[1],holeSizeR+zas_axis
				line2 -coord_hole[2],-holeSizeR-zas_axis,-coord_hole[2],holeSizeR+zas_axis
			endif
			if type_hole=3 then
				rot2 -ang_hole
				line2 -coord_hole[1]-holeSizeR-zas_axis,0,-coord_hole[2]+zas_axis,0
				line2 -coord_hole[1],-holeSizeR-zas_axis,-coord_hole[1],holeSizeR+zas_axis
			endif
		endif
	endif
	if yRot_r=0 then
		FILL FF
		ss = 0.0003
		IF thread="Partial" AND NOT(is_stud) THEN
			LR2=nPos
			LR3=wThk
		ENDIF
		DRAWINDEX 20 !!! ЗАКРОЕМ ЛИНИИ
		POLY2_B 4, 7,FP,BGP,
			strart_hole+ss, -holeSizeR, 0,
			strart_hole+insideLength-ss, -holeSizeR, 0,
			strart_hole+insideLength-ss,  holeSizeR, 0,
			strart_hole+ss,  holeSizeR, 0
	endif
endif


! Hex Bolt Metric
bCond = (abs(xRot) < EPS and abs(yRot_r) < EPS and abs(zRot) < EPS)
if not(bCond) then end
gosub 100    ! Hotspots
if show_type<>SHOW_3D then end

IF W_axis THEN
	!!! ДЕЛАМ ОСЬ+++
	LINE_TYPE LT
	PEN FP
	IF W_axis THEN LINE2 -wid,0, lenB+wid,0
ENDIF
LINE_TYPE 1
PEN gs_cont_pen

!!! ДЕЛАМ БОЛТ - ПОКАЗ РЕЗЬБЫ+++
IF show_hole2D THEN
	Line2 LR3,-diaB/2+tpi, LR2,-diaB/2+tpi 
	Line2 LR3, diaB/2-tpi, LR2, diaB/2-tpi
ENDIF

Line2 LR,-diaB/2+tpi, lenB+tpi,-diaB/2+tpi 
Line2 LR, diaB/2-tpi, lenB+tpi, diaB/2-tpi

Line2 lenB,-diaB/2, lenB+tpi,-diaB/2+tpi 
Line2 lenB, diaB/2, lenB+tpi, diaB/2-tpi

Line2 lenB+tpi, diaB/2-tpi, lenB+tpi, -diaB/2+tpi

!!! ДЕЛАМ ШПИЛЬКУ
IF is_stud THEN
	Line2 LR4,-diaB/2+tpi, -tpi,-diaB/2+tpi 
	Line2 LR4, diaB/2-tpi, -tpi, diaB/2-tpi
	Line2 0,-diaB/2, -tpi,-diaB/2+tpi 
	Line2 0, diaB/2, -tpi, diaB/2-tpi
	Line2 -tpi, diaB/2-tpi, -tpi, -diaB/2+tpi
ENDIF

end ! -------------------------------------------


100 : !!! ========== Hotspots ==========
	!!! ----- Length -----
	hotspot2  0  , 0, uid, lenB, 1+256 : uid = uid+1    ! base (anchor)
	hotspot2 -1  , 0, uid, lenB, 3     : uid = uid+1    ! ref
	hotspot2  lenB, 0, uid, lenB, 2     : uid = uid+1    ! moving


	!!! ----- Nut Position -----

	hotspot2  0   , 0, uid, nPos, 1 : uid = uid+1    ! base 
	hotspot2 -1   , 0, uid, nPos, 3 : uid = uid+1    ! ref
	hotspot2  nPos, 0, uid, nPos, 2 : uid = uid+1    ! moving

	IF is_stud  THEN
		
		!!! ШПИЛЬКА
		ADD2 wThk,0

			hotspot2  0   , 0, uid, POS_2, 1 : uid = uid+1    ! base 
			hotspot2 -1   , 0, uid, POS_2, 3 : uid = uid+1    ! ref
			hotspot2  POS_2, 0, uid, POS_2, 2 : uid = uid+1    ! moving

		DEL 1
		
		!!!+++++ ЗАЗОР М/У ГАЙКАМИ +++++
		ADD2 wThk+POS_2,0

		hotspot2  0, DIAB, uid, LZ, 1+128 : uid = uid+1    ! base 
		hotspot2 -1,    0, uid, LZ,     3 : uid = uid+1    ! ref
		hotspot2 LZ, DIAB, uid, LZ,     2 : uid = uid+1    ! moving

		DEL 1
	
	ENDIF

	!!! ----- Диаметр / Типоразмер -----
	mul2 1, sgn (nRad)
	add2 0, -diaB/2
	hotspot2 0,  0, uid, diaB, 1+128 : uid = uid+1    ! base 
	hotspot2 0, -1, uid, diaB, 3     : uid = uid+1    ! ref
	if select = "Диаметр" then
		hotspot2 0,  diaB, uid, diaB, 2       : uid = uid+1    ! moving
	else
		hotspot2 0,  diaB, uid, diaB, 2, refB : uid = uid+1    ! moving
	endif
	del 2
	!!! ----- Switch -----
	add2 -hThk, 0
	hotspot2 0,  0   , uid, nRad, 1+128     : uid = uid+1    ! base 
	hotspot2 0, -1   , uid, nRad, 3         : uid = uid+1    ! ref
	hotspot2 0,  nRad, uid, nRad, 2, select : uid = uid+1    ! moving
	del 1
	if abs (wThk) < EPS then return
	hotspot2 wThk, wod/2*sgn (nRad)
return

