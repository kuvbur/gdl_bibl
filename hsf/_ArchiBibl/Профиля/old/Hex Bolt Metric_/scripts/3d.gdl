hotspot 0,0,0,900
if GLOB_PREVIEW_MODE=1 or GLOB_PREVIEW_MODE=2 or GLOB_FEEDBACK_MODE=1 then
	res = 9
	bChamfer=0
	show_thread=0
else
	if GLOB_SEO_TOOL_MODE = 1 then
		res = 19
	else
		if res<3 then res=13
	endif
endif
if iscall then goto 3001
show_res=res
SHADOW OFF, OFF
resol show_res
material gs_ifc_mat
pen gs_cont_pen

roty 90    ! Horizontal
rotx xRot
roty yRot 
rotz zRot

if show_hole then
	ADDZ strart_hole-0.001
	if type_hole=2 or type_hole=3 then
		if abs(coord_hole[1])<abs(coord_hole[2]) then
			max_r=coord_hole[2]
		else
			max_r=coord_hole[1]
		endif
		HOTSPOT max_r,	0,0, unID,	ang_hole,	4+128: unID=unID+1
		HOTSPOT 0,	0, 0,	unID,	ang_hole,	6: unID=unID+1
		HOTSPOT 0,	0, 1,	unID,	ang_hole,	7: unID=unID+1
		HOTSPOT  max_r*cos(ang_hole)*0.6,	 max_r*sin(ang_hole)*0.6,0,	unID,	ang_hole, 5: unID=unID+1
		rotz ang_hole
		if type_hole=2 then
			HOTSPOT	0,					0,	0,	unID,	coord_hole[1],	1+128: unID=unID+1
			HOTSPOT	coord_hole[1],	0,	0,	unID,	coord_hole[1],	2: unID=unID+1
			HOTSPOT	-1,					0,	0,	unID,	coord_hole[1],	3: unID=unID+1
		endif
		HOTSPOT	0,					0,	0,	unID,	coord_hole[2],	1+128: unID=unID+1
		HOTSPOT	coord_hole[2],	0,	0,	unID,	coord_hole[2],	2: unID=unID+1
		HOTSPOT	-1,					0,	0,	unID,	coord_hole[2],	3: unID=unID+1
		del 1
	endif
	if show_hole_axis and show_type = NOT_SHOW then
		if type_hole=1 then
			lin_ 0,-holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0
			lin_ -holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0,0
			lin_ 0,-holeSizeR-zas_axis,insideLength+0.001,0,holeSizeR+zas_axis,insideLength+0.001
			lin_ -holeSizeR-zas_axis,0,insideLength+0.001,holeSizeR+zas_axis,0,insideLength+0.001
		endif
		if type_hole=2 then
			rotz ang_hole
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-holeSizeR-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-holeSizeR-zas_axis,0,insideLength+0.001
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[2],holeSizeR+zas_axis,0,coord_hole[2],-holeSizeR-zas_axis,0
			lin_ coord_hole[2],holeSizeR+zas_axis,insideLength+0.001,coord_hole[2],-holeSizeR-zas_axis,insideLength+0.001
			del 1
		endif
		if type_hole=3 then
			rotz ang_hole
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-zas_axis,0,insideLength+0.001
			del 1
		endif
	endif
	del 1
endif

if GLOB_SEO_TOOL_MODE = 1 and show_type<>SHOW_TOP and show_type<>SHOW_SIDE and show_hole and show_res>9 then
	ADDZ strart_hole-0.001
	if type_hole=1 then
		cylind insideLength+0.001, holeSizeR
	endif
	if type_hole=2 or type_hole=3 then
		rotz ang_hole
		PUT 0,holeSizeR,15
		PUT coord_hole[1],holeSizeR,15
		PUT coord_hole[1],0,915
		PUT 0,-180,4015
		PUT coord_hole[1],-holeSizeR,15
		PUT coord_hole[2],-holeSizeR,15
		if type_hole=2 then
			PUT coord_hole[2],0,915
			PUT 0,-180,4015
		else
			PUT coord_hole[2],holeSizeR,15
		endif
		PUT 0,holeSizeR,-1
		prism_ NSP/3,insideLength+0.001,get(NSP)
		del 1
	endif
	del 1
	end
endif

roty -90
call "Условный_болт" PARAMETERS ALL show_type=show_type, insideLength=insideLength, flatWidth=flatWidth, amg2d=amg2d
del 1

if show_type = NOT_SHOW and show_res>9 then
	HOTARC holeSizeR, 0, 360, 9010
	addx insideLength
	hotspot 0,0,0,9011
	HOTARC holeSizeR, 0, 360, 9012
	del 1
	end
endif

if show_type<>SHOW_3D then end
3001:
IF show_thread gosub 100    ! Bolt       
IF show_thread THEN gosub 200    ! Thread    
gosub 300    ! Bolt Head         
gosub 400    ! Nut      
gosub 500    ! Washers    
if not(iscall) then gosub 600    ! Hotspots

del 4


end ! -------------------------------------------


100 : !!! ========== Bolt ==========
	if abs(lenB-hit) > EPS then
		cylind lenB-hit, diaB/2
	endif
	addz lenB-hit
	IF not(show_thread)THEN tpi=0 !!!+++++
	cylind hit, diaB/2 - tpi 
	del 1
return


200 : !!! ========== Thread ========== 
	if req ("GDL_version") >= 1.564 then bAC12 = 1    ! >= AC12
	totAng  = 360*hit/tpi + 360   ! total angle

	for k = -stepAng to totAng + stepAng + EPS step stepAng    ! phantom points
		put diaB/2*cos(k), diaB/2*sin(k), tpi*k/360
	next k

	addz lenB-hit
	cutplane  1, 1, 0, 1    ! start
	del 1

	addz lenB
	cutplane  1, 1, 0, 0    ! end
	del 1

	addz lenB - hit - tpi/2
	
	if bAC12 then
		tubea 4, nsp/3, 1+2+16+32 + 128,    ! reduce Polycount
			0  ,  0    ,  0,
			tpi,  tpi/2,  0,    
			tpi, -tpi/2,  0,  
			0  ,  0    , -1,  
			get(nsp)
	else
		delta = atn((tpi/2)/tpi)
		xOff  = EPS*cos(delta)    ! additional points for rendering
		yOff  = EPS*sin(delta)

		tubea 8, nsp/3, 1+2+16+32,
			0       ,  0         ,  0,
			xOff    ,  yOff      ,  1,    ! rendering 
			tpi-xOff,  tpi/2-yOff,  1,    ! rendering
			tpi     ,  tpi/2     ,  0, 
			tpi     , -tpi/2     ,  0, 
			tpi-xOff, -tpi/2+yOff,  1,    ! rendering
			xOff    , -yOff      ,  1,    ! rendering 
			0       ,  0         , -1,
			get(nsp)
	endif

	del 1
	cutend
	cutend
return


300 : !!! ========== Bolt Head ==========
	IF is_stud THEN ADDZ POS_2
	if not(no_nut_right) then
		put -nDia/2, -nDia/2*tan(30)  , 15,
			-nDia/2,  nDia/2*tan(30)  , 15,
			 0     ,  nDia/(2*cos(30)), 15,
			 nDia/2,  nDia/2*tan(30)  , 15,
			 nDia/2, -nDia/2*tan(30)  , 15,
			 0     , -nDia/(2*cos(30)), 15,
			-nDia/2, -nDia/2*tan(30)  , -1
	
		group "Head"
			prism_ nsp/3, hThk, get(nsp)		
		endgroup
	
		group "hChamfer"
			mulz -1
			gosub 350
			del 1
			IF is_stud THEN
				addz hThk
				gosub 350
				del 1
			ENDIF
		endgroup
		addz -hThk
		if not(bChamfer) then placegroup "Head"
		if bChamfer then placegroup subgroup("Head", "hChamfer")
		if two_nut_right and is_stud then
			addz -nThk
			rotz 90
			if not(bChamfer) then placegroup "Head" 
			if bChamfer then placegroup subgroup("Head", "hChamfer")
			del 2
		endif
		del 1
		killgroup "Head"
		killgroup "hChamfer"
	endif
	IF is_stud THEN DEL 1
return


350 : !!! ========== Chamfer ==========
	if not(bChamfer) then return
	nutRad = abs(nRad)

	put 0            , nutRad     ,  2,
		nutRad-nDia/2, 0.96*nDia/2,  2,
		nutRad-nDia/2, nutRad     ,  2,
		0            , nutRad     , -1

	roty -90
	addx -(nutRad-nDia/2)
	revolve nsp/3, 360, 1+2+4+8+16+32, get(nsp)	
	del 2

return


400 : !!! ========== Nut ==========	
if not(no_nut_left) then
	put -nDia/2, -nDia/2*tan(30)  , 15,
		-nDia/2,  nDia/2*tan(30)  , 15,
		 0     ,  nDia/(2*cos(30)), 15,
		 nDia/2,  nDia/2*tan(30)  , 15,
		 nDia/2, -nDia/2*tan(30)  , 15,
		 0     , -nDia/(2*cos(30)), 15,
		-nDia/2, -nDia/2*tan(30)  , -1,

		 0     , 0          , 900,    ! smooth hole
		 diaB/2, 0          , 79,
		 diaB/2, 360-stepAng, 4000+79,
		 diaB/2, 0          , -1

	group "Nut"
		prism_ nsp/3, nThk, get(nsp)	
	endgroup

	group "nChamfer"
		mulz -1
		gosub 350
		del 1

		body -1

		addz nThk
		gosub 350
		del 1
	endgroup

	addz nPos + wThk
	if not(bChamfer) then placegroup "Nut" 
	if bChamfer then placegroup subgroup("Nut", "nChamfer")
	
	if two_nut_left then
		addz nThk
		rotz 90
		if not(bChamfer) then placegroup "Nut" 
		if bChamfer then placegroup subgroup("Nut", "nChamfer")
		del 2
	endif
	del 1
	killgroup "Nut"
	killgroup "nChamfer"
endif
return


500 : !!! ========== Washers ==========
	if abs (wThk) < EPS then return
IF not(is_stud) THEN       !!!++++++++
	nn = 2 : if no_nut_left then nn=1
	for i = 1 to nn
		prism_  3, wThk,
			0    , 0  ,  900+79,
			wod/2, 360, 4000+79,
			wid/2, 360, 4000+79

		addz nPos
	next i
		del nn
!!! ПРОСТО ДОБАВИМ++++++++++
ELSE
	if not(no_nut_left) then
		ADDZ nPos
				prism_  3, wThk,
					0    , 0  ,  900+79,
					wod/2, 360, 4000+79,
					wid/2, 360, 4000+79
		DEL 1
	endif
	if not(no_nut_right) then
		ADDZ POS_2
				prism_  3, wThk,
					0    , 0  ,  900+79,
					wod/2, 360, 4000+79,
					wid/2, 360, 4000+79
		DEL 1
	endif
ENDIF
return



600 : !!! ========== Hotspots ==========
	!!! ----- Length -----
	hotspot 0, 0,  0  , uid, lenB, 1+256 : uid = uid+1    ! base 
	hotspot 0, 0, -1  , uid, lenB, 3     : uid = uid+1    ! ref
	hotspot 0, 0,  lenB, uid, lenB, 2     : uid = uid+1    ! moving

	!!! ----- Nut Position -----
	hotspot 0, 0,  0   ,  uid, nPos, 1 : uid = uid+1    ! base 
	hotspot 0, 0, -1   ,  uid, nPos, 3 : uid = uid+1    ! ref
	hotspot 0, 0,  nPos,  uid, nPos, 2 : uid = uid+1    ! moving


	!!! ----- Диаметр / Типоразмер -----
	muly  sgn (nRad)
	addy -diaB/2
	hotspot 0,  0, 0, uid, diaB, 1+128 : uid = uid+1    ! base 
	hotspot 0, -1, 0, uid, diaB, 3     : uid = uid+1    ! ref
	if select = "Диаметр" then
		hotspot 0,  diaB, 0, uid, diaB, 2       : uid = uid+1    ! moving
	else
		hotspot 0,  diaB, 0, uid, diaB, 2, refB : uid = uid+1    ! moving
	endif
	del 2

	!!! ----- Switch -----
	addz -hThk
	hotspot 0,  0   , 0, uid, nRad, 1+128     : uid = uid+1    ! base 
	hotspot 0, -1   , 0, uid, nRad, 3         : uid = uid+1    ! ref
	hotspot 0,  nRad, 0, uid, nRad, 2, select : uid = uid+1    ! moving
	del 1


	if abs (wThk) < EPS then return 
	hotspot 0, wod/2*sgn(nRad), wThk

return