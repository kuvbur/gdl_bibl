! Hex Bolt Metric
if len<>0 and lenB<>len then PARAMETERS lenB = len 
PARAMETERS ang2 = abs(yRot) MOD 180 
call "Условный_болт" PARAMETERS ALL

!!! ---------- Values ----------
	values "thread" "Partial", "Complete"
	IF not(is_stud) THEN 
		HIDEPARAMETER "POS_2", "two_nut_right","no_nut_right"
		PARAMETERS two_nut_right = 0
		PARAMETERS no_nut_right = 0
	else
		HIDEPARAMETER "thread"
	endif
	gosub 10    ! table


	dim diam[18][11]
	for i = 1 to nsp/11
		diam[i][1]  = get(1) * 0.001
		diam[i][2]  = get(1) * 0.001
		diam[i][3]  = get(1) * 0.001
		diam[i][4]  = get(1) * 0.001
		diam[i][5]  = get(1) * 0.001
		diam[i][6]  = get(1) * 0.001
		diam[i][7]  = get(1) * 0.001
		diam[i][8]  = get(1) * 0.001
		diam[i][9]  = get(1) * 0.001
		diam[i][10] = get(1) * 0.001
		diam[i][11] = get(1) * 0.001
	next i


	!!! Create list of bolt Диаметрs
	if GLOB_MODPAR_NAME = "diaB" then   ! Диаметр
		for k = 1 to 18
			put diam [k][1]
		next k
	
		values "diaB" get (nsp)   ! Диаметр

		if abs(diaB-0.0016) < EPS then values "refB" "M1.6"
		if abs(diaB-0.0020) < EPS then values "refB" "M2"
		if abs(diaB-0.0025) < EPS then values "refB" "M2.5"
		if abs(diaB-0.0030) < EPS then values "refB" "M3"  
		if abs(diaB-0.0040) < EPS then values "refB" "M4"
		if abs(diaB-0.0050) < EPS then values "refB" "M5"
		if abs(diaB-0.0060) < EPS then values "refB" "M6"
		if abs(diaB-0.0080) < EPS then values "refB" "M8"
		if abs(diaB-0.0100) < EPS then values "refB" "M10"
		if abs(diaB-0.0120) < EPS then values "refB" "M12"
		if abs(diaB-0.0160) < EPS then values "refB" "M16"
		if abs(diaB-0.0200) < EPS then values "refB" "M20"
		if abs(diaB-0.0240) < EPS then values "refB" "M24"
		if abs(diaB-0.0300) < EPS then values "refB" "M30"
		if abs(diaB-0.0360) < EPS then values "refB" "M36"
		if abs(diaB-0.0420) < EPS then values "refB" "M42"
		if abs(diaB-0.0480) < EPS then values "refB" "M48"
		if abs(diaB-0.0640) < EPS then values "refB" "M64"
	else 
		values "refB" "M1.6", "M2", "M2.5", "M3", "M4", "M5", "M6", "M8", "M10", 
						"M12", "M16", "M20", "M24", "M30", "M36", "M42", "M48", "M64"   
	endif
	
	
	if GLOB_MODPAR_NAME = "refB" then   ! Типоразмер
		if refB = "M1.6" then values "diaB" 0.0016
		if refB = "M2"   then values "diaB" 0.0020
		if refB = "M2.5" then values "diaB" 0.0025
		if refB = "M3"   then values "diaB" 0.0030
		if refB = "M4"   then values "diaB" 0.0040
		if refB = "M5"   then values "diaB" 0.0050
		if refB = "M6"   then values "diaB" 0.0060
		if refB = "M8"   then values "diaB" 0.0080
		if refB = "M10"  then values "diaB" 0.0100
		if refB = "M12"  then values "diaB" 0.0120
		if refB = "M16"  then values "diaB" 0.0160
		if refB = "M20"  then values "diaB" 0.0200
		if refB = "M24"  then values "diaB" 0.0240
		if refB = "M30"  then values "diaB" 0.0300
		if refB = "M36"  then values "diaB" 0.0360
		if refB = "M42"  then values "diaB" 0.0420
		if refB = "M48"  then values "diaB" 0.0480
		if refB = "M64"  then values "diaB" 0.0640
	else
		for k = 1 to 18
			put diam [k][1]
		next k

		values "diaB" get (nsp)   ! Диаметр
	endif


	!!! create list of standard lengths
	while aa < 0.4 + 0.30*(diaB > 0.036) do
		while aa < 0.2 do
			while aa < 0.065 do
				aa = aa + 0.005
				if aa >= 2 * diaB then put aa
			endwhile 
			aa = aa + 0.01
			if aa >= 2 * diaB then put aa
		endwhile 
		aa = aa + 0.02
		if aa >= 2 * diaB then put aa
	endwhile

	values "lenB" get(nsp)   ! Length



	!!! Find nut for chosen bolt Диаметр
	bCond1 = (lenB < 0.125 or abs(lenB-0.125) < EPS)
	bCond2 = (lenB > 0.125 and (lenB < 0.200 or abs(lenB-0.200) < EPS))
	bCond3 = (lenB > 0.200)

	for j = 1 to 18 
		if abs (diam [j][1] - diaB) < EPS  then
			values "nDia" diam[j][2]       ! Nut Диаметр 
			values "nThk" diam[j][3]       ! Nut Thickness 
			values "hThk" diam[j][4]       ! Head Thickness 
			values "wid"  diam[j][5]       ! Washer Inner Диаметр
			values "wod"  diam[j][6]       ! Washer Outer Диаметр
			values "wThk" diam[j][7], 0    ! Washer Thickness 0=Off 
			values "tpi"  diam[j][8]       ! Thread Pitch

			if thread = "Partial" then     ! Thread Length 
				if bCond1 then values "hit" min (lenB, diam[j][9]) 
				if bCond2 then values "hit" min (lenB, diam[j][10])
				if bCond3 then values "hit" min (lenB, diam[j][11])
			endif
			if thread = "Complete" then values "hit" lenB

			dist = (diam[j][2]/2)/sin(60)   ! nut outer radius
			flag = (refB = "M3")
			values "wrench" str (diam[j][2]*1000, 1+flag, flag)   ! Wrench
		endif
	next j


	values "nPos" range [max (wThk, lenB - hit), lenB - wThk - nThk]    ! Nut Position 


	!!! ----- hotspot switch -----
	if GLOB_MODPAR_NAME = "nRad" or GLOB_MODPAR_NAME = "refB" then
		if nRad > 0 then values "select" "Диаметр"
		if nRad < 0 then values "select" "Типоразмер"
	else
		values "select" "Диаметр", "Типоразмер"
	endif

	if GLOB_MODPAR_NAME = "select" or GLOB_MODPAR_NAME = "diaB" then
		if select = "Диаметр"  then values "nRad"   dist
		if select = "Типоразмер" then values "nRad" (-dist)
	else
		values "nRad" (-dist), dist   ! nut outer radius
	endif


!!! ---------- Parameters ----------
	parameters res = res


!!! ---------- Display ----------
	if select = "Диаметр"  then HIDEPARAMETER "refB"
	if select = "Типоразмер" then HIDEPARAMETER "diaB"


end ! ------------------------------------------------------------------
 

10 : !!! diaB | nDia | nThk |  hThk | wid | wod | wThk | tpi | hit1 | hit2 | hit3    
IF NOT(is_stud) THEN !++++
	put  1.6   ,  3   ,  1.25,  1   ,  1.7,   4  , 0.3 , 0.35,   8  ,   8  ,   8,    ! M1.6  ?
		 2     ,  4   ,  1.5 ,  1.5 ,  2.2,   5  , 0.3 , 0.4 ,  10  ,  10  ,  10,    ! M2    1st choice
		 2.5   ,  5   ,  2   ,  1.75,  2.7,   6.5, 0.5 , 0.45,  11  ,  11  ,  11,    ! M2.5  1
		 3     ,  5.5 ,  2.25,  2.00,  3.2,   7  , 0.5 , 0.5 ,  12  ,  12  ,  12,    ! M3    1
		 4     ,  7   ,  3   ,  2.75,  4.3,   9  , 0.8 , 0.7 ,  14  ,  14  ,  14,    ! M4    1
		 5     ,  8   ,  4   ,  3.5 ,  5.3,  10  , 1   , 0.8 ,  16  ,  16  ,  16,    ! M5    1
		 6     , 10   ,  5   ,  4   ,  6.4,  12.5, 1.6 , 1   ,  18  ,  24  ,  24,    ! M6    1
		 8     , 13   ,  6.5 ,  5.5 ,  8.4,  17  , 1.6 , 1.25,  22  ,  28  ,  28,    ! M8    1
		10     , 17   ,  8   ,  7   , 10.5,  21  , 2   , 1.5 ,  26  ,  32  ,  45,    ! M10   1
		12     , 19   , 10   ,  8   , 13  ,  24  , 2.5 , 1.75,  30  ,  36  ,  49,    ! M12   1
		16     , 24   , 13   , 10   , 17  ,  30  , 3   , 2   ,  38  ,  44  ,  57,    ! M16   1
		20     , 30   , 16   , 13   , 21  ,  37  , 3   , 2.5 ,  46  ,  52  ,  65,    ! M20   1
		24     , 36   , 19   , 15   , 25  ,  44  , 4   , 3   ,  54  ,  60  ,  73,    ! M24   1
		30     , 46   , 24   , 19   , 31  ,  56  , 4   , 3.5 ,  66  ,  72  ,  85,    ! M30   1
		36     , 55   , 29   , 23   , 37  ,  66  , 5   , 4   ,  78  ,  84  ,  97,    ! M36   1
		42     , 65   , 34   , 26   , 43  ,  80  , 6   , 4.5 ,  90  ,  96  , 109,    ! M42   ?
		48     , 75   , 38   , 30   , 50  ,  90  , 7   , 5   , 102  , 108  , 121,    ! M48   ?  
		64     , 95   , 51   , 40   , 65  , 115  , 9   , 6   , 132  , 138  , 151     ! M64   ?  


!		 3.5   ,  6   ,  2.75,  2.5  ,  3.8,   8 , 0.5  , 0.6 ,  13  ,  13  ,  13,   ! M3.5  ! 2nd choice    
!		14     , 22   , 11.5 ,  8.8  , 15  ,  28 , 2.5  , 2   ,  34  ,  40  ,  53,   ! M14   ! 2
!		18     , 27   , 15   , 11.5  , 19  ,  34 , 3    , 2.5 ,  42  ,  48  ,  61,   ! M18   ! 2
!		22     , 33   , 18   , 14    , 23  ,  40 , 3.5  , 2.5 ,  50  ,  56  ,  69,   ! M22   ! 2
!		27     , 41   , 22   , 17    , 28  ,  48 , 4    , 3   ,  60  ,  66  ,  79,   ! M27   ! 2
!		33     , 50   , 26   , 21    , 34  ,  65 , 5    , 4   ,  72  ,  78  ,  91,   ! M33   ! 2
!		39     , 60   , 31   , 25    , 40  ,  75 , 6    , 4.5 ,  84  ,  90  , 103,   ! M39   ! 2
!		45     , 70   , 36   , 28    , 46  ,  85 , 7    , 5   ,  96  , 102  , 115    ! M45   ! 2

ELSE !!!+++ ДОБАВИМ ОДИН. ГОЛ. НА ШПИЛЬКУ + LSR +++
      !!! diaB | nDia | nThk |  hThk | wid | wod | wThk | tpi | hit1 | hit2 | hit3    

	put  1.6   ,  3   ,  1.25,  1.25, 1.7,   4  , 0.3 , 0.35,   8  ,   8  ,   8,    ! M1.6  ?
		 2     ,  4   ,  1.5 ,  1.5,  2.2,   5  , 0.3 , 0.4 ,  10  ,  10  ,  10,    ! M2    1st choice
		 2.5   ,  5   ,  2   ,  2,    2.7,   6.5, 0.5 , 0.45,  11  ,  11  ,  11,    ! M2.5  1
		 3     ,  5.5 ,  2.25,  2.25, 3.2,   7  , 0.5 , 0.5 ,  12  ,  12  ,  12,    ! M3    1
		 4     ,  7   ,  3   ,  3,    4.3,   9  , 0.8 , 0.7 ,  14  ,  14  ,  14,    ! M4    1
		 5     ,  8   ,  4   ,  4 ,   5.3,  10  , 1   , 0.8 ,  16  ,  16  ,  16,    ! M5    1
		 6     , 10   ,  5   ,  5   , 6.4,  12.5, 1.6 , 1   ,  18  ,  24  ,  24,    ! M6    1
		 8     , 13   ,  6.5 ,  6.5 , 8.4,  17  , 1.6 , 1.25,  22  ,  28  ,  28,    ! M8    1
		10     , 17   ,  8   ,  8   , 10.5,  21  , 2   , 1.5 ,  26  ,  32  ,  45,    ! M10   1
		12     , 19   , 10   , 10   , 13  ,  24  , 2.5 , 1.75,  30  ,  36  ,  49,    ! M12   1
		16     , 24   , 13   , 13   , 17  ,  30  , 3   , 2   ,  38  ,  44  ,  57,    ! M16   1
		20     , 30   , 16   , 16   , 21  ,  37  , 3   , 2.5 ,  46  ,  52  ,  65,    ! M20   1
		24     , 36   , 19   , 19   , 25  ,  44  , 4   , 3   ,  54  ,  60  ,  73,    ! M24   1
		30     , 46   , 24   , 24   , 31  ,  56  , 4   , 3.5 ,  66  ,  72  ,  85,    ! M30   1
		36     , 55   , 29   , 29   , 37  ,  66  , 5   , 4   ,  78  ,  84  ,  97,    ! M36   1
		42     , 65   , 34   , 34   , 43  ,  80  , 6   , 4.5 ,  90  ,  96  , 109,    ! M42   ?
		48     , 75   , 38   , 38   , 50  ,  90  , 7   , 5   , 102  , 108  , 121,    ! M48   ?  
		64     , 95   , 51   , 51   , 65  , 115  , 9   , 6   , 132  , 138  , 151     ! M64   ?  


ENDIF

return


