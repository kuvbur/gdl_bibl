!Check for User Input {
	rrr = Application_Query ("Parameter_Script", "FirstOccasion_in_Progress", isFirst)
	if isFirst then
		buttonID = glob_ui_button_ID
		modparName = glob_modpar_name
	else
		buttonID = 0
		modparName = ""
	endif
!} Check for User Input

!Menu {
	for iPage = 1 to nUIPages
		put iPage
	next iPage
	values "uiPage" get(nsp)
	if modparName = "uiPage" then
		uiPart = 1
		parameters uiPart = 1
	endif
	if displayPropertyControl then
		if bittest(displayPropertyControl, 0) then put 1
		if bittest(displayPropertyControl, 1) then put 2
		if bittest(displayPropertyControl, 2) then put 3
		values "editProperty" get(nsp)
	endif
	if nUIParts > 1 then
		for iPart = 1 to nUIParts
			put iPart
		next iPart
		values "uiPart" get(nsp)
	endif
	if nUISubParts > 1 then
		for iSubPart = 1 to nUISubParts
			put iSubPart
		next iSubPart
		values "uiSubPart" get(nsp)
	endif
!} Menu

!Plan View Symbol Parts {
	values "showParts" 0,
		1,
		5
!} Plan View Symbol Parts

!Model Parts {
	values "showParts3D" 0,
		1,
		5
!} Model Parts

!Adjust the Shaft Length {
	if modparName = "shaftLength2D" then
		if abs(cos(incline)) > .01 then
			shaftLength = shaftLength2D/cos(incline)
			parameters shaftLength = shaftLength
		endif
	endif
	if modparName = "shaftLength" or modparName = "Incline" then
		if abs(cos(incline)) > .01 then
			shaftLength2D = shaftLength*cos(incline)
			parameters shaftLength2D = shaftLength2D
		endif
	endif
!} Adjust the Shaft Length

!Bounding Box {
	if modparName = "shaftLength" or modparName = "shaftLength2D" or modparName = "Incline" then
		AC_toplevel = max(0, -shaftLength*sin(incline))
		AC_bottomlevel = min(0, -shaftLength*sin(incline))
		zzyzx = AC_toplevel - AC_bottomlevel
		parameters AC_toplevel = AC_toplevel,
			AC_bottomlevel = AC_bottomlevel,
			zzyzx = zzyzx
	endif
!} Bounding Box


