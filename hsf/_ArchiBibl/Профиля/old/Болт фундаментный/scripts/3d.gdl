gs_detlevel_3D="" : success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
if gs_detlevel_3D = `Простой` then
	show_thread=0
	bChamfer = 0
	res = 5
else
	res = 36
	bChamfer = 1
	show_thread=1
endif
call "Показ_болтов" PARAMETERS ALL ang1=rang, ang2=yRot_r, el_views=el_views RETURNED_PARAMETERS show_type

hotspot 0,0,0,900
if GLOB_PREVIEW_MODE=1 or GLOB_PREVIEW_MODE=2 or GLOB_FEEDBACK_MODE=1 then
	res = 9
	bChamfer=0
	show_thread=0
else
	if GLOB_SEO_TOOL_MODE = 1 then
		res = 19
	else
		if res<3 then res=13
	endif
endif
show_res=res
SHADOW OFF, OFF
resol show_res
material gs_ifc_mat
pen gs_cont_pen
model solid
rotx yRot
strart_hole=hp
if show_hole then
	ADDZ strart_hole-0.001
	if type_hole=2 or type_hole=3 then
		if abs(coord_hole[1])<abs(coord_hole[2]) then
			max_r=coord_hole[2]
		else
			max_r=coord_hole[1]
		endif
		HOTSPOT max_r,	0,insideLength, unID,	ang_hole,	4+128: unID=unID+1
		HOTSPOT 0,	0, insideLength,	unID,	ang_hole,	6: unID=unID+1
		HOTSPOT 0,	0, 1,	unID,	ang_hole,	7: unID=unID+1
		HOTSPOT  max_r*cos(ang_hole)*0.6,	 max_r*sin(ang_hole)*0.6,insideLength,	unID,	ang_hole, 5: unID=unID+1
		rotz ang_hole
		if type_hole=2 then
			HOTSPOT	0,					0,	insideLength,	unID,	coord_hole[1],	1+128: unID=unID+1
			HOTSPOT	coord_hole[1],	0,	insideLength,	unID,	coord_hole[1],	2: unID=unID+1
			HOTSPOT	-1,					0,	insideLength,	unID,	coord_hole[1],	3: unID=unID+1
		endif
		HOTSPOT	0,					0,	insideLength,	unID,	coord_hole[2],	1+128: unID=unID+1
		HOTSPOT	coord_hole[2],	0,	insideLength,	unID,	coord_hole[2],	2: unID=unID+1
		HOTSPOT	-1,					0,	insideLength,	unID,	coord_hole[2],	3: unID=unID+1
		del 1
	endif
	if show_hole_axis and show_type = NOT_SHOW then
		if type_hole=1 then
			lin_ 0,-holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0
			lin_ -holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0,0
			lin_ 0,-holeSizeR-zas_axis,insideLength+0.001,0,holeSizeR+zas_axis,insideLength+0.001
			lin_ -holeSizeR-zas_axis,0,insideLength+0.001,holeSizeR+zas_axis,0,insideLength+0.001
		endif
		if type_hole=2 then
			rotz ang_hole
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-holeSizeR-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-holeSizeR-zas_axis,0,insideLength+0.001
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[2],holeSizeR+zas_axis,0,coord_hole[2],-holeSizeR-zas_axis,0
			lin_ coord_hole[2],holeSizeR+zas_axis,insideLength+0.001,coord_hole[2],-holeSizeR-zas_axis,insideLength+0.001
			del 1
		endif
		if type_hole=3 then
			rotz ang_hole
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-zas_axis,0,insideLength+0.001
			del 1
		endif
	endif
	del 1
endif
if GLOB_SEO_TOOL_MODE = 1 and show_type<>SHOW_TOP and show_type<>SHOW_SIDE and show_hole and show_res>9 then
	ADDZ strart_hole-0.001
	if type_hole=1 then
		cylind insideLength+0.001, holeSizeR
	endif
	if type_hole=2 or type_hole=3 then
		rotz ang_hole
		PUT 0,holeSizeR,15
		PUT coord_hole[1],holeSizeR,15
		PUT coord_hole[1],0,915
		PUT 0,-180,4015
		PUT coord_hole[1],-holeSizeR,15
		PUT coord_hole[2],-holeSizeR,15
		if type_hole=2 then
			PUT coord_hole[2],0,915
			PUT 0,-180,4015
		else
			PUT coord_hole[2],holeSizeR,15
		endif
		PUT 0,holeSizeR,-1
		prism_ NSP/3,insideLength+0.001,get(NSP)
		del 1
	endif
	del 1
	end
endif

roty -90
call "Условный_болт" PARAMETERS ALL show_type=show_type, insideLength=insideLength, amg2d=amg2d
del 1

if show_type = NOT_SHOW and show_res>9 then
	HOTARC holeSizeR, 0, 360, 9010
	addx insideLength
	hotspot 0,0,0,9011
	HOTARC holeSizeR, 0, 360, 9012
	del 1
	end
endif

put -flatWidth/2, -flatWidth/2*tan(30)  , 15,
	-flatWidth/2,  flatWidth/2*tan(30)  , 15,
	 0     ,  flatWidth/(2*cos(30)), 15,
	 flatWidth/2,  flatWidth/2*tan(30)  , 15,
	 flatWidth/2, -flatWidth/2*tan(30)  , 15,
	 0     , -flatWidth/(2*cos(30)), 15,
	-flatWidth/2, -flatWidth/2*tan(30)  , -1,
	 0     , 0          , 900,    ! smooth hole
	 diaB/2, 0          , 79,
	 diaB/2, 360-stepAng, 4000+79,
	 diaB/2, 0          , -1
group "Nut"
	prism_ nsp/3, nutThick, get(nsp)	
endgroup
group "nChamfer"
	mulz -1
	gosub 350
	del 1
	body -1
	addz nutThick
	gosub 350
	del 1
endgroup

HOTSPOT	0, 0, 0, unID: unID=unID+1
HOTSPOT	0, 0, lsv-l0, unID: unID=unID+1
HOTSPOT	0, 0, 0, unID, lsv, 1+128: unID=unID+1
HOTSPOT	0, 0, lsv, unID, lsv, 2: unID=unID+1
HOTSPOT	0 ,0, -1, unID, lsv, 3: unID=unID+1

HOTSPOT	0, 0, 0, unID, hp, 1+128: unID=unID+1
HOTSPOT	0, 0, hp, unID, hp, 2: unID=unID+1
HOTSPOT	0 ,0, -1, unID, hp, 3: unID=unID+1

HOTSPOT 0, 0,hp+insideLength+t_washer_bg, unID, halfa_washer_bg, 1+128,a_washer_bg: unID=unID+1
HOTSPOT halfa_washer_bg,	0,hp+insideLength+t_washer_bg,	unID, halfa_washer_bg, 2,a_washer_bg: unID=unID+1
HOTSPOT -1, 0,hp+insideLength+t_washer_bg, unID, halfa_washer_bg, 3,a_washer_bg: unID=unID+1
HOTSPOT 0, 0,hp+insideLength+t_washer_bg, unID, halfb_washer_bg, 1+128,b_washer_bg: unID=unID+1
HOTSPOT 0,	halfb_washer_bg,hp+insideLength+t_washer_bg,	unID, halfb_washer_bg, 2,b_washer_bg: unID=unID+1
HOTSPOT 0, -1,hp+insideLength+t_washer_bg, unID, halfb_washer_bg, 3,b_washer_bg: unID=unID+1

if dnut then
	addz hp-t_washer_bg
	if t_washer_bg>0 then gosub "pl_washer_bg"
	addz -washerThick
	gosub "pl_washer"
	addz -nutThick
	gosub "pl_nut"
	del 3
endif

HOTSPOT	0, 0, hp, unID, insideLength, 1+128: unID=unID+1
HOTSPOT	0, 0, hp+insideLength, unID, insideLength, 2: unID=unID+1
HOTSPOT	0 ,0, -1, unID, insideLength, 3: unID=unID+1

addz hp+insideLength
if t_washer_bg>0 then
	gosub "pl_washer_bg"
	addz t_washer_bg
endif
gosub "pl_washer"
addz washerThick
gosub "pl_nut"
addz nutThick
rotz 90
gosub "pl_nut"
del top

rotx yRot
!!!--------Часть болта, находящаяся над заделкой
MATERIAL m2
lt = lsv-l0
if lt>0 then CYLIND lt, rb !Часть болта без резбьы
addz lt
MATERIAL m1
CYLIND l0, rb-0.0005 !Часть болта с резьбой
del top
!!!--------

if GLOB_VIEW_TYPE=2 or not(show_thread) then end

rotx yRot
if tip = "с отгибом" then
	MATERIAL m3
	lt = hz-rz-diaB
	addz -lt+(lsv-l0)*((lsv-l0)<0)
	CYLIND lt, rb
	addz -rb-rz
	rotz 90
	addx rb+rz
	roty -90
	ELBOW rz+rb, 90, rb
	del 3
	rotx 90
	addz -rb-rz-l
	CYLIND l, rb
endif

if tip = "прямой" then
	MATERIAL m3
	lt = hz
	addz -lt
	CYLIND lt, rb
endif

if tip = "с анк. плитой" then
	MATERIAL m3
	lt = hz-l0
		addz -lt
		CYLIND lt, rb
	MATERIAL m1
	lt = l0
	addz -lt
	CYLIND lt, rb
	hotspot 0,0,0
	gosub "pl_nut"
endif
del top
end
350:
	nutRad = abs(nutThick)
	put 0            , nutRad     ,  2,
		nutRad-flatWidth/2, 0.96*flatWidth/2,  2,
		nutRad-flatWidth/2, nutRad     ,  2,
		0            , nutRad     , -1
	roty -90
	addx -(nutRad-flatWidth/2)
	revolve nsp/3, 360, 1+2+4+8+16+32, get(nsp)	
	del 2
return

"pl_nut":
	if not(bChamfer) then placegroup "Nut" 
	if bChamfer then placegroup subgroup("Nut", "nChamfer")
return

"pl_washer":
	prism_  3, washerThick,
		0    , 0  ,  900+79,
		washerSize/2, 360, 4000+79,
		(diaB+0.004)/2, 360, 4000+79
return

"pl_washer_bg":
	prism_  4, t_washer_bg, a_washer_bg/2,b_washer_bg/2,15, -a_washer_bg/2,b_washer_bg/2,15, -a_washer_bg/2,-b_washer_bg/2,15, a_washer_bg/2,-b_washer_bg/2,15
return