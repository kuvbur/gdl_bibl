call "Шаблон профиля" parameters all
hotspot 0,0,0
if not(show) then end
success = LIBRARYGLOBAL ("Libraryflobals-km", "mat_by_type_code", mat_by_type_code)
is_mat_set = 0
if (show_spec or type_spec = "Не специфицировать") and not(is_mat_set) and mat_by_type_code<>1 then
	ignore_mat = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "ignore_mat", ignore_mat)
	col_mat = ignore_mat
	is_mat_set = 1
endif
if mat_by_type_code<>1 and not(is_mat_set) then
	if mat_by_type_code=2 then 
		name_mat_arr = "cov_mat_arr"
		inx_mat = cov_mat
	endif
	if mat_by_type_code=3 then
		name_mat_arr = "fire_keep_mat_arr"
		inx_mat = fire_keep_mat
	endif
	if mat_by_type_code=4 then
		name_mat_arr = "konstr_mat_arr"
		inx_mat = type_mat
	endif
	if mat_by_type_code=5 then
		name_mat_arr = "fire_mat_arr"
		inx_mat = fire_mat
	endif
	if mat_by_type_code=6 then
		name_mat_arr = "stal_mat_arr"
		inx_mat = stal_mat
	endif
	if mat_by_type_code=7 then
		name_mat_arr = "type_spec_mat_arr"
		inx_mat = type_spec_mat
	endif
	if mat_by_type_code=8 then
		name_mat_arr = "connect_mat_arr"
		inx_mat = upr_mat
	endif
	if inx_mat=99 or inx_mat<1 then 
		col_mat = 0
	else
		dim cov_mat_arr[]
		success = LIBRARYGLOBAL ("Libraryflobals-km", name_mat_arr, cov_mat_arr)
		col_mat = cov_mat_arr[inx_mat]
	endif
	is_mat_set = 1
endif
pen gs_cont_pen
material col_mat
if gs_shadow=0 then
	shadow off
else
	shadow on
endif


! Revision of Cutting Angles

! Cut1,Cut2
gs_cutang12_bot=(-gs_cutang1_bot)	!!<gs_cutang12_bot=(-gs_cutang2_bot)
gs_cutang21_bot=gs_cutang2_bot		!!<gs_cutang21_bot=gs_cutang1_bot
gs_cutang43_up=(-gs_cutang4_up)		!!<gs_cutang43_up=(-gs_cutang3_up)
gs_cutang34_up=(gs_cutang3_up)		!!<gs_cutang34_up=(gs_cutang4_up)

! Cut3
gs_cutang23_bot=-gs_cutang2_bot
gs_cutang33_up=-gs_cutang3_up


if show_only_axsis then
	axis_line_pen = only_axis_pen
	axis_line_type = only_axis_line
	only_axis = 1
	axis=1
endif

IF gs_stationary <>`Внизу` THEN
	mulz -1
	ang=ang*(-1)
ENDIF

rotz 90
rotx 90
IF gs_rot_axis=rot_axis_mtr[2] THEN rotx -ang
IF gs_rot_axis=rot_axis_mtr[1] THEN rotz ang

IF gs_stationary=`По центру` THEN
	Addy -ds/2
ENDIF
IF gs_rot_axis=rot_axis_mtr[2] THEN roty ang_pl
unID=1

! ==============================================================================
! HOTSPOTS
! ==============================================================================

hotspot 0,ds/2,0,unID : unID=unID+1				! BEAM LENGTH in AXIS
hotspot 0,ds/2,0,unID,a,1+256 : unID=unID+1
hotspot 0,ds/2,-1,unID,a,3 : unID=unID+1
hotspot 0,ds/2,a,unID,a,2 : unID=unID+1

IF gs_rot_axis=rot_axis_mtr[1] THEN

	IF gs_stationary <>`Внизу` THEN temp_ang=temp_ang*(-1)
		rotz -(temp_ang)

		HOTSPOT 0,0,a/2,unID,temp_ang,6,ang : unID=unID+1
		HOTSPOT 0,ds,a/2,unID,temp_ang,4+128,ang : unID=unID+1
		HOTSPOT 0,0,a/2+1,unID,temp_ang,7,ang : unID=unID+1
		HOTSPOT -(ds)*sin(temp_ang),(ds)*cos(temp_ang),a/2,unID,temp_ang,5,ang : unID=unID+1

	DEL 1

	! lower side
	IF gs_cutmode_bot=cut_mod_mtr[1] THEN
		HOTSPOT -ws/2,ds/1,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
		HOTSPOT -ws/2,0,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
		IF gs_stationary=`Внизу` THEN
			HOTSPOT ws/2,0,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		ELSE
			HOTSPOT ws/2,ds/1,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		ENDIF
	ENDIF

	IF gs_cutmode_bot=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			HOTSPOT -ws/2,ds/1,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			HOTSPOT -ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			HOTSPOT ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ELSE
			HOTSPOT -ws/2,0,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			HOTSPOT -ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			HOTSPOT ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ENDIF
	ENDIF

	! upper side
	IF gs_cutmode_up=cut_mod_mtr[1] THEN
		HOTSPOT -ws/2,ds/1,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
		HOTSPOT -ws/2,0,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
		IF gs_stationary=`Внизу` THEN
			HOTSPOT ws/2,0,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		ELSE
			HOTSPOT ws/2,ds/1,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		ENDIF
	ENDIF

	IF gs_cutmode_up=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			HOTSPOT -ws/2,ds/1,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			HOTSPOT -ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			HOTSPOT ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ELSE
			HOTSPOT -ws/2,0,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			HOTSPOT -ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			HOTSPOT ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ENDIF
	ENDIF
ENDIF

unID=100
IF gs_rot_axis=rot_axis_mtr[2] THEN
	! rotation of axis

	IF gs_stationary <>`Внизу` THEN temp_ang=temp_ang*(-1)
	IF gs_stationary=`По центру` THEN Addy ds/2

	ROTx temp_ang
		HOTSPOT 0,0,0,unID,temp_ang,6,ang : unID=unID+1
		HOTSPOT 0,0,1,unID,temp_ang,4+128,ang : unID=unID+1
		HOTSPOT -1,0,0,unID,temp_ang,7,ang : unID=unID+1
		HOTSPOT 0,a/2*sin(temp_ang),a/2*cos(temp_ang),unID,temp_ang,5,ang : unID=unID+1
	DEL 1

	IF gs_stationary=`По центру` THEN DEL 1

	! lower side
	IF gs_cutmode_bot=cut_mod_mtr[1] THEN
		HOTSPOT -ws/2,ds/1,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
		HOTSPOT -ws/2,0,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
		IF gs_stationary=`Внизу` THEN
			HOTSPOT ws/2,0,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		ELSE
			HOTSPOT ws/2,ds/1,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		ENDIF
	ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[2] THEN
		HOTSPOT -ws/2,ds/1,ds/2*tan(ang)+(ws/2*tan(gs_cutang21_bot)/cos(ang)),unID : unID=unID+1
		HOTSPOT -ws/2,0,-ds/2*tan(ang)+(ws/2*tan(gs_cutang21_bot)/cos(ang)),unID : unID=unID+1
		IF gs_stationary=`Внизу` THEN
			HOTSPOT ws/2,0,-ds/2*tan(ang)-(ws/2*tan(gs_cutang12_bot)/cos(ang)),unID : unID=unID+1
		ELSE
			HOTSPOT ws/2,ds/1,ds/2*tan(ang)-(ws/2*tan(gs_cutang12_bot)/cos(ang)),unID : unID=unID+1
		ENDIF
	ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			HOTSPOT -ws/2,ds/1,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			HOTSPOT -ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			HOTSPOT ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ELSE
			HOTSPOT -ws/2,0,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			HOTSPOT -ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			HOTSPOT ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ENDIF
	ENDIF

	! upper side
	IF gs_cutmode_up=cut_mod_mtr[1] THEN
		HOTSPOT -ws/2,ds/1,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
		HOTSPOT -ws/2,0,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
		IF gs_stationary=`Внизу` THEN
			HOTSPOT ws/2,0,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		ELSE
			HOTSPOT ws/2,ds/1,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		ENDIF
	ENDIF
	IF gs_cutmode_up=cut_mod_mtr[2] THEN
		HOTSPOT -ws/2,ds/1,a+ds/2*tan(ang)+(ws/2*tan(gs_cutang43_up)/cos(ang)),unID : unID=unID+1
		HOTSPOT -ws/2,0,a-ds/2*tan(ang)+(ws/2*tan(gs_cutang43_up)/cos(ang)),unID : unID=unID+1
		IF gs_stationary=`Внизу` THEN
			HOTSPOT ws/2,0,a-ds/2*tan(ang)-(ws/2*tan(gs_cutang34_up)/cos(ang)),unID : unID=unID+1
		ELSE
			HOTSPOT ws/2,ds/1,a+ds/2*tan(ang)-(ws/2*tan(gs_cutang34_up)/cos(ang)),unID : unID=unID+1
		ENDIF
	ENDIF
	IF gs_cutmode_up=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			HOTSPOT ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			HOTSPOT -ws/2,ds/1,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			HOTSPOT -ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ELSE
			HOTSPOT ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			HOTSPOT -ws/2,0,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			HOTSPOT -ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ENDIF
	ENDIF
ENDIF


IF gs_detlevel_3D=`Откл.` THEN END

! ==============================================================================
! CUTPOLY
! ==============================================================================
nCutPoly = 0

! Cutplanes on the bottom

dz = 0
IF gs_cutmode_bot=cut_mod_mtr[1] THEN								! Perpendicular to Axis
	ROTx -90
	IF ABS(gs_cutang2_bot) > EPS or ABS(gs_cutang1_bot) > EPS THEN
		dz=max(2*ws/2*tan(gs_cutang21_bot),2*ws/2*tan(-gs_cutang1_bot),1)
		CUTPOLYA 4,0,0,
				0,0,15,
				ws/2,ws/2*tan(gs_cutang12_bot),15,
				ws/2,dz,15,
				0,dz,15
		CUTPOLYA 4,0,0,
				0,0,15,
				-ws/2,ws/2*tan(-gs_cutang21_bot),15,
				-ws/2,dz,15,
				0,dz,15
		nCutPoly = nCutPoly + 2
	ENDIF
	DEL 1
ENDIF

IF gs_cutmode_bot=cut_mod_mtr[2] THEN !!!!!***************************** Along a Vertical Axis
	  IF gs_rot_axis=rot_axis_mtr[1] THEN
		ROTx -90
		ROTy -ang
			IF ABS(gs_cutang2_bot)>0.001 or ABS(gs_cutang1_bot)>0.001 THEN
			dz=max(2*((ws/2)*sin(ang)+(ds/2)*cos(ang))*tan(gs_cutang12_bot),2*((ws/2)*sin(ang)+(ds/2)*cos(ang))*tan(gs_cutang21_bot),1)
			ADDy 0
			CUTPOLYA 4,0,0,
				0,0,15,
				(ws/1)*sin(ang)+(ds/2)*cos(ang),((ws/1)*sin(ang)+(ds/2)*cos(ang))*tan(gs_cutang12_bot),15,
				ws/1+0.001,dz,15,
				0,dz,15

			CUTPOLYA 4,0,0,
				0,0,15,
				-ws/2-0.005,-ws/2*tan(gs_cutang21_bot),15,
				-ws/2-0.005,dz,15,
				0,dz,15
			DEL 1
			ENDIF
		DEL 2
	  ENDIF

	  IF gs_rot_axis=rot_axis_mtr[2] THEN
		ROTx ang-90
		ADDy ds/2*sin(ang)
			dz=max(2*ws/2*tan(gs_cutang21_bot)+ds*sin(ang),2*ws/2*tan(gs_cutang12_bot)+ds*sin(ang),1)
			dzz=dz*cos(ang)+ABS(ds*sin(ang))
			CUTPOLYA 4,0,0,
				0,0,15,
				ws/2,ws/2*tan(gs_cutang12_bot),15,
				ws/2,dzz,15,
				0,dzz,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-ws/2,-ws/2*tan(gs_cutang21_bot),15,
				-ws/2,dzz,15,
				0,dzz,15
		DEL 2
	  ENDIF

ENDIF

IF gs_cutmode_bot=cut_mod_mtr[3] THEN !!!!!***************************** Along a Horizontal Axis
	IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
		dz=abs(max((-ds/1)*tan(gs_cutang1_bot),(ds/1)*tan(gs_cutang23_bot),1))
		ADDy ds/2
		ROTy 90
		ROTz 90
		IF gs_stationary=`Внизу` THEN
			CUTPOLYA 6,0,0,
					(ds/1),-dz,15,
					(ds/1),(ds/1)*tan(gs_cutang1_bot),15,
					0,0,15,
					-(ds/1),-(ds/1)*tan(gs_cutang23_bot),15,
					-(ds/1),-dz,15,
					0,-dz,15
		ELSE
			CUTPOLYA 6,0,0,
					(ds/1),-dz,15,
					(ds/1),(ds/1)*tan(-gs_cutang23_bot),15,
					0,0,15,
					-(ds/1),-(ds/1)*tan(-gs_cutang1_bot),15,
					-(ds/1),-dz,15,
					0,-dz,15
		ENDIF
		DEL 3
	ENDIF
ENDIF

!!!!!!*********************************** Cutplanes on the top

dz2 = 0
ADDz a
IF gs_cutmode_up=cut_mod_mtr[1] THEN !!!!!***************************** Perpendicular to Axis
	ROTx -90
	IF ABS(gs_cutang4_up)>0.001 or ABS(gs_cutang3_up)>0.001 THEN
		dz2=max(1,ABS(2*((ws/2)*tan(gs_cutang43_up))),ABS(2*((ws/2)*tan(-gs_cutang34_up))))
		CUTPOLYA 4,0,0,
				0,0,15,
				ws/2,-ws/2*tan(-gs_cutang34_up),15,
				ws/2,-dz2,15,
				0,-dz2,15
		CUTPOLYA 4,0,0,
				0,0,15,
				-ws/2,-ws/2*tan(gs_cutang43_up),15,
				-ws/2,-dz2,15,
				0,-dz2,15
	ENDIF
	DEL 1
ENDIF
IF gs_cutmode_up=cut_mod_mtr[2] THEN !!!!!***************************** Along a Vertical Axis
	IF gs_rot_axis=rot_axis_mtr[2] THEN ADDz -ds/2*tan(ang)
	ROTx ang-90
	dz2=max(ABS(2*ws/2*tan(gs_cutang43_up)+ds*sin(ang)),ABS(2*ws/2*tan(gs_cutang34_up)+ds*sin(ang)),1)
	dzz2=dz2*cos(ang)+ABS(ds*sin(ang))
	CUTPOLYA 4,0,0,
			0,0,15,
			ws/2,-ws/2*tan(-gs_cutang34_up),15,
			ws/2,-dzz2,15,
			0,-dzz2,15
	CUTPOLYA 4,0,0,
			0,0,15,
			-ws/2,-ws/2*tan(gs_cutang43_up),15,
			-ws/2,-dzz2,15,
			0,-dzz2,15
	DEL 1
	IF gs_rot_axis=rot_axis_mtr[2] THEN DEL 1
ENDIF

IF gs_cutmode_up=cut_mod_mtr[3] THEN !!!!!***************************** Along a Horizontal Axis
	IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
		dz2=max(ABS(ds/1*tan(gs_cutang33_up)),ABS(ds/1*tan(gs_cutang4_up)),1)
		ADDy ds/2
		ROTy 90
		ROTz 90
		IF gs_stationary=`Внизу` THEN
			CUTPOLYA 6,0,0,
				(ds/1)/cos(gs_cutang33_up),dz2,15,
				(ds/1)/cos(gs_cutang4_up),((ds/1)/cos(gs_cutang4_up))*tan(gs_cutang33_up),15,
				0,0,15,
				-(ds/1)/cos(gs_cutang33_up),((ds/1)/cos(gs_cutang33_up))*tan(-gs_cutang4_up),15,
				-(ds/1)/cos(gs_cutang33_up),dz2,15,
				0,dz2,15
		ELSE
			CUTPOLYA 6,0,0,
				(ds/1)/cos(-gs_cutang4_up),dz2,15,
				(ds/1)/cos(-gs_cutang33_up),((ds/1)/cos(-gs_cutang33_up))*tan(-gs_cutang4_up),15,
				0,0,15,
				-(ds/1)/cos(-gs_cutang4_up),((ds/1)/cos(-gs_cutang4_up))*tan(gs_cutang33_up),15,
				-(ds/1)/cos(-gs_cutang4_up),dz2,15,
				0,dz2,15
		endif
		DEL 3
	endif
ENDIF

DEL 1

ADDz -dz

! ==============================================================================
!
! ==============================================================================

IF gs_stationary <> `Внизу` THEN
	addy ds
	muly -1
ENDIF

IF  FlangePos=`Вверху` THEN
	rotz 180
	roty 180
	ADDz -(a+dz+dz2)
	ADDy -ds
ENDIF

! SIMPLE
if show_only_axsis then goto 3030

IF gs_detlevel_3D=`Простой` OR GLOB_CONTEXT=2 THEN
	ADDx -ws/2
	PRISM_ 7, a+dz+dz2,
			0,0,15,
			ws,0,15,
			ws,wt,15,
			wt,wt,15,
			wt,ds,15,
			0,ds,15,
			0,0,-1
	DEL 1
ENDIF

! DETALIED
IF gs_detlevel_3D=`Детальный` OR GLOB_CONTEXT<>2  THEN
	ADDx -ws/2
	RESOL gs_resol
	PRISM_ 13,a+dz+dz2,
			ws,0,15,
			ws,wt-rr,79,
			ws-rr,wt-rr,979,
			ws-rr,wt,3079,
			wt+ft,wt,79,
			wt+ft,wt+ft,979,
			wt,wt+ft,3079,
			wt,ds-rr,79,
			wt-rr,ds-rr,979,
			wt-rr,ds,3079,
			0,ds,15,
			0,0,15,
			ws,0,-1
	DEL 1
ENDIF

3030:

ADDx -ws/2
if dbf=0 then dbf = ws/2
if tr=0 then tr = ds/2
cx = dbf !Координаты оси
cy = tr !Координаты оси 
call "macro_axsis" parameters cx=cx, cy=cy, axis=axis, A=a+dz+dz2, only_axis=only_axis, axis_line_pen=axis_line_pen
del 1

IF  FlangePos=`Вверху`  THEN DEL 4

IF gs_stationary <> `Внизу` THEN DEL 2

DEL 3

IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
	IF gs_cutmode_bot=cut_mod_mtr[1] THEN :CUTEND:CUTEND: ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[2] AND gs_rot_axis=rot_axis_mtr[1] THEN :CUTEND:CUTEND: ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[3] THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang1_bot>=0 or gs_cutang2_bot>=0 THEN
	IF gs_cutmode_bot=cut_mod_mtr[2] AND gs_rot_axis=rot_axis_mtr[2] THEN :CUTEND:CUTEND: ENDIF
!ENDIF

IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
	IF gs_cutmode_up=cut_mod_mtr[1] THEN :CUTEND:CUTEND: ENDIF
	IF gs_cutmode_up=cut_mod_mtr[3] THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang3_up>=0 or gs_cutang4_up>=0 THEN
	IF gs_cutmode_up=cut_mod_mtr[2] AND gs_rot_axis=rot_axis_mtr[2] THEN :CUTEND:CUTEND: ENDIF
!ENDIF

DEL 1

IF gs_stationary <>`Внизу` THEN DEl 1
IF gs_stationary=`По центру` THEN DEL 1

End
