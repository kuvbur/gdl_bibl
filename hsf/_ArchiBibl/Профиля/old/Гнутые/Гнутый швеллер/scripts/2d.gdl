if GLOB_CONTEXT=6 then
	k = 5
	pen SYMB_SECT_PEN
	MUL2 A_/1000, A_/1000 
	line2 0,0,0,k
	line2 0,0,k*0.5,0
	line2 0,k,k*0.5,k
	end
endif

if GLOB_STRUCTURE_DISPLAY = upr_n then
	if upr = 0 then end
	if ang=90 then
		rect2 -ds/2000,-ws/2000,ds/2000,ws/2000
	else
		project2 3,270,2
	endif
	end
endif
!
!ws=ws/1000
!ds=ds/1000
!wt=wt/1000
!ft=ft/1000
!rr=rr/1000
RRadius=rr

IF WebOutSide=1 THEN		!!! Arc Radius depends on flange side
	tempArcRadius=(ArcRadius-ws/2)
	ELSE
	tempArcRadius=(ArcRadius+ws/2)
ENDIF

unID=1

!!! ARC RADIUS
ROT2 ArcAngle/2
	HOTSPOT2 0,0,unID,ArcRadius,1 : unID=unID+1
	HOTSPOT2 ArcRadius,0,unID,ArcRadius,2 : unID=unID+1
	HOTSPOT2 -1,0,unID,ArcRadius,3 : unID=unID+1
DEL 1

!!! ARC ANGLE
HOTSPOT2 0,0,unID,ArcAngle,6 : unID=unID+1
HOTSPOT2 (ArcRadius),0,unID,ArcAngle,4+256 : unID=unID+1
HOTSPOT2 (ArcRadius)*cos(ArcAngle),(ArcRadius)*sin(ArcAngle),unID,ArcAngle,5 : unID=unID+1


IF (axis=1) THEN

	PEN axis_line_pen
	LINE_TYPE axis_line_type
	ARC2 0,0,tempArcRadius,0,ArcAngle
	HOTARC2 0,0,tempArcRadius,0,ArcAngle

ENDIF


IF axis=1 AND only_axis=0 OR axis=0 THEN

	PEN gs_cont_pen
	FILL gs_fill_type
	LINE_TYPE 1

	IF ABS(ArcAngle-360)<EPS THEN		!! ArcAngle = 360

		POLY2_b 4, 7, gs_fill_pen, gs_back_pen,
			0, 0, 900,
			(tempArcRadius+ws/2), 360, 4001,
			0, 0, 900,
			(tempArcRadius-ws/2), 360, 4001

		ELSE

		POLY2_b 9, 7, gs_fill_pen, gs_back_pen,
			tempArcRadius-ws/2, 0, 1,
			tempArcRadius+ws/2, 0, 1,
			0,0,900,
			0,ArcAngle,4000,
			(tempArcRadius+ws/2)*COS(ArcAngle),(tempArcRadius+ws/2)*SIN(ArcAngle),1,
			(tempArcRadius-ws/2)*COS(ArcAngle),(tempArcRadius-ws/2)*SIN(ArcAngle),1,
			0,0,900,
			0,-ArcAngle,4000,
			tempArcRadius-ws/2, 0, -1

	ENDIF

	IF gs_ShowMoreHtps2D=1 THEN
		HOTSPOT2 tempArcRadius-ws/2,0,unID : unID=unID+1
		HOTSPOT2 tempArcRadius+ws/2,0,unID : unID=unID+1
		HOTSPOT2 tempArcRadius,0,unID : unID=unID+1

		ROT2 ArcAngle
		HOTSPOT2 tempArcRadius-ws/2,0,unID : unID=unID+1
		HOTSPOT2 tempArcRadius+ws/2,0,unID : unID=unID+1
		HOTSPOT2 tempArcRadius,0,unID : unID=unID+1
		DEL 1


	ENDIF

ENDIF

