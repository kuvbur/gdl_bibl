
!-------------------Скрипт опорной плиты:----------------------------
Group "plate"
PRISM 4, plate_t,
	-plate_L/2,-plate_B/2,
	plate_L/2,-plate_B/2,
	plate_L/2,plate_B/2,
	-plate_L/2,plate_B/2
		!---Добавление плоскости для корекного отображения разрезов---
PLANE 4,
	-plate_L/2,-plate_B/2,plate_t,
	plate_L/2,-plate_B/2,plate_t,
	plate_L/2,plate_B/2,plate_t,
	-plate_L/2,plate_B/2,plate_t

	!-----Добавление HOTSPOT-----
	HOTSPOT 0,0,0
	HOTSPOT 0,0,plate_t
	HOTSPOT -plate_L/2,-plate_B/2,plate_t
	HOTSPOT plate_L/2,-plate_B/2,plate_t
	HOTSPOT plate_L/2,plate_B/2,plate_t
	HOTSPOT -plate_L/2,plate_B/2,plate_t
EndGroup


!---------------------Скрипт траверсы:-------------------------------
Group "traverse"
ROTX 90
ADDY plate_t
ADDZ column_B/2
	PRISM 6, traverse_t,
		-plate_L/2,0,
		plate_L/2,0,
		plate_L/2,TOREC_h,
		column_L/2,traverse_h,
		-column_L/2,traverse_h,
		-plate_L/2,TOREC_h

	!-----Добавление сварки-----
	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = -plate_L/2 !Начало, X
	pos_weld[1][2] = 0 !Начало, Y
	pos_weld[1][3] = 0 !Начало, Z
	pos_weld[2][1] = -column_L/2 !Конец, X
	pos_weld[2][2] = 0 !Конец, Y
	pos_weld[2][3] = 0 !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ
	
	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = column_L/2 !Начало, X
	pos_weld[1][2] = 0 !Начало, Y
	pos_weld[1][3] = 0 !Начало, Z
	pos_weld[2][1] = plate_L/2 !Конец, X
	pos_weld[2][2] = 0 !Конец, Y
	pos_weld[2][3] = 0 !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

	ang_axis_weld = 90 ! Угол поворота сварки
	pos_weld[1][1] = -plate_L/2 !Начало, X
	pos_weld[1][2] = 0 !Начало, Y
	pos_weld[1][3] = traverse_t !Начало, Z
	pos_weld[2][1] = plate_L/2 !Конец, X
	pos_weld[2][2] = 0 !Конец, Y
	pos_weld[2][3] = traverse_t !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

		ang_axis_weld = -90 ! Угол поворота сварки
	pos_weld[1][1] = column_L/2 !Начало, X
	pos_weld[1][2] = 0 !Начало, Y
	pos_weld[1][3] = 0 !Начало, Z
	pos_weld[2][1] = column_L/2 !Конец, X
	pos_weld[2][2] = traverse_h !Конец, Y
	pos_weld[2][3] = 0 !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

		ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = -column_L/2 !Начало, X
	pos_weld[1][2] = 0 !Начало, Y
	pos_weld[1][3] = 0 !Начало, Z
	pos_weld[2][1] = -column_L/2 !Конец, X
	pos_weld[2][2] = traverse_h !Конец, Y
	pos_weld[2][3] = 0 !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

	!-----Добавление HOTSPOT-----
	HOTSPOT -plate_L/2,0,0
	HOTSPOT plate_L/2,0,0
	HOTSPOT plate_L/2,TOREC_h,0
	HOTSPOT column_L/2,traverse_h,0
	HOTSPOT -column_L/2,traverse_h,0
	HOTSPOT -plate_L/2,TOREC_h,0


EndGroup


!----------------Скрипт дополнительных ребер:---------------------
Group "edge"
ROTY -90
ADDX plate_t
ADDZ -column_L/2
	PRISM 6, edge_t,
		SREZ_h,column_B/2+traverse_t,
		traverse_h,column_B/2+traverse_t,
		traverse_h,column_B/2+traverse_t+TOREC_h,
		TOREC_h,plate_B/2,
		0,plate_B/2,
		0,column_B/2+traverse_t+SREZ_h

	!Добавление сварки
	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = SREZ_h !Начало, X
	pos_weld[1][2] = column_B/2+traverse_t !Начало, Y
	pos_weld[1][3] = 0 !Начало, Z
	pos_weld[2][1] = traverse_h !Конец, X
	pos_weld[2][2] = column_B/2+traverse_t !Конец, Y
	pos_weld[2][3] = 0 !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

	ang_axis_weld = 90 ! Угол поворота сварки
	pos_weld[1][1] = SREZ_h !Начало, X
	pos_weld[1][2] = column_B/2+traverse_t !Начало, Y
	pos_weld[1][3] = edge_t !Начало, Z
	pos_weld[2][1] = traverse_h !Конец, X
	pos_weld[2][2] = column_B/2+traverse_t !Конец, Y
	pos_weld[2][3] = edge_t !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = 0 !Начало, X
	pos_weld[1][2] = plate_B/2 !Начало, Y
	pos_weld[1][3] = 0 !Начало, Z
	pos_weld[2][1] = 0 !Конец, X
	pos_weld[2][2] = column_B/2+traverse_t+SREZ_h !Конец, Y
	pos_weld[2][3] = 0 !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

	ang_axis_weld = 90 ! Угол поворота сварки
	pos_weld[1][1] = 0 !Начало, X
	pos_weld[1][2] = plate_B/2 !Начало, Y
	pos_weld[1][3] = edge_t !Начало, Z
	pos_weld[2][1] = 0 !Конец, X
	pos_weld[2][2] = column_B/2+traverse_t+SREZ_h !Конец, Y
	pos_weld[2][3] = edge_t !Конец, Z
	gosub "set_weld" ! Вызов функции СВАРКИ

	!-----Добавление HOTSPOT-----

EndGroup


!------------------ Построение базы колонны:--------------------------------------
addz -plate_t
	IF type_base = TYPE_PLATE THEN PLACEGROUP "plate"

	IF type_base = TYPE_TRAVERSE THEN 
		PLACEGROUP "plate"
		! Построение траверс с двух сторон:
		PLACEGROUP "traverse"
		MULY -1
		PLACEGROUP "traverse"
		del 1
	ENDIF

	IF type_base = TYPE_REBR THEN 
		PLACEGROUP "plate" 
		! Построение траверс с двух сторон:
		PLACEGROUP "traverse"
		MULY -1
		PLACEGROUP "traverse"
			! Построение ребер с четырех сторон:
		PLACEGROUP "edge"
		MULX -1
		PLACEGROUP "edge"
		DEL 1
		MULY -1
		PLACEGROUP "edge"
		MULX -1
		PLACEGROUP "edge"
		del 3
	ENDIF
del 1
end 
!----------------- Окончание выполнения построений----------------------------------

!Вызов параметров сварик
"set_weld": ! Функция сварки
	type_weld = 0
	type_weld_ang = 0
	call "Сварка 3д" parameters type_weld=type_weld, type_weld_ang = type_weld_ang, show_weld_line=0, pos_weld = pos_weld, ang_axis = ang_axis_weld, subpos=subpos, tip_konstr=tip_konstr
return
