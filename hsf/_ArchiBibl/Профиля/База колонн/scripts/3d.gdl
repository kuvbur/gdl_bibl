jt = 15
mask = 1+2+4+16+32
hotspot 0,0,0

!==================== Детальный или простой способ отображения ====================
jt = 15
success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
IF gs_detlevel_3D=`Простой` then
	det_lvl_2d = 1
	det_lvl_3d = 1
endif
IF gs_detlevel_3D=`Детальный` THEN
	det_lvl_2d = 2
	det_lvl_3d = 2
endif
IF gs_detlevel_3D=`Откл` THEN
	det_lvl_2d = 2
	det_lvl_3d = 0
endif

!=============== Группа подливки под колонну: ===============
Group "gravy"
	building_material gravy_mat !Назначение материала
	top_material = 0
	bSucceed = request{2} ("Building_Material_info", gravy_mat, "gs_bmat_surface", top_material)
	IF 	gravy_t<0.05 THEN gravy_t =0.05
	ADDZ -pl_t
	GOSUB "prof_gravy"
	n = NSP/3
	IF NSP>3 THEN EXTRUDE n, 0, 0, -gravy_t, mask, use(NSP)
	z=0
	gosub "set_hotspot"
		if NSP>3 then PLANE NSP/3, use(NSP)
		for i=1 to NSP step 3
			x=get(1) :	y=get(1) :  z=get(1)
			hotspot x, y, z
			hotspot x, y, z-gravy_t
		next i
	DEL 1
EndGroup
!=============== Группа прямоугольной опорной плиты: ===============
Group "plate_rect"
	building_material steel_mat !Назначение материала
		GOSUB "prof_plate_rect"
		n = NSP/3
		if NSP>3 then EXTRUDE n, 0, 0, -pl_t, mask, use(NSP)
		z=0
		gosub "set_hotspot"
		if NSP>3 then PLANE NSP/3, use(NSP)
		for i=1 to NSP step 3
			x=get(1) :	y=get(1) :  z=get(1)
			hotspot x, y, z
			hotspot x, y, z-pl_t
		next i
EndGroup
!=============== Группа круглой опорной плиты: ===============
Group "plate_circle"
	building_material steel_mat !Назначение материала
	ADDZ -pl_t
	CYLIND pl_t, ws/2+RA
	DEL 1
EndGroup
!=============== Группа траверса: ===============
Group "traverse"
	building_material steel_mat !Назначение материала
	if 	t_tr_h<EPS OR t_tr_h>tr_h then t_tr_h =0
	GOSUB "prof_traverse"
	n = NSP/3
	if NSP>3 then EXTRUDE n, 0, 0, tr_t, mask, use(NSP)
	z=0
	gosub "set_hotspot"
	if NSP>3 then PLANE NSP/3, use(NSP)
	for i=1 to NSP step 3
		x=get(1) :	y=get(1) :  z=get(1)
		hotspot x, y, z
		hotspot x, y, tr_t
	next i
		!Добавление сварки
	ang_axis_weld = 270 ! Угол поворота сварки
	pos_weld[1][1] = -ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = -ds*0.5-LA 	!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
		!Добавление сварки
	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = ds*0.5+RA 		!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
			!Добавление сварки
	ADDZ tr_t
	ang_axis_weld = 90 ! Угол поворота сварки
	pos_weld[1][1] = -ds*0.5-LA		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = ds*0.5+RA 		!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
	DEL 1
		!Добавление сварки
	pos_weld[1][1] = -ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0	 			!Начало, Z
	pos_weld[2][1] = -ds*0.5 	!Конец, X
	pos_weld[2][2] = tr_h			!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	IF LA < EPS THEN
		ang_axis_weld = 0 ! Угол поворота сварки
		gosub "set_weld_hands"  ! Вызов функции СВАРКИ
	ELSE
		ang_axis_weld = 0 ! Угол поворота сварки
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
	ENDIF
		!Добавление сварки
	pos_weld[1][1] = ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0	 			!Начало, Z
	pos_weld[2][1] = ds*0.5 	!Конец, X
	pos_weld[2][2] = tr_h			!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	IF RA < EPS THEN
		ang_axis_weld = 0 ! Угол поворота сварки
		gosub "set_weld_hands"  ! Вызов функции СВАРКИ
	ELSE
		ang_axis_weld = 270 ! Угол поворота сварки
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
	ENDIF
EndGroup

!=============== Группа ребра: ===============
Group "edge"
	building_material steel_mat !Назначение материала
	GOSUB "prof_edge"
	n = NSP/3
	if NSP>3 then EXTRUDE n, 0, 0, ed_t, mask, use(NSP)
	z=0
	gosub "set_hotspot"
	if NSP>3 then PLANE NSP/3, use(NSP)
	for i=1 to NSP step 3
		x=get(1) :	y=get(1) :  z=get(1)
		hotspot x, y, z
		hotspot x, y, ed_t
	next i
		!Добавление сварки
	ang_axis_weld = 270 ! Угол поворота сварки
	pos_weld[1][1] = -ws/2-itt-0.02 !Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = -ws/2-LB		!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
		!Добавление сварки
	ADDZ ed_t
	ang_axis_weld = 180 ! Угол поворота сварки
	pos_weld[1][1] = -ws/2-itt-0.02 !Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = -ws/2-LB		!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
	DEL 1
			!Добавление сварки
	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = -ws/2-itt			!Начало, X
	pos_weld[1][2] = 0.02	 			!Начало, Y
	pos_weld[1][3] = 0 					!Начало, Z
	pos_weld[2][1] = -ws/2-itt			!Конец, X
	pos_weld[2][2] = ed_h				!Конец, Y
	pos_weld[2][3] = 0					!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
		!Добавление сварки
	ADDZ ed_t
	ang_axis_weld = 90 ! Угол поворота сварки
	pos_weld[1][1] = -ws/2-itt			!Начало, X
	pos_weld[1][2] = 0.02	 			!Начало, Y
	pos_weld[1][3] = 0 					!Начало, Z
	pos_weld[2][1] = -ws/2-itt			!Конец, X
	pos_weld[2][2] = ed_h				!Конец, Y
	pos_weld[2][3] = 0					!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
	DEL 1
EndGroup

!=================================================================
!================= НАЧАЛО ПОСТРОЕНИЯ БАЗЫ КОЛОННЫ ================

IF type_base = TYPE_PLATE_RECT THEN PLACEGROUP "plate_rect" 	!Прямоугольная плита

IF type_base = TYPE_PLATE_CIRCLE THEN							!Круглая плита
	type_traverse = TYPE_TRAVERSE_NO
	PLACEGROUP "plate_circle"
ENDIF

IF type_gravy = TYPE_GRAVY_YES THEN PLACEGROUP "gravy" 			!Построение подливки под базу
IF type_traverse = TYPE_TRAVERSE_YES THEN						!Построение траверс
	ROTX 90
	ADDZ ws*0.5
	PLACEGROUP "traverse"
	DEL 1
	MULZ -1
	ADDZ ws*0.5
	PLACEGROUP "traverse"
	DEL 3
ENDIF

IF type_edge = TYPE_EDGE_YES AND type_base = TYPE_PLATE_RECT THEN	!Построение ребер для прямоугольной плиты
	IF edge_n = EDGE_N_2 THEN										!Два ребра с одной стороны
			ROTX 90
			ROTY 90
			ADDZ -ds*0.5
			PLACEGROUP "edge"
			DEL 1
			MULZ -1
			ADDZ -ds*0.5
			PLACEGROUP "edge"
			DEL 4
	ENDIF
ENDIF

goto "m_end"
!=================================================================
!================ ОКОНЧАНИЕ ПОСТРОЕНИЯ БАЗЫ КОЛОННЫ ==============

!=============== Расстановка HOTSPOT: ===============
"set_hotspot":
	if NSP>3 then
		n_form = NSP
		for i=1 to n_form step 3
			x=get(1) :	y=get(1) :  jt=get(1)
			put x,y,z
		next i
	endif
return
!=============== Скрипт координаты подливки под колонны: ===============
"prof_gravy": !"Подливка под базой"
		put -ds/2-LA-gr_L, -ws/2-LB-gr_L,jt
		put -ds/2-LA-gr_L, ws/2+RB+gr_L,jt
		put ds/2+RA+gr_L, ws/2+RB+gr_L,jt
		put ds/2+RA+gr_L, -ws/2-LB-gr_L,jt
return
!=============== Скрипт координаты прямоугольной опорной плиты: ===============
"prof_plate_rect": !"Плита базы"
		put -ds/2-LA, -ws/2-LB,jt
		put -ds/2-LA, ws/2+RB,jt
		put ds/2+RA, ws/2+RB,jt
		put ds/2+RA, -ws/2-LB,jt
return
!=============== Скрипт координаты траверсы: ===============
"prof_traverse": !"Траверса"
		put -ds/2-LA, 0,jt
		put -ds/2-LA, t_tr_h,jt
		put -ds/2, tr_h,jt
		put ds/2, tr_h,jt
		put ds/2+RA, t_tr_h,jt
		put ds/2+RA, 0,jt
return

!=============== Скрипт координаты ребра: ===============
"prof_edge": !"Ребро"
	IF type_traverse = TYPE_TRAVERSE_NO THEN itt = 0 ELSE itt = tr_t
	IF type_traverse = TYPE_TRAVERSE_YES THEN ed_h = tr_h
		put -ws/2-LB, 0,jt
		put -ws/2-LB, t_ed_h,jt
		put -ws/2-itt-0.02, ed_h,jt
		put -ws/2-itt, ed_h,jt
		put -ws/2-itt, 0.02,jt
		put -ws/2-itt-0.02,0,jt
return

!======================= Вызов параметров сварик =======================
"set_weld_auto": ! Функция сварки
	type_weld = 0
	type_weld_ang = 0
	call "Сварка 3д" parameters type_weld=type_weld, type_weld_ang = type_weld_ang, show_weld_line=0, pos_weld = pos_weld, ang_axis = ang_axis_weld, subpos=subpos, tip_konstr=tip_konstr
return

"set_weld_hands": ! Функция сварки
	type_weld = 0
	type_weld_ang = 1
	call "Сварка 3д" parameters type_weld=type_weld, type_weld_ang = type_weld_ang, show_weld_line=0, pos_weld = pos_weld, ang_axis = ang_axis_weld, subpos=subpos, tip_konstr=tip_konstr
return


"m_end":
