hotspot 0,0,0,900

gs_detlevel_3D="" : success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
if gs_detlevel_3D = `Простой` then
	show_thread=0
	bChamfer = 0
	res = 5
else
	res = 36
	bChamfer = 1
	show_thread=1
endif
call "Показ_болтов" PARAMETERS ALL ang1=rang, ang2=yRot_r, el_views=el_views RETURNED_PARAMETERS show_type,show_wash_list, show_hole

show = 0
call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
show = show_subpos
if not(show) then end

yRot = yRot-90
if GLOB_PREVIEW_MODE=1 or GLOB_PREVIEW_MODE=2 or GLOB_FEEDBACK_MODE=1 then
	res = 9
	bChamfer=0
	show_thread=0
else
	if GLOB_SEO_TOOL_MODE = 1 then
		res = 19
	else
		if res<3 then res=13
	endif
endif
show_res=res
SHADOW OFF, OFF
resol show_res
material gs_ifc_mat
pen gs_cont_pen
model solid
rotx yRot
strart_hole=hp
if show_hole then
	ADDZ strart_hole-0.001
	if type_hole=2 or type_hole=3 then
		if abs(coord_hole[1])<abs(coord_hole[2]) then
			max_r=coord_hole[2]
		else
			max_r=coord_hole[1]
		endif
		HOTSPOT max_r,	0,insideLength, unID,	ang_hole,	4+128: unID=unID+1
		HOTSPOT 0,	0, insideLength,	unID,	ang_hole,	6: unID=unID+1
		HOTSPOT 0,	0, 1,	unID,	ang_hole,	7: unID=unID+1
		HOTSPOT  max_r*cos(ang_hole)*0.6,	 max_r*sin(ang_hole)*0.6,insideLength,	unID,	ang_hole, 5: unID=unID+1
		rotz ang_hole
		if type_hole=2 then
			HOTSPOT	0,					0,	insideLength,	unID,	coord_hole[1],	1+128: unID=unID+1
			HOTSPOT	coord_hole[1],	0,	insideLength,	unID,	coord_hole[1],	2: unID=unID+1
			HOTSPOT	-1,					0,	insideLength,	unID,	coord_hole[1],	3: unID=unID+1
		endif
		HOTSPOT	0,					0,	insideLength,	unID,	coord_hole[2],	1+128: unID=unID+1
		HOTSPOT	coord_hole[2],	0,	insideLength,	unID,	coord_hole[2],	2: unID=unID+1
		HOTSPOT	-1,					0,	insideLength,	unID,	coord_hole[2],	3: unID=unID+1
		del 1
	endif
	if show_hole_axis and show_type = NOT_SHOW then
		rotz ang_hole
		if type_hole=1 then
			lin_ 0,-holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0
			lin_ -holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0,0
			lin_ 0,-holeSizeR-zas_axis,insideLength+0.001,0,holeSizeR+zas_axis,insideLength+0.001
			lin_ -holeSizeR-zas_axis,0,insideLength+0.001,holeSizeR+zas_axis,0,insideLength+0.001
		endif
		if type_hole=2 then
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-holeSizeR-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-holeSizeR-zas_axis,0,insideLength+0.001
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[2],holeSizeR+zas_axis,0,coord_hole[2],-holeSizeR-zas_axis,0
			lin_ coord_hole[2],holeSizeR+zas_axis,insideLength+0.001,coord_hole[2],-holeSizeR-zas_axis,insideLength+0.001
		endif
		if type_hole=3 then
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-zas_axis,0,insideLength+0.001
		endif
		del 1
	endif
	del 1
endif
if GLOB_SEO_TOOL_MODE = 1 and show_type<>SHOW_TOP and show_type<>SHOW_SIDE and show_hole then
	ADDZ strart_hole-0.001
	if type_hole=1 then
		cylind insideLength+0.001, holeSizeR
	endif
	if type_hole=2 or type_hole=3 then
		rotz ang_hole
		PUT 0,holeSizeR,15
		PUT coord_hole[1],holeSizeR,15
		PUT coord_hole[1],0,915
		PUT 0,-180,4015
		PUT coord_hole[1],-holeSizeR,15
		PUT coord_hole[2],-holeSizeR,15
		if type_hole=2 then
			PUT coord_hole[2],0,915
			PUT 0,-180,4015
		else
			PUT coord_hole[2],holeSizeR,15
		endif
		PUT 0,holeSizeR,-1
		prism_ NSP/3,insideLength+0.001,get(NSP)
		del 1
	endif
	del 1
	end
endif

roty -90
call "Условный_болт" PARAMETERS ALL show_type=show_type, insideLength=insideLength, amg2d=amg2d
del 1

if show_type = NOT_SHOW then
	HOTARC holeSizeR, 0, 360, 9010
	addx insideLength
	hotspot 0,0,0,9011
	HOTARC holeSizeR, 0, 360, 9012
	del 1
	end
endif

if show_type<>SHOW_3D then end

group "Nut"
	put -nut_diam/2, -nut_diam/2*tan(30)  , 15,
		-nut_diam/2,  nut_diam/2*tan(30)  , 15,
		 0     ,  nut_diam/(2*cos(30)), 15,
		 nut_diam/2,  nut_diam/2*tan(30)  , 15,
		 nut_diam/2, -nut_diam/2*tan(30)  , 15,
		 0     , -nut_diam/(2*cos(30)), 15,
		-nut_diam/2, -nut_diam/2*tan(30)  , -1,
	
		 0     , 0          , 900,    ! smooth hole
		 diaB/2, 0          , 79,
		 diaB/2, 360-10, 4000+79,
		 diaB/2, 0          , -1
	prism_ nsp/3, nut_thk, get(nsp)	
endgroup
group "nChamfer"
	nRad = nut_diam +0.1
	nDia = nut_diam 
	mulz -1
	gosub 350
	del 1
	body -1
	addz nut_thk
	gosub 350
	del 1
endgroup
group "Wash1"
	prism_  3, wash1_thk,
		0    , 0  ,  900+79,
		wash1_diam/2, 360, 4000+79,
		wash1_diam_in/2, 360, 4000+79
endgroup
group "Wash2"
	if wash2_type="Прямоугольная" then
		prism_  4, wash2_thk, wash2_a/2,wash2_b/2,15, -wash2_a/2,wash2_b/2,15, -wash2_a/2,-wash2_b/2,15, wash2_a/2,-wash2_b/2,15
	endif
	if wash2_type="Круглая" then
		prism_  3, wash2_thk,
			0    , 0  ,  900+79,
			wash2_diam/2, 360, 4000+79,
			wash2_diam_in/2, 360, 4000+79
	endif
endgroup

HOTSPOT	0, 0, 0, unID: unID=unID+1
HOTSPOT	0, 0, lsv-hit, unID: unID=unID+1
HOTSPOT	0, 0, 0, unID, lsv, 1+128: unID=unID+1
HOTSPOT	0, 0, lsv, unID, lsv, 2: unID=unID+1
HOTSPOT	0 ,0, -1, unID, lsv, 3: unID=unID+1

HOTSPOT	0, 0, 0, unID, hp, 1+128: unID=unID+1
HOTSPOT	0, 0, hp, unID, hp, 2: unID=unID+1
HOTSPOT	0 ,0, -1, unID, hp, 3: unID=unID+1

HOTSPOT	0, 0, hp, unID, insideLength, 1+128: unID=unID+1
HOTSPOT	0, 0, hp+insideLength, unID, insideLength, 2: unID=unID+1
HOTSPOT	0 ,0, -1, unID, insideLength, 3: unID=unID+1

HOTSPOT	0, 0, 0, unID, hz, 1+128: unID=unID+1
HOTSPOT	0, 0, -hz, unID, hz, 2: unID=unID+1
HOTSPOT	0 ,0, 1, unID, hz, 3: unID=unID+1

addz hp
if (qty_wash_start=3) or (qty_wash_start=2) then
	addz -wash2_thk
	gosub "pl_wash2"
endif
if (qty_wash_start=1) or (qty_wash_start=2) then
	addz -wash1_thk
	gosub "pl_wash1"
endif
if qty_nut_start>0 then
	addz -nut_thk
	gosub "pl_nut"
	del 1
endif
if qty_nut_start>1 then
	addz -2*nut_thk
	rotz 90
	gosub "pl_nut"
	del 1
endif

del top
rotx yRot

addz hp+insideLength
if (qty_wash_end=3) or (qty_wash_end=2) then
	gosub "pl_wash2"
	addz wash2_thk
endif
if (qty_wash_end=1) or (qty_wash_end=2) then
	gosub "pl_wash1"
	addz wash1_thk
endif
if qty_nut_end>0 then gosub "pl_nut"
if qty_nut_end=2 then
	addz nut_thk
	rotz 90
	gosub "pl_nut"
	del 1
endif

del top
rotx yRot
!!!--------Часть болта, находящаяся над заделкой
MATERIAL m2
lt = lsv-hit
if lt>0 then CYLIND lt, rb !Часть болта без резбьы
addz lt
MATERIAL m1
CYLIND hit, rb-0.0005 !Часть болта с резьбой
del top
!!!--------

if GLOB_VIEW_TYPE=2 or not(show_thread) then end

rotx yRot
if isp = "с отгибом" then
	MATERIAL m3
	lt = hz-rz-rb*2
	addz -lt+(lsv-hit)*((lsv-hit)<0)
	CYLIND lt, rb
	addz -rb-rz
	rotz 90
	addx rb+rz
	roty -90
	ELBOW rz+rb, 90, rb
	del 3
	rotx 90
	addz -rb-rz-bolt_thk
	CYLIND bolt_thk, rb
endif

if isp = "прямой" then
	MATERIAL m3
	lt = hz
	addz -lt
	CYLIND lt, rb
endif

if isp = "с анк. плитой" then
	MATERIAL m3
	lt = hz-hit
		addz -lt
		CYLIND lt, rb
	MATERIAL m1
	lt = hit
	addz -lt
	CYLIND lt, rb
	addz nut_thk*0.5
	gosub "pl_nut"
	addz nut_thk
	wash2_a = bolt_diam
	wash2_b = wash2_a
	gosub "pl_wash2"
endif
del top

killgroup "Nut"
killgroup "Wash1"
killgroup "Wash2"
killgroup "nChamfer"
end

350 : !!! ========== Chamfer ==========
	if not(bChamfer) then return
	nutRad = abs(nRad)
	put 0            , nutRad     ,  2,
		nutRad-nDia/2, 0.96*nDia/2,  2,
		nutRad-nDia/2, nutRad     ,  2,
		0            , nutRad     , -1
	roty -90
	addx -(nutRad-nDia/2)
	revolve nsp/3, 360, 1+2+4+8+16+32, get(nsp)	
	del 2
return

"pl_nut":
	if not(bChamfer) then placegroup "Nut" 
	if bChamfer then placegroup subgroup("Nut", "nChamfer")
return

"pl_wash1":
	placegroup "Wash1"
	hotspot 0,0,0
	hotspot 0,0,wash1_thk
return

"pl_wash2":
	if show_wash_list then placegroup "Wash2"
	hotspot 0,0,0
	hotspot 0,0,wash2_thk
	hotspot 0,wash2_b/2,0
	hotspot 0,wash2_b/2,wash2_thk
	hotspot wash2_a/2,wash2_b/2,0
	hotspot wash2_a/2,wash2_b/2,wash2_thk
	hotspot wash2_a/2,0,0
	hotspot wash2_a/2,0,wash2_thk
	hotspot 0,-wash2_b/2,0
	hotspot 0,-wash2_b/2,wash2_thk
	hotspot -wash2_a/2,-wash2_b/2,0
	hotspot wash2_a/2,-wash2_b/2,wash2_thk
	hotspot -wash2_a/2,0,0
	hotspot -wash2_a/2,0,wash2_thk
return
