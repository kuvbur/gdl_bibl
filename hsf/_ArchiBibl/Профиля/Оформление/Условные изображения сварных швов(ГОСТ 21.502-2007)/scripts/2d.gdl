MUL2 .01*GLOB_SCALE, .01*GLOB_SCALE

dl=dl*100/GLOB_SCALE
rz=rz/10
rz1=rz1/10
hz=hz/10


if soed="Стыковое" then h1=0
if soed="Угловое, тавровое, внахлестку" then h1=hz/2


!!! ЗАВОДСКОЙ ШОВ !!!

IF sow="Заводской" then

hotspot2 0,0, 1,dl,1+256
hotspot2 dl,0, 2,dl,2

if stsow="Видимая" then
C=round_int(dl/rz)

if (C*rz>dl) then C=C-1

r1=(dl-C*rz)/2

add2 r1,h1

pen tol
for i=0 to C
gosub 111
add2 rz,0
next i
endif


if stsow="Невидимая" then
t1=rz+rz+rz1
C1=dl/t1
C=round_int(C1)

if C<=0 then C=1

r1=(dl-(C*t1))/2

add2 r1+rz1/2,h1

pen ton
for i=0 to C-1
gosub 111
add2 rz,0: gosub 111
add2 rz,0: gosub 111
add2 rz1,0
next i
endif


ENDIF  !!! Для заводского шва





!!! МОНТАЖНЫЙ ШОВ !!!


IF sow="Монтажный" then


hotspot2 0,0, 1,dl,1+256
hotspot2 dl,0, 2,dl,2

if stsow="Видимая" then
C=round_int(dl/rz)

if (C*rz>dl) or (C*rz+hz)>dl then C=C-1

r1=(dl-C*rz)/2

add2 r1,h1

pen tol
for i=0 to C
gosub 222
add2 rz,0
next i
endif

if stsow="Невидимая" then
t1=rz+rz+rz1
C1=dl/t1
C=round_int(C1)

if C<=0 then C=1

r1=(dl-(C*t1))/2

add2 r1+rz1/2,h1

pen ton
for i=0 to C-1
gosub 222
add2 rz,0: gosub 222
add2 rz,0: gosub 222
add2 rz1,0
next i
endif
ENDIF   !!! Для монтажного шва



IF sow="Шов сварного соединения внахлестку, контактный, точечный" then
L1=ri1/10
L2=L1/2
L3=L2/2

x1=-L3: y1=-L3
x2=0: y2=-L2
x3=L3: y3=-L3
x4=-L3: y4=L3
x5=0: y5=L2
x6=L3: y6=L3

pen tol
line2 x1,y1, x2,y2
line2 x2,y2, x3,y3

line2 x4,y4, x5,y5
line2 x5,y5, x6,y6

circle2 0,0, L3


hotline2 x1,y1, x2,y2
hotline2 x2,y2, x3,y3
hotline2 x4,y4, x5,y5
hotline2 x5,y5, x6,y6
hotarc2 0,0, L3, 0, 360

hotspot2 0,0
hotspot2 x1,y1
hotspot2 x2,y2
hotspot2 x3,y3
hotspot2 x4,y4
hotspot2 x5,y5
hotspot2 x6,y6
ENDIF



IF sow="Шов сварного соединения электрозаклепочный внахлестку (с курглым отверстием)" then
L1=ri2/10
L2=L1/2
L3=0.85*L2

x1=0: y1=L2
x2=L2: y2=L2
x3=-L2: y3=-L2
x4=0: y4=-L2
x5=-L2: y5=0
x6=L2: y6=0

pen ton
line2 x1,y1, x4,y4
line2 x5,y5, x6,y6

pen tol
line2 x1,y1, x2,y2
line2 x2,y2, x3,y3
line2 x3,y3, x4,y4
circle2 0,0, L3

hotline2 x1,y1, x4,y4
hotline2 x5,y5, x6,y6
hotline2 x1,y1, x2,y2
hotline2 x2,y2, x3,y3
hotline2 x3,y3, x4,y4
hotarc2 0,0, L3, 0, 360

hotspot2 0,0
hotspot2 x1,y1
hotspot2 x2,y2
hotspot2 x3,y3
hotspot2 x4,y4
ENDIF



!!! Торцевой (треугольник)

IF sow="Торцевой (треугольник)" then
L1=ri3/10

x1=0: y1=0
x2=0: y2=L1
x3=L1: y3=0

pen tol
set fill strih

poly2 3,7,
x1,y1,
x2,y2,
x3,y3

hotline2 x1,y1, x2,y2
hotline2 x2,y2, x3,y3
hotline2 x3,y3, x1,y1

hotspot2 x1,y1
hotspot2 x2,y2
hotspot2 x3,y3

ENDIF


!!! Торцевой (дуга)

IF sow="Торцевой (дуга)" then
L1=ri3/10

x1=0: y1=0
x2=0: y2=L1
x3=L1: y3=0

pen tol
set fill strih

poly2_ 5,3,
x1,y1,1,
x2,y2,1,
x1,y1,900,
0,-90,4001,
x1,y1,1

hotline2 x1,y1, x2,y2
hotline2 x1,y1, x3,y3
hotarc2 0,0,L1,0,90

hotspot2 x1,y1
hotspot2 x2,y2
hotspot2 x3,y3


ENDIF





END   !!! Конец программы


111:   !!! Заводской шов
line2 0,-hz/2, 0,hz/2
hotline2 0,-hz/2, 0,hz/2
RETURN


222:   !!! Монтажный шов
line2 -hz/2, hz/2, hz/2, -hz/2
line2 -hz/2, -hz/2, hz/2, hz/2

hotline2 -hz/2, hz/2, hz/2, -hz/2
hotline2 -hz/2, -hz/2, hz/2, hz/2
RETURN