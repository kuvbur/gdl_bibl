EPS=0.00001
unID=1 : unIDR=800 : unIDD=4000
is = 1 : ie = 2
ix = 1 : iy = 2 : iz = 3
n = REQUEST ("Name_of_main", "", name)
is_call = 0 : if name<>"" then is_call = 1

if GLOB_SCRIPT_TYPE=2 or GLOB_SCRIPT_TYPE=3 then
	success = LIBRARYGLOBAL ("Libraryflobals-km", "show_connect", show_connect)
	show = 0
	if show_connect and upr then show = 1
	if not(upr) then show = 1
	if show then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_list", show_list)
		show = show_list
	endif
	if show then 
		call "Показ_по_типам" PARAMETERS tip_konstr=tip_konstr RETURNED_PARAMETERS show_type
		show = show_type
	endif
	if show then 
		call "Показ_по_группам" PARAMETERS ALL el_views=el_views RETURNED_PARAMETERS show_el
		show = show_el
	endif
	if show then 
		call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
		show = show_subpos
	endif
	if show then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_izd", show_izd)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_metall", show_metall)
		if type_spec = "Металл" and not(show_metall) then show = 0
		if type_spec = "Изделие" and not(show_izd) then show = 0
	endif
	if show then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "ignore_id", ignore_id)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_ingore", show_ingore)
		show_spec = 0 : IF STRSTR (GLOB_ID, ignore_id, 1) > 0 or type_spec="Не специфицировать" THEN show_spec = 1
		if not(show_ingore) and show_spec then show = 0
	endif
else
	show = 1
endif

if strlen(syst_name)>1 and (GLOB_SCRIPT_TYPE=2 or GLOB_SCRIPT_TYPE=3) then
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_axis", show_axis)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_axis_and", show_axis_and)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_conventions", show_conventions)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_op", show_op)
	call "Показ_систем" PARAMETERS All isMep=0, isEqp=1 RETURNED_PARAMETERS show_el
else
	show_el = 1
endif

!--------- Подготовка данных для сварки -------------------
dim pos_weld[2][3]
dim ttype_weld[]
dim itype_weld[] : i=0
i=i+1 : itype_weld[i]=0 : ttype_weld[i] = "Нет"
i=i+1 : itype_weld[i]=1 : ttype_weld[i] = "Автоматический угловой"
i=i+1 : itype_weld[i]=2 : ttype_weld[i] = "Автоматический стыковой"
i=i+1 : itype_weld[i]=3 : ttype_weld[i] = "Ручной угловой"
i=i+1 : itype_weld[i]=4 : ttype_weld[i] = "Ручной стыковой"

show_weld_line = 1

if vardim1(weld_side)<vardim1(kontur) or vardim1(weld_side_up)<vardim1(kontur) then
	dim weld_side_[90]
	dim weld_side_coord_[90][2]
	for i=1 to 90
		weld_side_[i] = 0
		weld_side_coord_[i][1] = 0
		weld_side_coord_[i][2] = 0
	next i
	parameters weld_side = weld_side_, weld_side_up = weld_side_
	parameters weld_side_coord = weld_side_coord_
endif

if vardim1(weld_side_ang)<vardim1(kontur) or vardim1(weld_side_ang_up)<vardim1(kontur) then
	dim weld_side_ang_[90]
	for i=1 to 90
		weld_side_ang_[i] = 0
	next i
	parameters weld_side_ang = weld_side_ang_, weld_side_ang_up = weld_side_ang_
endif

!--------- Подготовка данных для сварки -------------------
kontur[n_t+1][1] = kontur[1][1]
kontur[n_t+1][2] = kontur[1][2]

s_plit_o = 0
for i = 1 to n_t
	x1 = kontur[i][1]
	y1 = kontur[i][2]
	x2 = kontur[i+1][1]
	y2 = kontur[i+1][2]
	s_tr = 0.5 * (x1 + x2) * (y1 - y2)
	s_plit_o = s_plit_o  + s_tr
next i

