if strlen(syst_name)>1 then
	if show_el < EPS then end
	IF show_op < EPS THEN END
endif

call "Шаблон профиля" parameters all
if not(show) then end

if GLOB_CONTEXT=6 then
	k = 5
	pen SYMB_SECT_PEN
	MUL2 A_/1000, A_/1000 
	line2 0,0,k,0
	end
endif

chStoryDist = GLOB_CH_STORY_DIST
IF gs_StorySensitive = 0 THEN chStoryDist = 0
if chStoryDist > EPS then !"Этажом выше"
	gs_cont_pen = gs_cont_pen_up
	gs_fill_type = gs_fill_type_up
	gs_fill_pen = gs_fill_pen_up
	gs_back_pen = gs_back_pen_up
	gs_line_type = gs_line_type_up
	mark = mark_up
endif
if chStoryDist < EPS then
	if abs(chStoryDist) < EPS then !"Собственный этаж"
		
	else !"Этажом ниже"
		gs_cont_pen = gs_cont_pen_down
		gs_fill_type = gs_fill_type_down
		gs_fill_pen = gs_fill_pen_down
		gs_back_pen = gs_back_pen_down
		gs_line_type = gs_line_type_down
		mark = mark_down
	endif
endif

pen gs_cont_pen
fill gs_fill_type
line_type gs_line_type
SECT_ATTRS gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen, gs_line_type

project2{2} 3, 270, 32+3, gs_back_pen, 0, 0, 90
if (rx mod 180)=0 then
	kx = cos (ry)
	for i = 1 to n_t
		gosub "change_2d"
		line2 kontur[i][1] * kx, kontur[i][2], kontur[i+1][1] * kx, kontur[i+1][2]
		hotline2 kontur[i][1] * kx, kontur[i][2], kontur[i+1][1] * kx, kontur[i+1][2]
	next i
endif

goto "2dend"

"change_2d":
	HOTSPOT2 0, kontur[i][2], unID, kontur[i][1], 1+128: unID=unID+1
	HOTSPOT2 -1, kontur[i][2], unID, kontur[i][1], 3: unID=unID+1
	HOTSPOT2 kontur[i][1] * kx, kontur[i][2], unID, kontur[i][1], 2: unID=unID+1
	HOTSPOT2 kontur[i][1] * kx, 0,unID, kontur[i][2], 1+128: unID=unID+1
	HOTSPOT2 kontur[i][1] * kx, -1,unID, kontur[i][2], 3: unID=unID+1
	HOTSPOT2 kontur[i][1] * kx, kontur[i][2],unID, kontur[i][2], 2: unID=unID+1

	!-- Тип сварки
	j = i - 1 : if i=1 then j = n_t
	dx = kontur[j][1]-kontur[i][1]
	dy = kontur[j][2]-kontur[i][2]
	gosub "xytorphi"
	add2 kontur[i][1], kontur[i][2]
	rot2 phi
	add2 r/3,0
	HOTSPOT2 0, 0, unID, weld_side_coord[i][1], 1+128, weld_side[i]: unID=unID+1
	HOTSPOT2 0, -1, unID, weld_side_coord[i][1], 3, weld_side[i]: unID=unID+1
	HOTSPOT2 0, weld_side_coord[i][1], unID, weld_side_coord[i][1], 2, weld_side[i], "Тип шва снизу": unID=unID+1
	del 1
	add2 2*r/3,0
	HOTSPOT2 0, 0, unID, weld_side_coord[i][2], 1+128, weld_side_up[i]: unID=unID+1
	HOTSPOT2 0, -1, unID, weld_side_coord[i][2], 3, weld_side_up[i]: unID=unID+1
	HOTSPOT2 0, weld_side_coord[i][2], unID, weld_side_coord[i][2], 2, weld_side_up[i], "Тип шва сверху": unID=unID+1
	del 3
return

"xytorphi":
	r = SQR(dx*dx+dy*dy)
	if dx>0 and dy>=0 then phi = atn(dy/dx)
	if dx>0 and dy<0 then phi = atn(dy/dx)+360
	if dx<0 then phi = atn(dy/dx)+180
	if dx=0 and dy>0 then phi = 90
	if dx=0 and dy<0 then phi = 270
	if dx=0 and dy=0 then phi = 0
return

"2dend":
