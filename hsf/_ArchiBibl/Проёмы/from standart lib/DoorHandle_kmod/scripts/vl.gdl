
if not(bWindow) | bSlidingWindow then
	! =============================================================================
	! Door Handle Types
	! =============================================================================

	dim iHandleTypesEnabled[]
	dim stHandleTypesEnabled[]
	idx = 1
	
	iHandleTypesEnabled[idx] = 0: idx = idx + 1	! NONE
	if not(bINTHandles) then
		iHandleTypesEnabled[idx] = 1: idx = idx + 1	! CROSS
	endif
	if bittest(iHandleCategoryEnable, 0) then			! KNOB
		for i=2 to 7
			if not(bINTHandles & (i=2 | i=6 | i=7)) then
				iHandleTypesEnabled[idx] = i: idx = idx + 1
			endif
		next i
	endif
	if bittest(iHandleCategoryEnable, 1) then			! HANDLE (NORMAL)
		for i=8 to 14
			iHandleTypesEnabled[idx] = i: idx = idx + 1
		next i
	endif
	if bittest(iHandleCategoryEnable, 2) then			! HANDLE (FIX)
		for i=15 to 17
			if not(bINTHandles & (i=16)) then
				iHandleTypesEnabled[idx] = i: idx = idx + 1
			endif
		next i
	endif
	if bittest(iHandleCategoryEnable, 3) then			! HORIZONTAL BAR
		for i=18 to 19
			iHandleTypesEnabled[idx] = i: idx = idx + 1
		next i
	endif
	if bittest(iHandleCategoryEnable, 4) then			! VERTICAL BAR
		for i=20 to 21
			iHandleTypesEnabled[idx] = i: idx = idx + 1
		next i
	endif
	if bittest(iHandleCategoryEnable, 5) then			! BOMB SHELTER
		iHandleTypesEnabled[idx] = 22: idx = idx + 1
	endif
	if bittest(iHandleCategoryEnable, 6) then			! PANIC BAR
		for i=23 to 24
			iHandleTypesEnabled[idx] = i: idx = idx + 1
		next i
	endif
	if bittest(iHandleCategoryEnable, 7) then			! HANDLE (FIX 3) - INT SlidingOnly
		iHandleTypesEnabled[idx] = 25: idx = idx + 1
	endif
	iHandleTypesEnabled[idx] = 99: idx = idx + 1	! CUSTOM
	
	stHandleTypesEnabled[1] = stHandleTypesNone
	for i=2 to vardim1(iHandleTypesEnabled) - 1
		stHandleTypesEnabled[i] = stHandleTypes[iHandleTypesEnabled[i]]
	next i
	stHandleTypesEnabled[i] = stHandleTypesCustom
	
	if bSetableHandleUI then
		values{2} "gs_handle_set_to_edit"	HANDLE_IN_MAIN_PANEL,	stHandleSetToLeaf[1],
											HANDLE_IN_SECOND_PANEL,	stHandleSetToLeaf[2]
	endif

	if vardim1(iHandleTypesEnabled) > 0 then
		values "gs_handle_type_m"	 iHandleTypesEnabled
		values "gs_handle_type"		stHandleTypesEnabled
		if bSecondPanel then
			values{2} "gs_handle_type2_m"	iHandleTypesEnabled, stHandleTypesEnabled
		endif
	
		if bSecondHandle & bDifferentHandles then
			values "gs_handle_type_out_m"	 iHandleTypesEnabled
			values "gs_handle_type_out"		stHandleTypesEnabled
		endif
	
		if bSecondPanel & bSecondHandle & bDifferentHandles then
			values "gs_handle_type2_out_m"	 iHandleTypesEnabled
			values "gs_handle_type2_out"	stHandleTypesEnabled
		endif

		if bPassDoor then
			values{2} "gs_handle_typ_in_m"	iHandleTypesEnabled, stHandleTypesEnabled
			values{2} "gs_handle_typ_out_m"	iHandleTypesEnabled, stHandleTypesEnabled
		endif
	endif
	
	if GLOB_MODPAR_NAME = "gs_handle_type" then
		for i=1 to vardim1(stHandleTypesEnabled)
			if gs_handle_type = stHandleTypesEnabled[i] then
				gs_handle_type_m = iHandleTypesEnabled[i]
				parameters gs_handle_type_m = gs_handle_type_m
				i = vardim1(stHandleTypesEnabled)	! Force loop end
			endif
		next i
	else
		if gs_handle_type_m > 0 & gs_handle_type_m < 99 then
			gs_handle_type = stHandleTypes[gs_handle_type_m]
		else
			if gs_handle_type_m = 0  then gs_handle_type = stHandleTypesNone
			if gs_handle_type_m = 99 then gs_handle_type = stHandleTypesCustom
		endif
		parameters gs_handle_type = gs_handle_type
	endif
	
	if bSecondHandle & bDifferentHandles then
		if GLOB_MODPAR_NAME = "gs_handle_type_out" then
			for i=1 to vardim1(stHandleTypesEnabled)
				if gs_handle_type_out = stHandleTypesEnabled[i] then
					gs_handle_type_out_m = iHandleTypesEnabled[i]
					parameters gs_handle_type_out_m = gs_handle_type_out_m
					i = vardim1(stHandleTypesEnabled)	! Force loop end
				endif
			next i
		else
			if gs_handle_type_out_m > 0 & gs_handle_type_out_m < 99 then
				gs_handle_type_out = stHandleTypes[gs_handle_type_out_m]
			else
				if gs_handle_type_out_m = 0  then gs_handle_type_out = stHandleTypesNone
				if gs_handle_type_out_m = 99 then gs_handle_type_out = stHandleTypesCustom
			endif
			parameters gs_handle_type_out = gs_handle_type_out
		endif
	endif
	
	if bSecondPanel & bSecondHandle & bDifferentHandles then
		if GLOB_MODPAR_NAME = "gs_handle_type2_out" then
			for i=1 to vardim1(stHandleTypesEnabled)
				if gs_handle_type2_out = stHandleTypesEnabled[i] then
					gs_handle_type2_out_m = iHandleTypesEnabled[i]
					parameters gs_handle_type2_out_m = gs_handle_type2_out_m
					i = vardim1(stHandleTypesEnabled)	! Force loop end
				endif
			next i
		else
			if gs_handle_type2_out_m > 0 & gs_handle_type2_out_m < 99 then
				gs_handle_type2_out = stHandleTypes[gs_handle_type2_out_m]
			else
				if gs_handle_type2_out_m = 0  then gs_handle_type2_out = stHandleTypesNone
				if gs_handle_type2_out_m = 99 then gs_handle_type2_out = stHandleTypesCustom
			endif
			parameters gs_handle_type2_out = gs_handle_type2_out
		endif
	endif

	
	! =============================================================================
	! Door Handle Height and Position
	! =============================================================================
	
	if	( not(bPassDoor) & \
		gs_handle_type_m = 0 & \
		(not(bSecondHandle & bDifferentHandles) | gs_handle_type_out_m = 0) ) | \
		( bPassDoor & \
		  gs_handle_typ_in_m = 0 & \
		  gs_handle_typ_out_m = 0 ) then
		lock "gs_han_height", "gs_han_pos"
		hideparameter "gs_han_height", "gs_han_pos"
	endif

	if bPassDoor & gs_handle_type_m = 0 then
		lock "gs_main_han_height"
		hideparameter "gs_main_han_height"
	endif
		
	if not(bINTHandles) then
		if gs_handle_type_m = 18 | gs_handle_type_m = 19 | gs_handle_type_m = 23 | gs_handle_type_m = 24 then
			lock "gs_han_pos"
		endif
		if gs_handle_type_m = 20 | gs_handle_type_m = 21 then
			lock "gs_han_height"
		endif
	endif
	
	if bSecondPanel then
		if gs_handle_type2_m = 0 & (not(bSecondHandle & bDifferentHandles) | gs_handle_type2_out_m = 0) then
			lock "gs_han_height2", "gs_han_pos2"
			hideparameter "gs_han_height2", "gs_han_pos2"
		endif
	
		if not(bINTHandles) then
			if gs_handle_type2_m = 18 | gs_handle_type2_m = 19 | gs_handle_type_m = 23 | gs_handle_type_m = 24 then
				lock "gs_han_pos2"
			endif
			if gs_handle_type2_m = 20 | gs_handle_type2_m = 21 then
				lock "gs_han_height2"
			endif
		endif
	endif
	
	
	! =============================================================================
	! Door Handle Materials
	! =============================================================================
	
	if (gs_handle_type_m <= 1 | gs_handle_type_m = 99) &\
		(not(bSecondHandle & bDifferentHandles) | gs_handle_type_out_m <= 1 | gs_handle_type_out_m = 99) &\
		(not(bSecondPanel) | gs_handle_type2_m <= 1 | gs_handle_type2_m = 99) &\
		(not(bSecondPanel & bSecondHandle & bDifferentHandles) | gs_handle_type2_out_m <= 1 | gs_handle_type2_out_m = 99) then
		lock "gs_handle_mat", "gs_handle_ac_mat"
		hideparameter "gs_handle_mat", "gs_handle_ac_mat"
	endif
	
	if not(	gs_handle_type_m = 9 |\
			gs_handle_type_m = 12 |\
			gs_handle_type_m = 13 |\
			gs_handle_type_m = 14 |\
			gs_handle_type_m = 15 |\
			gs_handle_type_m = 23 |\
			gs_handle_type_m = 24) &\
		(not(bSecondPanel) | not(gs_handle_type2_m = 9 |\
			gs_handle_type2_m = 12 |\
			gs_handle_type2_m = 13 |\
			gs_handle_type2_m = 14 |\
			gs_handle_type2_m = 15 |\
			gs_handle_type2_m = 23 |\
			gs_handle_type2_m = 24)) &\
		(not(bSecondHandle & bDifferentHandles) | not(gs_handle_type_out_m = 9 |\
			gs_handle_type_out_m = 12 |\
			gs_handle_type_out_m = 13 |\
			gs_handle_type_out_m = 14 |\
			gs_handle_type_out_m = 15 |\
			gs_handle_type_out_m = 23 |\
			gs_handle_type_out_m = 24)) &\
		(not(bSecondPanel & bSecondHandle & bDifferentHandles) | not(gs_handle_type2_out_m = 9 |\
			gs_handle_type2_out_m = 12 |\
			gs_handle_type2_out_m = 13 |\
			gs_handle_type2_out_m = 14 |\
			gs_handle_type2_out_m = 15 |\
			gs_handle_type2_out_m = 23 |\
			gs_handle_type2_out_m = 24)) then
		lock "gs_handle_ac_mat"
		hideparameter "gs_handle_ac_mat"
	endif
	
	
	! =============================================================================
	! Door Custom Handle
	! =============================================================================
	
	if gs_handle_type_m = 99 then
		dim gs_pt_handle[]
		call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle
		values "gs_cust_handle" gs_pt_handle
	else
		lock "gs_cust_handle"
		hideparameter "gs_cust_handle"
	endif
	if bSecondPanel then
		if gs_handle_type2_m = 99 then
			dim gs_pt_handle[]
			call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle
			values "gs_cust_handle2" gs_pt_handle
		else
			lock "gs_cust_handle2"
			hideparameter "gs_cust_handle2"
		endif
	endif
	if bSecondHandle & bDifferentHandles then
		if bHasPanelPict then
			values "gs_subHandle" 1, 2, 3	! Outside, Inside, Outside and Inside
		else
			values "gs_subHandle" 1, 2	! Outside, Inside		
		endif
	
		if gs_handle_type_out_m = 99 then
			dim gs_pt_handle[]
			call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle
			values "gs_cust_handle_out" gs_pt_handle
		else
			lock "gs_cust_handle_out"
			hideparameter "gs_cust_handle_out"
		endif
	endif
	if bSecondPanel & bSecondHandle & bDifferentHandles then
		if bHasPanelPict then
			values "gs_subHandle2" 1, 2, 3	! Outside, Inside, Outside and Inside
		else
			values "gs_subHandle2" 1, 2	! Outside, Inside		
		endif

		if gs_subHandle2 = 3 then
			hideparameter "gs_handle_type2_m"
			gs_handle_type2_m = gs_handle_type2_out_m
			parameters gs_handle_type2_m = gs_handle_type2_m
		endif

		if gs_handle_type2_out_m = 99 then
			dim gs_pt_handle[]
			call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle
			values "gs_cust_handle2_out" gs_pt_handle
		else
			lock "gs_cust_handle2_out"
			hideparameter "gs_cust_handle2_out"
		endif
	endif

	! =============================================================================
	! Door UI
	! =============================================================================
	
	if bSecondHandle & bDifferentHandles then
		if bHasPanelPict then
			values "gs_subHandle" 1, 2, 3	! Outside, Inside, Outside and Inside
		else
			values "gs_subHandle" 1, 2	! Outside, Inside		
		endif
	endif

	if gs_subHandle = 3 then
		hideparameter "gs_handle_type_m"
		gs_handle_type_m = gs_handle_type_out_m
		parameters gs_handle_type_m = gs_handle_type_m
	endif

else
	! =============================================================================
	! Window Handle Types
	! =============================================================================
	
	dim iHandleWinTypesEnabled[]
	dim stHandleWinTypesEnabled[]
	idx = 1
	
!	iHandleWinTypesEnabled[idx] = 0: idx = idx + 1		! NONE
	if bittest(iHandleWinCategoryEnable, 0) then			! WINDOW HANDLES
		for i=1 to 8
			iHandleWinTypesEnabled[idx] = i: idx = idx + 1
		next i
	endif
	iHandleWinTypesEnabled[idx] = 99: idx = idx + 1		! CUSTOM
	
!	stHandleWinTypesEnabled[1] = stHandleTypesNone
	for i=1 to vardim1(iHandleWinTypesEnabled) - 1
		stHandleWinTypesEnabled[i] = stHandleWinTypes[iHandleWinTypesEnabled[i]]
	next i
	stHandleWinTypesEnabled[i] = stHandleTypesCustom
	
	if vardim1(iHandleWinTypesEnabled) > 0 then
		values "gs_handle_type_win_m"	 iHandleWinTypesEnabled
		values "gs_handle_type_win"		stHandleWinTypesEnabled
	endif
	
	if GLOB_MODPAR_NAME = "gs_handle_type_win" then
		for i=1 to vardim1(stHandleWinTypesEnabled)
			if gs_handle_type_win = stHandleWinTypesEnabled[i] then
				gs_handle_type_win_m = iHandleWinTypesEnabled[i]
				parameters gs_handle_type_win_m = gs_handle_type_win_m
				i = vardim1(stHandleWinTypesEnabled)	! Force loop end
			endif
		next i
	else
		if gs_handle_type_win_m > 0 & gs_handle_type_win_m < 99 then
			gs_handle_type_win = stHandleWinTypes[gs_handle_type_win_m]
		else
			if gs_handle_type_win_m = 0  then gs_handle_type_win = stHandleTypesNone
			if gs_handle_type_win_m = 99 then gs_handle_type_win = stHandleTypesCustom
		endif
		parameters gs_handle_type_win = gs_handle_type_win
	endif

	! =============================================================================
	! Window Custom Handle
	! =============================================================================

	if gs_handle_type_win_m = 99 then
		dim gs_pt_handle_win[]
		call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle_win
		values "gs_cust_handle_win" gs_pt_handle_win
	else
		lock "gs_cust_handle_win"
		hideparameter "gs_cust_handle_win"
	endif

	if not(gs_window_handle) then
		hideparameter "gs_handle_type_win", "gs_cust_handle_win", "gs_handle_win_mat"
		lock "gs_handle_type_win", "gs_handle_type_win_m", "gs_cust_handle_win", "gs_handle_win_mat"
	else
		if gs_handle_type_win_m = 0 then
			hideparameter "gs_handle_win_mat"
			lock "gs_handle_win_mat"
		endif
	endif
endif

