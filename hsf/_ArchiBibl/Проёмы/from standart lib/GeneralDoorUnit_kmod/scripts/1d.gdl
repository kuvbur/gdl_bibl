
! const values for parameter: iTypeDemolitionFill
RENOVATION_FILL_EXIST		= 1
RENOVATION_FILL_DEMOLISHED	= 2
RENOVATION_FILL_NEW			= 3
RENOVATION_FILL_NEW_FULL	= 4

! const values for parameter: iOpeningMethod
OPENMETHOD_OPEN_SIMP		= 1
OPENMETHOD_OPEN_DOUB		= 2
OPENMETHOD_PIVOT			= 3
OPENMETHOD_SLIDE_INT		= 4
OPENMETHOD_SLIDE_EXT_SIMP	= 5
OPENMETHOD_SLIDE_EXT_DOUB	= 6
OPENMETHOD_POCKET_SIMP		= 7
OPENMETHOD_POCKET_DOUB		= 8
OPENMETHOD_SLIDFOLD_SIMP	= 9
OPENMETHOD_SLIDFOLD_BIFOLD	= 10

! const values for parameter: iThresholdType
TRESHOLD_OFF          = 1
TRESHOLD_NORMAL       = 2
TRESHOLD_EXTENDED     = 3
TRESHOLD_BOTHEXTENDED = 4
TRESHOLD_BENEATHPANEL = 5
TRESHOLD_PROFILED     = 6
TRESHOLD_FLAT         = 7
TRESHOLD_ANSLAG       = 8
TRESHOLD_GUMMI        = 9
TRESHOLD_HEVE         = 10
TRESHOLD_HC           = 11
TRESHOLD_BRANN        = 12
TRESHOLD_BADEROMS     = 13
TRESHOLD_AKKUSTIKK    = 14
TRESHOLD_SIMPLE       = 15
TRESHOLD_SLOPINGEXT   = 16
TRESHOLD_METAL        = 17
TRESHOLD_EXTENDED_SWE = 18
TRESHOLD_NOTAVAILABLE = -1


! const values for parameter: iSillType
SILL_TIMBER       = 1
SILL_SOLID_22     = 10
SILL_METAL_1      = 11
SILL_PARALLEL     = 12
SILL_METAL_2      = 13
SILL_PARALLEL_2   = 14
SILL_SIMPLEBLOCK  = 15
SILL_BLOCK        = 16
SILL_DUTCH        = 17
SILL_BRICK        = 2
SILL_STONE        = 3
SILL_CERAMIC      = 4
SILL_ETHERNIT     = 5
SILL_CONCRETE_TUB = 6
SILL_ALUMINIUM    = 7
SILL_SOLID_11     = 8
SILL_SOLID_12     = 9

! Contact person: <SzL, FeZs, NJ>

EPS = 0.0001

! iRevealType:
REVEAL_EDGE				= 0
REVEAL_NO				= 1
REVEAL_POSITIVE			= 2
REVEAL_NEGATIVE			= 3
REVEAL_SLANTED			= 4
REVEAL_SPLAYED			= 5
REVEAL_HISTORIC			= 6
REVEAL_DOUBLE			= 7
REVEAL_DOUBLE_SPLAYED	= 8
REVEAL_OUTSIDE_SPLAYED	= 9

! gs_optype_m
SASH_FIXED					= 1		! Fixed Glass
SASH_FIXED_SCREW			= 2		! Fixed Sash
SASH_SIDE_HUNG				= 3		! Side Hung
SASH_SIDE_HUNG_M			= 4		! Side Hung Mirrored
SASH_TILT_TURN				= 5		! Tilt and Turn
SASH_TILT_TURN_M			= 6		! Tilt and Turn Mirrored
SASH_BOTTOM_HUNG			= 7		! Bottom Hung
SASH_TOP_HUNG				= 8		! Top Hung
SASH_HOR_PIVOT				= 9		! Horizontal Pivot Hung
SASH_VER_PIVOT				= 10	! Vertical Pivotted
SASH_TILT_SLIDE				= 11	! Tilt and Slide
SASH_LEFT_SLIDING			= 12	! Left Sliding
SASH_RIGHT_SLIDING			= 13	! Right Sliding
SASH_UP_SLIDING				= 14	! Sliding Up
SASH_DOWN_SLIDING			= 15	! Sliding Down
SASH_VER_PIVOT_M			= 16	! Vertical Pivotted Mirrored
SASH_FIXED_SLIDE			= 17	! Fixed Sliding Sash
SASH_DOUBLE_SIDE_HUNG		= 18	! Double Sash
SASH_DOUBLE_TILT_TURN		= 19	! Double Tilt and Turn
SASH_DOUBLE_TILT_TURN_M		= 20	! Double Tilt and Turn Mirrored
SASH_SLIDING_HOR			= 21	! Sliding
SASH_SLIDING_HOR_M			= 22	! Sliding Mirrored
SASH_DOUBLE_SLIDING_HOR		= 23	! Double Sliding
SASH_DOUBLE_SLIDING_HOR_M	= 24	! Double Sliding Mirrored
SASH_SLIDING_VER			= 25	! Single Hung
SASH_DOUBLE_SLIDING_VER		= 26	! Double Hung

! iMullionPosition
MULLION_OUTSIDE		= 1
MULLION_INSIDE		= 2
MULLION_THROUGHOUT	= 3

! gs_leaf_optype_m:
LEAF_1L_SIDE_HUNG			= 1
LEAF_1L_SIDE_HUNG_M			= 2
LEAF_1L_DOUBLE_ACTING		= 3
LEAF_1L_DOUBLE_ACTING_M		= 4
LEAF_2L_SIDE_HUNG			= 5
LEAF_2L_DOUBLE_ACTING		= 6
LEAF_2L_DOUBLE_EGRESS		= 7
LEAF_2L_DOUBLE_EGRESS_M		= 8
LEAF_1L_PIVOT				= 9
LEAF_1L_PIVOT_M				= 10
LEAF_2L_1SLIDING			= 11
LEAF_2L_1SLIDING_M			= 12
LEAF_2L_2SLIDING			= 13
LEAF_2L_2SLIDING_M			= 14
LEAF_3L_2SLIDING			= 15
LEAF_3L_2SLIDING_M			= 16
LEAF_4L_2SLIDING			= 17
LEAF_4L_3SLIDING			= 18
LEAF_4L_3SLIDING_M			= 19
LEAF_4L_4SLIDING			= 20
LEAF_4L_4SLIDING_M			= 21
LEAF_1L_EXTERNAL_SLIDING	= 22
LEAF_1L_EXTERNAL_SLIDING_M	= 23
LEAF_2L_EXTERNAL_SLIDING	= 24
LEAF_1L_POCKET				= 25
LEAF_1L_POCKET_M			= 26
LEAF_2L_POCKET				= 27
LEAF_1L_MULTIPANEL			= 28
LEAF_1L_MULTIPANEL_M		= 29
LEAF_2L_MULTIPANEL			= 30
LEAF_REVOLVING				= 31
LEAF_3L_2SLIDING_CENTER		= 32
LEAF_GAR_1L_UP_AND_OVER		= 100
LEAF_GAR_1L_UP_SLIDING		= 101
LEAF_GAR_1L_SECTIONAL		= 102
LEAF_GAR_2L_SIDE_HUNG		= 103
LEAF_GAR_2L_ROLLSIDE		= 104
LEAF_GAR_1L_ROLLUP			= 105

! gs_door_rail_ext_m:
EXT_RAIL_OFF				= 1
EXT_RAIL_BOTT				= 2
EXT_RAIL_TOP				= 3
EXT_RAIL_BOTT_TOP			= 4

DET2D_FS_OFF				= 0
DET2D_FS_SCALESENS			= -1
DET2D_FS_SIMPLE				= 11		! Simple --> INT 200
DET2D_FS_MIDDLE				= 21		! Middle --> INT 100
DET2D_FS_DETAILED			= 30		! Detailed --> INT 50
DET2D_FS_SIMPLE_CHI			= 51		! Chinese Simple
DET2D_FS_DETAILED_CHI		= 52		! Chinese Detailed

! iLoD2D
SYMBD_OFF							= 0
SYMBD_SCALESENS						= 1
SYMBD_S_1LINE						= 11
SYMBD_S_2LINE						= 12
SYMBD_S_1LINE_FRAME					= 13
SYMBD_S_1LINECL						= 14
SYMBD_S_5							= 15
SYMBD_S_6							= 16
SYMBD_S_7							= 17
SYMBD_S_8							= 18
SYMBD_S_9							= 19
SYMBD_M_1LINELEAF_2LINE				= 21
SYMBD_M_2LINELEAFCL_GLASS			= 22
SYMBD_M_1LINELEAF_1LINE				= 23
SYMBD_M_4							= 24
SYMBD_M_5							= 25
SYMBD_M_6							= 26
SYMBD_M_7							= 27
SYMBD_M_8							= 28
SYMBD_M_9							= 29
SYMBD_D_2LINELEAFOP_GLASS			= 31
SYMBD_D_2LINELEAFCL_REBATEONGLASS	= 32
SYMBD_D_2LINELEAFOP_2LINE			= 33
SYMBD_D_2LINELEAFOP_SASH			= 34
SYMBD_D_1LINELEAF_GLASS				= 35
SYMBD_D_2LINELEAFCL_SASHWLINE		= 36
SYMBD_D_1LINELEAF_SASHWLINE			= 37
SYMBD_D_2LEAFOPGLASS_GLASS			= 38
SYMBD_D_9							= 39

SYMBGLASS_NOGLASS					= 0
SYMBGLASS_SIMPLEGLASS				= 1
SYMBGLASS_DOUBLEGLASS				= 2


! gs_iSwingType:
LINE_SHAPE_SWING_NONE			= 0
LINE_SHAPE_SWING_CURVED			= 1
LINE_SHAPE_SWING_STRAIGHT		= 2
LINE_SHAPE_SWING_SIMPLE_LINE	= 3
LINE_SHAPE_SWING_LEAF_LINE		= 4

! ifc_optype:
DOOR_OP_NOT_DEFINED						= 0
DOOR_OP_SINGLE_DOOR_SINGLE_SWING		= 1
DOOR_OP_DOUBLE_DOOR_SINGLE_SWING		= 2
DOOR_OP_SINGLE_DOOR_DOUBLE_SWING		= 3
DOOR_OP_DOUBLE_DOOR_DOUBLE_SWING		= 4
DOOR_OP_DOUBLE_DOOR_SINGLE_SWING_OPP	= 5
DOOR_OP_SINGLE_DOOR_SLIDING				= 6
DOOR_OP_DOUBLE_DOOR_SLIDING				= 7
DOOR_OP_SINGLE_DOOR_FOLDING				= 8
DOOR_OP_DOUBLE_DOOR_FOLDING				= 9
DOOR_OP_REVOLVING						= 10
DOOR_OP_ROLLING_UP						= 11
DOOR_OP_USER_DEFINED					= 12

! gs_iSlidingType:
LINE_SHAPE_SLIDING_NONE			= 0
LINE_SHAPE_SLIDING_ARROW		= 1
LINE_SHAPE_SLIDING_SIMPLE_LINE	= 2

! gs_frame_style_m
FRAME_STYLE_BLOCK				= 1
FRAME_STYLE_DOUBLE_REBATE		= 2
FRAME_STYLE_SIMPLE_REBATE		= 3
FRAME_STYLE_METAL				= 4
FRAME_STYLE_DOUBLE_EGRESS_METAL = 5
FRAME_STYLE_EXTRUSION			= 6
FRAME_STYLE_WRAPPED_EXTRUSION	= 7 

! iVentilationProfileType
VENTPROFILE_OVER_FRAME		= 2
VENTPROFILE_OVER_GLAZING	= 3

! iMullionStyle:
MULLION_NONE			= 1
MULLION_HVGRID			= 2
MULLION_EDITABLEGRID	= 3
MULLION_CUSTOM			= 4
MULLION_PRAIRIE			= 5
MULLION_DIVIDED			= 6
MULLION_SUNBURST_1		= 7
MULLION_SUNBURST_2		= 8
MULLION_SUNBURST_3		= 9
MULLION_SUNBURST_4		= 10
MULLION_SUNBURST_5		= 11
MULLION_SUNBURST_6		= 12
MULLION_SOLID_GLAZED	= 13
MULLION_DIVIDED_FRAME	= 14
MULLION_SOLID_PANEL		= 15
MULLION_VENT_PANEL		= 16
MULLION_FRENCH_PANEL	= 17

! gs_kickboard_set_to_edit
KICKBOARD_IN_MAIN_PANEL		= 1
KICKBOARD_IN_SECOND_PANEL	= 2

dim stKickBoardSetToLeaf[2]
	stKickBoardSetToLeaf[1] = `Полотно Двери`
	stKickBoardSetToLeaf[2] = `Боковая Панель`


ac_unit_width = ac_unit_width + gs_unit_extra_left + gs_unit_extra_right

gs_slidingext	= 0
gs_pocket		= 0
gs_bifold		= 0
gs_sliding 		= 0

if	gs_leaf_optype_m = LEAF_1L_EXTERNAL_SLIDING | \
	gs_leaf_optype_m = LEAF_1L_EXTERNAL_SLIDING_M | \
	gs_leaf_optype_m = LEAF_2L_EXTERNAL_SLIDING then
		gs_slidingext	= 1
endif
if	gs_leaf_optype_m = LEAF_1L_POCKET | \
	gs_leaf_optype_m = LEAF_1L_POCKET_M | \
	gs_leaf_optype_m = LEAF_2L_POCKET then
		gs_pocket		= 1
endif
if	gs_leaf_optype_m = LEAF_1L_MULTIPANEL | \
	gs_leaf_optype_m = LEAF_1L_MULTIPANEL_M | \
	gs_leaf_optype_m = LEAF_2L_MULTIPANEL then
		gs_bifold		= 1
endif
if	gs_leaf_optype_m = LEAF_2L_1SLIDING | \
	gs_leaf_optype_m = LEAF_2L_1SLIDING_M | \
	gs_leaf_optype_m = LEAF_2L_2SLIDING | \
	gs_leaf_optype_m = LEAF_2L_2SLIDING_M | \
	gs_leaf_optype_m = LEAF_3L_2SLIDING | \
	gs_leaf_optype_m = LEAF_3L_2SLIDING_M | \
	gs_leaf_optype_m = LEAF_4L_2SLIDING | \
	gs_leaf_optype_m = LEAF_4L_3SLIDING | \
	gs_leaf_optype_m = LEAF_4L_3SLIDING_M | \
	gs_leaf_optype_m = LEAF_4L_4SLIDING | \
	gs_leaf_optype_m = LEAF_4L_4SLIDING_M then
		gs_sliding 		= 1
endif

if not(gs_pocket) then
	gs_pocket_door_frame	= 0
endif

if gs_sliding then
    SlidLeafNum = 1
    if	gs_leaf_optype_m = LEAF_2L_1SLIDING |\
	    gs_leaf_optype_m = LEAF_2L_1SLIDING_M |\
	    gs_leaf_optype_m = LEAF_2L_2SLIDING |\
	    gs_leaf_optype_m = LEAF_2L_2SLIDING_M then SlidLeafNum = 2
    if	gs_leaf_optype_m = LEAF_3L_2SLIDING |\
	    gs_leaf_optype_m = LEAF_3L_2SLIDING_M then SlidLeafNum = 3
    if	gs_leaf_optype_m = LEAF_4L_2SLIDING |\
	    gs_leaf_optype_m = LEAF_4L_3SLIDING |\
	    gs_leaf_optype_m = LEAF_4L_3SLIDING_M |\
	    gs_leaf_optype_m = LEAF_4L_4SLIDING |\
	    gs_leaf_optype_m = LEAF_4L_4SLIDING_M then SlidLeafNum = 4

    PanelElongOverlap = ((SlidLeafNum-1) / SlidLeafNum) * gs_leaf_overlap
    if	gs_leaf_optype_m = LEAF_4L_2SLIDING |\
	    gs_leaf_optype_m = LEAF_4L_4SLIDING |\
	    gs_leaf_optype_m = LEAF_4L_4SLIDING_M then PanelElongOverlap = gs_leaf_overlap/2
endif


! =============================================================================
! Uniform Sashes
! =============================================================================

iMullionStyleOriginal_02 = iMullionStyle_02
iMullionStyleOriginal_05 = iMullionStyle_05
iMullionStyleOriginal_06 = iMullionStyle_06
iMullionStyleOriginal_07 = iMullionStyle_07
iMullionStyleOriginal_09 = iMullionStyle_09
iMullionStyleOriginal_10 = iMullionStyle_10
iMullionStyleOriginal_11 = iMullionStyle_11

if gs_sash_uniform then

	! Changes made here must be transferred to the migration macros too!

	iMullionStyle_02 = iMullionStyle_00
	iMullionStyle_05 = iMullionStyle_00
	iMullionStyle_06 = iMullionStyle_00
	iMullionStyle_07 = iMullionStyle_00
	iMullionStyle_09 = iMullionStyle_00
	iMullionStyle_10 = iMullionStyle_00
	iMullionStyle_11 = iMullionStyle_00

	gs_cust_panel_02 = gs_cust_panel_00
	gs_cust_panel_05 = gs_cust_panel_00
	gs_cust_panel_06 = gs_cust_panel_00
	gs_cust_panel_07 = gs_cust_panel_00
	gs_cust_panel_09 = gs_cust_panel_00
	gs_cust_panel_10 = gs_cust_panel_00
	gs_cust_panel_11 = gs_cust_panel_00
	gs_cust_panel2_02 = gs_cust_panel_00
	gs_cust_panel2_05 = gs_cust_panel_00
	gs_cust_panel2_06 = gs_cust_panel_00
	gs_cust_panel2_07 = gs_cust_panel_00
	gs_cust_panel2_09 = gs_cust_panel_00
	gs_cust_panel2_10 = gs_cust_panel_00
	gs_cust_panel2_11 = gs_cust_panel_00

	vgn2_02 = vgn_02
	hgn2_02 = hgn_02

	if gs_sidelight_left then
		if (iMullionStyle_00 = MULLION_FRENCH_PANEL) then
			sashFrameWidth[1][5] = sashFrameWidth[1][9]
			sashFrameWidth[2][5] = sashFrameWidth[2][9]
			sashFrameWidth[3][5] = sashFrameWidth[3][9]
			sashFrameWidth[4][5] = sashFrameWidth[4][9]
		endif

		vgn_05 = vgn_09
		hgn_05 = hgn_09
		vgn2_05 = vgn2_09
		hgn2_05 = hgn2_09
		gs_horiz_div_height_05 = gs_horiz_div_height_09

		if gs_bSidelight_fr_hdiv_left then
			vgn_07 = vgn_11
			hgn_07 = hgn_11
			vgn2_07 = vgn2_11
			hgn2_07 = hgn2_11
			gs_horiz_div_height_07 = gs_horiz_div_height_11
		endif

		vgn_06 = vgn_09
		hgn_06 = hgn_02
		vgn2_06 = vgn2_09
		hgn2_06 = hgn2_02
		gs_horiz_div_height_06 = gs_horiz_div_height_02
		vgn_10 = vgn_09
		hgn_10 = hgn_02
		vgn2_10 = vgn2_09
		hgn2_10 = hgn2_02
		gs_horiz_div_height_10 = gs_horiz_div_height_02
	else
		vgn_06 = vgn_05
		hgn_06 = hgn_02
		vgn2_06 = vgn2_05
		hgn2_06 = hgn2_02
		gs_horiz_div_height_06 = gs_horiz_div_height_02
	endif
endif


! =============================================================================
! Frame Thickness
! =============================================================================

gs_frame_thk = gs_fthk

if gs_frame_wthk  | gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION then
	if gs_sframe then
		gs_frame_thkin = gs_frame_thk
	endif
else
	gs_frame_thkin = gs_frame_thk
endif


! iLeafShape
LEAF_SHAPE_RECTANGULAR		= 1
LEAF_SHAPE_ARCHED			= 2
LEAF_SHAPE_HALFARCHED		= 3

! iWindowShape:
SHAPE_RECTANGULAR			= 0
SHAPE_ARCHED				= 1
SHAPE_ROUND					= 2
SHAPE_EYEBROWS				= 3
SHAPE_HALFCIRCLE			= 4 	! straight side is horizontal
SHAPE_MULTISIDE				= 5
SHAPE_OCTAGONAL				= 6
SHAPE_HALFARCH				= 7
SHAPE_PENTAGON				= 8
SHAPE_QUARTERROUND			= 9
SHAPE_HALFROUND				= 10 	! straight side is vertical
SHAPE_TRAPEZOID				= 11
SHAPE_TRIANGLE				= 12
SHAPE_GOTHICARCH			= 13
SHAPE_ELLIPSEARCH			= 14
SHAPE_ROMBUS				= 15
SHAPE_HORSESHOE				= 16
SHAPE_OGEECENTERED			= 17
SHAPE_SARACENIC				= 18
SHAPE_T						= 19
SHAPE_HALF_T_LEFT			= 20
SHAPE_ARCHED_T				= 21
SHAPE_ARCHED_HALF_T_LEFT	= 22
SHAPE_PARALLELOGRAM			= 23
SHAPE_ELLIPSE				= 24
SHAPE_CORNER_TRIANGLE		= 25
SHAPE_QUARTERROUND_NOFRAME	= 26
SHAPE_HALF_T_RIGHT			= 27
SHAPE_ARCHED_HALF_T_RIGHT	= 28

! gs_opening_dir_sl_m:
OPENING_DIR_DEFAULT_OUTSIDE	= 1
OPENING_DIR_FLIPPED_INSIDE	= 2

! gs_SidelightTransomRelation_m:
SIDELIGHT_UNDER_TRANSOM		= 1
SIDELIGHT_OVER_TRANSOM		= 2
SIDELIGHT_CROSSES_TRANSOM	= 3

! gs_iLeavesConfig:
LEAVESCONFIG_UNIFORM	= 1
LEAVESCONFIG_ASYMM		= 2

! Frame Element Joinery/
MITERED_JOINT	= 1
BUTT_JOINT		= 2

! Edit Sash/gs_sash_set_to_edit:
SET_TRANSOM 	= 1
SET_SIDELIGHT_2	= 2
SET_SIDELIGHT_1	= 3
SET_TRANSOM_2	= 4
SET_TRANSOM_1	= 5


iLeafShape = LEAF_SHAPE_RECTANGULAR
if not(gs_door_transom) & bArchedTop then
	if	gs_leaf_optype_m = LEAF_1L_SIDE_HUNG |\
		gs_leaf_optype_m = LEAF_1L_SIDE_HUNG_M |\
		gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING |\
		gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING_M then

		iLeafShape = LEAF_SHAPE_ARCHED
	else
		iLeafShape = LEAF_SHAPE_HALFARCHED
	endif
endif

bFrameWOutRebate = (gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING |\
					gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING_M |\
					gs_leaf_optype_m = LEAF_2L_DOUBLE_ACTING |\
					gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS |\
					gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS_M |\
					gs_leaf_optype_m = LEAF_1L_PIVOT |\
					gs_leaf_optype_m = LEAF_1L_PIVOT_M |\
					gs_leaf_optype_m = LEAF_2L_1SLIDING |\
					gs_leaf_optype_m = LEAF_2L_1SLIDING_M |\
					gs_leaf_optype_m = LEAF_2L_2SLIDING |\
					gs_leaf_optype_m = LEAF_2L_2SLIDING_M |\
					gs_leaf_optype_m = LEAF_3L_2SLIDING |\
					gs_leaf_optype_m = LEAF_3L_2SLIDING_M |\
					gs_leaf_optype_m = LEAF_4L_2SLIDING |\
					gs_leaf_optype_m = LEAF_4L_3SLIDING |\
					gs_leaf_optype_m = LEAF_4L_3SLIDING_M |\
					gs_leaf_optype_m = LEAF_4L_4SLIDING |\
					gs_leaf_optype_m = LEAF_4L_4SLIDING_M |\
					gs_leaf_optype_m = LEAF_1L_EXTERNAL_SLIDING |\
					gs_leaf_optype_m = LEAF_1L_EXTERNAL_SLIDING_M |\
					gs_leaf_optype_m = LEAF_2L_EXTERNAL_SLIDING |\
					gs_leaf_optype_m = LEAF_1L_POCKET |\
					gs_leaf_optype_m = LEAF_1L_POCKET_M |\
					gs_leaf_optype_m = LEAF_2L_POCKET |\
					gs_leaf_optype_m = LEAF_1L_MULTIPANEL |\
					gs_leaf_optype_m = LEAF_1L_MULTIPANEL_M |\
					gs_leaf_optype_m = LEAF_2L_MULTIPANEL)

bForceToShowFrameStyle = (	gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING |\
							gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING_M |\
							gs_leaf_optype_m = LEAF_2L_DOUBLE_ACTING |\
							gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS |\
							gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS_M)


gs_rebate_width_Sliding = gs_rebate_width
if bFrameWOutRebate then gs_rebate_width_Sliding = 0



! Uniform frame width
if gs_uniform_framewidth then
	gs_frame_width_left 			= gs_frame_width
	gs_frame_width_left_2			= gs_frame_width
	gs_frame_width_low_left_sl		= gs_frame_width
	gs_frame_width_right			= gs_frame_width
	gs_frame_width_right_2			= gs_frame_width
	gs_frame_width_low_right_sl		= gs_frame_width
	gs_frame_width_transom			= gs_frame_width
	gs_frame_width_upper			= gs_frame_width
endif

if bArchedTop then
	gs_frame_width_left 			= gs_frame_width_right
	gs_frame_width_upper			= gs_frame_width_right
endif


if abs(gs_frame_width_left_2)<EPS then gs_frame_width_left_2 = gs_frame_width
if abs(gs_frame_width_right_2)<EPS then gs_frame_width_right_2 = gs_frame_width
if abs(gs_frame_width_low_left_sl)<EPS then gs_frame_width_low_left_sl = gs_frame_width
if abs(gs_frame_width_low_right_sl)<EPS then gs_frame_width_low_right_sl = gs_frame_width
if abs(gs_frame_width_transom)<EPS then gs_frame_width_transom = gs_frame_width

openingArchHeight = ac_wallhole_height - gs_shoulder_h


if gs_sidelight_left then
	gs_sidelight_WHole_width_left = gs_sidelight_width_left + gs_tolerance_left - gs_frame_width_left_2 - gs_tolerance_left_2
else
	gs_sidelight_WHole_width_left = 0
endif
if gs_sidelight_right then
	gs_sidelight_WHole_width_right = gs_sidelight_width_right + gs_tolerance_right - gs_frame_width_right_2 - gs_tolerance_right_2
else
	gs_sidelight_WHole_width_right = 0
endif

!********** middle stacking for multipanel folding doors




!********** different frame widths

gs_frame_width_orig = gs_frame_width

!********** to leaves configuration

is2Leaf =	(gs_leaf_optype_m = LEAF_2L_SIDE_HUNG		|\
			 gs_leaf_optype_m = LEAF_2L_DOUBLE_ACTING	|\
			 gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS	|\
			 gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS_M	)

bShowLeavesConfig = (is2Leaf & not(bArchedTop & not(gs_door_transom)))

!************** door leaf / leafs width and height

if gs_iLeavesConfig = LEAVESCONFIG_UNIFORM | not(bShowLeavesConfig) then
	lar = ac_leaf_width / 2
	lal = ac_leaf_width / 2
else
	lar = gs_SecondLeaf_w
	lal = ac_leaf_width - gs_SecondLeaf_w
endif


! =============================================================================
! Casement IDs
! =============================================================================

dim CasementTypeArray[3][3]
CasementTypeArray[1][1]	= 1
CasementTypeArray[1][2]	= 1
CasementTypeArray[1][3]	= 1
CasementTypeArray[2][1]	= 1
CasementTypeArray[2][2]	= 1
CasementTypeArray[2][3]	= 1
CasementTypeArray[3][1]	= 1
CasementTypeArray[3][2]	= 1
CasementTypeArray[3][3]	= 1

dim CasementShapeArray[3][3]
CasementShapeArray[1][1]	= 1
if bArchedTop then
	CasementShapeArray[1][2]	= 2	! SASH_SHAPE_ARCHED
else
	CasementShapeArray[1][2]	= 1
endif
CasementShapeArray[1][3]	= 1
CasementShapeArray[2][1]	= 1
CasementShapeArray[2][2]	= 1
CasementShapeArray[2][3]	= 1
CasementShapeArray[3][1]	= 1
CasementShapeArray[3][2]	= 1
CasementShapeArray[3][3]	= 1


!************************* rebate width

bOpeningDirectionSL	= (gs_opening_dir_sl_m = OPENING_DIR_FLIPPED_INSIDE)


! =============================================================================
! Sidelight-Transom Relation handling
! =============================================================================
if gs_SidelightTransomRelation_m = SIDELIGHT_UNDER_TRANSOM then
	gs_LeftMullion_Top		= 0
	gs_RightMullion_Top		= 0
	gs_Transom_toLeft		= 1
	gs_Transom_toRight		= 1
endif
if gs_SidelightTransomRelation_m = SIDELIGHT_OVER_TRANSOM then
	gs_LeftMullion_Top		= 1
	gs_RightMullion_Top		= 1
	gs_Transom_toLeft		= 0
	gs_Transom_toRight		= 0
endif
if gs_SidelightTransomRelation_m = SIDELIGHT_CROSSES_TRANSOM then
	gs_LeftMullion_Top		= 1
	gs_RightMullion_Top		= 1
	gs_Transom_toLeft		= 1
	gs_Transom_toRight		= 1
endif


simple_sash_width = gs_sash_width-gs_sidelight_rebate_width

!! CURVED TOP CALCULATION !!
if bArchedTop then
	ace = ac_wallhole_width / 2
	bce = max(0.005, ac_wallhole_height - gs_shoulder_h)

	if bce > EPS then
		r2=(ace^2+bce^2)/(2*bce)
		if abs(ace/r2) > (1 - EPS) then r2=r2+0.0000001

		fce				= sqr(r2^2-ace^2)
		fcefw			= sqr((r2-gs_frame_width_left-gs_tolerance_left)^2-(ace-gs_frame_width_left-gs_tolerance_left)^2)
		fce_Sl_rebate	= sqr((r2-gs_frame_width_left-gs_tolerance_left+gs_sidelight_rebate_width)^2-(ace-gs_frame_width_left-gs_tolerance_left+gs_sidelight_rebate_width)^2)
		fce_simple		= sqr((r2-gs_frame_width_left-gs_tolerance_left-simple_sash_width)^2-(ace-gs_frame_width_left-gs_tolerance_left-simple_sash_width)^2)
		fce_leaf_rebate	= sqr((r2-gs_frame_width_left-gs_tolerance_left+gs_rebate_width)^2-(ace-gs_frame_width_left-gs_tolerance_left+gs_rebate_width)^2)
	else
		bArchedTop = 0
	endif
endif
!! END OF CURVED TOP CALCULATION !!


! =============================================================================
! Sash Sizes
! =============================================================================

dim sashWidthArray[3][3]
dim sashHeightArray[3][3]
dim sashPosXArray[3][3]
dim sashPosYArray[3][3]
for i=1 to 3
	for j=1 to 3
		sashWidthArray[i][j]	= 0
		sashHeightArray[i][j]	= 0
		sashPosXArray[i][j]		= 0
		sashPosYArray[i][j]		= 0
	next j
next i

! Transom Sashes ==============================================================

cavityHeight = gs_transom_height - gs_frame_width_transom - gs_frame_width_upper + 2*gs_sidelight_rebate_width
cavityBottom = ac_unit_height - gs_transom_height + gs_frame_width_transom - gs_sidelight_rebate_width

! Upper Left Sash -------------------------------------------------------------

if gs_door_transom & gs_sidelight_left & (gs_SidelightTransomRelation_m = SIDELIGHT_CROSSES_TRANSOM) then
	cavityLeft	= -ac_unit_width/2 + gs_frame_width_left - gs_sidelight_rebate_width
	cavityRight	= -ac_unit_width/2 + gs_sidelight_width_left - gs_frame_width_left_2 + gs_sidelight_rebate_width

	sashWidthArray[1][1]	= cavityRight - cavityLeft
	sashHeightArray[1][1]	= cavityHeight
	sashPosXArray[1][1]		= cavityLeft + (cavityRight - cavityLeft)/2
	sashPosYArray[1][1]		= cavityBottom
endif

! Upper Middle Sash -----------------------------------------------------------

if gs_door_transom then
	if gs_sidelight_left &\
	((gs_SidelightTransomRelation_m = SIDELIGHT_CROSSES_TRANSOM) |\
	 (gs_SidelightTransomRelation_m = SIDELIGHT_OVER_TRANSOM)) then
		cavityLeft	= -ac_unit_width/2 + gs_sidelight_width_left - gs_sidelight_rebate_width
	else
		cavityLeft	= -ac_unit_width/2 + gs_frame_width_left - gs_sidelight_rebate_width
	endif
	if gs_sidelight_right &\
	((gs_SidelightTransomRelation_m = SIDELIGHT_CROSSES_TRANSOM) |\
	 (gs_SidelightTransomRelation_m = SIDELIGHT_OVER_TRANSOM)) then
		cavityRight	=  ac_unit_width/2 - gs_sidelight_width_right + gs_sidelight_rebate_width
	else
		cavityRight	=  ac_unit_width/2 - gs_frame_width_right + gs_sidelight_rebate_width
	endif

	sashWidthArray[1][2]	= cavityRight - cavityLeft
	sashHeightArray[1][2]	= cavityHeight
	sashPosXArray[1][2]		= cavityLeft + (cavityRight - cavityLeft)/2
	sashPosYArray[1][2]		= cavityBottom
endif

! Upper Right Sash ------------------------------------------------------------

if gs_door_transom & gs_sidelight_right & (gs_SidelightTransomRelation_m = SIDELIGHT_CROSSES_TRANSOM) then
	cavityLeft	= ac_unit_width/2 - gs_sidelight_width_right + gs_frame_width_right_2 - gs_sidelight_rebate_width
	cavityRight	= ac_unit_width/2 - gs_frame_width_right + gs_sidelight_rebate_width

	sashWidthArray[1][3]	= cavityRight - cavityLeft
	sashHeightArray[1][3]	= cavityHeight
	sashPosXArray[1][3]		= cavityRight - (cavityRight - cavityLeft)/2
	sashPosYArray[1][3]		= cavityBottom
endif


! Sidelight Sashes ============================================================

if gs_door_transom &\
	((gs_SidelightTransomRelation_m = SIDELIGHT_CROSSES_TRANSOM) |\
	 (gs_SidelightTransomRelation_m = SIDELIGHT_UNDER_TRANSOM)) then
	cavityTop = ac_unit_height - gs_transom_height + gs_sidelight_rebate_width
else
	cavityTop = ac_unit_height - gs_frame_width_upper + gs_sidelight_rebate_width
endif

! Left Sidelight Sash ---------------------------------------------------------

if gs_sidelight_left then
	if gs_bSidelight_fr_hdiv_left then
		cavityBottom = gs_sidelight_fr_hdiv_hgt_left + gs_sidelight_fr_hdiv_wdt_left - gs_sidelight_rebate_width
	else
		cavityBottom = gs_sidelight_parapet_hgt_left + gs_frame_width_low_left_sl + (gs_sidelight_parapet_hgt_left > EPS) * gs_tolerance_low_left_sl - gs_sidelight_rebate_width
	endif
	cavityLeft	= -ac_unit_width/2 + gs_frame_width_left - gs_sidelight_rebate_width
	cavityRight	= -ac_unit_width/2 + gs_sidelight_width_left - gs_frame_width_left_2 + gs_sidelight_rebate_width

	sashWidthArray[2][1]	= cavityRight - cavityLeft
	sashHeightArray[2][1]	= cavityTop - cavityBottom
	sashPosXArray[2][1]		= cavityLeft + (cavityRight - cavityLeft)/2
	sashPosYArray[2][1]		= cavityBottom
endif

! Left Sidelight Lower Sash ---------------------------------------------------------

if gs_sidelight_left & gs_bSidelight_fr_hdiv_left then
	cavityLowTop = gs_sidelight_fr_hdiv_hgt_left + gs_sidelight_rebate_width
	cavityLowBottom = gs_sidelight_parapet_hgt_left + gs_frame_width_low_left_sl + (gs_sidelight_parapet_hgt_left > EPS) * gs_tolerance_low_left_sl - gs_sidelight_rebate_width

	sashWidthArray[3][1]	= cavityRight - cavityLeft
	sashHeightArray[3][1]	= cavityLowTop - cavityLowBottom
	sashPosXArray[3][1]		= cavityLeft + (cavityRight - cavityLeft)/2
	sashPosYArray[3][1]		= cavityLowBottom
endif


! Right Sidelight Sash --------------------------------------------------------

if gs_sidelight_right then
	if gs_bSidelight_fr_hdiv_right then
		cavityBottom = gs_sidelight_fr_hdiv_hgt_right + gs_sidelight_fr_hdiv_wdt_right - gs_sidelight_rebate_width
	else
		cavityBottom = gs_sidelight_parapet_hgt_right + gs_frame_width_low_right_sl + (gs_sidelight_parapet_hgt_right > EPS) * gs_tolerance_low_right_sl - gs_sidelight_rebate_width
	endif
	cavityLeft	= ac_unit_width/2 - gs_sidelight_width_right + gs_frame_width_right_2 - gs_sidelight_rebate_width
	cavityRight	= ac_unit_width/2 - gs_frame_width_right + gs_sidelight_rebate_width

	sashWidthArray[2][3]	= cavityRight - cavityLeft
	sashHeightArray[2][3]	= cavityTop - cavityBottom
	sashPosXArray[2][3]		= cavityRight - (cavityRight - cavityLeft)/2
	sashPosYArray[2][3]		= cavityBottom
endif

! Right Sidelight Lower Sash ---------------------------------------------------------

if gs_sidelight_right & gs_bSidelight_fr_hdiv_right then
	cavityLowTop = gs_sidelight_fr_hdiv_hgt_right + gs_sidelight_rebate_width
	cavityLowBottom = gs_sidelight_parapet_hgt_right + gs_frame_width_low_right_sl + (gs_sidelight_parapet_hgt_right > EPS) * gs_tolerance_low_right_sl - gs_sidelight_rebate_width

	sashWidthArray[3][3]	= cavityRight - cavityLeft
	sashHeightArray[3][3]	= cavityLowTop - cavityLowBottom
	sashPosXArray[3][3]		= cavityLeft + (cavityRight - cavityLeft)/2
	sashPosYArray[3][3]		= cavityLowBottom
endif


! =============================================================================
! Ventilation Profile
! =============================================================================

if not(bEnableVentilationProfile & bVentilationProfile & (iVentilationProfileType = VENTPROFILE_OVER_GLAZING)) then
	isVentilationProfileInSash_01	= 0
	isVentilationProfileInSash_02	= 0
	isVentilationProfileInSash_03	= 0
	isVentilationProfileInSash_04	= 0
	isVentilationProfileInSash_05	= 0
	isVentilationProfileInSash_06	= 0
	isVentilationProfileInSash_07	= 0
	isVentilationProfileInSash_08	= 0
	isVentilationProfileInSash_09	= 0
	isVentilationProfileInSash_10	= 0
	isVentilationProfileInSash_11	= 0
endif


! =============================================================================
! Enable Kick Board
! =============================================================================

bEnableKickBoard	= not(bBombShelter)
bSecondKickBoard	= bEnableKickBoard & bAsymmetricLeafs


! =============================================================================
! Parameters locking
! =============================================================================

bUncutFillEnable = (iDoorPanelType = 15 |\
					iDoorPanelType = 17 |\
					iDoorPanelType = 18 |\
					iDoorPanelType = 19 |\
					iDoorPanelType = 20 |\
					iDoorPanelType = 21 |\
					iDoorPanelType = 35 |\
					iDoorPanelType = 39 |\
					iDoorPanelType = 40 |\
					iDoorPanelType = 45 |\
					iDoorPanelType = 46 |\
					iDoorPanelType = 47 |\
					iDoorPanelType = 48 |\
					iDoorPanelType = 53 |\
					iDoorPanelType = 54 |\
					iDoorPanelType = 64 |\
					iDoorPanelType = 75 |\
					iDoorPanelType = 36 |\
					iDoorPanelType = 37 |\
					iDoorPanelType = 38 |\
					iDoorPanelType = 39)

bUncutParametersLock = not(gs_sidelight_left) & not(gs_sidelight_right) & not(bUncutFillEnable)
bGlass2DParametersLock = (lod2D_Glazing = 2) | not(bUncutFillEnable)	! TODO B-276
bGlass3DParametersLock =   (iDoorPanelType = 0 |\
							iDoorPanelType = 1 |\
							iDoorPanelType = 5 |\
							iDoorPanelType = 6 |\
							iDoorPanelType = 8 |\
							iDoorPanelType = 7 |\
							iDoorPanelType = 9 |\
							iDoorPanelType = 10 |\
							iDoorPanelType = 11 |\
							iDoorPanelType = 12 |\
							iDoorPanelType = 41 |\
							iDoorPanelType = 42 |\
							iDoorPanelType = 43 |\
							iDoorPanelType = 44 |\
							iDoorPanelType = 49 |\
							iDoorPanelType = 50 |\
							iDoorPanelType = 57 |\
							iDoorPanelType = 58 |\
							iDoorPanelType = 59 |\
							iDoorPanelType = 60 |\
							iDoorPanelType = 69 |\
							iDoorPanelType = 73 |\
							iDoorPanelType = 74) & not(gs_door_transom)

if gs_sash_uniform then
	_bOneFrenchSidelightSashOnLeft = not(gs_bSidelight_fr_hdiv_left) & (iMullionStyle_00 = MULLION_FRENCH_PANEL)
	_bOneFrenchSidelightSashOnRight = not(gs_bSidelight_fr_hdiv_right) & (iMullionStyle_00 = MULLION_FRENCH_PANEL)
else
	_bOneFrenchSidelightSashOnLeft = not(gs_bSidelight_fr_hdiv_left) & (iMullionStyle_09 = MULLION_FRENCH_PANEL)
	_bOneFrenchSidelightSashOnRight = not(gs_bSidelight_fr_hdiv_right) & (iMullionStyle_05 = MULLION_FRENCH_PANEL)
endif

bDisableSidelightSashWidth = ((gs_sidelight_left & _bOneFrenchSidelightSashOnLeft & not(gs_sidelight_right)) exor\
							  (not(gs_sidelight_left) & gs_sidelight_right & _bOneFrenchSidelightSashOnRight)) |\
							 (gs_sidelight_left & _bOneFrenchSidelightSashOnLeft & gs_sidelight_right & _bOneFrenchSidelightSashOnRight)
