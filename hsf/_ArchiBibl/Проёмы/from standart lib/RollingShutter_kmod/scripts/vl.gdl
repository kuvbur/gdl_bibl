
! -----------------------------------------------------------------------------
! Placement
! -----------------------------------------------------------------------------

dim enabledRollingConst[]
dim iEnabledRollingconst[]
nEnabledTypes = 0

for i = 1 to vardim1(stShutRollPlacement)
	if bittest (gs_shutRoll_placementEnable, i-1) then
		nEnabledTypes = nEnabledTypes + 1
		enabledRollingConst[nEnabledTypes] = stShutRollPlacement[i]
		iEnabledRollingconst[nEnabledTypes] = i
	endif
next i

values "gs_shutRoll_placement" enabledRollingConst
values "gs_shutRoll_iPlacement" iEnabledRollingconst

if GLOB_MODPAR_NAME = "gs_shutRoll_placement" then
	gs_shutRoll_iPlacement = SHUTROLL_ONFACADE
	if gs_shutRoll_placement = stShutRollPlacement[2] then gs_shutRoll_iPlacement = SHUTROLL_TOWINDOW
	if gs_shutRoll_placement = stShutRollPlacement[3] then gs_shutRoll_iPlacement = SHUTROLL_BEHIND
	parameters gs_shutRoll_iPlacement = gs_shutRoll_iPlacement
else
	parameters gs_shutRoll_placement = stShutRollPlacement[gs_shutRoll_iPlacement]
endif

! -----------------------------------------------------------------------------
! Defaults by Placement
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "gs_shutRoll_placement" | GLOB_MODPAR_NAME = "gs_shutRoll_iPlacement" then
	if gs_shutRoll_iPlacement = SHUTROLL_ONFACADE then
		gs_shutRoll_distFromHead = 0
		parameters gs_shutRoll_distFromHead = gs_shutRoll_distFromHead
	endif
	if gs_shutRoll_iPlacement = SHUTROLL_TOWINDOW then
		gs_shutRoll_distFromHead = -gs_shutRoll_boxHeight
		parameters gs_shutRoll_distFromHead = gs_shutRoll_distFromHead
	endif
	if gs_shutRoll_iPlacement = SHUTROLL_BEHIND then
		gs_shutRoll_distFromHead = gs_reveal_top
		parameters gs_shutRoll_distFromHead = gs_shutRoll_distFromHead

		if WIDO_SILL < railWidth then
			gs_shutRoll_railDepth =  min(WALL_THICKNESS - gs_shutRoll_boxWidth, WIDO_SILL + WIDO_FRAME_THICKNESS * 2)
		else
			gs_shutRoll_railDepth = min(WALL_THICKNESS - gs_shutRoll_boxWidth, WIDO_SILL - railWidth)
		endif
		parameters gs_shutRoll_railDepth = gs_shutRoll_railDepth
	endif
endif


! -----------------------------------------------------------------------------
! Box parameters
! -----------------------------------------------------------------------------

if gs_shutRoll_iPlacement = SHUTROLL_ONFACADE | \
	gs_shutRoll_iPlacement = SHUTROLL_TOWINDOW then

	values "gs_shutRoll_boxType" stShutRollBoxType
	values "gs_shutRoll_iBoxType" SHUTROLL_BOX_RECT, SHUTROLL_BOX_ARCHED

	hideparameter	"gs_shutRoll_boxOffsetLeft",
					"gs_shutRoll_boxOffsetRight",
					"gs_shutRoll_bIncludeBoxHeight"
	lock	"gs_shutRoll_boxOffsetLeft",
			"gs_shutRoll_boxOffsetRight",
			"gs_shutRoll_bIncludeBoxHeight"			
else
	values "gs_shutRoll_boxType" stShutRollBoxType[1]
	values "gs_shutRoll_iBoxType" SHUTROLL_BOX_RECT
	gs_shutRoll_iBoxType = SHUTROLL_BOX_RECT
	gs_shutRoll_boxType = stShutRollBoxType[1]

	hideparameter "gs_shutRoll_boxType"
	lock "gs_shutRoll_boxType", "gs_shutRoll_iBoxType"

	values "gs_shutRoll_boxOffsetLeft" range [0,)
	values "gs_shutRoll_boxOffsetRight" range [0,)
endif

if GLOB_MODPAR_NAME = "gs_shutRoll_boxType" then
	gs_shutRoll_iBoxType = 1
	if gs_shutRoll_boxType = stShutRollBoxType[2] then gs_shutRoll_iBoxType = 2
	parameters gs_shutRoll_iBoxType = gs_shutRoll_iBoxType
else
	gs_shutRoll_boxType = stShutRollBoxType[gs_shutRoll_iBoxType]
	parameters gs_shutRoll_boxType = gs_shutRoll_boxType
endif

if gs_shutRoll_iBoxType = SHUTROLL_BOX_ARCHED then
	values "gs_shutRoll_boxWidth" range [gs_shutRoll_boxHeight/2 + railThk, )
	values "gs_shutRoll_boxHeight" range (0, 2 * (gs_shutRoll_boxWidth - railThk))
else
	values "gs_shutRoll_boxHeight" range (0, )
	values "gs_shutRoll_boxWidth" range (0, )
endif
values "gs_shutRoll_segmentWidth" range[0.01,)

values "gs_shutter_opHeight" range [0, sunShadeHeight3D + gs_shutRoll_distFromHead]
values "gs_shutter_closedHeight" range [0, sunShadeHeight3D + gs_shutRoll_distFromHead]

values "gs_shutRoll_boxDefaultSize" stShutRollBoxDefaultSize
values "gs_shutRoll_iBoxDefaultSize" SHUTROLL_BOXSIZE_EXT, SHUTROLL_BOXSIZE_INT

if GLOB_MODPAR_NAME = "gs_shutRoll_boxDefaultSize" then
	gs_shutRoll_iBoxDefaultSize =  SHUTROLL_BOXSIZE_EXT
	if gs_shutRoll_boxDefaultSize = stShutRollBoxDefaultSize[2] then gs_shutRoll_iBoxDefaultSize =  SHUTROLL_BOXSIZE_INT
	parameters gs_shutRoll_iBoxDefaultSize = gs_shutRoll_iBoxDefaultSize
else
	gs_shutRoll_boxDefaultSize = stShutRollBoxDefaultSize[1]
	if gs_shutRoll_iBoxDefaultSize =  SHUTROLL_BOXSIZE_INT then gs_shutRoll_boxDefaultSize = stShutRollBoxDefaultSize[2]
	parameters gs_shutRoll_boxDefaultSize = gs_shutRoll_boxDefaultSize
endif

! -----------------------------------------------------------------------------
! Box Position Control
! -----------------------------------------------------------------------------

if gs_shutRoll_iPlacement = SHUTROLL_ONFACADE then
	values "gs_shutRoll_distFromWall" range [0,)
endif

! -----------------------------------------------------------------------------
! Control opening height
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "gs_shutter_opHeight" then
	gs_shutter_closedHeight = sunShadeHeight3D + gs_shutRoll_distFromHead - gs_shutter_opHeight
	parameters gs_shutter_closedHeight = gs_shutter_closedHeight
else
	gs_shutter_opHeight = max (0, sunShadeHeight3D + gs_shutRoll_distFromHead - gs_shutter_closedHeight)
	parameters gs_shutter_opHeight = gs_shutter_opHeight
endif

! -----------------------------------------------------------------------------
! Rail Position
! -----------------------------------------------------------------------------

values "gs_shutRoll_railPos" stShutRollRailPos
values "gs_shutRoll_iRailPos" SHUTROLL_ONCASING, SHUTROLL_ONWALL

if GLOB_MODPAR_NAME = "gs_shutRoll_railPos" then
	gs_shutRoll_iRailPos =  SHUTROLL_ONCASING
	if gs_shutRoll_railPos = stShutRollRailPos[2] then gs_shutRoll_iRailPos =  SHUTROLL_ONWALL
	parameters gs_shutRoll_iRailPos = gs_shutRoll_iRailPos
else
	gs_shutRoll_railPos = stShutRollRailPos[1]
	if gs_shutRoll_iRailPos =  SHUTROLL_ONWALL then gs_shutRoll_railPos = stShutRollRailPos[2]
	parameters gs_shutRoll_railPos = gs_shutRoll_railPos
endif

! -----------------------------------------------------------------------------
! Hiding unused parameters
! -----------------------------------------------------------------------------

if gs_shutRoll_iPlacement = SHUTROLL_ONFACADE then
	lock "gs_shutRoll_cutWallBehind", "gs_shutRoll_railPos", "gs_shutRoll_iRailPos", "gs_shutRoll_showCasingBehind"
	hideparameter "gs_shutRoll_cutWallBehind", "gs_shutRoll_railPos", "gs_shutRoll_iRailPos", "gs_shutRoll_showCasingBehind"
else
	lock "gs_shutRoll_boxAngle",
			"gs_shutRoll_distFromWall"
	hideparameter "gs_shutRoll_boxAngle",
			"gs_shutRoll_distFromWall"
endif

if gs_shutRoll_iPlacement <> SHUTROLL_BEHIND then
	lock "gs_shutRoll_railDepth"
	hideparameter "gs_shutRoll_railDepth"
endif

if not((gs_shutRoll_iPlacement = SHUTROLL_BEHIND) & gs_shutRoll_cutWallBehind) then
	lock	"gs_sunShade_insFillType",
			"gs_sunShade_insFillPen",
			"gs_sunShade_insFillBackPen"

	hideparameter	"gs_sunShade_insFillType",
					"gs_sunShade_insFillPen",
					"gs_sunShade_insFillBackPen"
endif
