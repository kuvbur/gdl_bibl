call "Проём в стене" parameters all, WALL_COMPS_NAME_ = WALL_COMPS_NAME,WALL_SKINS_NUMBER_ = WALL_SKINS_NUMBER, WALL_SKINS_PARAMS_ = WALL_SKINS_PARAMS, WALL_THICKNESS_ = WALL_THICKNESS 

dim arr_t[]
!values "nazn" "АС", "ОВ", "ВК", "ТХ", "ЭЛ", "ЭМ","ТС",CUSTOM
parameters krugl = 1
parameters otdel_otk = 0

dim perem_qty_type[4]
for i=1 to 4
	perem_qty_type[i] = 0
next i
parameters perem_qty_type = perem_qty_type
parameters use_bst = 0
LOCK "perem_qty_type", "use_bst"

dim type_sal_arr[] : i=0
dim sort_sal_arr[]
dim gost_sal_arr[]
i=i+1 : type_sal_arr[i] = "Сальник нажимной" : sort_sal_arr[i] = "Сальники нажимные.txt" : gost_sal_arr[i] = "Серия 5.900-3" 
i=i+1 : type_sal_arr[i] = "Сальник набивной" : sort_sal_arr[i] = "Сальники набивные.txt" : gost_sal_arr[i] = "Серия 5.900-2" 
i=i+1 : type_sal_arr[i] = "Гильза" : sort_sal_arr[i] = "Гильзы_10704_91.txt" : gost_sal_arr[i] = "ГОСТ 10704-91" 

values "type_sal" type_sal_arr
var = type_sal : arr_t = type_sal_arr : gosub "get_inx"
if inx>0 then fname = sort_sal_arr[inx]
if inx>0 then parameters obozn = gost_sal_arr[inx]

dim dy_arr[] : dy_arr[1]=0
dim l_arr[] : l_arr[1]=0
dy=0 : l=0 : v=0 : obozn_p = "" : dn=0 : d2=0 : d5=0
ch1 = open("text", fname, "separator='\t', mode=ro, LIBRARY")
rw = 2
1:
n = input (ch1, rw, 1, dy, l, v, obozn_p, dn, d2, d5)
if n <> -1 then
	dy = dy/1000
	var = dy : arr_t = dy_arr : gosub "get_inx"
	if inx = 0 then
		if dy_arr[1]=0 then
			end_row = 1
		else
			end_row = vardim1(dy_arr)+1
		endif
		dy_arr[end_row] = dy
	endif

	l = l/1000
	var = l : arr_t = l_arr : gosub "get_inx"
	if inx = 0 then
		if l_arr[1]=0 then
			end_row = 1
		else
			end_row = vardim1(l_arr)+1
		endif
		l_arr[end_row] = l
	endif
	rw = rw + 1
	goto 1
else
	goto "close"
endif
"close":
close ch1

values "dys" dy_arr

if type_sal <> "Гильза" then
	if auto_dlin then
		dim razn_arr[]
		min_razn = 100
		for i=1 to vardim1(l_arr)
			razn=l_arr[i]-max_t
			if razn>=0 then
				min_razn = min(min_razn, razn)
				razn_arr[i] = razn
			endif
		next i
		var = min_razn : arr_t = razn_arr : gosub "get_inx"
		if inx>0 then parameters dlin_sal = l_arr[inx]
	else
		values "dlin_sal" l_arr
	endif
	HIDEPARAMETER "tst"
else
	if auto_dlin then parameters dlin_sal = max_t
	values "tst" l_arr
endif

dy=0 : l=0 : v=0 : obozn_p = "" : dn=0 : d2=0 : d5=0
ch2 = open("text", fname, "separator='\t', mode=ro, LIBRARY")
rw = 2
2:
n = input (ch1, rw, 1, dy, l, v, obozn_p, dn, d2, d5)
if n <> -1 then
	dy = dy/1000
	l = l/1000
	if type_sal <> "Гильза" then
		lt = dlin_sal
	else
		lt = tst
	endif
	if abs(dy-dys)<EPS and abs(lt-l)<EPS then
		flag = 0
		if vartype(v)=2 then flag = 1
		if vartype(d2)=2 then flag = 1
		if vartype(d5)=2 then flag = 1
		if vartype(obozn_p)=1 then flag = 1
		if flag then
			parameters naen = "ОШИБКА ЧТЕНИЯ"
			parameters ves = 0
		else
			if type_sal <> "Гильза" then
				parameters naen = "Сальник " + obozn_p + ", Ду" + str("%.0mm",dys)
				parameters ves = v
				parameters dns = dn/1000
			else
				parameters naen = "Труба " + obozn_p + " L=" + str("%.0mm",dlin_sal)
				parameters ves = v * dlin_sal
				parameters dns = dn/1000
				parameters dns = dys-tst*2
			endif
			parameters d2s = d2/1000
			parameters d5s = d5/1000
		endif
	endif
	rw = rw + 1
	goto 2
else
	goto "close2"
endif
"close2":
close ch2

!if type_sal <> "Гильза" then
!	parameters A = d5s+5/1000
!else
!	parameters A = dys
!endif
!LOCK "A"

!values "A" dy_arr
!values "B" dy_arr

dim param_name_out[] : i=0
i=i+1 : param_name_out[i] = "id - ID элемента"
i=i+1 : param_name_out[i] = "dys - Условный проход"
i=i+1 : param_name_out[i] = "otm_niz - Отметка оси"

parameters param_name_out = param_name_out
end
"get_inx":
	inx = 0
	for i=1 to vardim1(arr_t)
		if var = arr_t[i] then inx = i
	next i
return