!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

MATERIAL sMat
PEN gs_cont_pen
RESOL res
IF gs_detlevel_3D_m = 1 THEN 
	SHADOW OFF
else
	SHADOW ON
endif
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	addStatus = 3
ENDIF


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


unID = 1
HOTSPOT	A/2, 0, 0, unID: unID=unID+1
HOTSPOT	-A/2, 0, 0, unID: unID=unID+1
HOTSPOT	0, A/2, 0, unID: unID=unID+1
HOTSPOT	0, -A/2, 0, unID: unID=unID+1
HOTSPOT	0, 0, -ZZYZX, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN
	db=INT((2*B/3)/0.015)
	mar=2*B/3-db*0.015

	FOR I=1 TO db STEP 2
	ADDy -B/3+I*0.015+mar/2-0.015/2
		PLANE 4,
			-A/3+eps+0.000*(I=2 OR I=db-1), -0.015/2, -eps,
			-A/3+eps+0.000*(I=2 OR I=db-1), 0.015/2, -eps,
			A/3-eps-0.000*(I=2 OR I=db-1), 0.015/2, -eps,
			A/3-eps-0.000*(I=2 OR I=db-1), -0.015/2, -eps
	DEL 1
	NEXT I


	PRISM_ 14, -edgeThick,
				A/2,            0,      15+64,
				   0, 0,    900,
				   0,          180,   4001,
			   -A/2, 0,      15+64,
				   0, 0,    900,
				   0,          180,   4001,
				A/2,            0,     -1,
				A/3,            0,      15+64,
				   0, 0,    900,
				   0,          180,   4001,
			   -A/3, 0,      15+64,
				   0, 0,    900,
				   0,          180,   4001,
				A/3,            0,     -1

	ADDz -edgeThick
		EXTRUDE 7, 0, 0, -(ZZYZX-edgeThick)/2, addStatus+16+32,
				A/2-edgeThick,            0,      1,
				   0, 0,    900,
				   0,          180,   4001,
			   -A/2+edgeThick, 0,      1,
				   0, 0,    900,
				   0,          180,   4001,
				A/2-edgeThick,            0,     -1
	DEL 1

	ADDz -edgeThick-2*(ZZYZX-edgeThick)/2
		CONE (ZZYZX-edgeThick)/2,  ConWidth_1/2, A/2-edgeThick, 90, 90
	DEL 1
ELSE
	PRISM_ 7, -edgeThick,
				A/2,            0,      15+64,
				   0, 0,    900,
				   0,          180,   4001,
			   -A/2, 0,      15+64,
				   0, 0,    900,
				   0,          180,   4001,
				A/2,            0,     -1


	ADDz -edgeThick
		EXTRUDE 7, 0, 0, -(ZZYZX-edgeThick), addStatus+16+32,
				ConWidth_1/2,            0,      1,
				   0, 0,    900,
				   0,          180,   4001,
			   -ConWidth_1/2, 0,      1,
				   0, 0,    900,
				   0,          180,   4001,
				ConWidth_1/2,            0,     -1
	DEL 1
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, MEP_StrConnectionData = MEP_StrConnectionData

DEL TOP

END
