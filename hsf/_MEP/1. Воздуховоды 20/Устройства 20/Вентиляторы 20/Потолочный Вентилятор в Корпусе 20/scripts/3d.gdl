
! Cabinet Ceiling Fan


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	addStatus = 1
ENDIF


				! --- MAIN BOX --- !

unID = 1
HOTSPOT prismHalfWidth, prismHalfDepth, 0: unID=unID+1
HOTSPOT -prismHalfWidth, prismHalfDepth, 0: unID=unID+1
HOTSPOT -prismHalfWidth, -prismHalfDepth, 0: unID=unID+1
HOTSPOT prismHalfWidth, -prismHalfDepth, 0: unID=unID+1
HOTSPOT prismInsideHalfWidth, prismInsideHalfDepth, frameThick: unID=unID+1
HOTSPOT -prismInsideHalfWidth, prismInsideHalfDepth, frameThick: unID=unID+1
HOTSPOT -prismInsideHalfWidth, -prismInsideHalfDepth, frameThick: unID=unID+1
HOTSPOT prismInsideHalfWidth, -prismInsideHalfDepth, frameThick: unID=unID+1
HOTSPOT prismInsideHalfWidth, prismInsideHalfDepth, ZZYZX: unID=unID+1
HOTSPOT -prismInsideHalfWidth, prismInsideHalfDepth, ZZYZX: unID=unID+1
HOTSPOT -prismInsideHalfWidth, -prismInsideHalfDepth, ZZYZX: unID=unID+1
HOTSPOT prismInsideHalfWidth, -prismInsideHalfDepth, ZZYZX: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN
	PRISM_ 10, frameThick,
		prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, prismHalfDepth, -1,
		prismInsideHalfWidth, prismInsideHalfDepth, 10,
		-prismInsideHalfWidth, prismInsideHalfDepth, 10,
		-prismInsideHalfWidth, -prismInsideHalfDepth, 10,
		prismInsideHalfWidth, -prismInsideHalfDepth, 10,
		prismInsideHalfWidth, prismInsideHalfDepth, -1

	ADDz frameThick
	EXTRUDE 5, 0, 0, dTHeight, addStatus+2+16+32,
			prismInsideHalfWidth, prismInsideHalfDepth, 0,
			-prismInsideHalfWidth, prismInsideHalfDepth, 0,
			-prismInsideHalfWidth, -prismInsideHalfDepth, 0,
			prismInsideHalfWidth, -prismInsideHalfDepth, 0,
			prismInsideHalfWidth, prismInsideHalfDepth, -1
	DEL 1

	 insideSize = grillWidth/4
	 holeNum = MAX(INT((grillWidth-insideSize)/0.035), 1)
	 grillThick = (grillWidth-insideSize)/(holeNum*2)
	 increment = grillThick*2
	 insideHalfSizeW = prismInsideHalfWidth-(grillWidth-insideSize)
	 insideHalfSizeD = prismInsideHalfDepth-(grillWidth-insideSize)

	PUT prismInsideHalfWidth, 	 prismInsideHalfDepth, 	0
	PUT -prismInsideHalfWidth,	 prismInsideHalfDepth, 	0
	PUT -prismInsideHalfWidth,	 -prismInsideHalfDepth, 0
	PUT prismInsideHalfWidth, 	 -prismInsideHalfDepth, 0
	PUT prismInsideHalfWidth, 	 prismInsideHalfDepth, 	-1

	FOR i = 1 TO holeNum
		PUT grillThick/2, 	 insideHalfSizeD+increment*(i-1), 	1
		PUT insideHalfSizeW+increment*(i-1),	 insideHalfSizeD+increment*(i-1), 	1
		PUT insideHalfSizeW+increment*(i-1),	 grillThick/2, 1
		PUT insideHalfSizeW+grillThick+increment*(i-1), 	 grillThick/2, 1
		PUT insideHalfSizeW+grillThick+increment*(i-1), 	 insideHalfSizeD+grillThick+increment*(i-1), 	1
		PUT grillThick/2, 	 insideHalfSizeD+grillThick+increment*(i-1), 	1
		PUT grillThick/2, 	 insideHalfSizeD+increment*(i-1), 	-1

		PUT -grillThick/2, 	 insideHalfSizeD+increment*(i-1), 	1
		PUT -insideHalfSizeW-increment*(i-1),	 insideHalfSizeD+increment*(i-1), 	1
		PUT -insideHalfSizeW-increment*(i-1),	 grillThick/2, 1
		PUT -insideHalfSizeW-grillThick-increment*(i-1), 	 grillThick/2, 1
		PUT -insideHalfSizeW-grillThick-increment*(i-1), 	 insideHalfSizeD+grillThick+increment*(i-1), 	1
		PUT -grillThick/2, 	 insideHalfSizeD+grillThick+increment*(i-1), 	1
		PUT -grillThick/2, 	 insideHalfSizeD+increment*(i-1), 	-1

		PUT -grillThick/2, 	 -insideHalfSizeD-increment*(i-1), 	1
		PUT -insideHalfSizeW-increment*(i-1),	 -insideHalfSizeD-increment*(i-1), 	1
		PUT -insideHalfSizeW-increment*(i-1),	 -grillThick/2, 1
		PUT -insideHalfSizeW-grillThick-increment*(i-1), 	 -grillThick/2, 1
		PUT -insideHalfSizeW-grillThick-increment*(i-1), 	 -insideHalfSizeD-grillThick-increment*(i-1), 	1
		PUT -grillThick/2, 	 -insideHalfSizeD-grillThick-increment*(i-1), 	1
		PUT -grillThick/2, 	 -insideHalfSizeD-increment*(i-1), 	-1

		PUT grillThick/2, 	 -insideHalfSizeD-increment*(i-1), 	1
		PUT insideHalfSizeW+increment*(i-1),	 -insideHalfSizeD-increment*(i-1), 	1
		PUT insideHalfSizeW+increment*(i-1),	 -grillThick/2, 1
		PUT insideHalfSizeW+grillThick+increment*(i-1), 	 -grillThick/2, 1
		PUT insideHalfSizeW+grillThick+increment*(i-1), 	 -insideHalfSizeD-grillThick-increment*(i-1), 	1
		PUT grillThick/2, 	 -insideHalfSizeD-grillThick-increment*(i-1), 	1
		PUT grillThick/2, 	 -insideHalfSizeD-increment*(i-1), 	-1
	NEXT i

	POLY_ NSP/3,
		GET (NSP)
ELSE
	PRISM_ 5, frameThick,
		prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, prismHalfDepth, -1

	ADDz frameThick
	EXTRUDE 5, 0, 0, dTHeight, 1+2+16+32,
			prismInsideHalfWidth, prismInsideHalfDepth, 0,
			-prismInsideHalfWidth, prismInsideHalfDepth, 0,
			-prismInsideHalfWidth, -prismInsideHalfDepth, 0,
			prismInsideHalfWidth, -prismInsideHalfDepth, 0,
			prismInsideHalfWidth, prismInsideHalfDepth, -1
	DEL 1
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1

DEL TOP

END
