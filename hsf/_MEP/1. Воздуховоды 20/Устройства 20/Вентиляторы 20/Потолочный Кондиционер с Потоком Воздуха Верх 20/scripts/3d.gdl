
! Rooftop Upblast Fan


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

addStatus1 = 0
addStatus3 = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	addStatus1 = 1
	addStatus3 = 3
ENDIF


				! --- HOTSPOT --- !

unID = 1
HOTSPOT -footSize/2, footSize/2, 0, unID: unID=unID+1
HOTSPOT -footSize/2, -footSize/2, 0, unID: unID=unID+1
HOTSPOT footSize/2, -footSize/2, 0, unID: unID=unID+1
HOTSPOT footSize/2, footSize/2, 0, unID: unID=unID+1


				! --- MAIN BOX --- !

ADD -footSize/2, -footSize/2, 0
BLOCK footSize, footSize, footHeight
DEL 1

IF gs_detlevel_3D_m = 2 THEN
	ADDz footHeight
	MUL A/0.70, A/0.70, (ZZYZX-footHeight)/0.55
	CYLIND 0.10, 0.20
	ADDz 0.10
	ROTy -90
	REVOLVE 11, 360, addStatus3,
		0, 0.20, 0,
		0.01414213562373, 0.2058578643763, 1,
		0.02, 0.22, 1,
		0.02, 0.2295429313288, 1,
		0.02282370089659, 0.2525327091355, 1,
		0.03109541695799, 0.2740965051983, 1,
		0.04437118759079, 0.2930769447206, 1,
		0.06184558224306, 0.3082805200201, 1,
		0.08241669792557, 0.31878041463, 1,
		0.1049804391087, 0.3240130768752, 1,
		0.35, 0.35, 0
	DEL 1
	CYLIND 0.12, 0.13
	ADDz 0.12
	ROTy -90
	REVOLVE 5, 360, addStatus1+2,
		0, 0.20, 0,
		0.23, 0.21, 0,
		0.3161650008843, 0.1820800847737, 1,
		0.3261755991803, 0.1748162046437, 1,
		0.33, 0.1630539773395, 1
	DEL 5
ELSE
	ADDz footHeight
	MUL A/0.70, A/0.70, (ZZYZX-footHeight)/0.55
	CYLIND 0.12, 0.20
	ADDz 0.12
	CONE 0.33, 0.30, 0.35, 90, 90
	ADDz 0.33
	CONE 0.10, 0.21, 0.18, 90, 90
	DEL 4
ENDIF


						! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1

DEL TOP

END
