
! Propeller Fan


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	addStatus = 3
ENDIF


				! --- HOTSPOT --- !

unID = 1
HOTSPOT 0, -dTDepth/2-propellerDepth, dTHeight/2-propellerDia/2, unID: unID=unID+1
HOTSPOT -propellerDia/2, -dTDepth/2-propellerDepth, dTHeight/2, unID: unID=unID+1
HOTSPOT propellerDia/2, -dTDepth/2-propellerDepth, dTHeight/2, unID: unID=unID+1
HOTSPOT 0, -dTDepth/2-propellerDepth, dTHeight/2+propellerDia/2, unID: unID=unID+1

HOTSPOT -dTWidth/2, -dTDepth/2, 0, unID: unID=unID+1
HOTSPOT -dTWidth/2, -dTDepth/2, dTHeight, unID: unID=unID+1
HOTSPOT dTWidth/2, -dTDepth/2, 0, unID: unID=unID+1
HOTSPOT dTWidth/2, -dTDepth/2, dTHeight, unID: unID=unID+1

HOTSPOT -dTWidth/2, dTDepth/2, 0, unID: unID=unID+1
HOTSPOT -dTWidth/2, dTDepth/2, dTHeight, unID: unID=unID+1
HOTSPOT dTWidth/2, dTDepth/2, 0, unID: unID=unID+1
HOTSPOT dTWidth/2, dTDepth/2, dTHeight, unID: unID=unID+1


				! --- MAIN BOX --- !

ADDz dTHeight/2
IF gs_detlevel_3D_m = 2 THEN
	ROTx 90
	ADDz -dTDepth/2

	EXTRUDE 5, 0, 0, dTDepth, addStatus+16+32,
		-dTWidth/2, -dTHeight/2, 0,
		dTWidth/2, 	-dTHeight/2, 0,
		dTWidth/2, 	dTHeight/2,  0,
		-dTWidth/2, dTHeight/2,  0,
		-dTWidth/2, -dTHeight/2, -1

	ADDz dTDepth

	POLY_ 7,
		-dTWidth/2, -dTHeight/2, 0,
		dTWidth/2, 	-dTHeight/2, 0,
		dTWidth/2, 	dTHeight/2,  0,
		-dTWidth/2, dTHeight/2,  0,
		-dTWidth/2, -dTHeight/2, -1,
		0, 				0, 		 900,
		propellerDia/2, 360, 	 4000

	EXTRUDE 2, 0, 0, propellerDepth, addStatus+16+32,
		0, 				0, 		901,
		propellerDia/2, 360, 	4001

	RESOL res/2
	CYLIND propellerDepth, propellerCDia/2

	FOR i = 1 TO 4
		ROTz 45+90*(i-1)
		ADD propellerCDia/3, 0, propellerDepth*0.95
		ROTy 90+ATN((propellerCDia*2/3)/(propellerDepth*0.75))
			RECT SQR((propellerDepth*0.75)^2+(propellerCDia*2/3)^2), (propellerDia/2)-0.015*(dTWidth/0.625)
		DEL 3
	NEXT i

	ADDz -axisLength
	RESOL res/3
		CYLIND axisLength, axisDia/2
	ADDz -cwheelThick
	RESOL res
		CYLIND cwheelThick, cwheelDia/2
	DEL 2

	ADD -dTWidth/2+engineDist, -dTHeight/2+engineDist, -axisLength+ewheelThick
		CYLIND engineLength, engineDia/2
	ADDz -ewheelThick
	RESOL res/3
		CYLIND ewheelThick, axisDia/2
	ADDz -ewheelThick
	RESOL res
		CYLIND ewheelThick, ewheelDia/2
	DEL 3

	ADDz -cwheelThick*0.9-axisLength
	MUL dTWidth/0.625, dTWidth/0.625, 1
	EXTRUDE 5, 0, 0, cwheelThick*0.8, 16+32,
			0.0465663864264, -0.06261365774744,      1,
			-0.1583310271203, -0.2149978576208,      1,
			-0.2149978576208, -0.1583310271203,      1001,
			-0.06261365774744, 0.0465663864264,      1,
			0.0465663864264, -0.06261365774744,      1001
	DEL 2

	ADD -dTWidth/2+0.12*(dTWidth/0.625), dTHeight/2-0.04*(dTWidth/0.625), 0
	MUL dTWidth/0.625, dTWidth/0.625, dTWidth/0.625
	ROTz -ATN((dTHeight-0.145)/(dTWidth-0.17))
	ROTx -90
	POLY_ 8,
		0, 0.0001, 1,
		0, 0.08+0.075, 1,
		0.66, 0.08+0.075, 1,
		0.66, 0.0001, 1,
		0.61, 0.0001, 1,
		0.61, 0.03+0.075, 1,
		0.05, 0.03+0.075, 1,
		0.05, 0.0001, 1
	ADDx 0.396
	ROTy 90-(6*((0.50-dTWidth)/0.25))*(dTWidth < 0.50)
	POLY_ 7,
		0.001, 0.08+0.075, 1,
		0.001, 0.03+0.075, 1,
		0.325, 0.03+0.075, 1,
		0.325, 0.0001, 1,
		0.375, 0.0001, 1,
		0.375, 0.205+0.075, 1,
		0.125, 0.205+0.075, 1
	DEL 6


	DEL 3
ELSE
	ADD -dTWidth*0.45, dTDepth/2, -dTHeight*0.45
		BLOCK dTWidth*0.9, cwheelThick+axisLength-dTDepth, dTHeight*0.9
	DEL 1
	ADD -dTWidth/2, -dTDepth/2, -dTHeight/2
		BLOCK dTWidth, dTDepth, dTHeight
	DEL 1
	ADD -propellerDia/2, -dTDepth/2-propellerDepth, -propellerDia/2
		BLOCK propellerDia, propellerDepth, propellerDia
	DEL 1
ENDIF
DEL 1


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 0,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 0
END
