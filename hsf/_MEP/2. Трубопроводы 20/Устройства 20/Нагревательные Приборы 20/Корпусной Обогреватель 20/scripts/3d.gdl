
! Cabinet Unit Heater


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- MAIN BOX --- !

ADD A/2, -B/2, 0

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT -A, B, 0, unID: unID=unID+1
HOTSPOT -A, 0, 0, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1
HOTSPOT -A, B, ZZYZX, unID: unID=unID+1
HOTSPOT -A, 0, ZZYZX, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN
	GROUP "box"
		ROTy -90
		PRISM_ 7, A,
			0, B, 15,
			0, toeDepth, 15,
			toeHeight, toeDepth, 15,
			toeHeight, 0, 15,
			ZZYZX, 0, 15,
			ZZYZX, B, 15,
			0, B, -1
		DEL 1
	ENDGROUP

	GROUP "Grills"
		ADD -A+0.10, 0, toeHeight+0.05
		BLOCK grillWidth, grillDepth, grillHeight
		ADDz ZZYZX-toeHeight-0.10-grillHeight
		BLOCK grillWidth, grillDepth, grillHeight
		DEL 2
	ENDGROUP

	result_=SUBGROUP("box","Grills")

	PLACEGROUP result_

	KILLGROUP "box"
	KILLGROUP "Grills"

	 numV = 5
	 numH = INT(grillWidth/0.09)
	FOR k = 1 TO 2
		ADD -A+0.10, 0, toeHeight+0.05+(ZZYZX-toeHeight-0.10-grillHeight)*(k-1)
			FOR i=1 TO numV-1
				ADDz (grillHeight/numV)*i
				RECT grillWidth, grillDepth
				DEL 1
			NEXT i
			FOR i=1 TO numH-1
				ADDx (grillWidth/numH)*i
				ROTy -90
				RECT grillHeight, grillDepth
				DEL 2
			NEXT i
		DEL 1
	NEXT k
ELSE
	ROTy -90
	PRISM_ 7, A,
		0, B, 15,
		0, toeDepth, 15,
		toeHeight, toeDepth, 15,
		toeHeight, 0, 15,
		ZZYZX, 0, 15,
		ZZYZX, B, 15,
		0, B, -1
	DEL 1
ENDIF

DEL 1

END
