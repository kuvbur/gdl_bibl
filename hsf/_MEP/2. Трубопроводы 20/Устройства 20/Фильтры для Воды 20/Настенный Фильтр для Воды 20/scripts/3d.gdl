
! Wall-Mounted Water Filter


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT -A/2, B/2, 0, unID: unID=unID+1
HOTSPOT -A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT A/2, B/2, 0, unID: unID=unID+1
HOTSPOT -A/2, B/2, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, -B/2, ZZYZX, unID: unID=unID+1
HOTSPOT A/2, -B/2, ZZYZX, unID: unID=unID+1
HOTSPOT A/2, B/2, ZZYZX, unID: unID=unID+1


				! --- MAIN BOX --- !

IF gs_detlevel_3D_m = 2 THEN
	FOR i=1 TO 3
		ADD -A/2+cRad+(cRad*2+0.01*WRate)*(i-1), 0, 0
			CYLIND ZZYZX-LHeight-0.10*HRate, cRad
		DEL 1
	NEXT i

	ADDz ZZYZX-LHeight-0.10*HRate
	FPRISM_ MainBoxMaterial, MainBoxMaterial,
			MainBoxMaterial, MainBoxMaterial,
			4, 0.10*HRate, 45, ch,
			-A/2, -B/2, 15,
			-A/2, B/2, 15,
			A/2, B/2, 15,
			A/2, -B/2, 15
	DEL 1
ELSE
	PRISM_ 4, ZZYZX-LHeight,
			-A/2, -B/2, 15,
			-A/2, B/2, 15,
			A/2, B/2, 15,
			A/2, -B/2, 15
ENDIF

ADD 0, -B/2, ZZYZX-LHeight
ROTy -90
ADDz -A/2+ch
PRISM_ 6, A-ch*2,
	0, ch, 15,
	0, B, 15,
	LHeight, B, 15,
	LHeight, B-LThick, 15,
	LThick, B-LThick, 15,
	LThick, ch, 15
DEL 3

ADD -A/2+ch+0.005, B/2-LThick-boxDepth-0.03, ZZYZX-LHeight+LThick
	BLOCK 0.15, boxDepth, 0.07
DEL 1


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 1
END
