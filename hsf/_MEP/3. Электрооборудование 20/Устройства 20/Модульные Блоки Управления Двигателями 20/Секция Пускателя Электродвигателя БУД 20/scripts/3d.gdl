
! MCC Motor Starter Section


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res


DEFINE MATERIAL	"Shaft Black Plastic" 20,
	0.147059, 0.135294, 0.103922,
	0.9, 0.6, 0.2, 0, 10, 0,
	1, 0.978103, 0.968383,
	0, 0, 0,
	0, 0, 1,
	0

DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0


				! --- MCC --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, B, ZZYZX, unID: unID=unID+1


ADDy 0.01
BLOCK A, B-0.01, ZZYZX
DEL 1

ROTx 90
ADDz -0.01
FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
	4, 0.01, 45, 0.01,
	0.005, footH-0.005, 15,
	A-0.005, footH-0.005, 15,
	A-0.005, 0, 15,
	0.005, 0, 15
DEL 2

ROTx 90
ADD 0, ZZYZX-headH, -0.01
FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
	4, 0.01, 45, 0.01,
	0.005, headH-0.005, 15,
	A-0.005, headH-0.005, 15,
	A-0.005, 0, 15,
	0.005, 0, 15
DEL 2

yy = (ZZYZX-footH-headH) / numUnits

ROTx 90
ADD 0, footH, -0.01

FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
	4, 0.01, 45, 0.01,
	A-sideW+0.005, ZZYZX-footH-headH-0.005, 15,
	A-0.005, ZZYZX-footH-headH-0.005, 15,
	A-0.005, 0, 15,
	A-sideW+0.005, 0, 15

IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed

	RESOL 12

	FOR i=1 TO numUnits
		FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
			4, 0.01, 45, 0.01,
			0.005, yy-0.005, 15,
			A-sideW, yy-0.005, 15,
			A-sideW, 0, 15,
			0.005, 0, 15

		MATERIAL "Shaft Black Plastic"
		ADD A-sideW-0.05, 2*yy/3, 0.011
		POLY 4,
			-0.015, 0.075,
			-0.015,-0.075,
			 0.015,-0.075,
			 0.015, 0.075
		ROTx 90
		ROTy 90
		ADDz -0.004
		PRISM_ 4,0.008,
			-0.05, 0, 15,
			 0.05, 0, 79,
			 -0.5, 1, 879,
			-0.05, 0, 1079
		MATERIAL	"Shaft Aluminum"
		ROTx 90
		ADD -0.01,0.004,0
		ROTy 90
		PRISM 8,0.02,
			0.035, 0.004,
			0, 0.004,
			0, 0.01,
			0.045, 0.01,
			0.045,-0.01,
			0,-0.01,
			0,-0.004,
			0.035,-0.004
		DEL 7

		ADD 0.10,0.12,0.011
		POLY_ 10,
			-0.045, 0.065, 1,
			-0.045,-0.065, 1,
			 0.045,-0.065, 1,
			 0.045, 0.065, 1,
			-0.045, 0.065, -1,
			-0.035, 0.055, 1,
			-0.035,-0.055, 1,
			 0.035,-0.055, 1,
			 0.035, 0.055, 1,
			-0.035, 0.055, -1
		ADD -0.045,0.02,0
		BLOCK 0.09, 0.02, 0.02
		DEL 1
		MATERIAL "Shaft Black Plastic"
		POLY_ 4,
			-0.035, 0.055, 1,
			-0.035,-0.055, 1,
			 0.035,-0.055, 1,
			 0.035, 0.055, 1
		DEL 1

		ADDy yy
	NEXT i
	DEL numUnits + 2
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple

	FOR i=1 TO numUnits
		FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
			4, 0.01, 45, 0.01,
			0.005, yy-0.005, 15,
			A-sideW, yy-0.005, 15,
			A-sideW, 0, 15,
			0.005, 0, 15

		MATERIAL "Shaft Black Plastic"
		ADD A-sideW-0.05, 2*yy/3, 0.011
		POLY 4,
			-0.015, 0.075,
			-0.015,-0.075,
			 0.015,-0.075,
			 0.015, 0.075
		ROTx 90
		ROTy 90
		ADDz -0.004
		MATERIAL	"Shaft Aluminum"
		ROTx 90
		ADD -0.01,0.004,0
		ROTy 90
		PRISM 4,0.02,
			0, 0.01,
			0.045, 0.01,
			0.045,-0.01,
			0,-0.01
		DEL 7

		ADD 0.10,0.12,0.011
		ADD -0.045,0.02,0
		BLOCK 0.09, 0.02, 0.02
		DEL 1
		MATERIAL "Shaft Black Plastic"
		POLY_ 4,
			-0.035, 0.055, 1,
			-0.035,-0.055, 1,
			 0.035,-0.055, 1,
			 0.035, 0.055, 1
		DEL 1

		ADDy yy
	NEXT i
	DEL numUnits + 2
ENDIF
MATERIAL bodyMaterial

