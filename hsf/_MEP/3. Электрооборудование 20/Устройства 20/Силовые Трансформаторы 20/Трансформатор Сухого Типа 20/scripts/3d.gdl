
! Dry Type Transformer


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res


DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0


				! --- POWER TRANSFORMER --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1
HOTSPOT A, B, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1


tck = 0.02 * A
ftck = 3 * tck
ventH = 0.2 * ZZYZX

IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed

	IF boxShapeID = 1 THEN
		GOSUB 100
	ENDIF

	IF boxShapeID = 2 THEN
		GOSUB 200

		MATERIAL	"Shaft Aluminum"
		ROTx 90
		ADD 0.12, MIN(0.9, 0.6 * ZZYZX), 0
		CYLIND 0.005, 0.025
		DEL 2
	ENDIF

	IF boxShapeID = 3 THEN
		GOSUB 300

		MATERIAL	"Shaft Aluminum"
		ROTx 90
		ADD 0.12, MIN(0.9, 0.6 * ZZYZX), 0
		CYLIND 0.005, 0.025
		DEL 2
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple

	IF boxShapeID = 1 THEN
		GOSUB 100
	ENDIF

	IF boxShapeID = 2 THEN
		GOSUB 200
	ENDIF

	IF boxShapeID = 3 THEN
		GOSUB 300
	ENDIF
ENDIF
END

100:
	ROTx 90
	PRISM_ 10, -B,
		0,0, 15,
		0, ZZYZX, 15,
		A, ZZYZX, 15,
		A, 0, 15,
		0, 0, -1,
		tck, ZZYZX-ventH, 13,
		tck, ZZYZX-tck, 15,
		A-tck, ZZYZX-tck, 15,
		A-tck, ZZYZX-ventH, 9,
		tck, ZZYZX-ventH, -1

	ROTY 90
	ADD 0, ZZYZX-ventH, tck
	PRISM_ 4,A-2*tck,
		0,0,	15,
		ventH/2, 0.7 * ventH,	15,
		B-ventH/2, 0.7 * ventH,	15,
		B, 0,	10
	DEL 3
RETURN

200:
	ROTx 90
	PRISM_ 18, -B,
		0,0, 15,
		0, ZZYZX, 15,
		A, ZZYZX, 15,
		A, 0, 15,
		A-ftck, 0, 15,
		A-ftck, tck, 15,
		A-tck, tck, 15,
		A-tck, footHeight, 15,
		tck, footHeight, 15,
		tck, tck, 15,
		ftck, tck, 15,
		ftck, 0, 15,
		0, 0, -1,
		tck, ZZYZX-ventH, 13,
		tck, ZZYZX-tck, 15,
		A-tck, ZZYZX-tck, 15,
		A-tck, ZZYZX-ventH, 9,
		tck, ZZYZX-ventH, -1

	ROTY 90
	ADD 0, ZZYZX-ventH, tck
	PRISM_ 4,A-2*tck,
		0,0,	15,
		ventH/2, 0.7 * ventH,	15,
		B-ventH/2, 0.7 * ventH,	15,
		B, 0,	10
	DEL 3
RETURN

300:
	ADDy 0.03
	BLOCK A, B-0.03, ZZYZX
	DEL 1

	xx = (A - 0.01) / 3

	ROTx 90
	ADDz -0.03
	FOR i=1 TO 3
		FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
			4, 0.03, 45, 0.03,
			0.01, ZZYZX-0.01, 15,
			xx, ZZYZX-0.01, 15,
			xx, footHeight, 15,
			0.01, footHeight, 15
		ADDx xx
	NEXT i
	DEL 5
RETURN

