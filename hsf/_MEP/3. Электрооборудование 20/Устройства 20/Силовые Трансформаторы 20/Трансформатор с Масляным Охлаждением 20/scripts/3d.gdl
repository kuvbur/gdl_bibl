
! Wet Type Transformer


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res


DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0


				! --- POWER TRANSFORMER --- !
hID = 1

px = (A - wx) / 2

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, dy2, 0, unID: unID=unID+1
HOTSPOT A, dy2, 0, unID: unID=unID+1
HOTSPOT px, B, dz1, unID: unID=unID+1
HOTSPOT px+wx, B, dz1, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, dy2, dz2, unID: unID=unID+1
HOTSPOT A, dy2, dz2, unID: unID=unID+1
HOTSPOT px, B, dz3, unID: unID=unID+1
HOTSPOT px+wx, B, dz3, unID: unID=unID+1



IF GLOB_CONTEXT <> 23 THEN
	ROTz -90
	ROTx -90
	PRISM 6, A,
		-0.01,	 0,
		-0.01,	-ZZYZX,
		-dy1,	-ZZYZX,
		-dy1,	-dz2,
		-dy2,	-dz2,
		-dy2,	 0
	ADDz (A - wx) / 2
	PRISM 4,wx,
		-B,	-dz1,
		-B,	-dz3,
		-dy2,		-dz3,
		-dy2,		-dz1
	DEL 3

	ROTx 90
	ADDz -0.01
	FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
		4, 0.01, 45, 0.01,
		0.005, footH-0.005, 15,
		A-0.005, footH-0.005, 15,
		A-0.005, 0, 15,
		0.005, 0, 15
	DEL 2

	ROTx 90
	ADD 0, ZZYZX-headH, -0.01
	FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
		4, 0.01, 45, 0.01,
		0.005, headH-0.005, 15,
		A-0.005, headH-0.005, 15,
		A-0.005, 0, 15,
		0.005, 0, 15
	DEL 2


	ROTx 90
	ADD 0, footH, -0.01

	FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
		4, 0.01, 45, 0.01,
		A/2-0.005, ZZYZX-footH-headH-0.005, 15,
		A-0.005, ZZYZX-footH-headH-0.005, 15,
		A-0.005, 0, 15,
		A/2-0.005, 0, 15

	FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
		4, 0.01, 45, 0.01,
		A/2+0.005, 0, 15,
		A/2+0.005, ZZYZX-footH-headH-0.005, 15,
		0.005, ZZYZX-footH-headH-0.005, 15,
		0.005, 0, 15

	DEL 2
ENDIF

IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed

	MATERIAL handleMaterial
	RESOL 8
	ADD A/2+0.08, 0, ZZYZX/2
	ROTx 90
	CYLIND 0.05, 0.02
	ADDz 0.05
	PRISM 4, 0.01,
		-0.01, 0.015,
		-0.01,-0.12,
		 0.01,-0.12,
		 0.01, 0.015
	DEL 3
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple

	MATERIAL handleMaterial
	ADD A/2+0.08-0.01, 0, ZZYZX/2
	ROTy 90
	PRISM 6, 0.02,
		-0.015, 0,
		-0.015,-0.06,
		 0.12, -0.06,
		 0.12, -0.05,
		 0.015,-0.05,
		 0.015, 0
	DEL 2
ENDIF

