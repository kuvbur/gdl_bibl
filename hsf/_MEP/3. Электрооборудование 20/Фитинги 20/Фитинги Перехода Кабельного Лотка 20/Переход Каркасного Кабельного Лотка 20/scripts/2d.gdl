
! Cable Ladder Reducer


CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen

PEN gs_cont_pen
IF gs_fill_pen THEN SET FILL gs_fill_type

ADD2 MEP_Position[1][1], MEP_Position[1][2]
HOTSPOT2 outCoords[1][1], outCoords[1][2], hceID_1
HOTSPOT2 outCoords[4][1], outCoords[4][2], hceID_2
IF ABS(MEP_offset_y) < EPS THEN
	HOTLINE2 outCoords[1][1], outCoords[1][2], outCoords[4][1], outCoords[4][2]
ELSE
	HOTLINE2 outCoords[1][1], outCoords[1][2], outCoords[7][1], outCoords[7][2]
	HOTLINE2 outCoords[8][1], outCoords[8][2], outCoords[4][1], outCoords[4][2]
	HOTLINE2 outCoords[7][1], outCoords[7][2], outCoords[8][1], outCoords[8][2]
ENDIF
DEL 1

cc = MEP_CoveredCableTray
IF ABS(ABS(dZz)-1) < EPS AND MEP_CoveredCableTray THEN
	DRAWINDEX 20
	 aa = MEP_Position[2][1]
	 bb = MEP_Position[2][2]

	 arCosVal = aa/1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = -1 THEN arCosVal = -1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = 1 THEN arCosVal = 1
	alpha = ACS(arCosVal)
	IF SGN(bb) = -1 THEN alpha = 360-alpha

	ADD2 MEP_Position[1][1], MEP_Position[1][2]
	ROT2 alpha
	MUL2 1, SGN(dZz)

	PUT 0, cableTrayHalfWidthA, 1
	IF ABS(MEP_offset_y+cableTrayHalfWidthB-cableTrayHalfWidthA) > EPS THEN
		PUT cableTrayStraightLength, cableTrayHalfWidthA, 1
		PUT MEP_StraightLength-cableTrayStraightLength, MEP_offset_y+cableTrayHalfWidthB, 1
	ENDIF
	PUT MEP_StraightLength, MEP_offset_y+cableTrayHalfWidthB, MEP_CoveredCableTray
	PUT MEP_StraightLength, MEP_offset_y-cableTrayHalfWidthB, 1
	IF ABS(MEP_offset_y-cableTrayHalfWidthB+cableTrayHalfWidthA) > EPS THEN
		PUT MEP_StraightLength-cableTrayStraightLength, MEP_offset_y-cableTrayHalfWidthB, 1
		PUT cableTrayStraightLength, -cableTrayHalfWidthA, 1
	ENDIF
	PUT 0, -cableTrayHalfWidthA, MEP_CoveredCableTray

	POLY2_B NSP/3, 1 + 2*MEP_CoveredCableTray + 4 + 64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	DEL 3

	cc = 0
ENDIF

DRAWINDEX 10
PEN gs_cont_pen
IF gs_fill_pen THEN PEN gs_fill_pen
PROJECT2{3} 3, 270, 3+32, 4, gs_back_pen, 0, 0, 0, PARAMETERS cable_carrier_show = 1, MEP_cline_show_3D = 0, MEP_CoveredCableTray = cc, gs_detlevel_3D_m = 2
PEN gs_cont_pen
PROJECT2{3} 3, 270, 2, 8, 0, 0, 0, 0, PARAMETERS cable_carrier_show = 1, MEP_cline_show_3D = 0, MEP_CoveredCableTray = cc, gs_detlevel_3D_m = 2


IF MEP_cline_show THEN			! -----> center line <----- !
	DRAWINDEX 30
	PEN MEP_cline_pen
	SET LINE_TYPE MEP_cline_type

	ADD2 MEP_Position[1][1], MEP_Position[1][2]
	IF ABS(MEP_offset_y) < EPS THEN
		LINE2 outCoords[1][1], outCoords[1][2], outCoords[4][1], outCoords[4][2]
	ELSE
		LINE2 outCoords[1][1], outCoords[1][2], outCoords[7][1], outCoords[7][2]
		LINE2 outCoords[8][1], outCoords[8][2], outCoords[4][1], outCoords[4][2]
		LINE2 outCoords[7][1], outCoords[7][2], outCoords[8][1], outCoords[8][2]
	ENDIF
	DEL 1
ENDIF
