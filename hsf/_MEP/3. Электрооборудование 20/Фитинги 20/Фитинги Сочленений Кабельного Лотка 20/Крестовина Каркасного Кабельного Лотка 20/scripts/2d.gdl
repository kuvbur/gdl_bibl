
! Cable Ladder Cross


CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen

PEN gs_cont_pen
IF gs_fill_pen THEN SET FILL gs_fill_type

ADD2 MEP_Position[1][1], MEP_Position[1][2]
HOTSPOT2 outCoords[13][1], outCoords[13][2], HptsUnIDBeginForConnEnds
HOTLINE2 outCoords[1][1], outCoords[1][2], outCoords[4][1], outCoords[4][2]
HOTLINE2 outCoords[7][1], outCoords[7][2], outCoords[13][1], outCoords[13][2]
HOTLINE2 outCoords[10][1], outCoords[10][2], outCoords[13][1], outCoords[13][2]

IF ABS(ABS(dZz)-1) > EPS THEN
	HOTSPOT2 outCoords[1][1], outCoords[1][2], hceID_1
	HOTSPOT2 outCoords[4][1], outCoords[4][2], hceID_2
	HOTSPOT2 outCoords[7][1], outCoords[7][2], hceID_3
	HOTSPOT2 outCoords[10][1], outCoords[10][2], hceID_4
ENDIF
DEL 1


IF ABS(ABS(dZz)-1) < EPS THEN
	mainTangentLen = MEP_StraightLength/2-ConWidth_3/2-MEP_CrossRadius
	branchTangentLen_3 = ConLength_3-MEP_CrossRadius
	branchTangentLen_4 = ConLength_4-MEP_CrossRadius

	 aa = MEP_Position[2][1]
	 bb = MEP_Position[2][2]

	 arCosVal = aa/1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = -1 THEN arCosVal = -1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = 1 THEN arCosVal = 1
	alpha = ACS(arCosVal)
	IF SGN(bb) = -1 THEN alpha = 360-alpha

	ADD2 MEP_Position[1][1], MEP_Position[1][2]
	ROT2 alpha
	MUL2 1, SGN(dZz)

	ladderNum = MAX(4, INT(MEP_StraightLength/(0.75*ConWidth_1)))
	ladderDist = MEP_StraightLength/ladderNum
	ladderHalfDist = ladderDist/2
	ladderThk = MAX(0.03*(ladderDist > 0.06), (0.75*ConWidth_1)/10)
	FOR i = 1 TO ladderNum						! -----> main ladders <----- !
		ADD2 ladderHalfDist+ladderDist*(i-1), 0
		PUT ladderThk/2, cableTrayHalfWidth, 1
		PUT -ladderThk/2, cableTrayHalfWidth, 1
		PUT -ladderThk/2, -cableTrayHalfWidth, 1
		PUT ladderThk/2, -cableTrayHalfWidth, 1
		PUT ladderThk/2, cableTrayHalfWidth, -1

		POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

			GET (NSP)

		DEL 1
	NEXT i

	branchTangentLen = branchTangentLen_3
	ConLength = ConLength_3
	ConWidth = ConWidth_3
	cableTrayHalfWidthBC = cableTrayHalfWidthB
	FOR k = 1 TO 2
		MUL2 1, 1-2*(k-1)

		ladderNum = MAX(1.5, INT(ConLength_3/(0.75*ConWidth_3))+0.5)
		ladderDist = ConLength_3/ladderNum
		ladderNum = INT(ladderNum)
		ladderThk = MAX(0.03*(ladderDist > 0.06), (0.75*ConWidth_3)/10)

		IF ABS(MEP_CrossTypeID - 1) < EPS THEN		! -----> branch ladders <----- !
			FOR i = 1 TO ladderNum+1
				ADD2 0, cableTrayHalfWidth+ladderDist*(i-1)

				IF ladderDist*(i-1)+ladderThk > MEP_CrossRadius THEN
					PUT MEP_StraightLength/2-cableTrayHalfWidthBC, 0, 1
					PUT MEP_StraightLength/2-cableTrayHalfWidthBC, ladderThk, 1
					PUT MEP_StraightLength/2+cableTrayHalfWidthBC, ladderThk, 1
					PUT MEP_StraightLength/2+cableTrayHalfWidthBC, 0, 1
					PUT MEP_StraightLength/2-cableTrayHalfWidthBC, 0, -1
				ELSE
					xx = cableTrayHalfWidthBC+MEP_CrossRadius-SQR(MEP_CrossRadius^2 - (MEP_CrossRadius-ladderDist*(i-1))^2)
					xx2 = cableTrayHalfWidthBC+MEP_CrossRadius-SQR(MEP_CrossRadius^2 - (MEP_CrossRadius-ladderDist*(i-1)-ladderThk)^2)

					PUT MEP_StraightLength/2-xx, 0, 1
					PUT MEP_StraightLength/2-cableTrayHalfWidthBC-MEP_CrossRadius, MEP_CrossRadius-ladderDist*(i-1), 901
					PUT MEP_StraightLength/2-xx2, ladderThk, 3001
					PUT MEP_StraightLength/2+xx2, ladderThk, 1
					PUT MEP_StraightLength/2+cableTrayHalfWidthBC+MEP_CrossRadius, MEP_CrossRadius-ladderDist*(i-1), 901
					PUT MEP_StraightLength/2+xx, 0, 3001
					PUT MEP_StraightLength/2-xx, 0, -1
				ENDIF

				POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

					GET (NSP)

				DEL 1
			NEXT i
		ELSE
			FOR i = 1 TO ladderNum+1
				ADD2 0, cableTrayHalfWidth+ladderDist*(i-1)
				PUT MEP_StraightLength/2-cableTrayHalfWidthBC, 0, 1
				PUT MEP_StraightLength/2-cableTrayHalfWidthBC, ladderThk, 1
				PUT MEP_StraightLength/2+cableTrayHalfWidthBC, ladderThk, 1
				PUT MEP_StraightLength/2+cableTrayHalfWidthBC, 0, 1
				PUT MEP_StraightLength/2-cableTrayHalfWidthBC, 0, -1

				POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

					GET (NSP)
				DEL 1
			NEXT i
		ENDIF
		DEL 1

		branchTangentLen = branchTangentLen_4
		ConLength = ConLength_4
		ConWidth = ConWidth_4
		cableTrayHalfWidthBC = cableTrayHalfWidthC
	NEXT k

	IF ABS(MEP_CrossTypeID - 1) < EPS THEN
		PUT 0, cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2-cableTrayHalfWidthB-MEP_CrossRadius, cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2-cableTrayHalfWidthB-MEP_CrossRadius, cableTrayHalfWidth+MEP_CrossRadius, 901
		PUT MEP_StraightLength/2-cableTrayHalfWidthB, cableTrayHalfWidth+MEP_CrossRadius, 3001
		PUT MEP_StraightLength/2-cableTrayHalfWidthB, cableTrayHalfWidth+MEP_CrossRadius+branchTangentLen_3, MEP_CoveredCableTray
		PUT MEP_StraightLength/2+cableTrayHalfWidthB, cableTrayHalfWidth+MEP_CrossRadius+branchTangentLen_3, 1
		PUT MEP_StraightLength/2+cableTrayHalfWidthB, cableTrayHalfWidth+MEP_CrossRadius, 1
		PUT MEP_StraightLength/2+cableTrayHalfWidthB+MEP_CrossRadius, cableTrayHalfWidth+MEP_CrossRadius, 901
		PUT MEP_StraightLength/2+cableTrayHalfWidthB+MEP_CrossRadius, cableTrayHalfWidth, 3001
		PUT MEP_StraightLength, cableTrayHalfWidth, MEP_CoveredCableTray
		PUT MEP_StraightLength, -cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2+cableTrayHalfWidthC+MEP_CrossRadius, -cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2+cableTrayHalfWidthC+MEP_CrossRadius, -cableTrayHalfWidth-MEP_CrossRadius, 901
		PUT MEP_StraightLength/2+cableTrayHalfWidthC, -cableTrayHalfWidth-MEP_CrossRadius, 3001
		PUT MEP_StraightLength/2+cableTrayHalfWidthC, -cableTrayHalfWidth-MEP_CrossRadius-branchTangentLen_4, MEP_CoveredCableTray
		PUT MEP_StraightLength/2-cableTrayHalfWidthC, -cableTrayHalfWidth-MEP_CrossRadius-branchTangentLen_4, 1
		PUT MEP_StraightLength/2-cableTrayHalfWidthC, -cableTrayHalfWidth-MEP_CrossRadius, 1
		PUT MEP_StraightLength/2-cableTrayHalfWidthC-MEP_CrossRadius, -cableTrayHalfWidth-MEP_CrossRadius, 901
		PUT MEP_StraightLength/2-cableTrayHalfWidthC-MEP_CrossRadius, -cableTrayHalfWidth, 3001
		PUT 0, -cableTrayHalfWidth, MEP_CoveredCableTray
		PUT 0, cableTrayHalfWidth, 1
	ELSE
		PUT 0, cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2-cableTrayHalfWidthB, cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2-cableTrayHalfWidthB, cableTrayHalfWidth+ConLength_3, MEP_CoveredCableTray
		PUT MEP_StraightLength/2+cableTrayHalfWidthB, cableTrayHalfWidth+ConLength_3, 1
		PUT MEP_StraightLength/2+cableTrayHalfWidthB, cableTrayHalfWidth, 1
		PUT MEP_StraightLength, cableTrayHalfWidth, MEP_CoveredCableTray
		PUT MEP_StraightLength, -cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2+cableTrayHalfWidthC, -cableTrayHalfWidth, 1
		PUT MEP_StraightLength/2+cableTrayHalfWidthC, -cableTrayHalfWidth-ConLength_4, MEP_CoveredCableTray
		PUT MEP_StraightLength/2-cableTrayHalfWidthC, -cableTrayHalfWidth-ConLength_4, 1
		PUT MEP_StraightLength/2-cableTrayHalfWidthC, -cableTrayHalfWidth, 1
		PUT 0, -cableTrayHalfWidth, MEP_CoveredCableTray
		PUT 0, cableTrayHalfWidth, 1
	ENDIF

	POLY2_B NSP/3, 1 + 2*MEP_CoveredCableTray + 4 + 64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
			MEP_NumberConnections = MEP_NumberConnections,
			MEP_NumConnectionData = gs_Connections,
			gs_AddConnections = gs_AddConnections,
			DrawingNum = 1,
			MEP_InsShow = 0,
			MEP_cline_show = 0,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type=gs_fill_type,
			gs_fill_pen=gs_fill_pen,
			gs_back_pen=gs_back_pen,
			ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConShow_1 = 0, ConEdit_1 = 0,
			ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConShow_2 = 0, ConEdit_2 = 0,
			ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConShow_3 = 0, ConEdit_3 = 1,
			ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConShow_4 = 0, ConEdit_4 = 1

	DEL 3
ELSE
	PEN gs_cont_pen
	IF gs_fill_pen THEN PEN gs_fill_pen
	PROJECT2{3} 3, 270, 3+32, 4, gs_back_pen, 0, 0, 0, PARAMETERS cable_carrier_show = 1, MEP_cline_show_3D = 0, gs_detlevel_3D_m = 2
	PEN gs_cont_pen
	PROJECT2{3} 3, 270, 2, 8, 0, 0, 0, 0, PARAMETERS cable_carrier_show = 1, MEP_cline_show_3D = 0, gs_detlevel_3D_m = 2
ENDIF

IF MEP_cline_show THEN			! -----> center line <----- !
	PEN MEP_cline_pen
	SET LINE_TYPE MEP_cline_type

	ADD2 MEP_Position[1][1], MEP_Position[1][2]
	LINE2 outCoords[1][1], outCoords[1][2], outCoords[4][1], outCoords[4][2]
	LINE2 outCoords[7][1], outCoords[7][2], outCoords[10][1], outCoords[10][2]
	DEL 1
ENDIF
