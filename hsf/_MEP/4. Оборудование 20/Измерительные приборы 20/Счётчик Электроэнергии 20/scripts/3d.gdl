
! Electricity Meter


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN
	MainBoxMaterial = sMat
	HouseMaterial = sMat
	DialPlateMaterial = sMat
	WheelMaterial = sMat
ENDIF
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	addStatus = 3
ENDIF


ADD A/2, B, 0
				! --- HOTSPOTS --- !

unID = 1
HOTSPOT A/2, 0, 0, unID: unID=unID+1
HOTSPOT -A/2, 0, 0, unID: unID=unID+1
HOTSPOT -A/2, -B, 0, unID: unID=unID+1
HOTSPOT A/2, -B, 0, unID: unID=unID+1
HOTSPOT A/2, 0, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, 0, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, -B, ZZYZX, unID: unID=unID+1
HOTSPOT A/2, -B, ZZYZX, unID: unID=unID+1


				! --- MAIN BOX --- !

IF withHouse THEN
	MATERIAL HouseMaterial

	ROTx 90
	EXTRUDE 5, 0, 0, B, addStatus+16+32,
		-A/2, 0, 0,
		A/2, 0, 0,
		A/2, ZZYZX, 0,
		-A/2, ZZYZX, 0,
		-A/2, 0, -1

	POLY_ 5,
		-A/2, 0, 1,
		A/2, 0, 1,
		A/2, ZZYZX, 1,
		-A/2, ZZYZX, 1,
		-A/2, 0, -1

	IF isCovered THEN
		ADDz B
		POLY_ 5,
			-A/2, 0, 1,
			A/2, 0, 1,
			A/2, ZZYZX, 1,
			-A/2, ZZYZX, 1,
			-A/2, 0, -1
		DEL 1
	ENDIF
	DEL 1

	ADDz ZZYZX/2
ELSE
	ADDz meterBoxSizeZ/2
ENDIF

MATERIAL MainBoxMaterial

ADDy -EPS
ADD -meterBoxSizeX/2, -backPlateThk, -meterBoxSizeZ/2
BLOCK meterBoxSizeX, backPlateThk, meterBoxSizeZ
ADD meterBoxSizeX/10, -meterBoxDepth, meterBoxPosZ
BLOCK meterBoxWidth, meterBoxDepth, meterBoxHeight

IF gs_detlevel_3D_m = 2 THEN
	MATERIAL DialPlateMaterial

	ww = meterBoxWidth*0.80
	hh = meterBoxHeight*0.80
	ROTx 90
	ADD meterBoxWidth/10, meterBoxHeight/10, EPS
	POLY_ 5,
		0, 0, 1,
		ww, 0, 1,
		ww, hh, 1,
		0, hh, 1,
		0, 0, -1

	MATERIAL WheelMaterial
	ADD ww*0.10, hh*0.60, EPS
	ww = ww*0.80
	hh = hh*0.15
	POLY_ 5,
		0, -hh/2, 1,
		ww, -hh/2, 1,
		ww, hh/2, 1,
		0, hh/2, 1,
		0, -hh/2, -1
	DEL 3
ENDIF
DEL 5


END
