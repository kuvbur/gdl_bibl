
! Absorption Chiller


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


IF mbShow THEN

					! --- HOTSPOTS --- !

	unID = 1
	HOTSPOT cylindLength/2-0.44*ww, 0.58*dd, 0, unID: unID=unID+1
	HOTSPOT -cylindLength/2+0.44*ww, 0.58*dd, 0, unID: unID=unID+1
	HOTSPOT -cylindLength/2+0.44*ww, -0.58*dd, 0, unID: unID=unID+1
	HOTSPOT cylindLength/2-0.44*ww, -0.58*dd, 0, unID: unID=unID+1

	cl = A
	IF gs_detlevel_3D_m = 2 THEN cl = cylindLength

	HOTSPOT cl/2, B/2, legHeight+B/2, unID: unID=unID+1
	HOTSPOT -cl/2, B/2, legHeight+B/2, unID: unID=unID+1
	HOTSPOT -cl/2, -B/2, legHeight+B/2, unID: unID=unID+1
	HOTSPOT cl/2, -B/2, legHeight+B/2, unID: unID=unID+1

	HOTSPOT cl/2, 0, legHeight, unID: unID=unID+1
	HOTSPOT -cl/2, 0, legHeight, unID: unID=unID+1
	HOTSPOT -cl/2, 0, legHeight+B, unID: unID=unID+1
	HOTSPOT cl/2, 0, legHeight+B, unID: unID=unID+1


					! --- MAIN BOX --- !

	IF gs_detlevel_3D_m = 2 THEN
		ADDz legHeight+B/2
		ROTy 90
		ADDz -cylindLength/2
		CYLIND cylindLength, B/2
		DEL 3

		FOR i = 1 TO 2
			MULx 1-2*(i-1)
			ADD -cylindLength/2, 0, legHeight+B/2
			ROTy -90
			MUL dd, dd, ww
			PRISM_ 7, 0.05,
				-0.14, 0.60, 15,
				0.04, 0.60, 79,
				0.14, 0.50, 1079,
				0.14, -0.50, 79,
				0.04, -0.60, 1079,
				-0.14, -0.60, 15,
				-0.14, 0.60, -1

			ADDx -0.375
			PRISM_ 9, 0.05,
				-0.14, 0.425, 15,
				0, 0.60, 15,
				0.09, 0.60, 79,
				0.14, 0.55, 1079,
				0.14, -0.55, 79,
				0.09, -0.60, 1079,
				0, -0.60, 15,
				-0.14, -0.425, 15,
				-0.14, 0.425, -1
			DEL 1

			ADD 0.375, 0.265, 0
			PRISM_ 7, 0.05,
				-0.14, 0.265, 15,
				0.04, 0.265, 79,
				0.14, 0.165, 1079,
				0.14, -0.165, 79,
				0.04, -0.265, 1079,
				-0.14, -0.265, 15,
				-0.14, 0.265, -1
			DEL 1

			ADD 0.375+0.05, -0.285, 0
			PRISM_ 7, 0.05,
				-0.09, 0.15, 15,
				0.02, 0.15, 79,
				0.09, 0.08, 1079,
				0.09, -0.08, 79,
				0.02, -0.15, 1079,
				-0.09, -0.15, 15,
				-0.09, 0.15, -1
			DEL 5
		NEXT i
	ELSE
		ADDz legHeight+B/2
		ROTy 90
		ADDz -A/2
		CYLIND A, B/2
		DEL 3
	ENDIF

	ROTy 90
	CUTPOLYA 2, 2, 0,
		-legHeight-B/2, 0, 913,
		B/2, 360, 4013
	DEL 1

	FOR i = 1 TO 2
		MULx 1-2*(i-1)
		ADDx -cylindLength/2+0.44*ww

		ADD 0, -0.58*dd, legHeight+B/2-SQR((B/2)^2-(0.58*dd)^2)
		ROTy -90
		PRISM_ 6, -0.60,
			-0.76, 0, 15,
			0, 0, 15,
			0, -0.08, 15,
			-0.22, -0.30, 15,
			-0.76, -0.30, 15,
			-0.76, 0, -1
		DEL 2

		ROTz 90
		ROTy -90
		ADDz -0.58*dd
		PRISM_ 5, 1.16*dd,
			0, 0, 15,
			legHeight+B/2, 0, 15,
			legHeight+B/2, -0.30*ww, 15,
			0, -0.15*ww, 15,
			0, 0, -1
		DEL 5
	NEXT i

	CUTEND
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 13,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 13,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 13,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 13,
	ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConPosZ_5=ConPosZ_5, ConLength_5=ConLength_5, ConEdit_5 = 13,
	ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConPosZ_6=ConPosZ_6, ConLength_6=ConLength_6, ConEdit_6 = 13,
	ConPosX_7=ConPosX_7, ConPosY_7=ConPosY_7, ConPosZ_7=ConPosZ_7, ConLength_7=ConLength_7, ConEdit_7 = 13,
	ConPosX_8=ConPosX_8, ConPosY_8=ConPosY_8, ConPosZ_8=ConPosZ_8, ConLength_8=ConLength_8, ConEdit_8 = 13
END
