
! Fire Detect and Alarm Equipment
DrawingNum = 1



				! --- OTHER VIEWS --- !

ROT2 (SYMB_MIRRORED - NOT(SYMB_MIRRORED)) * (DrawingNum > 1) * SYMB_ROTANGLE

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180 * SYMB_MIRRORED,		 2	! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180 * NOT(SYMB_MIRRORED), 2	! Left


unID = 1

				! --- CONNECTIONS --- !

IF objType = 1 OR objType = 3 THEN		! Alarm Pressure Switch and Water Motor Alarm
	ConEdit_1 = 0
ENDIF

IF objType = 2 THEN		! Water Flow Indicato
	ConEdit_1 = 1
ENDIF

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1
DEL 1


PEN gs_cont_pen
SET FILL gs_fill_type


DrawingNum = 1
IF DrawingNum = 1 AND GLOB_CONTEXT <> 22 THEN	! Top

				! --- FIRE DETECTION AND ALARM EQUIPMENT --- !

	HOTSPOT2 0, 0, unID: unID=unID+1
	IF objType = 1 THEN		! Alarm Pressure Switch
		ADD2 -A/2, -A/2

		HOTSPOT2 A, 0, unID: unID=unID+1
		HOTSPOT2 A, B, unID: unID=unID+1
		HOTSPOT2 0, B, unID: unID=unID+1
		HOTSPOT2 0,0, unID: unID=unID+1

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,0, 1,
			0, B, 1,
			A, B, 1,
			A, 0, 1

		POLY2 4, 5,
			dx, dx,
			dx, B-dx,
			A-dx, B-dx,
			A-dx, dx

		POLY2 4, 5,
			dx2, dx2,
			dx2, B-dx2,
			A-dx2, B-dx2,
			A-dx2, dx2

		LINE2 dx,dx, dx2,dx2
		LINE2 A-dx,dx, A-dx2,dx2
		LINE2 dx,B-dx, dx2,B-dx2
		LINE2 A-dx,B-dx, A-dx2,B-dx2
		DEL 1
	ENDIF

	IF objType = 2 THEN		! Water Flow Indicator
		HOTSPOT2 -0.4*A, -B/2, unID: unID=unID+1
		HOTSPOT2 0.4*A, -B/2, unID: unID=unID+1
		HOTSPOT2 -0.4*A, B/2, unID: unID=unID+1
		HOTSPOT2 0.4*A, B/2, unID: unID=unID+1

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-A/2,	-meterW/2, 1,
			-A/2,	 meterW/2, 1,
			 A/2,	 meterW/2, 1,
			 A/2,	-meterW/2, 1
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.4*A, -B/2, 1,
			-0.4*A, -meterW/2, 1,
			 0.4*A, -meterW/2, 1,
			 0.4*A, -B/2, 1
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.4*A, B/2, 1,
			-0.4*A, meterW/2, 1,
			 0.4*A, meterW/2, 1,
			 0.4*A, B/2, 1
	ENDIF

	IF objType = 3 THEN		! Water Motor Alarm
		tck = 0.1*A
		HOTSPOT2 -A/2, B/2, unID: unID=unID+1
		HOTSPOT2 -A/2, -B/2, unID: unID=unID+1
		HOTSPOT2 A/2, 0.47*B, unID: unID=unID+1
		HOTSPOT2 A/2, -0.47*B, unID: unID=unID+1

		POLY2_B 8, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-A/2, B/2, 1,
			-A/2+tck, B/2, 0,
			-A/2+tck, 0.47*B, 1,
			 A/2, 0.47*B, 1,
			 A/2, -0.47*B, 1,
			-A/2+tck, -0.47*B, 0,
			-A/2+tck, -B/2, 1,
			-A/2, -B/2, 1
		LINE2	-A/2+tck, B/2,
				-A/2+tck, -B/2
	ENDIF
ENDIF
DEL TOP

END
