
! Fire Dept Inlet Connection


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


unID = 1

				! --- FIRE DEPARTMENT INLET CONNECTION --- !

IF gs_detlevel_3D_m = 2 THEN	! Detailed

	IF objType = 1 THEN		! 2-Way Flush FD Connection Outlet Body
		GOSUB 100
	ENDIF

	IF objType = 2 THEN		! 2-Way Flush FD Inlet Connection Plate
		GOSUB 200
	ENDIF

	IF objType = 3 THEN		! 3-Way Flush FD Connection Outlet Body
		GOSUB 300
	ENDIF

	IF objType = 4 THEN		! 3-Way Flush FD Inlet Connection Plate
		GOSUB 400
	ENDIF

	IF objType = 5 OR objType = 7 THEN		! 2-Way Projecting FD Inlet Connection
		IF objType = 5 THEN
			fthk = MIN(0.15*A, ConWidth_1/2)
			cx = fthk/3+ConWidth_1/2
		ELSE
			fthk = 0.05*A
			cx = fthk+ConWidth_1/2
		ENDIF

		REVOLVE 2,360, 1+2+4+16+32,
			0,	 B/2, 2,
			fthk,B/2, 2

		GROUP "Main"
			REVOLVE 2,360, 1+2+4+16+32,
				fthk,		1.1 * ConWidth_1/2, 2,
				cx+0.0001,	1.1 * ConWidth_1/2, 2
		ENDGROUP

		GROUP "Branch"
			pathL = A/SQR(2)
			ADDx cx
			ROTx 90
			CUTPLANE
			DEL 1
			ROTz 45
			REVOLVE 4,360, 1+2+4+16+32,
				0,			 1.1 * ConWidth_1/2, 2,
				pathL-0.1*A, 1.1 * ConWidth_1/2, 2,
				pathL-0.1*A, 1.2 * ConWidth_1/2, 2,
				pathL,	 1.2 * ConWidth_1/2, 2
			HOTSPOT pathL,0,0, unID: unID=unID+1
			CUTEND
			DEL 1

			ROTx 90
			CUTPLANE 180
			DEL 1
			ROTz -45
			REVOLVE 4,360, 1+2+4+16+32,
				0,			 1.1 * ConWidth_1/2, 2,
				pathL-0.1*A, 1.1 * ConWidth_1/2, 2,
				pathL-0.1*A, 1.2 * ConWidth_1/2, 2,
				pathL,	 1.2 * ConWidth_1/2, 2
			HOTSPOT pathL,0,0, unID: unID=unID+1
			CUTEND
			DEL 2
		ENDGROUP

		PLACEGROUP ADDGROUP("Main", "Branch")
		KILLGROUP "Main"
		KILLGROUP "Branch"
	ENDIF

	IF objType = 6 OR objType = 8 THEN		! 3-Way Projecting FD Inlet Connection
		IF objType = 6 THEN
			fthk = MIN(0.15*A, ConWidth_1/2)
			cx = fthk/3+ConWidth_1/2
		ELSE
			fthk = 0.05*A
			cx = fthk+ConWidth_1/2
		ENDIF

		REVOLVE 2,360, 1+2+4+16+32,
			0,	 B/2, 2,
			fthk,B/2, 2
		REVOLVE 4,360, 1+2+4+16+32,
			fthk,		 1.1 * ConWidth_1/2, 2,
			0.9*A, 1.1 * ConWidth_1/2, 2,
			0.9*A, 1.2 * ConWidth_1/2, 2,
			A,	 1.2 * ConWidth_1/2, 2
			HOTSPOT A,0,0, unID: unID=unID+1

		ROTy 90
		CUTPOLYA 2,1,0,
			0,0,913,
			1.1 * ConWidth_1/2, 360,4013
		DEL 1

		pathL = A/SQR(2)
		ADDx cx
		ROTz 45
		REVOLVE 4,360, 1+2+4+16+32,
			0,			 1.1 * ConWidth_1/2, 2,
			pathL-0.1*A, 1.1 * ConWidth_1/2, 2,
			pathL-0.1*A, 1.2 * ConWidth_1/2, 2,
			pathL,	 1.2 * ConWidth_1/2, 2
			HOTSPOT pathL,0,0, unID: unID=unID+1
		ROTz -90
		REVOLVE 4,360, 1+2+4+16+32,
			0,			 1.1 * ConWidth_1/2, 2,
			pathL-0.1*A, 1.1 * ConWidth_1/2, 2,
			pathL-0.1*A, 1.2 * ConWidth_1/2, 2,
			pathL,	 1.2 * ConWidth_1/2, 2
			HOTSPOT pathL,0,0, unID: unID=unID+1
		DEL 3
		CUTEND
	ENDIF

	IF objType = 9 THEN		! Single Flush FD Inlet Connection
		REVOLVE 6,360, 1+2+4+16+32,
			0, B/2, 2,
			0.1*A, B/2, 2,
			0.1*A, 1.2 * ConWidth_1/2, 2,
			0.7*A, 1.2 * ConWidth_1/2, 2,
			0.7*A, 1.5 * ConWidth_1/2, 2,
			A, 1.5 * ConWidth_1/2, 2
		HOTSPOT A,0,0, unID: unID=unID+1
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple

	IF objType = 1 THEN		! 2-Way Flush FD Connection Outlet Body
		GOSUB 100
	ENDIF

	IF objType = 2 THEN		! 2-Way Flush FD Inlet Connection Plate
		GOSUB 200
	ENDIF

	IF objType = 3 THEN		! 3-Way Flush FD Connection Outlet Body
		GOSUB 300
	ENDIF

	IF objType = 4 THEN		! 3-Way Flush FD Inlet Connection Plate
		GOSUB 400
	ENDIF

	IF objType = 5 OR objType = 7 THEN		! 2-Way Projecting FD Inlet Connection
		IF objType = 5 THEN
			fthk = 0.15*A
			cx = fthk/3+ConWidth_1/2
		ELSE
			fthk = 0.05*A
			cx = fthk+ConWidth_1/2
		ENDIF

		REVOLVE 2,360, 1+2+4+16+32,
			0,	 B/2, 2,
			fthk,B/2, 2

		GROUP "Main2"
			REVOLVE 2,360, 1+2+4+16+32,
				fthk,		1.1 * ConWidth_1/2, 2,
				cx+0.0001,	1.1 * ConWidth_1/2, 2
		ENDGROUP

		GROUP "Branch2"
			pathL = A/SQR(2)
			ADDx cx
			ROTx 90
			CUTPLANE
			DEL 1
			ROTz 45
			REVOLVE 2,360, 1+2+4+16+32,
				0,			 1.1 * ConWidth_1/2, 2,
				pathL, 1.1 * ConWidth_1/2, 2
			CUTEND
			DEL 1

			ROTx 90
			CUTPLANE 180
			DEL 1
			ROTz -45
			REVOLVE 2,360, 1+2+4+16+32,
				0,			 1.1 * ConWidth_1/2, 2,
				pathL, 1.1 * ConWidth_1/2, 2
			CUTEND
			DEL 2
		ENDGROUP

		PLACEGROUP ADDGROUP("Main2", "Branch2")
		KILLGROUP "Main2"
		KILLGROUP "Branch2"
	ENDIF

	IF objType = 6 OR objType = 8 THEN		! 3-Way Projecting FD Inlet Connection
		IF objType = 6 THEN
			fthk = 0.15*A
			cx = fthk/3+ConWidth_1/2
		ELSE
			fthk = 0.05*A
			cx = fthk+ConWidth_1/2
		ENDIF

		REVOLVE 2,360, 1+2+4+16+32,
			0,	 B/2, 2,
			fthk,B/2, 2
		REVOLVE 2,360, 1+2+4+16+32,
			fthk,	 1.1 * ConWidth_1/2, 2,
			A, 1.1 * ConWidth_1/2, 2

		ROTy 90
		CUTPOLYA 2,1,0,
			0,0,913,
			1.1 * ConWidth_1/2, 360,4013
		DEL 1

		pathL = A/SQR(2)
		ADDx cx
		ROTz 45
		REVOLVE 2,360, 1+2+4+16+32,
			0,		 1.1 * ConWidth_1/2, 2,
			pathL,	 1.1 * ConWidth_1/2, 2
		ROTz -90
		REVOLVE 2,360, 1+2+4+16+32,
			0,		 1.1 * ConWidth_1/2, 2,
			pathL,	 1.1 * ConWidth_1/2, 2
		DEL 3
		CUTEND
	ENDIF

	IF objType = 9 THEN		! Single Flush FD Inlet Connection
		REVOLVE 4,360, 1+2+4+16+32,
			0, B/2, 2,
			0.1*A, B/2, 2,
			0.1*A, 1.5 * ConWidth_1/2, 2,
			A, 1.5 * ConWidth_1/2, 2
	ENDIF
ENDIF

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = ConEdit_3,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = ConEdit_4
END


! 2-Way Flush FD Connection Outlet Body
100:
	bR = ZZYZX/2
	cx = 1.05 * bR
	RR = B/2-bR

	HOTSPOT cx,0,0, unID: unID=unID+1

	GROUP "Inlet_1"
		ROTy 90
		CYLIND cx, bR
		DEL 1
	ENDGROUP

	GROUP "Arch_1"
		ADDx cx+RR
		ROTy -90
		REVOLVE 2,180, 1+2+4+8,
			0,RR,901,
			bR,360,4001
		DEL 2
	ENDGROUP

	PLACEGROUP ADDGROUP("Inlet_1", "Arch_1")
	KILLGROUP "Inlet_1"
	KILLGROUP "Arch_1"

	ADD cx+RR, RR, 0
	ROTy 90
	PRISM_ 2,A-cx-RR,
		0,0,978,
		bR,360,4078
	DEL 2
	ADD cx+RR,-RR, 0
	ROTy 90
	PRISM_ 2,A-cx-RR,
		0,0,978,
		bR,360,4078
	DEL 2
RETURN


! 2-Way Flush FD Inlet Connection Plate
200:
	thk=MAX(0.005, 0.05*A)
	ROTy 90
	PRISM 4, thk,
		-ZZYZX/2, -B/2,
		 ZZYZX/2, -B/2,
		 ZZYZX/2,  B/2,
		-ZZYZX/2,  B/2

	HOTSPOT	-ZZYZX/2, -B/2, 0, unID: unID=unID+1
	HOTSPOT	 ZZYZX/2, -B/2, 0, unID: unID=unID+1
	HOTSPOT	 ZZYZX/2,  B/2, 0, unID: unID=unID+1
	HOTSPOT	-ZZYZX/2,  B/2, 0, unID: unID=unID+1
	DEL 1

	ADD thk, ConPosY_1,0
	ROTy 90
	CYLIND A-thk, ConWidth_1/2
	HOTSPOT 0,0,A-thk, unID: unID=unID+1
	DEL 2

	ADD thk, ConPosY_2,0
	ROTy 90
	CYLIND A-thk, ConWidth_2/2
	HOTSPOT 0,0,A-thk, unID: unID=unID+1
	DEL 2
RETURN


! 3-Way Flush FD Connection Outlet Body

300:
	bR = ZZYZX/2
	cx = 1.05 * bR
	RR = B/2-2*bR

!	HOTSPOT cx,0,0, unID: unID=unID+1

	L=B-2*(RR+bR)
	ADD cx,-L/2,0
	ROTx -90
	PRISM_ 2,L,
		0,0,974,
		bR,360,4074
	DEL 2

	ADDx cx
	ROTz 180
	ROTx 90
	ARMC bR,bR, cx, 0,0,90
	DEL 3

	IF A-cx > bR THEN
		ADDx cx
		ROTx 90
		ARMC bR,bR, A-cx, 0,0,90
		DEL 2
	ENDIF

	FOR i=1 TO 2
		ADD cx+RR,B/2-bR-RR,0
		ROTy -90
		REVOLVE 2,90, 1+2+4+8,
			0,RR,901,
			bR,360,4001
		DEL 2
		ADD cx+RR,B/2-bR, 0
		ROTy 90
		PRISM_ 2,A-cx-RR,
			0,0,978,
			bR,360,4078
		DEL 2

		MULy -1
	NEXT i
	DEL 2

RETURN


! 3-Way Flush FD Inlet Connection Plate
400:
	thk=MAX(0.005, 0.05*A)
	ROTy 90
	PRISM 4, thk,
		-ZZYZX/2, -B/2,
		 ZZYZX/2, -B/2,
		 ZZYZX/2,  B/2,
		-ZZYZX/2,  B/2

	HOTSPOT	-ZZYZX/2, -B/2, 0, unID: unID=unID+1
	HOTSPOT	 ZZYZX/2, -B/2, 0, unID: unID=unID+1
	HOTSPOT	 ZZYZX/2,  B/2, 0, unID: unID=unID+1
	HOTSPOT	-ZZYZX/2,  B/2, 0, unID: unID=unID+1
	DEL 1

	ADD thk, ConPosY_1,0
	ROTy 90
	CYLIND A-thk, ConWidth_1/2
	HOTSPOT 0,0,A-thk, unID: unID=unID+1
	DEL 2

	ADD thk, ConPosY_2,0
	ROTy 90
	CYLIND A-thk, ConWidth_2/2
	HOTSPOT 0,0,A-thk, unID: unID=unID+1
	DEL 2

	ADD thk, ConPosY_3,0
	ROTy 90
	CYLIND A-thk, ConWidth_3/2
	HOTSPOT 0,0,A-thk, unID: unID=unID+1
	DEL 2
RETURN
