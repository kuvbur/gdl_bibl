
! Vertical Fan Coil Unit


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
!!RESOL res


				! --- MAIN BOX --- !

ADD -A/2, -B/2, 0

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX-grillHeight, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX-grillHeight, unID: unID=unID+1
HOTSPOT A, B, ZZYZX-grillHeight, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX-grillHeight, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN
	ADDz legHeight
		BLOCK A, B, ZZYZX-legHeight-grillHeight
	DEL 1

	ADD legWidth, B/2-grillDepth/2, legHeight*(2/3)
		BLOCK grillWidth, grillDepth, legHeight/3
	DEL 1

	BLOCK legWidth, B, legHeight

	ADDx A-legWidth
		BLOCK legWidth, B, legHeight
	DEL 1

	ADD A/2-grillWidth/2, B/2-grillDepth/2, ZZYZX-grillHeight
	PRISM_ 10, grillHeight,
		0, 0, 15,
		grillWidth, 0, 15,
		grillWidth, grillDepth, 15,
		0, grillDepth, 15,
		0, 0, -1,
		grillFrameThick, grillFrameThick, 15,
		grillWidth-grillFrameThick, grillFrameThick, 15,
		grillWidth-grillFrameThick, grillDepth-grillFrameThick, 15,
		grillFrameThick, grillDepth-grillFrameThick, 15,
		grillFrameThick, grillFrameThick, -1

	HOTSPOT 0, 0, grillHeight, unID: unID=unID+1
	HOTSPOT grillWidth, 0, grillHeight, unID: unID=unID+1
	HOTSPOT grillWidth, grillDepth, grillHeight, unID: unID=unID+1
	HOTSPOT 0, grillDepth, grillHeight, unID: unID=unID+1

	 num2 = (grillDepth-grillFrameThick*2)/grillHeight
	 num = INT(num2)
	IF ABS(num-num2) < EPS THEN num = num-1
	FOR i=1 TO num
		ADD grillFrameThick, grillFrameThick+(num2-num)*grillHeight/2+grillHeight*(i-1), 0
		ROTx 45
			RECT grillWidth-grillFrameThick*2, grillHeight*SQR(2)
		DEL 2
	NEXT i
	DEL 1
ELSE
	BLOCK A, B, ZZYZX-grillHeight

	ADD A/2-grillWidth/2, B/2-grillDepth/2, ZZYZX-grillHeight
		BLOCK grillWidth, grillDepth, grillHeight

		HOTSPOT 0, 0, grillHeight, unID: unID=unID+1
		HOTSPOT grillWidth, 0, grillHeight, unID: unID=unID+1
		HOTSPOT grillWidth, grillDepth, grillHeight, unID: unID=unID+1
		HOTSPOT 0, grillDepth, grillHeight, unID: unID=unID+1
	DEL 1
ENDIF

DEL 1

END
