
! Horizontal Expansion Tank


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT -dTWidth/2, 0, legHeight+dTDepth/2, unID: unID=unID+1
HOTSPOT dTWidth/2, 0, legHeight+dTDepth/2, unID: unID=unID+1

endRed2 = endRed
IF gs_detlevel_3D = `Простой` THEN endRed2 = 0

HOTSPOT -dTWidth/2+endRed2, -dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1
HOTSPOT -dTWidth/2+endRed2, dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1
HOTSPOT dTWidth/2-endRed2, -dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1
HOTSPOT dTWidth/2-endRed2, dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1

HOTSPOT -dTWidth/2+endRed2, 0, legHeight, unID: unID=unID+1
HOTSPOT dTWidth/2-endRed2, 0, legHeight, unID: unID=unID+1
HOTSPOT -dTWidth/2+endRed2, 0, legHeight+dTDepth, unID: unID=unID+1
HOTSPOT dTWidth/2-endRed2, 0, legHeight+dTDepth, unID: unID=unID+1


				! --- TANK --- !

IF gs_detlevel_3D_m = 2 AND ConStatus_1 THEN
	GROUP "cylind"
		ADD -dTWidth/2, 0, legHeight+dTDepth/2
		ROTy 90
			EXTRUDE 2, 0, 0, endRed, 1+2+32,
				0, 0, 901,
				ConWidth_1/2, 360, 4001
		DEL 2
	ENDGROUP
ENDIF

IF gs_detlevel_3D_m = 2 THEN
	GROUP "tank"
		ADD -dTWidth/2, 0, legHeight+dTDepth/2
		CALL "MEP_m_Tank" PARAMETERS	tankLen = dTWidth, tankDia = dTDepth, endRed = endRed,
										ringOn = 0, typ = 2, tankPos = 0, tankMat = MainBoxMaterial,
										gs_cont_pen = gs_cont_pen, res = res
		DEL 1
	ENDGROUP
ELSE
	ADD -dTWidth/2, 0, legHeight+dTDepth/2
	ROTy 90
		CYLIND dTWidth, dTDepth/2
	DEL 2
ENDIF

IF gs_detlevel_3D_m = 2 THEN
	result_1=ADDGROUP("cylind","tank")
	PLACEGROUP result_1

	KILLGROUP "cylind"
	KILLGROUP "tank"
ENDIF


				! --- SUPPORT --- !

ADDx -dTWidth/2+endRed+0.15
ROTz 90
CALL "MEP_m_Tank_support_4" PARAMETERS	supLen = 0.10, supHeight = legHeight+dTDepth/4, supThick = 0.02,
										footWidth = dTDepth+0.06*dd, footThick = 0.025,
										tankDia = dTDepth, tankDeepening = dTDepth/4,
										gs_detlevel_3D_m = gs_detlevel_3D_m, supMat = MainBoxMaterial,
										gs_cont_pen = gs_cont_pen, res = res, hptsID = 100
DEL 2

ADDx dTWidth/2-endRed-0.15
ROTz -90
CALL "MEP_m_Tank_support_4" PARAMETERS	supLen = 0.10, supHeight = legHeight+dTDepth/4, supThick = 0.02,
										footWidth = dTDepth+0.06*dd, footThick = 0.025,
										tankDia = dTDepth, tankDeepening = dTDepth/4,
										gs_detlevel_3D_m = gs_detlevel_3D_m, supMat = MainBoxMaterial,
										gs_cont_pen = gs_cont_pen, res = res, hptsID = 200
DEL 2


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, ConEdgeVisible_1 = 0
END
