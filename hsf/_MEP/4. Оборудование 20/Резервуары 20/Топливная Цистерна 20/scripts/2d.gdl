
! Fuel Tank



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
HOTSPOT2 0, 0, unID: unID=unID+1

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top


				! --- MAIN BOX --- !

	HOTSPOT2 -dTWidth/2, -dTDepth/2, unID: unID=unID+1
	HOTSPOT2 dTWidth/2, -dTDepth/2, unID: unID=unID+1
	HOTSPOT2 dTWidth/2, 0, unID: unID=unID+1
	HOTSPOT2 dTWidth/2, dTDepth/2, unID: unID=unID+1
	HOTSPOT2 -dTWidth/2, dTDepth/2, unID: unID=unID+1
	HOTSPOT2 -dTWidth/2-sideTubeDist-sideTubeRadius, 0, unID: unID=unID+1

	PUT -dTWidth/2, -dTDepth/2, 1,
		dTWidth/2, -dTDepth/2, 1,
		dTWidth/2, dTDepth/2, 1,
		-dTWidth/2, dTDepth/2, 1,
		-dTWidth/2, -dTDepth/2, -1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	PUT -dTWidth/2, -sideTubeRadius, 1,
		-dTWidth/2-sideTubeDist, -sideTubeRadius, 1,
		-dTWidth/2-sideTubeDist, sideTubeRadius, 1001,
		-dTWidth/2, sideTubeRadius, 1,
		-dTWidth/2, -sideTubeRadius, -1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	ARC2 -dTWidth/2-sideTubeDist, 0, sideTubeRadius, -90, 90

	CIRCLE2 0, 0, openingRadius


				! --- SUPPORT --- !

	num = INT((dTWidth-0.30*ww-0.15*dd)/1.20)
	IF num < EPS THEN num = 1
	IF num > 3 THEN num = 3

	FOR p = 1 TO num+1
		ADD2 -dTWidth/2+0.15*ww+((dTWidth-0.30*ww-0.15*dd)/(num))*(p-1), 0
		ROT2 90
		CALL "MEP_m_Tank_support_4" PARAMETERS	supLen = 0.15*dd, supHeight = legHeight+dTDepth/4, supThick = 0.035,
												footWidth = dTDepth+0.08*dd, footThick = 0.035,
												tankDia = dTDepth, tankDeepening = dTDepth/4,
												gs_detlevel_3D_m = gs_detlevel_3D_m, supMat = MainBoxMaterial,
												gs_cont_pen = gs_cont_pen, gs_fill_pen = gs_fill_pen,
												gs_back_pen = gs_back_pen, gs_fill_type = gs_fill_type,
												hptsID = 200*p
		DEL 2
	NEXT p
ENDIF


				! --- OTHER VIEWS --- !

ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 2,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 2,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 2,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 2,
	ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConPosZ_5=ConPosZ_5, ConLength_5=ConLength_5, ConEdit_5 = 2,
	ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConPosZ_6=ConPosZ_6, ConLength_6=ConLength_6, ConEdit_6 = 2,
	ConPosX_7=ConPosX_7, ConPosY_7=ConPosY_7, ConPosZ_7=ConPosZ_7, ConLength_7=ConLength_7, ConEdit_7 = 2
DEL 1

END
