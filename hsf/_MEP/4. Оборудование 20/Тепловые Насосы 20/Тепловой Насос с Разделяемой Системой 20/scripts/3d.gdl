
! Split System Heat Pump


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen


unID = 1
IF mbShow THEN

					! --- HOTSPOTS --- !

	ADD -A/2, -B/2, 0
	HOTSPOT cornerCh, 0, 0, unID: unID=unID+1
	HOTSPOT A-cornerCh, 0, 0, unID: unID=unID+1
	HOTSPOT A, cornerCh, 0, unID: unID=unID+1
	HOTSPOT A, B-cornerCh, 	0, unID: unID=unID+1
	HOTSPOT A-cornerCh, B, 	0, unID: unID=unID+1
	HOTSPOT cornerCh, B, 0, unID: unID=unID+1
	HOTSPOT 0, B-cornerCh, 0, unID: unID=unID+1
	HOTSPOT 0, cornerCh, 0, unID: unID=unID+1

	HOTSPOT cornerCh, 0, ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT A-cornerCh, 0, ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT A, cornerCh, ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT A, B-cornerCh, 	ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT A-cornerCh, B, 	ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT cornerCh, B, ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT 0, B-cornerCh, ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT 0, cornerCh, ZZYZX-grillHeight, unID: unID=unID+1
	DEL 1


					! --- MAIN BOX --- !

	IF gs_detlevel_3D_m = 2 THEN
		ADD -A/2, -B/2, 0
		PRISM_ 8, ZZYZX-grillHeight,
			cornerCh, 0, 15,
			A-cornerCh, 0, 15,
			A, cornerCh, 15,
			A, B-cornerCh, 15,
			A-cornerCh, B, 15,
			cornerCh, B, 15,
			0, B-cornerCh, 15,
			0, cornerCh, 15

		 dist = edgeThick/TAN(67.5)
		ADDz ZZYZX-grillHeight
		FPRISM_ MainBoxMaterial,MainBoxMaterial,
			MainBoxMaterial, MainBoxMaterial,
			9, upperHeight, 45, upperHeight,
			cornerCh+dist, edgeThick, 15,
			A-cornerCh-dist, edgeThick, 15,
			A-edgeThick, cornerCh+dist, 15,
			A-edgeThick, B-cornerCh-dist, 15,
			A-cornerCh-dist, B-edgeThick, 15,
			cornerCh+dist, B-edgeThick, 15,
			edgeThick, B-cornerCh-dist, 15,
			edgeThick, cornerCh+dist, 15,
			cornerCh+dist, edgeThick, -1


						! --- GRILL --- !

		ADD A/2, B/2+(B/2-grillDia/2)-edgeThick-upperHeight, upperHeight
		CYLIND cylindHeight, grillDia/2

		ADDz grillHeight-upperHeight

		 Thick = 0.01
		 HalfThick = Thick/2
		 alpha_i = 360/SegmentsNum

		alpha = 0
		FOR i = 1 TO SegmentsNum
			PUT (SQR((insideDia/2)^2-HalfThick^2))*COS(alpha)+HalfThick*SIN(alpha), (SQR((insideDia/2)^2-HalfThick^2))*SIN(alpha)-HalfThick*COS(alpha), 1+3000*(i>1)
			PUT (SQR(Rad^2-HalfThick^2))*COS(alpha)+HalfThick*SIN(alpha), (SQR(Rad^2-HalfThick^2))*SIN(alpha)-HalfThick*COS(alpha), 1
			PUT (SQR(Rad^2-HalfThick^2))*COS(alpha)-HalfThick*SIN(alpha), (SQR(Rad^2-HalfThick^2))*SIN(alpha)+HalfThick*COS(alpha), 1
			PUT (SQR((insideDia/2)^2-HalfThick^2))*COS(alpha)-HalfThick*SIN(alpha), (SQR((insideDia/2)^2-HalfThick^2))*SIN(alpha)+HalfThick*COS(alpha), 1
			PUT 0, 0, 901

			ROTz alpha
			ADD SQR(Rad^2-HalfThick^2), -HalfThick, 0
			ROTy 90
			RECT grillHeight-upperHeight-cylindHeight, Thick
			DEL 3

			alpha = alpha+alpha_i
		NEXT i
		PUT (SQR((insideDia/2)^2-HalfThick^2))*COS(alpha)+HalfThick*SIN(alpha), (SQR((insideDia/2)^2-HalfThick^2))*SIN(alpha)-HalfThick*COS(alpha), 3001

		POLY_ NSP/3,

			GET(NSP)

		DEL 4
	ELSE
		ADD -A/2, -B/2, 0
		PRISM_ 8, ZZYZX-grillHeight+upperHeight,
			cornerCh, 0, 15,
			A-cornerCh, 0, 15,
			A, cornerCh, 15,
			A, B-cornerCh, 15,
			A-cornerCh, B, 15,
			cornerCh, B, 15,
			0, B-cornerCh, 15,
			0, cornerCh, 15
		DEL 1
		ADD -grillDia/2, -grillDia/2+(B/2-grillDia/2)-edgeThick-upperHeight, ZZYZX-grillHeight+upperHeight
		BLOCK grillDia, grillDia, grillHeight-upperHeight
		DEL 1
	ENDIF
ENDIF
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 17,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 17
END
