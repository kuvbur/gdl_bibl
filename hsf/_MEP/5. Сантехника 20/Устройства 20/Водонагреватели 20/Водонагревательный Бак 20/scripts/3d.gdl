
! Water Heater Tank


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN
	MainBoxMaterial = sMat
	operatorMaterial = sMat
ENDIF
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT -dTWidth/2, 0,	0, unID: unID=unID+1
HOTSPOT  dTWidth/2, 0,	0, unID: unID=unID+1
HOTSPOT  0,  dTWidth/2,	0, unID: unID=unID+1
HOTSPOT  0, -dTWidth/2,	0, unID: unID=unID+1

HOTSPOT -dTWidth/2, 0,	dTHeight, unID: unID=unID+1
HOTSPOT  dTWidth/2, 0,	dTHeight, unID: unID=unID+1
HOTSPOT  0,  dTWidth/2,	dTHeight, unID: unID=unID+1
HOTSPOT  0, -dTWidth/2,	dTHeight, unID: unID=unID+1


				! --- TANK --- !

PRISM_ 2, dTHeight,
	0,0,979,
	dTWidth/2, 360, 4079

IF ConStatus_4 THEN
	ADDz dTHeight + sh
	ROTy -90
	REVOLVE 2,360, 16+32,
		0,sc/2, 2,
		sh, ConWidth_4/2+ConWallThickness_4, 2
	DEL 2
ENDIF

CUTPOLYA 2,1,0,
	0,0,913,
	dTWidth/2, 360, 4013


				! --- BOXES --- !

MATERIAL operatorMaterial

ADD -lowerBoxWidth/2, -lowerBoxDepth, boxPosZ
BLOCK lowerBoxWidth, lowerBoxDepth, lowerBoxHeight
DEL 1

ADD -upperBoxWidth/2, -upperBoxDepth, boxPosZ+lowerBoxHeight
BLOCK upperBoxWidth, upperBoxDepth, upperBoxHeight
DEL 1

CUTEND

				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 15,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 15,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 14,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 1
END
