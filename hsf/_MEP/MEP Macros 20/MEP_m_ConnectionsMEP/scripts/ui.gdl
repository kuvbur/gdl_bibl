
! Zsolt Feke/2007


DIM tabIDs[]
DIM tabNames[]
DIM tabIcons[]

DIM idTab_CustomPage[]
	idTab_CustomPage[1] = 0

cPageIdx = 1
actIdx = 1
page_num = VARDIM1(ui_page_idx)
FOR i = 1 TO page_num
	IF ui_page_idx[i] = 1 THEN		! Built-in General Settings
		pageIdx = i
		tabIDs[pageIdx] = pageIdx
		IF ui_page_str_attr[1][1] = "" THEN
			tabNames[pageIdx] = cs_ui_pag[1]			! "General Settings"
		ELSE
			tabNames[pageIdx] = ui_page_str_attr[1][1]	! Custom Page Name
		ENDIF
		tabIcons[pageIdx] = "ui_tabpg_MEP_general_MEP"
		idTab_GeneralSettings = pageIdx

		actIdx = actIdx + 1
	ENDIF
	IF ui_page_idx[i] = 2 THEN		! Built-in Visualization
		pageIdx = i
		tabIDs[pageIdx] = pageIdx
		IF ui_page_str_attr[2][1] = "" THEN
			tabNames[pageIdx] = cs_ui_pag[2]			! "Visualization"
		ELSE
			tabNames[pageIdx] = ui_page_str_attr[2][1]	! Custom Page Name
		ENDIF
		tabIcons[pageIdx] = "ui_tabpg_MEP_visual_MEP"
		idTab_Visualization = pageIdx

		actIdx = actIdx + 1
	ENDIF
	IF ui_page_idx[i] = 3 THEN		! Built-in Connections
		pageIdx = i
		tabIDs[pageIdx] = pageIdx
		IF ui_page_str_attr[3][1] = "" THEN
			tabNames[pageIdx] = cs_ui_pag[3]			! "Connections"
		ELSE
			tabNames[pageIdx] = ui_page_str_attr[3][1]	! Custom Page Name
		ENDIF
		tabIcons[pageIdx] = "ui_tabpg_MEP_connections_MEP"
		idTab_Connections = pageIdx

		actIdx = actIdx + 1
	ENDIF
	FOR j = 1 TO 20
		IF ui_page_idx[i] = 3+j THEN
			pageIdx = i
			tabIDs[pageIdx] = pageIdx
			tabNames[pageIdx] = ui_page_str_attr[3+j][1]	! Custom Page Name
			tabIcons[pageIdx] = ui_page_str_attr[3+j][2]
			idTab_CustomPage[cPageIdx] = pageIdx

			cPageIdx = cPageIdx + 1
			actIdx = actIdx + 1
		ENDIF
	NEXT j
NEXT i

FILE_DEPENDENCE "ui_tabpg_MEP_general_MEP"
FILE_DEPENDENCE "ui_tabpg_MEP_visual_MEP"
FILE_DEPENDENCE "ui_tabpg_MEP_connections_MEP"

FOR  i = actIdx TO 20
	tabIDs[i]	= 0
	tabIcons[i]	= ""
	tabNames[i]	= ""
NEXT i

UI_CURRENT_PAGE gs_ui_current_page


! =============================================================================
! Tabpage Selector Control + Others
! =============================================================================

UI_PAGE gs_ui_current_page	! Place it on every page

CALL "ui_tabcontrol" PARAMETERS controlType = 0,
								gs_ui_current_page = gs_ui_current_page,
								nTabs = VARDIM1(ui_page_idx),
								tabTitles = tabNames,
								tabIcons = tabIcons,
								drawBox = 1

!!FOR i = 1 TO VARDIM1(ui_pict_name)							! Picture on Tabpage
!!	ui_cur_page = 0
!!	FOR j = 1 TO page_num
!!		IF ui_page_idx[j] = ui_pict_attr[i][1] THEN ui_cur_page = j
!!	NEXT j
!!
!!	IF ABS(ui_cur_page - gs_ui_current_page) < EPS AND ui_pict_name[i] <> "" THEN
!!		UI_PICT ui_pict_name[i],	ui_pict_attr[i][2],
!!									ui_pict_attr[i][3],
!!									ui_pict_attr[i][4],
!!									ui_pict_attr[i][5], 1
!!	ENDIF
!!NEXT i

FOR i = 1 TO VARDIM1(ui_sep_attr)							! Separator on Tabpage
	ui_cur_page = 0
	FOR j = 1 TO page_num
		IF ui_page_idx[j] = ui_sep_attr[i][1] THEN ui_cur_page = j
	NEXT j

	IF ABS(ui_cur_page - gs_ui_current_page) < EPS THEN
		UI_SEPARATOR	ui_sep_attr[i][2],
						ui_sep_attr[i][3],
						ui_sep_attr[i][4],
						ui_sep_attr[i][5]
	ENDIF
NEXT i

IF ui_set_attr[1][1] THEN 									! Fields on Tabpage
	UI_STYLE 0, 0
	var_num = 1
	FOR i = 1 TO VARDIM1(ui_set_attr)
		IF ui_set_attr[i][1] THEN
			ui_cur_page = 0
			FOR j = 1 TO page_num
				IF ui_page_idx[j] = ui_set_attr[i][1] THEN ui_cur_page = j
			NEXT j
		ENDIF

		IF ui_set_attr[i][1] THEN
			fields 			= ui_set_attr[i][2]
			distX 			= ui_set_attr[i][3]
			distY 			= ui_set_attr[i][4]
			textWidthOut 	= ui_set_attr[i][5]
			textWidthIn		= ui_set_attr[i][6]
			textHeight	 	= ui_set_attr[i][7]
			adX 			= ui_set_attr[i][8]
			adY	 			= ui_set_attr[i][9]
			next_row 		= 0
		ELSE
			IF ABS(ui_cur_page - gs_ui_current_page) < EPS THEN
				IF ABS(fields - 1) < EPS AND ui_set_attr[i][2] > -EPS OR ABS(fields - 3) < EPS AND ui_set_attr[i][2] > -EPS THEN
					UI_OUTFIELD ui_set_str[var_num][1], distX, distY+adY*next_row, textWidthOut, textHeight, ui_set_attr[i][2]
				ELSE
					adX = 0
				ENDIF
				IF ABS(fields - 2) < EPS AND ui_set_attr[i][2] > -EPS OR ABS(fields - 3) < EPS AND ui_set_attr[i][2] > -EPS THEN
					UI_INFIELD ui_set_str[var_num][2], distX+adX, distY+adY*next_row, textWidthIn, textHeight
				ENDIF
				next_row = next_row + 1
			ENDIF
			var_num = var_num + 1
		ENDIF
	NEXT i
ENDIF


! =============================================================================
! Current Tabpages
! =============================================================================

IF idTab_GeneralSettings THEN
	UI_PAGE idTab_GeneralSettings			! === General Settings ===

	IF ABS(ui_system_var_pos - 1) < EPS THEN
		UI_OUTFIELD `Система MEP:`, 220, 240, 60, 15, 0
		UI_INFIELD "ConSystem_1", 290, 240, 133, 15
	ENDIF
ENDIF

IF idTab_Visualization THEN
	UI_PAGE idTab_Visualization				! === Visualization ===

	UI_SEPARATOR 222, 44, 222, 252

		x1 = 25
		x2 = x1 + 30
		x3 = x2 + 150
		y = 45

		UI_STYLE 0,1
		UI_OUTFIELD `Символ Плана`, x2, y, x3-x2,18		! Plan Symbol
		UI_STYLE 0, 0
		y = y + 25

		cx = 40
		UI_PICT 4, x1, y, 24,17, 1		! ui_frame_pen_MEP.png
		UI_OUTFIELD `Перо Контура`, x2,y+3, x3-x2-cx-1,18	! Contour Pen
		UI_INFIELD "gs_cont_pen",	 x3-cx,y, cx,22
		y = y + 27

		UI_PICT 3, x1, y-2, 13,17, 1		! ui_fill_type_MEP.png

		UI_OUTFIELD `Тип Штриховки`, x2,y, x3-x2,15, 0			! Fill Type
		UI_INFIELD "gs_fill_type",		 x2,y+16, x3-x2,25
		y = y + 48

		cx = 40
		UI_PICT 2, x1, y, 24,17, 1		! ui_fill_pen_MEP.png

		UI_OUTFIELD `Перо Штриховки`, x2,y+3, x3-x2-cx-1,18, 0	! Fill Pen
		UI_INFIELD "gs_fill_pen",	 x3-cx,y, cx,22
		y = y + 27

		UI_PICT 1, x1, y, 24,17, 1		! ui_back_pen_MEP.png

		UI_OUTFIELD `Перо Фона Штриховки`, x2,y+3, x3-x2-cx-1,18, 0	! Background Pen
		UI_INFIELD "gs_back_pen",	  x3-cx,y, cx,22
		y = y + 25


		DIM ui_vis_default_str[][]
			ui_vis_default_str[1][1] = `3D-отображение`
			ui_vis_default_str[2][1] = `Покрытие`
			ui_vis_default_str[3][1] = `Покрытие`
			ui_vis_default_str[4][1] = `Изоляция`
			ui_vis_default_str[5][1] = `Покрытие`
			ui_vis_default_str[6][1] = `Покрытие Системы`

			ui_vis_default_str[1][2] = "gs_detlevel_3D"
			ui_vis_default_str[2][2] = "gs_PipeMat"
			ui_vis_default_str[3][2] = "gs_DuctMat"
			ui_vis_default_str[4][2] = "gs_InsMat"
			ui_vis_default_str[5][2] = "gs_BodyMat"
			ui_vis_default_str[6][2] = "useSysMat"

		x1 = 237
		x2 = x1 + 30
		x3 = x2 + 150
		y = 45

		UI_STYLE 0,1
		UI_OUTFIELD `3D-вид`, x2, y, x3-x2,18		! 3D View
		UI_STYLE 0, 0

		IF ui_vis_attr[1][1] THEN
			next_var = 2
			FOR i = 2 TO VARDIM1(ui_vis_attr)
				IF ui_vis_attr[i][1] THEN
					distX 		= ui_vis_attr[i][2]
					distY 		= ui_vis_attr[i][3]
					distRows 	= ui_vis_attr[i][4]
				ELSE
					idx = ui_vis_attr[1][next_var]
					desc = ui_vis_str[idx][1]
					var_name = ui_vis_str[idx][2]
					var_type = ui_vis_attr[i][3]
					IF ui_vis_str[idx][1] = "" AND ui_vis_attr[1][next_var] < 7 THEN desc = ui_vis_default_str[idx][1]
					IF ui_vis_str[idx][2] = "" AND ui_vis_attr[1][next_var] < 7 THEN var_name = ui_vis_default_str[idx][2]

					IF ui_vis_str[idx][2] = "" THEN
						IF ABS(ui_vis_attr[1][next_var] - 1) < EPS OR ABS(ui_vis_attr[1][next_var] - 6) < EPS THEN var_type = 1
					ENDIF

					IF  ABS(var_type - 1) < EPS THEN
						adX = 114
						UI_OUTFIELD desc, distX, distY, 110, 15
						UI_INFIELD var_name, distX+adX, distY, 65, 15
						distY = distY + 15 + distRows
					ELSE
						x2 = distX + 30
						x3 = x2 + 150

						IF ui_vis_attr[i][2] THEN
							UI_PICT 5, distX, distY-2, 12,17, 1			! ui_mat_lock_MEP.png
						ELSE
							UI_PICT 6, distX, distY-2, 12,17, 1			! ui_mat_MEP.png
						ENDIF

						UI_OUTFIELD desc, x2, distY, x3-x2, 15, ui_vis_attr[i][2]	! description
						UI_INFIELD var_name, x2, distY+16, x3-x2, 25 ! Variable
						distY = distY + 40 + distRows

					ENDIF
					next_var = next_var + 1
				ENDIF
			NEXT i
		ENDIF
ENDIF

IF idTab_Connections THEN
	UI_PAGE idTab_Connections				! === Connections ===

	DIM conTabIDs[]
	DIM conTabNames[]
	DIM conTabIcons[]

	actIdx = 1
	vis_con_count = 0
	FOR i = 1 TO MEP_NumberConnections
		IF ABS(MEP_NumConnectionData[i][22] - 1) < EPS THEN
			GOSUB 10
			vis_con_count = vis_con_count + 1
		ENDIF
	NEXT i
	IF vis_con_count < EPS THEN GOTO 99

	FOR i=actIdx TO 20
		conTabIDs[i]	= 0
		conTabIcons[i]	= ""
		conTabNames[i]	= ""
	NEXT i

	UI_SEPARATOR 222, 44, 222, 252

	IF ui_con_type[ui_current_con] <> 4 THEN
		px = 30
		py = 20
		cx = px
		cy = py

		UI_STYLE 0,1

		UI_infield{3} "ui_current_con", 15, 44, 195, cy+5,
			2, "",
			0, 0,
			cx, cy, px, py,
			conTabIcons[1],		conTabNames[1],		conTabIDs[1],
			conTabIcons[2],		conTabNames[2],		conTabIDs[2],
			conTabIcons[3],		conTabNames[3],		conTabIDs[3],
			conTabIcons[4],		conTabNames[4],		conTabIDs[4],
			conTabIcons[5],		conTabNames[5],		conTabIDs[5],
			conTabIcons[6],		conTabNames[6],		conTabIDs[6],
			conTabIcons[7],		conTabNames[7],		conTabIDs[7],
			conTabIcons[8],		conTabNames[8],		conTabIDs[8],
			conTabIcons[9],		conTabNames[9],		conTabIDs[9],
			conTabIcons[10],	conTabNames[10],	conTabIDs[10],
			conTabIcons[11],	conTabNames[11],	conTabIDs[11],
			conTabIcons[12],	conTabNames[12],	conTabIDs[12],
			conTabIcons[13],	conTabNames[13],	conTabIDs[13],
			conTabIcons[14],	conTabNames[14],	conTabIDs[14],
			conTabIcons[15],	conTabNames[15],	conTabIDs[15],
			conTabIcons[16],	conTabNames[16],	conTabIDs[16],
			conTabIcons[17],	conTabNames[17],	conTabIDs[17],
			conTabIcons[18],	conTabNames[18],	conTabIDs[18],
			conTabIcons[19],	conTabNames[19],	conTabIDs[19],
			conTabIcons[20],	conTabNames[20],	conTabIDs[20]
	ENDIF

	idx = 0
	distX = 32: distY = 75
!!	idx_str = STR(ui_con_type[ui_current_con], 1, 0)
!!	IF idx_str = "3" THEN idx_str = "1"
!!	IF ABS(MEP_NumConnectionData[ui_current_con][19]-1) < EPS THEN
!!		IF ABS(gs_AddConnections[ui_current_con][1] - 1) < EPS THEN
!!			IF ABS(MEP_NumConnectionData[ui_current_con][21]-1) < EPS THEN pict_name = "ui_rect_"+idx_str+"_HVAC_simpleb_MEP"
!!			IF ABS(MEP_NumConnectionData[ui_current_con][21]-2) < EPS THEN pict_name = "ui_circ_"+idx_str+"_HVAC_simpleb_MEP"
!!			IF ABS(MEP_NumConnectionData[ui_current_con][21]-3) < EPS THEN pict_name = "ui_oval_"+idx_str+"_HVAC_simpleb_MEP"
!!
!!		ELSE
!!			IF ABS(MEP_NumConnectionData[ui_current_con][21]-1) < EPS THEN pict_name = "ui_rect_"+idx_str+"_HVAC_flanged_MEP"
!!			IF ABS(MEP_NumConnectionData[ui_current_con][21]-2) < EPS THEN pict_name = "ui_circ_"+idx_str+"_HVAC_flanged_MEP"
!!			IF ABS(MEP_NumConnectionData[ui_current_con][21]-3) < EPS THEN pict_name = "ui_oval_"+idx_str+"_HVAC_flanged_MEP"
!!		ENDIF
!!		UI_PICT pict_name, distX, distY, 160, 160, 1
!!		idx = 1
!!	ENDIF
!!	IF ABS(MEP_NumConnectionData[ui_current_con][19]-2) < EPS THEN
!!		conStyle = 1
!!		IF ABS(gs_AddConnections[ui_current_con][1] - 1) < EPS THEN conStyle = 0
!!		IF ABS(gs_AddConnections[ui_current_con][1] - 3) < EPS AND ABS(MEP_NumConnectionData[ui_current_con][20]-1) < EPS THEN conStyle = 2
!!
!!		IF ABS(conStyle) < EPS THEN pict_name = "ui_circ_"+idx_str+"_Pipe_notflanged_MEP"
!!		IF ABS(conStyle - 1) < EPS THEN pict_name = "ui_circ_"+idx_str+"_Pipe_flanged_MEP"
!!		IF ABS(conStyle - 2) < EPS THEN pict_name = "ui_circ_"+idx_str+"_Pipe_HUB_male_MEP"
!!
!!		UI_PICT pict_name, distX, distY, 160, 160, 1
!!		idx = 2
!!	ENDIF

	idx_int = ui_con_type[ui_current_con]
	IF ABS(idx_int - 3) < EPS THEN idx_int = 1
	IF ABS(idx_int - 4) < EPS THEN idx_int = 0
	IF ABS(MEP_NumConnectionData[ui_current_con][19]-1) < EPS THEN
		pict_idx = MEP_NumConnectionData[ui_current_con][21]*100+gs_AddConnections[ui_current_con][1]*10+idx_int

		IF ABS(pict_idx - 110) < EPS THEN pict_idx = 16
		IF ABS(pict_idx - 111) < EPS THEN pict_idx = 17
		IF ABS(pict_idx - 112) < EPS THEN pict_idx = 18

		IF ABS(pict_idx - 120) < EPS THEN pict_idx = 19
		IF ABS(pict_idx - 121) < EPS THEN pict_idx = 20
		IF ABS(pict_idx - 122) < EPS THEN pict_idx = 21

		IF ABS(pict_idx - 210) < EPS THEN pict_idx = 22
		IF ABS(pict_idx - 211) < EPS THEN pict_idx = 23
		IF ABS(pict_idx - 212) < EPS THEN pict_idx = 24

		IF ABS(pict_idx - 220) < EPS THEN pict_idx = 25
		IF ABS(pict_idx - 221) < EPS THEN pict_idx = 26
		IF ABS(pict_idx - 222) < EPS THEN pict_idx = 27

		IF ABS(pict_idx - 310) < EPS THEN pict_idx = 28
		IF ABS(pict_idx - 311) < EPS THEN pict_idx = 29
		IF ABS(pict_idx - 312) < EPS THEN pict_idx = 30

		IF ABS(pict_idx - 320) < EPS THEN pict_idx = 31
		IF ABS(pict_idx - 321) < EPS THEN pict_idx = 32
		IF ABS(pict_idx - 322) < EPS THEN pict_idx = 33

		UI_PICT pict_idx, distX, distY, 160, 160, 1
		idx = 1
	ENDIF
	IF ABS(MEP_NumConnectionData[ui_current_con][19]-2) < EPS THEN
		conStyle = 2
		IF ABS(gs_AddConnections[ui_current_con][1] - 1) < EPS THEN conStyle = 1
		IF ABS(gs_AddConnections[ui_current_con][1] - 3) < EPS AND ABS(MEP_NumConnectionData[ui_current_con][20]-1) < EPS THEN conStyle = 3

		pict_idx = 10*conStyle+idx_int

		IF ABS(pict_idx - 10) < EPS THEN pict_idx = 7
		IF ABS(pict_idx - 11) < EPS THEN pict_idx = 8
		IF ABS(pict_idx - 12) < EPS THEN pict_idx = 9

		IF ABS(pict_idx - 20) < EPS THEN pict_idx = 10
		IF ABS(pict_idx - 21) < EPS THEN pict_idx = 11
		IF ABS(pict_idx - 22) < EPS THEN pict_idx = 12

		IF ABS(pict_idx - 30) < EPS THEN pict_idx = 13
		IF ABS(pict_idx - 31) < EPS THEN pict_idx = 14
		IF ABS(pict_idx - 32) < EPS THEN pict_idx = 15

		UI_PICT pict_idx, distX, distY, 160, 160, 1
		idx = 2
	ENDIF


	DIM	connections_str[][]
		connections_str[1][1] = ""

	DIM	connections_attr[][]

		connections_attr[1][1] = 1
		connections_attr[1][2] = 3
		connections_attr[1][3] = 235
		IF ui_con_type[ui_current_con] <> 4 THEN
			connections_attr[1][4] = 50
		ELSE
			connections_attr[1][4] = 75
		ENDIF
		connections_attr[1][5] = 115
		connections_attr[1][6] = 73
		connections_attr[1][7] = 15
		connections_attr[1][8] = 120
		connections_attr[1][9] = 25

	GOSUB 10+idx

	IF connections_attr[1][1] THEN 									! Fields on Tabpage
		UI_STYLE 0, 0
		var_num = 1
		FOR i = 1 TO VARDIM1(connections_attr)
			IF connections_attr[i][1] THEN
				fields 			= connections_attr[i][2]
				distX 			= connections_attr[i][3]
				distY 			= connections_attr[i][4]
				textWidthOut 	= connections_attr[i][5]
				textWidthIn		= connections_attr[i][6]
				textHeight	 	= connections_attr[i][7]
				adX 			= connections_attr[i][8]
				adY	 			= connections_attr[i][9]
				next_row 		= 0
			ELSE
				IF connections_attr[i][3] THEN
					IF ABS(fields - 1) < EPS OR ABS(fields - 3) < EPS THEN
						UI_OUTFIELD connections_str[var_num][1], distX, distY+adY*next_row, textWidthOut, textHeight, connections_attr[i][2]
					ELSE
						adX = 0
					ENDIF
					IF ABS(fields - 2) < EPS OR ABS(fields - 3) < EPS THEN
						UI_INFIELD connections_str[var_num][2], distX+adX, distY+adY*next_row, textWidthIn, textHeight
					ENDIF
					next_row = next_row + 1
				ENDIF
				var_num = var_num + 1
			ENDIF
		NEXT i

		IF ABS(ui_system_var_pos - 2) < EPS THEN
			bFlag = 0
			IF ABS(ui_con_type[ui_current_con] - 2) < EPS AND ABS(same_con-1) < EPS THEN bFlag = 4

			UI_OUTFIELD `Система MEP:`, 235, 230, 60, 15, bFlag
			paramName = "ConSystem_"+STR(ui_current_con, 1, 0)
			UI_INFIELD paramName, 310, 235, 118, 15
		ENDIF

		IF ABS(ui_system_var_pos - 3) < EPS AND ABS(ui_con_type[ui_current_con] - 1) < EPS THEN
			UI_OUTFIELD `Система MEP:`, 235, 230, 60, 15, 0
			paramName = "ConSystem_"+STR(ui_current_con, 1, 0)
			UI_INFIELD paramName, 310, 235, 118, 15
		ENDIF
	ENDIF
ENDIF

99:

FOR i = 1 TO VARDIM1(idTab_CustomPage)				! === Custom Pages ===
	UI_PAGE idTab_CustomPage[i]
NEXT i

GOTO 999


10:
	conTabIDs[actIdx] = actIdx
	conTabNames[actIdx] = MEP_StrConnectionData[i][3]			! name of connection
	IF ABS(MEP_NumConnectionData[i][19]-1) < EPS THEN
		conTabIcons[actIdx] = "ui_tabpg_MEP_duct_MEP"
		IF ABS(MEP_NumConnectionData[i][22]) < EPS THEN conTabIcons[actIdx] = "ui_tabpg_MEP_off_duct_MEP"
	ENDIF
	IF ABS(MEP_NumConnectionData[i][19]-2) < EPS THEN
		conTabIcons[actIdx] = "ui_tabpg_MEP_pipe_MEP"
		IF ABS(MEP_NumConnectionData[i][22]) < EPS THEN conTabIcons[actIdx] = "ui_tabpg_MEP_off_pipe_MEP"
	ENDIF
	IF ABS(MEP_NumConnectionData[i][19]-3) < EPS THEN
		conTabIcons[actIdx] = "ui_tabpg_MEP_electr_MEP"
		IF ABS(MEP_NumConnectionData[i][22]) < EPS THEN conTabIcons[actIdx] = "ui_tabpg_MEP_off_electr_MEP"
	ENDIF
	actIdx = actIdx + 1

	FILE_DEPENDENCE "ui_tabpg_MEP_duct_MEP"
	FILE_DEPENDENCE "ui_tabpg_MEP_off_duct_MEP"
	FILE_DEPENDENCE "ui_tabpg_MEP_pipe_MEP"
	FILE_DEPENDENCE "ui_tabpg_MEP_off_pipe_MEP"
	FILE_DEPENDENCE "ui_tabpg_MEP_electr_MEP"
	FILE_DEPENDENCE "ui_tabpg_MEP_off_electr_MEP"
RETURN

11:
	connections_str[1][1] = `Тип Сечения`
	connections_str[2][1] = "(A) "+`Ширина`
	connections_str[3][1] = "(B) "+`Высота`
	connections_str[4][1] = `Толщина Стенки`
	connections_str[5][1] = "(C) "+`Длина Соединения`
	connections_str[6][1] = `Тип Соединения`
	connections_str[7][1] = "(D) "+`Ширина Фланца`

	connections_str[1][2] = "ConType_"+STR(ui_current_con, 1, 0)
	connections_str[2][2] = "ConWidth_"+STR(ui_current_con, 1, 0)
	connections_str[3][2] = "ConDepth_"+STR(ui_current_con, 1, 0)
	connections_str[4][2] = "ConWallThickness_"+STR(ui_current_con, 1, 0)
	connections_str[5][2] = "ConLength_"+STR(ui_current_con, 1, 0)
	connections_str[6][2] = "ConConnectorType_"+STR(ui_current_con, 1, 0)
	connections_str[7][2] = "ConConnectorWidth_"+STR(ui_current_con, 1, 0)

	connections_attr[2][2] = 0: connections_attr[2][3] = 1
	connections_attr[3][2] = 0: connections_attr[3][3] = 1
	connections_attr[4][2] = 0: connections_attr[4][3] = 1
	connections_attr[5][2] = 0: connections_attr[5][3] = 1
	connections_attr[6][2] = 0: connections_attr[6][3] = 1
	connections_attr[7][2] = 0: connections_attr[7][3] = 1
	connections_attr[8][2] = 0: connections_attr[8][3] = 1

	IF ABS(MEP_NumConnectionData[ui_current_con][21]-2) < EPS THEN
		connections_str[5][1] 	= "(B) "+`Длина Соединения`
		connections_str[7][1] 	= "(C) "+`Ширина Фланца`
		connections_attr[4][3] 	= 0
	ENDIF

	IF ABS(gs_AddConnections[ui_current_con][1] - 1) < EPS THEN connections_attr[8][3] 	= 0

	IF ABS(ui_con_type[ui_current_con] - 1) < EPS OR ABS(ui_con_type[ui_current_con] - 3) < EPS THEN
		connections_str[7][1] = "(C) "+`Ширина Фланца`

		connections_attr[6][3] = 0

		IF ABS(ui_con_type[ui_current_con] - 3) < EPS THEN connections_attr[5][3] = 0

		IF ABS(MEP_NumConnectionData[ui_current_con][21]-2) < EPS THEN connections_str[7][1] 	= "(B) "+`Ширина Фланца`
	ENDIF

	IF ABS(ui_con_type[ui_current_con] - 2) < EPS THEN
		s_con_ID = 1
		FOR ss = 1 TO VARDIM1(same_con_ID)
			IF ABS(ui_current_con - same_con_ID[ss][1]) < EPS THEN s_con_ID = same_con_ID[ss][2]
		NEXT ss

		connections_str[1][1] = `Как и `+MEP_StrConnectionData[s_con_ID][3]
		connections_str[6][1] = `Тип Соединения`
		connections_str[7][1] = "(A) "+`Ширина Фланца`

		connections_str[1][2] = "same_con"

		connections_attr[3][3] = 0
		connections_attr[4][3] = 0
		connections_attr[5][3] = 0
		connections_attr[6][3] = 0

		IF ABS(same_con-1) < EPS THEN
			connections_attr[7][2] = 4
			connections_attr[8][2] = 4
		ENDIF
	ENDIF
RETURN

12:
	IF ABS(gs_AddConnections[ui_current_con][1] - 3) < EPS THEN
		ConnectorDiameter_str	= `Диаметр HUB`
		ConnectorLength_str	= `Длина HUB`
	ELSE
		ConnectorDiameter_str	= `Диаметр Фланца`
		ConnectorLength_str	= `Ширина Фланца`
	ENDIF

	connections_str[1][1] = `Номинальный Размер`
	connections_str[2][1] = "(A) "+`Диаметр`
	connections_str[3][1] = "(B) "+`Длина Соединения`
	connections_str[4][1] = `Тип Соединения`
	connections_str[5][1] = "(C) "+ConnectorDiameter_str
	connections_str[6][1] = "(D) "+ConnectorLength_str

	connections_str[1][2] = "ConNominalWidth_"+STR(ui_current_con, 1, 0)
	connections_str[2][2] = "ConWidth_"+STR(ui_current_con, 1, 0)
	connections_str[3][2] = "ConLength_"+STR(ui_current_con, 1, 0)
	connections_str[4][2] = "ConConnectorType_"+STR(ui_current_con, 1, 0)
	connections_str[5][2] = "ConConnectorWidth_"+STR(ui_current_con, 1, 0)
	connections_str[6][2] = "ConConnectorWidth2_"+STR(ui_current_con, 1, 0)

	connections_attr[2][2] = 0: connections_attr[2][3] = 1
	connections_attr[3][2] = 0: connections_attr[3][3] = 1
	connections_attr[4][2] = 0: connections_attr[4][3] = 1
	connections_attr[5][2] = 0: connections_attr[5][3] = 1
	connections_attr[6][2] = 0: connections_attr[6][3] = 1
	connections_attr[7][2] = 0: connections_attr[7][3] = 1

	IF ABS(conStyle-1) < EPS THEN
		connections_attr[6][3] = 0
		connections_attr[7][3] = 0
	ENDIF

	IF ABS(conStyle - 3) < EPS THEN
		connections_str[6][1] = "(C) "+ConnectorLength_str

		connections_attr[6][3] = 0
	ENDIF

	IF ABS(ui_con_type[ui_current_con] - 1) < EPS OR ABS(ui_con_type[ui_current_con] - 3) < EPS THEN
		connections_str[5][1] = "(B) "+ConnectorDiameter_str
		connections_str[6][1] = "(C) "+ConnectorLength_str
		IF ABS(conStyle - 3) < EPS THEN	connections_str[6][1] = "(B) "+ConnectorLength_str

		connections_attr[4][3] = 0
	ENDIF

	IF ABS(ui_con_type[ui_current_con] - 2) < EPS THEN
		s_con_ID = 1
		FOR ss = 1 TO VARDIM1(same_con_ID)
			IF ABS(ui_current_con - same_con_ID[ss][1]) < EPS THEN s_con_ID = same_con_ID[ss][2]
		NEXT ss

		connections_str[1][1] = `Как и `+MEP_StrConnectionData[s_con_ID][3]
		connections_str[5][1] = "(A) "+ConnectorDiameter_str
		connections_str[6][1] = "(B) "+ConnectorLength_str
		IF ABS(conStyle - 3) < EPS THEN	connections_str[6][1] = "(A) "+ConnectorLength_str

		connections_str[1][2] = "same_con"

		connections_attr[3][3] = 0
		connections_attr[4][3] = 0

		IF ABS(same_con-1) < EPS THEN
			connections_attr[5][2] = 4
			connections_attr[6][2] = 4
			connections_attr[7][2] = 4
		ENDIF
	ENDIF
RETURN

999:
