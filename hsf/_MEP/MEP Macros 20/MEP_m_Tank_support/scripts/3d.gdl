
! Gen Tank support
! Zsolt Feke


PEN gs_cont_pen
MATERIAL supMat
RESOL res

unID = hptsID
supWidth = 2*SQR(tankRad^2-(tankRad-tankDeepening)^2)
alphaTankDeepening = 180-2*ATN((supWidth/2)/tankDeepening)

aa = footWidth/2-footThick-(supWidth/2+supThick*SIN(alphaTankDeepening))
bb = supHeight-supThick*COS(alphaTankDeepening)-footThick
alphaSup = ATN(aa/bb)


XL = -footWidth/2+footThick+supThick/COS(alphaSup)
YL = -(supHeight-tankDeepening+tankRad-footThick)
M = TAN(90-alphaSup)

Ae = M^2+1
Be = 2*M*YL-2*(M^2)*XL
Ce = (M^2)*(XL^2)-2*M*XL*YL+YL^2-(tankRad+supThick)^2

IP_X = (-Be-SQR(Be^2-4*Ae*Ce))/(2*Ae)
IP_Y = M*(IP_X-XL)+YL+supHeight-tankDeepening+tankRad


ROTx 90

PUT	0, supHeight-tankDeepening+tankRad, 979,
	tankRad, 360, 4079

CUTPOLYA NSP/3, 2, 0,

	GET(NSP)

PUT	-footWidth/2, 0, 15
PUT	footWidth/2, 0, 15
PUT	footWidth/2, footThick, 15
IF gs_detlevel_3D THEN PUT	footWidth/2-footThick, footThick, 15
IF gs_detlevel_3D THEN PUT	supWidth/2+supThick*SIN(alphaTankDeepening), supHeight-supThick*COS(alphaTankDeepening), 15
PUT	supWidth/2, supHeight, 15
PUT	-supWidth/2, supHeight, 15
IF gs_detlevel_3D THEN PUT	-supWidth/2-supThick*SIN(alphaTankDeepening), supHeight-supThick*COS(alphaTankDeepening), 15
IF gs_detlevel_3D THEN PUT	-footWidth/2+footThick, footThick, 15
PUT	-footWidth/2, footThick, 15
PUT	-footWidth/2, 0, -1

IF gs_detlevel_3D THEN
	PUT	-supThick/2, footThick, 15
	PUT	-supThick/2, supHeight-tankDeepening-supThick+(tankRad+supThick-SQR((tankRad+supThick)^2-(supThick/2)^2)), 79
	PUT	0, supHeight-tankDeepening+tankRad, 979
	PUT	IP_X, IP_Y, 3015
	PUT	-footWidth/2+footThick+supThick/COS(alphaSup), footThick, 15
	PUT	-supThick/2, footThick, -1
	PUT	supThick/2, footThick, 15
	PUT	supThick/2, supHeight-tankDeepening-supThick+(tankRad+supThick-SQR((tankRad+supThick)^2-(supThick/2)^2)), 79
	PUT	0, supHeight-tankDeepening+tankRad, 979
	PUT	-IP_X, IP_Y, 3015
	PUT	footWidth/2-footThick-supThick/COS(alphaSup), footThick, 15
	PUT	supThick/2, footThick, -1
ENDIF

PRISM_ NSP/3, supLen,

	GET(NSP)

CUTEND

DEL 1

HOTSPOT footWidth/2, 0, 0, unID: unID=unID+1
HOTSPOT -footWidth/2, 0, 0, unID: unID=unID+1
HOTSPOT footWidth/2, -supLen, 0, unID: unID=unID+1
HOTSPOT -footWidth/2, -supLen, 0, unID: unID=unID+1
