hID =1
!!!!=============================== МАТЕРИАЛЫ =================================
MEP_UNKNOW = 0
MEP_ST3 = 1
MEP_AISI = 2
MEP_09G2S = 3
MEP_PVC = 4
MEP_GSH = 5
MEP_VG = 6
MEP_PE = 7
MEP_PPR_1 = 8
MEP_PPR_2 = 9
MEP_PPR_3 = 10
MEP_PPR_4 = 11
MEP_PPR_HV = 12
MEP_PVC_PIN = 13
MEP_COOPER = 14
!!!!============================= ТИПЫ ЭЛЕМЕНТОВ ===============================
MEP_TYPE_TUBE = 1
MEP_TYPE_PEREH = 2
MEP_TYPE_TR = 3
MEP_TYPE_OTVOD = 4
MEP_TYPE_FLANGE = 5
MEP_TYPE_OP = 6
MEP_TYPE_EQONTUBE = 7
MEP_TYPE_ZAGL = 8

EPS = 0.00001
flangeth1 = 0
flangeout1 = 0
flangeth2 = 0
flangeout2 = 0
show_el = 1
show_axis = 0
show_axis_and = 1
show_conventions = 0
show_size = 0
show_isol = 0
hide_conventions = 0
if (GLOB_SCRIPT_TYPE = 2 or GLOB_SCRIPT_TYPE = 3) and (GLOB_PREVIEW_MODE <> 0 or GLOB_FEEDBACK_MODE <> 0) then
	gs_detlevel_2D_m = 1
	gs_detlevel_3D_m = 1
else
	if not(calledparam) then call "Показ_систем" PARAMETERS All isMep=1, isEqp=0 RETURNED_PARAMETERS show_el, syst_name
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_axis", show_axis)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_axis_and", show_axis_and)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_isol", show_isol)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_conventions", show_conventions)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_size", show_size)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "gs_detlevel_2D_m",gs_detlevel_2D_m)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "gs_detlevel_3D_m", gs_detlevel_3D_m)
endif
flangeout1 = 0 : flangeout2 = 0
if haskey(mep_con.find.flangeout) then flangeout1 = mep_con.find.flangeout
if haskey(mep_con.find.flangeout1) then flangeout1 = mep_con.find.flangeout1
if haskey(mep_con.find.flangeout2) then
	flangeout2 = mep_con.find.flangeout2
else
	flangeout2 = flangeout1
endif
flangeth1 = 0.003
if abs(flangeout1)>EPS and flangeout1<EPS then
	flangeth1 = -flangeth1
	flangeout1 = abs(flangeout1)
endif
flangeth2 = 0.003
if abs(flangeout2)>EPS and flangeout2<EPS then
	flangeth2 = -flangeth2
	flangeout2 = abs(flangeout2)
endif

if haskey(mep_con.group) then
	if mep_con.group<>MEP_PVC_PIN and mep_con.group<>MEP_PPR_1 and mep_con.group<>MEP_PPR_2 and mep_con.group<>MEP_PPR_3 and mep_con.group<>MEP_PPR_4 and mep_con.group<>MEP_PPR_HV then
		flangeth1 = 0
		flangeout1 = 0
		flangeth2 = 0
		flangeout2 = 0
	endif
endif
is_sech = 0 : if (show_el-1)>EPS then is_sech = 1
IF gs_detlevel_3D_m = 2 THEN res = 22
IF gs_detlevel_3D_m = 1 THEN res = 12
IF gs_detlevel_3D_m = 0 THEN res = 12
!--------------------------------------Основной--------------------------------

name = ""
n = REQUEST ("Name_of_main", "", name)
!if STRSTR(name, "Прямолинейная Труба")=0 then show_axis = 0

pipeDiameterA		= MEP_NumConnectionData[1][12]
connectorDiameterA	= MEP_NumConnectionData[1][15]
IF connectorDiameterA < pipeDiameterA THEN connectorDiameterA = pipeDiameterA
connectorWidthA		= MEP_NumConnectionData[1][17]
connectedA			= MEP_NumConnectionData[1][18]
connectionSexA		= MEP_NumConnectionData[1][20]
connectionStyleA	= gs_AddConnections[1][1]

pipeDiameterB		= MEP_NumConnectionData[2][12]
connectorDiameterB	= MEP_NumConnectionData[2][15]
IF connectorDiameterB < pipeDiameterB THEN connectorDiameterB = pipeDiameterB
connectorWidthB		= MEP_NumConnectionData[2][17]
connectedB			= MEP_NumConnectionData[2][18]
connectionSexB		= MEP_NumConnectionData[2][20]
connectionStyleB	= gs_AddConnections[2][1]

flangeA = 1
IF connectorWidthA < EPS OR ABS(gs_AddConnections[1][1] - 1) < EPS THEN
	flangeA = 0
	connectorWidthA = 0
ENDIF
if flangeout1>EPS then flangeA = 0
cutbackA = connectorWidthA
cutbackCLA = connectorWidthA*(ABS(connectionStyleA-3) < EPS AND ABS(connectionSexA-2) < EPS AND ABS(connectedA-1) < EPS)
cutbackIA = connectorWidthA*(ABS(connectionStyleA-3) < EPS AND ABS(connectionSexA-1) < EPS)
IF ABS(connectionStyleA-3) < EPS AND ABS(connectionSexA-1) < EPS THEN
	flangeA = 0
	IF GLOB_CONTEXT = 5 OR ABS(connectedA) < EPS THEN
		cutbackA = 0
		connectorWidthA = 0
	ENDIF
ENDIF
edgeVisibleA = 1
IF flangeA THEN
	edgeVisibleA = 0
	cutbackA = 0
ENDIF

flangeB = 1
IF connectorWidthB < EPS OR ABS(gs_AddConnections[2][1] - 1) < EPS THEN
	flangeB = 0
	connectorWidthB = 0
ENDIF
if flangeout2>EPS then flangeB = 0
cutbackB = connectorWidthB
cutbackCLB = connectorWidthB*(ABS(connectionStyleB-3) < EPS AND ABS(connectionSexB-2) < EPS AND ABS(connectedB-1) < EPS)
cutbackIB = connectorWidthB*(ABS(connectionStyleB-3) < EPS AND ABS(connectionSexB-1) < EPS)
IF ABS(connectionStyleB-3) < EPS AND ABS(connectionSexB-1) < EPS THEN
	flangeB = 0
	IF GLOB_CONTEXT = 5 OR ABS(connectedB) < EPS THEN
		cutbackB = 0
		connectorWidthB = 0
	ENDIF
ENDIF
edgeVisibleB = 1
IF flangeB THEN
	edgeVisibleB = 0
	cutbackB = 0
ENDIF


						! ---------------- !
						! --- Z vector --- !
						! ---------------- !

dZx = -MEP_Position[3][2]*MEP_Position[2][3]+MEP_Position[3][3]*MEP_Position[2][2]
dZy = -MEP_Position[3][3]*MEP_Position[2][1]+MEP_Position[3][1]*MEP_Position[2][3]
dZz = -MEP_Position[3][1]*MEP_Position[2][2]+MEP_Position[3][2]*MEP_Position[2][1]


						! ------------------------ !
						! --- 2 points of pipe --- !
						! ------------------------ !

! --- x,y,z coordinates of 1st point ---

rx1 = MEP_Position[1][1]
ry1 = MEP_Position[1][2]
rz1 = MEP_Position[1][3]


! --- x,y,z coordinates of 2nd point ---

rx2 = rx1+MEP_Position[2][1]*MEP_StraightLength
ry2 = ry1+MEP_Position[2][2]*MEP_StraightLength
rz2 = rz1+MEP_Position[2][3]*MEP_StraightLength

dx = rx2-rx1
dy = ry2-ry1
dz = rz2-rz1

! --- dirAngOnXYPlane of the route ---

IF ABS(ABS(MEP_Position[2][3])-1) < EPS THEN
	 arCosVal = MEP_Position[3][1]/1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = -1 THEN arCosVal = -1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = 1 THEN arCosVal = 1
	dirAngOnXYPlane = ACS(arCosVal)-90
	IF SGN(MEP_Position[3][2]) = -1 THEN dirAngOnXYPlane = 180-dirAngOnXYPlane
ELSE
	IF ABS(dx)>EPS OR ABS(dy)>EPS THEN
		arCosVal = dx/(SQR(dx^2+dy^2))

		IF ABS(arCosVal) > 1 AND SGN(arCosVal) = -1 THEN arCosVal = -1
		IF ABS(arCosVal) > 1 AND SGN(arCosVal) = 1 THEN arCosVal = 1

		dirAngOnXYPlane = ACS(arCosVal)
		IF SGN(dy) = -1 THEN dirAngOnXYPlane = 360-dirAngOnXYPlane
	ENDIF
ENDIF
