
! Gen Tank
! Zsolt Feke


PEN gs_cont_pen
MATERIAL tankMat
RESOL res


IF typ = 1 THEN
	 chRad = endRed
	 tankRad = tankDia/2
	 sNum = MAX(INT(res/6), 2)
	 alpha_i = 90/sNum

		PUT 0, 0, 1
		 alpha = 0
		FOR i=1 TO sNum+1
			PUT chRad-chRad*COS(alpha), tankRad-chRad+chRad*SIN(alpha), 1-1*(i=1 OR i=sNum+1)
			 alpha = alpha+alpha_i
		NEXT i

		 alpha = 0
		FOR i=1 TO sNum+1
			PUT tankLen-chRad+chRad*SIN(alpha), tankRad-chRad+chRad*COS(alpha), 1-1*(i=1 OR i=sNum+1)
			 alpha = alpha+alpha_i
		NEXT i

		PUT tankLen, 0, 1

	ROTy -90*tankPos
	REVOLVE NSP/3, 360, 4+8,

		GET (NSP)

	IF ringOn THEN
		FOR i = 1 TO 2
			ADDx ring_AxisPos+(tankLen-ring_AxisPos*2-ringWidth)*(i=2)
			ROTy 90
			PRISM_ 4, ringWidth,
				0, 0, 979,
				tankRad+ringThick, 360, 4079,
				0, 0, 979,
				tankRad, 360, 4079
			DEL 2
		NEXT i
	ENDIF
	DEL 1
ENDIF


IF typ = 2 THEN
	 tankRad = tankDia/2
	if endRed<EPS then endRed=ring_AxisPos
	if endRed<EPS then endRed=EPS
	 alphaTankEnd = (180-2*ATN(tankRad/endRed))
	 Rad = tankRad/SIN(alphaTankEnd)

	 sNum = MAX(INT(res/6), 2)
	 alpha_i = alphaTankEnd/sNum

		PUT 0, 0, 1

		 alpha = 0
		FOR i=1 TO sNum+1
			PUT Rad-Rad*COS(alpha), Rad*SIN(alpha), 1-1*(i=sNum+1)
			 alpha = alpha+alpha_i
		NEXT i

		 alpha = 0
		FOR i=1 TO sNum+1
			PUT tankLen-Rad+Rad*COS(alphaTankEnd-alpha), Rad*SIN(alphaTankEnd-alpha), 1-1*(i=1)
			 alpha = alpha+alpha_i
		NEXT i

	ROTy -90*tankPos
	REVOLVE NSP/3, 360, 4+8,

		GET (NSP)

	IF ringOn THEN
		FOR i = 1 TO 2
			ADDx ring_AxisPos+(tankLen-ring_AxisPos*2-ringWidth)*(i=2)
			ROTy 90
			PRISM_ 4, ringWidth,
				0, 0, 979,
				tankRad+ringThick, 360, 4079,
				0, 0, 979,
				tankRad, 360, 4079
			DEL 2
		NEXT i
	ENDIF
	DEL 1
ENDIF
