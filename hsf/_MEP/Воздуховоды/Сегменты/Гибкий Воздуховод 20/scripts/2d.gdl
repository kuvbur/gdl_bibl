!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el
!!!================= Настройки видимости по диапазону ======================
!!!По умолчанию - вид не в сечении, элемент видим
is_sech = 0
is_show = 1
!!!Абсолютные значения границ видимости
rx1 = MEP_Position[1][1]
ry1 = MEP_Position[1][2]
rz1 = MEP_Position[1][3]
rx2 = rx1+MEP_Position[2][1]*MEP_StraightLength
ry2 = ry1+MEP_Position[2][2]*MEP_StraightLength
rz2 = rz1+MEP_Position[2][3]*MEP_StraightLength
dx = rx2-rx1
dy = ry2-ry1
dz = rz2-rz1
flag = 0
SYMB_POS_Z_temp = SYMB_POS_Z
IF SGN(dz) = 0 then
	_MEP_StraightLength = 0
	flag = 1
endif
IF SGN(dx) = 0 AND SGN(dy) = 0 and flag = 0 THEN
	_MEP_StraightLength = MEP_StraightLength
	IF rz1 > rz2 THEN
		SYMB_POS_Z_temp = SYMB_POS_Z
		SYMB_POS_Z = SYMB_POS_Z-_MEP_StraightLength
	ENDIF
	flag = 1
endif
if flag = 0 then
	_MEP_StraightLength = MEP_StraightLength
	Length=SQR(dx^2+dy^2)
	ar = ABS(dz)/_MEP_StraightLength
	br = Length/_MEP_StraightLength
	cr = ABS(dz)/Length
	IF rz1 > rz2 THEN
		SYMB_POS_Z_temp = SYMB_POS_Z
		SYMB_POS_Z = SYMB_POS_Z-_MEP_StraightLength
	ENDIF
	dz_temp = ABS(dz)
	dz = ABS(dz)-cutbackA*ar-cutbackB*ar
	SYMB_POS_Z_temp = SYMB_POS_Z
	SYMB_POS_Z = SYMB_POS_Z+cutbackA*ar
	flag = 1
endif
down_level = GLOB_CUTPLANES_INFO[3]+GLOB_ELEVATION + GLOB_CSTORY_ELEV
up_level = GLOB_CUTPLANES_INFO[2]+GLOB_ELEVATION + GLOB_CSTORY_ELEV
!!Ниже границы видимости
IF down_level - SYMB_POS_Z > EPS then
	if (down_level - (SYMB_POS_Z+_MEP_StraightLength)) > EPS THEN !!Закончился до границы
		stype = "1"
		is_show = 0
	else	
		stype = "2"
		is_sech = 1
	endif
endif
!!Выше границы видимости
IF up_level - SYMB_POS_Z < EPS then
	if up_level - (SYMB_POS_Z+_MEP_StraightLength) < EPS THEN !!Закончился до границы
		is_show = 0
		stype = "3"
	else
		stype = "4"
		is_sech = 1
	endif
endif
if is_sech then is_show = 1
SYMB_POS_Z = SYMB_POS_Z_temp
!!!================= Настройки видимости по диапазону ======================
if AC_AutoOnStoryVisibility and is_show=0 THEN end

numPoint = NumFlexibleDuctPoints

PEN gs_cont_pen
IF gs_fill_pen THEN SET FILL gs_fill_type

FOR i = 1 TO VARDIM1(MEP_ControlPoints)
	unID = i-1
	IF i < 2 THEN unID = hceID_1
	IF i > VARDIM1(MEP_ControlPoints)-1 THEN unID = hceID_2
	HOTSPOT2 MEP_ControlPoints[i][1], MEP_ControlPoints[i][2], unID
NEXT i

FOR i = 1 TO numPoint-1
	HOTLINE2 MEP_FlexibleDuctPoints[i][1], MEP_FlexibleDuctPoints[i][2], MEP_FlexibleDuctPoints[i+1][1], MEP_FlexibleDuctPoints[i+1][2]
NEXT i

PEN gs_cont_pen
IF gs_fill_pen THEN PEN gs_fill_pen
if not(show_axis) then PROJECT2{3} 3, 270, 3+32, 4, gs_back_pen, 0, 0, 0, PARAMETERS gs_detlevel_3D_m = DETLEV_3D_DETAILED, duct_show = 1, MEP_InsShow = 0, MEP_cline_show_3D = 0
PEN gs_cont_pen
if not(show_axis) then PROJECT2{3} 3, 270, 2, 8, 0, 0, 0, 0, PARAMETERS gs_detlevel_3D_m = DETLEV_3D_DETAILED, duct_show = 1, MEP_InsShow = 0, MEP_cline_show_3D = 0
IF MEP_InsShow and show_isol and not(show_axis) THEN PROJECT2{3} 3, 270, 2, 8, gs_back_pen, 0, 0, 0, PARAMETERS gs_detlevel_3D_m = DETLEV_3D_DETAILED, duct_show = 0, MEP_InsShow = 1, MEP_cline_show_3D = 0
tt=ConWidth_1/2
sumr = 0
shag = 0.3
IF (show_axis_and or show_axis) THEN
	PEN MEP_cline_pen
	SET LINE_TYPE MEP_cline_type
	FOR i = 1 TO numPoint-1
		dx = MEP_FlexibleDuctPoints[i+1][1] - MEP_FlexibleDuctPoints[i][1]
		dy = MEP_FlexibleDuctPoints[i+1][2] - MEP_FlexibleDuctPoints[i][2]
		r = SQR(dx*dx+dy*dy+dz*dz)
		if r>EPS then
			if dx>EPS and (dy>EPS or abs(dy)<EPS) then phi = atn(dy/dx)
			if dx>EPS and dy<EPS and abs(dy)>EPS then phi = atn(dy/dx)+360
			if dx<EPS and abs(dx)>EPS then phi = atn(dy/dx)+180
			if abs(dx)<EPS and dy>EPS then phi = 90
			if abs(dx)<EPS and dy<EPS and abs(dy)<EPS then phi = 270
			if abs(dx)<EPS and abs(dy)<EPS then phi = 0
			add2 MEP_FlexibleDuctPoints[i][1], MEP_FlexibleDuctPoints[i][2]
			rot2 phi-90
			nz = int(r/shag)
			if nz<EPS then
				sumr = sumr + r
				if sumr>shag then
					line2 -tt,r/2,tt,r/2
					sumr = 0
				endif
			else
				xx = 0
				for in=1 to nz
					line2 -tt,xx,tt,xx
					xx = xx + shag
				next in
				sumr = r-shag*nz
			endif
			del 2
		endif
		LINE2 MEP_FlexibleDuctPoints[i][1], MEP_FlexibleDuctPoints[i][2], MEP_FlexibleDuctPoints[i+1][1], MEP_FlexibleDuctPoints[i+1][2]
	NEXT i
ENDIF
