
! Sidewall Exhaust Fan


call "FM_types_MEP" parameters
DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 1		! Rectangle
	shape_typ_to_validation[2] = 1		! Circle
	shape_typ_to_validation[3] = 0		! Oval

!IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
!	gs_detlevel_3D_m = 2	! Detailed
!	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
!	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
!	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
!ELSE
!	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
!	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
!	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
!	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
!ENDIF
!
!VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
!VALUES "gs_detlevel_3D_m" 2, 1, 0

IF useSysMat THEN LOCK "MainBoxMaterial"


					! --- MAIN PARAMETERS --- !

IF NOT(SetProgram) THEN
	IF A < 0.20 THEN A = 0.20
	VALUES "A" RANGE [0.20,]

	IF B < A*0.30 THEN B = A*0.30
	VALUES "B" RANGE [A*0.30,]

	ZZYZX = A
	PARAMETERS ZZYZX = ZZYZX
ENDIF


					! --- CONNECTIONS --- !

PARAMETERS MEP_NumberConnections = 1


					! --- DUCT END CONNECTION --- !

ConID = 1
ConName = `Соединение Воздуховода`

ConDVecX = 0
ConDVecY = -1
ConDVecZ = 0

ConWVecX = 1
ConWVecY = 0
ConWVecZ = 0


IF NOT(SetProgram) THEN
	IF ConWidth_1 > A-0.15-ConWallThickness_1*2 THEN ConWidth_1 = A-0.15-ConWallThickness_1*2
	VALUES "ConWidth_1" RANGE (0, A-0.15-ConWallThickness_1*2]
	IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
		ConDepth_1 = ConWidth_1
		PARAMETERS ConDepth_1 = ConDepth_1
	ELSE
		IF ConDepth_1 > A-0.05-ConWallThickness_1*2  THEN ConDepth_1 = A-0.15-ConWallThickness_1*2
		VALUES "ConDepth_1" RANGE (0, A-0.15-ConWallThickness_1*2]
	ENDIF

	PARAMETERS ConPosX_1 = 0
	PARAMETERS ConPosY_1 = 0
	PARAMETERS ConPosZ_1 = 0
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_1,
	ConPosY = ConPosY_1,
	ConPosZ = ConPosZ_1,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_1,
	ConNominalDepth = ConNominalDepth_1,
	ConWidth = ConWidth_1,
	ConDepth = ConDepth_1,
	ConLength = ConLength_1,
	ConWallThickness = ConWallThickness_1,
	ConConnectorWidth = ConConnectorWidth_1,
	ConConnectorDepth = ConConnectorDepth_1,
	ConConnectorWidth2 = ConConnectorWidth2_1,
	ConToolType = ConToolType_1,
	ConStatus = ConStatus_1,
	ConType_1 = ConType_1,
	ConConnectorType_1 = ConConnectorType_1,
	ConSystem_1 = ConSystem_1,
	ui_page_idx = ui_page_idx,
	ui_current_con = ui_current_con,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF setProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF

values "FM_HeatOutput_unit" `З`, `БТЕ/час`
values "FM_RefrigeratingOutput_unit" `З`, `БТЕ/час`


if FM_HeatOutput_unit = `БТЕ/час` then
	hideparameter "FM_HeatOutput"
else
	hideparameter "FM_HeatOutput_btu"
endif

if FM_RefrigeratingOutput_unit = `БТЕ/час` then
	hideparameter "FM_RefrigeratingOutput"
else
	hideparameter "FM_RefrigeratingOutput_btu"
endif


!1000 W = 3414,426 Btu/hour
if GLOB_MODPAR_NAME = "FM_HeatOutput_btu" then
	FM_HeatOutput = FM_HeatOutput_btu / 3.414426
	parameters FM_HeatOutput = FM_HeatOutput
else
	FM_HeatOutput_btu = FM_HeatOutput * 3.414426
	parameters FM_HeatOutput_btu = FM_HeatOutput_btu
endif

!1000 W = 3414,426 Btu/hour
if GLOB_MODPAR_NAME = "FM_RefrigeratingOutput_btu" then
	FM_RefrigeratingOutput = FM_RefrigeratingOutput_btu / 3.414426
	parameters FM_RefrigeratingOutput = FM_RefrigeratingOutput
else
	FM_RefrigeratingOutput_btu = FM_RefrigeratingOutput * 3.414426
	parameters FM_RefrigeratingOutput_btu = FM_RefrigeratingOutput_btu
endif
