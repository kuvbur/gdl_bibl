
! Cabinet Ceiling Fan


call "FM_types_MEP" parameters
DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 1		! Rectangle
	shape_typ_to_validation[2] = 1		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0


					! --- MAIN PARAMETERS --- !

IF NOT(SetProgram) THEN
	IF dTWidth< 0.10 THEN dTWidth = 0.10
	VALUES "dTWidth" RANGE [0.10,]
	IF dTDepth< 0.10 THEN dTDepth = 0.10
	VALUES "dTDepth" RANGE [0.10,]
	IF dTHeight < 0.10 THEN dTHeight = 0.10
	VALUES "dTHeight" RANGE [0.10,]
ENDIF

A = dTWidth+frameWidth*2
PARAMETERS A = A
B = dTDepth+frameWidth*2
PARAMETERS B = B
ZZYZX = dTHeight+frameThick
PARAMETERS ZZYZX = ZZYZX
LOCK "A", "B", "ZZYZX"


					! --- CONNECTIONS --- !

PARAMETERS MEP_NumberConnections = 1


					! --- THE ONLY CONNECTION --- !

ConID = 1
ConName = `Соединение Воздуховода`

ConDVecX = 0
ConDVecY = -1
ConDVecZ = 0

ConWVecX = 1
ConWVecY = 0
ConWVecZ = 0


IF NOT(SetProgram) THEN
	IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
		IF ConWidth_1 > MIN(dTWidth-0.05-ConWallThickness_1*2, dTHeight-0.05-ConWallThickness_1*2) THEN ConWidth_1 = MIN(dTWidth-0.05-ConWallThickness_1*2, dTHeight-0.05-ConWallThickness_1*2)
		IF ConWidth_1 < EPS THEN ConWidth_1 = MIN(dTWidth-0.05-ConWallThickness_1*2 , dTHeight-0.05-ConWallThickness_1*2 )
		VALUES "ConWidth_1" RANGE (0, MIN(dTWidth-0.05-ConWallThickness_1*2 , dTHeight-0.05-ConWallThickness_1*2 )]
		ConDepth_1 = ConWidth_1
		PARAMETERS ConDepth_1 = ConDepth_1
		HIDEPARAMETER "ConDepth_1"
	ELSE

		VALUES "ConWidth_1" RANGE (0, dTWidth-0.05-ConWallThickness_1*2 ]
		IF ConDepth_1 > dTHeight-0.05 THEN ConDepth_1 = dTHeight-0.05-ConWallThickness_1*2
		IF ConDepth_1 < EPS THEN ConDepth_1 = dTHeight-0.05-ConWallThickness_1*2
		VALUES "ConDepth_1" RANGE (0, dTHeight-0.05-ConWallThickness_1*2 ]
	ENDIF

	VALUES "ConWallThickness_1" RANGE [0, MIN((dTWidth-0.05-ConWidth_1)/2, (dTHeight-0.05-ConDepth_1)/2)]

	PARAMETERS ConPosX_1 = 0
	PARAMETERS ConPosY_1 = -dTDepth/2
	PARAMETERS ConPosZ_1 = ZZYZX-0.025-ConDepth_1/2
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_1,
	ConPosY = ConPosY_1,
	ConPosZ = ConPosZ_1,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_1,
	ConNominalDepth = ConNominalDepth_1,
	ConWidth = ConWidth_1,
	ConDepth = ConDepth_1,
	ConLength = ConLength_1,
	ConWallThickness = ConWallThickness_1,
	ConConnectorWidth = ConConnectorWidth_1,
	ConConnectorDepth = ConConnectorDepth_1,
	ConConnectorWidth2 = ConConnectorWidth2_1,
	ConToolType = ConToolType_1,
	ConStatus = ConStatus_1,
	ConType_1 = ConType_1,
	ConConnectorType_1 = ConConnectorType_1,
	ConSystem_1 = ConSystem_1,
	ui_page_idx = ui_page_idx,
	ui_current_con = ui_current_con,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF setProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF
