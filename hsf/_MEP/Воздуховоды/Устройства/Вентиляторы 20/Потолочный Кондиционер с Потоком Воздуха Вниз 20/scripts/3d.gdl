
! Rooftop Downblast Fan


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOT --- !

unID = 1
HOTSPOT -footSize/2, footSize/2, 0, unID: unID=unID+1
HOTSPOT -footSize/2, -footSize/2, 0, unID: unID=unID+1
HOTSPOT footSize/2, -footSize/2, 0, unID: unID=unID+1
HOTSPOT footSize/2, footSize/2, 0, unID: unID=unID+1


				! --- MAIN BOX --- !

ADD -footSize/2, -footSize/2, 0
BLOCK footSize, footSize, footHeight
DEL 1

IF gs_detlevel_3D_m = 2 THEN
	ADDz footHeight
	MUL A/0.55, A/0.55, (ZZYZX-footHeight)/0.30
	ROTy -90
	REVOLVE 17, 360, 1+2,
		0, 0.205, 0,
		0.03, 0.175, 1,
		0.03, 0.175, 0,
		0.08, 0.175, 0,
		0.08, 0.275, 0,
		0.08495931856261, 0.2743514390142, 1,
		0.1173393995531, 0.2634165879698, 1,
		0.1441868002615, 0.2422681202354, 1,
		0.1633251636199, 0.2110254121445, 1,
		0.17, 0.175, 1,
		0.17, 0.1619326893048, 1,
		0.1732613163215, 0.1525944182926, 1,
		0.1816271073995, 0.1473168213693, 1,
		0.276745785201, 0.1253663572613, 1,
		0.2891343994958, 0.1192403513278, 1,
		0.2968165714526, 0.1095834687585, 1,
		0.30, 0.09613462139048, 1
	DEL 3
ELSE
	ADDz footHeight
	MUL A/0.55, A/0.55, (ZZYZX-footHeight)/0.30
	CYLIND 0.08, 0.205
	ADDz 0.08
	CONE 0.22, 0.275, 0.11, 90, 90
	DEL 3
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1

DEL TOP

END
