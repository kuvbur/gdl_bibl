!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
if not(not(show_axis) and GLOB_CONTEXT=2) then call "2dmacro" parameters all, type_symb = type_symb
if show_axis then
	hotspot 0,0,0
	end
endif
duct_show = not(show_axis)
res = 24
MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


				! --- MAIN BOX + LOUVERS --- !

unID = 1
HOTSPOT prismHalfWidth, prismHalfDepth, 0, unID: unID=unID+1
HOTSPOT -prismHalfWidth, prismHalfDepth, 0, unID: unID=unID+1
HOTSPOT -prismHalfWidth, -prismHalfDepth, 0, unID: unID=unID+1
HOTSPOT prismHalfWidth, -prismHalfDepth, 0, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN
	PRISM_ 10, edgeHeight,
		prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, prismHalfDepth, -1,
		prismInsideHalfWidth, prismInsideHalfDepth, 15,
		-prismInsideHalfWidth, prismInsideHalfDepth, 15,
		-prismInsideHalfWidth, -prismInsideHalfDepth, 15,
		prismInsideHalfWidth, -prismInsideHalfDepth, 15,
		prismInsideHalfWidth, prismInsideHalfDepth, -1

	ADDz edgeHeight
	ROTz 90
	ROTy -90

	DIM StraightPosition[3][3]
		StraightPosition[1][1] = 0
		StraightPosition[1][2] = 0
		StraightPosition[1][3] = 0

		StraightPosition[2][1] = 1
		StraightPosition[2][2] = 0
		StraightPosition[2][3] = 0

		StraightPosition[3][1] = 0
		StraightPosition[3][2] = 1
		StraightPosition[3][3] = 0

	DIM ConnectionData[2][26]

		FOR i = 1 TO 2
			FOR j = 1 TO 26
				ConnectionData[i][j] = 0
			NEXT j
		NEXT i

		ConnectionData[1][12] = A
		ConnectionData[1][13] = B
		ConnectionData[1][21] = 1

		ConnectionData[2][12] = ConWidth_1+ConWallThickness_1*2
		ConnectionData[2][13] = ConDepth_1+ConWallThickness_1*2
		ConnectionData[2][21] = MEP_NumConnectionData[1][21]

	CALL "MEP_m_Duct_4" PARAMETERS 	MEP_Position = StraightPosition,
									MEP_NumberConnections = 2,
									MEP_NumConnectionData = ConnectionData,
									MEP_StraightLength = ZZYZX-edgeHeight,
									duct_show = 1,
									showHtps = 0,
									gs_DuctMat = sMat,
									MEP_InsShow = 0,
									MEP_cline_show_3D = 0,
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_cont_pen = gs_cont_pen, MEP_StrConnectionData = MEP_StrConnectionData, calledparam = 1
	 DEL 3

	ang = 0
	IF A < B THEN
		A_temp = A
		A = B
		B = A_temp

		ang = 90
	ENDIF

	ROTz ang
	divThick = (B-edgeThick*2)/12
	louverThick = divThick-0.003
	IF ABS(MEP_NumConnectionData[1][21] - 1) < EPS THEN dist = MAX((A-ConWidth_1-ConWallThickness_1*2)/2,(B-ConDepth_1-ConWallThickness_1*2)/2)
	IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN dist = MAX((A-ConWidth_1-ConWallThickness_1*2)/2,(B-ConWidth_1-ConWallThickness_1*2)/2)
	height = edgeHeight + ((ZZYZX-edgeHeight)/dist)*(edgeThick+divThick+louverThick)
	ruledLHeight = 0.04
	IF (ruledLHeight-height) > -EPS THEN ruledLHeight = height-0.001
	ruledLHalfWidth = A/2-edgeThick-divThick*5
	ruledLHalfDepth = divThick
	ruledLInsideHalfWidth = ruledLHalfWidth-divThick

	RULED 4, 1+2+4+16+32,
		ruledLHalfWidth, ruledLHalfDepth, 0,
		-ruledLHalfWidth, ruledLHalfDepth, 0,
		-ruledLHalfWidth, -ruledLHalfDepth, 0,
		ruledLHalfWidth, -ruledLHalfDepth, 0,

		ruledLInsideHalfWidth, 0, ruledLHeight,
		-ruledLInsideHalfWidth, 0, ruledLHeight,
		-ruledLInsideHalfWidth, 0, ruledLHeight,
		ruledLInsideHalfWidth, 0, ruledLHeight

	FOR i = 1 TO 4
		ruledLHalfWidth = ruledLHalfWidth+divThick
		ruledLHalfDepth = ruledLHalfDepth+divThick
		ruledLInsideHalfWidth = ruledLHalfWidth-louverThick
		ruledLInsideHalfDepth = ruledLHalfDepth-louverThick

		RULED 4, 4+16+32,
			ruledLHalfWidth, ruledLHalfDepth, 0,
			-ruledLHalfWidth, ruledLHalfDepth, 0,
			-ruledLHalfWidth, -ruledLHalfDepth, 0,
			ruledLHalfWidth, -ruledLHalfDepth, 0,

			ruledLInsideHalfWidth, ruledLInsideHalfDepth, ruledLHeight,
			-ruledLInsideHalfWidth, ruledLInsideHalfDepth, ruledLHeight,
			-ruledLInsideHalfWidth, -ruledLInsideHalfDepth, ruledLHeight,
			ruledLInsideHalfWidth, -ruledLInsideHalfDepth, ruledLHeight
	NEXT i
	DEL 1
ELSE
	PRISM_ 5, ZZYZX,
		prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, prismHalfDepth, 15,
		-prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, -prismHalfDepth, 15,
		prismHalfWidth, prismHalfDepth, -1
ENDIF


							! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = sMat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, MEP_StrConnectionData = MEP_StrConnectionData

DEL TOP

END

