
call "2dmacro" parameters all, type_symb = type_symb
! Linear Slot Diffuser


CALL "MEP_m_RequestPlanViewGlobals" PARAMETERS ALL returned_parameters gs_symbtype_2D_m

call "FM_types_MEP" parameters
DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 1		! Rectangle
	shape_typ_to_validation[2] = 1		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0

IF useSysMat AND MEP_StrConnectionData[1][2] <> "0" THEN LOCK "MainBoxMaterial"
IF MEP_StrConnectionData[1][2] = "0" THEN LOCK "useSysMat"

values "gs_panelStyle" `1 щель`, `2 щели`, `3 щели`
values "gs_panelStyle_m" 1, 2, 3

if GLOB_MODPAR_NAME = "gs_panelStyle" then
	if gs_panelStyle = `1 щель`	then
		gs_panelStyle_m = 1
		dTDepth = 0.07
	endif
	if gs_panelStyle = `2 щели` then
		gs_panelStyle_m = 2
		dTDepth = 0.15
	endif
	if gs_panelStyle = `3 щели` then
		gs_panelStyle_m = 3
		dTDepth = 0.23
	endif
	parameters gs_panelStyle_m = gs_panelStyle_m
	parameters dTDepth = dTDepth
else
	if gs_panelStyle_m = 1 then gs_panelStyle = `1 щель`
	if gs_panelStyle_m = 2 then gs_panelStyle = `2 щели`
	if gs_panelStyle_m = 3 then gs_panelStyle = `3 щели`
	parameters gs_panelStyle = gs_panelStyle
endif

					! --- MAIN PARAMETERS --- !

IF NOT(SetProgram) THEN
	IF dTHeight < 0.051 THEN dTHeight = 0.051
	VALUES "dTHeight" RANGE [0.051,]
	IF dTWidth < 0.011 THEN dTWidth = 0.011
	VALUES "dTWidth" RANGE [0.011,]
	IF dTDepth < 0.011 THEN dTDepth = 0.011
	VALUES "dTDepth" RANGE [0.011,]
	VALUES "frameWidth" RANGE [0,]
ENDIF

PARAMETERS A = dTWidth+frameWidth*2
PARAMETERS B = dTDepth+frameWidth*2
PARAMETERS zzyzx = dTHeight
LOCK "A", "B", "ZZYZX"


					! --- CONNECTIONS --- !

PARAMETERS MEP_NumberConnections = 1


					! --- THE ONLY CONNECTION --- !

ConID = 1
ConName = `Соединение Конца Воздуховода`

ConDVecX = 0
ConDVecY = -1
ConDVecZ = 0

ConWVecX = 1
ConWVecY = 0
ConWVecZ = 0

IF NOT(SetProgram) THEN
	IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
		VALUES "ConWidth_1" RANGE (0, MIN(dTWidth-ConWallThickness_1*2, dTHeight-ConWallThickness_1*2)]
		PARAMETERS ConDepth_1 = ConWidth_1
		HIDEPARAMETER "ConDepth_1"
	ELSE
		VALUES "ConWidth_1" RANGE (0, dTWidth-ConWallThickness_1*2]
		VALUES "ConDepth_1" RANGE (0, dTHeight-ConWallThickness_1*2]
	ENDIF

	VALUES "ConLength_1" RANGE [0, ]
	VALUES "ConPosX_1" RANGE [-dTWidth/2+ConWidth_1/2+ConWallThickness_1, dTWidth/2-ConWidth_1/2-ConWallThickness_1]
	PARAMETERS ConPosY_1 = -dTDepth/2
	PARAMETERS ConPosZ_1 = dTHeight/2

	VALUES "ConWallThickness_1" RANGE [0, MIN((dTWidth-ConWidth_1)/2, (dTHeight-ConDepth_1)/2)]
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_1,
	ConPosY = ConPosY_1,
	ConPosZ = ConPosZ_1,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_1,
	ConNominalDepth = ConNominalDepth_1,
	ConWidth = ConWidth_1,
	ConDepth = ConDepth_1,
	ConLength = ConLength_1,
	ConWallThickness = ConWallThickness_1,
	ConConnectorWidth = ConConnectorWidth_1,
	ConConnectorDepth = ConConnectorDepth_1,
	ConConnectorWidth2 = ConConnectorWidth2_1,
	ConToolType = ConToolType_1,
	ConStatus = ConStatus_1,
	ConType_1 = ConType_1,
	ConConnectorType_1 = ConConnectorType_1,
	ConSystem_1 = ConSystem_1,
	ui_page_idx = ui_page_idx,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF SetProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF

FOR k = 1 TO MEP_NumberConnections
	FOR i = 1 TO 9
		j = 1+INT((i-0.5)/3)
		MEP_NumConnectionData[k][i] = outCoords[j+3*(k-1)][i-3*(j-1)]
		PARAMETERS MEP_NumConnectionData[k][i] = MEP_NumConnectionData[k][i]
	NEXT i
NEXT k
