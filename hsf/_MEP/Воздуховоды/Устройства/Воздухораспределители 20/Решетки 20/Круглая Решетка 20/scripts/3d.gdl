!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
if not(not(show_axis) and GLOB_CONTEXT=2) then
	rotx 90
	call "2dmacro" parameters all, type_symb = type_symb
	del 1
endif
if show_axis then
	hotspot 0,0,0
	end
endif
duct_show = duct_show*not(show_axis)

MATERIAL sMat
PEN gs_cont_pen
RESOL res

				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


unID = 1
HOTSPOT prismHalfWidth, 0, 0, unID: unID=unID+1
HOTSPOT -prismHalfWidth, 0, 0, unID: unID=unID+1
HOTSPOT 0, -prismHalfWidth, 0, unID: unID=unID+1
HOTSPOT 0, prismHalfWidth, 0, unID: unID=unID+1
HOTSPOT prismInsideHalfWidth, 0, 0, unID: unID=unID+1
HOTSPOT -prismInsideHalfWidth, 0, 0, unID: unID=unID+1
HOTSPOT 0, -prismInsideHalfWidth, 0, unID: unID=unID+1
HOTSPOT 0, prismInsideHalfWidth, 0, unID: unID=unID+1
HOTSPOT prismInsideHalfWidth, 0, ConLength_1, unID: unID=unID+1
HOTSPOT -prismInsideHalfWidth, 0, ConLength_1, unID: unID=unID+1
HOTSPOT 0, -prismInsideHalfWidth, ConLength_1, unID: unID=unID+1
HOTSPOT 0, prismInsideHalfWidth, ConLength_1, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN
	ADDz -frameThick
	PRISM_ 4, frameThick,
		0, 0, 979,
		prismHalfWidth, 360, 4079,
		0, 0, 979,
		prismInsideHalfWidth, 360, 4079

	IF ConWidth_1+ConWallThickness_1*2 > 0.03 THEN

		louverDepth = MAX(frameThick, MIN(0.025, ConLength_1))
		louverDist = louverSize
		num = INT((ConWidth_1+ConWallThickness_1*2)/louverDist)
		louverDist = (ConWidth_1+ConWallThickness_1*2)/num

		FOR j = 1 TO 1+(grilleType < 2)
			ROTz 90-90*(j-1)
			FOR i = 1 TO num-1
				louverPos = prismInsideHalfWidth-louverDist*i
				louverHalfWidth = SQR((prismInsideHalfWidth*0.99)^2-louverPos^2)
				ADD -louverHalfWidth, louverPos, 0
				ROTx 90
				RECT louverHalfWidth*2, louverDepth
				DEL 2
			NEXT i
			DEL 1
		NEXT j
	ENDIF
	DEL 1
ELSE
	ADDz -frameThick

	PRISM_ 2, frameThick,
		0, 0, 979,
		prismHalfWidth, 360, 4079

	DEL 1
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, MEP_StrConnectionData = MEP_StrConnectionData

DEL TOP

END
