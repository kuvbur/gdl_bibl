!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

IF GLOB_CONTEXT = 2 THEN res = 24

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

call "2dmacro" parameters all, type_symb = type_symb, dirAngOnXYPlane = dirAngOnXYPlane
if show_axis then 
	hotspot 0,0,0
	end
endif

				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


				! --- HOTSPOT --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

HOTSPOT -A/2, -dTWidth/2, 0, unID: unID=unID+1
HOTSPOT -A/2, dTWidth/2, 0, unID: unID=unID+1
HOTSPOT A/2, dTWidth/2, 0, unID: unID=unID+1
HOTSPOT A/2, -dTWidth/2, 0, unID: unID=unID+1


				! --- DAMPER --- !

DIM StraightPosition[][]

StraightPosition[1][1] = -A/2
StraightPosition[1][2] = 0
StraightPosition[1][3] = 0

StraightPosition[2][1] = 1
StraightPosition[2][2] = 0
StraightPosition[2][3] = 0

StraightPosition[3][1] = 0
StraightPosition[3][2] = 1
StraightPosition[3][3] = 0

CALL "MEP_m_Duct_4" PARAMETERS 	MEP_Position = StraightPosition,
								gs_Connections = gs_Connections,
								MEP_NumberConnections = MEP_NumberConnections,
								MEP_NumConnectionData = MEP_NumConnectionData,
								MEP_StrConnectionData = MEP_StrConnectionData,
								MEP_StraightLength = A,
								gs_AddConnections = gs_AddConnections,
								duct_show = 1,
								gs_DuctMat = MainBoxMaterial,
								MEP_InsShow = 0,
								MEP_cline_show_3D = 0,
								gs_detlevel_3D_m = gs_detlevel_3D_m,
								gs_cont_pen = gs_cont_pen


ROTz -90
ADDy -A/2
ROTx 90
POLY_ 2,
	0, 0, 901,
	dTWidth/2, 360, 4001
ADDz -A
POLY_ 2,
	0, 0, 901,
	dTWidth/2, 360, 4001
DEL 3

ADD 0, -A/2, ZZYZX/2
ROTx -90
IF (ZZYZX-0.025) > -EPS AND A > 0.075 THEN
	CUTPOLYA 2, 2, 0,
		0, dTWidth/2, 979,
		dTWidth/2, 360, 4079
ENDIF
DEL 2

IF (ZZYZX-0.025) > -EPS AND A > 0.075 THEN
	RESOL res/2
	ADD dTWidth/2, 0, 0
	ROTy 90
	ROTz 180
	IF gs_detlevel_3D_m = 2 OR GLOB_CONTEXT = 2 THEN
		ADDz -dTWidth/2
		PRISM_ 2, dTWidth/2+0.025,
			0, 0, 979,
			0.00625, 360, 4079
		DEL 1

		Length = A/2-0.0375

		ADDz 0.025
		PRISM_ 5, 0.005,
			0.0125, 0, 79,
			0.0125, Length, 79,
			-0.0125, Length, 1079,
			-0.0125, 0, 79,
			0.0125, 0, 1079
		DEL 1
	ELSE
		ADD -0.0125, -0.0125, -dTWidth/2
			BLOCK 0.025, Length+0.025, dTWidth/2+0.03
		DEL 1
	ENDIF
	DEL 3
	RESOL res
	CUTEND
ENDIF
DEL 2

END
