call "mep_ov_clapan_param" parameters all


call "FM_types_MEP" parameters

DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 1		! Rectangle
	shape_typ_to_validation[2] = 0		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0

IF useSysMat AND MEP_StrConnectionData[1][2] <> "0" THEN LOCK "MainBoxMaterial"
IF MEP_StrConnectionData[1][2] = "0" THEN LOCK "useSysMat"


					! --- MAIN PARAMETERS --- !

	IF NOT(setProgram) THEN
		VALUES "bladeNum" RANGE [1, ]
		IF A < 0.06 THEN
			A = 0.06
			PARAMETERS A = A
		ENDIF
		VALUES "A" RANGE [0.06, ]
	ENDIF


					! --- CONNECTIONS --- !

	PARAMETERS MEP_NumberConnections = 2


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Впускное Соединение Воздуховода`

	ConDVecX = -1
	ConDVecY = 0
	ConDVecZ = 0

	ConWVecX = 0
	ConWVecY = -1
	ConWVecZ = 0


	IF NOT(setProgram) THEN
		isConLength = 1
		IF ABS(gs_AddConnections[1][1] - 1) < EPS THEN isConLength = 0
		ConLength_1 = ConWallThickness_1*isConLength
		PARAMETERS ConLength_1 = ConLength_1

		IF ConWidth_1 < EPS THEN
			ConWidth_1 = 0.001
			PARAMETERS ConWidth_1 = ConWidth_1
		ENDIF
		VALUES "ConWidth_1" RANGE (0,]

		IF ConDepth_1 < EPS THEN
			ConDepth_1 = 0.001
			PARAMETERS ConDepth_1 = ConDepth_1
		ENDIF
		VALUES "ConDepth_1" RANGE (0,]

		IF ConWallThickness_1 < 0 THEN
			ConWallThickness_1 = 0
			PARAMETERS ConWallThickness_1 = ConWallThickness_1
		ENDIF
		VALUES "ConWallThickness_1" RANGE [0,]

		ConPosX_1 = -A/2+ConLength_1
		PARAMETERS ConPosX_1 = ConPosX_1
		ConPosY_1 = 0
		PARAMETERS ConPosY_1 = ConPosY_1
		ConPosZ_1 = 0
		PARAMETERS ConPosZ_1 = ConPosZ_1

		IF B < ConWallThickness_1*2 THEN
			B = ConWallThickness_1*2+EPS
			PARAMETERS B = B
		ENDIF
		VALUES "B" RANGE (ConWallThickness_1*2,]

		IF GLOB_MODPAR_NAME = "B" THEN
			ConWidth_1 = B - ConWallThickness_1*2
			PARAMETERS ConWidth_1 = ConWidth_1
		ENDIF

		IF ZZYZX < ConWallThickness_1*2 THEN
			ZZYZX = ConWallThickness_1*2+EPS
			PARAMETERS ZZYZX = ZZYZX
		ENDIF
		VALUES "ZZYZX" RANGE (ConWallThickness_1*2,]

		IF GLOB_MODPAR_NAME = "ZZYZX" THEN
			ConDepth_1 = ZZYZX - ConWallThickness_1*2
			PARAMETERS ConDepth_1 = ConDepth_1
		ENDIF
	ELSE
		IF ABS(MEP_NumConnectionData[2][10])>0.5 AND ABS(MEP_NumConnectionData[1][10])<0.5 THEN
			MEP_NumConnectionData[1][12] = MEP_NumConnectionData[2][12]
			PARAMETERS MEP_NumConnectionData[1][12] = MEP_NumConnectionData[1][12]
			MEP_NumConnectionData[1][13] = MEP_NumConnectionData[2][13]
			PARAMETERS MEP_NumConnectionData[1][13] = MEP_NumConnectionData[1][13]
			MEP_NumConnectionData[1][14] = MEP_NumConnectionData[2][14]
			PARAMETERS MEP_NumConnectionData[1][14] = MEP_NumConnectionData[1][14]
			MEP_StrConnectionData[1][2] = MEP_StrConnectionData[2][2]
			PARAMETERS MEP_StrConnectionData[1][2] = MEP_StrConnectionData[1][2]
			IF same_con THEN
				MEP_StrConnectionData[1][1] = MEP_StrConnectionData[2][1]
				PARAMETERS MEP_StrConnectionData[1][1] = MEP_StrConnectionData[1][1]
				MEP_NumConnectionData[1][15] = MEP_NumConnectionData[2][15]
				PARAMETERS MEP_NumConnectionData[1][15] = MEP_NumConnectionData[1][15]
			ENDIF
		ENDIF
		IF ABS(MEP_NumConnectionData[1][10])>0.5 AND ABS(MEP_NumConnectionData[2][10])<0.5 THEN
			MEP_StrConnectionData[2][2] = MEP_StrConnectionData[1][2]
			PARAMETERS MEP_StrConnectionData[2][2] = MEP_StrConnectionData[2][2]
		ENDIF
	ENDIF


	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_page_idx = ui_page_idx,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"


	IF NOT(setProgram) THEN
		IF ABS(ConWidth_1 + ConWallThickness_1*2 - B) > EPS THEN
			B = ConWidth_1+ConWallThickness_1*2
			PARAMETERS B = B
		ENDIF

		IF ABS(ConDepth_1 + ConWallThickness_1*2 - ZZYZX) > EPS THEN
			ZZYZX = ConDepth_1+ConWallThickness_1*2
			PARAMETERS ZZYZX = ZZYZX
		ENDIF
	ENDIF


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Выпускное Соединение Воздуховода`

	ConDVecX = 1
	ConDVecY = 0
	ConDVecZ = 0

	ConWVecX = 0
	ConWVecY = 1
	ConWVecZ = 0


	IF NOT(setProgram) THEN
		IF same_con THEN
			ConConnectorType_2 = ConConnectorType_1
			PARAMETERS ConConnectorType_2 = ConConnectorType_2
			MEP_StrConnectionData[2][1] = MEP_StrConnectionData[1][1]
			PARAMETERS MEP_StrConnectionData[2][1] = MEP_StrConnectionData[2][1]
			gs_AddConnections[2][1] = gs_AddConnections[1][1]
			PARAMETERS gs_AddConnections[2][1] = gs_AddConnections[2][1]
			ConConnectorWidth_2 = ConConnectorWidth_1
			PARAMETERS ConConnectorWidth_2 = ConConnectorWidth_2
			ConSystem_2 = ConSystem_1
			PARAMETERS ConSystem_2 = ConSystem_2
			MEP_StrConnectionData[2][2] = MEP_StrConnectionData[1][2]
			PARAMETERS MEP_StrConnectionData[2][2] = MEP_StrConnectionData[2][2]

			LOCK "ConConnectorType_2", "ConConnectorWidth_2", "ConSystem_2"
		ENDIF

		isConLength = 1
		IF ABS(gs_AddConnections[2][1] - 1) < EPS THEN isConLength = 0
		ConLength_2 = ConWallThickness_1*isConLength
		PARAMETERS ConLength_2 = ConLength_2

		ConWidth_2 = ConWidth_1
		PARAMETERS ConWidth_2 = ConWidth_2
		ConDepth_2 = ConDepth_1
		PARAMETERS ConDepth_2 = ConDepth_2
		ConWallThickness_2 = ConWallThickness_1
		PARAMETERS ConWallThickness_2 = ConWallThickness_2

		ConPosX_2 = A/2-ConLength_2
		PARAMETERS ConPosX_2 = ConPosX_2
		ConPosY_2 = 0
		PARAMETERS ConPosY_2 = ConPosY_2
		ConPosZ_2 = 0
		PARAMETERS ConPosZ_2 = ConPosZ_2
	ENDIF


	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2,
		ConPosY = ConPosY_2,
		ConPosZ = ConPosZ_2,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF setProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF

FOR k = 1 TO MEP_NumberConnections
	FOR i = 1 TO 9
		j = 1+INT((i-0.5)/3)
		MEP_NumConnectionData[k][i] = outCoords[j+3*(k-1)][i-3*(j-1)]
		PARAMETERS MEP_NumConnectionData[k][i] = MEP_NumConnectionData[k][i]
	NEXT i
NEXT k
