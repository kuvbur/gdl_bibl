
! VAV Reheat Water Coil


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT -dTWidth/2, -dTDepth/2, 0, unID: unID=unID+1
HOTSPOT -dTWidth/2, -dTDepth/2, dTHeight, unID: unID=unID+1
HOTSPOT dTWidth/2, -dTDepth/2, 0, unID: unID=unID+1
HOTSPOT dTWidth/2, -dTDepth/2, dTHeight, unID: unID=unID+1

HOTSPOT -dTWidth/2, dTDepth/2, 0, unID: unID=unID+1
HOTSPOT -dTWidth/2, dTDepth/2, dTHeight, unID: unID=unID+1
HOTSPOT dTWidth/2, dTDepth/2, 0, unID: unID=unID+1
HOTSPOT dTWidth/2, dTDepth/2, dTHeight, unID: unID=unID+1


				! --- MAIN BOX --- !

ADD -dTWidth/2, -dTDepth/2, 0
	BLOCK dTWidth, dTDepth, dTHeight
DEL 1

dist = 0.07*(dTHeight/0.30)
alpha = 180-2*ATN((dist/2)/0.03)
rr = (dist/2)/SIN(alpha)

ADD -dTWidth/2, 0, dTHeight*0.75
ROTx 90
ADDz -0.005
PRISM_ 3, 0.01,
	0, dist/2, 79,
	rr-0.03, 0, 915,
	0, -dist/2, 3015
DEL 3

ADD -dTWidth/2, 0, dTHeight/4
ROTx 90
ADDz -0.005
PRISM_ 3, 0.01,
	0, dist/2, 79,
	rr-0.03, 0, 915,
	0, -dist/2, 3015
DEL 3


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 1,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 14,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 14
END
