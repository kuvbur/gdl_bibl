!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
gs_DuctMat = sMat
res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple
duct_show = duct_show*not(show_axis) 
PEN gs_cont_pen
RESOL res

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]

HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200

operatedMode = 0
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	operatedMode = 1
	addStatus = 3
ENDIF
IF connectedA + connectedB < 1 + EPS THEN
	unID = 1
	HOTSPOT 0, 0, 0, hceID_1, MEP_StraightLength, 1+256*(connectedA < EPS)
	HOTSPOT -1, 0, 0, HptsUnIDBeginForConnEnds, MEP_StraightLength, 3
	IF ABS(connectedA) < EPS AND ABS(connectedB - 1) < EPS THEN
		hID = hceID_2+1
		HOTSPOT MEP_StraightLength, 0, 0, hceID_2
	ELSE
		hID = hceID_2
	ENDIF
	HOTSPOT MEP_StraightLength, 0, 0, hID, MEP_StraightLength, 2+128*connectedB
ELSE
	HOTSPOT 0, 0, 0, hceID_1
	HOTSPOT MEP_StraightLength, 0, 0, hceID_2
ENDIF

MULz -1

IF ((cl_show and show_axis_and) or show_axis) AND operatedMode < EPS THEN
	PEN MEP_cline_pen
	LIN_ dist, 0, -oHeight, dist+oLen, 0, -oHeight
	HOTLINE dist, 0, -oHeight, dist+oLen, 0, -oHeight,3
	hotspot dist, 0, -oHeight
	hotspot dist+oLen, 0, -oHeight
	MODEL WIRE
	FOR i = 1 TO 2
		ADDx MEP_StraightLength*(i > 1)
		MULx 1-2*(i > 1)
		ROTx 90
		ADDy -bendRadius
			HOTARC bendRadius, 90-angReal, 90,1
			ARC bendRadius, 90-angReal, 90
		DEL 2
		ADD bendRadius*SIN(angReal), 0, -bendRadius+bendRadius*COS(angReal)
		ROTy angReal
		ROTx -90
		ADDy -bendRadius
			HOTARC bendRadius, 90-angReal, 90,2
			ARC bendRadius, 90-angReal, 90
		DEL 6
	NEXT i
	MODEL SURFACE
	PEN gs_cont_pen
ENDIF

MATERIAL gs_DuctMat

IF duct_show THEN
	FOR i = 1 TO 2
		ADDx MEP_StraightLength*(i > 1)
		MULx 1-2*(i > 1)

		edgeS = 1: edgeE = 0
		GOSUB 10

		ADD bendRadius*SIN(angReal), 0, -bendRadius+bendRadius*COS(angReal)
		ROTy -180+angReal
		ROTz 180
			edgeS = 0: edgeE = 0
			GOSUB 10
		DEL 5
	NEXT i

	IF oLen > EPS THEN
		ADD dist, 0, -oHeight
		ROTy 90
		EXTRUDE 5, 0, 0, oLen, addStatus,
			ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, ductHalfWidth, -1
		DEL 2
	ENDIF
ENDIF

IF flangeA AND duct_show and not(show_axis) THEN
	ROTy 90
	PRISM_ 10, connectorLength,
		connectorHalfHeightA, connectorHalfWidthA, 15,
		-connectorHalfHeightA, connectorHalfWidthA, 15,
		-connectorHalfHeightA, -connectorHalfWidthA, 15,
		connectorHalfHeightA, -connectorHalfWidthA, 15,
		connectorHalfHeightA, connectorHalfWidthA, -1,
		ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, ductHalfWidth, -1
	DEL 1
ENDIF

IF flangeB AND duct_show and not(show_axis) THEN
	ADDx MEP_StraightLength-connectorLength
	ROTy 90
	PRISM_ 10, connectorLength,
		connectorHalfHeightB, connectorHalfWidthB, 15,
		-connectorHalfHeightB, connectorHalfWidthB, 15,
		-connectorHalfHeightB, -connectorHalfWidthB, 15,
		connectorHalfHeightB, -connectorHalfWidthB, 15,
		connectorHalfHeightB, connectorHalfWidthB, -1,
		ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, ductHalfWidth, -1
	DEL 2
ENDIF


IF MEP_InsShow and show_isol and not(show_axis) THEN
	MATERIAL gs_InsMat

	ductHalfWidth = ductHalfWidth + MEP_InsThickness
	ductHalfHeight = ductHalfHeight + MEP_InsThickness

	FOR i = 1 TO 2
		ADDx MEP_StraightLength*(i > 1)
		MULx 1-2*(i > 1)

		edgeS = 1: edgeE = 0
		GOSUB 10

		ADD bendRadius*SIN(angReal), 0, -bendRadius+bendRadius*COS(angReal)
		ROTy -180+angReal
		ROTz 180
			edgeS = 0: edgeE = 0
			GOSUB 10
		DEL 5
	NEXT i

	IF oLen > EPS THEN
		ADD dist, 0, -oHeight
		ROTy 90
		EXTRUDE 5, 0, 0, oLen, addStatus,
			ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, ductHalfWidth, -1
		DEL 2
	ENDIF
ENDIF
DEL 2


END

10:
	ROTx -90
	ADDz -ductHalfWidth

	IF operatedMode THEN
		PUT 0, -ductHalfHeight, 15
		PUT 0, bendRadius, 915
		PUT (bendRadius+ductHalfHeight)*SIN(angReal), bendRadius-(bendRadius+ductHalfHeight)*COS(angReal), 3015
		PUT (bendRadius-ductHalfHeight)*SIN(angReal), bendRadius-(bendRadius-ductHalfHeight)*COS(angReal), 15
		PUT 0, bendRadius, 915
		PUT 0, ductHalfHeight, 3015
		PUT 0, -ductHalfHeight, -1
	ELSE
		PUT 0, -ductHalfHeight, 77+2*edgeS
		PUT 0, bendRadius, 979
		PUT (bendRadius+ductHalfHeight)*SIN(angReal), bendRadius-(bendRadius+ductHalfHeight)*COS(angReal), 3000+7*edgeE
		PUT (bendRadius-ductHalfHeight)*SIN(angReal), bendRadius-(bendRadius-ductHalfHeight)*COS(angReal), 77+2*edgeE
		PUT 0, bendRadius, 979
		PUT 0, ductHalfHeight, 3000+7*edgeS
	ENDIF

	PRISM_ NSP/3, ductHalfWidth*2,

		GET (NSP)

	DEL 2
RETURN
