!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
gs_DuctMat = sMat
duct_show = duct_show*not(show_axis)
IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

PEN gs_cont_pen
RESOL res

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200

addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	addStatus = 7
ENDIF

IF connectedA + connectedB < 1 + EPS THEN
	unID = 1
	HOTSPOT 0, 0, 0, hceID_1, MEP_StraightLength, 1+256*(connectedA < EPS)
	HOTSPOT -1, 0, 0, HptsUnIDBeginForConnEnds, MEP_StraightLength, 3
	IF ABS(connectedA) < EPS AND ABS(connectedB - 1) < EPS THEN
		hID = hceID_2+1
		HOTSPOT MEP_StraightLength, 0, 0, hceID_2
	ELSE
		hID = hceID_2
	ENDIF
	HOTSPOT MEP_StraightLength, 0, 0, hID, MEP_StraightLength, 2+128*connectedB
ELSE
	HOTSPOT 0, 0, 0, hceID_1
	HOTSPOT MEP_StraightLength, 0, 0, hceID_2
ENDIF

MATERIAL gs_DuctMat

IF flangeA AND duct_show THEN
	ROTy 90
	PRISM_ 10, connectorLength,
		connectorHalfHeightA, connectorHalfWidthA, 15,
		-connectorHalfHeightA, connectorHalfWidthA, 15,
		-connectorHalfHeightA, -connectorHalfWidthA, 15,
		connectorHalfHeightA, -connectorHalfWidthA, 15,
		connectorHalfHeightA, connectorHalfWidthA, -1,
		ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, ductHalfWidth, -1
	DEL 1
ENDIF

IF flangeB AND duct_show THEN
	ADDx MEP_StraightLength-connectorLength
	ROTy 90
	PRISM_ 10, connectorLength,
		connectorHalfHeightB, connectorHalfWidthB, 15,
		-connectorHalfHeightB, connectorHalfWidthB, 15,
		-connectorHalfHeightB, -connectorHalfWidthB, 15,
		connectorHalfHeightB, -connectorHalfWidthB, 15,
		connectorHalfHeightB, connectorHalfWidthB, -1,
		ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, ductHalfWidth, -1
	DEL 2
ENDIF

FOR k = 1 TO 1+MEP_InsShow
	ADDz -ductHalfHeight

	IF NOT(duct_show) AND k = 1 THEN GOTO 10
		PUT 0, ductHalfWidth, 15
		PUT inletStraightLength, ductHalfWidth, 15
		PUT inletStraightLength+inletInclinedLength, totalHalfWidth, 15
		PUT MEP_StraightLength-outletInclinedLength-outletStraightLength, totalHalfWidth, 15
		PUT MEP_StraightLength-outletStraightLength, ductHalfWidth, 15
		PUT MEP_StraightLength, ductHalfWidth, 7+addStatus
		PUT MEP_StraightLength, -ductHalfWidth, 15
		PUT MEP_StraightLength-outletStraightLength, -ductHalfWidth, 15
		PUT MEP_StraightLength-outletInclinedLength-outletStraightLength, -totalHalfWidth, 15
		PUT inletStraightLength+inletInclinedLength, -totalHalfWidth, 15
		PUT inletStraightLength, -ductHalfWidth, 15
		PUT 0, -ductHalfWidth, 7+addStatus
		PUT 0, ductHalfWidth, -1
		PUT posHoleX+oLen/2, posHoleY+oWidth/2, 13
		PUT posHoleX-oLen/2-MEP_InsThickness*(k > 1), posHoleY+oWidth/2, 79
		PUT posHoleX-oLen/2-MEP_InsThickness*(k > 1), posHoleY-oWidth/2, 1079
		PUT posHoleX+oLen/2, posHoleY-oWidth/2, 13
		PUT posHoleX+oLen/2+(oWidth/2)/TAN(angFlow/2)-EPS, posHoleY, 13
		PUT posHoleX+oLen/2, posHoleY+oWidth/2, -1

		PRISM_ NSP/3, ductHalfHeight*2,

			GET(NSP)
	10:

	DEL 1
	 MATERIAL gs_InsMat
	 ductHalfWidth = ductHalfWidth + MEP_InsThickness
	 ductHalfHeight = ductHalfHeight + MEP_InsThickness
	 totalHalfWidth = totalHalfWidth + MEP_InsThickness
	 oLen = oLen - MEP_InsThickness*2
	 oWidth = oWidth - MEP_InsThickness*2
NEXT k

DEL 1


END
