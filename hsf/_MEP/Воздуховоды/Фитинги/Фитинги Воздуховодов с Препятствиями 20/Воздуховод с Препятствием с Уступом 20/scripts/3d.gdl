!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
gs_DuctMat = sMat
IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

duct_show = duct_show*not(show_axis)
PEN gs_cont_pen
RESOL res

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200

operatedMode = 0
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	operatedMode = 1
	addStatus = 3
ENDIF

HOTSPOT 0, 0, 0, hceID_1
HOTSPOT MEP_StraightLength, 0, oHeight, hceID_2

IF ((MEP_cline_show and show_axis_and) or show_axis) AND operatedMode < EPS THEN
	pen MEP_cline_pen
	LIN_ 0, 0, 0, aLen, 0, 0
	LIN_ aLen, 0, 0, aLen+oHeight/(TAN(rd_angle)), 0, oHeight
	LIN_ aLen+oHeight/(TAN(rd_angle)), 0, oHeight, aLen*2+oHeight/(TAN(rd_angle)), 0, oHeight
	HOTSPOT aLen, 0, 0, hceID_1 : hceID_1 = hceID_1 + 1
	HOTSPOT aLen+oHeight/(TAN(rd_angle)), 0, oHeight, hceID_1 : hceID_1 = hceID_1 + 1
	HOTSPOT aLen*2+oHeight/(TAN(rd_angle)), 0, oHeight, hceID_1 : hceID_1 = hceID_1 + 1
	HOTLINE 0, 0, 0, aLen, 0, 0, hceID_1 : hceID_1 = hceID_1 + 1 
	HOTLINE aLen, 0, 0, aLen+oHeight/(TAN(rd_angle)), 0, oHeight, hceID_1 : hceID_1 = hceID_1 + 1
	HOTLINE aLen+oHeight/(TAN(rd_angle)), 0, oHeight, aLen*2+oHeight/(TAN(rd_angle)), 0, oHeight, hceID_1 : hceID_1 = hceID_1 + 1
	PEN gs_cont_pen
ENDIF


MATERIAL gs_DuctMat

IF duct_show THEN GOSUB 100

IF flangeA AND duct_show THEN
	ROTy 90
	IF ABS(shapeType - 1) < EPS THEN
		PRISM_ 10, connectorLength,
			connectorHalfHeightA, connectorHalfWidthA, 15,
			-connectorHalfHeightA, connectorHalfWidthA, 15,
			-connectorHalfHeightA, -connectorHalfWidthA, 15,
			connectorHalfHeightA, -connectorHalfWidthA, 15,
			connectorHalfHeightA, connectorHalfWidthA, -1,
			ductHalfHeight, ductHalfWidth, 15,
			-ductHalfHeight, ductHalfWidth, 15,
			-ductHalfHeight, -ductHalfWidth, 15,
			ductHalfHeight, -ductHalfWidth, 15,
			ductHalfHeight, ductHalfWidth, -1
	ELSE
		IF ABS(ductHalfWidth - ductHalfHeight) < EPS THEN
		PRISM_ 4, connectorLength,
				0, 0, 979,
				connectorHalfWidthA, 360, 4079,
				0, 0, 979,
				ductHalfWidth, 360, 4079
		ELSE
			IF ductHalfWidth > ductHalfHeight THEN
				rr = ductHalfHeight
				crr = connectorHalfHeightA

				PRISM_ 12, connectorLength,
					-connectorHalfHeightA, connectorHalfWidthA-crr, 79,
					-connectorHalfHeightA, -connectorHalfWidthA+crr, 79,
					connectorHalfHeightA, -connectorHalfWidthA+crr, 1079,
					connectorHalfHeightA, connectorHalfWidthA-crr, 79,
					-connectorHalfHeightA, connectorHalfWidthA-crr, 1079,
					-connectorHalfHeightA, connectorHalfWidthA-crr, -1,
					-ductHalfHeight, ductHalfWidth-rr, 79,
					-ductHalfHeight, -ductHalfWidth+rr, 79,
					ductHalfHeight, -ductHalfWidth+rr, 1079,
					ductHalfHeight, ductHalfWidth-rr, 79,
					-ductHalfHeight, ductHalfWidth-rr, 1079,
					-ductHalfHeight, ductHalfWidth-rr, -1
			ELSE
				rr = ductHalfWidth
				crr = connectorHalfWidthA

				PRISM_ 12, connectorLength,
					connectorHalfHeightA-crr, connectorHalfWidthA, 79,
					-connectorHalfHeightA+crr, connectorHalfWidthA, 79,
					-connectorHalfHeightA+crr, -connectorHalfWidthA, 1079,
					connectorHalfHeightA-crr, -connectorHalfWidthA, 79,
					connectorHalfHeightA-crr, connectorHalfWidthA, 1079,
					connectorHalfHeightA-crr, connectorHalfWidthA, -1,
					ductHalfHeight-rr, ductHalfWidth, 79,
					-ductHalfHeight+rr, ductHalfWidth, 79,
					-ductHalfHeight+rr, -ductHalfWidth, 1079,
					ductHalfHeight-rr, -ductHalfWidth, 79,
					ductHalfHeight-rr, ductHalfWidth, 1079,
					ductHalfHeight-rr, ductHalfWidth, -1
			ENDIF
		ENDIF
	ENDIF
	DEL 1
ENDIF

IF flangeB AND duct_show THEN
	ADD MEP_StraightLength-connectorLength, 0, oHeight
	ROTy 90
	IF ABS(shapeType - 1) < EPS THEN
		PRISM_ 10, connectorLength,
			connectorHalfHeightB, connectorHalfWidthB, 15,
			-connectorHalfHeightB, connectorHalfWidthB, 15,
			-connectorHalfHeightB, -connectorHalfWidthB, 15,
			connectorHalfHeightB, -connectorHalfWidthB, 15,
			connectorHalfHeightB, connectorHalfWidthB, -1,
			ductHalfHeight, ductHalfWidth, 15,
			-ductHalfHeight, ductHalfWidth, 15,
			-ductHalfHeight, -ductHalfWidth, 15,
			ductHalfHeight, -ductHalfWidth, 15,
			ductHalfHeight, ductHalfWidth, -1
	ELSE
		IF ABS(ductHalfWidth - ductHalfHeight) < EPS THEN
		PRISM_ 4, connectorLength,
				0, 0, 979,
				connectorHalfWidthB, 360, 4079,
				0, 0, 979,
				ductHalfWidth, 360, 4079
		ELSE
			IF ductHalfWidth > ductHalfHeight THEN
				rr = ductHalfHeight
				crr = connectorHalfHeightB

				PRISM_ 12, connectorLength,
					-connectorHalfHeightB, connectorHalfWidthB-crr, 79,
					-connectorHalfHeightB, -connectorHalfWidthB+crr, 79,
					connectorHalfHeightB, -connectorHalfWidthB+crr, 1079,
					connectorHalfHeightB, connectorHalfWidthB-crr, 79,
					-connectorHalfHeightB, connectorHalfWidthB-crr, 1079,
					-connectorHalfHeightB, connectorHalfWidthB-crr, -1,
					-ductHalfHeight, ductHalfWidth-rr, 79,
					-ductHalfHeight, -ductHalfWidth+rr, 79,
					ductHalfHeight, -ductHalfWidth+rr, 1079,
					ductHalfHeight, ductHalfWidth-rr, 79,
					-ductHalfHeight, ductHalfWidth-rr, 1079,
					-ductHalfHeight, ductHalfWidth-rr, -1
			ELSE
				rr = ductHalfWidth
				crr = connectorHalfWidthB

				PRISM_ 12, connectorLength,
					connectorHalfHeightB-crr, connectorHalfWidthB, 79,
					-connectorHalfHeightB+crr, connectorHalfWidthB, 79,
					-connectorHalfHeightB+crr, -connectorHalfWidthB, 1079,
					connectorHalfHeightB-crr, -connectorHalfWidthB, 79,
					connectorHalfHeightB-crr, connectorHalfWidthB, 1079,
					connectorHalfHeightB-crr, connectorHalfWidthB, -1,
					ductHalfHeight-rr, ductHalfWidth, 79,
					-ductHalfHeight+rr, ductHalfWidth, 79,
					-ductHalfHeight+rr, -ductHalfWidth, 1079,
					ductHalfHeight-rr, -ductHalfWidth, 79,
					ductHalfHeight-rr, ductHalfWidth, 1079,
					ductHalfHeight-rr, ductHalfWidth, -1
			ENDIF
		ENDIF

	ENDIF
	DEL 2
ENDIF

IF MEP_InsShow and show_isol and not(show_axis) THEN
	MATERIAL gs_InsMat

	ductHalfWidth = ductHalfWidth + MEP_InsThickness
	ductHalfHeight = ductHalfHeight + MEP_InsThickness

	GOSUB 100
ENDIF
DEL 1


END

100:
	PUT -1, 0, 0, 0
	PUT 0, 0, 0, 0
	PUT aLen, 0, 0, 0
	PUT aLen+oHeight/(TAN(rd_angle)), 0, oHeight, 0
	PUT MEP_StraightLength, 0, oHeight, 0
	PUT MEP_StraightLength*2, 0, oHeight, 0

	IF ABS(shapeType - 1) < EPS THEN
		TUBE 5, NSP/4, addStatus+16+32,
			-ductHalfWidth, ductHalfHeight, 0,
			ductHalfWidth, ductHalfHeight, 0,
			ductHalfWidth, -ductHalfHeight, 0,
			-ductHalfWidth, -ductHalfHeight, 0,
			-ductHalfWidth, ductHalfHeight, 0,

			GET(NSP)
	ELSE
		IF ABS(ductHalfWidth - ductHalfHeight) < EPS THEN
			TUBE 2, NSP/4, addStatus+16+32,
				0, 0, 901,
				ductHalfWidth, 360, 4001,

				GET(NSP)
		ELSE
			IF ductHalfWidth > ductHalfHeight THEN
				rr = ductHalfHeight
				TUBE 5, NSP/4, addStatus+16+32,
					-ductHalfWidth+rr, ductHalfHeight, 1,
					ductHalfWidth-rr, ductHalfHeight, 1,
					ductHalfWidth-rr, -ductHalfHeight, 1001,
					-ductHalfWidth+rr, -ductHalfHeight, 1,
					-ductHalfWidth+rr, ductHalfHeight, 1001,

					GET(NSP)
			ELSE
				rr = ductHalfWidth
				TUBE 5, NSP/4, addStatus+16+32,
					ductHalfWidth, ductHalfHeight-rr, 1,
					ductHalfWidth, -ductHalfHeight+rr, 1,
					-ductHalfWidth, -ductHalfHeight+rr, 1001,
					-ductHalfWidth, ductHalfHeight-rr, 1,
					ductHalfWidth, ductHalfHeight-rr, 1001,

					GET(NSP)
			ENDIF
		ENDIF
	ENDIF
RETURN
