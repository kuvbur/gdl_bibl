!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
gs_DuctMat = sMat
IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF
duct_show = duct_show*not(show_axis)
IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


PEN gs_cont_pen
RESOL res

HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200

operatedMode = 0
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	operatedMode = 1
	addStatus = 3
ENDIF

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]

HOTSPOT 0, 0, 0, hceID_1
HOTSPOT MEP_StraightLength, 0, oHeight, hceID_2

pen MEP_cline_pen
IF ((MEP_cline_show and show_axis_and) or show_axis) AND operatedMode < EPS THEN LIN_ 0, 0, 0, MEP_StraightLength, 0, oHeight
PEN gs_cont_pen

MATERIAL gs_DuctMat

IF duct_show THEN
	IF MEP_StraightLength > EPS THEN
		ROTy 90
		EXTRUDE 5, -oHeight, 0, MEP_StraightLength, addStatus+16+32,
			ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, ductHalfWidth, -1
		DEL 1
	ENDIF
ENDIF

IF flangeA AND duct_show THEN
	ROTy 90
	PRISM_ 10, connectorLength,
		connectorHalfHeightA, connectorHalfWidthA, 15,
		-connectorHalfHeightA, connectorHalfWidthA, 15,
		-connectorHalfHeightA, -connectorHalfWidthA, 15,
		connectorHalfHeightA, -connectorHalfWidthA, 15,
		connectorHalfHeightA, connectorHalfWidthA, -1,
		ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, ductHalfWidth, -1
	DEL 1
ENDIF

IF flangeB AND duct_show THEN
	ADD MEP_StraightLength-connectorLength, 0, oHeight
	ROTy 90
	PRISM_ 10, connectorLength,
		connectorHalfHeightB, connectorHalfWidthB, 15,
		-connectorHalfHeightB, connectorHalfWidthB, 15,
		-connectorHalfHeightB, -connectorHalfWidthB, 15,
		connectorHalfHeightB, -connectorHalfWidthB, 15,
		connectorHalfHeightB, connectorHalfWidthB, -1,
		ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, ductHalfWidth, 15,
		-ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, -ductHalfWidth, 15,
		ductHalfHeight, ductHalfWidth, -1
	DEL 2
ENDIF

IF MEP_InsShow and show_isol and not(show_axis) THEN
	MATERIAL gs_InsMat

	ductHalfWidth = ductHalfWidth + MEP_InsThickness
	ductHalfHeight = ductHalfHeight + MEP_InsThickness

	IF MEP_StraightLength > EPS THEN
		ROTy 90
		EXTRUDE 5, -oHeight, 0, MEP_StraightLength, addStatus,
			ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, ductHalfWidth, 0,
			-ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, -ductHalfWidth, 0,
			ductHalfHeight, ductHalfWidth, -1
		DEL 1
	ENDIF
ENDIF
DEL 1


END
