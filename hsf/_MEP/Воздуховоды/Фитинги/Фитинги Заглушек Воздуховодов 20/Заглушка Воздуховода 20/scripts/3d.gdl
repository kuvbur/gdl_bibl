!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
PEN gs_cont_pen
call "2dmacro" parameters all, type_symb = type_symb
if show_axis then 
	hotspot2 0,0
	end
endif
IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple
gs_DuctMat = sMat
MATERIAL gs_DuctMat
PEN gs_cont_pen
RESOL res


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

capHalfWidth = ConWidth_1/2+ConWallThickness_1
capHalfHeight = ConDepth_1/2+ConWallThickness_1
capThick = EPS
IF ABS(gs_AddConnections[1][1] - 2) < EPS THEN
	capHalfWidth = capHalfWidth + ConConnectorWidth_1
	capHalfHeight = capHalfHeight + ConConnectorWidth_1
	capThick = MAX(EPS, ConWallThickness_1)
ENDIF


ROTx 90
IF ABS(MEP_NumConnectionData[1][21] - 1) < EPS THEN
	PRISM_ 5, capThick,
		capHalfWidth, capHalfHeight, 15,
		-capHalfWidth, capHalfHeight, 15,
		-capHalfWidth, -capHalfHeight, 15,
		capHalfWidth, -capHalfHeight, 15,
		capHalfWidth, capHalfHeight, -1
ENDIF
IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
		PRISM_ 2, capThick,
			0, 0, 979,
			capHalfWidth, 360, 4079
ENDIF
IF ABS(MEP_NumConnectionData[1][21] - 3) < EPS THEN
	IF ABS(capHalfWidth - capHalfHeight) < EPS THEN
		PRISM_ 2, capThick,
			0, 0, 979,
			capHalfWidth, 360, 4079
	ELSE
		maxSize = MAX(capHalfWidth, capHalfHeight)
		minSize = MIN(capHalfWidth, capHalfHeight)
		rr = minSize

		IF capHalfWidth > capHalfHeight THEN
			PRISM_ 5, capThick,
				-maxSize+rr, minSize, 79,
				maxSize-rr, minSize, 79,
				maxSize-rr, -minSize, 1079,
				-maxSize+rr, -minSize, 79,
				-maxSize+rr, minSize, 1079
		ELSE
			ROTz 90
			PRISM_ 5, capThick,
				-maxSize+rr, minSize, 79,
				maxSize-rr, minSize, 79,
				maxSize-rr, -minSize, 1079,
				-maxSize+rr, -minSize, 79,
				-maxSize+rr, minSize, 1079
			DEL 1
		ENDIF
	ENDIF
ENDIF


IF MEP_InsShow THEN
	MATERIAL gs_InsMat

	MEP_InsThickness = MAX(0, MEP_InsThickness - ConConnectorWidth_1*(ABS(gs_AddConnections[1][1] - 2) < EPS))
	IF MEP_InsThickness > EPS THEN
		capInsHalfWidth = capHalfWidth + MEP_InsThickness
		capInsHalfHeight = capHalfHeight + MEP_InsThickness
		IF ABS(MEP_NumConnectionData[1][21] - 1) < EPS THEN
			PRISM_ 10, EPS,
				capInsHalfWidth, capInsHalfHeight, 15,
				-capInsHalfWidth, capInsHalfHeight, 15,
				-capInsHalfWidth, -capInsHalfHeight, 15,
				capInsHalfWidth, -capInsHalfHeight, 15,
				capInsHalfWidth, capInsHalfHeight, -1,
				capHalfWidth, capHalfHeight, 15,
				-capHalfWidth, capHalfHeight, 15,
				-capHalfWidth, -capHalfHeight, 15,
				capHalfWidth, -capHalfHeight, 15,
				capHalfWidth, capHalfHeight, -1
		ENDIF
		IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
			PRISM_ 4, EPS,
				0, 0, 979,
				capInsHalfWidth, 360, 4079,
				0, 0, 979,
				capHalfWidth, 360, 4079
		ENDIF
		IF ABS(MEP_NumConnectionData[1][21] - 3) < EPS THEN
			IF ABS(capHalfWidth - capHalfHeight) < EPS THEN
				PRISM_ 4, EPS,
					0, 0, 979,
					capInsHalfWidth, 360, 4079,
					0, 0, 979,
					capHalfWidth, 360, 4079
			ELSE
				maxSize = MAX(capHalfWidth, capHalfHeight)
				minSize = MIN(capHalfWidth, capHalfHeight)
				rr = minSize
				maxInsSize = MAX(capInsHalfWidth, capInsHalfHeight)
				minInsSize = MIN(capInsHalfWidth, capInsHalfHeight)
				rrIns = minInsSize

				IF capHalfWidth > capHalfHeight THEN
					PRISM_ 12, EPS,
						-maxInsSize+rrIns, minInsSize, 79,
						maxInsSize-rrIns, minInsSize, 79,
						maxInsSize-rrIns, -minInsSize, 1079,
						-maxInsSize+rrIns, -minInsSize, 79,
						-maxInsSize+rrIns, minInsSize, 1079,
						-maxInsSize+rrIns, minInsSize, -1,
						-maxSize+rr, minSize, 79,
						maxSize-rr, minSize, 79,
						maxSize-rr, -minSize, 1079,
						-maxSize+rr, -minSize, 79,
						-maxSize+rr, minSize, 1079,
						-maxSize+rr, minSize, -1
				ELSE
					ROTz 90
					PRISM_ 12, EPS,
						-maxInsSize+rrIns, minInsSize, 79,
						maxInsSize-rrIns, minInsSize, 79,
						maxInsSize-rrIns, -minInsSize, 1079,
						-maxInsSize+rrIns, -minInsSize, 79,
						-maxInsSize+rrIns, minInsSize, 1079,
						-maxInsSize+rrIns, minInsSize, -1,
						-maxSize+rr, minSize, 79,
						maxSize-rr, minSize, 79,
						maxSize-rr, -minSize, 1079,
						-maxSize+rr, -minSize, 79,
						-maxSize+rr, minSize, 1079,
						-maxSize+rr, minSize, -1
					DEL 1
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

DEL TOP

END
