!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el


PEN gs_cont_pen
SET FILL gs_fill_type

HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200
hceID_3 = HptsUnIDBeginForConnEnds+300

ADD2 MEP_Position[1][1], MEP_Position[1][2]
HOTSPOT2 outCoords[1][1], outCoords[1][2], hceID_1
HOTSPOT2 outCoords[4][1], outCoords[4][2], hceID_2
HOTSPOT2 outCoords[7][1], outCoords[7][2], hceID_3
DEL 1


IF ABS(dZx) < EPS AND ABS(dZy) < EPS THEN
	dx = MEP_Position[2][1]
	dy = MEP_Position[2][2]

	length = SQR(dx^2+dy^2)
	IF ABS(length-1) > EPS THEN
		dx = dx/length
		dy = dy/length
	ENDIF

	 arCosVal = dx/1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = -1 THEN arCosVal = -1
	IF ABS(arCosVal) > 1 AND SGN(arCosVal) = 1 THEN arCosVal = 1
	dirAngOnXYPlane = ACS(arCosVal)
	IF SGN(dy) = -1 THEN dirAngOnXYPlane = 360-dirAngOnXYPlane

	dd = ConThroatLength_2+ConThroatLength_3+MEP_NumConnectionData[1][12]
	r2 = ConThroatLength_2+MEP_NumConnectionData[2][12]
	r3 = ConThroatLength_3+MEP_NumConnectionData[3][12]
	xx = (dd^2-r3^2+r2^2)/(2*dd)
	yy = SQR(r2^2-xx^2)
	xx = xx-ConThroatLength_2-MEP_NumConnectionData[1][12]/2

	ConThroatLength_2 = ConThroatLength_2 - MEP_NumConnectionData[1][14]
	ConThroatLength_3 = ConThroatLength_3 - MEP_NumConnectionData[1][14]

	ADD2 MEP_Position[1][1], MEP_Position[1][2]
	ROT2 dirAngOnXYPlane
	MUL2 1, 1-2*(SGN(dZz) = -1)

if not(show_axis) then
	PUT 0, ductHalfWidthA, 1
	PUT 0, ductHalfWidthA+ConThroatLength_2, 901
	PUT ConThroatLength_2*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-ConThroatLength_2*COS(ConAngle_2), 3001
	PUT (ConThroatLength_2+ductHalfWidthB*2)*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-(ConThroatLength_2+ductHalfWidthB*2)*COS(ConAngle_2), 1
	PUT yy, -xx, 3001
	PUT 0, -ductHalfWidthA-ConThroatLength_3, 901
	PUT (ConThroatLength_3+ductHalfWidthC*2)*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+(ConThroatLength_3+ductHalfWidthC*2)*COS(ConAngle_3), 3001
	PUT ConThroatLength_3*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+ConThroatLength_3*COS(ConAngle_3), 1
	PUT 0, -ductHalfWidthA, 3001

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)
endif
	IF flangeA and not(show_axis) THEN
		length = connectorLengthA
		halfWidthC = connectorHalfWidthA

		GOSUB 10
	ENDIF
	IF flangeB and not(show_axis) THEN
		length = connectorLengthB
		halfWidthC = connectorHalfWidthB

		ADD2 ConPosX_2+ConLength_2*COS(ConAngle_2), ConPosY_2+ConLength_2*SIN(ConAngle_2)
		ROT2 -(180-ConAngle_2)
		GOSUB 10
		DEL 2
	ENDIF

	IF flangeC and not(show_axis) THEN
		length = connectorLengthC
		halfWidthC = connectorHalfWidthC

		ADD2 ConPosX_3+ConLength_3*COS(ConAngle_3), ConPosY_3+ConLength_3*(-SIN(ConAngle_3))
		ROT2 180-ConAngle_3
		GOSUB 10
		DEL 2
	ENDIF

	IF MEP_InsShow and show_isol and not(show_axis) THEN
		ductHalfWidthA = ductHalfWidthA+MEP_InsThickness
		ductHalfHeight = ductHalfHeight+MEP_InsThickness
		ductHalfWidthB = ductHalfWidthB+MEP_InsThickness
		ductHalfWidthC = ductHalfWidthC+MEP_InsThickness

		r2 = r2+MEP_InsThickness
		r3 = r3+MEP_InsThickness
		xx = (dd^2-r3^2+r2^2)/(2*dd)
		yy = SQR(r2^2-xx^2)
		xx = xx-ConThroatLength_2-MEP_NumConnectionData[1][12]/2

		ConThroatLength_2 = ConThroatLength_2 - MEP_InsThickness
		ConThroatLength_3 = ConThroatLength_3 - MEP_InsThickness

		PUT 0, ductHalfWidthA, 1
		PUT 0, ductHalfWidthA+ConThroatLength_2, 901
		PUT ConThroatLength_2*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-ConThroatLength_2*COS(ConAngle_2), 3000
		PUT (ConThroatLength_2+ductHalfWidthB*2)*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-(ConThroatLength_2+ductHalfWidthB*2)*COS(ConAngle_2), 1
		PUT yy, -xx, 3001
		PUT 0, -ductHalfWidthA-ConThroatLength_3, 901
		PUT (ConThroatLength_3+ductHalfWidthC*2)*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+(ConThroatLength_3+ductHalfWidthC*2)*COS(ConAngle_3), 3000
		PUT ConThroatLength_3*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+ConThroatLength_3*COS(ConAngle_3), 1
		PUT 0, -ductHalfWidthA, 3001

		POLY2_ NSP/3, 1,

			GET (NSP)

	ENDIF

	DEL 3
ELSE
	IF gs_fill_pen THEN PEN gs_fill_pen
	if not(show_axis) then PROJECT2{3} 3, 270, 3+32, 4, gs_back_pen, 0, 0, 0, PARAMETERS duct_show = 1, MEP_InsShow = 0, gs_detlevel_3D_m = 2

	PEN gs_cont_pen
	if not(show_axis) then PROJECT2{3} 3, 270, 2+32, 2+8, 0, 0, 0, 0, PARAMETERS duct_show = 1, MEP_InsShow = 0, gs_detlevel_3D_m = 2

	IF MEP_InsShow and show_isol and not(show_axis) THEN PROJECT2{3} 3, 270, 2+32, 8, 0, 0, 0, 0, PARAMETERS duct_show = 0, MEP_InsShow = 1, MEP_cline_show_3D = 0, gs_detlevel_3D_m = 2

	IF ((MEP_cline_show and show_axis_and) or show_axis) THEN		! -----> center line <----- !
		PEN MEP_cline_pen
		SET LINE_TYPE MEP_cline_type

		PROJECT2{3} 3, 270, 3+32, 8, 0, 0, 0, 0, PARAMETERS duct_show = 0, MEP_InsShow = 0, gs_detlevel_3D = `Detailed`
	ENDIF
	DEL TOP
ENDIF

END

10:
	PUT 0, halfWidthC, 1
	PUT 0, -halfWidthC, 1
	PUT length, -halfWidthC, 1
	PUT length, halfWidthC, 1
	PUT 0, halfWidthC, -1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)
RETURN
