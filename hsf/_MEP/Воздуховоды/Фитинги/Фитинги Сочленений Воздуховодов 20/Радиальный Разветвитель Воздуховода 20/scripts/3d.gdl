!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
gs_DuctMat = sMat
IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF
duct_show = duct_show*not(show_axis)
IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

gs_DuctMat=sMat

PEN gs_cont_pen
RESOL res


XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200
hceID_3 = HptsUnIDBeginForConnEnds+300

HOTSPOT ConPosX_1, ConPosY_1, ConPosZ_1, hceID_1
HOTSPOT ConPosX_2+ConLength_2*COS(ConAngle_2), ConPosY_2+ConLength_2*SIN(ConAngle_2), ConPosZ_2, hceID_2
HOTSPOT ConPosX_3+ConLength_3*COS(ConAngle_3), ConPosY_3+ConLength_3*(-SIN(ConAngle_3)), ConPosZ_3, hceID_3

operatedMode = 0
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	operatedMode = 1
	addStatus = 8
ENDIF

dd = ConThroatLength_2+ConThroatLength_3+MEP_NumConnectionData[1][12]
r2 = ConThroatLength_2+MEP_NumConnectionData[2][12]
r3 = ConThroatLength_3+MEP_NumConnectionData[3][12]
xx = (dd^2-r3^2+r2^2)/(2*dd)
yy = SQR(r2^2-xx^2)
xx = xx-ConThroatLength_2-MEP_NumConnectionData[1][12]/2

ConThroatLength_2 = ConThroatLength_2 - MEP_NumConnectionData[1][14]
ConThroatLength_3 = ConThroatLength_3 - MEP_NumConnectionData[1][14]

IF duct_show THEN
	MATERIAL gs_DuctMat

	ADDz -ductHalfHeight
	PRISM_ 9, ductHalfHeight*2,
		0, ductHalfWidthA, 79,
		0, ductHalfWidthA+ConThroatLength_2, 915,
		ConThroatLength_2*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-ConThroatLength_2*COS(ConAngle_2), 3007+addStatus,
		(ConThroatLength_2+ductHalfWidthB*2)*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-(ConThroatLength_2+ductHalfWidthB*2)*COS(ConAngle_2), 79,
		yy, -xx, 3079,
		0, -ductHalfWidthA-ConThroatLength_3, 915,
		(ConThroatLength_3+ductHalfWidthC*2)*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+(ConThroatLength_3+ductHalfWidthC*2)*COS(ConAngle_3), 3007+addStatus,
		ConThroatLength_3*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+ConThroatLength_3*COS(ConAngle_3), 79,
		0, -ductHalfWidthA, 3007+addStatus
	DEL 1

	halfHeight = ductHalfHeight
	halfHeightC = connectorHalfHeight

	IF flangeA THEN
		length = connectorLengthA
		halfWidth = ductHalfWidthA
		halfWidthC = connectorHalfWidthA

		GOSUB 10
	ENDIF
	IF flangeB THEN
		length = connectorLengthB
		halfWidth = ductHalfWidthB
		halfWidthC = connectorHalfWidthB

		ADD ConPosX_2+ConLength_2*COS(ConAngle_2), ConPosY_2+ConLength_2*SIN(ConAngle_2), ConPosZ_2
		ROTz -(180-ConAngle_2)
		GOSUB 10
		DEL 2
	ENDIF
	IF flangeC THEN
		length = connectorLengthC
		halfWidth = ductHalfWidthC
		halfWidthC = connectorHalfWidthC

		ADD ConPosX_3+ConLength_3*COS(ConAngle_3), ConPosY_3+ConLength_3*(-SIN(ConAngle_3)), ConPosZ_3
		ROTz 180-ConAngle_3
		GOSUB 10
		DEL 2
	ENDIF
ENDIF

IF ((MEP_cline_show and show_axis_and) or show_axis) AND operatedMode < EPS THEN			! -----> center line <----- !
	PEN MEP_cline_pen
	MODEL WIRE
	ADDy ductHalfWidthA+ConThroatLength_2
	MULy -1
	ROTz 90-ConAngle_2
	ARC ConThroatLength_2+ductHalfWidthB, 0, ConAngle_2
	DEL 3

	ADDy -ductHalfWidthA-ConThroatLength_3
	ROTz 90-ConAngle_3
	ARC ConThroatLength_3+ductHalfWidthC, 0, ConAngle_3
	DEL 2
	MODEL SOLID
	PEN gs_cont_pen
ENDIF

IF MEP_InsShow and show_isol and not(show_axis) THEN
	MATERIAL gs_InsMat

	ductHalfWidthA = ductHalfWidthA+MEP_InsThickness
	ductHalfHeight = ductHalfHeight+MEP_InsThickness
	ductHalfWidthB = ductHalfWidthB+MEP_InsThickness
	ductHalfWidthC = ductHalfWidthC+MEP_InsThickness

	r2 = r2+MEP_InsThickness
	r3 = r3+MEP_InsThickness
	xx = (dd^2-r3^2+r2^2)/(2*dd)
	yy = SQR(r2^2-xx^2)
	xx = xx-ConThroatLength_2-MEP_NumConnectionData[1][12]/2

	ConThroatLength_2 = ConThroatLength_2 - MEP_InsThickness
	ConThroatLength_3 = ConThroatLength_3 - MEP_InsThickness

	ADDz -ductHalfHeight
	PRISM_ 9, ductHalfHeight*2,
		0, ductHalfWidthA, 79,
		0, ductHalfWidthA+ConThroatLength_2, 915,
		ConThroatLength_2*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-ConThroatLength_2*COS(ConAngle_2), 3007+addStatus,
		(ConThroatLength_2+ductHalfWidthB*2)*SIN(ConAngle_2), ductHalfWidthA+ConThroatLength_2-(ConThroatLength_2+ductHalfWidthB*2)*COS(ConAngle_2), 79,
		yy, -xx, 3079,
		0, -ductHalfWidthA-ConThroatLength_3, 915,
		(ConThroatLength_3+ductHalfWidthC*2)*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+(ConThroatLength_3+ductHalfWidthC*2)*COS(ConAngle_3), 3007+addStatus,
		ConThroatLength_3*SIN(ConAngle_3), -ductHalfWidthA-ConThroatLength_3+ConThroatLength_3*COS(ConAngle_3), 79,
		0, -ductHalfWidthA, 3007+addStatus
	DEL 1
ENDIF
DEL 1

END


10:
	ROTy 90
	PRISM_ 10, length,
		halfHeightC, halfWidthC, 15,
		-halfHeightC, halfWidthC, 15,
		-halfHeightC, -halfWidthC, 15,
		halfHeightC, -halfWidthC, 15,
		halfHeightC, halfWidthC, -1,
		halfHeight, halfWidth, 15,
		-halfHeight, halfWidth, 15,
		-halfHeight, -halfWidth, 15,
		halfHeight, -halfWidth, 15,
		halfHeight, halfWidth, -1
	DEL 1
RETURN
