!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
duct_show = duct_show*not(show_axis) 
PEN gs_cont_pen
RESOL res
gs_DuctMat = sMat
duct_show = duct_show*not(show_axis) 

HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200
hceID_3 = HptsUnIDBeginForConnEnds+300

operatedMode = 0
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	operatedMode = 1
	addStatus = 8
ENDIF

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


IF duct_show and not(show_axis) THEN				! -----> duct+connection <----- !

	MATERIAL gs_DuctMat


	HOTSPOT ConPosX_1, ConPosY_1, ConPosZ_1, hceID_1
	HOTSPOT ConPosX_2+ConLength_2*COS(ang), ConPosY_2+ConLength_2*SIN(ang), ConPosZ_2, hceID_2
	HOTSPOT ConPosX_3, ConPosY_3, ConPosZ_3, hceID_3

	branchLeftLength = branchLength - connectorLengthB
	branchRightLength = branchLength - connectorLengthC

	ADDz -ductHalfHeightA

	ang = branchAngle/2

	PUT connectorLengthA, -ductHalfWidthA, 7+addStatus
	PUT connectorLengthA, ductHalfWidthA, 15
	PUT mainLength+ductHalfWidthA/TAN(ang)-ductHalfWidthB/SIN(ang), ductHalfWidthA, 15
	PUT mainLength+branchLeftLength*COS(ang)-ductHalfWidthB*SIN(ang), branchLeftLength*SIN(ang)+ductHalfWidthB*COS(ang), 7+addStatus
	PUT mainLength+branchLeftLength*COS(ang)+ductHalfWidthB*SIN(ang), branchLeftLength*SIN(ang)-ductHalfWidthB*COS(ang), 15
	PUT mainLength+ductHalfWidthB/SIN(ang), 0, 15
	PUT mainLength+branchRightLength*COS(ang)+ductHalfWidthB*SIN(ang), -branchRightLength*SIN(ang)+ductHalfWidthB*COS(ang), 7+addStatus
	PUT mainLength+branchRightLength*COS(ang)-ductHalfWidthB*SIN(ang), -branchRightLength*SIN(ang)-ductHalfWidthB*COS(ang), 15
	PUT mainLength+ductHalfWidthA/TAN(ang)-ductHalfWidthB/SIN(ang), -ductHalfWidthA, 15

	PRISM_ NSP/3, ductHalfHeightA*2,

		GET(NSP)

	DEL 1

	IF flangeA THEN
		length		= connectorLengthA
		halfWidth 	= ductHalfWidthA
		halfHeight 	= ductHalfHeightA
		halfWidthC 	= connectorHalfWidthA
		halfHeightC = connectorHalfHeightA

		ROTy 90
		GOSUB 200+shapeTypeA
		DEL 1
	ENDIF

	IF flangeB THEN
		length		= connectorLengthB
		halfWidth 	= ductHalfWidthB
		halfHeight 	= ductHalfHeightA
		halfWidthC 	= connectorHalfWidthB
		halfHeightC = connectorHalfHeightB

		ADD ConPosX_2+ConLength_2*COS(ang), ConPosY_2+ConLength_2*SIN(ang), ConPosZ_2
		ROTz ang-180
		ROTy 90
		GOSUB 200+shapeTypeB
		DEL 3
	ENDIF

	IF flangeC THEN
		length		= connectorLengthC
		halfWidth 	= ductHalfWidthC
		halfHeight 	= ductHalfHeightA
		halfWidthC 	= connectorHalfWidthC
		halfHeightC = connectorHalfHeightC

		ADD ConPosX_3+ConLength_3*COS(ang), ConPosY_3+ConLength_3*(-SIN(ang)), ConPosZ_3
		ROTz -ang+180
		ROTy 90
		GOSUB 200+shapeTypeC
		DEL 3
	ENDIF
ENDIF


IF (show_axis_and or show_axis) AND operatedMode < EPS THEN			! -----> center line <----- !
	PEN MEP_cline_pen
	LIN_ 0, 0, 0, mainLength, 0, 0
	LIN_ mainLength, 0, 0, mainLength+branchLength*COS(ang), branchLength*SIN(ang), 0
	LIN_ mainLength, 0, 0, mainLength+branchLength*COS(ang), -branchLength*SIN(ang), 0
ENDIF


IF MEP_InsShow and show_isol and not(show_axis) THEN			! -----> insulation <----- !
	MATERIAL gs_InsMat

	ductHalfWidthA	= ductHalfWidthA+MEP_InsThickness
	ductHalfHeightA	= ductHalfHeightA+MEP_InsThickness

	ductHalfWidthB	= ductHalfWidthB+MEP_InsThickness

	ang = branchAngle/2
	ADDz -ductHalfHeightA

	PUT 0, -ductHalfWidthA, 7+addStatus
	PUT 0, ductHalfWidthA, 15
	PUT mainLength+ductHalfWidthA/TAN(ang)-ductHalfWidthB/SIN(ang), ductHalfWidthA, 15
	PUT mainLength+branchLength*COS(ang)-ductHalfWidthB*SIN(ang), branchLength*SIN(ang)+ductHalfWidthB*COS(ang), 7+addStatus
	PUT mainLength+branchLength*COS(ang)+ductHalfWidthB*SIN(ang), branchLength*SIN(ang)-ductHalfWidthB*COS(ang), 15
	PUT mainLength+ductHalfWidthB/SIN(ang), 0, 15
	PUT mainLength+branchLength*COS(ang)+ductHalfWidthB*SIN(ang), -branchLength*SIN(ang)+ductHalfWidthB*COS(ang), 7+addStatus
	PUT mainLength+branchLength*COS(ang)-ductHalfWidthB*SIN(ang), -branchLength*SIN(ang)-ductHalfWidthB*COS(ang), 15
	PUT mainLength+ductHalfWidthA/TAN(ang)-ductHalfWidthB/SIN(ang), -ductHalfWidthA, 15

	PRISM_ NSP/3, ductHalfHeightA*2,

		GET(NSP)

	DEL 1
ENDIF

DEL 1

END


					! ----------------- !
					! -- Rectangular -- !
					! ----------------- !

201:
	PRISM_ 10, length,
		halfHeightC, halfWidthC, 15,
		-halfHeightC, halfWidthC, 15,
		-halfHeightC, -halfWidthC, 15,
		halfHeightC, -halfWidthC, 15,
		halfHeightC, halfWidthC, -1,
		halfHeight, halfWidth, 15,
		-halfHeight, halfWidth, 15,
		-halfHeight, -halfWidth, 15,
		halfHeight, -halfWidth, 15,
		halfHeight, halfWidth, -1
RETURN


					! ---------------- !
					! --- Circular --- !
					! ---------------- !

202:
	PRISM_ 4, length,
		0, 0, 979,
		halfWidthC, 360, 4079,
		0, 0, 979,
		halfWidth, 360, 4079
RETURN


					! ---------------- !
					! ----- Oval ----- !
					! ---------------- !

203:
	IF ABS(halfWidth-halfHeight) < EPS THEN
		PRISM_ 4, length,
			0, 0, 979,
			halfWidthC, 360, 4079,
			0, 0, 979,
			halfWidth, 360, 4079
	ENDIF
	IF NOT(ABS(halfWidth-halfHeight) < EPS) AND halfWidth > halfHeight THEN
		rrC = halfHeightC
		rr = halfHeight
		PRISM_ 12, length,
			halfHeightC, -halfWidthC+rrC, 79,
			halfHeightC, halfWidthC-rrC, 79,
			-halfHeightC, halfWidthC-rrC, 1079,
			-halfHeightC, -halfWidthC+rrC, 79,
			halfHeightC, -halfWidthC+rrC, 1079,
			halfHeightC, -halfWidthC+rrC, -1,
			halfHeight, -halfWidth+rr, 79,
			halfHeight, halfWidth-rr, 79,
			-halfHeight, halfWidth-rr, 1079,
			-halfHeight, -halfWidth+rr, 79,
			halfHeight, -halfWidth+rr, 1079,
			halfHeight, -halfWidth+rr, -1
	ENDIF
	IF NOT(ABS(halfWidth-halfHeight) < EPS) AND halfWidth < halfHeight THEN
		rrC = halfWidthC
		rr = halfWidth
		PRISM_ 12, length,
			halfHeightC-rrC, halfWidthC, 79,
			-halfHeightC+rrC, halfWidthC, 79,
			-halfHeightC+rrC, -halfWidthC, 1079,
			halfHeightC-rrC, -halfWidthC, 79,
			halfHeightC-rrC, halfWidthC, 1079,
			halfHeightC-rrC, halfWidthC, -1,
			halfHeight-rr, halfWidth, 79,
			-halfHeight+rr, halfWidth, 79,
			-halfHeight+rr, -halfWidth, 1079,
			halfHeight-rr, -halfWidth, 79,
			halfHeight-rr, halfWidth, 1079,
			halfHeight-rr, halfWidth, -1
	ENDIF
RETURN
