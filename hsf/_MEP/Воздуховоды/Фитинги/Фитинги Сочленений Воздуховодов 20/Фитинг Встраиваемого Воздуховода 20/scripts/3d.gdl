!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Duct Nested Fitting


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

PEN gs_cont_pen
MATERIAL gs_DuctMat


XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200
hceID_3 = HptsUnIDBeginForConnEnds+300
hceID_4 = HptsUnIDBeginForConnEnds+400

HOTSPOT ConPosX_1, ConPosY_1, ConPosZ_1, hceID_1
HOTSPOT ConPosX_2, ConPosY_2, ConPosZ_2, hceID_2
HOTSPOT ConPosX_3, ConPosY_3, ConPosZ_3, hceID_3
IF NOT(typeID = CONNTYPE_ID_1) AND NOT(typeID = CONNTYPE_ID_2) THEN HOTSPOT ConPosX_4, ConPosY_4, ConPosZ_4, hceID_4

ROTy 90

thick = MEP_NumConnectionData[1][14]*8

IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	addStatus = 3
ELSE
	addStatus = 0

	hID = 1
	IF typeID = CONNTYPE_ID_1 OR typeID = CONNTYPE_ID_3 THEN
		IF not(show_axis) THEN PLANE 4,
			ductHalfHeightA-EPS, -ConWidth_1/2+ConWidth_2, MEP_StraightLength,
			-ductHalfHeightA+EPS, -ConWidth_1/2+ConWidth_2, MEP_StraightLength,
			-ductHalfHeightA+EPS, -ConWidth_1/2+ConWidth_2, MEP_StraightLength-thick,
			ductHalfHeightA-EPS, -ConWidth_1/2+ConWidth_2, MEP_StraightLength-thick

		HOTSPOT 0, -ConWidth_1/2, MEP_StraightLength, hID, ConWidth_2, 1+128: hID = hID+1
		HOTSPOT 0, -ConWidth_1/2-1, MEP_StraightLength, hID, ConWidth_2, 3: hID = hID+1
		HOTSPOT 0, -ConWidth_1/2+ConWidth_2, MEP_StraightLength, hID, ConWidth_2, 2: hID = hID+1
	ENDIF
	IF typeID = CONNTYPE_ID_2 OR typeID = CONNTYPE_ID_5 THEN
		IF not(show_axis) THEN PLANE 4,
			-ConDepth_1/2+ConDepth_2, ductHalfWidthA-EPS, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_2, -ductHalfWidthA+EPS, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_2, -ductHalfWidthA+EPS, MEP_StraightLength-thick,
			-ConDepth_1/2+ConDepth_2, ductHalfWidthA-EPS, MEP_StraightLength-thick

		HOTSPOT -ConDepth_1/2, 0, MEP_StraightLength, hID, ConDepth_2, 1+128: hID = hID+1
		HOTSPOT -ConDepth_1/2-1, 0, MEP_StraightLength, hID, ConDepth_2, 3: hID = hID+1
		HOTSPOT -ConDepth_1/2+ConDepth_2, 0, MEP_StraightLength, hID, ConDepth_2, 2: hID = hID+1
	ENDIF
	IF typeID = CONNTYPE_ID_3 THEN
		IF not(show_axis) THEN PLANE 4,
			-ConDepth_1/2+ConDepth_3, -ConWidth_1/2+ConWidth_2+EPS, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_3, ductHalfWidthA-EPS, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_3, ductHalfWidthA-EPS, MEP_StraightLength-thick,
			-ConDepth_1/2+ConDepth_3, -ConWidth_1/2+ConWidth_2+EPS, MEP_StraightLength-thick

		HOTSPOT -ConDepth_1/2, ConWidth_1/2-ConWidth_3/2, MEP_StraightLength, hID, ConDepth_3, 1+128: hID = hID+1
		HOTSPOT -ConDepth_1/2-1, ConWidth_1/2-ConWidth_3/2, MEP_StraightLength, hID, ConDepth_3, 3: hID = hID+1
		HOTSPOT -ConDepth_1/2+ConDepth_3, ConWidth_1/2-ConWidth_3/2, MEP_StraightLength, hID, ConDepth_3, 2: hID = hID+1
	ENDIF
	IF typeID = CONNTYPE_ID_4 THEN
		IF not(show_axis) THEN PLANE 4,
			ductHalfHeightA-EPS, ConWidth_1/2-ConWidth_2, MEP_StraightLength,
			-ductHalfHeightA+EPS, ConWidth_1/2-ConWidth_2, MEP_StraightLength,
			-ductHalfHeightA+EPS, ConWidth_1/2-ConWidth_2, MEP_StraightLength-thick,
			ductHalfHeightA-EPS, ConWidth_1/2-ConWidth_2, MEP_StraightLength-thick

		IF not(show_axis) THEN PLANE 4,
			-ConDepth_1/2+ConDepth_3, ConWidth_1/2-ConWidth_2-EPS, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_3, -ductHalfWidthA+EPS, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_3, -ductHalfWidthA+EPS, MEP_StraightLength-thick,
			-ConDepth_1/2+ConDepth_3, ConWidth_1/2-ConWidth_2-EPS, MEP_StraightLength-thick

		HOTSPOT 0, ConWidth_1/2, MEP_StraightLength, hID, ConWidth_2, 1+128: hID = hID+1
		HOTSPOT 0, ConWidth_1/2+1, MEP_StraightLength, hID, ConWidth_2, 3: hID = hID+1
		HOTSPOT 0, ConWidth_1/2-ConWidth_2, MEP_StraightLength, hID, ConWidth_2, 2: hID = hID+1

		HOTSPOT -ConDepth_1/2, -ConWidth_1/2+ConWidth_3/2, MEP_StraightLength, hID, ConDepth_3, 1+128: hID = hID+1
		HOTSPOT -ConDepth_1/2-1, -ConWidth_1/2+ConWidth_3/2, MEP_StraightLength, hID, ConDepth_3, 3: hID = hID+1
		HOTSPOT -ConDepth_1/2+ConDepth_3, -ConWidth_1/2+ConWidth_3/2, MEP_StraightLength, hID, ConDepth_3, 2: hID = hID+1
	ENDIF
	IF typeID = CONNTYPE_ID_5 THEN
		IF not(show_axis) THEN PLANE 4,
			ductHalfHeightA-EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_2+EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength,
			-ConDepth_1/2+ConDepth_2+EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength-thick,
			ductHalfHeightA-EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength-thick

		HOTSPOT ConDepth_1/2-ConDepth_3/2, -ConWidth_1/2, MEP_StraightLength, hID, ConWidth_3, 1+128: hID = hID+1
		HOTSPOT ConDepth_1/2-ConDepth_3/2, -ConWidth_1/2-1, MEP_StraightLength, hID, ConWidth_3, 3: hID = hID+1
		HOTSPOT ConDepth_1/2-ConDepth_3/2, -ConWidth_1/2+ConWidth_3, MEP_StraightLength, hID, ConWidth_3, 2: hID = hID+1
	ENDIF
	IF typeID = CONNTYPE_ID_6 THEN
		IF not(show_axis) THEN PLANE 4,
			ConDepth_1/2-ConDepth_2, ductHalfWidthA-EPS, MEP_StraightLength,
			ConDepth_1/2-ConDepth_2, -ductHalfWidthA+EPS, MEP_StraightLength,
			ConDepth_1/2-ConDepth_2, -ductHalfWidthA+EPS, MEP_StraightLength-thick,
			ConDepth_1/2-ConDepth_2, ductHalfWidthA-EPS, MEP_StraightLength-thick

		IF not(show_axis) THEN PLANE 4,
			ConDepth_1/2-ConDepth_2-EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength,
			-ductHalfHeightA+EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength,
			-ductHalfHeightA+EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength-thick,
			ConDepth_1/2-ConDepth_2-EPS, -ConWidth_1/2+ConWidth_3, MEP_StraightLength-thick

		HOTSPOT ConDepth_1/2, 0, MEP_StraightLength, hID, ConDepth_2, 1+128: hID = hID+1
		HOTSPOT ConDepth_1/2+1, 0, MEP_StraightLength, hID, ConDepth_2, 3: hID = hID+1
		HOTSPOT ConDepth_1/2-ConDepth_2, 0, MEP_StraightLength, hID, ConDepth_2, 2: hID = hID+1

		HOTSPOT -ConDepth_1/2+ConDepth_3/2, -ConWidth_1/2, MEP_StraightLength, hID, ConWidth_3, 1+128: hID = hID+1
		HOTSPOT -ConDepth_1/2+ConDepth_3/2, -ConWidth_1/2-1, MEP_StraightLength, hID, ConWidth_3, 3: hID = hID+1
		HOTSPOT -ConDepth_1/2+ConDepth_3/2, -ConWidth_1/2+ConWidth_3, MEP_StraightLength, hID, ConWidth_3, 2: hID = hID+1
	ENDIF
ENDIF


IF duct_show and not(show_axis) THEN		! -----> duct flanges <----- !
	MATERIAL gs_DuctMat

	IF flangeA THEN
		length		= connectorLengthA
		halfWidth 	= ductHalfWidthA
		halfHeight 	= ductHalfHeightA
		halfWidthC 	= connectorHalfWidthA
		halfHeightC = connectorHalfHeightA

		GOSUB 200+shapeTypeA
	ENDIF

	ADDz connectorLengthA

	ww = ductHalfWidthA: dd = ductHalfHeightA

	PUT dd, ww, 0
	PUT -dd, ww, 0
	PUT -dd, -ww, 0
	PUT dd, -ww, 0

	mm = MEP_StraightLength-connectorLengthA-connectorLengthB

	PUT dd, ww, mm
	PUT -dd, ww, mm
	PUT -dd, -ww, mm
	PUT dd, -ww, mm

	RULED NSP/6, addStatus+4+16+32,

		GET(NSP)

	DEL 1

ENDIF

IF MEP_InsShow and show_isol and not(show_axis) THEN			! -----> insulation <----- !
	MATERIAL gs_InsMat

	ductInsHalfWidthA 	= ductHalfWidthA+MEP_InsThickness
	ductInsHalfHeightA 	= ductHalfHeightA+MEP_InsThickness

	ww = ductInsHalfWidthA: dd = ductInsHalfHeightA

	PUT dd, ww, 0
	PUT -dd, ww, 0
	PUT -dd, -ww, 0
	PUT dd, -ww, 0

	PUT dd, ww, MEP_StraightLength
	PUT -dd, ww, MEP_StraightLength
	PUT -dd, -ww, MEP_StraightLength
	PUT dd, -ww, MEP_StraightLength

	RULED NSP/6, addStatus+4+16+32,

		GET(NSP)
ENDIF

DEL 2


END


201:
	PRISM_ 10, length,
		halfHeightC, halfWidthC, 15,
		-halfHeightC, halfWidthC, 15,
		-halfHeightC, -halfWidthC, 15,
		halfHeightC, -halfWidthC, 15,
		halfHeightC, halfWidthC, -1,
		halfHeight, halfWidth, 15,
		-halfHeight, halfWidth, 15,
		-halfHeight, -halfWidth, 15,
		halfHeight, -halfWidth, 15,
		halfHeight, halfWidth, -1
RETURN

202:
	PRISM_ 4, length,
		0, 0, 979,
		halfWidthC, 360, 4079,
		0, 0, 979,
		halfWidth, 360, 4079
RETURN

203:
	IF ABS(halfWidth-halfHeight) < EPS THEN
		PRISM_ 4, length,
			0, 0, 979,
			halfWidthC, 360, 4079,
			0, 0, 979,
			halfWidth, 360, 4079
	ENDIF
	IF NOT(ABS(halfWidth-halfHeight) < EPS) AND halfWidth > halfHeight THEN
		rrC = halfHeightC
		rr = halfHeight
		PRISM_ 12, length,
			halfHeightC, -halfWidthC+rrC, 79,
			halfHeightC, halfWidthC-rrC, 79,
			-halfHeightC, halfWidthC-rrC, 1079,
			-halfHeightC, -halfWidthC+rrC, 79,
			halfHeightC, -halfWidthC+rrC, 1079,
			halfHeightC, -halfWidthC+rrC, -1,
			halfHeight, -halfWidth+rr, 79,
			halfHeight, halfWidth-rr, 79,
			-halfHeight, halfWidth-rr, 1079,
			-halfHeight, -halfWidth+rr, 79,
			halfHeight, -halfWidth+rr, 1079,
			halfHeight, -halfWidth+rr, -1
	ENDIF
	IF NOT(ABS(halfWidth-halfHeight) < EPS) AND halfWidth < halfHeight THEN
		rrC = halfWidthC
		rr = halfWidth
		PRISM_ 12, length,
			halfHeightC-rrC, halfWidthC, 79,
			-halfHeightC+rrC, halfWidthC, 79,
			-halfHeightC+rrC, -halfWidthC, 1079,
			halfHeightC-rrC, -halfWidthC, 79,
			halfHeightC-rrC, halfWidthC, 1079,
			halfHeightC-rrC, halfWidthC, -1,
			halfHeight-rr, halfWidth, 79,
			-halfHeight+rr, halfWidth, 79,
			-halfHeight+rr, -halfWidth, 1079,
			halfHeight-rr, -halfWidth, 79,
			halfHeight-rr, halfWidth, 1079,
			halfHeight-rr, halfWidth, -1
	ENDIF
RETURN
