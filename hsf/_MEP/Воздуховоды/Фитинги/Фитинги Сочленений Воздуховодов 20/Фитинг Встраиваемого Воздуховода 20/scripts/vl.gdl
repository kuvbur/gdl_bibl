
! Duct Nested Fitting


call "FM_types_MEP" parameters
DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 1		! Rectangle
	shape_typ_to_validation[2] = 0		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0

VALUES "MEP_StraightLength" RANGE (0, ]
VALUES "MEP_InsThickness" RANGE(0, ]

VALUES{2} "typeID"	CONNTYPE_ID_1,	type_dim[1],
					CONNTYPE_ID_2,	type_dim[2],
					CONNTYPE_ID_3,	type_dim[3],
					CONNTYPE_ID_4,	type_dim[4],
					CONNTYPE_ID_5,	type_dim[5],
					CONNTYPE_ID_6,	type_dim[6]

IF useSysMat AND MEP_StrConnectionData[1][2] <> "0" THEN LOCK "gs_DuctMat", "gs_InsMat"
IF MEP_StrConnectionData[1][2] = "0" THEN LOCK "useSysMat"

					! --- CONNECTIONS --- !

IF typeID = CONNTYPE_ID_1 THEN MEP_NumberConnections = 3
IF typeID = CONNTYPE_ID_2 THEN MEP_NumberConnections = 3
IF typeID = CONNTYPE_ID_3 THEN MEP_NumberConnections = 4
IF typeID = CONNTYPE_ID_4 THEN MEP_NumberConnections = 4
IF typeID = CONNTYPE_ID_5 THEN MEP_NumberConnections = 4
IF typeID = CONNTYPE_ID_6 THEN MEP_NumberConnections = 4

IF MEP_NumberConnections < 4 THEN
	ConStatus_4 = 0
	PARAMETERS ConStatus_4 = ConStatus_4
ELSE
	ConStatus_4 = 1
	PARAMETERS ConStatus_4 = ConStatus_4
ENDIF

gs_ui = 1


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Основной`

	ConDVecX = -1
	ConDVecY = 0
	ConDVecZ = 0

	ConWVecX = 0
	ConWVecY = -1
	ConWVecZ = 0


	IF NOT(setProgram) THEN
		IF ConWidth_1 < EPS THEN
			ConWidth_1 = 0.001
			PARAMETERS ConWidth_1 = ConWidth_1
		ENDIF
		VALUES "ConWidth_1" RANGE (0,]

		IF ConDepth_1 < EPS THEN
			ConDepth_1 = 0.001
			PARAMETERS ConDepth_1 = ConDepth_1
		ENDIF
		VALUES "ConDepth_1" RANGE (0,]

		IF ConWallThickness_1 < 0 THEN
			ConWallThickness_1 = 0
			PARAMETERS ConWallThickness_1 = ConWallThickness_1
		ENDIF
		VALUES "ConWallThickness_1" RANGE [0,]

		isConLength = 1
		IF ABS(gs_AddConnections[1][1] - 1) < EPS THEN isConLength = 0
		ConLength_1 = ConWallThickness_1*isConLength
		PARAMETERS ConLength_1 = ConLength_1
		ConPosX_1 = ConLength_1
		PARAMETERS ConPosX_1 = ConPosX_1
		ConPosY_1 = 0
		PARAMETERS ConPosY_1 = ConPosY_1
		ConPosZ_1 = 0
		PARAMETERS ConPosZ_1 = ConPosZ_1

		LOCK "ConStatus_1"
	ENDIF


	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		MEP_bEnableInsulation = 1,
		MEP_InsShow = MEP_InsShow,
		gs_ui = gs_ui,
		ui_page_idx = ui_page_idx,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[1][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[1][2] = ConName
PARAMETERS MEP_ConInfo[1][3] = "0"


IF NOT(setProgram) THEN
	ConWallThickness_2 = ConWallThickness_1
	PARAMETERS ConWallThickness_2 = ConWallThickness_2
	ConWallThickness_3 = ConWallThickness_1
	PARAMETERS ConWallThickness_3 = ConWallThickness_3
	ConWallThickness_4 = ConWallThickness_1
	PARAMETERS ConWallThickness_4 = ConWallThickness_4

	IF GLOB_MODPAR_NAME = "typeID" THEN
		IF typeID = CONNTYPE_ID_1 THEN
			ConWidth_2 = (ConWidth_1)/2
			PARAMETERS ConWidth_2 = ConWidth_2
			ConWidth_3 = ConWidth_2
			PARAMETERS ConWidth_3 = ConWidth_3
		ENDIF
		IF typeID = CONNTYPE_ID_2 THEN
			ConDepth_2 = (ConDepth_1)/2
			PARAMETERS ConDepth_2 = ConDepth_2
			ConDepth_3 = ConDepth_2
			PARAMETERS ConDepth_3 = ConDepth_3
		ENDIF
		IF typeID = CONNTYPE_ID_3 OR typeID = CONNTYPE_ID_4 THEN
			ConWidth_2 = (ConWidth_1)/2
			PARAMETERS ConWidth_2 = ConWidth_2
			ConWidth_3 = ConWidth_2
			PARAMETERS ConWidth_3 = ConWidth_3
			ConWidth_4 = ConWidth_2
			PARAMETERS ConWidth_4 = ConWidth_4

			ConDepth_3 = (ConDepth_1)/2
			PARAMETERS ConDepth_3 = ConDepth_3
			ConDepth_4 = ConDepth_3
			PARAMETERS ConDepth_4 = ConDepth_4
		ENDIF
		IF typeID = CONNTYPE_ID_5 OR typeID = CONNTYPE_ID_6 THEN
			ConWidth_3 = (ConWidth_1)/2
			PARAMETERS ConWidth_3 = ConWidth_3
			ConWidth_4 = ConWidth_3
			PARAMETERS ConWidth_4 = ConWidth_4

			ConDepth_2 = (ConDepth_1)/2
			PARAMETERS ConDepth_2 = ConDepth_2
			ConDepth_3 = ConDepth_2
			PARAMETERS ConDepth_3 = ConDepth_3
			ConDepth_4 = ConDepth_2
			PARAMETERS ConDepth_4 = ConDepth_4
		ENDIF
	ENDIF
ENDIF


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Патрубок 1`

	ConDVecX = 1
	ConDVecY = 0
	ConDVecZ = 0

	ConWVecX = 0
	ConWVecY = 1
	ConWVecZ = 0


	IF NOT(setProgram) THEN
		ConSystem_2 = ConSystem_1
		PARAMETERS ConSystem_2 = ConSystem_2
		MEP_StrConnectionData[2][2] = MEP_StrConnectionData[1][2]
		PARAMETERS MEP_StrConnectionData[2][2] = MEP_StrConnectionData[2][2]

		IF typeID = CONNTYPE_ID_1 THEN
			VALUES "ConWidth_2" RANGE [0.001, ConWidth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConWidth_3" THEN
				ConWidth_2 = ConWidth_1-ConWidth_3
				PARAMETERS ConWidth_2 = ConWidth_2
			ENDIF

			ConDepth_2 = ConDepth_1
			PARAMETERS ConDepth_2 = ConDepth_2
			LOCK "ConDepth_2"

			ConPosY_2 = -ConWidth_1/2+ConWidth_2/2
			PARAMETERS ConPosY_2 = ConPosY_2
			ConPosZ_2 = 0
			PARAMETERS ConPosZ_2 = ConPosZ_2
		ENDIF

		IF typeID = CONNTYPE_ID_2 THEN
			VALUES "ConDepth_2" RANGE [0.001, ConDepth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConDepth_3" THEN
				ConDepth_2 = ConDepth_1-ConDepth_3
				PARAMETERS ConDepth_2 = ConDepth_2
			ENDIF

			ConWidth_2 = ConWidth_1
			PARAMETERS ConWidth_2 = ConWidth_2
			LOCK "ConWidth_2"

			ConPosY_2 = 0
			PARAMETERS ConPosY_2 = ConPosY_2

			ConPosZ_2 = ConDepth_1/2-ConDepth_2/2
			PARAMETERS ConPosZ_2 = ConPosZ_2
		ENDIF

		IF typeID = CONNTYPE_ID_3 OR typeID = CONNTYPE_ID_4 THEN
			VALUES "ConWidth_2" RANGE [0.001, ConWidth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConWidth_3" THEN
				ConWidth_2 = ConWidth_1-ConWidth_3
				PARAMETERS ConWidth_2 = ConWidth_2
				ConWidth_4 = ConWidth_3
				PARAMETERS ConWidth_4 = ConWidth_4
			ENDIF

			IF GLOB_MODPAR_NAME = "ConWidth_4" THEN
				ConWidth_2 = ConWidth_1-ConWidth_4
				PARAMETERS ConWidth_2 = ConWidth_2
				ConWidth_3 = ConWidth_4
				PARAMETERS ConWidth_3 = ConWidth_3
			ENDIF

			ConDepth_2 = ConDepth_1
			PARAMETERS ConDepth_2 = ConDepth_2
			LOCK "ConDepth_2"

			IF typeID = CONNTYPE_ID_3 THEN
				ConPosY_2 = -ConWidth_1/2+ConWidth_2/2
				PARAMETERS ConPosY_2 = ConPosY_2
			ENDIF
			IF typeID = CONNTYPE_ID_4 THEN
				ConPosY_2 = ConWidth_1/2-ConWidth_2/2
				PARAMETERS ConPosY_2 = ConPosY_2
			ENDIF
			ConPosZ_2 = 0
			PARAMETERS ConPosZ_2 = ConPosZ_2
		ENDIF

		IF typeID = CONNTYPE_ID_5 OR typeID = CONNTYPE_ID_6 THEN
			VALUES "ConDepth_2" RANGE [0.001, ConDepth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConDepth_3" THEN
				ConDepth_2 = ConDepth_1-ConDepth_3
				PARAMETERS ConDepth_2 = ConDepth_2
				ConDepth_4 = ConDepth_3
				PARAMETERS ConDepth_4 = ConDepth_4
			ENDIF

			IF GLOB_MODPAR_NAME = "ConDepth_4" THEN
				ConDepth_2 = ConDepth_1-ConDepth_4
				PARAMETERS ConDepth_2 = ConDepth_2
				ConDepth_3 = ConDepth_4
				PARAMETERS ConDepth_3 = ConDepth_3
			ENDIF

			ConWidth_2 = ConWidth_1
			PARAMETERS ConWidth_2 = ConWidth_2
			LOCK "ConWidth_2"

			ConPosY_2 = 0
			PARAMETERS ConPosY_2 = ConPosY_2

			IF typeID = CONNTYPE_ID_5 THEN
				ConPosZ_2 = ConDepth_1/2-ConDepth_2/2
				PARAMETERS ConPosZ_2 = ConPosZ_2
			ENDIF
			IF typeID = CONNTYPE_ID_6 THEN
				ConPosZ_2 = -ConDepth_1/2+ConDepth_2/2
				PARAMETERS ConPosZ_2 = ConPosZ_2
			ENDIF
		ENDIF

		isConLength = 1
		IF ABS(gs_AddConnections[2][1] - 1) < EPS THEN isConLength = 0
		ConLength_2 = ConWallThickness_1*isConLength
		PARAMETERS ConLength_2 = ConLength_2
		ConPosX_2 = MEP_StraightLength-ConLength_2
		PARAMETERS ConPosX_2 = ConPosX_2

		LOCK "ConStatus_2"
	ENDIF


	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2,
		ConPosY = ConPosY_2,
		ConPosZ = ConPosZ_2,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		gs_ui = gs_ui,
		MEP_bEnableInsulation = 1,
		MEP_InsShow = MEP_InsShow,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[2][1] = STR(ConToolType_2, 1, 0)
PARAMETERS MEP_ConInfo[2][2] = ConName
PARAMETERS MEP_ConInfo[2][3] = "0"


					! --- 3rd CONNECTION --- !

	ConID = 3
	ConName = `Патрубок 2`

	ConDVecX = 1
	ConDVecY = 0
	ConDVecZ = 0

	ConWVecX = 0
	ConWVecY = 1
	ConWVecZ = 0

	IF NOT(setProgram) THEN
		ConSystem_3 = ConSystem_1
		PARAMETERS ConSystem_3 = ConSystem_3
		MEP_StrConnectionData[3][2] = MEP_StrConnectionData[1][2]
		PARAMETERS MEP_StrConnectionData[3][2] = MEP_StrConnectionData[3][2]

		IF typeID = CONNTYPE_ID_1 THEN
			VALUES "ConWidth_3" RANGE [0.001, ConWidth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConWidth_2" THEN
				ConWidth_3 = ConWidth_1-ConWidth_2
				PARAMETERS ConWidth_3 = ConWidth_3
			ENDIF

			ConDepth_3 = ConDepth_1
			PARAMETERS ConDepth_3 = ConDepth_3
			LOCK "ConDepth_3"

			ConPosY_3 = ConWidth_1/2-ConWidth_3/2
			PARAMETERS ConPosY_3 = ConPosY_3
			ConPosZ_3 = 0
			PARAMETERS ConPosZ_3 = ConPosZ_3
		ENDIF

		IF typeID = CONNTYPE_ID_2 THEN
			VALUES "ConDepth_3" RANGE [0.001, ConDepth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConDepth_2" THEN
				ConDepth_3 = ConDepth_1-ConDepth_2
				PARAMETERS ConDepth_3 = ConDepth_3
			ENDIF

			ConWidth_3 = ConWidth_1
			PARAMETERS ConWidth_3 = ConWidth_3
			LOCK "ConWidth_3"

			ConPosY_3 = 0
			PARAMETERS ConPosY_3 = ConPosY_3

			ConPosZ_3 = -ConDepth_1/2+ConDepth_3/2
			PARAMETERS ConPosZ_3 = ConPosZ_3
		ENDIF

		IF typeID = CONNTYPE_ID_3 OR typeID = CONNTYPE_ID_4 THEN
			VALUES "ConWidth_3" RANGE [0.001, ConWidth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConWidth_2" THEN
				ConWidth_3 = ConWidth_1-ConWidth_2
				PARAMETERS ConWidth_3 = ConWidth_3
			ENDIF

			VALUES "ConDepth_3" RANGE [0.001, ConDepth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConDepth_4" THEN
				ConDepth_3 = ConDepth_1-ConDepth_4
				PARAMETERS ConDepth_3 = ConDepth_3
			ENDIF

			IF typeID = CONNTYPE_ID_3 THEN
				ConPosY_3 = ConWidth_1/2-ConWidth_3/2
				PARAMETERS ConPosY_3 = ConPosY_3
			ENDIF
			IF typeID = CONNTYPE_ID_4 THEN
				ConPosY_3 = -ConWidth_1/2+ConWidth_3/2
				PARAMETERS ConPosY_3 = ConPosY_3
			ENDIF
			ConPosZ_3 = ConDepth_1/2-ConDepth_3/2
			PARAMETERS ConPosZ_3 = ConPosZ_3
		ENDIF

		IF typeID = CONNTYPE_ID_5 OR typeID = CONNTYPE_ID_6 THEN
			VALUES "ConDepth_3" RANGE [0.001, ConDepth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConDepth_2" THEN
				ConDepth_3 = ConDepth_1-ConDepth_2
				PARAMETERS ConDepth_3 = ConDepth_3
				ConDepth_4 = ConDepth_3
				PARAMETERS ConDepth_4 = ConDepth_4
			ENDIF

			IF GLOB_MODPAR_NAME = "ConDepth_4" THEN
				ConDepth_2 = ConDepth_1-ConDepth_4
				PARAMETERS ConDepth_2 = ConDepth_2
				ConDepth_3 = ConDepth_4
				PARAMETERS ConDepth_3 = ConDepth_3
			ENDIF

			VALUES "ConWidth_3" RANGE [0.001, ConWidth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConWidth_4" THEN
				ConWidth_3 = ConWidth_1-ConWidth_4
				PARAMETERS ConWidth_3 = ConWidth_3
			ENDIF

			ConPosY_3 = -ConWidth_1/2+ConWidth_3/2
			PARAMETERS ConPosY_3 = ConPosY_3

			IF typeID = CONNTYPE_ID_5 THEN
				ConPosZ_3 = -ConDepth_1/2+ConDepth_3/2
				PARAMETERS ConPosZ_3 = ConPosZ_3
			ENDIF
			IF typeID = CONNTYPE_ID_6 THEN
				ConPosZ_3 = ConDepth_1/2-ConDepth_3/2
				PARAMETERS ConPosZ_3 = ConPosZ_3
			ENDIF
		ENDIF

		ConLength_3 = ConWallThickness_1*isConLength
		PARAMETERS ConLength_3 = ConLength_3
		ConPosX_3 = MEP_StraightLength-ConLength_3
		PARAMETERS ConPosX_3 = ConPosX_3

		LOCK "ConStatus_3"
	ENDIF


	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_3,
		ConPosY = ConPosY_3,
		ConPosZ = ConPosZ_3,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_3,
		ConNominalDepth = ConNominalDepth_3,
		ConWidth = ConWidth_3,
		ConDepth = ConDepth_3,
		ConLength = ConLength_3,
		ConWallThickness = ConWallThickness_3,
		ConConnectorWidth = ConConnectorWidth_3,
		ConConnectorDepth = ConConnectorDepth_3,
		ConConnectorWidth2 = ConConnectorWidth2_3,
		ConToolType = ConToolType_3,
		ConStatus = ConStatus_3,
		ConType_3 = ConType_3,
		ConConnectorType_3 = ConConnectorType_3,
		ConSystem_3 = ConSystem_3,
		gs_ui = gs_ui,
		MEP_bEnableInsulation = 1,
		MEP_InsShow = MEP_InsShow,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[3][1] = STR(ConToolType_3, 1, 0)
PARAMETERS MEP_ConInfo[3][2] = ConName
PARAMETERS MEP_ConInfo[3][3] = "0"


						! ------------------- !
						! --- calculation --- !
						! ------------------- !

IF ABS(MEP_NumConnectionData[1][21] - 1) < EPS THEN
	gs_list_area = (MEP_NumConnectionData[1][12] + MEP_NumConnectionData[1][13])*2*MEP_StraightLength
	PARAMETERS gs_list_area = gs_list_area
ENDIF

IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
	gs_list_area = MEP_NumConnectionData[1][12]*PI*MEP_StraightLength
	PARAMETERS gs_list_area = gs_list_area
ENDIF

IF ABS(MEP_NumConnectionData[1][21] - 3) < EPS THEN
	gs_list_area = ((MIN(MEP_NumConnectionData[1][12], MEP_NumConnectionData[1][13]))*PI+ABS(MEP_NumConnectionData[1][12]-MEP_NumConnectionData[1][13])*2)*MEP_StraightLength
	PARAMETERS gs_list_area = gs_list_area
ENDIF


IF MEP_NumberConnections < 4 THEN
	HIDEPARAMETER 	"ConNominalWidth_4",
					"ConNominalDepth_4",
					"ConWidth_4",
					"ConDepth_4",
					"ConLength_4",
					"ConWallThickness_4",
					"ConConnectorWidth_4",
					"ConConnectorDepth_4",
					"ConConnectorWidth2_4",
					"ConSystem_4",
					"ConToolType_4",
					"ConType_4",
					"ConStatus_4",
					"ConConnectorType_4",
					"ConPosX_4",
					"ConPosY_4",
					"ConPosZ_4"

	PARAMETERS MEP_NumberConnections = 4
	PARAMETERS MEP_NumConnectionData[4][22] = 0
	PARAMETERS gs_AddConnections[4][22] = 0
	GOTO 1000
ENDIF


					! --- 4th CONNECTION --- !

	ConID = 4
	ConName = `Патрубок 3`

	ConDVecX = 1
	ConDVecY = 0
	ConDVecZ = 0

	ConWVecX = 0
	ConWVecY = 1
	ConWVecZ = 0

	IF NOT(setProgram) THEN
		ConSystem_4 = ConSystem_1
		PARAMETERS ConSystem_4 = ConSystem_4
		MEP_StrConnectionData[4][2] = MEP_StrConnectionData[1][2]
		PARAMETERS MEP_StrConnectionData[4][2] = MEP_StrConnectionData[4][2]

		IF typeID = CONNTYPE_ID_3 OR typeID = CONNTYPE_ID_4 THEN
			VALUES "ConWidth_4" RANGE [0.001, ConWidth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConWidth_2" THEN
				ConWidth_4 = ConWidth_1-ConWidth_2
				PARAMETERS ConWidth_4 = ConWidth_4
			ENDIF

			VALUES "ConDepth_4" RANGE [0.001, ConDepth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConDepth_3" THEN
				ConDepth_4 = ConDepth_1-ConDepth_3
				PARAMETERS ConDepth_4 = ConDepth_4
			ENDIF

			IF typeID = CONNTYPE_ID_3 THEN
				ConPosY_4 = ConWidth_1/2-ConWidth_4/2
				PARAMETERS ConPosY_4 = ConPosY_4
			ENDIF
			IF typeID = CONNTYPE_ID_4 THEN
				ConPosY_4 = -ConWidth_1/2+ConWidth_4/2
				PARAMETERS ConPosY_4 = ConPosY_4
			ENDIF

			ConPosZ_4 = -ConDepth_1/2+ConDepth_4/2
			PARAMETERS ConPosZ_4 = ConPosZ_4
		ENDIF

		IF typeID = CONNTYPE_ID_5 OR typeID = CONNTYPE_ID_6 THEN
			VALUES "ConDepth_4" RANGE [0.001, ConDepth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConDepth_2" THEN
				ConDepth_4 = ConDepth_1-ConDepth_2
				PARAMETERS ConDepth_4 = ConDepth_4
				ConDepth_3 = ConDepth_4
				PARAMETERS ConDepth_3 = ConDepth_3
			ENDIF

			IF GLOB_MODPAR_NAME = "ConDepth_3" THEN
				ConDepth_2 = ConDepth_1-ConDepth_3
				PARAMETERS ConDepth_2 = ConDepth_2
				ConDepth_4 = ConDepth_3
				PARAMETERS ConDepth_4 = ConDepth_4
			ENDIF

			VALUES "ConWidth_4" RANGE [0.001, ConWidth_1-0.001]

			IF GLOB_MODPAR_NAME = "ConWidth_3" THEN
				ConWidth_4 = ConWidth_1-ConWidth_3
				PARAMETERS ConWidth_4 = ConWidth_4
			ENDIF

			ConPosY_4 = ConWidth_1/2-ConWidth_4/2
			PARAMETERS ConPosY_4 = ConPosY_4

			IF typeID = CONNTYPE_ID_5 THEN
				ConPosZ_4 = -ConDepth_1/2+ConDepth_4/2
				PARAMETERS ConPosZ_4 = ConPosZ_4
			ENDIF
			IF typeID = CONNTYPE_ID_6 THEN
				ConPosZ_4 = ConDepth_1/2-ConDepth_4/2
				PARAMETERS ConPosZ_4 = ConPosZ_4
			ENDIF
		ENDIF

		ConLength_4 = ConWallThickness_1*isConLength
		PARAMETERS ConLength_4 = ConLength_4
		ConPosX_4 = MEP_StraightLength-ConLength_4
		PARAMETERS ConPosX_4 = ConPosX_4

		LOCK "ConStatus_4"
	ENDIF


	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_4,
		ConPosY = ConPosY_4,
		ConPosZ = ConPosZ_4,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_4,
		ConNominalDepth = ConNominalDepth_4,
		ConWidth = ConWidth_4,
		ConDepth = ConDepth_4,
		ConLength = ConLength_4,
		ConWallThickness = ConWallThickness_4,
		ConConnectorWidth = ConConnectorWidth_4,
		ConConnectorDepth = ConConnectorDepth_4,
		ConConnectorWidth2 = ConConnectorWidth2_4,
		ConToolType = ConToolType_4,
		ConStatus = ConStatus_4,
		ConType_4 = ConType_4,
		ConConnectorType_4 = ConConnectorType_4,
		ConSystem_4 = ConSystem_4,
		gs_ui = gs_ui,
		MEP_bEnableInsulation = 1,
		MEP_InsShow = MEP_InsShow,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[4][1] = STR(ConToolType_4, 1, 0)
PARAMETERS MEP_ConInfo[4][2] = ConName
PARAMETERS MEP_ConInfo[4][3] = "0"


1000:

FOR k = 1 TO 4
	FOR i = 1 TO 9
		j = 1+INT((i-0.5)/3)
		IF i < 4 THEN ad = MEP_Position[1][i]
		MEP_NumConnectionData[k][i] = outCoords[j+3*(k-1)][i-3*(j-1)]+ad
		PARAMETERS MEP_NumConnectionData[k][i] = MEP_NumConnectionData[k][i]
	NEXT i
NEXT k

IF SetProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ELSE

							! ------------------- !
							! --- calculation --- !
							! ------------------- !

	calcLength = MEP_StraightLength
	gs_list_area = (ConWidth_1 + ConDepth_1)*2*calcLength
	PARAMETERS gs_list_area = gs_list_area
ENDIF
