ishow_zone_num = 1
success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_crane_num", ishow_zone_num)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_gruz", show_gruz)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_work_zone", show_work_zone)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "pen_work", pen_work)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "line_work", line_work)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "mat_work", mat_work)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_save_zone", show_save_zone)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "pen_save", pen_save)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "line_save", line_save)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "mat_save", mat_save)

success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_plosh", show_plosh)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "lin_pl", lin_pl)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "pen_pl", pen_pl)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "fill_pl", fill_pl)

success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_crane_putt", show_crane_putt)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "pen_crane", pen_crane)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "line_crane", line_crane)
success = LIBRARYGLOBAL ("Libraryflobals-mep", "mat_crane", mat_crane)

ds = 0.3
ws = 0.13
wt = 9/1000
ft = 15/1000
crane_op_addz = crane_h1*0.4+crane_h1*0.8
_crane_h = ZZYZX

if crane_type<>CRANE_MONO_MUN and crane_type<>CRANE_MONO_EL then
	HOTSPOT pos_xy[ishow_zone_num][1]+crane_B/2, 0, -ZZYZX/2, unID, pos_xy[ishow_zone_num][2], 1+128: unID=unID+1
	HOTSPOT pos_xy[ishow_zone_num][1]+crane_B/2, pos_xy[ishow_zone_num][2], -ZZYZX/2, unID, pos_xy[ishow_zone_num][2], 2: unID=unID+1
	HOTSPOT pos_xy[ishow_zone_num][1]+crane_B/2, -1, -ZZYZX/2, unID, pos_xy[ishow_zone_num][2], 3: unID=unID+1
	HOTSPOT crane_B/2, pos_xy[ishow_zone_num][2], -ZZYZX/2, unID, pos_xy[ishow_zone_num][1], 1+128: unID=unID+1
	HOTSPOT pos_xy[ishow_zone_num][1]+crane_B/2, pos_xy[ishow_zone_num][2], -ZZYZX/2, unID, pos_xy[ishow_zone_num][1], 2: unID=unID+1
	HOTSPOT -1+crane_B/2, pos_xy[ishow_zone_num][2], -ZZYZX/2, unID, pos_xy[ishow_zone_num][1], 3: unID=unID+1
else
	HOTSPOT crane_B/2, 0, -ZZYZX/2, unID, pos_xy[ishow_zone_num][1], 1+128: unID=unID+1
	HOTSPOT pos_xy[ishow_zone_num][1]+crane_B/2, 0, -ZZYZX/2, unID, pos_xy[ishow_zone_num][1], 2: unID=unID+1
	HOTSPOT -1+crane_B/2, 0, -ZZYZX/2, unID, pos_xy[ishow_zone_num][1], 3: unID=unID+1
endif

gosub "putt_3d"
addx pos_xy[ishow_zone_num][1]
if crane_type<>CRANE_MONO_MUN and crane_type<>CRANE_MONO_EL then gosub "balk_3d"
add crane_B/2, pos_xy[ishow_zone_num][2], 0
if show_gruz then gosub "car_3d"
del 1
gosub "cruk_3d"
gosub "teleg_3d"
del 1
for i=1 to crane_nprol
	add 0, B/crane_nprol, 0
	gosub "putt_3d"
	addx pos_xy[ishow_zone_num][1]
	gosub "teleg_3d"
	del 1
next i
del top
if show_work_zone then gosub "zone_work_3d"
if show_save_zone then gosub "zone_save_3d"

if pl_rasp = "Вконце" then
	add A,B,0
	rotx 180
endif
if show_plosh and pl_rasp<>"Без площадки" then gosub "plosh_3d"


end

"balk_3d":
	gosub "set_crane"
	add crane_B/2, 0, -crane_h1
	hotspot 0,0,0
	bbalk = 0.3
	put -bbalk/2, -crane_l, 15
	put bbalk/2, -crane_l, 15
	put bbalk/2, B+crane_l, 15
	put -bbalk/2, B+crane_l, 15
	if crane_type = CRANE_BEAM_OP then addz crane_op_addz 
	PRISM_ NSP/3, crane_h1*0.8, get(NSP)
	add bbalk/2+0.001,-0.25+pos_xy[ishow_zone_num][2],crane_h1*0.4
	roty 90
	CIRCLE crane_h1*0.8*0.49
	addz -bbalk-0.002
	CIRCLE crane_h1*0.8*0.49
	addy 0.5
	CIRCLE crane_h1*0.8*0.49
	addz bbalk+0.002
	CIRCLE crane_h1*0.8*0.49
	del 6+(crane_type = CRANE_BEAM_OP)
	if crane_type = CRANE_BEAM_OP then addz crane_op_addz 
	add crane_B/2, pos_xy[ishow_zone_num][2], -crane_h1
	lin_ -bbalk/2-0.001, -0.25+crane_h1*0.8*0.49, crane_h1*0.8*0.49, -bbalk/2-0.001, 0.25-crane_h1*0.8*0.49, crane_h1*0.8*0.49	
	lin_ -bbalk/2-0.001, 0, crane_h1*0.8*0.49, -bbalk/2-0.001, 0, 0
	lin_ bbalk/2+0.001, -0.25+crane_h1*0.8*0.49, crane_h1*0.8*0.49, bbalk/2+0.001, 0.25-crane_h1*0.8*0.49, crane_h1*0.8*0.49	
	lin_ bbalk/2+0.001, 0, crane_h1*0.8*0.49, bbalk/2+0.001, 0, 0
	del 1
	if crane_type = CRANE_BEAM_OP then del 1
return

"cruk_3d":
	if crane_type = CRANE_BEAM_OP then addz crane_op_addz 
	gosub "set_crane"
	if crane_type<>CRANE_MONO_MUN and crane_type<>CRANE_MONO_EL then 
		add crane_B/2, pos_xy[ishow_zone_num][2], 0
	else
		add crane_B/2, 0, crane_h1
	endif
	lin_ 0,0,-crane_h1, 0,0,-_crane_h+crane_h1*0.8
	addz -_crane_h+crane_h1*0.4
	roty 90
	rotx 45
	model wire
	arc crane_h1*0.4, -180, 90
	model solid
	hotspot 0,0,-_crane_h
	del 4
	if crane_type = CRANE_BEAM_OP then del 1
return

"car_3d":
	if crane_type = CRANE_BEAM_OP then crane_op_addz 
	gosub "set_crane"
	addz -_crane_h-gruz_strop
	lin_ 0,0,gruz_strop, -gruz_A/2, -gruz_B/2, 0
	lin_ 0,0,gruz_strop, gruz_A/2, -gruz_B/2, 0
	lin_ 0,0,gruz_strop, gruz_A/2, gruz_B/2, 0
	lin_ 0,0,gruz_strop, -gruz_A/2, gruz_B/2, 0
	hotspot 0,0,0
	hotspot 0,0,gruz_strop
	hotspot 0,0,-gruz_H
	put -gruz_A/2, -gruz_B/2, 15
	put gruz_A/2, -gruz_B/2, 15
	put gruz_A/2, gruz_B/2, 15
	put -gruz_A/2, gruz_B/2, 15
	PRISM_ NSP/3, -gruz_H, get(NSP)
	del 1
	if crane_type = CRANE_BEAM_OP then del 1
return

"teleg_3d":
	gosub "set_crane"
	addz ds*0.5
	hotspot 0,-ws/2,0
	hotspot 0,ws/2,0
	hotspot crane_B,ws/2,0
	hotspot crane_B,-ws/2,0
	addx ds*0.4
	rotx 90
	addz ws/2
	CIRCLE ds*0.4
	addz -ws
	CIRCLE ds*0.4
	del 4
	addx crane_B-ds*0.4
	rotx 90
	addz ws/2 
	CIRCLE ds*0.4
	addz -ws
	CIRCLE ds*0.4
	del 5
	if crane_type = CRANE_BEAM_OP then 
		crane_h1v = -crane_h1v
		addz -crane_h1v
	else
		addz -crane_h1v
	endif
	lin_ ds*0.4,-ws/2,0, ds*0.4,-ws/2, ds*0.1+crane_h1v
	lin_ ds*0.4,ws/2,0, ds*0.4,ws/2, ds*0.1+crane_h1v
	lin_ crane_B-ds*0.4,-ws/2,0, crane_B-ds*0.4,-ws/2, ds*0.1+crane_h1v
	lin_ crane_B-ds*0.4,ws/2,0, crane_B-ds*0.4,ws/2, ds*0.1+crane_h1v
	lin_ ds*0.4,-ws/2,0, crane_B-ds*0.4,-ws/2, 0
	lin_ ds*0.4,ws/2,0, crane_B-ds*0.4,ws/2, 0
	lin_ crane_B-ds*0.4,ws/2,0, crane_B-ds*0.4,-ws/2, 0
	lin_ ds*0.4,ws/2,0, ds*0.4,-ws/2, 0
	if crane_type = CRANE_BEAM_OP then
		crane_h1v = -crane_h1v
		del 1
	else
		del 1
	endif
return

"putt_3d":
	if show_crane_putt then 
		gosub "set_crane"
		jt = 15
		n_form = 0
		tstart = NSP
		put -ws*0.5,-ds*0.5,jt
		put -ws*0.5,-ds*0.5+ft,jt
		put -wt*0.5,-ds*0.5+ft,jt
		put -wt*0.5,0,jt
		put -wt*0.5,ds*0.5-ft,jt
		put -ws*0.5,ds*0.5-ft,jt
		put -ws*0.5,ds*0.5,jt
		put ws*0.5,ds*0.5,jt
		put ws*0.5,ds*0.5-ft,jt
		put wt*0.5,ds*0.5-ft,jt
		put wt*0.5,0,jt
		put wt*0.5,-ds*0.5+ft,jt
		put ws*0.5,-ds*0.5+ft,jt
		put ws*0.5,-ds*0.5,jt
		put -ws*0.5,-ds*0.5,-1
		tend = NSP
		n_form = (tend - tstart)/3
		n_path = 0
		tstart = NSP
		if (crane_type=CRANE_MONO_MUN or crane_type=CRANE_MONO_EL) and tal_n>2 then
			if tal_n=3 then
				PUT -0.1, 0, ds/2, 0
				put 0,0,ds/2, 0
				put tal_xy[1][1]-tal_r, 0, ds/2, 0
				for an = 270 to 360 step 10
					x=tal_xy[1][1]-tal_r+tal_r*cos(an)
					y=tal_r+tal_r*sin(an)
					put x, y, ds/2, 0
				next an
				put tal_xy[1][1], tal_r, ds/2, 0
				put tal_xy[1][1], tal_xy[1][2], ds/2, 0
				put tal_xy[1][1], tal_xy[1][2]+0.1, ds/2, 0
			endif
			if tal_n=4 then
				PUT -0.1, 0, ds/2, 0
				put 0,0,ds/2, 0
				put tal_xy[1][1]-tal_r, 0, ds/2, 0
				for an = 270 to 360 step 10
					x=tal_xy[1][1]-tal_r+tal_r*cos(an)
					y=tal_r+tal_r*sin(an)
					put x, y, ds/2, 0
				next an
				put tal_xy[1][1], tal_r, ds/2, 0
				put tal_xy[1][1], tal_xy[1][2]-tal_r, ds/2, 0
				for an = 0 to 90 step 10
					x=tal_xy[1][1]-tal_r+tal_r*cos(an)
					y=tal_xy[1][2]-tal_r+tal_r*sin(an)
					put x, y, ds/2, 0
				next an
				put tal_xy[1][1]-tal_r, tal_xy[1][2], ds/2, 0
				put tal_xy[2][1], tal_xy[1][2], ds/2, 0
				put tal_xy[2][1]-0.1, tal_xy[1][2], ds/2, 0
			endif
		else
			PUT -0.1, 0, ds/2, 0
			PUT 0, 0, ds/2, 0
			PUT A, 0, ds/2,0
			PUT A+0.1, 0, ds/2,0
		endif
		tend = NSP
		n_path = (tend - tstart)/4
		mask = 1+2+16+32
		TUBE n_form, n_path, mask, GET(n_form*3+n_path*4)
		hotspot 0,0,0
		hotspot A,0,0
		hotspot 0,0,ds
		hotspot A,0,ds
	endif
return

"zone_work_3d":
	model SURFACE
	gosub "set_work"
	jt = 5
	n_form = 0
	tstart = NSP
	put -(crane_l-crane_l1l2),-ds*0.5-ZZYZX,jt
	put B+crane_l-crane_l1l2,-ds*0.5-ZZYZX,jt
	put B+crane_l-crane_l1l2,-ZZYZX-crane_hp-ds*0.5,jt
	put -(crane_l-crane_l1l2),-ZZYZX-crane_hp-ds*0.5,jt
	put -(crane_l-crane_l1l2),-ds*0.5-ZZYZX,-1
	tend = NSP
	n_form = (tend - tstart)/3
	n_path = 0
	tstart = NSP
	PUT -0.1, 0, ds/2, 0
	PUT crane_B/2, 0, ds/2, 0
	PUT A-crane_B/2, 0, ds/2,0
	PUT A-crane_B/2+0.1, 0, ds/2,0
	tend = NSP
	n_path = (tend - tstart)/4
	mask = 1+2+16+32
	TUBE n_form, n_path, mask, GET(n_form*3+n_path*4)
	model solid
return

"zone_save_3d":
	model SURFACE
	gosub "set_save"
	jt = 5
	n_form = 0
	tstart = NSP
	put -crane_l-l_save,ds*0.5+l_save,jt
	put B+crane_l+l_save,ds*0.5+l_save,jt
	put B+crane_l+l_save,-ZZYZX-crane_hp-ds*0.5,jt
	put -crane_l-l_save,-ZZYZX-crane_hp-ds*0.5,jt
	put -crane_l-l_save,ds*0.5+l_save,-1
	tend = NSP
	n_form = (tend - tstart)/3
	n_path = 0
	tstart = NSP
	PUT -l_save-0.1, 0, ds/2, 0
	PUT -l_save, 0, ds/2, 0
	PUT A+l_save, 0, ds/2,0
	PUT A+l_save+0.1, 0, ds/2,0
	tend = NSP
	n_path = (tend - tstart)/4
	mask = 1+2+16+32
	TUBE n_form, n_path, mask, GET(n_form*3+n_path*4)
	model solid
return

"set_crane":
	pen pen_crane
	MATERIAL mat_crane
	SECT_ATTRS GLOB_FILL_INDEX_BACKGROUND, SYMB_FBGD_PEN, pen_crane, pen_crane, 1
return

"set_save":
	pen pen_save
	MATERIAL mat_save
	SECT_ATTRS GLOB_FILL_INDEX_BACKGROUND, SYMB_FBGD_PEN, pen_save, pen_save, line_save
return

"set_work":
	pen pen_work
	MATERIAL mat_work
	SECT_ATTRS GLOB_FILL_INDEX_BACKGROUND, SYMB_FBGD_PEN, pen_work, pen_work, line_work
return


"plosh_3d":
	addz -pl_otm
	pen pen_pl
	put -pl_b, -pl_l_b, 15
	put -pl_b, B+pl_l_b, 15
	put pl_L, B+pl_l_b, 15
	put pl_L, B-pl_d, 15
	put 0, B-pl_d, 15
	put 0, pl_d, 15
	put pl_L, pl_d, 15
	put pl_L, -pl_l_b, 15
	put -pl_b, -pl_l_b, -1
	PRISM_ NSP/3, 0.2, use(NSP)
	model wire
	PRISM_ NSP/3, -1, use(NSP)
	model solid
!	POLY2_B NSP/3, 1+4+2, pen_pl, SYMB_FBGD_PEN, GET(NSP)
!	if pl_rasp_l = "Вверху" then add2 0,B+pl_l_b
!	line2 pl_L,-pl_l_b, pl_L+pl_l_l ,-pl_l_b
!	line2 pl_L,0, pl_L+pl_l_l ,0
!	LINE_TYPE 1
!	line2 pl_L+pl_l_l ,-pl_l_b, pl_L+pl_l_l ,0
!	for kkk=pl_L to pl_L+pl_l_l step pl_l_l/int(pl_l_l/0.2)
!		line2 kkk ,-pl_l_b,kkk,0
!	next kkk
!	if pl_rasp_l = "Вверху" then del top
	add -pl_b/2,B/2,0
	CALL "Отметка на плане" PARAMETERS ALL, cz = SYMB_POS_Z+pl_otm, ang2 = 0
	del 1
return