!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Steam Air Curtain


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

unID = 1
IF mbShow THEN
				! --- HOTSPOTS --- !

	HOTSPOT A/2, B/2, 0, unID: unID=unID+1
	HOTSPOT -A/2, B/2, 0, unID: unID=unID+1
	HOTSPOT -A/2, -B/2+B*(wrate01+wrate02), 0, unID: unID=unID+1
	HOTSPOT A/2, -B/2+B*(wrate01+wrate02), 0, unID: unID=unID+1

	HOTSPOT A/2, B/2, ZZYZX, unID: unID=unID+1
	HOTSPOT -A/2, B/2, ZZYZX, unID: unID=unID+1
	HOTSPOT -A/2, -B/2, ZZYZX, unID: unID=unID+1
	HOTSPOT A/2, -B/2, ZZYZX, unID: unID=unID+1
	HOTSPOT A/2, -B/2, ZZYZX*hrate, unID: unID=unID+1
	HOTSPOT -A/2, -B/2, ZZYZX*hrate, unID: unID=unID+1


				! --- MAIN BOX --- !

	ROTy -90
	ADD 0, -B/2, -A/2
	IF gs_detlevel_3D_m = 2 THEN
		GROUP "mainbox"
		PRISM_ 7, A,
			0, B, 15,
			ZZYZX, B, 15,
			ZZYZX, 0, 15,
			ZZYZX*hrate, 0, 15,
			ZZYZX*hrate, B*wrate01, 15,
			0, B*(wrate01+wrate02), 15,
			0, B, -1
		ENDGROUP

		GROUP "boxes"
			 num = INT(A/0.25)
			 boxDist = (A-0.05*num)/num+0.05
			FOR i = 1 TO num
				ADD ZZYZX*hrate+0.025, 0, 0.025+boxDist*(i-1)
				BLOCK ZZYZX*(1-hrate)-0.05, 0.025, boxDist-0.05
				DEL 1
			NEXT i
		ENDGROUP

		result = SUBGROUP("mainbox","boxes")
		PLACEGROUP result

		KILLGROUP "mainbox"
		KILLGROUP "boxes"

		FOR i = 1 TO num
			ADDz boxDist*(i-1)
			 numH = 3
			 distH = (boxDist-0.05)/(numH+1)
			FOR j = 1 TO numH
				ADD ZZYZX*hrate+0.025, 0, 0.025+distH*j
				RECT ZZYZX*(1-hrate)-0.05, 0.025
				DEL 1
			NEXT j

			 numV =  INT((ZZYZX*(1-hrate)-0.05)/0.045)-1
			IF numV < EPS THEN numV = 1
			 distV = (ZZYZX*(1-hrate)-0.05)/(numV+1)
			FOR j = 1 TO numV
				ADD ZZYZX*hrate+0.025+distV*j, 0, 0.025
				ROTy -90
				RECT boxDist-0.05, 0.025
				DEL 2
			NEXT j
			DEL 1
		NEXT i
	ELSE
		PRISM_ 7, A,
			0, B, 15,
			ZZYZX, B, 15,
			ZZYZX, 0, 15,
			ZZYZX*hrate, 0, 15,
			ZZYZX*hrate, B*wrate01, 15,
			0, B*(wrate01+wrate02), 15,
			0, B, -1
	ENDIF
	DEL 2
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 17,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 17, MEP_StrConnectionData = MEP_StrConnectionData
END
