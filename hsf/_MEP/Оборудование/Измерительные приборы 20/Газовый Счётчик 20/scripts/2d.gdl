
! Gas Meter



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
!!HOTSPOT2 0, 0, unID: unID=unID+1

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

				! --- MAIN BOX --- !

	HOTSPOT2 A/2, B/2, unID: unID=unID+1
	HOTSPOT2 -A/2, B/2, unID: unID=unID+1
	HOTSPOT2 -A/2, -B/2, unID: unID=unID+1
	HOTSPOT2 A/2, -B/2, unID: unID=unID+1
	HOTSPOT2 0, -B/2, unID: unID=unID+1

	DRAWINDEX 10

	PUT meterBoxSizeX/2, meterBoxSizeY/2-chsize, 1,
		meterBoxSizeX/2-chsize, meterBoxSizeY/2, 1,
		-meterBoxSizeX/2+chsize, meterBoxSizeY/2, 1,
		-meterBoxSizeX/2, meterBoxSizeY/2-chsize, 1,
		-meterBoxSizeX/2, -meterBoxSizeY/2+chsize, 1,
		-meterBoxSizeX/2+chsize, -meterBoxSizeY/2, 1,
		meterBoxSizeX/2-chsize, -meterBoxSizeY/2, 1,
		meterBoxSizeX/2, -meterBoxSizeY/2+chsize, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	LINE2 meterBoxSizeX/2, meterBoxSizeY/2-chsize, -meterBoxSizeX/2, meterBoxSizeY/2-chsize
	LINE2 meterBoxSizeX/2, -meterBoxSizeY/2+chsize, -meterBoxSizeX/2, -meterBoxSizeY/2+chsize


	IF withHouse THEN
		DRAWINDEX 20

		PUT A/2, B/2, 1,
			-A/2, B/2, 1,
			-A/2, -B/2, 1,
			A/2, -B/2, 1

		POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

			GET (NSP)
	ENDIF
ENDIF


DRAWINDEX 10

				! --- OTHER VIEWS --- !

ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 5,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 5
DEL 1

END
