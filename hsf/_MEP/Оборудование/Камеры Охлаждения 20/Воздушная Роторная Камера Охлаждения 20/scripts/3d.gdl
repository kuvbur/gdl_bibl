
! Air Cooled Rotary Chiller


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

operatedMode = 0
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	operatedMode = 1
	addStatus = 3
ENDIF


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT boxLength/2, B/2, 0, unID: unID=unID+1
HOTSPOT -boxLength/2, B/2, 0, unID: unID=unID+1
HOTSPOT -boxLength/2, -B/2, 0, unID: unID=unID+1
HOTSPOT boxLength/2, -B/2, 0, unID: unID=unID+1
HOTSPOT boxLength/2, B/2, legHeight+boxHeight, unID: unID=unID+1
HOTSPOT -boxLength/2, B/2, legHeight+boxHeight, unID: unID=unID+1
HOTSPOT -boxLength/2, -B/2, legHeight+boxHeight, unID: unID=unID+1
HOTSPOT boxLength/2, -B/2, legHeight+boxHeight, unID: unID=unID+1

HOTSPOT boxLength/2+sideBoxThick, -sideBoxDepth/2, legHeight+0.10, unID: unID=unID+1
HOTSPOT boxLength/2+sideBoxThick, sideBoxDepth/2, legHeight+0.10, unID: unID=unID+1
HOTSPOT boxLength/2+sideBoxThick, -sideBoxDepth/2, legHeight+0.10+sideBoxHeight, unID: unID=unID+1
HOTSPOT boxLength/2+sideBoxThick, sideBoxDepth/2, legHeight+0.10+sideBoxHeight, unID: unID=unID+1


				! --- SUPPORT --- !

ADD -boxLength/2, -B/2, 0
ROTy -90
CUTPOLYA 5, 2, 0,
	legThick, legThick, 15,
	legThick, B-legThick, 15,
	legHeight-legThick, B-legThick, 15,
	legHeight-legThick, legThick, 15,
	legThick, legThick, -1
DEL 1

num = INT(boxLength/2.5)
IF NOT(ABS((num/2)-INT(NUM/2)) < EPS) THEN num = num-1
IF num < 2 THEN num = 2
cutLength = (boxLength-(num+1)*legThick)/num

FOR i = 1 TO num
ADDx (legThick+cutLength)*(i-1)
ROTx 90
CUTPOLYA 5, 2, 0,
	legThick, legThick, 15,
	legThick+cutLength, legThick, 15,
	legThick+cutLength, legHeight-legThick, 15,
	legThick, legHeight-legThick, 15,
	legThick, legThick, -1
DEL 2
NEXT i

PUT 0, 0, 15
PUT boxLength, 0, 15
PUT boxLength, B, 15
PUT 0, B, 15
PUT 0, 0, -1
FOR j = 1 TO num
	PUT legThick+(legThick+cutLength)*(j-1), legThick, 15
	PUT legThick+cutLength+(legThick+cutLength)*(j-1), legThick, 15
	PUT legThick+cutLength+(legThick+cutLength)*(j-1), B-legThick, 15
	PUT legThick+(legThick+cutLength)*(j-1), B-legThick, 15
	PUT legThick+(legThick+cutLength)*(j-1), legThick, -1
NEXT j

PRISM_ NSP/3, legHeight,
	GET (NSP)

FOR i = 1 TO num+1
CUTEND
NEXT i


				! --- MAIN BOX --- !

ADDz legHeight
BLOCK boxLength, B, boxHeight
DEL 2

ADD -lowerBoxLength/2, -lowerBoxSize/2, legHeight/2-lowerBoxSize/2
BLOCK lowerBoxLength, lowerBoxSize, legHeight/2+lowerBoxSize/2
DEL 1

ADD boxLength/2, -sideBoxDepth/2, legHeight+0.10
BLOCK sideBoxThick, sideBoxDepth, sideBoxHeight
DEL 1


				! --- GRILL --- !

num = INT((boxLength-0.15*(boxLength/5.20))/(cylindDia*1.1))
IF num < 2 THEN num = 2
cylindDist2 = (boxLength-0.30*(boxLength/5.20)-cylindDia)/(num-1)

IF gs_detlevel_3D_m = 2 THEN
	FOR k = 1 TO num
		ADD -boxLength/2+0.15*(boxLength/5.20)+cylindDia/2+cylindDist2*(k-1), -cylindDia/2-cylindDist, legHeight+boxHeight
		EXTRUDE 2, 0, 0, cylindHeight, addStatus+16+32,
			0, 0, 901,
			cylindDia/2, 360, 4001
		ADDz cylindHeight
			IF operatedMode < EPS THEN GOSUB 500
		DEL 1

		ADDy cylindDia+cylindDist*2
		EXTRUDE 2, 0, 0, cylindHeight, addStatus+16+32,
			0, 0, 901,
			cylindDia/2, 360, 4001
		ADDz cylindHeight
			IF operatedMode < EPS GOSUB 500
		DEL 3
	NEXT k
ELSE
	FOR k = 1 TO num
		ADD -boxLength/2+0.15+cylindDia/2+(cylindDia*1.1)*(k-1), -cylindDia/2-cylindDist, legHeight+boxHeight
		EXTRUDE 2, 0, 0, cylindHeight, 1+2+16+32,
			0, 0, 901,
			cylindDia/2, 360, 4001

		ADDy cylindDia+cylindDist*2
		EXTRUDE 2, 0, 0, cylindHeight, 1+2+16+32,
			0, 0, 901,
			cylindDia/2, 360, 4001
		DEL 2
	NEXT k
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 12
END


500:
	 SegmetAng = 360/SegmentsNum
	 SegmetHalfAng = SegmetAng/2
	 TotalInsideRad = Rad-HalfThick/TAN(SegmetHalfAng)-Thick
	 DivisionInsideDist = (TotalInsideRad-Thick*(DivisionNum-1))/DivisionNum

	ROTz SegmetHalfAng

	PUT 0, 0, 901
	PUT Rad, 360, 4001

	alpha = 0
	FOR i = 1 TO SegmentsNum
		InsideDist = DivisionInsideDist
		CenterPointPosX = (HalfThick/SIN(SegmetHalfAng))*COS(alpha)
		CenterPointPosY = (HalfThick/SIN(SegmetHalfAng))*SIN(alpha)
		FOR j = 1 TO DivisionNum
			InsideDist = InsideDist+Thick*(j>1)
			PointOnArcEndPosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)-(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
			PointOnArcEnd2PosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)+(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
			PointOnArcEndPosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)+(InsideDist*SIN(SegmetHalfAng))*COS(alpha)
			PointOnArcEnd2PosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)-(InsideDist*SIN(SegmetHalfAng))*COS(alpha)

			IF j = 1 THEN
				PUT CenterPointPosX, CenterPointPosY, 1
				PUT PointOnArcEndPosX, PointOnArcEndPosY, 1
				PUT 0, 0, 901
				PUT PointOnArcEnd2PosX, PointOnArcEnd2PosY, 3001
				PUT CenterPointPosX, CenterPointPosY, -1
			ELSE
				InsideDist = InsideDist+DivisionInsideDist
				OPointOnArcEndPosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)-(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
				OPointOnArcEnd2PosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)+(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
				OPointOnArcEndPosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)+(InsideDist*SIN(SegmetHalfAng))*COS(alpha)
				OPointOnArcEnd2PosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)-(InsideDist*SIN(SegmetHalfAng))*COS(alpha)

				PUT PointOnArcEndPosX, PointOnArcEndPosY, 1
				PUT 0, 0, 901
				PUT PointOnArcEnd2PosX, PointOnArcEnd2PosY, 3001
				PUT OPointOnArcEnd2PosX, OPointOnArcEnd2PosY, 1
				PUT 0, 0, 901
				PUT OPointOnArcEndPosX, OPointOnArcEndPosY, 3001
				PUT PointOnArcEndPosX, PointOnArcEndPosY, -1
			ENDIF
		NEXT j
		 alpha = alpha+SegmetAng
	NEXT i


	POLY_ NSP/3,

		GET(NSP)

	DEL 1
RETURN
