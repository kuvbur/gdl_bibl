
! Gas Fired



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
HOTSPOT2 0, 0, unID: unID=unID+1

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top


				! --- HOTSPOTS --- !

	HOTSPOT2 -A/2, -B/2, unID: unID=unID+1
	HOTSPOT2 -A/2, B/2, unID: unID=unID+1
	HOTSPOT2 A/2, B/2, unID: unID=unID+1
	HOTSPOT2 A/2, -B/2, unID: unID=unID+1


				! --- MAIN BOX --- !

	PUT -A/2, -B/2, 1,
		-A/2, B/2, 1,
		A/2, B/2, 1,
		A/2, -B/2, 1,
		-A/2, -B/2, -1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)


				! --- GRILL --- !

	 num = INT((B-grillDia)/grillDistY)
	 dist = grillDistY*num

	FOR k=1 TO 1+num
		ADD2 A/2-grillDistX, -dist/2+grillDistY*(k-1)
			GOSUB 500
		DEL 1
	NEXT k
ENDIF


				! --- OTHER VIEWS --- !

ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 5,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 5
DEL 1


END


500:
	 SegmetAng = 360/SegmentsNum
	 SegmetHalfAng = SegmetAng/2
	 TotalInsideRad = Rad-HalfThick/TAN(SegmetHalfAng)-Thick
	 DivisionInsideDist = (TotalInsideRad-Thick*(DivisionNum-1))/DivisionNum

	ROT2 SegmetHalfAng

	PUT 0, 0, 901
	PUT Rad, 360, 4001

	alpha = 0
	FOR i = 1 TO SegmentsNum
		InsideDist = DivisionInsideDist
		CenterPointPosX = (HalfThick/SIN(SegmetHalfAng))*COS(alpha)
		CenterPointPosY = (HalfThick/SIN(SegmetHalfAng))*SIN(alpha)
		FOR j = 1 TO DivisionNum
			InsideDist = InsideDist+Thick*(j>1)
			PointOnArcEndPosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)-(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
			PointOnArcEnd2PosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)+(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
			PointOnArcEndPosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)+(InsideDist*SIN(SegmetHalfAng))*COS(alpha)
			PointOnArcEnd2PosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)-(InsideDist*SIN(SegmetHalfAng))*COS(alpha)

			IF j = 1 THEN
				PUT CenterPointPosX, CenterPointPosY, 1
				PUT PointOnArcEndPosX, PointOnArcEndPosY, 1
				PUT 0, 0, 901
				PUT PointOnArcEnd2PosX, PointOnArcEnd2PosY, 3001
				PUT CenterPointPosX, CenterPointPosY, -1
			ELSE
				InsideDist = InsideDist+DivisionInsideDist
				OPointOnArcEndPosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)-(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
				OPointOnArcEnd2PosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)+(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
				OPointOnArcEndPosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)+(InsideDist*SIN(SegmetHalfAng))*COS(alpha)
				OPointOnArcEnd2PosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)-(InsideDist*SIN(SegmetHalfAng))*COS(alpha)

				PUT PointOnArcEndPosX, PointOnArcEndPosY, 1
				PUT 0, 0, 901
				PUT PointOnArcEnd2PosX, PointOnArcEnd2PosY, 3001
				PUT OPointOnArcEnd2PosX, OPointOnArcEnd2PosY, 1
				PUT 0, 0, 901
				PUT OPointOnArcEndPosX, OPointOnArcEndPosY, 3001
				PUT PointOnArcEndPosX, PointOnArcEndPosY, -1
			ENDIF
		NEXT j
		 alpha = alpha+SegmetAng
	NEXT i


	POLY2_ NSP/3, 1,

		GET(NSP)

	DEL 1
RETURN
