
! Fire Detect and Alarm Equipment


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- FIRE DETECTION AND ALARM EQUIPMENT --- !

unID = 1
IF gs_detlevel_3D_m = 2 THEN	! Detailed
	IF objType = 1 THEN		! Alarm Pressure Switch
		CYLIND neckH, ConWidth_1
		ADD -A/2, -A/2, neckH

		HOTSPOT A, 0, 0, unID: unID=unID+1
		HOTSPOT A, B, 0, unID: unID=unID+1
		HOTSPOT 0, B, 0, unID: unID=unID+1
		HOTSPOT 0,0, 0, unID: unID=unID+1

		BLOCK A, B, bottomThk

		ADDz bottomThk
		hh = ZZYZX - neckH - bottomThk
		FPRISM_ MainBoxMaterial, MainBoxMaterial, MainBoxMaterial, MainBoxMaterial,
			4,hh, 90-alpha, hh,
			dx, dx, 15,
			dx, B-dx, 15,
			A-dx, B-dx, 15,
			A-dx, dx, 15
		LIN_ A-dx2, dx2, hh, A-dx2, B-dx2, hh
		DEL 2
	ENDIF

	IF objType = 2 THEN		! Water Flow Indicator
		ROTy 90
		CIRCLE ConWidth_1/2
		DEL 1

		ROTz 90
		ROTx 90
		ADDz -0.4*A

		hy1 = 0.20*ConWidth_1
		hy2 = 0.60*ConWidth_1
		hy22= 0.52*ConWidth_1
		hy3 = 0.80*ConWidth_1
		hx = SQR((ConWidth_1/2)^2-hy1^2)

		PRISM_ 13, 0.8*A,
			-B/2+hy1, hy1, 15,
			-B/2, hy22, 15,
			-B/2, hy2, 15,
			-ConWidth_1/2, hy2, 15,
			-ConWidth_1/3, hy3, 15,
			 ConWidth_1/3, hy3, 15,
			 ConWidth_1/2, hy2, 15,
			 B/2, hy2, 15,
			 B/2, hy22, 15,
			 B/2-hy1, hy1, 15,
			 hx,	  hy1, 79,
			0,0,979,
			-hx,	  hy1, 3015
		DEL 3

		TOLER 0.003
		RR = 0.08*ConWidth_1
		ROTx 180
		REVOLVE 2,180,1+2+16+32,
			0,0.55*ConWidth_1+RR, 901,
			RR,360,4001
		DEL 1
		ADDy 0.55*ConWidth_1+RR
		CYLIND hy1, RR
		DEL 1
		ADDy -0.55*ConWidth_1-RR
		CYLIND hy1, RR
		DEL 1

		ADDz hy3
		CYLIND 0.1*ZZYZX,0.25*A
		ADDz 0.1*ZZYZX
		ROTx 90
		ROTy 90
		ADDz -A/2

		RESOL 12
		R = ZZYZX/10
		PRISM_ 9,A,
			-meterW/2, R,	79,
			-meterW/2, 0.9*ZZYZX-hy3-R,	79,
			-meterW/2+R, 0.9*ZZYZX-hy3,	1079,
			 meterW/2-R, 0.9*ZZYZX-hy3,	79,
			 meterW/2, 0.9*ZZYZX-hy3-R,	1079,
			 meterW/2, R,	79,
			 meterW/2-R, 0,	1079,
			-meterW/2+R, 0,	79,
			-meterW/2, R,	1079
		DEL 5
	ENDIF

	IF objType = 3 THEN		! Water Motor Alarm
		tck = 0.1*A

		ADDz ZZYZX-B/2
		ROTy 90
		ADDz -A/2
		CYLIND tck, B/2
		DEL 2

		ROTy 90
		ADDz -A/2+tck
		ROTz 90
		HOTSPOT 0,B/2,0, unID: unID=unID+1
		PRISM_ 4,0.9*A,
			-0.47*B, -ZZYZX+B/2, 15,
			-0.47*B, 0, 79,
			 0.47*B, 0, 1079,
			 0.47*B, -ZZYZX+B/2, 15
		DEL 3
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple
	IF objType = 1 THEN		! Alarm Pressure Switch
		CYLIND neckH, ConWidth_1
		ADD -A/2, -A/2, neckH

		HOTSPOT A, 0, 0, unID: unID=unID+1
		HOTSPOT A, B, 0, unID: unID=unID+1
		HOTSPOT 0, B, 0, unID: unID=unID+1
		HOTSPOT 0,0, 0, unID: unID=unID+1

		hh = ZZYZX - neckH
		FPRISM_ MainBoxMaterial, MainBoxMaterial, MainBoxMaterial, MainBoxMaterial,
			4,hh, 90-alpha, hh,
			dx, dx, 15,
			dx, B-dx, 15,
			A-dx, B-dx, 15,
			A-dx, dx, 15

		dx2 = dx + hh * SIN(alpha)
		LIN_ A-dx2, dx2, hh, A-dx2, B-dx2, hh
		DEL 1
	ENDIF

	IF objType = 2 THEN		! Water Flow Indicator
		ROTy 90
		CIRCLE ConWidth_1/2
		DEL 1

		MATERIAL MainBoxMaterial

		ROTz 90
		ROTx 90
		ADDz -0.4*A

		hy1 = 0.20*ConWidth_1
		hy2 = 0.60*ConWidth_1
		hy22= 0.52*ConWidth_1
		hy3 = 0.80*ConWidth_1
		hx = SQR((ConWidth_1/2)^2-hy1^2)

		PRISM_ 11, 0.8*A,
			-B/2+hy1, hy1, 15,
			-B/2, hy2, 15,
			-ConWidth_1/2, hy2, 15,
			-ConWidth_1/3, hy3, 15,
			 ConWidth_1/3, hy3, 15,
			 ConWidth_1/2, hy2, 15,
			 B/2, hy2, 15,
			 B/2-hy1, hy1, 15,
			 hx,	  hy1, 79,
			0,0,979,
			-hx,	  hy1, 3015
		DEL 3

		RR = 0.08*ConWidth_1
		ROTx 180
		REVOLVE 2,180,1+2+16+32,
			0,0.55*ConWidth_1+RR, 901,
			RR,360,4001
		DEL 1
		ADDy 0.55*ConWidth_1+RR
		CYLIND hy1, RR
		DEL 1
		ADDy -0.55*ConWidth_1-RR
		CYLIND hy1, RR
		DEL 1

		RESOL 6
		ADDz hy3
		CYLIND 0.1*ZZYZX,0.25*A
		ADDz 0.1*ZZYZX
		ROTx 90
		ROTy 90
		ADDz -A/2
		PRISM 4,A,
			-meterW/2, 0,
			-meterW/2, 0.9*ZZYZX-hy3,
			 meterW/2, 0.9*ZZYZX-hy3,
			 meterW/2, 0
		DEL 5
	ENDIF

	IF objType = 3 THEN		! Water Motor Alarm
		tck = 0.1*A

		ADDz ZZYZX-B/2
		ROTy 90
		ADDz -A/2
		CYLIND tck, B/2
		DEL 2

		ROTy 90
		ADDz -A/2+tck
		ROTz 90
		HOTSPOT 0,B/2,0, unID: unID=unID+1
		PRISM_ 4,0.9*A,
			-0.47*B, -ZZYZX+B/2, 15,
			-0.47*B, 0, 79,
			 0.47*B, 0, 1079,
			 0.47*B, -ZZYZX+B/2, 15
		DEL 3
	ENDIF
ENDIF

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1
END
