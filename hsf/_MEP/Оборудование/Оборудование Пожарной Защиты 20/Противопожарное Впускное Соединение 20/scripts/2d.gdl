
! Fire Dept Inlet Connection



PEN gs_cont_pen
SET FILL gs_fill_type


unID = 1
DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

				! --- FIRE DEPARTMENT INLET CONNECTION --- !

	IF GLOB_CONTEXT <> 22 THEN

		IF objType = 1 THEN		! 2-Way Flush FD Connection Outlet Body
			bR = ZZYZX/2
			cx = 1.05 * bR
			RR = B/2-bR

			dx=SQR((RR+bR)^2 - bR^2)

			HOTSPOT2 0,0, unID: unID=unID+1
			HOTSPOT2 cx,0, unID: unID=unID+1

			POLY2_B 13, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, bR, 1,
				cx+RR-dx, bR, 1,
				cx+RR,0, 901,
				cx+RR,RR+bR,3001,
				A,RR+bR, 1,
				A,RR-bR, 1,
				cx+RR,RR-bR, 1,
				cx+RR,-RR+bR, 1001,
				A,-RR+bR, 1,
				A,-RR-bR, 1,
				cx+RR,-RR-bR,1,
				cx+RR-dx, -bR, 1001,
				0, -bR, 1
			LINE2 cx,0, cx+RR-dx, bR
			LINE2 cx,0, cx+RR-dx,-bR
		ENDIF

		IF objType = 2 THEN		! 2-Way Flush FD Inlet Connection Plate
			thk=MAX(0.005, 0.05*A)
			POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,	-B/2, 1,
				thk,-B/2, 1,
				thk, B/2, 1,
				0,	 B/2, 1
			HOTSPOT2 0, 0, unID: unID=unID+1
			HOTSPOT2 0, B/2, unID: unID=unID+1
			HOTSPOT2 0,-B/2, unID: unID=unID+1

			ADD2 thk,ConPosY_1
			POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,			-ConWidth_1/2, 1,
				A-thk,-ConWidth_1/2, 1,
				A-thk, ConWidth_1/2, 1,
				0,			 ConWidth_1/2, 1
			HOTSPOT2 A-thk,0, unID: unID=unID+1
			DEL 1

			ADD2 thk,ConPosY_2
			POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,			-ConWidth_2/2, 1,
				A-thk,-ConWidth_2/2, 1,
				A-thk, ConWidth_2/2, 1,
				0,			 ConWidth_2/2, 1
			HOTSPOT2 A-thk,0, unID: unID=unID+1
			DEL 1
		ENDIF

		IF objType = 3 THEN		! 3-Way Flush FD Connection Outlet Body
			bR = ZZYZX/2
			cx = 1.05 * bR
			RR = B/2-2*bR

			HOTSPOT2 0,0, unID: unID=unID+1
			HOTSPOT2 cx,0, unID: unID=unID+1

			POLY2_B 19, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, bR, 1,
				cx-bR, bR, 1,
				0, 1, 801,
				cx+RR,B/2,1001,
				A,B/2, 1,
				A,B/2-2*bR, 1,
				cx+RR,B/2-2*bR, 1,
				cx+bR,bR, 1001,
				A,bR, 1,
				A,-bR, 1,
				cx+bR,-bR, 1,
				0, -1, 801,
				cx+RR,-B/2+2*bR, 1001,
				A,-B/2+2*bR, 1,
				A,-B/2, 1,
				cx+RR,-B/2,1,
				cx-bR,-bR,1001,
				0, -bR, 1,
				0, bR, -1
			LINE2 cx-bR,-bR, cx+bR, bR
			LINE2 cx-bR, bR, cx+bR,-bR
		ENDIF

		IF objType = 4 THEN		! 3-Way Flush FD Inlet Connection Plate
			thk=MAX(0.005, 0.05*A)
			POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,	-B/2, 1,
				thk,-B/2, 1,
				thk, B/2, 1,
				0,	 B/2, 1
			HOTSPOT2 0, 0, unID: unID=unID+1
			HOTSPOT2 0, B/2, unID: unID=unID+1
			HOTSPOT2 0,-B/2, unID: unID=unID+1

			ADD2 thk,ConPosY_1
			POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,			-ConWidth_1/2, 1,
				A-thk,-ConWidth_1/2, 1,
				A-thk, ConWidth_1/2, 1,
				0,			 ConWidth_1/2, 1
			HOTSPOT2 A-thk,0, unID: unID=unID+1
			DEL 1

			ADD2 thk,ConPosY_2
			POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,			-ConWidth_2/2, 1,
				A-thk,-ConWidth_2/2, 1,
				A-thk, ConWidth_2/2, 1,
				0,			 ConWidth_2/2, 1
			HOTSPOT2 A-thk,0, unID: unID=unID+1
			DEL 1

			ADD2 thk,ConPosY_3
			POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,			-ConWidth_3/2, 1,
				A-thk,-ConWidth_3/2, 1,
				A-thk, ConWidth_3/2, 1,
				0,			 ConWidth_3/2, 1
			HOTSPOT2 A-thk,0, unID: unID=unID+1
			DEL 1
		ENDIF

		IF objType = 5 OR objType = 7 THEN		! 2-Way Projecting FD Inlet Connection
			IF objType = 5 THEN
				fthk = MIN(0.15*A, ConWidth_1/2)
				cx = fthk/3+ConWidth_1/2
			ELSE
				fthk = 0.05*A
				cx = fthk+ConWidth_1/2
			ENDIF

			cR =1.1 * ConWidth_1/2
			cR2=1.2 * ConWidth_1/2
			pathL = A/SQR(2)

			POLY2_B 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,		B/2, 1,
				fthk,	B/2, 0,
				fthk,	cR, 1,
				cx-cR*TAN(22.5), cR, 1,
				cx, 0, 1,
				cx-cR*TAN(22.5), -cR, 1,
				fthk,	-cR, 0,
				fthk,	-B/2, 1,
				0,		-B/2, 1
			LINE2 fthk,	B/2, fthk,-B/2
			HOTSPOT2 cx,0, unID: unID=unID+1

			ADD2 cx,0
			FOR i=1 TO 2
				ROT2 45
				POLY2_B 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
					0,0, 1,
					cR*TAN(22.5), cR, 1,
					pathL-0.1*A, cR, 0,
					pathL-0.1*A, cR2, 1,
					pathL,  cR2, 1,
					pathL, -cR2, 1,
					pathL-0.1*A, -cR2, 0,
					pathL-0.1*A, -cR, 1,
					cR*TAN(45), -1.1 * ConWidth_1/2, 1
				HOTSPOT2 pathL,0, unID: unID=unID+1
				LINE2 pathL-0.1*A, cR2, pathL-0.1*A, -1.2 * ConWidth_1/2
				DEL 1
				MUL2 1,-1
			NEXT i
			DEL 3
		ENDIF

		IF objType = 6 OR objType = 8 THEN		! 3-Way Projecting FD Inlet Connection
			IF objType = 6 THEN
				fthk = MIN(0.15*A, ConWidth_1/2)
				cx = fthk/3+ConWidth_1/2
			ELSE
				fthk = 0.05*A
				cx = fthk+ConWidth_1/2
			ENDIF

			cR =1.1 * ConWidth_1/2
			cR2=1.2 * ConWidth_1/2
			pathL = A/SQR(2)

			POLY2_B 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,		B/2, 1,
				fthk,	B/2, 0,
				fthk,	cR, 1,
				cx-cR*TAN(22.5), cR, 1,
				cx, 0, 1,
				cx-cR*TAN(22.5), -cR, 1,
				fthk,	-cR, 0,
				fthk,	-B/2, 1,
				0,		-B/2, 1
			LINE2 fthk,	B/2, fthk,-B/2
			HOTSPOT2 cx,0, unID: unID=unID+1

			POLY2_B 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
				cx,0, 1,
				cx+cR*TAN(90-22.5), cR, 1,
				0.9*A, cR, 0,
				0.9*A, cR2, 1,
				A, cR2, 1,
				A, -cR2, 1,
				0.9*A, -cR2, 0,
				0.9*A, -cR, 1,
				cx+cR*TAN(90-22.5), -cR, 1
			LINE2 0.9*A, cR2, 0.9*A, -cR2
			HOTSPOT2 A,0, unID: unID=unID+1

			ADD2 cx,0
			FOR i=1 TO 2
				ROT2 45
				POLY2_B 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
					0,0, 1,
					cR*TAN(22.5), cR, 1,
					pathL-0.1*A, cR, 0,
					pathL-0.1*A, cR2, 1,
					pathL,  1.2 * ConWidth_1/2, 1,
					pathL, -1.2 * ConWidth_1/2, 1,
					pathL-0.1*A, -cR2, 0,
					pathL-0.1*A, -cR, 1,
					cR*TAN(90-22.5), -cR, 1
				HOTSPOT2 pathL,0, unID: unID=unID+1
				LINE2 pathL-0.1*A, cR2, pathL-0.1*A, -1.2 * ConWidth_1/2
				DEL 1
				MUL2 1,-1
			NEXT i
			DEL 3
		ENDIF

		IF objType = 9 THEN		! Single Flush FD Inlet Connection
			HOTSPOT2 0,0, unID: unID=unID+1
			HOTSPOT2 A,0, unID: unID=unID+1

			POLY2_B 12, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, B/2, 1,
				0.1*A, B/2, 0,
				0.1*A, 1.2 * ConWidth_1/2, 1,
				0.7*A, 1.2 * ConWidth_1/2, 0,
				0.7*A, 1.5 * ConWidth_1/2, 1,
				A, 1.5 * ConWidth_1/2, 1,

				A, -1.5 * ConWidth_1/2, 1,
				0.7*A, -1.5 * ConWidth_1/2, 0,
				0.7*A, -1.2 * ConWidth_1/2, 1,
				0.1*A, -1.2 * ConWidth_1/2, 0,
				0.1*A, -B/2, 1,
				0, -B/2, 1

			LINE2 0.1*A, B/2, 0.1*A, -B/2
			LINE2 0.7*A, 1.5 * ConWidth_1/2, 0.7*A, -1.5 * ConWidth_1/2
		ENDIF
	ENDIF
ENDIF
DEL TOP


				! --- OTHER VIEWS --- !

ROT2 (SYMB_MIRRORED - NOT(SYMB_MIRRORED)) * (DrawingNum > 1) * SYMB_ROTANGLE

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180 * SYMB_MIRRORED,		 2	! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180 * NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = ConEdit_3,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConLength_4=ConLength_4, ConEdit_4 = ConEdit_4
DEL 1

END
