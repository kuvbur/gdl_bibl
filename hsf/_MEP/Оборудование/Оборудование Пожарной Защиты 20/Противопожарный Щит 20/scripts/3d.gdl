
! Hose Cabinet


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
IF useSysMat THEN glassMaterial = sMat
IF useSysMat THEN hoseMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


unID = 1

				! --- FIRE HOUSE CABINET --- !

IF gs_detlevel_3D_m = 2 THEN	! Detailed

	IF hoseType = 1 THEN

		ADDy B/2 + ConPosY_1

		tck = 0.005
		doorTck = 0.01
		cabinD = B
		ROTx 90
		GOSUB 100
		GOSUB 300

		dfw = 0.06		! Door Frame Width
		dov = 0			! Door Overhang

		ADDz B
		GOSUB 200
		DEL 2

		DEL 1
	ENDIF

	IF hoseType = 2 THEN

		ADDy B/2 + ConPosY_1

		tck = 0.005
		doorTck = 0.01
		cabinD = B
		ROTx 90
		GOSUB 100
		GOSUB 400

		dfw = 0.06		! Door Frame Width
		dov = 0			! Door Overhang

		ADDz B
		GOSUB 200
		DEL 2

		DEL 1
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple

	IF hoseType = 1 THEN
		ADDy B

		tck = 0.005
		doorTck = 0.01
		cabinD = B
		ROTx 90
		GOSUB 110
		GOSUB 310

		dfw = 0.06		! Door Frame Width
		dov = 0			! Door Overhang

		ADDz B
		GOSUB 210
		DEL 2

		DEL 1
	ENDIF

	IF hoseType = 2 THEN

		ADDy B/2 + ConPosY_1

		tck = 0.005
		doorTck = 0.01
		cabinD = B
		ROTx 90
		GOSUB 110
		GOSUB 410

		dfw = 0.06		! Door Frame Width
		dov = 0			! Door Overhang

		ADDz B
		GOSUB 210
		DEL 2

		DEL 1
	ENDIF
ENDIF
DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1 = ConLength_1, ConEdit_1 = 14
END


100:
	MATERIAL MainBoxMaterial
	PRISM_ 10, cabinD,
		-A/2,	0,			15,
		-A/2,	ZZYZX,	15,
		 A/2,	ZZYZX,	15,
		 A/2,	0,			15,
		-A/2,	0,			-1,
		-A/2+tck,	tck,			15,
		-A/2+tck,	ZZYZX-tck,	15,
		 A/2-tck,	ZZYZX-tck,	15,
		 A/2-tck,	tck,			15,
		-A/2+tck,	tck,			-1

	PRISM_ 4, tck,
		-A/2+tck,	tck,			15,
		-A/2+tck,	ZZYZX-tck,	15,
		 A/2-tck,	ZZYZX-tck,	15,
		 A/2-tck,	tck,			15

	HOTSPOT -A/2, 0, 0, unID: unID=unID+1
	HOTSPOT  A/2, 0, 0, unID: unID=unID+1
	HOTSPOT -A/2, ZZYZX, 0, unID: unID=unID+1
	HOTSPOT  A/2, ZZYZX, 0, unID: unID=unID+1

	HOTSPOT -A/2, 0, B, unID: unID=unID+1
	HOTSPOT  A/2, 0, B, unID: unID=unID+1
	HOTSPOT -A/2, ZZYZX, B, unID: unID=unID+1
	HOTSPOT  A/2, ZZYZX, B, unID: unID=unID+1
RETURN


110:
	MATERIAL MainBoxMaterial
	EXTRUDE 4,0,0,B, 1+4+16+32,
		-A/2,	0,			0,
		-A/2,	ZZYZX,	0,
		 A/2,	ZZYZX,	0,
		 A/2,	0,			0

	HOTSPOT -A/2, 0, 0, unID: unID=unID+1
	HOTSPOT  A/2, 0, 0, unID: unID=unID+1
	HOTSPOT -A/2, ZZYZX, 0, unID: unID=unID+1
	HOTSPOT  A/2, ZZYZX, 0, unID: unID=unID+1

	HOTSPOT -A/2, 0, B, unID: unID=unID+1
	HOTSPOT  A/2, 0, B, unID: unID=unID+1
	HOTSPOT -A/2, ZZYZX, B, unID: unID=unID+1
	HOTSPOT  A/2, ZZYZX, B, unID: unID=unID+1
RETURN


200:
	MATERIAL MainBoxMaterial
	PRISM_ 10, doorTck,
		-A/2-dov,	-dov,			15,
		-A/2-dov,	 ZZYZX+dov,	15,
		 A/2+dov,	 ZZYZX+dov,	15,
		 A/2+dov,	-dov,			15,
		-A/2-dov,	-dov,			-1,
		-A/2-dov+dfw,	-dov+dfw,			15,
		-A/2-dov+dfw,	 ZZYZX+dov-dfw,	15,
		 A/2+dov-dfw,	 ZZYZX+dov-dfw,	15,
		 A/2+dov-dfw,	-dov+dfw,			15,
		-A/2-dov+dfw,	-dov+dfw,			-1

	MATERIAL glassMaterial
	POLY_ 4,
		-A/2-dov+dfw,	-dov+dfw,			1,
		-A/2-dov+dfw,	 ZZYZX+dov-dfw,	1,
		 A/2+dov-dfw,	 ZZYZX+dov-dfw,	1,
		 A/2+dov-dfw,	-dov+dfw,			1
RETURN

210:
	MATERIAL MainBoxMaterial
	ADDz doorTck
	POLY_ 10,
		-A/2-dov,	-dov,			1,
		-A/2-dov,	 ZZYZX+dov,	1,
		 A/2+dov,	 ZZYZX+dov,	1,
		 A/2+dov,	-dov,			1,
		-A/2-dov,	-dov,			-1,
		-A/2-dov+dfw,	-dov+dfw,			1,
		-A/2-dov+dfw,	 ZZYZX+dov-dfw,	1,
		 A/2+dov-dfw,	 ZZYZX+dov-dfw,	1,
		 A/2+dov-dfw,	-dov+dfw,			1,
		-A/2-dov+dfw,	-dov+dfw,			-1

	MATERIAL glassMaterial
	POLY_ 4,
		-A/2-dov+dfw,	-dov+dfw,			1,
		-A/2-dov+dfw,	 ZZYZX+dov-dfw,	1,
		 A/2+dov-dfw,	 ZZYZX+dov-dfw,	1,
		 A/2+dov-dfw,	-dov+dfw,			1
	DEL 1
RETURN


300:
	MATERIAL MainBoxMaterial
	RESOL 8

	ADD -A/2+tck+1.5*ConWidth_1,ConPosZ_1,B/2
	ROTz -90
	ROTx 90
	ARMC 1.2*ConWidth_1/2, ConWidth_1/2, 1.5*ConWidth_1, 0,0,90
	DEL 1

	ADDy 0.05
	ROTx 90
	CYLIND 1.5*ConWidth_1+0.05, 1.2*ConWidth_1/2
	ADDz -0.02

	RESOL 6
	CYLIND 0.02, 0.2*ConWidth_1
	ADDz -0.01

	RESOL 8
	CYLIND 0.01, ConWidth_1
	DEL 6

	tx = 0.03
	tnum = MAX(1, INT((A-2*ConWidth_1) / (2 * tx)))

	dx = tnum*tx*2-tx
	dy = 0.7*ConWidth_1

	ADD -A/2+tck+1.5*ConWidth_1,ConPosZ_1-1.5*ConWidth_1,B/2
	ROTx 90
	PRISM 4, 0.01,
		-ConWidth_1/2, -dy,
		-ConWidth_1/2,  dy,
		dx, dy,
		dx,-dy
	ADDx dx
	MULz -1
	RESOL 8
	MATERIAL hoseMaterial
	CONE 2*ConWidth_1, ConWidth_1/2, 0.3*ConWidth_1, 90, 90
	DEL 3

	px=0
	ADD 0, -0.01, -0.05
	FOR i=1 TO tnum
		PUT px,		 0, 0,
			px,		-ConPosZ_1+tx+3*ConWidth_1, 1,
			px+tx,	-ConPosZ_1+tx+3*ConWidth_1, 1001,
			px+tx,	 0, 0
		px=px+2*tx
	NEXT i

	RESOL 4
	EXTRUDE NSP/3, 0,0, 0.1, 16+32, GET(NSP)

	DEL 2
RETURN

310:
	MATERIAL MainBoxMaterial
	RESOL 6

	ADD -A/2+tck+1.5*ConWidth_1,ConPosZ_1,B/2
	ROTz -90
	ROTx 90
	ARMC 1.2*ConWidth_1/2, ConWidth_1/2, 1.5*ConWidth_1, 0,0,90
	DEL 1

	ADDy 0.05
	ROTx 90
	CYLIND 1.5*ConWidth_1+0.05, 1.2*ConWidth_1/2
	ADDz -0.02

	ADDz -0.01
	RESOL 8
	CYLIND 0.01, ConWidth_1
	DEL 6

	tx = 0.03
	tnum = INT((A-2*ConWidth_1) / (2 * tx))

	dx = tnum*tx*2-tx
	dy = 0.7*ConWidth_1

	ADD -A/2+tck+1.5*ConWidth_1,ConPosZ_1-1.5*ConWidth_1,B/2
	ROTx 90
	MATERIAL hoseMaterial
	PRISM 4, ConPosZ_1-3*ConWidth_1,
		0, -dy,
		0,  dy,
		dx, dy,
		dx,-dy
	ADDx dx
	MULz -1
	RESOL 8
	CONE 2*ConWidth_1, ConWidth_1/2, 0.3*ConWidth_1, 90, 90
	DEL 4
RETURN



400:
	MATERIAL MainBoxMaterial
	ADD -A/2+tck+1.5*ConWidth_1,ConPosZ_1,B/2
	ROTy 180
	ARMC 1.2*ConWidth_1/2, ConWidth_1/2, 1.5*ConWidth_1, 0,0,90
	DEL 1
	ARMC 1.2*ConWidth_1/2, ConWidth_1/2, 1.5*ConWidth_1, 0,0,90

	ROTx 90
	ADDy 0.05
	ROTx 90
	CYLIND 1.5*ConWidth_1+0.05, 1.2*ConWidth_1/2
	ADDz -0.02

	RESOL 6
	CYLIND 0.02, 0.2*ConWidth_1
	ADDz -0.01

	RESOL 8
	CYLIND 0.01, ConWidth_1
	DEL 6

	ADD 0, ZZYZX/2, B/2-0.05

	RESOL res
	CYLIND 0.12, 0.05*A

	MATERIAL hoseMaterial
	PRISM_ 3,0.1,
		0,0,979,
		0.8*MIN(ZZYZX,A)/2, 360,4079,
		0.05*A, 360,4079
	PRISM 4,0.1,
		0,-0.8*MIN(ZZYZX,A)/2,
		-A/2+tck+3*ConWidth_1,-ZZYZX/2+ConPosZ_1,
		-A/2+tck+3*ConWidth_1,-ZZYZX/2+ConPosZ_1+0.001,
		0,-0.8*MIN(ZZYZX,A)/2+0.001

	DEL 1
RETURN

410:
	MATERIAL MainBoxMaterial
	ADD -A/2+tck+1.5*ConWidth_1,ConPosZ_1,B/2
	ROTy 180
	ARMC 1.2*ConWidth_1/2, ConWidth_1/2, 1.5*ConWidth_1, 0,0,90
	DEL 1
	ARMC 1.2*ConWidth_1/2, ConWidth_1/2, 1.5*ConWidth_1, 0,0,90

	ROTx 90
	ADDy 0.05
	ROTx 90
	CYLIND 1.5*ConWidth_1+0.05, 1.2*ConWidth_1/2
	ADDz -0.03
	RESOL 6
	CYLIND 0.01, ConWidth_1
	DEL 5

	MATERIAL hoseMaterial
	RESOL 12
	ADD 0, ZZYZX/2, B/2-0.05
	CYLIND 0.1, 0.8*MIN(ZZYZX,A)/2

	PRISM 4,0.1,
		0,-0.8*MIN(ZZYZX,A)/2,
		-A/2+tck+3*ConWidth_1,-ZZYZX/2+ConPosZ_1,
		-A/2+tck+3*ConWidth_1,-ZZYZX/2+ConPosZ_1+0.001,
		0,-0.8*MIN(ZZYZX,A)/2+0.001

	DEL 1
RETURN

