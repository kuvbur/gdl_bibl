

CALL "MEP_m_RequestPlanViewGlobals" PARAMETERS ALL returned_parameters gs_symbtype_2D_m

call "FM_types_MEP" parameters
DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 0		! Rectangle
	shape_typ_to_validation[2] = 1		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0

IF useSysMat AND MEP_StrConnectionData[1][2] <> "0" THEN LOCK "MainBoxMaterial"
IF MEP_StrConnectionData[1][2] = "0" THEN LOCK "useSysMat"

IF sprinklerType = 3 OR sprinklerType = 5 OR sprinklerType = 7 THEN HIDEPARAMETER "gs_symbtype_2D"


					! --- MAIN PARAMETERS --- !

IF NOT(SetProgram) THEN
	VALUES "A" RANGE [ConWidth_1,]
	VALUES "B" RANGE [ConWidth_1,]
	VALUES "ZZYZX" RANGE [ConWidth_1,]
ENDIF


					! --- OBJECT SPECIFIC PARAMETERS --- !

VALUES "sprinklerType" 1, 2, 3, 4, 5, 6, 7
VALUES "sprinklerTypeStr" type_dim


					! --- CONNECTIONS --- !

PARAMETERS MEP_NumberConnections = 1


					! --- WATER CONNECTION --- !

ConID = 1
ConName = `Подключение Воды`

IF (sprinklerType = 3 OR sprinklerType = 5) THEN
	IF GLOB_MODPAR_NAME = "B" THEN
		ZZYZX = B
		PARAMETERS ZZYZX = ZZYZX
	ENDIF
	IF ABS(ZZYZX - B) > EPS THEN
		B = ZZYZX
		PARAMETERS B = B
	ENDIF

	ConDVecX = 1
	ConDVecY = 0
	ConDVecZ = 0

	ConWVecX = 0
	ConWVecY = -1
	ConWVecZ = 0
ENDIF

IF (sprinklerType = 1 OR sprinklerType = 2 OR sprinklerType = 4 OR sprinklerType = 6) THEN
	IF GLOB_MODPAR_NAME = "B" THEN
		A = B
		PARAMETERS A = A
	ENDIF
	IF ABS(A - B) > EPS THEN
		B = A
		PARAMETERS B = B
	ENDIF

	ConDVecX = 0
	ConDVecY = 0
	ConDVecZ = 1

	ConWVecX = 1
	ConWVecY = 0
	ConWVecZ = 0
ENDIF

IF (sprinklerType = 7) THEN
	IF GLOB_MODPAR_NAME = "B" THEN
		A = B
		PARAMETERS A = A
	ENDIF
	IF ABS(A - B) > EPS THEN
		B = A
		PARAMETERS B = B
	ENDIF

	ConDVecX = 0
	ConDVecY = 0
	ConDVecZ = -1

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0
ENDIF


IF NOT(SetProgram) THEN
	VALUES "ConLength_1" RANGE [0,]
	VALUES "ConWidth_1" RANGE (0, A)
	PARAMETERS ConDepth_1 = ConWidth_1

	PARAMETERS ConPosX_1 = 0
	PARAMETERS ConPosY_1 = 0
	PARAMETERS ConPosZ_1 = 0
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_1,
	ConPosY = ConPosY_1,
	ConPosZ = ConPosZ_1,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_1,
	ConNominalDepth = ConNominalDepth_1,
	ConWidth = ConWidth_1,
	ConDepth = ConDepth_1,
	ConLength = ConLength_1,
	ConWallThickness = ConWallThickness_1,
	ConConnectorWidth = ConConnectorWidth_1,
	ConConnectorDepth = ConConnectorDepth_1,
	ConConnectorWidth2 = ConConnectorWidth2_1,
	ConToolType = ConToolType_1,
	ConStatus = ConStatus_1,
	ConType_1 = ConType_1,
	ConConnectorType_1 = ConConnectorType_1,
	ConSystem_1 = ConSystem_1,
	ui_page_idx = ui_page_idx,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF setProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF

FOR k = 1 TO MEP_NumberConnections
	FOR i = 1 TO 9
		j = 1+INT((i-0.5)/3)
		MEP_NumConnectionData[k][i] = outCoords[j+3*(k-1)][i-3*(j-1)]
		PARAMETERS MEP_NumConnectionData[k][i] = MEP_NumConnectionData[k][i]
	NEXT i
NEXT k
