!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el



IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


				! --- DRY SPRINKLER --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN	! Detailed
	RESOL res*2/3
	IF sprinklerType = 1 THEN	! Horizontal Sidewall Dry Sprinkler
		dTWidth = 0.10
		dTDepth = 0.08
		dTHeight = 0.08

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT -dTWidth, 0, 0, unID: unID=unID+1

		REVOLVE 4, 360, 1+2+16+32,
			 0, dTDepth/2, 2,
			-thk, dTDepth/2, 2,
			-thk, diam/2, 2,
			-0.25 * dTWidth,diam/2,  2

		ADD -dTWidth+0.25 * dTWidth, 0, -thk/2
		PRISM_ 4,thk,
			0, 0.25 * dTWidth, 79,
			0,-0.25 * dTWidth, 79,
			-1, 0, 879,
			0, 0.25 * dTWidth, 1079
		DEL 1

		RESOL 6
		ADDx -0.25 * dTWidth
		ROTy 180
		forkW = diam
		forkL = 0.5 * dTWidth
		GOSUB 100
		DEL 3
	ENDIF

	IF sprinklerType = 2 THEN	! Pendent Dry Sprinkler
		dTWidth = 0.06
		dTDepth = 0.06
		dTHeight = 0.06

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT 0, 0, -dTHeight, unID: unID=unID+1

		ADDz -0.15 * dTHeight
		CYLIND 0.15 * dTHeight, diam/2

		RESOL 6
		ROTy 90
		forkW = diam
		forkL = 0.848 * dTHeight
		GOSUB 100
		DEL 2

		TOLER 0.001
		ADDz -dTHeight
		ROTy -90
		REVOLVE 3,360, 1+16+32,
			0, 0.005, 1,
			0, 1, 801,
			dTWidth/6, dTWidth/2, 1001
		DEL 3
	ENDIF

	IF sprinklerType = 3 THEN	! Recessed Horizontal Sidewall Dry Sprinkler
		dTWidth = 0.075
		dTDepth = 0.08
		dTHeight = 0.08

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT -dTWidth, 0, 0, unID: unID=unID+1

		REVOLVE 5, 360, 2+16+32,
			-0.25 * dTWidth,diam/2,  2,
			-0.25 * dTWidth,dTDepth/2,  2,
			-0.25 * dTWidth+thk,dTDepth/2, 2,
			-0.25 * dTWidth+thk,diam/2, 2,
			 0, diam/2, 2

		ADD -dTWidth+0.25 * dTWidth, 0, -thk/2
		PRISM_ 4,thk,
			0, 0.25 * dTWidth, 79,
			0,-0.25 * dTWidth, 79,
			-1, 0, 879,
			0, 0.25 * dTWidth, 1079
		DEL 1

		RESOL 6
		ROTy 180
		forkW = diam
		forkL = 0.75 * dTWidth
		GOSUB 100
		DEL 2
	ENDIF

	IF sprinklerType = 4 THEN	! Recessed Pendent Dry Sprinkler
		dTWidth = 0.08
		dTDepth = 0.08
		dTHeight = 0.05

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT 0, 0, -dTHeight, unID: unID=unID+1

		ROTy -90
		REVOLVE 5, 360, 2+16+32,
			-0.25 * dTWidth,diam/2,  2,
			-0.25 * dTWidth,dTDepth/2,  2,
			-0.25 * dTWidth+thk,dTDepth/2, 2,
			-0.25 * dTWidth+thk,diam/2, 2,
			 0, diam/2, 2
		DEL 1

		ADDz 0!-0.15 * dTHeight

		RESOL 6
		ROTy 90
		forkW = 0.8*diam
		forkL = 0.98 * dTHeight
		GOSUB 100
		DEL 2

		TOLER 0.001
		ADDz -dTHeight
		ROTy -90
		REVOLVE 3,360, 1+16+32,
			0, 0.005, 1,
			0, 1, 801,
			dTWidth/9, dTWidth/3, 1001
		DEL 3
	ENDIF

	IF sprinklerType = 5 THEN	! Upright Dry Sprinkler
		dTWidth = 0.06
		dTDepth = 0.06
		dTHeight = 0.06

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT 0, 0, dTHeight, unID: unID=unID+1

		ROTx 180
		ADDz -0.15 * dTHeight
		CYLIND 0.15 * dTHeight, diam/2

		RESOL 6
		ROTy 90
		forkW = diam
		forkL = 0.848 * dTHeight
		GOSUB 100
		DEL 2

		TOLER 0.001
		ADDz -dTHeight
		ROTy -90
		REVOLVE 3,360, 1+16+32,
			0, 0.005, 1,
			0, 1, 801,
			dTWidth/6, dTWidth/2, 1001
		DEL 4
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple
	IF sprinklerType = 1 THEN	! Horizontal Sidewall Dry Sprinkler
		dTWidth = 0.10
		dTDepth = 0.08
		dTHeight = 0.08

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT -dTWidth, 0, 0, unID: unID=unID+1

		REVOLVE 3, 360, 1+2+16+32,
			 0, dTDepth/2, 2,
			 0, diam/2, 2,
			-0.25 * dTWidth,diam/2,  2

		ADDx -dTWidth+0.25 * dTWidth
		POLY_ 4,
			0, 0.25 * dTWidth, 1,
			0,-0.25 * dTWidth, 1,
			-1, 0, 879,
			0, 0.25 * dTWidth, 1001
		DEL 1

		RESOL 4
		ADDx -0.25 * dTWidth
		ROTy 180
		forkW = diam
		forkL = 0.5 * dTWidth
		GOSUB 110
		DEL 3
	ENDIF

	IF sprinklerType = 2 THEN	! Pendent Dry Sprinkler
		dTWidth = 0.06
		dTDepth = 0.06
		dTHeight = 0.06

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT 0, 0, -dTHeight, unID: unID=unID+1

		ADDz -0.15 * dTHeight
		CYLIND 0.15 * dTHeight, diam/2

		RESOL 4
		ROTy 90
		forkW = diam
		forkL = 0.848 * dTHeight
		GOSUB 110
		DEL 2

		RESOL 8
		ADDz -dTHeight
		ROTy -90
		REVOLVE 3,360, 1+16+32,
			0, 0.01, 1,
			0, 1, 801,
			dTWidth/6, dTWidth/2, 1001
		DEL 3
	ENDIF

	IF sprinklerType = 3 THEN	! Recessed Horizontal Sidewall Dry Sprinkler
		dTWidth = 0.075
		dTDepth = 0.08
		dTHeight = 0.08

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT -dTWidth, 0, 0, unID: unID=unID+1

		REVOLVE 3, 360, 2+16+32,
			-0.25 * dTWidth,dTDepth/2, 2,
			-0.25 * dTWidth,diam/2, 2,
			 0, diam/2, 2

		ADDx -dTWidth+0.25 * dTWidth
		POLY_ 4,
			0, 0.25 * dTWidth, 1,
			0,-0.25 * dTWidth, 1,
			-1, 0, 879,
			0, 0.25 * dTWidth, 1001
		DEL 1

		RESOL 4
		ROTy 180
		forkW = diam
		forkL = 0.75 * dTWidth
		GOSUB 110
		DEL 2
	ENDIF

	IF sprinklerType = 4 THEN	! Recessed Pendent Dry Sprinkler
		dTWidth = 0.08
		dTDepth = 0.08
		dTHeight = 0.05

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT 0, 0, -dTHeight, unID: unID=unID+1

		ROTy -90
		REVOLVE 3, 360, 2+16+32,
			-0.25 * dTWidth,dTDepth/2,  2,
			-0.25 * dTWidth,diam/2, 2,
			 0, diam/2, 2
		DEL 1

		ADDz 0!-0.15 * dTHeight

		RESOL 4
		ROTy 90
		forkW = 0.8*diam
		forkL = 0.98 * dTHeight
		GOSUB 110
		DEL 2

		RESOL 8
		ADDz -dTHeight
		ROTy -90
		REVOLVE 3,360, 1+16+32,
			0, 0.01, 1,
			0, 1, 801,
			dTWidth/9, dTWidth/3, 1001
		DEL 3
	ENDIF

	IF sprinklerType = 5 THEN	! Upright Dry Sprinkler
		dTWidth = 0.06
		dTDepth = 0.06
		dTHeight = 0.06

		MUL A/dTWidth, B/dTDepth, ZZYZX/dTHeight

		thk = 0.05*dTDepth
		diam = 0.5*dTDepth

		HOTSPOT 0, 0, dTHeight, unID: unID=unID+1

		ROTx 180
		ADDz -0.15 * dTHeight
		CYLIND 0.15 * dTHeight, diam/2

		RESOL 4
		ROTy 90
		forkW = diam
		forkL = 0.848 * dTHeight
		GOSUB 110
		DEL 2

		RESOL 8
		ADDz -dTHeight
		ROTy -90
		REVOLVE 3,360, 1+16+32,
			0, 0.01, 1,
			0, 1, 801,
			dTWidth/6, dTWidth/2, 1001
		DEL 4
	ENDIF
ENDIF

RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, MEP_StrConnectionData = MEP_StrConnectionData

DEL TOP

END


100:
	REVOLVE 4,360,1+2+16+32,
		0,			forkW/9, 2,
		0.8*forkL,	forkW/9, 2,
		0.8*forkL,	forkW/7, 2,
		forkL,		forkW/7, 2
	alpha = ATN((forkW/2 - forkW/7) / (0.2*forkL))

	ADDz -0.025*forkW
	PRISM 6,0.05*forkW,
		0,			forkW/2,
		0.8*forkL,	forkW/2,
		forkL,		forkW/7,
		0.8*forkL,	forkW/7,
		0.8*forkL-(forkW/6)/TAN(alpha),	forkW/3,
		0,			forkW/3
	PRISM 6,0.05*forkW,
		0,			-forkW/2,
		0.8*forkL,	-forkW/2,
		forkL,		-forkW/7,
		0.8*forkL,	-forkW/7,
		0.8*forkL-(forkW/6)/TAN(alpha),	-forkW/3,
		0,			-forkW/3
	DEL 1
RETURN

110:
	REVOLVE 2,360,1+2+16+32,
		0,			forkW/8, 2,
		forkL,		forkW/8, 2
	alpha = ATN((forkW/2 - forkW/8) / (0.2*forkL))

	POLY 6,
		0,			forkW/2,
		0.8*forkL,	forkW/2,
		forkL,		forkW/8,
		0.8*forkL,	forkW/8,
		0.8*forkL-(forkW/6)/TAN(alpha),	forkW/3,
		0,			forkW/3
	POLY 6,
		0,			-forkW/2,
		0.8*forkL,	-forkW/2,
		forkL,		-forkW/8,
		0.8*forkL,	-forkW/8,
		0.8*forkL-(forkW/6)/TAN(alpha),	-forkW/3,
		0,			-forkW/3
RETURN
