
if gs_detlevel_3D_m = 0 then end

if not (gs_shadow) then shadow off

bDetailed = (gs_detlevel_3D_m = 2)
if bDetailed then
	resol gs_resol
else
	resol 8
endif

pen gs_cont_pen


! ------------------------------------------------------------------------------
! Hotspots
! ------------------------------------------------------------------------------

unID = 1
hotspot 0, 0, 0, unID : unID = unID + 1
hotspot -a/2 + radPanel, -b/2 + radPanel, 0, unID : unID = unID + 1
hotspot  a/2 - radPanel, -b/2 + radPanel, 0, unID : unID = unID + 1
hotspot  a/2 - radPanel,  b/2 - radPanel, 0, unID : unID = unID + 1
hotspot -a/2 + radPanel,  b/2 - radPanel, 0, unID : unID = unID + 1

hotspot -a/2 + radPanel, -b/2 + radPanel, panelThk, unID : unID = unID + 1
hotspot  a/2 - radPanel, -b/2 + radPanel, panelThk, unID : unID = unID + 1
hotspot  a/2 - radPanel,  b/2 - radPanel, panelThk, unID : unID = unID + 1
hotspot -a/2 + radPanel,  b/2 - radPanel, panelThk, unID : unID = unID + 1

hotspot -gs_boxLength/2, -gs_boxWidth/2, zzyzx, unID : unID = unID + 1
hotspot  gs_boxLength/2, -gs_boxWidth/2, zzyzx, unID : unID = unID + 1
hotspot  gs_boxLength/2,  gs_boxWidth/2, zzyzx, unID : unID = unID + 1
hotspot -gs_boxLength/2,  gs_boxWidth/2, zzyzx, unID : unID = unID + 1


! ------------------------------------------------------------------------------
! Setting materials for MEP
! ------------------------------------------------------------------------------
if useSysMat then
	gs_matPanel		= sMat
	gs_matBox		= sMat
	gs_matGrille	= sMat
	gs_matLouvre	= sMat
endif

! ------------------------------------------------------------------------------
! Panel
! ------------------------------------------------------------------------------
material gs_matPanel

gosub 100	! contour coordinates
addz panelThk - 0.003
prism_ nsp/3, 0.003,
	get (nsp)
del 1

roofAngleNeeded = 0
for i = 1 to 2	! 2 holes for  louvres
	gosub 100 + 100 * i

	cutpolya nsp/3, 2, 0,
		get (nsp)
next i

gosub 100	! contour coordinates

addz panelThk - 0.003
mulz -1

if bDetailed then
	fprism_ gs_matPanel, gs_matPanel, gs_matPanel, gs_matPanel,
		nsp/3, panelThk - 0.003, 10, min (panelThk - 0.003, panelThk*2/3),
		get (nsp)
else
	prism_ nsp/3, panelThk - 0.003,
		get (nsp)
endif

del 2

for i = 1 to 2
	cutend
next i

! ------------------------------------------------------------------------------
! Box
! ------------------------------------------------------------------------------
material gs_matBox
addz panelThk
prism_ 5, zzyzx - panelThk,
		-gs_boxLength/2,			 gs_boxWidth/2,		15,
		 gs_boxLength/2,			 gs_boxWidth/2,		15,
		 gs_boxLength/2,			-gs_boxWidth/2,		15,
		-gs_boxLength/2,			-gs_boxWidth/2,		15,
		-gs_boxLength/2,			 gs_boxWidth/2,		-1
del 1


! ------------------------------------------------------------------------------
! Louvre
! ------------------------------------------------------------------------------
if bDetailed then
	roofAngleNeeded = 1

	gosub 200	! top louvre coordinates

	croof_ gs_matLouvre, gs_matLouvre, gs_matLouvre,
			nsp/4, 0, gs_boxWidth/2 - 0.01, 1, gs_boxWidth/2 - 0.01, 0, 20, louvreThk,
			get(nsp)

	gosub 300	! bottom louvre coordinates

	croof_ gs_matLouvre, gs_matLouvre, gs_matLouvre,
			nsp/4, 0, -gs_boxWidth/2 + 0.01, 1, -gs_boxWidth/2 + 0.01, 0, -20, louvreThk,
			get(nsp)

endif


! ------------------------------------------------------------------------------
! MEP connections
! ------------------------------------------------------------------------------

pen gs_cont_pen
if gs_detlevel_3D_m = 1 then resol 8
if gs_detlevel_3D_m = 2 then resol 24

call "MEP_m_ConnectionsMEP_4" parameters SetProgram = SetProgram,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = gs_matBox,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 17,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 17,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 17


! ------------------------------------------------------------------------------
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ------------------------------------------------------------------------------


! ------------------------------------------------------------------------------
! Subroutines
! ------------------------------------------------------------------------------


! --- contour coordinates ------------------------------------------------------
100:
	put 	-a/2 + radPanel,	-b/2,				14,
			 a/2 - radPanel,	-b/2,				14,
			 a/2,				-b/2 + radPanel,	1014,
			 a/2,				 b/2 - radPanel,	14,
			 a/2 - radPanel,	 b/2,				1014,
			-a/2 + radPanel,	 b/2,				14,
			-a/2,				 b/2 - radPanel,	1014,
			-a/2,				-b/2 + radPanel,	14,
			-a/2 + radPanel,	-b/2,				1014,
			-a/2 + radPanel,	-b/2,				-1
return


! --- top louvre coordinates ---------------------------------------------------
200:
	put	-gs_boxLength/2 + 0.035, gs_boxWidth/2 - 0.005
	if roofAngleNeeded then
		put 0, 15
	else
		put 15
	endif

	put	 gs_boxLength/2 - 0.035, gs_boxWidth/2 - 0.005
	if roofAngleNeeded then
		put 0, 15
	else
		put 15
	endif

	put	 gs_boxLength/2 - 0.035, gs_boxWidth/2 - 0.065
	if roofAngleNeeded then
		put 0, 1015
	else
		put 1015
	endif

	put	-gs_boxLength/2 + 0.035, gs_boxWidth/2 - 0.065
	if roofAngleNeeded then
		put 0, 15
	else
		put 15
	endif

	put	-gs_boxLength/2 + 0.035, gs_boxWidth/2 - 0.005
	if roofAngleNeeded then
		put 0, 1015
	else
		put 1015
	endif
return


! --- bottom louvre coordinates ------------------------------------------------
300:
	put	-gs_boxLength/2 + 0.035, -gs_boxWidth/2 + 0.005
	if roofAngleNeeded then
		put 0, 15
	else
		put 15
	endif

	put	 gs_boxLength/2 - 0.035, -gs_boxWidth/2 + 0.005
	if roofAngleNeeded then
		put 0, 15
	else
		put 15
	endif

	put	 gs_boxLength/2 - 0.035, -gs_boxWidth/2 + 0.065
	if roofAngleNeeded then
		put 0, 1015
	else
		put 1015
	endif

	put	-gs_boxLength/2 + 0.035, -gs_boxWidth/2 + 0.065
	if roofAngleNeeded then
		put 0, 15
	else
		put 15
	endif

	put	-gs_boxLength/2 + 0.035, -gs_boxWidth/2 + 0.005
	if roofAngleNeeded then
		put 0, 1015
	else
		put 1015
	endif
return


