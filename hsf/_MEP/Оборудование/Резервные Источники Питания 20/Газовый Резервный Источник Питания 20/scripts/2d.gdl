
! Gas Emergency Power Generator



PEN gs_cont_pen
SET FILL gs_fill_type


unID = 1
DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

				! --- GAS POWER GENERATOR --- !

	HOTSPOT2 0, 0, unID: unID=unID+1
	HOTSPOT2 A, 0, unID: unID=unID+1
	HOTSPOT2 0, B, unID: unID=unID+1
	HOTSPOT2 A, B, unID: unID=unID+1

	IF GLOB_CONTEXT <> 22 THEN
		! Chaising
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			coolerThk,0, 1,
			coolerThk,4*railThk, 1,
			A,4*railThk, 1,
			A,0,1
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			coolerThk,B, 1,
			coolerThk,B-4*railThk, 1,
			A,B-4*railThk, 1,
			A,B,1
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			coolerThk,4*railThk, 1,
			coolerThk,B-4*railThk, 1,
			A,B-4*railThk, 1,
			A,4*railThk,1

		! Gas Motor
		motorL = motorLength-3*coolerThk
		ADD2 3*coolerThk, B/2
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, motorWidth/2, 1,
			0,-motorWidth/2,1,
			motorL,-motorWidth/2, 1,
			motorL, motorWidth/2,1
		LINE2 0.7*motorL,-motorWidth/2, 0.7*motorL,motorWidth/2
		DEL 1

		! Generator
		ADD2 motorLength, B/2
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, genWidth/2, 1,
			0,-genWidth/2,1,
			A-boxDepth-motorLength,-genWidth/2, 1,
			A-boxDepth-motorLength, genWidth/2,1
		DEL 1

		! Box
		ADD2 A-boxDepth, B/2
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, B/2, 1,
			0,-B/2,1,
			boxDepth,-B/2, 1,
			boxDepth, B/2,1
		DEL 1

		! Cooler
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,0, 1,
			0,B, 1,
			coolerThk,B, 1,
			coolerThk,0,1
		ADD2 0, B/2
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			3*coolerThk,-0.6*motorWidth, 1,
			3*coolerThk,0.6*motorWidth, 1,
			coolerThk,0.6*motorWidth, 1,
			coolerThk,-0.6*motorWidth,1
		DEL 1

	ENDIF
ENDIF
DEL TOP


				! --- OTHER VIEWS --- !

ROT2 (SYMB_MIRRORED - NOT(SYMB_MIRRORED)) * (DrawingNum > 1) * SYMB_ROTANGLE

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180 * SYMB_MIRRORED,		 2	! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180 * NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 5
DEL 1

END
