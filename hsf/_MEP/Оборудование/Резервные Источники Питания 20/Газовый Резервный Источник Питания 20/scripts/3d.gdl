
! Gas Emergency Power Generator


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

DEFINE MATERIAL	"Shaft Black Plastic" 20,
	0.147059, 0.135294, 0.103922,
	0.9, 0.6, 0.2, 0, 10, 0,
	1, 0.978103, 0.968383,
	0, 0, 0,
	0, 0, 1,
	0

DEFINE MATERIAL	"Shaft White Plastic" 20,
	1, 1, 1,
	0.98, 0.8, 0.25, 0, 3, 0,
	1, 1, 1,
	0.654902, 0.654902, 0.654902,
	100, 0, -1,
	0

DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0


				! --- GAS POWER GENERATOR --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1

HOTSPOT A, 0, boxBottom+boxHeight, unID: unID=unID+1
HOTSPOT A, B, boxBottom+boxHeight, unID: unID=unID+1


IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed
	! Chaising
	ROTy 90
	ROTz 90
	railL = A

	FOR i=1 TO 2
		GOSUB 100
		ADDx B
		MULx -1
	NEXT i
	DEL 6

	ADD 0, railThk, railH-railThk-4*railThk
	BLOCK A,B-2*railThk,4*railThk
	DEL 1


	! Cooler
	coolerH = motorTop - 0.1
	ADDz railH + 0.1
	ROTx 90
	PRISM 8,-B,
		0, 0.06,
		0, coolerH - 0.06,
		0.02, coolerH - 0.06,
		0.02, coolerH,
		coolerThk, coolerH,
		coolerThk, 0,
		0.02, 0,
		0.02, 0.06
	DEL 2

	ADD coolerThk, B/2-0.6*motorWidth,railH + 0.15
	BLOCK 2*coolerThk, 1.2*motorWidth, coolerH - 0.1
	DEL 1

	! Gas Motor
	motorL = motorLength-3*coolerThk
	ADD 3*coolerThk, (B-motorWidth)/2, railH
	ROTx 90
	PRISM 6,-motorWidth,
		0,0,
		0,motorTop-railH,
		0.7*motorL,motorTop-railH,
		0.7*motorL,0.85*motorTop-railH,
		motorL,0.85*motorTop-railH,
		motorL,0
	DEL 2

	! Box
	ADD A-boxDepth,0,railH
	BLOCK boxDepth, 2*railThk, ZZYZX - railH
	ADDy B-2*railThk
	BLOCK boxDepth, 2*railThk, ZZYZX - railH
	DEL 2
	ADD A-boxDepth, 2*railThk, boxBottom
	BLOCK boxDepth, B-4*railThk, boxHeight
	DEL 1

	! Metering
	ADD A+0.001,0.3*B,boxBottom+0.12
	ROTz 90
	ROTx 90
		MATERIAL	"Shaft Black Plastic"
		IF useSysMat THEN MATERIAL sMat
		POLY_ 10,
			-0.060, 0.050, 1,
			-0.060,-0.050, 1,
			 0.060,-0.050, 1,
			 0.060, 0.050, 1,
			-0.060, 0.050, -1,
			-0.050, 0.040, 1,
			-0.050,-0.020, 1,
			 0.050,-0.020, 1,
			 0.050, 0.040, 1,
			-0.050, 0.040, -1

		ROTz 30
		POLY 4,
			-0.001, 0.030,
			-0.001,-0.020,
			 0.001,-0.020,
			 0.001, 0.030
		DEL 1
		MATERIAL "Shaft White Plastic"
		IF useSysMat THEN MATERIAL sMat
		POLY_ 4,
			-0.050, 0.040, 1,
			-0.050,-0.020, 1,
			 0.050,-0.020, 1,
			 0.050, 0.040, 1
	DEL 3

	! Button
	ADD A+0.001,0.3*B + MIN(0.2, 0.45*B),boxBottom+0.12
	ROTy 90
	ROTz 90
	MATERIAL "Shaft Aluminum"
	IF useSysMat THEN MATERIAL sMat
	POLY_ 3,
		0,0, 901,
		0.03,360,4001,
		0.02,360,4001

	CYLIND 0.01,0.02
	ADDz 0.01
	PRISM 4, 0.01,
		-0.01, 0.02,
		-0.01,-0.06,
		 0.01,-0.06,
		 0.01, 0.02
	DEL 4

	MATERIAL MainBoxMaterial

	! Generator
	RESOL res
	ADD motorLength, B/2,genWidth/2+railH
	ROTy 90
	genL = A-boxDepth-motorLength+0.1
	CYLIND genL, genWidth/2
	ADDz genL/2
	IF genWidth>EPS THEN
		CUTPOLYA 2,1,0,
			0,0,913,
			genWidth/2,360,4013
		PRISM 4, genL/2,
			0,-genWidth/3,
			-0.6*genWidth,-genWidth/3,
			-0.6*genWidth, genWidth/3,
			0, genWidth/3
		CUTEND
	ENDIF
	DEL 2
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple
	! Chaising
	ROTy 90
	ROTz 90
	railL = A

	FOR i=1 TO 2
		GOSUB 100
		ADDx B
		MULx -1
	NEXT i
	DEL 6

	ADD 0, railThk, railH-railThk-4*railThk
	BLOCK A,B-2*railThk,4*railThk
	DEL 1

	! Cooler
	coolerH = motorTop - 0.1
	RESOL 10
	ADDz railH + 0.1
	BLOCK coolerThk, B, coolerH
	DEL 1


	ADD coolerThk, B/2-0.6*motorWidth,railH + 0.15
	BLOCK 2*coolerThk, 1.2*motorWidth, coolerH - 0.1
	DEL 1


	! Gas Motor
	motorL = motorLength-3*coolerThk
	ADD 3*coolerThk, (B-motorWidth)/2, railH
	ROTx 90
	PRISM 6,-motorWidth,
		0,0,
		0,motorTop-railH,
		0.7*motorL,motorTop-railH,
		0.7*motorL,0.85*motorTop-railH,
		motorL,0.85*motorTop-railH,
		motorL,0
	DEL 2

	! Box
	ADD A-boxDepth,0,railH
	BLOCK boxDepth, 2*railThk, ZZYZX - railH
	ADDy B-2*railThk
	BLOCK boxDepth, 2*railThk, ZZYZX - railH
	DEL 2
	ADD A-boxDepth, 2*railThk, boxBottom
	BLOCK boxDepth, B-4*railThk, boxHeight
	DEL 1

	! Generator
	RESOL 10
	ADD motorLength, B/2,genWidth/2+railH
	ROTy 90
	genL = A-boxDepth-motorLength+0.1
	CYLIND genL, genWidth/2
	ADDz genL/2
	IF genWidth>EPS THEN
		CUTPOLYA 2,1,0,
			0,0,913,
			genWidth/2,360,4013
		PRISM 4, genL/2,
			0,-genWidth/3,
			-0.6*genWidth,-genWidth/3,
			-0.6*genWidth, genWidth/3,
			0, genWidth/3
		CUTEND
	ENDIF
	DEL 2
ENDIF

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 15
END


100:
	PRISM 8, railL,
		0,			0,
		0,			railH,
		4*railThk,	railH,
		4*railThk,	railH-railThk,
		railThk,	railH-railThk,
		railThk,	railThk,
		4*railThk,	railThk,
		4*railThk,	0
RETURN

110:
	PRISM 4, railL,
		0,			0,
		0,			railH,
		4*railThk,	railH,
		4*railThk,	0
RETURN
