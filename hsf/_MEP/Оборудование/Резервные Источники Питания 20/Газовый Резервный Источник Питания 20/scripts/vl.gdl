
! Gas Emergency Power Generator


call "FM_types_MEP" parameters
DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 0		! Rectangle
	shape_typ_to_validation[2] = 1		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0


					! --- MAIN PARAMETERS --- !

IF NOT(SetProgram) THEN
!!	maxZ=-99999
!!	FOR i=1 TO MEP_NumberConnections
!!		actZ = O_Connections[i][PZ] - O_Connections[i][CL] * O_Connections[i][VZ]
!!		IF actZ > maxZ THEN maxZ=actZ
!!	NEXT i

	minW = 0.50
	IF A<minW THEN
		A=minW
		PARAMETERS A=minW
	ENDIF
	VALUES "A" RANGE [minW,]

	minD = 0.30
	IF B<minD THEN
		B=minD
		PARAMETERS B=minD
	ENDIF
	VALUES "B" RANGE [minD,]

	minH = 0
	IF ZZYZX<minH THEN
		ZZYZX=minH
		PARAMETERS ZZYZX=minH
	ENDIF
	VALUES "ZZYZX" RANGE [minH,]
ENDIF


					! --- OBJECT SPECIFIC PARAMETERS --- !

IF NOT(SetProgram) THEN
	IF GLOB_MODPAR_NAME="A" THEN
		IF motorLength>0.7*A THEN
			motorLength=0.7*A
			PARAMETERS motorLength=motorLength
		ENDIF
		IF boxDepth>0.25*A THEN
			boxDepth=0.25*A
			PARAMETERS boxDepth=boxDepth
		ENDIF
	ENDIF

	motorMaxL=A-2*coolerThk-boxDepth/2-boxDepth
	IF motorLength>motorMaxL THEN
		motorLength=motorMaxL
		PARAMETERS motorLength=motorLength
	ENDIF
	VALUES "motorLength" RANGE[minMotorLength,motorMaxL]
	VALUES "boxDepth" RANGE[0.05,0.25*A]

	coolerThk = MIN(0.08 * ZZYZX, motorLength/4)		! Compute coolerThk again

	IF motorWidth>B THEN
		motorWidth=B
		PARAMETERS motorWidth=motorWidth
	ENDIF
	VALUES "motorWidth" RANGE[0.1, B]

	motorTopMin=2*railH
	motorTopMax=ZZYZX
	IF motorTop<motorTopMin THEN
		motorTop=motorTopMin
		PARAMETERS motorTop=motorTop
	ENDIF
	IF motorTop>motorTopMax THEN
		motorTop=motorTopMax
		PARAMETERS motorTop=motorTop
	ENDIF
	VALUES "motorTop" RANGE[motorTopMin, motorTopMax]

	boxBottomMin=railH
	boxBottomMax=ZZYZX-0.2
	IF boxBottom<boxBottomMin THEN
		boxBottom=boxBottomMin
		PARAMETERS boxBottom=boxBottom
	ENDIF
	IF boxBottom>boxBottomMax THEN
		boxBottom=boxBottomMax
		PARAMETERS boxBottom=boxBottom
	ENDIF
	VALUES "boxBottom" RANGE[railH, boxBottomMax]
ENDIF


					! --- CONNECTIONS --- !

PARAMETERS MEP_NumberConnections = 2


					! --- CONNECTION --- !

ConID = 1
ConName = `Соединение Трубы`

ConDVecX = 0
ConDVecY = -1
ConDVecZ = 0

ConWVecX = 1
ConWVecY = 0
ConWVecZ = 0

IF NOT(SetProgram) THEN
	maxCW = B/3

	VALUES "ConLength_1" RANGE [0,]
	VALUES "ConWidth_1" RANGE (0, maxCW]
	VALUES "ConDepth_1" RANGE (0, maxCW]

	IF ConWidth_1<EPS THEN
		ConWidth_1=EPS
		PARAMETERS ConWidth_1=EPS
	ENDIF
	IF ConWidth_1>maxCW THEN
		ConWidth_1=maxCW
		PARAMETERS ConWidth_1=maxCW
	ENDIF

	VALUES "ConPosX_1" RANGE[3*coolerThk+ConWidth_1/2, motorLength-ConWidth_1/2]
	PARAMETERS ConPosY_1 = B/2-motorWidth/2
	VALUES "ConPosZ_1" RANGE[railH+ConWidth_1/2, 0.85*motorTop-ConWidth_1/2]
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_1,
	ConPosY = ConPosY_1,
	ConPosZ = ConPosZ_1,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_1,
	ConNominalDepth = ConNominalDepth_1,
	ConWidth = ConWidth_1,
	ConDepth = ConDepth_1,
	ConLength = ConLength_1,
	ConWallThickness = ConWallThickness_1,
	ConConnectorWidth = ConConnectorWidth_1,
	ConConnectorDepth = ConConnectorDepth_1,
	ConConnectorWidth2 = ConConnectorWidth2_1,
	ConToolType = ConToolType_1,
	ConStatus = ConStatus_1,
	ConType_1 = ConType_1,
	ConConnectorType_1 = ConConnectorType_1,
	ConSystem_1 = ConSystem_1,
	ui_page_idx = ui_page_idx,
	ui_current_con = ui_current_con,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1,
	bUIConnectionStatusCheckbox = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"


					! --- CONNECTION --- !

ConID = 2
ConName = `Соединение Воздуховода`

ConDVecX = 0
ConDVecY = 0
ConDVecZ = 1

ConWVecX = 1
ConWVecY = 0
ConWVecZ = 0

IF NOT(SetProgram) THEN
	maxCW = B/3

	VALUES "ConLength_2" RANGE [0,]
	VALUES "ConWidth_2" RANGE (0, maxCW]
	VALUES "ConDepth_2" RANGE (0, maxCW]

	IF ConWidth_2<EPS THEN
		ConWidth_2=EPS
		PARAMETERS ConWidth_2=EPS
	ENDIF
	IF ConWidth_2>maxCW THEN
		ConWidth_2=maxCW
		PARAMETERS ConWidth_2=maxCW
	ENDIF

	VALUES "ConPosX_2" RANGE[3*coolerThk+ConWidth_2/2+ConWallThickness_2, 3*coolerThk+0.7*(motorLength-3*coolerThk)-ConWidth_2/2-ConWallThickness_2]
	VALUES "ConPosY_2" RANGE[B/2-motorWidth/2+ConWidth_2/2+ConWallThickness_2, B/2+motorWidth/2-ConWidth_2/2-ConWallThickness_2]
	PARAMETERS ConPosZ_2 = motorTop
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_2,
	ConPosY = ConPosY_2,
	ConPosZ = ConPosZ_2,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_2,
	ConNominalDepth = ConNominalDepth_2,
	ConWidth = ConWidth_2,
	ConDepth = ConDepth_2,
	ConLength = ConLength_2,
	ConWallThickness = ConWallThickness_2,
	ConConnectorWidth = ConConnectorWidth_2,
	ConConnectorDepth = ConConnectorDepth_2,
	ConConnectorWidth2 = ConConnectorWidth2_2,
	ConToolType = ConToolType_2,
	ConStatus = ConStatus_2,
	ConType_2 = ConType_2,
	ConConnectorType_2 = ConConnectorType_2,
	ConSystem_2 = ConSystem_2,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1,
	bUIConnectionStatusCheckbox = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF setProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF
