
! Diesel Emergency Power Generator


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- DIESEL POWER GENERATOR --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1

HOTSPOT A, B/2+boxWidth/2, boxBottom+boxHeight, unID: unID=unID+1
HOTSPOT A, B/2-boxWidth/2, boxBottom+boxHeight, unID: unID=unID+1


IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed
	! Chaising
	ROTy 90
	ROTz 90
	railL = A

	FOR i=1 TO 2
		GOSUB 100
		ADDx B
		MULx -1
	NEXT i
	DEL 6

	ROTy 90
	ROTx -90
	ADD -railH+railThk, -2*coolerThk-railH, railThk
	railL = B-2*railThk
	GOSUB 100
	ADDy -motorLength+2*coolerThk+railH
	GOSUB 100
	DEL 2
	ADD -railH+railThk, -A+boxDepth-railH, railThk
	GOSUB 100
	ADD -boxBottom+railH-railThk,0,(B-genWidth)/2
	BLOCK boxBottom-railH+railThk,railH,genWidth
	DEL 4


	! Cooler
	ADDz railH + 0.1
	ROTx 90
	PRISM 8,-B,
		0, 0.06,
		0, ZZYZX - railH - 0.1 - 0.06,
		0.02, ZZYZX - railH - 0.1 - 0.06,
		0.02, ZZYZX - railH - 0.1,
		coolerThk, ZZYZX - railH - 0.1,
		coolerThk, 0,
		0.02, 0,
		0.02, 0.06
	DEL 1

	ADD coolerThk, B/2, (ZZYZX - railH - 0.1)/2
	ROTy 90
	CYLIND coolerThk, 0.3*B
	DEL 3

	! Cooler Pipes
	RESOL 10
	IF pipeR > pipeD/2 THEN
		ADD 2*coolerThk, B/2, motorTop
		ROTz -90
		FOR i=1 TO 2
			ADDx 0.35*motorWidth
			REVOLVE 2,90,1+2+16,
				0,pipeR,901,
				pipeD/2,360,4001
			ADDz pipeR
			ROTz -90
			REVOLVE 2,360,1+2+16+32,
				0,pipeD/2, 1,
				coolerThk,pipeD/2, 2
			DEL 3
			MULx -1
		NEXT i
		DEL 4
	ENDIF

	! Diesel Motor
	ADD 2*coolerThk, (B-motorWidth)/2, railH

	BLOCK motorLength-2*coolerThk, motorWidth, motorTop-railH
	DEL 1

	! Box
	ADD A-boxDepth, (B-boxWidth)/2, boxBottom
	ROTx 90
	ROTy 90
	PRISM 5, boxDepth,
		0,0,
		0,boxHeight-0.15*boxWidth,
		0.2*boxWidth,boxHeight,
		boxWidth,boxHeight,
		boxWidth,0
	DEL 2

	ADD 0.4*boxDepth, 0.2*boxWidth,boxHeight
	BLOCK 0.6*boxDepth, 0.2*boxWidth, 0.25*boxHeight
	DEL 2

	! Generator
	RESOL res
	ADD motorLength, B/2,genWidth/2+railH
	ROTy 90
	CYLIND A-boxDepth-motorLength, genWidth/2
	DEL 2
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple
	! Chaising
	ROTy 90
	ROTz 90
	railL = A

	FOR i=1 TO 2
		GOSUB 100
		ADDx B
		MULx -1
	NEXT i
	DEL 6

	ROTy 90
	ROTx -90
	ADD -railH+railThk, -2*coolerThk-railH, railThk
	railL = B-2*railThk
	GOSUB 110
	ADDy -motorLength+2*coolerThk+railH
	GOSUB 110
	DEL 2
	ADD -railH+railThk, -A+boxDepth-railH, railThk
	GOSUB 110
	ADD -boxBottom+railH-railThk,0,(B-genWidth)/2
	BLOCK boxBottom-railH+railThk,railH,genWidth
	DEL 4


	! Cooler
	RESOL 10
	ADDz railH + 0.1
	BLOCK coolerThk, B, ZZYZX - railH - 0.1
	ADD coolerThk, B/2, (ZZYZX - railH - 0.1)/2
	ROTy 90
	CYLIND coolerThk, 0.3*B
	DEL 3

	! Cooler Pipes
	RESOL 6
	IF pipeR > pipeD/2 THEN
		ADD 2*coolerThk, B/2, motorTop
		ROTz -90
		FOR i=1 TO 2
			ADDx 0.35*motorWidth
			REVOLVE 2,90,1+2+16,
				0,pipeR,901,
				pipeD/2,360,4001
			ADDz pipeR
			ROTz -90
			REVOLVE 2,360,1+2+16+32,
				0,pipeD/2, 1,
				coolerThk,pipeD/2, 2
			DEL 3
			MULx -1
		NEXT i
		DEL 4
	ENDIF

	! Diesel Motor
	ADD 2*coolerThk, (B-motorWidth)/2, railH
	BLOCK motorLength-2*coolerThk, motorWidth, motorTop-railH
	DEL 1

	! Box
	ADD A-boxDepth, (B-boxWidth)/2, boxBottom
	BLOCK boxDepth, boxWidth, boxHeight
	ADD 0.4*boxDepth, 0.2*boxWidth,boxHeight
	BLOCK 0.6*boxDepth, 0.2*boxWidth, 0.25*boxHeight
	DEL 2

	! Generator
	RESOL 10
	ADD motorLength, B/2,genWidth/2+railH
	ROTy 90
	CYLIND A-boxDepth-motorLength, genWidth/2
	DEL 2
ENDIF

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 15
END

100:
	PRISM 8, railL,
		0,			0,
		0,			railH,
		4*railThk,	railH,
		4*railThk,	railH-railThk,
		railThk,	railH-railThk,
		railThk,	railThk,
		4*railThk,	railThk,
		4*railThk,	0
RETURN

110:
	PRISM 4, railL,
		0,			0,
		0,			railH,
		4*railThk,	railH,
		4*railThk,	0
RETURN
