!------------------- 3д начало -------------------------
if show_el < EPS then end
if GLOB_SCRIPT_TYPE=2 then gs_detlevel_3D_m = gs_detlevel_2D_m
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
MATERIAL sMat
PEN gs_cont_pen
RESOL res
hotspot 0,0,0


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, legHeight, unID: unID=unID+1
HOTSPOT 0, 0, legHeight+ZZYZX, unID: unID=unID+1

endRed2 = endRed
IF gs_detlevel_3D = `Простой` THEN endRed2 = 0

HOTSPOT -A/2, 0, legHeight+endRed2, unID: unID=unID+1
HOTSPOT A/2, 0, legHeight+endRed2, unID: unID=unID+1
HOTSPOT 0, -B/2, legHeight+endRed2, unID: unID=unID+1
HOTSPOT 0, B/2, legHeight+endRed2, unID: unID=unID+1

HOTSPOT -A/2, 0, legHeight+ZZYZX-endRed2, unID: unID=unID+1
HOTSPOT A/2, 0, legHeight+ZZYZX-endRed2, unID: unID=unID+1
HOTSPOT 0, -B/2, legHeight+ZZYZX-endRed2, unID: unID=unID+1
HOTSPOT 0, B/2, legHeight+ZZYZX-endRed2, unID: unID=unID+1


				! --- TANK --- !

IF gs_detlevel_3D_m = 2 THEN
	GROUP "Tank"
		ADDz legHeight
		CALL "MEP_m_Tank" PARAMETERS	tankLen = ZZYZX, tankDia = A, endRed = endRed,
										ringOn = 0, typ = 2, tankPos = 1, tankMat = MainBoxMaterial,
										gs_cont_pen = gs_cont_pen, res = res
		DEL 1
	ENDGROUP

	GROUP "Pipes+Legs"
	IF Constatus_5 THEN
		ADDz legHeight
		EXTRUDE 2, 0, 0, endRed, 1+2+32,
			0, 0, 901,
			ConWidth_5/2, 360, 4001
		DEL 1
	ENDIF

	IF Constatus_6 THEN
		ADDz legHeight+ZZYZX-endRed
		EXTRUDE 2, 0, 0, endRed, 1+2+16,
			0, 0, 901,
			ConWidth_6/2, 360, 4001
		DEL 1
	ENDIF

	legDia = 0.064*(A/0.61)
	legDist = (A/2)*0.70
	if legHeight>EPS then
	FOR i = 1 TO 4
		ROTz 90*(i-1)
		HOTSPOT 0, legDist, 0
		ADD 0, legDist, 0
		PRISM_ 2, legHeight*0.85,
			0, 0, 979,
			legDia/2, 360, 4079
		ADDz legHeight*0.85
		PRISM_ 2, endRed+legHeight*0.15,
			0, 0, 979,
			(legDia/2)*1.45, 360, 4079
		DEL 3
	NEXT i
	endif
	ENDGROUP

	Tank_with_Pipe = ADDGROUP ("Tank", "Pipes+Legs")

	PLACEGROUP Tank_with_Pipe

	KILLGROUP "Tank"
	KILLGROUP "Pipes+Legs"
	KILLGROUP Tank_with_Pipe
ELSE
	ADDz legHeight
		CYLIND ZZYZX, B/2
	DEL 1

	legDia = 0.064*(A/0.61)
	legDist = (A/2)*0.70
	if legHeight>EPS then
	FOR i = 1 TO 4
		ROTz 90*(i-1)
		HOTSPOT 0, legDist, 0, unID: unID=unID+1
		ADD 0, legDist, 0
		PRISM_ 2, legHeight,
			0, 0, 979,
			legDia/2, 360, 4079
		DEL 2
	NEXT i
	endif
ENDIF


CUTPOLYA 2, 2, 0,
	0, 0, 913,
	A/2, 360, 4013

IF Constatus_1 THEN
	ADDz ConPosZ_1
	ROTy -90
	EXTRUDE 2, 0, 0, B/2, 16,
		0, 0, 901,
		ConWidth_1/2, 360, 4001
	DEL 2
ENDIF

IF Constatus_2 THEN
	ADDz ConPosZ_2
	ROTy -90
	EXTRUDE 2, 0, 0, B/2, 16,
		0, 0, 901,
		ConWidth_2/2, 360, 4001
	DEL 2
ENDIF

IF Constatus_3 THEN
	ADDz ConPosZ_3
	ROTy 90
	EXTRUDE 2, 0, 0, B/2, 16,
		0, 0, 901,
		ConWidth_3/2, 360, 4001
	DEL 2
ENDIF

IF Constatus_4 THEN
	ADDz ConPosZ_4
	ROTy 90
	EXTRUDE 2, 0, 0, B/2, 16,
		0, 0, 901,
		ConWidth_4/2, 360, 4001
	DEL 2
ENDIF
CUTEND


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 14, ConEdgeVisible_1 = 0,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 14, ConEdgeVisible_2 = 0,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 14, ConEdgeVisible_3 = 0,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 14, ConEdgeVisible_4 = 0,
	ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConPosZ_5=ConPosZ_5, ConLength_5=ConLength_5, ConEdit_5 = 1, ConEdgeVisible_5 = 0,
	ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConPosZ_6=ConPosZ_6, ConLength_6=ConLength_6, ConEdit_6 = 1, ConEdgeVisible_6 = 0, MEP_StrConnectionData = MEP_StrConnectionData, mep_con = mep_con, mep_data = mep_data
END
