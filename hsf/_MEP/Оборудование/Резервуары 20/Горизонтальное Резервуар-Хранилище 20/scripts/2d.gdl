!-----------2d---------------------
if show_el < EPS then end
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END

! Horizontal Storage Tank



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
HOTSPOT2 0, 0, unID: unID=unID+1

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top


				! --- MAIN BOX --- !

	HOTSPOT2 -dTWidth/2+endRed, -dTDepth/2, unID: unID=unID+1
	HOTSPOT2 dTWidth/2-endRed, -dTDepth/2, unID: unID=unID+1
	HOTSPOT2 dTWidth/2, 0, unID: unID=unID+1
	HOTSPOT2 dTWidth/2-endRed, dTDepth/2, unID: unID=unID+1
	HOTSPOT2 -dTWidth/2+endRed, dTDepth/2, unID: unID=unID+1
	HOTSPOT2 -dTWidth/2, 0, unID: unID=unID+1

	ADD2 -dTWidth/2, 0
	CALL "MEP_m_Tank" PARAMETERS	tankLen = dTWidth, tankDia = dTDepth, endRed = endRed,
									ringOn = 0, typ = 2, tankPos = 0,
									gs_cont_pen = gs_cont_pen, gs_fill_pen = gs_fill_pen,
									gs_back_pen = gs_back_pen, gs_fill_type = gs_fill_type, hptsID = 100
	DEL 1

	LINE2 dTWidth/2-endRed, -dTDepth/2, dTWidth/2-endRed, dTDepth/2
	LINE2 -dTWidth/2+endRed, -dTDepth/2, -dTWidth/2+endRed, dTDepth/2


				! --- CONNECTIONS --- !

!	IF Constatus_1 THEN CIRCLE2 ConPosX_1, ConPosY_1, ConWidth_1/2
!	IF Constatus_2 THEN CIRCLE2 ConPosX_2, ConPosY_2, ConWidth_2/2

	IF Constatus_5 THEN
		 ConPosX_5L = -(dTWidth/2-rr+SQR(rr^2-(ABS(ConPosY_5+ConWidth_5/2))^2))
		 ConPosX_5R = -(dTWidth/2-rr+SQR(rr^2-(ABS(ConPosY_5-ConWidth_5/2))^2))

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			ConPosX_5, 0, 1,
			0, 1, 801,
			ConPosX_5L, ConPosY_5+ConWidth_5/2, 1001,
			ConPosX_5, ConPosY_5+ConWidth_5/2, 0

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			ConPosX_5, 0, 1,
			0, -1, 801,
			ConPosX_5R, ConPosY_5-ConWidth_5/2, 1001,
			ConPosX_5, ConPosY_5-ConWidth_5/2, 0
	ENDIF

	IF Constatus_6 THEN
		 ConPosX_6L = (dTWidth/2-rr+SQR(rr^2-(ABS(ConPosY_6+ConWidth_6/2))^2))
		 ConPosX_6R = (dTWidth/2-rr+SQR(rr^2-(ABS(ConPosY_6-ConWidth_6/2))^2))

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			ConPosX_6, 0, 1,
			0, 1, 801,
			ConPosX_6L, ConPosY_6+ConWidth_6/2, 1001,
			ConPosX_6, ConPosY_6+ConWidth_6/2, 0

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			ConPosX_6, 0, 1,
			0, -1, 801,
			ConPosX_6R, ConPosY_6-ConWidth_6/2, 1001,
			ConPosX_6, ConPosY_6-ConWidth_6/2, 0
	ENDIF

				! --- SUPPORT --- !

	ADD2 -dTWidth/2+endRed+supDist, 0
	ROT2 90
	CALL "MEP_m_Tank_support_4" PARAMETERS	supLen = supLen, supHeight = legHeight+dTDepth/4, supThick = 0.02,
											footWidth = dTDepth+0.05*dd, footThick = 0.025,
											tankDia = dTDepth, tankDeepening = dTDepth/4,
											gs_cont_pen = gs_cont_pen, gs_fill_pen = gs_fill_pen,
											gs_back_pen = gs_back_pen, gs_fill_type = gs_fill_type, hptsID = 200
	DEL 2

	ADD2 dTWidth/2-endRed-supDist, 0
	ROT2 -90
	CALL "MEP_m_Tank_support_4" PARAMETERS	supLen = supLen, supHeight = legHeight+dTDepth/4, supThick = 0.02,
											footWidth = dTDepth+0.05*dd, footThick = 0.025,
											tankDia = dTDepth, tankDeepening = dTDepth/4,
											gs_cont_pen = gs_cont_pen, gs_fill_pen = gs_fill_pen,
											gs_back_pen = gs_back_pen, gs_fill_type = gs_fill_type, hptsID = 300
	DEL 2
ENDIF


				! --- OTHER VIEWS --- !

ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 2,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 2,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 2,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 2,
	ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConPosZ_5=ConPosZ_5, ConLength_5=ConLength_5, ConEdit_5 = 1, ConEdgeVisible_5 = 0,
	ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConPosZ_6=ConPosZ_6, ConLength_6=ConLength_6, ConEdit_6 = 1, ConEdgeVisible_6 = 0
DEL 1

END
