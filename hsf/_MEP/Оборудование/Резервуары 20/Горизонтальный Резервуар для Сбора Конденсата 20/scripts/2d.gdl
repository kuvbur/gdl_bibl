
! Horiz Pressure Operated Condensate Pump



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

				! --- HORIZONTAL PRESSURE OPERATED CONDENSATE PUMP --- !

HOTSPOT2 -A/2+0.4*tankR,  tankR, unID: unID=unID+1
HOTSPOT2 -A/2+0.4*tankR, -tankR, unID: unID=unID+1
HOTSPOT2  A/2-0.4*tankR, -tankR, unID: unID=unID+1
HOTSPOT2  A/2-0.4*tankR,  tankR, unID: unID=unID+1


!	IF GLOB_CONTEXT <> 22 THEN

		ADD2 ConPosX_1,0
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,ConWidth_1/2, 1,
			pipeL1,ConWidth_1/2, 1,
			pipeL1,-ConWidth_1/2, 1,
			0,-ConWidth_1/2, 1
		DEL 1
		ADD2 ConPosX_2,0
		MUL2 -1,1
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,ConWidth_2/2, 1,
			pipeL2,ConWidth_2/2, 1,
			pipeL2,-ConWidth_2/2, 1,
			0,-ConWidth_2/2, 1
		DEL 2

		ADD2 -A/2,0
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0.4*tankR, tankR, 1,
			0.4*tankR, -tankR, 1,
			tankL-0.4*tankR, -tankR, 1,
			tankL-0.4*tankR, tankR, 1
		ADD2 0.4*tankR,0
		MUL2 0.4,1
		POLY2_B 3, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,tankR, 1,
			-1,0,801,
			0,-tankR,1001
		DEL 2
		ADD2 tankL-0.4*tankR,0
		MUL2 -0.4,1
		POLY2_B 3, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,tankR, 1,
			-1,0,801,
			0,-tankR,1001
		DEL 3

		! Top
		minX = -A/2+pipeL1+0.5*tankR
		maxX =  A/2-pipeL1-0.5*tankR

		ADD2 MAX(MIN(ConPosX_3, maxX-topR+ConWidth_3/2), minX+topR-ConWidth_3/2), 0
		CIRCLE2 0,0,topR
		DEL 1

		! Valve
		ADD2 A/2, MIN(tankR-valveR, tankR/2)
		POLY2_B 14, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.135*tankR, -valveR/2, 1,
			-0.155*tankR, valveR/2, 1,
			-0.15*tankR, valveR, 1,
			0.1*tankR+1.5*valveR, valveR, 1,
			0.1*tankR+1.5*valveR, valveR/2, 1,
			0.1*tankR+1.5*valveR+valveR, valveR/2, 1,
			0.1*tankR+1.5*valveR+valveR, 2*valveR, 1,
			0.1*tankR+1.5*valveR+valveR+valveR/5, 2*valveR, 1,

			0.1*tankR+1.5*valveR+valveR+valveR/5, -2*valveR, 1,
			0.1*tankR+1.5*valveR+valveR, -2*valveR, 1,
			0.1*tankR+1.5*valveR+valveR, -valveR/2, 1,
			0.1*tankR+1.5*valveR, -valveR/2, 1,
			0.1*tankR+1.5*valveR, -valveR, 1,
			-0.11*tankR, -valveR, 1

		LINE2 0.1*tankR+1.5*valveR, valveR/2, 0.1*tankR+1.5*valveR, -valveR/2
		LINE2 0.1*tankR+1.5*valveR+valveR, valveR/2, 0.1*tankR+1.5*valveR+valveR, -valveR/2

		POLY2_B 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0.1*tankR,0,901,
			1.1*valveR,360,4001
		DEL 1
!	ENDIF
ENDIF

				! --- OTHER VIEWS --- !

DEL TOP
ROT2 (SYMB_MIRRORED - NOT(SYMB_MIRRORED)) * (DrawingNum > 1) * SYMB_ROTANGLE

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180 * SYMB_MIRRORED,		 2	! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180 * NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 1,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 2,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConLength_4=ConLength_4, ConEdit_4 = 2
DEL 1

END
