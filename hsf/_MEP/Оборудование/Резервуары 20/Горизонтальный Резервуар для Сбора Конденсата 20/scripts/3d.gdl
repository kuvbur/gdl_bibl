
! Horiz Pressure Operated Condensate Pump


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HORIZONTAL PRESSURE OPERATED CONDENSATE PUMP --- !

IF gs_detlevel_3D_m = 2 THEN	! Detailed
	GROUP "Connections1"
		RESOL 10

		ADD ConPosX_1,0,ConPosZ_1
		REVOLVE 2,360,1+2+4+16+32,
			0,ConWidth_1/2, 2,
			2*pipeL1,ConWidth_1/2, 2
		DEL 1

		ADD ConPosX_2,0,ConPosZ_2
		MULx -1
		REVOLVE 2,360,1+2+4+16+32,
			0,ConWidth_2/2, 2,
			2*pipeL2,ConWidth_2/2, 2
		DEL 2
	ENDGROUP

	RESOL res

	ADD -A/2+pipeL1,0,ZZYZX/2
	ROTy 90
	CUTPOLYA 2,1,0,
		0,0,913,
		tankR,360,4013
	DEL 2

	ADDx -A/2+0.4*tankR+0.15*tankL
	ROTy -90
	PRISM 4,0.08*tankL,
		0,-0.8*tankR,
		ZZYZX/2,-0.8*tankR,
		ZZYZX/2, 0.8*tankR,
		0, 0.8*tankR
	DEL 2

	ADDx A/2-0.4*tankR-(0.15-0.08)*tankL
	ROTy -90
	PRISM 4,0.08*tankL,
		0,-0.8*tankR,
		ZZYZX/2,-0.8*tankR,
		ZZYZX/2, 0.8*tankR,
		0, 0.8*tankR
	DEL 2

	CUTEND

	GROUP "tank1"
		RESOL res
		ADD -A/2,0,ZZYZX/2
		ROTy 90

		ADDz 0.4*tankR
		CYLIND tankL-0.8*tankR,tankR
		MULz -1
		ELLIPS 0.4*tankR,tankR
		DEL 2

		ADDz tankL-0.4*tankR
		ELLIPS 0.4*tankR,tankR
		DEL 3
	ENDGROUP

	minX = -A/2+pipeL1+0.5*tankR
	maxX =  A/2-pipeL1-0.5*tankR

	ADD MAX(MIN(ConPosX_3, maxX-topR+ConWidth_3/2), minX+topR-ConWidth_3/2), 0, ZZYZX/2
	ROTy -90
	IF tankR*2 > ZZYZX-EPS THEN ZZYZX = tankR*2+EPS
	ARMC tankR,topR,ZZYZX/2,0,0,90
	DEL 2


	! Valves
	dR=MIN(tankR-valveR, SQR(2)*tankR/2)

	RESOL 8
	ADD A/2-0.4*tankR,0,ZZYZX/2

	ROTy 90
	ROTz 45
	ARME 0.5*tankR+1.5*valveR, tankR, valveR, 0.4*tankR, dR
	ADD 0,dR,0.5*tankR+1.5*valveR
	CYLIND valveR,valveR/2
	ADDz valveR
	CYLIND valveR/5,2*valveR
	DEL 2
	CUTPOLYA 2,1,0,
		0,dR,913,
		valveR,360,4013
	DEL 1
	ROTz -45
	ARME 0.5*tankR+1.5*valveR, tankR, valveR, 0.4*tankR, dR
	ADD 0,dR,0.5*tankR+1.5*valveR
	CYLIND valveR,valveR/2
	ADDz valveR
	CYLIND valveR/5,2*valveR
	DEL 2
	CUTPOLYA 2,1,0,
		0,dR,913,
		valveR,360,4013
	DEL 2

	ADD 0.5*tankR,tankR/2,-0.5*tankR-1.1*valveR
	CYLIND tankR+2.2*valveR,1.1*valveR
	DEL 1

	CUTEND
	CUTEND
	DEL 1

	PLACEGROUP ADDGROUP ("Connections1", "tank1")
	KILLGROUP "Connections1"
	KILLGROUP "tank1"
ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple
	GROUP "Connections2"
		RESOL 6

		ADD ConPosX_1,0,ConPosZ_1
		REVOLVE 2,360,1+2+4+16+32,
			0,ConWidth_1/2, 2,
			pipeL1+A/4,ConWidth_1/2, 2
		DEL 1

		ADD ConPosX_2,0,ConPosZ_2
		MULx -1
		REVOLVE 2,360,1+2+4+16+32,
			0,ConWidth_2/2, 2,
			pipeL2+A/4,ConWidth_2/2, 2
		DEL 2
	ENDGROUP

	RESOL res

	ADD -A/2+pipeL1,0,ZZYZX/2
	ROTy 90
	CUTPOLYA 2,1,0,
		0,0,913,
		tankR,360,4013
	DEL 2

	ADDx -A/2+0.4*tankR+0.15*tankL
	ROTy -90
	PRISM 4,0.08*tankL,
		0,-0.8*tankR,
		ZZYZX/2,-0.8*tankR,
		ZZYZX/2, 0.8*tankR,
		0, 0.8*tankR
	DEL 2

	ADDx A/2-0.4*tankR-(0.15-0.08)*tankL
	ROTy -90
	PRISM 4,0.08*tankL,
		0,-0.8*tankR,
		ZZYZX/2,-0.8*tankR,
		ZZYZX/2, 0.8*tankR,
		0, 0.8*tankR
	DEL 2

	CUTEND

	GROUP "tank2"
		RESOL res
		ADD -A/2,0,ZZYZX/2
		ROTy 90

		ADDz 0.4*tankR
		CYLIND tankL-0.8*tankR,tankR
		MULz -1
		ELLIPS 0.4*tankR,tankR
		DEL 2

		ADDz tankL-0.4*tankR
		ELLIPS 0.4*tankR,tankR
		DEL 3
	ENDGROUP

	minX = -A/2+pipeL1+0.5*tankR
	maxX =  A/2-pipeL1-0.5*tankR

	ADD MAX(MIN(ConPosX_3, maxX-topR+ConWidth_3/2), minX+topR-ConWidth_3/2), 0, ZZYZX/2
	ROTy -90
	ARMC tankR,topR,ZZYZX/2,0,0,90
	DEL 2


	! Valves
	dR=SQR(2)*tankR/2

	RESOL 6
	ADD A/2-0.4*tankR,0,ZZYZX/2

	ROTy 90
	ROTz 45
	ARME 0.5*tankR+2.5*valveR, tankR, valveR, 0.4*tankR, dR
	ADD 0,dR,0.5*tankR+1.5*valveR
	ADDz valveR
	CYLIND valveR/5,2*valveR
	DEL 2
	CUTPOLYA 2,1,0,
		0,dR,913,
		valveR,360,4013
	DEL 1
	ROTz -45
	ARME 0.5*tankR+2.5*valveR, tankR, valveR, 0.4*tankR, dR
	ADD 0,dR,0.5*tankR+1.5*valveR
	ADDz valveR
	CYLIND valveR/5,2*valveR
	DEL 2
	CUTPOLYA 2,1,0,
		0,dR,913,
		valveR,360,4013
	DEL 2

	ADD 0.5*tankR,tankR/2,-0.5*tankR-1.1*valveR
	ROTz 90
	CYLIND tankR+2.2*valveR,1.1*valveR
	DEL 2

	CUTEND
	CUTEND
	DEL 1

	PLACEGROUP ADDGROUP ("Connections2", "tank2")
	KILLGROUP "Connections2"
	KILLGROUP "tank2"
ENDIF

				! --- CONNECTIONS --- !

DEL TOP
RESOL res

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 1,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 12,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 12
END
