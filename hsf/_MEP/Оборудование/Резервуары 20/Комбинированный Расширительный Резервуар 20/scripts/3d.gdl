
! Combination Expansion Tank


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, mainboxHeight-0.045*hh, unID: unID=unID+1
HOTSPOT 0, 0, -dTHeight-0.045*hh, unID: unID=unID+1

endRed = 0
IF gs_detlevel_3D_m = 2 THEN
	endRed = 0.025*hhh
	IF endRed > A/2 THEN endRed = A/2
ENDIF

HOTSPOT 0, -B/2, -0.045*hh-endRed, unID: unID=unID+1
HOTSPOT 0, B/2, -0.045*hh-endRed, unID: unID=unID+1
HOTSPOT -A/2, 0, -0.045*hh-endRed, unID: unID=unID+1
HOTSPOT A/2, 0, -0.045*hh-endRed, unID: unID=unID+1

HOTSPOT 0, -B/2, -dTHeight-0.045*hh+endRed, unID: unID=unID+1
HOTSPOT 0, B/2, -dTHeight-0.045*hh+endRed, unID: unID=unID+1
HOTSPOT -A/2, 0, -dTHeight-0.045*hh+endRed, unID: unID=unID+1
HOTSPOT A/2, 0, -dTHeight-0.045*hh+endRed, unID: unID=unID+1

HOTSPOT -mainboxWidth/2, -mainboxDepth/2, -0.03*hh, unID: unID=unID+1
HOTSPOT mainboxWidth/2, -mainboxDepth/2, -0.03*hh, unID: unID=unID+1
HOTSPOT mainboxWidth/2, mainboxDepth/2, -0.03*hh, unID: unID=unID+1
HOTSPOT -mainboxWidth/2, mainboxDepth/2, -0.03*hh, unID: unID=unID+1


				! --- MAIN BOX --- !

ADD -mainboxWidth/2, -mainboxDepth/2, -0.03*hh
FPRISM_ MainBoxMaterial, MainBoxMaterial,
		MainBoxMaterial, MainBoxMaterial,
		4, 0.08*hh, 45, 0.02*hh,
		0, 0, 15,
		mainboxWidth, 0, 15,
		mainboxWidth, mainboxDepth, 15,
		0, mainboxDepth, 15
DEL 1

ADDz 0.05*hh
IF gs_detlevel_3D_m = 2 THEN
	CYLIND 0.015*hh, 0.0175*ww
	ADDz 0.015*hh
	CYLIND 0.11*hh, 0.02*ww
	DEL 1
ELSE
	CYLIND 0.125*hh, 0.02*ww
ENDIF
DEL 1

GROUP "cylind"
	ADDz -0.045*hh-endRed
	CYLIND 0.015*hh+endRed, 0.02*ww
	DEL 1
ENDGROUP


				! --- TANK --- !

GROUP "tank"
	ADDz -dTHeight-0.045*hh
	IF gs_detlevel_3D_m = 2 THEN
		CALL "MEP_m_Tank" PARAMETERS	tankLen = dTHeight, tankDia = A, endRed = endRed,
										ringOn = 0, typ = 2, tankPos = 1, tankMat = MainBoxMaterial,
										gs_cont_pen = gs_cont_pen, res = res
	ELSE
		CYLIND dTHeight, A/2
	ENDIF
	DEL 1
ENDGROUP

result_1=ADDGROUP("cylind","tank")
PLACEGROUP result_1

KILLGROUP "cylind"
KILLGROUP "tank"


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 1
END
