!-----------3d---------------------
if show_el < EPS and GLOB_VIEW_TYPE<>2 then end
if GLOB_VIEW_TYPE=2 then gs_detlevel_3D_m = gs_detlevel_2D_m
IF gs_detlevel_3D_m < EPS THEN END
if GLOB_PREVIEW_MODE = 0 and GLOB_FEEDBACK_MODE = 0 then CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
MainBoxMaterial = sMat
MATERIAL sMat
PEN gs_cont_pen
SHADOW ON : if gs_detlevel_3D_m =1 THEN SHADOW OFF
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1

HOTSPOT 0, -B/2, endRed, unID: unID=unID+1
HOTSPOT 0, B/2, endRed, unID: unID=unID+1
HOTSPOT -A/2, 0, endRed, unID: unID=unID+1
HOTSPOT A/2, 0, endRed, unID: unID=unID+1

HOTSPOT 0, -B/2, ZZYZX-endRed, unID: unID=unID+1
HOTSPOT 0, B/2, ZZYZX-endRed, unID: unID=unID+1
HOTSPOT -A/2, 0, ZZYZX-endRed, unID: unID=unID+1
HOTSPOT A/2, 0, ZZYZX-endRed, unID: unID=unID+1


				! --- TANK --- !

IF gs_detlevel_3D_m = 2 THEN
	GROUP "cylind"
		ADDz ZZYZX-endRed
		EXTRUDE 2, 0, 0, endRed, 1+2+16,
			0, 0, 901,
			ConWidth_1/2, 360, 4001
		DEL 1
	ENDGROUP

	GROUP "tank"
		CALL "MEP_m_Tank" PARAMETERS	tankLen = ZZYZX, tankDia = A, endRed = endRed,
										ringOn = 0, typ = 2, tankPos = 1, tankMat = MainBoxMaterial,
										gs_cont_pen = gs_cont_pen, res = res
	ENDGROUP

	result_1=ADDGROUP("cylind","tank")
	PLACEGROUP result_1

	KILLGROUP "cylind"
	KILLGROUP "tank"
ELSE
	CYLIND ZZYZX, A/2
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, ConEdgeVisible_1 = 0, MEP_StrConnectionData = MEP_StrConnectionData, mep_con = mep_con, mep_data = mep_data
END
