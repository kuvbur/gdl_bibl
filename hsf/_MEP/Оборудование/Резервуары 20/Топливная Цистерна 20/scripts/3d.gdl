
! Fuel Tank


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT -dTWidth/2, -dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1
HOTSPOT dTWidth/2, -dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1
HOTSPOT dTWidth/2, dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1
HOTSPOT -dTWidth/2, dTDepth/2, legHeight+dTDepth/2, unID: unID=unID+1

HOTSPOT -dTWidth/2, 0, legHeight+dTDepth, unID: unID=unID+1
HOTSPOT dTWidth/2, 0, legHeight+dTDepth, unID: unID=unID+1
HOTSPOT dTWidth/2, 0, legHeight, unID: unID=unID+1
HOTSPOT -dTWidth/2, 0, legHeight, unID: unID=unID+1


				! --- TANK --- !

ADD -dTWidth/2, 0, legHeight+dTDepth/2
ROTy 90
CYLIND dTWidth, dTDepth/2
CUTPOLYA 2, 2, 0,
	0, 0, 913,
	dTDepth/2, 360, 4013
DEL 2

IF gs_detlevel_3D_m = 2 THEN
	ADDz legHeight+dTDepth/2
		CYLIND dTDepth/2+upperOpeningHeight-openingCapHeight, openingRadius
	ADDz dTDepth/2+upperOpeningHeight-openingCapHeight
		CYLIND openingCapHeight, openingRadius*1.2
	DEL 2
ELSE
	ADDz legHeight+dTDepth/2
		CYLIND dTDepth/2+upperOpeningHeight, openingRadius
	DEL 1
ENDIF


IF Constatus_1 THEN
	ADD ConPosX_1, 0, legHeight+dTDepth/2
	EXTRUDE 2, 0, 0, dTDepth/2, 16,
		0, 0, 901,
		ConWidth_1/2, 360, 4001
	DEL 1
ENDIF

IF Constatus_2 THEN
	ADD ConPosX_2, 0, legHeight+dTDepth/2
	EXTRUDE 2, 0, 0, dTDepth/2, 16,
		0, 0, 901,
		ConWidth_2/2, 360, 4001
	DEL 1
ENDIF

IF Constatus_3 THEN
	ADD ConPosX_3, 0, legHeight+dTDepth/2
	EXTRUDE 2, 0, 0, dTDepth/2, 16,
		0, 0, 901,
		ConWidth_3/2, 360, 4001
	DEL 1
ENDIF

IF Constatus_4 THEN
	ADD ConPosX_4, 0, legHeight+dTDepth/2
	EXTRUDE 2, 0, 0, dTDepth/2, 16,
		0, 0, 901,
		ConWidth_4/2, 360, 4001
	DEL 1
ENDIF

IF Constatus_5 THEN
	ADD ConPosX_5, 0, legHeight+dTDepth/2
	EXTRUDE 2, 0, 0, dTDepth/2, 16,
		0, 0, 901,
		ConWidth_5/2, 360, 4001
	DEL 1
ENDIF

IF Constatus_6 THEN
	ADD ConPosX_6, 0, legHeight+dTDepth/2
	EXTRUDE 2, 0, 0, dTDepth/2, 16,
		0, 0, 901,
		ConWidth_6/2, 360, 4001
	DEL 1
ENDIF

IF Constatus_7 THEN
	ADD ConPosX_7, 0, legHeight+dTDepth/2
	EXTRUDE 2, 0, 0, dTDepth/2, 16,
		0, 0, 901,
		ConWidth_7/2, 360, 4001
	DEL 1
ENDIF

CUTEND

IF gs_detlevel_3D_m = 2 THEN
	RESOL res/2
	ADD -dTWidth/2, 0, legHeight
	TUBE 2, 5, 1+2+16+32,
		0, 0, 901,
		sideTubeRadius, 360, 4001,

		1, 0, 0.075, 0,
		0, 0, 0.075, 0,
		-sideTubeDist, 0, 0.075, 0,
		-sideTubeDist, 0, dTDepth, 0,
		-sideTubeDist, 0, dTDepth+1, 0
	DEL 1

	ADD -dTWidth/2, 0, legHeight+dTDepth-0.095
	ROTy -90
		CYLIND sideTubeDist, sideTubeRadius
	DEL 2
	RESOL res
ELSE
	ADD -dTWidth/2-sideTubeDist-sideTubeRadius, -sideTubeRadius, legHeight+0.075-sideTubeRadius
		BLOCK sideTubeDist+sideTubeRadius, sideTubeRadius*2, dTDepth-0.075+sideTubeRadius
	DEL 1
ENDIF


				! --- SUPPORT --- !

num = INT((dTWidth-0.30*ww-0.15*dd)/1.20)
IF num < EPS THEN num = 1
IF num > 3 THEN num = 3

FOR p = 1 TO num+1
	ADDx -dTWidth/2+0.15*ww+((dTWidth-0.30*ww-0.15*dd)/(num))*(p-1)
	ROTz 90
	CALL "MEP_m_Tank_support_4" PARAMETERS	supLen = 0.15*dd, supHeight = legHeight+dTDepth/4, supThick = 0.035,
											footWidth = dTDepth+0.08*dd, footThick = 0.035,
											tankDia = dTDepth, tankDeepening = dTDepth/4,
											gs_detlevel_3D_m = gs_detlevel_3D_m, supMat = MainBoxMaterial,
											gs_cont_pen = gs_cont_pen, res = res, hptsID = 200*p
	DEL 2
NEXT p


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 12, ConEdgeVisible_1 = 0,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 12, ConEdgeVisible_2 = 0,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 12, ConEdgeVisible_3 = 0,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 12, ConEdgeVisible_4 = 0,
	ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConPosZ_5=ConPosZ_5, ConLength_5=ConLength_5, ConEdit_5 = 12, ConEdgeVisible_5 = 0,
	ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConPosZ_6=ConPosZ_6, ConLength_6=ConLength_6, ConEdit_6 = 12, ConEdgeVisible_6 = 0,
	ConPosX_7=ConPosX_7, ConPosY_7=ConPosY_7, ConPosZ_7=ConPosZ_7, ConLength_7=ConLength_7, ConEdit_7 = 12, ConEdgeVisible_7 = 0
END
