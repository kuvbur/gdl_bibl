
! Single Package Heat Pump


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen


unID = 1
IF mbShow THEN

					! --- HOTSPOTS --- !

	HOTSPOT A/2, 	B/2, 	0, unID: unID=unID+1
	HOTSPOT A/2, 	-B/2, 0, unID: unID=unID+1
	HOTSPOT -A/2, -B/2, 0, unID: unID=unID+1
	HOTSPOT -A/2, B/2, 	0, unID: unID=unID+1
	HOTSPOT A/2, 	B/2, 	ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT A/2, 	-B/2, ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT -A/2, -B/2, ZZYZX-grillHeight, unID: unID=unID+1
	HOTSPOT -A/2, B/2, 	ZZYZX-grillHeight, unID: unID=unID+1


					! --- MAIN BOX --- !

	ADD -A/2, -B/2, 0
		BLOCK A, B, ZZYZX-grillHeight
	DEL 1


					! --- GRILL --- !

	dist = MIN(A/2, B/2)
	dist2 = MAX(A, B)

	ww = 0: dd = 0
	IF (dist2-A)<EPS THEN ww = 1
	IF (dist2-B)<EPS THEN dd = 1

	FOR k=1 TO 1+((dist2-dist*2)>grillDia*1.05)
		ADD -A/2+dist+(dist2-dist*2)*(k-1)*ww, B/2-dist-(dist2-dist*2)*(k-1)*dd, ZZYZX-grillHeight

		IF gs_detlevel_3D_m = 2 THEN
			GOSUB 500
		ELSE
			ADD -grillDia/2, -grillDia/2, 0
			BLOCK grillDia, grillDia, grillHeight
			DEL 1
		ENDIF

		DEL 1
	NEXT k
ENDIF

RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 17,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 17
END


500:
	 SegmetAng = 360/SegmentsNum
	 SegmetHalfAng = SegmetAng/2
	 TotalInsideRad = Rad-HalfThick/TAN(SegmetHalfAng)-Thick
	 DivisionInsideDist = (TotalInsideRad-Thick*(DivisionNum-1))/DivisionNum

	ADDz grillHeight
	ROTz SegmetHalfAng

	PUT 0, 0, 901
	PUT Rad, 360, 4001

	alpha = 0
	FOR i = 1 TO SegmentsNum
		InsideDist = DivisionInsideDist
		CenterPointPosX = (HalfThick/SIN(SegmetHalfAng))*COS(alpha)
		CenterPointPosY = (HalfThick/SIN(SegmetHalfAng))*SIN(alpha)
		FOR j = 1 TO DivisionNum
			InsideDist = InsideDist+Thick*(j>1)
			PointOnArcEndPosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)-(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
			PointOnArcEnd2PosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)+(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
			PointOnArcEndPosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)+(InsideDist*SIN(SegmetHalfAng))*COS(alpha)
			PointOnArcEnd2PosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)-(InsideDist*SIN(SegmetHalfAng))*COS(alpha)

			IF j = 1 THEN
				PUT CenterPointPosX, CenterPointPosY, 1
				PUT PointOnArcEndPosX, PointOnArcEndPosY, 1
				PUT 0, 0, 901
				PUT PointOnArcEnd2PosX, PointOnArcEnd2PosY, 3001
				PUT CenterPointPosX, CenterPointPosY, -1
			ELSE
				InsideDist = InsideDist+DivisionInsideDist
				OPointOnArcEndPosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)-(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
				OPointOnArcEnd2PosX = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*COS(alpha)+(InsideDist*SIN(SegmetHalfAng))*SIN(alpha)
				OPointOnArcEndPosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)+(InsideDist*SIN(SegmetHalfAng))*COS(alpha)
				OPointOnArcEnd2PosY = (HalfThick/SIN(SegmetHalfAng)+InsideDist*COS(SegmetHalfAng))*SIN(alpha)-(InsideDist*SIN(SegmetHalfAng))*COS(alpha)

				PUT PointOnArcEndPosX, PointOnArcEndPosY, 1
				PUT 0, 0, 901
				PUT PointOnArcEnd2PosX, PointOnArcEnd2PosY, 3001
				PUT OPointOnArcEnd2PosX, OPointOnArcEnd2PosY, 1
				PUT 0, 0, 901
				PUT OPointOnArcEndPosX, OPointOnArcEndPosY, 3001
				PUT PointOnArcEndPosX, PointOnArcEndPosY, -1
			ENDIF
		NEXT j
		 alpha = alpha+SegmetAng
	NEXT i


	POLY_ NSP/3,

		GET(NSP)

	DEL 2

	FOR i = 1 TO SegmentsNum/2
		ROTz (360/SegmentsNum)*i
		ROTx 90
		PUT grillDia/2, 0, 1
		PUT grillDia/2, grillHeight-0.001, 1
		PUT -grillDia/2, grillHeight-0.001, 1
		PUT -grillDia/2, 0, 1
		PUT -grillDia/2+Thick, 0, 1
		PUT -grillDia/2+Thick, grillHeight-0.001-Thick, 1
		PUT grillDia/2-Thick, grillHeight-0.001-Thick, 1
		PUT grillDia/2-Thick, 0, 1

		POLY_ NSP/3,

			GET(NSP)
		DEL 2
	NEXT i
RETURN
