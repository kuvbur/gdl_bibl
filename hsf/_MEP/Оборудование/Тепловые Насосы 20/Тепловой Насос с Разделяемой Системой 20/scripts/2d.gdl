
! Split System Heat Pump



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
HOTSPOT2 0, 0, unID: unID=unID+1

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top


				! --- MAIN BOX --- !

	ADD2 -A/2, -B/2
	HOTSPOT2 cornerCh, 0, unID: unID=unID+1
	HOTSPOT2 A-cornerCh, 0, unID: unID=unID+1
	HOTSPOT2 A, cornerCh, unID: unID=unID+1
	HOTSPOT2 A, B-cornerCh, unID: unID=unID+1
	HOTSPOT2 A-cornerCh, B, unID: unID=unID+1
	HOTSPOT2 cornerCh, B, unID: unID=unID+1
	HOTSPOT2 0, B-cornerCh, unID: unID=unID+1
	HOTSPOT2 0, cornerCh, unID: unID=unID+1

	PUT cornerCh, 0, 1,
		A-cornerCh, 0, 1,
		A, cornerCh, 1,
		A, B-cornerCh, 1,
		A-cornerCh, B, 1,
		cornerCh, B, 1,
		0, B-cornerCh, 1,
		0, cornerCh, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	 dist = edgeThick/TAN(67.5)
	PUT cornerCh+dist, edgeThick, 1,
		A-cornerCh-dist, edgeThick, 1,
		A-edgeThick, cornerCh+dist, 1,
		A-edgeThick, B-cornerCh-dist, 1,
		A-cornerCh-dist, B-edgeThick, 1,
		cornerCh+dist, B-edgeThick, 1,
		edgeThick, B-cornerCh-dist, 1,
		edgeThick, cornerCh+dist, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	 dist = (edgeThick+upperHeight)/TAN(67.5)
	PUT cornerCh+dist, edgeThick+upperHeight, 1,
		A-cornerCh-dist, edgeThick+upperHeight, 1,
		A-edgeThick-upperHeight, cornerCh+dist, 1,
		A-edgeThick-upperHeight, B-cornerCh-dist, 1,
		A-cornerCh-dist, B-edgeThick-upperHeight, 1,
		cornerCh+dist, B-edgeThick-upperHeight, 1,
		edgeThick+upperHeight, B-cornerCh-dist, 1,
		edgeThick+upperHeight, cornerCh+dist, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)
	DEL 1


				! --- GRILL --- !

	ADD2 0, (B/2-grillDia/2)-edgeThick-upperHeight
	CIRCLE2 0, 0, grillDia/2

	 Thick = 0.01
	 HalfThick = Thick/2
	 alpha_i = 360/SegmentsNum

	CIRCLE2 0, 0, Rad
	alpha = 0
	FOR i = 1 TO SegmentsNum
		PUT (SQR((insideDia/2)^2-HalfThick^2))*COS(alpha)+HalfThick*SIN(alpha), (SQR((insideDia/2)^2-HalfThick^2))*SIN(alpha)-HalfThick*COS(alpha), 1+3000*(i>1)
		PUT (SQR(Rad^2-HalfThick^2))*COS(alpha)+HalfThick*SIN(alpha), (SQR(Rad^2-HalfThick^2))*SIN(alpha)-HalfThick*COS(alpha), 1
		PUT (SQR(Rad^2-HalfThick^2))*COS(alpha)-HalfThick*SIN(alpha), (SQR(Rad^2-HalfThick^2))*SIN(alpha)+HalfThick*COS(alpha), 1
		PUT (SQR((insideDia/2)^2-HalfThick^2))*COS(alpha)-HalfThick*SIN(alpha), (SQR((insideDia/2)^2-HalfThick^2))*SIN(alpha)+HalfThick*COS(alpha), 1
		PUT 0, 0, 901

		alpha = alpha+alpha_i
	NEXT i
	PUT (SQR((insideDia/2)^2-HalfThick^2))*COS(alpha)+HalfThick*SIN(alpha), (SQR((insideDia/2)^2-HalfThick^2))*SIN(alpha)-HalfThick*COS(alpha), 3001

	POLY2_ NSP/3, 1,

		GET(NSP)

	DEL 1

	SET FILL gs_fill_type

	PEN gs_cont_pen
	IF gs_fill_pen THEN PEN gs_fill_pen
	PROJECT2{3} 3, 270, 3+32, 4, gs_back_pen, 0, 0, 0, PARAMETERS gs_detlevel_3D_m = 1, mbShow = 0
	PEN gs_cont_pen
	PROJECT2{3} 3, 270, 2, 8, 0, 0, 0, 0, PARAMETERS gs_detlevel_3D_m = 1, mbShow = 0
ENDIF


				! --- OTHER VIEWS --- !

ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1,  ConShow_1 = 0, ConEdit_1 = 13,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2,  ConShow_2 = 0, ConEdit_2 = 13
DEL 1

END
