!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Cyclone Dust Collector


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT -A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT -A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1

IF NOT(SetProgram) THEN
	unID = 10
	HOTSPOT -A/2+legWidth/2, -B/2+legWidth/2, 0,				unID, 	supportHeight, 1+128: unID=unID+1
	HOTSPOT -A/2+legWidth/2, -B/2+legWidth/2, -1,				unID, 	supportHeight, 3: unID=unID+1
	HOTSPOT -A/2+legWidth/2, -B/2+legWidth/2, supportHeight, 	unID, 	supportHeight, 2: unID=unID+1

	HOTSPOT -A/2+legWidth/2, B/2-legWidth/2, 0,					unID, 	supportHeight, 1+128: unID=unID+1
	HOTSPOT -A/2+legWidth/2, B/2-legWidth/2, -1,				unID, 	supportHeight, 3: unID=unID+1
	HOTSPOT -A/2+legWidth/2, B/2-legWidth/2, supportHeight, 	unID, 	supportHeight, 2: unID=unID+1

	HOTSPOT A/2-legWidth/2, B/2-legWidth/2, 0,					unID, 	supportHeight, 1+128: unID=unID+1
	HOTSPOT A/2-legWidth/2, B/2-legWidth/2, -1,					unID, 	supportHeight, 3: unID=unID+1
	HOTSPOT A/2-legWidth/2, B/2-legWidth/2, supportHeight, 		unID, 	supportHeight, 2: unID=unID+1

	HOTSPOT A/2-legWidth/2, -B/2+legWidth/2, 0,					unID, 	supportHeight, 1+128: unID=unID+1
	HOTSPOT A/2-legWidth/2, -B/2+legWidth/2, -1,				unID, 	supportHeight, 3: unID=unID+1
	HOTSPOT A/2-legWidth/2, -B/2+legWidth/2, supportHeight, 	unID, 	supportHeight, 2: unID=unID+1
ELSE
	HOTSPOT -A/2+legWidth/2, -B/2+legWidth/2, supportHeight, unID: unID=unID+1
	HOTSPOT -A/2+legWidth/2, B/2-legWidth/2, supportHeight, unID: unID=unID+1
	HOTSPOT A/2-legWidth/2, B/2-legWidth/2, supportHeight, unID: unID=unID+1
	HOTSPOT A/2-legWidth/2, -B/2+legWidth/2, supportHeight, unID: unID=unID+1
ENDIF


				! --- MAIN BOX --- !

IF gs_detlevel_3D_m = 2 THEN
	GROUP "lower_legs"
		ADD -A/2, -B/2, 0
		BLOCK legWidth, legWidth, 1.30*supScale-legWidth
		ADDx A-legWidth
		BLOCK legWidth, legWidth, 1.30*supScale-legWidth
		ADDy B-legWidth
		BLOCK legWidth, legWidth, 1.30*supScale-legWidth
		ADDx -A+legWidth
		BLOCK legWidth, legWidth, 1.30*supScale-legWidth
		DEL 4

		ADD -A/2+legWidth/2, -B/2+legWidth/2, 1.30*supScale-legWidth
		BLOCK legWidth, legWidth, supportHeight-1.30*supScale+legWidth
		ADDx A-legWidth*2
		BLOCK legWidth, legWidth, supportHeight-1.30*supScale+legWidth
		ADDy B-legWidth*2
		BLOCK legWidth, legWidth, supportHeight-1.30*supScale+legWidth
		ADDx -A+legWidth*2
		BLOCK legWidth, legWidth, supportHeight-1.30*supScale+legWidth
		DEL 4
	ENDGROUP

	GROUP "lower_frame"
		ADDz 1.30*supScale-legWidth
		PRISM_ 10, legWidth,
			-A/2, -B/2, 15,
			A/2, -B/2, 15,
			A/2, B/2, 15,
			-A/2, B/2, 15,
			-A/2, -B/2, -1,
			-A/2+legWidth, -B/2+legWidth, 15,
			A/2-legWidth, -B/2+legWidth, 15,
			A/2-legWidth, B/2-legWidth, 15,
			-A/2+legWidth, B/2-legWidth, 15,
			-A/2+legWidth, -B/2+legWidth, -1
		DEL 1

		ADDz supportHeight-legWidth
		PRISM_ 10, legWidth,
			-A/2+legWidth/2, -B/2+legWidth/2, 15,
			A/2-legWidth/2, -B/2+legWidth/2, 15,
			A/2-legWidth/2, B/2-legWidth/2, 15,
			-A/2+legWidth/2, B/2-legWidth/2, 15,
			-A/2+legWidth/2, -B/2+legWidth/2, -1,
			-A/2+legWidth*1.5, -B/2+legWidth*1.5, 15,
			A/2-legWidth*1.5, -B/2+legWidth*1.5, 15,
			A/2-legWidth*1.5, B/2-legWidth*1.5, 15,
			-A/2+legWidth*1.5, B/2-legWidth*1.5, 15,
			-A/2+legWidth*1.5, -B/2+legWidth*1.5, -1
		DEL 1
	ENDGROUP

	result = ADDGROUP("lower_legs","lower_frame")
	PLACEGROUP result

	KILLGROUP "lower_legs"
	KILLGROUP "lower_frame"

	CYLIND 0.85*supScale, A/2-legWidth-0.10
	ADDz 0.90*supScale
	CYLIND 0.30*supScale, 0.125*wScale
	ADDz 0.30*supScale
	CONE 1.20*supScale, 0.125*wScale, A/2-legWidth*1.5-0.02, 90, 90
	DEL 2

	ADDz supportHeight-legWidth
	CYLIND ZZYZX-supportHeight-0.30-(ZZYZX-ConPosZ_1-0.15)+legWidth, A/2-legWidth*1.5-0.02
	DEL 1
ELSE
	ADD -A/2, -B/2, 0
	BLOCK A, B, 1.30*supScale
	DEL 1
	ADD -A/2+legWidth/2, -B/2+legWidth/2, 1.30*supScale
	BLOCK A-legWidth, B-legWidth, 1.20*supScale
	DEL 1
	ADDz supportHeight
	CYLIND ZZYZX-supportHeight-0.30-(ZZYZX-ConPosZ_1-0.15), A/2-legWidth*1.5-0.02
	DEL 1
ENDIF

ADDz ConPosZ_1+0.15
CYLIND ZZYZX-ConPosZ_1-0.15, 0.15*wScale
DEL 1

 rr = A/2-legWidth
ADDz ConPosZ_1-0.15 !ZZYZX-0.80
PRISM_ 4, 0.30,
	A/2-legWidth/2, -rr, 15,
	0, -rr, 79,
	SQR(rr^2-(rr-0.30)^2), -(rr-0.30), 1015,
	A/2-legWidth/2, -(rr-0.30), 15
DEL 1

CUTPOLYA 2, 2, 0,
	0, 0, 915,
	A/2-legWidth*1.5-0.02, 360, 4015

ADD -A/2+legWidth/2, -(A/2-legWidth*1.5-0.02), ConPosZ_2-0.15
BLOCK A/2-legWidth/2, 0.30, 0.30
DEL 1

CUTEND


ConWidth_1 = ConWidth_1+ConWallThickness_1*2
ConDepth_1 = ConDepth_1+ConWallThickness_1*2

ADD A/2-legWidth/2, -A/2+legWidth+0.15, ConPosZ_1
DIM StraightPosition[3][3]
	StraightPosition[1][1] = 0
	StraightPosition[1][2] = 0
	StraightPosition[1][3] = 0

	StraightPosition[2][1] = 1
	StraightPosition[2][2] = 0
	StraightPosition[2][3] = 0

	StraightPosition[3][1] = 0
	StraightPosition[3][2] = 1
	StraightPosition[3][3] = 0

DIM ConnectionData[2][26]
	FOR i = 1 TO 2
		FOR j = 1 TO 26
			ConnectionData[i][j] = 0
		NEXT j
	NEXT i

	ConnectionData[1][12] = 0.30
	ConnectionData[1][13] = 0.30
	ConnectionData[1][21] = 1

	ConnectionData[2][12] = ConWidth_1
	ConnectionData[2][13] = ConDepth_1
	ConnectionData[2][21] = MEP_NumConnectionData[1][21]

	CALL "MEP_m_Duct_4" PARAMETERS 	MEP_Position = StraightPosition,
									MEP_NumberConnections = 2,
									MEP_NumConnectionData = ConnectionData,
									MEP_StraightLength = 0.2875,
									duct_show = 1,
									showHtps = 0,
									gs_DuctMat = MainBoxMaterial,
									MEP_InsShow = 0,
									MEP_cline_show_3D = 0,
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_cont_pen = gs_cont_pen
DEL 1


ConWidth_2 = ConWidth_2+ConWallThickness_2*2
ConDepth_2 = ConDepth_2+ConWallThickness_2*2

ADD -A/2+legWidth/2, -(A/2-legWidth*1.5-0.02)+0.15, ConPosZ_2
ROTz 180

	ConnectionData[2][12] = ConWidth_2
	ConnectionData[2][13] = ConDepth_2
	ConnectionData[2][21] = MEP_NumConnectionData[2][21]

	CALL "MEP_m_Duct_4" PARAMETERS 	MEP_Position = StraightPosition,
									MEP_NumberConnections = 2,
									MEP_NumConnectionData = ConnectionData,
									MEP_StraightLength = 0.2875,
									duct_show = 1,
									showHtps = 0,
									gs_DuctMat = MainBoxMaterial,
									MEP_InsShow = 0,
									MEP_cline_show_3D = 0,
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_cont_pen = gs_cont_pen
DEL 2


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 4,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 4, MEP_StrConnectionData = MEP_StrConnectionData
END
