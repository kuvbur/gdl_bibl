!-----------3d---------------------
if show_el < EPS then end
if show_op<EPS then end
if GLOB_VIEW_TYPE=2 then gs_detlevel_3D_m = gs_detlevel_2D_m
if (GLOB_PREVIEW_MODE or GLOB_FEEDBACK_MODE or GLOB_SEO_TOOL_MODE) then gs_detlevel_3D_m = 1
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
shadow off
if show_axis then
	pen MEP_cline_pen
else
	pen gs_cont_pen
endif
if not(mun_dn) then material sMat

resol res
GROUP "Спутник"
	if op_type_txt = "Б" then 
		gosub "sputnik"
		del 1
	endif
ENDGROUP


GROUP "Труба"
	CYLIND op_h,op_R
ENDGROUP

GROUP "Опора"
	add -op_L-op_X,-op_BB/2,0
	put 0, 0, 15
	put 0, op_vc, 15
	put op_L-op_vc, op_h, 15
	put op_L, op_h, 15
	put op_L, 0, 15
	ROTx 90
	PRISM_ NSP/3, op_S, use(NSP)
	DEL 1
	op_Y=op_B-op_S
	ADDy op_Y
	ROTx 90
	PRISM_ NSP/3, op_S, get(NSP)
	DEL 2
	ADDy -2*op_S
	BLOCK op_L, op_B+2*op_S, op_S
	DEL 1
ENDGROUP

osn = SUBGROUP ("Опора","Труба")

RESULT = addgroup("Спутник", osn)


if SetProgram = 1 then
	dZx = -MEP_Position[3][2]*MEP_Position[2][3]+MEP_Position[3][3]*MEP_Position[2][2]
	dZy = -MEP_Position[3][3]*MEP_Position[2][1]+MEP_Position[3][1]*MEP_Position[2][3]
	dZz = -MEP_Position[3][1]*MEP_Position[2][2]+MEP_Position[3][2]*MEP_Position[2][1]
	
	XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
			MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
			MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]
	roty 90
	rotz 90
endif
if op_reverse then rotx 180

hotspot 0,0,0
PLACEGROUP RESULT
rotz 180
PLACEGROUP RESULT
!del 1
del top

end
"sputnik":
	ADDz -(op_lp-op_h)/2
	sc = 15
	if NSP>0 then tt = max(get(NSP))
	resol res*5
	sin_half_ang = (0.5*op_B1)/(op_r+op_rs)
	ang = ASN(sin_half_ang)
	point_1_x = (op_r+op_rs)*cos(ang)
	point_1_y = (op_r+op_rs)*sin(ang)
	point_2_x = (op_r+op_rs)*cos(-ang)
	point_2_y = (op_r+op_rs)*sin(-ang)
	point_3_x = op_r*cos(-ang)
	point_3_y = op_r*sin(-ang)
	point_4_x = op_r*cos(ang)
	point_4_y = op_r*sin(ang)
	put point_1_x, point_1_y, 13
	put 0, 0, 900
	put point_2_x, point_2_y, 3000+sc
	put point_3_x, point_3_y, 13
	put 0, 0, 900
	put point_4_x, point_4_y, 3000+sc
	put 0, 0, 700
	if NSP>0 then PRISM_ NSP/3, op_lp, GET(NSP)
	resol res
return