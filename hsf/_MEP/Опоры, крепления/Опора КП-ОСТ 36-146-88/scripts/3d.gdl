!-----------3d---------------------
if show_el < EPS then end
if show_op<EPS then end
if GLOB_VIEW_TYPE=2 then gs_detlevel_3D_m = gs_detlevel_2D_m
if (GLOB_PREVIEW_MODE or GLOB_FEEDBACK_MODE or GLOB_SEO_TOOL_MODE) then gs_detlevel_3D_m = 1
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
shadow off
if show_axis then
	pen MEP_cline_pen
else
	pen gs_cont_pen
endif
resol res
if not(mun_dn) then material sMat

HOTSPOT 0,0,0

GROUP "Хомут"
	if op_subtype = "КХ" then
		homut_r = op_r
		homut_t = op_lp
		homut_t1 = op_B1/2 - op_r
		if (op_type_num = "11" or op_type_num = "21") and op_DN<108/1000 then
			addx op_L/2-op_lp/2
			gosub "homut"
		else
			gosub "homut"
			addx op_L - op_lp
			gosub "homut"
		endif
	endif
ENDGROUP

GROUP "Спутник"
	if op_type_txt = "Б" then
		gosub "sputnik"
		addx op_L - op_lp
		gosub "sputnik"
		del 1
	endif
ENDGROUP

GROUP "Основная опора"
	if op_type_num = "11" or op_type_num = "21" then
		BLOCK op_L, op_S, op_h1
		ADDy op_B-op_S
		BLOCK op_L, op_S, op_h1
		DEL 1
		BLOCK op_L, op_B, op_S
		ADDx op_L/2-op_S
		ADDy op_S
		BLOCK op_S, op_B-2*op_S, op_h2
		DEL 2
	endif
	
	if op_type_num = "12" or op_type_num = "22" then
		BLOCK op_L,op_S,op_h1
		ADDy op_B-op_S
		BLOCK op_L,op_S,op_h1
		DEL 1
		BLOCK op_L,op_B,op_S
		ADDy op_S
		ADDx op_S
		BLOCK op_S,op_B-2*op_S,op_h1
		DEL 2
		ADDx op_L-2*op_S
		ADDy op_S
		BLOCK op_S,op_B-2*op_S,op_h1
		DEL 2
	endif

	if op_type_num = "13" or op_type_num = "23" then
		BLOCK op_L,op_S,op_h1
		ADDy op_B-op_S
		BLOCK op_L,op_S,op_h1
		DEL 1
		BLOCK op_L,op_B,op_S
		ADDy op_S
		ADDx op_S
		BLOCK op_S,op_B-2*op_S,op_h1
		DEL 2
		ADDx op_L-2*op_S
		ADDy op_S
		BLOCK op_S,op_B-2*op_S,op_h1
		DEL 2
		ADDx op_L/2-op_S
		ADDy op_S
		BLOCK op_S, op_B-2*op_S, op_h2
		DEL 2
	endif
ENDGROUP

GROUP "Труба"
	ADDy op_B/2
	ADDz op_h+op_r+dz
	ROTy 90
	CYLIND op_L,op_r
	del 3
ENDGROUP



osn = SUBGROUP ("Основная опора","Труба")
RESULT = addgroup("Спутник", osn)

dirAngOnXYPlane = 0
if SetProgram = 1 then
	dZx = -MEP_Position[3][2]*MEP_Position[2][3]+MEP_Position[3][3]*MEP_Position[2][2]
	dZy = -MEP_Position[3][3]*MEP_Position[2][1]+MEP_Position[3][1]*MEP_Position[2][3]
	dZz = -MEP_Position[3][1]*MEP_Position[2][2]+MEP_Position[3][2]*MEP_Position[2][1]
	
	XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
			MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
			MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]
	rotx 90
	IF ABS(dZz-1) < EPS THEN
		 arCosVal = MEP_Position[3][1]/1
		IF ABS(arCosVal) > 1 AND SGN(arCosVal) = -1 THEN arCosVal = -1
		IF ABS(arCosVal) > 1 AND SGN(arCosVal) = 1 THEN arCosVal = 1
		dirAngOnXYPlane = ACS(arCosVal)-90
		IF SGN(MEP_Position[3][2]) = -1 THEN dirAngOnXYPlane = 180-dirAngOnXYPlane
	ENDIF
endif

call "2dmacro" parameters all, type_symb = type_symb, dirAngOnXYPlane = dirAngOnXYPlane
if show_axis then 
	hotspot 0,0,0
	end
endif

ADDx -op_L/2
ADDy -op_B/2
ADDz -op_h-op_r
PLACEGROUP RESULT
PLACEGROUP "Хомут"

KILLGROUP osn
KILLGROUP RESULT
KILLGROUP "Спутник"
KILLGROUP "Основная опора"
KILLGROUP "Труба"

end
"homut":
	resol res+12
	ADDy op_B/2
	ADDz op_h+op_r+dz
	roty 90
	rotz 90
	put -homut_r-homut_t1, 0, 1
	put -homut_r, 0, 1
	put 0, 0, 900
	put 0, homut_r, 3001
	put 0, 0, 900
	put homut_r, 0, 3001
	put homut_r+homut_t1, 0, 1
	EXTRUDE NSP/3, 0,0,homut_t, 16+64+32, GET(NSP)
	
	put -homut_r-homut_t1, 0, 1
	put -homut_r, 0, 1
	put 0, 0, 900
	put 0, -homut_r, 3001
	put 0, 0, 900
	put homut_r, 0, 3001
	put homut_r+homut_t1, 0, 1
	EXTRUDE NSP/3, 0,0,homut_t, 16+64+32, GET(NSP)
	del 4
	resol res
return

"sputnik":
	ADDy op_B/2
	ADDz op_h+op_r+dz
	roty 90
	sc = 15
	if NSP>0 then tt = max(get(NSP))
	resol res+12
	sin_half_ang = (0.5*op_B1)/(op_r+op_rs)
	ang = ASN(sin_half_ang)
	point_1_x = (op_r+op_rs)*cos(ang)
	point_1_y = (op_r+op_rs)*sin(ang)
	point_2_x = (op_r+op_rs)*cos(-ang)
	point_2_y = (op_r+op_rs)*sin(-ang)
	point_3_x = op_r*cos(-ang)
	point_3_y = op_r*sin(-ang)
	point_4_x = op_r*cos(ang)
	point_4_y = op_r*sin(ang)
	put point_1_x, point_1_y, 15
	put 0, 0, 900
	put point_2_x, point_2_y, 3000+sc
	put point_3_x, point_3_y, 15
	put 0, 0, 900
	put point_4_x, point_4_y, 3000+sc
	put 0, 0, 700
	if NSP>0 then PRISM_ NSP/3, op_lp, GET(NSP)
	del 3
	resol res
return
