!!!!============================= ТИПЫ ЭЛЕМЕНТОВ ===============================
MEP_TYPE_TUBE = 1
MEP_TYPE_PEREH = 2
MEP_TYPE_TR = 3
MEP_TYPE_OTVOD = 4
MEP_TYPE_FLANGE = 5
MEP_TYPE_OP = 6
MEP_TYPE_EQONTUBE = 7

EPS = 0.00001
unIDD=1

!-----------Основной---------------------
if (GLOB_SCRIPT_TYPE = 2 or GLOB_SCRIPT_TYPE = 3) and (GLOB_PREVIEW_MODE <> 0 or GLOB_FEEDBACK_MODE <> 0) then
	gs_detlevel_2D_m = 1
	gs_detlevel_3D_m = 1
	show_axis = 0
	show_axis_and = 1
	show_conventions = 0
	show_size = 0
	show_el = 1
	show_isol = 0
	hide_conventions = 0
	show_op = 1
else
	if SetProgram = 0 then
		call "Показ_систем" PARAMETERS All isMep=0, isEqp=1 RETURNED_PARAMETERS show_el
	else
		call "Показ_систем" PARAMETERS All isMep=1, isEqp=1 RETURNED_PARAMETERS show_el, syst_name
	endif
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_op", show_op)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_axis", show_axis)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_axis_and", show_axis_and)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_isol", show_isol)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_conventions", show_conventions)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_size", show_size)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "gs_detlevel_2D_m",gs_detlevel_2D_m)
	success = LIBRARYGLOBAL ("Libraryflobals-mep", "gs_detlevel_3D_m", gs_detlevel_3D_m)
endif
IF gs_detlevel_3D_m = 2 THEN res = 22
IF gs_detlevel_3D_m = 1 THEN res = 12
IF gs_detlevel_3D_m = 0 THEN res = 12

is_sech = 0 : if (show_el-1)>EPS then is_sech = 1
type_symb = 61
n = REQUEST ("Name_of_main", "", name)
is_call = 0 : if name<>"" then is_call = 1
file_name = "Хомут_36-146-88"
is_error = 0 : error_msg = ""

if GLOB_PREVIEW_MODE = 0 then
	otm_elem = SYMB_POS_Z
	txt2 = ""
	aaz = SYMB_POS_Z
	if abs(aaz)<EPS then
		az = "0.000"
		znak1 = ""
	 else
		az = str("%.3m", abs(aaz))
		if aaz < EPS then 
			znak1 = "-"
		else
			znak1 = "+"
		endif
	endif
	parameters otm_niz = znak1 + az, otm_elem = otm_elem 
endif
unID=300
angx = 0
if byotm and type_column=1 then
	if abs(Rotation-90)<EPS then !Вверх
		if otm_elem > homut_otm then angx = 180
	endif
	if abs(Rotation-270)<EPS then !Вниз
		if otm_elem < homut_otm then angx = 180
	endif
	column_l = abs(otm_elem - homut_otm)
endif


flag_custom_dn = 1
flag_custom = 1
if homut_obozn = "ОСТ 36-146-88" then flag_custom = 0

dim tDN[]
ch1 = open("text", file_name, "separator='\t', mode=ro, LIBRARY")
i = 2 : j = 1 : DN = 0 : DN1 = 0
n = 1
while n <> -1 and i<1000 do
	n = input (ch1, i, 1, DN)
	if vartype(DN)=1 then
		DN = DN / 1000
		if abs(DN1-DN)>EPS then
			DN1 = DN
			tDN[j] = DN
			j = j + 1
		endif
	endif
	i = i + 1
endwhile
close ch1

if is_call = 1 then
	dZx = -MEP_Position[3][2]*MEP_Position[2][3]+MEP_Position[3][3]*MEP_Position[2][2]
	dZy = -MEP_Position[3][3]*MEP_Position[2][1]+MEP_Position[3][1]*MEP_Position[2][3]
	dZz = -MEP_Position[3][1]*MEP_Position[2][2]+MEP_Position[3][2]*MEP_Position[2][1]
endif

inx = 0
for i=1 to vardim1(tDN)
	if homut_dn = tDN[i] then
		inx = i+1
		i = vardim1(tDN)
	endif
next i

if inx > EPS and homut_obozn<>"PVC" then flag_custom_dn = 0

if GLOB_MODPAR_NAME="homut_dn" or GLOB_MODPAR_NAME="homut_type" or GLOB_MODPAR_NAME="homut_obozn" or homut_ves<EPS or SetProgram then
	if inx > EPS then
		ch1 = open("text", file_name, "separator='\t', mode=ro, LIBRARY")
		n = input (ch1, inx, 1, DN, r, e, d, l, l1, C, Lab, Lvg, Vab, Vvg)
		if vartype(r)=1 then homut_r = r/1000
		if vartype(e)=1 then homut_e = e/1000
		if vartype(d)=1 then homut_d = d/1000
		if vartype(l)=1 then homut_l = l/1000
		if vartype(l1)=1 then homut_l1 = l1/1000
		if vartype(C)=1 then homut_C = C/1000
		if homut_type = "A" or homut_type = "Б" then
			if vartype(Lab)=1 then homut_dlin = Lab/1000
			if vartype(Vab)=1 then homut_ves = Vab
		else
			if vartype(Lvg)=1 then homut_dlin = Lvg/1000
			if vartype(Vvg)=1 then homut_ves = Vvg
		endif
		close ch1
	else
		homut_r = homut_dn/2 + 2/1000
	endif
endif

if homut_dlin<EPS or flag_custom_dn = 1 or flag_custom = 1 then
!	homut_r = homut_dn/2 + 2/1000
	homut_dlin = 3.15*homut_dn/2+homut_l
	if homut_type = "A" or homut_type = "Б" then
		homut_dlin = homut_dlin + homut_l 
	else
		homut_dlin = homut_dlin + 5/1000
	endif
	homut_ves = homut_dlin * homut_d * homut_d * 3.15 / 4
endif
homut_rhom = homut_d / 2
if homut_obozn =  "ОСТ 36-146-88" then homut_naen = "Хомут для трубы Дн" + str("%.0mm",homut_dn) + " с резьбой М" + str("%.0mm",homut_d) + ", L=" + str("%.0mm",homut_dlin) + "мм."
if homut_obozn =  "PVC" then homut_naen = "Опора для труб PVC-U d" + str("%.0mm",homut_dn) + ", Model " + homut_type 
if SetProgram = 0 then
	naen = homut_naen
	obozn = homut_obozn
	ves = homut_ves
endif
