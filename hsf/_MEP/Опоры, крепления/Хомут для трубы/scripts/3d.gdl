!-----------3d---------------------
if show_el < EPS then end
if show_op<EPS then end
if GLOB_VIEW_TYPE=2 then gs_detlevel_3D_m = gs_detlevel_2D_m
if (GLOB_PREVIEW_MODE or GLOB_FEEDBACK_MODE or GLOB_SEO_TOOL_MODE) then gs_detlevel_3D_m = 1
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen
m2 = "!Краска-15"
if show_axis then
	pen MEP_cline_pen
else
	pen gs_cont_pen
endif
resol res
shadow off
if not(mun_dn) then material sMat

hotspot 0,0,0
hotspot -homut_r,0,0
hotspot 0,0,-homut_r
hotspot homut_r,0,0
hotspot 0,0,homut_r

dirAngOnXYPlane = 0
if SetProgram then
	dZx = -MEP_Position[3][2]*MEP_Position[2][3]+MEP_Position[3][3]*MEP_Position[2][2]
	dZy = -MEP_Position[3][3]*MEP_Position[2][1]+MEP_Position[3][1]*MEP_Position[2][3]
	dZz = -MEP_Position[3][1]*MEP_Position[2][2]+MEP_Position[3][2]*MEP_Position[2][1]
	
	XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
			MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
			MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]
	rotx 90
	rotz 90
	IF ABS(dZz-1) < EPS THEN
		 arCosVal = MEP_Position[3][1]/1
		IF ABS(arCosVal) > 1 AND SGN(arCosVal) = -1 THEN arCosVal = -1
		IF ABS(arCosVal) > 1 AND SGN(arCosVal) = 1 THEN arCosVal = 1
		dirAngOnXYPlane = ACS(arCosVal)-90
		IF SGN(MEP_Position[3][2]) = -1 THEN dirAngOnXYPlane = 180-dirAngOnXYPlane
	ENDIF
endif


call "2dmacro" parameters all, type_symb = type_symb, dirAngOnXYPlane = dirAngOnXYPlane
if show_axis then 
	hotspot 0,0,0
	end
endif

rotx angx

!!call "macro_local_ax" parameters all
!!pen gs_cont_pen

if homut_obozn  = "PVC" then
	dt = 0.01
	t = 0.01
	rotx 90
	addy -homut_r-dt
	addz -t/2
	material sMat
	put -homut_r-dt-0.005, 0, 15
	put -homut_r-dt-0.005, dt, 15
	put -homut_r-0.005, dt, 15
	put -homut_r-0.005, dt+homut_r, 15
	put homut_r+0.005, -180, 15+2000
	put homut_r+0.005, dt+homut_r, 15
	put homut_r+0.005, dt, 15
	put homut_r+0.005+dt, dt, 15
	put homut_r+0.005+dt, 0, 15
	put -homut_r-dt-0.005, 0, -1
	put 0,homut_r+dt,900+15
	put homut_r,360,4000+15
	PRISM_ NSP/3, t, get(NSP)
	del 2
	addz -homut_r-dt
else
	add - homut_r - homut_rhom, 0, 0
	ELBOW homut_r + homut_rhom, 180, homut_rhom
	del 1
	
	add - homut_r - homut_rhom,0,-homut_l+homut_l1
	CYLIND homut_l-homut_l1, homut_rhom
	hotspot 0,0,0
	
	addz -homut_l1
	material m2
	CYLIND homut_l1, homut_rhom
	hotspot 0,0,0
	
	del 2
	rotz 180
	
	if not(mun_dn) then material sMat
	if homut_type = "A" or homut_type = "Б" then
		add - homut_r - homut_rhom,0,-homut_l+homut_l1
		CYLIND homut_l-homut_l1, homut_rhom
		hotspot 0,0,0
		addz -homut_l1
		material m2
		CYLIND homut_l1, homut_rhom
		hotspot 0,0,0
		del 2
	else
		homut_l1 = 5/1000
		add - homut_r - homut_rhom,0,-homut_l1
		CYLIND homut_l1, homut_rhom
		hotspot 0,0,0
		del 1
	endif
	addz -homut_r-dt
endif

b=0.15
if type_column>0 then
	if type_column=1 then
		if not(byotm) then
			HOTSPOT	0,	0,	0,	unID,	column_l,	1+128: unID=unID+1
			HOTSPOT	0,	0,	-column_l,	unID,	column_l,	2: unID=unID+1
			HOTSPOT	0,	0,	1,	unID,	column_l,	3: unID=unID+1
		endif
		if column_l>EPS then
			t_plast = 0.005
			material m2
			put -b/2, -b/2, 15
			put -b/2, b/2, 15
			put b/2, b/2, 15
			put b/2, -b/2, 15
			PRISM_ NSP/3, -column_l, get(NSP)
	
			b= homut_r*2 + 0.1
			put -b/2, -b/2, 15
			put -b/2, b/2, 15
			put b/2, b/2, 15
			put b/2, -b/2, 15
			PRISM_ NSP/3, t_plast, get(NSP)
		endif
	endif
	if type_column=2 then
		if column_l>EPS then
			if op_reverse then mulx -1
			roty 90
			HOTSPOT	b/2,	0,	0,	unID,	column_l,	1+128: unID=unID+1
			HOTSPOT	b/2,	0,	-column_l,	unID,	column_l,	2: unID=unID+1
			HOTSPOT	b/2,	0,	1,	unID,	column_l,	3: unID=unID+1
			addz homut_r*2
			addx b/2
			material m2
			put -b/2, -b/2, 15
			put -b/2, b/2, 15
			put b/2, b/2, 15
			put b/2, -b/2, 15
			PRISM_ NSP/3, -column_l-homut_r*2, get(NSP)
		endif
	endif
endif
