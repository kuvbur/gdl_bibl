
! Tankless Water Heater


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN
	MainBoxMaterial = sMat
	operatorMaterial = sMat
ENDIF
MATERIAL MainBoxMaterial

PEN gs_cont_pen
RESOL res

DEFINE MATERIAL	"Shaft Black Plastic" 20,
	0.147059, 0.135294, 0.103922,
	0.9, 0.6, 0.2, 0, 10, 0,
	1, 0.978103, 0.968383,
	0, 0, 0,
	0, 0, 1,
	0


				! --- WATER HEATER --- !

unID = 1
HOTSPOT -A/2, 0, 0, unID: unID=unID+1
HOTSPOT  A/2, 0, 0, unID: unID=unID+1
HOTSPOT  A/2, -B, 0, unID: unID=unID+1
HOTSPOT -A/2, -B, 0, unID: unID=unID+1

HOTSPOT -A/2, 0, ZZYZX, unID: unID=unID+1
HOTSPOT  A/2, 0, ZZYZX, unID: unID=unID+1
HOTSPOT  A/2, -B, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, -B, ZZYZX, unID: unID=unID+1


IF gs_detlevel_3D_m = 2 THEN	! Detailed
	ROTx 90
	ADDy 0.3
	CUTPOLY 4,
		-0.025,0,
		-0.025,0.02,
		 0.025,0.02,
		 0.025,0
	DEL 2
	EXTRUDE 6, 0,0,ZZYZX, 1+2+16+32,
		-A/2, 0,	0,
		-A/2, -B+cy,	0,
		-A/2+cx, -B,	0,
		 A/2-cx, -B,	0,
		 A/2, -B+cy,	0,
		 A/2, 0,	0
	CUTEND

	ROTx 90
	POLY 4,
		-A/2, 0,
		-A/2, ZZYZX,
		 A/2, ZZYZX,
		 A/2, 0
	DEL 1

	px = 0.35 * A
	ADD 0,-B, 0.10
	ROTx 90

	MATERIAL operatorMaterial
	IF useSysMat THEN MATERIAL sMat

	PRISM_ 10, 0.005,
		-px,	0,		15,
		-px,	0.12,	15,
		 px,	0.12,	15,
		 px,	0,		15,
		 px,	0,		79,
		-px,	0,		-1,
		-0.6*px, 0.06, 900,
		0.018,360,4079,
		0.6*px, 0.06, 900,
		0.018,360,4079

	ADD 0,0.06, 0.005
	CYLIND 0.005,0.02
	ROTz 18
	ADD -0.005, -0.02, 0.005
	BLOCK 0.01, 0.04,0.01
	DEL 3

	ADDz 0.002
	MATERIAL "Shaft Black Plastic"
	IF useSysMat THEN MATERIAL sMat

	POLY_ 2,
		-0.6*px, 0.06, 901,
		0.018,360,4001
	POLY_ 2,
		0.6*px, 0.06, 901,
		0.018,360,4001

	DEL 3
ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple
	PRISM 4, ZZYZX,
		-A/2, 0,
		-A/2, -B,
		 A/2, -B,
		 A/2, 0

	px = 0.35 * A
	ADD 0,-B, 0.10
	ROTx 90

	MATERIAL operatorMaterial
	IF useSysMat THEN MATERIAL sMat

	PRISM 4, 0.005,
		-px,	0,
		-px,	0.12,
		 px,	0.12,
		 px,	0
	DEL 2
ENDIF

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 15,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 15,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 15,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 15
END
