if show_el<EPS then end
IF gs_detlevel_2D_m = 0 THEN END

PEN gs_cont_pen
SET FILL gs_fill_type


unID = 1
DrawingNum = 1
IF DrawingNum = 1 THEN	! Top
	DRAWINDEX 20

				! --- KITCHEN SINK --- !

	HOTSPOT2 0, 0, unID: unID=unID+1
	HOTSPOT2 A, 0, unID: unID=unID+1
	HOTSPOT2 0, B, unID: unID=unID+1
	HOTSPOT2 A, B, unID: unID=unID+1

	IF GLOB_CONTEXT <> 22 THEN
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,0, 1,
			0,B, 1,
			A,B,1,
			A,0,1

		CIRCLE2 ConPosX_3,ConPosY_3, ConWidth_3/2
		CIRCLE2 ConPosX_4,ConPosY_4, ConWidth_4/2

		gx = A/4-3*edgeThick/4
		gy = B-edgeThick-edgeThickTop

		ADD2 A/4+edgeThick/4, edgeThick
		GOSUB 100
		DEL 1

		gx = A/4-3*edgeThick/4
		gy = B-edgeThick-edgeThickTop

		ADD2 3*A/4-edgeThick/4, edgeThick
		GOSUB 100
		DEL 1
	ENDIF
ENDIF
DEL TOP

DRAWINDEX 10

				! --- OTHER VIEWS --- !

ROT2 (SYMB_MIRRORED - NOT(SYMB_MIRRORED)) * (DrawingNum > 1) * SYMB_ROTANGLE

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180 * SYMB_MIRRORED,		 2	! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180 * NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

IF iDirHCWaterConn = 1 THEN
	ConEdit_1 = 15
	ConEdit_2 = 15
ELSE
	ConEdit_1 = 5
	ConEdit_2 = 5

	CIRCLE2 ConPosX_1, ConPosY_1, ConWidth_1/2
	CIRCLE2 ConPosX_2, ConPosY_2, ConWidth_2/2
ENDIF

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 5,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConLength_4=ConLength_4, ConEdit_4 = 5
DEL 1

END

100:
	POLY2_ 9, 5,
		-gx,	gy-R,	1,
		-gx,	R,		1,
		-gx+R,	0,		1001,
		 gx-R,	0,		1,
		 gx, 	R,		1001,
		 gx, 	gy-R,	1,
		 gx-R,	gy,		1001,
		-gx+R,	gy,		1,
		-gx,	gy-R,	1001
RETURN
