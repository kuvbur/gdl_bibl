if show_el<EPS then end
IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

operatedMode = 0
addStatus = 0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	operatedMode = 1
	addStatus = 2
ENDIF


				! --- KITCHEN SINK --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1

gx = A/4-3*edgeThick/4
gy = B-edgeThick-edgeThickTop

ADD A/4+edgeThick/4, edgeThick,0
GOSUB 200
DEL 1

gx = A/4-3*edgeThick/4
gy = B-edgeThick-edgeThickTop

ADD 3*A/4-edgeThick/4, edgeThick,0
GOSUB 200
DEL 1

IF operatedMode THEN
	ADDz -0.001
	PRISM_ 4, 0.001,
		0,0, 15,
		0,B, 15,
		A,B,15,
		A,0,15
	DEL 1
ELSE
	POLY_ 4,
		0,0, 1,
		0,B, 1,
		A,B,1,
		A,0,1
ENDIF

CUTEND
CUTEND

IF gs_detlevel_3D_m = 2 THEN
	CUTPOLYA 2,1,0,
		ConPosX_3,ConPosY_3,915,
		ConWidth_3/2,360,4015
ENDIF

gx = A/4-3*edgeThick/4
gy = B-edgeThick-edgeThickTop

ADD A/4+edgeThick/4, edgeThick,-ZZYZX
GOSUB 100
DEL 1
IF gs_detlevel_3D_m = 2 THEN CUTEND

IF gs_detlevel_3D_m = 2 THEN
	CUTPOLYA 2,1,0,
		ConPosX_4,ConPosY_4,915,
		ConWidth_4/2,360,4015
ENDIF
gx = A/4-3*edgeThick/4
gy = B-edgeThick-edgeThickTop

ADD 3*A/4-edgeThick/4, edgeThick,-ZZYZX
GOSUB 100
DEL 1
IF gs_detlevel_3D_m = 2 THEN CUTEND

ADD A/2, B-0.03, 0
GOSUB 1000
DEL 1

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

IF iDirHCWaterConn = 1 THEN
	ConEdit_1 = 16
	ConEdit_2 = 16
ELSE
	ConEdit_1 = 12
	ConEdit_2 = 12
ENDIF

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 15,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 15
END

100:
	IF gs_detlevel_3D_m = 2 THEN
		EXTRUDE 9, 0,0,ZZYZX,1+addStatus+4+16+32,
			-gx,	gy-R,	1,
			-gx,	R,		1,
			-gx+R,	0,		1001,
			 gx-R,	0,		1,
			 gx, 	R,		1001,
			 gx, 	gy-R,	1,
			 gx-R,	gy,		1001,
			-gx+R,	gy,		1,
			-gx,	gy-R,	1001
	ENDIF
	IF gs_detlevel_3D_m = 1 THEN
		EXTRUDE 4, 0,0,ZZYZX,1+addStatus+4+16+32,
			-gx,	gy,	0,
			-gx,	0,	0,
			 gx,	0,	0,
			 gx, 	gy,	0
	ENDIF
RETURN


200:
	IF gs_detlevel_3D_m = 2 THEN
		CUTPOLYA 9,1,0,
			-gx,	gy-R,	1,
			-gx,	R,		1,
			-gx+R,	0,		1001,
			 gx-R,	0,		1,
			 gx, 	R,		1001,
			 gx, 	gy-R,	1,
			 gx-R,	gy,		1001,
			-gx+R,	gy,		1,
			-gx,	gy-R,	1001
	ENDIF
	IF gs_detlevel_3D_m = 1 THEN
		CUTPOLYA 4,1,0,
			-gx,	gy,	1,
			-gx,	0,	1,
			 gx,	0,	1,
			 gx, 	gy,	1
	ENDIF
RETURN

1000:	! Tap

CYLIND 0.02,0.017
ROTx 90
ROTy -90
ADDz -0.01
PRISM_ 10,0.02,
	0.147986393591, 0.09234439178515,      15,
	0.04117544321573, 0.09234439178515,      79,
	-0.01198261107796, 0.04424275934702,      79,
	-0.01198261107796,         0.02,      15,
	0.01198261107796,         0.02,      15,
	0.01198261107796, 0.04424275934702,      79,
	0.04737063515823, 0.07626465611579,      79,
	0.127986393591, 0.07626465611579,      15,
	0.127986393591, 0.05893770166193,      15,
	0.147986393591, 0.05893770166193,      15
DEL 3

ADDx 0.07
CYLIND 0.02,0.017
ADDz 0.02
ROTz 10
PRISM 4,0.01,
	-0.007, 0.01,
	-0.007,-0.08,
	 0.007,-0.08,
	 0.007, 0.01
DEL 3

ADDx -0.07
CYLIND 0.02,0.017
ADDz 0.02
ROTz -24
PRISM 4,0.01,
	-0.007, 0.01,
	-0.007,-0.08,
	 0.007,-0.08,
	 0.007, 0.01
DEL 3

RETURN
