if show_el<EPS then end
IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- SERVICE SINK --- !

unID = 1
HOTSPOT -A/2, 0, ZZYZX-sinkDepth, unID: unID=unID+1
HOTSPOT  A/2, 0, ZZYZX-sinkDepth, unID: unID=unID+1

HOTSPOT -A/2, 0, 0, unID: unID=unID+1
HOTSPOT  A/2, 0, 0, unID: unID=unID+1
HOTSPOT -A/2, -B, 0, unID: unID=unID+1
HOTSPOT  A/2, -B, 0, unID: unID=unID+1

HOTSPOT -A/2, 0, -sinkDepth, unID: unID=unID+1
HOTSPOT  A/2, 0, -sinkDepth, unID: unID=unID+1
HOTSPOT -A/2, -B, -sinkDepth, unID: unID=unID+1
HOTSPOT  A/2, -B, -sinkDepth, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN	! Detailed

	ROTx 90
	PRISM 4,backThick,
		-A/2+edgeThick, -sinkDepth,
		-A/2+edgeThick, ZZYZX-sinkDepth,
		 A/2-edgeThick, ZZYZX-sinkDepth,
		 A/2-edgeThick, -sinkDepth
	DEL 1


	PUT	-A/2+edgeThick, 0, 1
	da=360/res
	alpha=270
	FOR i=1 TO res/4+1
		PUT -A/2+edgeThick+R + R*SIN(alpha),
			-B+edgeThick+R - R*COS(alpha), 1
		alpha=alpha+da
	NEXT i
	alpha=180
	FOR i=1 TO res/4+1
		PUT  A/2-edgeThick-R - R*SIN(alpha),
			-B+edgeThick+R + R*COS(alpha), 1
		alpha=alpha+da
	NEXT i
	PUT	 A/2-edgeThick, 0, 1

	CUTPOLYA 2,1,0,
		ConPosX_3,ConPosY_3,915,
		ConWidth_3/2,360,4015

	ADDz -sinkDepth
	POLY_ NSP/3,GET(NSP)
	DEL 1
	CUTEND


	PUT	-A/2+edgeThick,1,0,0,
		-A/2+edgeThick, 0, 0, 0

	da=360/res
	alpha=270
	FOR i=1 TO res/4+1
		PUT -A/2+edgeThick+R + R*SIN(alpha),
			-B+edgeThick+R - R*COS(alpha), 0,0
		alpha=alpha+da
	NEXT i
	alpha=180
	FOR i=1 TO res/4+1
		PUT  A/2-edgeThick-R - R*SIN(alpha),
			-B+edgeThick+R + R*COS(alpha), 0,0
		alpha=alpha+da
	NEXT i

	PUT	 A/2-edgeThick, 0, 0, 0,
		 A/2-edgeThick,1,0,0


	RESOL 8
	TUBE 6, NSP/4, 1+2+16+32,
		0,-sinkDepth, 0,
		0,-edgeThick,	1,
		-edgeThick/2,-edgeThick, 901,
		-edgeThick/2-edgeThick/2*SIN(45),-edgeThick-edgeThick/2*COS(45), 1001,
		-EPS,-edgeThick, 1,
		-EPS,-sinkDepth, 1,
		GET(NSP)

ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple

	ROTx 90
	PRISM 4,backThick,
		-A/2+edgeThick, -sinkDepth,
		-A/2+edgeThick, ZZYZX-sinkDepth,
		 A/2-edgeThick, ZZYZX-sinkDepth,
		 A/2-edgeThick, -sinkDepth
	DEL 1

res2 = 4

	PUT	-A/2+edgeThick, 0, 1
	da=360/res2
	alpha=270
	FOR i=1 TO res2/4+1
		PUT -A/2+edgeThick+R + R*SIN(alpha),
			-B+edgeThick+R - R*COS(alpha), 1
		alpha=alpha+da
	NEXT i
	alpha=180
	FOR i=1 TO res2/4+1
		PUT  A/2-edgeThick-R - R*SIN(alpha),
			-B+edgeThick+R + R*COS(alpha), 1
		alpha=alpha+da
	NEXT i
	PUT	 A/2-edgeThick, 0, 1

	ADDz -sinkDepth
	POLY_ NSP/3,GET(NSP)
	DEL 1


	PUT	-A/2+edgeThick,1,0,0,
		-A/2+edgeThick, 0, 0, 0

	da=360/res2
	alpha=270
	FOR i=1 TO res2/4+1
		PUT -A/2+edgeThick+R + R*SIN(alpha),
			-B+edgeThick+R - R*COS(alpha), 0,0
		alpha=alpha+da
	NEXT i
	alpha=180
	FOR i=1 TO res2/4+1
		PUT  A/2-edgeThick-R - R*SIN(alpha),
			-B+edgeThick+R + R*COS(alpha), 0,0
		alpha=alpha+da
	NEXT i

	PUT	 A/2-edgeThick, 0, 0, 0,
		 A/2-edgeThick,1,0,0

	RESOL 4
	TUBE 6, NSP/4, 1+2+16+32,
		0,-sinkDepth, 0,
		0,-edgeThick,	1,
		-edgeThick/2,-edgeThick, 901,
		-edgeThick/2-edgeThick/2*SIN(45),-edgeThick-edgeThick/2*COS(45), 1001,
		-EPS,-edgeThick, 1,
		-EPS,-sinkDepth, 1,
		GET(NSP)
ENDIF

RESOL res
ADD (ConPosX_1 + ConPosX_2)/2, -0.05, (ConPosZ_1 + ConPosZ_2)/2
GOSUB 1000
DEL 1

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 15
END

1000:	! Tap

tx = ABS(ConPosX_1 - ConPosX_2)/2
bx = 0.006
by = 0.030

FOR i=1 TO 2
	ADDx -tx
	ROTx 90
	CYLIND 0.05,0.015
	ADDz 0.02
	IF tx>0.015 THEN
		ARMC 0.015, 0.015, tx, 0,0,90
	ENDIF
	ADDz 0.035
	ROTz i*15
	PRISM 12,0.007,
		-bx,bx,
		-bx,by,
		 bx,by,
		 bx,bx,
		 by,bx,
		 by,-bx,
		 bx,-bx,
		 bx,-by,
		-bx,-by,
		-bx,-bx,
		-by,-bx,
		-by, bx
	DEL 5

	MULX -1
NEXT i
DEL 2


ADDy -0.02
ROTy -90
ARMC 0.015, 0.015, 0.02, 0,0,90
DEL 1
ROTx 90
ROTy -90
ADDz -0.01
PRISM_ 10,0.02,
	0.147986393591, 0.09234439178515,      15,
	0.04117544321573, 0.09234439178515,      79,
	-0.01198261107796, 0.04424275934702,      79,
	-0.01198261107796,         0.02,      15,
	0.01198261107796,         0.02,      15,
	0.01198261107796, 0.04424275934702,      79,
	0.04737063515823, 0.07626465611579,      79,
	0.127986393591, 0.07626465611579,      15,
	0.127986393591, 0.05893770166193,      15,
	0.147986393591, 0.05893770166193,      15
DEL 4

RETURN
