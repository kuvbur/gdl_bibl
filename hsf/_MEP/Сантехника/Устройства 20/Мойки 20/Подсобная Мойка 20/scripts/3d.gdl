
! Utility Sink


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- SCRUB SINK --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1
HOTSPOT A, B, ZZYZX, unID: unID=unID+1


IF gs_detlevel_3D_m = 2 THEN	! Detailed

	ADD A/2, B/2, ZZYZX-sinkDepth
	FOR i=1 TO 2
		ADD -A/2, -B/2+dx, 0

		ADDy -dx
		ROTy 90
		ROTx 45
		CUTPLANE 180
		DEL 3

		ADD A,-dx,0
		ROTy 90
		ROTx -45
		CUTPLANE
		DEL 3

		EXTRUDE 4,0,-dx,sinkDepth, 1+2+4+16+32,
			0,0, 0,
			0,edgeThick, 0,
			A, edgeThick, 0,
			A, 0, 0
		CUTEND
		CUTEND
		DEL 1

		ADD -A/2+dx, -B/2, 0
		ROTz 90

		ADDy dx
		ROTy 90
		ROTx -45
		CUTPLANE 180
		DEL 3

		ADD B,dx,0
		ROTy 90
		ROTx 45
		CUTPLANE
		DEL 3

		EXTRUDE 4,0,dx,sinkDepth, 1+2+4+16+32,
			0,0, 0,
			0,-edgeThick, 0,
			B, -edgeThick, 0,
			B, 0, 0
		CUTEND
		CUTEND
		DEL 2

		MULy -1
		MULx -1
	NEXT i
	DEL 5


	CUTPOLYA 2,1,0,
		ConPosX_1,ConPosY_1,915,
		ConWidth_1/2,360,4015

	ADDz ZZYZX-sinkDepth
	PRISM 4,edgeThick,
		dx+edgeThick,dx+edgeThick,
		dx+edgeThick,B-dx-edgeThick,
		A-dx-edgeThick,B-dx-edgeThick,
		A-dx-edgeThick,dx+edgeThick
	DEL 1

	CUTEND

ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple
	ADDz ZZYZX-sinkDepth
	PLANE 4,
		dx,dx,0,
		0,0,sinkDepth,
		A,0,sinkDepth,
		A-dx,dx,0
	PLANE 4,
		dx,B-dx,0,
		0,B,sinkDepth,
		A,B,sinkDepth,
		A-dx,B-dx,0
	PLANE 4,
		dx,B-dx,0,
		0,B,sinkDepth,
		0,0,sinkDepth,
		dx,dx,0
	PLANE 4,
		A-dx,B-dx,0,
		A,B,sinkDepth,
		A,0,sinkDepth,
		A-dx,dx,0

	ADDz EPS
	POLY 4,
		dx,dx,
		dx,B-dx,
		A-dx,B-dx,
		A-dx,dx
	DEL 2
ENDIF


ADD dx, dx, 0
BLOCK legThk,legThk,ZZYZX-sinkDepth
ADD legThk,0,legH
BLOCK A-2*(dx+legThk),legThk,legThk
DEL 2

ADD A-dx, B-dx, 0
ROTz 180
BLOCK legThk,legThk,ZZYZX-sinkDepth
ADD legThk,0,legH
BLOCK A-2*(dx+legThk),legThk,legThk
DEL 3

ADD A-dx, dx, 0
ROTz 90
BLOCK legThk,legThk,ZZYZX-sinkDepth
ADD legThk,0,legH
BLOCK B-2*(dx+legThk),legThk,legThk
DEL 3

ADD dx, B-dx, 0
ROTz 270
BLOCK legThk,legThk,ZZYZX-sinkDepth
ADD legThk,0,legH
BLOCK B-2*(dx+legThk),legThk,legThk
DEL 3

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 15
END
