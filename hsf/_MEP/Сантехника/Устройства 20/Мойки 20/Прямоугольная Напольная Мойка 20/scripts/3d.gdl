
! Rectangular Floor Sink


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- FLOOR SINK --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1
HOTSPOT A, B, ZZYZX, unID: unID=unID+1

IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	EXTRUDE 4, 0,0,ZZYZX,1+2+4+16+32,
		0,0, 0,
		0,B, 0,
		A,B,0,
		A,0,0
ELSE
	EXTRUDE 4, 0,0,ZZYZX,4+16+32,
		0,0, 0,
		0,B, 0,
		A,B,0,
		A,0,0

	ADDz ZZYZX
	POLY_ 10,
		0,0, 1,
		0,B, 1,
		A,B,1,
		A,0,1,
		0,0, -1,
		edgeThick, edgeThick, 1,
		edgeThick, B-edgeThick, 1,
		A-edgeThick,B-edgeThick, 1,
		A-edgeThick,edgeThick,1,
		edgeThick, edgeThick, -1
	DEL 1
	ADDz 0.02

	IF gs_detlevel_3D_m = 2 THEN
		CUTPOLYA 2,1,0,
			ConPosX_1,ConPosY_1,915,
			ConWidth_1/2,360,4015
	ENDIF
	EXTRUDE 4, 0,0,ZZYZX-0.02,1+4+16+32,
		edgeThick, edgeThick, 0,
		edgeThick, B-edgeThick, 0,
		A-edgeThick,B-edgeThick, 0,
		A-edgeThick,edgeThick,0
	IF gs_detlevel_3D_m = 2 THEN CUTEND
	DEL 1
ENDIF
DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 15
END
