
! Stacked Washer and Dryer


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- WASHER AND DRYER BODY --- !

unID = 1
IF mbShow THEN

	HOTSPOT -A/2, 0,0, unID: unID=unID+1
	HOTSPOT  A/2, 0,0, unID: unID=unID+1
	HOTSPOT  A/2,-B,0, unID: unID=unID+1
	HOTSPOT -A/2,-B,0, unID: unID=unID+1
	HOTSPOT -A/2, 0,ZZYZX, unID: unID=unID+1
	HOTSPOT  A/2, 0,ZZYZX, unID: unID=unID+1
	HOTSPOT  A/2,-B,ZZYZX, unID: unID=unID+1
	HOTSPOT -A/2,-B,ZZYZX, unID: unID=unID+1

	buttonR = MIN(0.04, A/12)

	IF gs_detlevel_3D_m = 2 THEN	! Detailed
		ADDx A/2
		ROTx 90
		ROTy -90
		PRISM 7,A,
			0,	0,
			B,	0,
			B,	washerHeight,
			sWidth, 	washerHeight,
			dryerDepth, washerHeight + dryerDepth - sWidth,
			dryerDepth,	ZZYZX,
			0,			ZZYZX
		DEL 3

		! --- Washer's door

		RESOL res/2
		R = 0.02
		gx = A/2 - 0.04
		gy = B-sWidth-0.14
		ADD 0,-B+0.04,washerHeight
		PRISM_ 9, 0.01,
			-gx,	gy-R,	79,
			-gx,	0+R,	79,
			-gx+R,	0,		1079,
			 gx-R,	0,		79,
			 gx, 	0+R,	1079,
			 gx, 	gy-R,	79,
			 gx-R,	gy,		1079,
			-gx+R,	gy,		79,
			-gx,	gy-R,	1079
		DEL 1

		! --- Dryer's door

		R = 0.02
		gx = A/2 - 0.04
		gy = ZZYZX-washerHeight-dryerDepth+sWidth-0.18

		ROTx 90
		ADD 0,washerHeight + dryerDepth - sWidth + 0.04, dryerDepth
		PRISM_ 9, 0.01,
			-gx,	gy-R,	79,
			-gx,	0+R,	79,
			-gx+R,	0,		1079,
			 gx-R,	0,		79,
			 gx, 	0+R,	1079,
			 gx, 	gy-R,	79,
			 gx-R,	gy,		1079,
			-gx+R,	gy,		79,
			-gx,	gy-R,	1079

		DEL 2

		! --- Buttons

		RESOL res/2
		ROTx 90
		ADD 0.4*A, ZZYZX-0.06, dryerDepth
		CYLIND 0.03,buttonR
		DEL 1
		ADD -0.4*A, ZZYZX-0.06, dryerDepth
		CYLIND 0.03,buttonR
		DEL 1
		ADD -0.2*A, ZZYZX-0.06, dryerDepth
		CYLIND 0.03,buttonR
		DEL 2
	ENDIF

	IF gs_detlevel_3D_m = 1 THEN	! Simple
		ADDx A/2
		ROTx 90
		ROTy -90
		PRISM 7,A,
			0,	0,
			B,	0,
			B,	washerHeight,
			sWidth,		washerHeight,
			dryerDepth, washerHeight + dryerDepth - sWidth,
			dryerDepth,	ZZYZX,
			0,			ZZYZX
		DEL 3
	ENDIF

ENDIF


				! --- CONNECTIONS --- !

DEL TOP
RESOL res

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 16
END
