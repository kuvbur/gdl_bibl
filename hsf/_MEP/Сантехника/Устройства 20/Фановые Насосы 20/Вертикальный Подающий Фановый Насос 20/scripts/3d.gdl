
! Vert. Disch. Sewage Pump


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- SEWAGE PUMP --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT B/2, 0, 0, unID: unID=unID+1
HOTSPOT 0, B/2, 0, unID: unID=unID+1
HOTSPOT 0, -B/2, 0, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1

footH = ConPosZ_1-bottomThk
footTck = 0.05 * B


IF gs_detlevel_3D_m = 2 THEN	! Detailed

	RESOL 16

	ADDz ConPosZ_1 - bottomThk/2
	ROTy -90
	REVOLVE 3,360,1+2+4+16+32,
		-bottomThk/2, (B-bottomThk)/2, 1,
		0,1, 801,
		bottomThk/2, (B-bottomThk)/2, 1001
	DEL 2

	ADD ConPosX_1, 0, ConPosZ_1 - bottomThk/2
	ROTy -90
	MULz 2
	ADDz -B/4+ConWidth_1/3
	REVOLVE 4,180,1+2+4+16+32,
		-bottomThk/3, MAX(0.001, B/4-bottomThk/2), 1,
		0, 1, 801,
		0, B/4, 1001,
		bottomThk/2, B/4, 2
	DEL 4

	ADDz ConPosZ_1 - bottomThk/2
	ROTy -90
	REVOLVE 4,360,1+2+16+32,
		bottomThk/2, motorLowerD/2, 2,
		bottomThk/2+motorMiddleZ, motorLowerD/2, 2,
		bottomThk/2+2*motorMiddleZ, motorUpperD/2, 2,
		ZZYZX-ConPosZ_1 + bottomThk/2, motorUpperD/2, 2
	DEL 2


	IF footH > 0.001 THEN
		TOLER 0.05

		ROTy -90
		FOR i=1 TO 3
			ADDz (B-bottomThk)/2 - footTck
			REVOLVE 3,360, 1+2+16+32,
				footH, footTck, 1,
				0.1 * footH, 0.5*footTck, 1,
				0, 0.001, 1001
			DEL 1
			ROTx 120
		NEXT i
		DEL 4
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple

		ADDz ConPosZ_1 - bottomThk/2
		ROTy -90
		REVOLVE 3,360,1+2+4+16+32,
			-bottomThk/2, (B-bottomThk)/2, 1,
			0,1, 801,
			bottomThk/2, (B-bottomThk)/2, 1001
		DEL 2

		ADD ConPosX_1, 0, ConPosZ_1 - bottomThk/2
		ROTy -90
		MULz 2
		ADDz -B/4+ConWidth_1/3
		REVOLVE 4,180,1+2+4+16+32,
			-bottomThk/3, MAX(0.001, B/4-bottomThk/2), 1,
			0, 1, 801,
			0, B/4, 1001,
			bottomThk/2, B/4, 2
		DEL 4


	ADDz ConPosZ_1 - bottomThk/2
	ROTy -90
	REVOLVE 2,360,1+2+16+32,
		bottomThk/2, motorLowerD/2, 2,
		ZZYZX-ConPosZ_1 + bottomThk/2, motorUpperD/2, 2
	DEL 2

	IF footH > 0 THEN
		TOLER 0.2

		ROTy -90
		FOR i=1 TO 3
			ADDz (B-bottomThk)/2 - footTck
			REVOLVE 2,360, 1+2+16+32,
				footH, footTck, 1,
				0 * footH, 0.5*footTck, 1
			DEL 1
			ROTx 120
		NEXT i
		DEL 4
	ENDIF
ENDIF
DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1
END
