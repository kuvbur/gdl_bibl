!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Threaded Suction Diffuser


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- THREADED SUCTION DIFFUSER --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A+ConPosX_2,0, 0, unID: unID=unID+1

ADDx ConPosX_2
REVOLVE 2,360,1+2+4+8+16+32,
	0, ZZYZX/2, 2,
	A, ZZYZX/2, 2
DEL 1

ROTy -90
ARMC ZZYZX/2, ConWidth_1/2, ConPosZ_1+0.0004, 0, 0, 90
DEL 1

py = 0.3*(ZZYZX/2)
pX = 1.3*(ZZYZX/2)

ROTz 90
ROTx 90

CUTPOLYA 2,1,0,
	0,0, 913,
	ZZYZX/2,360,4013

ADDz ConPosX_2+0.75*A
MULy (ZZYZX*0.65)/B
PRISM_  2, 0.15*A,
		0, 0, 979,
		B/2, 360, 4079
CUTEND
DEL 4


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 12, MEP_StrConnectionData = MEP_StrConnectionData
END
