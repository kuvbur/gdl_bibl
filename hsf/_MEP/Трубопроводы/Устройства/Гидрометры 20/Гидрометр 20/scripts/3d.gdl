!-----------3d---------------------
if show_el < EPS then end
if GLOB_VIEW_TYPE=2 then gs_detlevel_3D_m = gs_detlevel_2D_m
if (GLOB_PREVIEW_MODE or GLOB_FEEDBACK_MODE or GLOB_SEO_TOOL_MODE and gs_detlevel_3D_m > EPS) then gs_detlevel_3D_m = 1
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Water Meter


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple
IF GLOB_CONTEXT = 2 THEN res = 24

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


						! ---------------------- !
						! --- object rotator --- !
						! ---------------------- !

IF rot_conn THEN
	dist_x = A/2
	dist_z = ZZYZX - ConWidth_1/2

	CALL "MEP_m_object_rotator" PARAMETERS 	rot_conn = rot_conn,
											MEP_Position = MEP_Position,
											dist_x = dist_x,
											dist_y = dist_z,
											dist_z = dist_z,
											angleDiffFromDefaultStatus = 90,
											Rotation = Rotation,
											symbRotangle = SYMB_ROTANGLE,
											gs_Connections = gs_Connections,
											MEP_NumberConnections = MEP_NumberConnections,
											MEP_NumConnectionData = MEP_NumConnectionData,
											defaultMatrix = defaultMatrix
ENDIF


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


			! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

HOTSPOT 0, 0, -ConWidth_1/2, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX-ConWidth_1/2, unID: unID=unID+1


				! --- MAIN BOX --- !

ROTy 90
CUTPOLYA 2, 2, 0,
	0, 0, 913,
	ConWidth_1/2, 360, 4013
DEL 1

IF boxDia < ConWidth_1+EPS THEN
	CYLIND ZZYZX-ConWidth_1/2, boxDia/2
ELSE
	ADDz -ConWidth_1/2
		CYLIND ZZYZX, boxDia/2
	DEL 1
ENDIF
CUTEND

DIM StraightPosition[][]

StraightPosition[1][1] = -A/2
StraightPosition[1][2] = 0
StraightPosition[1][3] = 0

StraightPosition[2][1] = 1
StraightPosition[2][2] = 0
StraightPosition[2][3] = 0

StraightPosition[3][1] = 0
StraightPosition[3][2] = 1
StraightPosition[3][3] = 0

CALL "MEP_m_Pipe_4" PARAMETERS 	MEP_Position = StraightPosition,
								MEP_NumberConnections = MEP_NumberConnections,
								MEP_NumConnectionData = MEP_NumConnectionData,
								MEP_StrConnectionData = MEP_StrConnectionData,
								MEP_StraightLength = A,
								gs_AddConnections = gs_AddConnections,
								pipe_show = 1,
								gs_PipeMat = MainBoxMaterial,
								MEP_InsShow = 0,
								MEP_cline_show_3D = 0,
								gs_detlevel_3D_m = gs_detlevel_3D_m,
								gs_cont_pen = gs_cont_pen

DEL 1

END
