!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el

PEN gs_cont_pen
SET FILL gs_fill_type

HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100

unID = 1
IF ABS(dZz-1) < EPS THEN
	ADD2 MEP_Position[1][1], MEP_Position[1][2]
	ROT2 dirAngOnXYPlane

	HOTSPOT2 0, B, unID: unID=unID+1
	HOTSPOT2 A/2, B-A/2, unID: unID=unID+1
	HOTSPOT2 -A/2, B-A/2, unID: unID=unID+1

	dist = B-A
	rr = (MAX(ZZYZX, ConWidth_1*1.2))/2
	rrr = (rr+ConWidth_1/2)/2
	ww = ConWidth_1/2

	PUT ww, 0, 1
	PUT ww, dist*0.29, 1
	PUT rr, dist*0.29, 1
	PUT rr, dist*0.40, 1
	PUT rrr, dist*0.40, 1
	PUT rrr, dist*0.61, 1
	PUT rr, dist*0.61, 1
	PUT rr, dist*0.72, 1
	PUT ww, dist*0.72, 1
	PUT ww, dist+A/2-SQR((A/2)^2-(ww)^2), 0
	PUT -ww, dist+A/2-SQR((A/2)^2-(ww)^2), 1
	PUT -ww, dist*0.72, 1
	PUT -rr, dist*0.72, 1
	PUT -rr, dist*0.61, 1
	PUT -rrr, dist*0.61, 1
	PUT -rrr, dist*0.40, 1
	PUT -rr, dist*0.40, 1
	PUT -rr, dist*0.29, 1
	PUT -ww, dist*0.29, 1
	PUT -ww, 0, 0

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET(NSP)

	LINE2 ww, dist*0.29, -ww, dist*0.29
	LINE2 rrr, dist*0.40, -rrr, dist*0.40
	LINE2 rrr, dist*0.61, -rrr, dist*0.61
	LINE2 ww, dist*0.72, -ww, dist*0.72

	PUT 0, B-A/2, 901
	PUT A/2, 360, 4001

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET(NSP)

	CIRCLE2 0, B-A/2, (A/2)*0.9

	ADD2 0, B-A/2
	ROT2 -45
	LINE2 0, (A/2)*0.8, -(A/2)*0.25,-(A/2)*0.75
	LINE2 0, (A/2)*0.8, (A/2)*0.25,-(A/2)*0.75
	LINE2 -(A/2)*0.25,-(A/2)*0.75, (A/2)*0.25,-(A/2)*0.75
	DEL 2

				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
			MEP_NumberConnections = MEP_NumberConnections,
			MEP_NumConnectionData = gs_Connections,
			gs_AddConnections = gs_AddConnections,
			DrawingNum = 1,
			MEP_InsShow = 0,
			MEP_cline_show = 0,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type=gs_fill_type,
			gs_fill_pen=gs_fill_pen,
			gs_back_pen=gs_back_pen,
			ConPosX_1 = ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 1, ConEdgeVisible_1 = 0, MEP_StrConnectionData = MEP_StrConnectionData
	DEL 2
ELSE
	HOTSPOT2 outCoords[5][1], outCoords[5][2], unID: unID=unID+1
	HOTSPOT2 outCoords[6][1], outCoords[6][2], unID: unID=unID+1
	HOTSPOT2 outCoords[7][1], outCoords[7][2], unID: unID=unID+1
	HOTSPOT2 outCoords[1][1], outCoords[1][2], hceID_1

	SET FILL gs_fill_type

	PEN gs_cont_pen
	IF gs_fill_pen THEN PEN gs_fill_pen
	PROJECT2{3} 3, 270, 3+32, 4, gs_back_pen, 0, 0, 0, PARAMETERS gs_detlevel_3D_m = 2
	PEN gs_cont_pen
	PROJECT2{3} 3, 270, 2, 8, 0, 0, 0, 0, PARAMETERS gs_detlevel_3D_m = 2
ENDIF

END
