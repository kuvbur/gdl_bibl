!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


				! --- frame of CONNECTION --- !

unID = 1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT	A/2, B-A/2, 0, unID: unID=unID+1
HOTSPOT	-A/2, B-A/2, 0, unID: unID=unID+1

dist = B-A
rr = (MAX(ZZYZX, ConWidth_1*1.2))/2
rrr = (rr+ConWidth_1/2)/2
IF gs_detlevel_3D_m = 2 THEN
	GROUP "1"
		ROTx -90
			CYLIND B-A/2, ConWidth_1/2
		DEL 1
	ENDGROUP

	GROUP "2"
		ADD 0, B-A/2, -ZZYZX/2
			CYLIND ZZYZX, A/2
		DEL 1
		ADDy dist*0.29
		ROTx -90
			CYLIND dist*0.11, rr
		DEL 2
		ADDy dist*0.61
		ROTx -90
			CYLIND dist*0.11, rr
		DEL 2
		ADDy dist*0.40
		ROTx -90
			CYLIND dist*0.21, rrr
		DEL 2
	ENDGROUP

	result_01=ADDGROUP ("2", "1")
	PLACEGROUP result_01

	KILLGROUP "1"
	KILLGROUP "2"
	KILLGROUP result_01
ELSE
	GROUP "3"
		ROTx -90
			CYLIND B-A/2, ConWidth_1/2
		DEL 1
	ENDGROUP

	GROUP "4"
		ADD 0, B-A/2, -ConWidth_1/2
			CYLIND ConWidth_1, A/2
		DEL 1

		ADDy dist*0.29
		ROTx -90
			CYLIND dist*0.43, rr
		DEL 2
	ENDGROUP

	result_02=ADDGROUP ("4", "3")
	PLACEGROUP result_02

	KILLGROUP "4"
	KILLGROUP "3"
	KILLGROUP result_02
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, MEP_StrConnectionData = MEP_StrConnectionData

DEL TOP

END
