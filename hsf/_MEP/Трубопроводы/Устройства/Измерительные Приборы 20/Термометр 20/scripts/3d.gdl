!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


rr = (MAX(ZZYZX, ConWidth_1*1.2))/2
rrr = (rr+ConWidth_1/2)/2

unID = 1
HOTSPOT	A/2, dist, ZZYZX/2, unID: unID=unID+1
HOTSPOT	-A/2, dist, ZZYZX/2, unID: unID=unID+1
HOTSPOT	A/2, dist, -ZZYZX/2, unID: unID=unID+1
HOTSPOT	-A/2, dist, -ZZYZX/2, unID: unID=unID+1
HOTSPOT	A/2, B, ZZYZX/2, unID: unID=unID+1
HOTSPOT	-A/2, B, ZZYZX/2, unID: unID=unID+1
HOTSPOT	A/2, B, -ZZYZX/2, unID: unID=unID+1
HOTSPOT	-A/2, B, -ZZYZX/2, unID: unID=unID+1

IF gs_detlevel_3D_m = 2 THEN
	GROUP "1"
		ROTx -90
			CYLIND B*0.275, ConWidth_1/2
		DEL 1
	ENDGROUP

	GROUP "2"
		ADD -A/2, B*0.275, -ZZYZX/2
			BLOCK A, B*0.725, ZZYZX
		DEL 1

		ADDy dist*0.29
		ROTx -90
			CYLIND dist*0.11, rr
		DEL 2
		ADDy dist*0.61
		ROTx -90
			CYLIND dist*0.11, rr
		DEL 2
		ADDy dist*0.40
		ROTx -90
			CYLIND dist*0.21, rrr
		DEL 2
	ENDGROUP

	result_01=ADDGROUP ("2", "1")
	PLACEGROUP result_01

	KILLGROUP "1"
	KILLGROUP "2"
	KILLGROUP result_01
ELSE
	GROUP "3"
		ROTx -90
			CYLIND B*0.275, ConWidth_1/2
		DEL 1
	ENDGROUP

	GROUP "4"
		ADD -A/2, B*0.275, -ZZYZX/2
			BLOCK A, B*0.725, ZZYZX
		DEL 1

		ADDy dist*0.29
		ROTx -90
			CYLIND dist*0.43, rr
		DEL 2
	ENDGROUP

	result_02=ADDGROUP ("4", "3")
	PLACEGROUP result_02

	KILLGROUP "4"
	KILLGROUP "3"
	KILLGROUP result_02
ENDIF


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = MainBoxMaterial,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 1, MEP_StrConnectionData = MEP_StrConnectionData

DEL TOP

END
