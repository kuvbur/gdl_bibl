
! 3 Way Valve


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple
IF GLOB_CONTEXT = 2 THEN res = 24

IF useSysMat AND MEP_StrConnectionData[1][2] <> "0" THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


						! ---------------------- !
						! --- object rotator --- !
						! ---------------------- !

IF rot_conn THEN
	dist_x = A/2
	dist_z = ConWidth_1/2+actL+boxHeight

	CALL "MEP_m_object_rotator" PARAMETERS 	rot_conn = rot_conn,
											MEP_Position = MEP_Position,
											dist_x = dist_x,
											dist_z = dist_z,
											Rotation = Rotation,
											symbRotangle = SYMB_ROTANGLE,
											gs_Connections = gs_Connections,
											MEP_NumberConnections = MEP_NumberConnections,
											MEP_NumConnectionData = MEP_NumConnectionData,
											defaultMatrix = defaultMatrix
ENDIF


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]

!!hptsL = pipeR+actL

			! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

HOTSPOT 0, 0, ConWidth_1/2+actL+boxHeight, unID: unID=unID+1
HOTSPOT 0, 0, -ConWidth_1/2-ConLength_3, unID: unID=unID+1

IF gs_detlevel_3D_m = 0 THEN	! Wireframe
!!	LIN_ -A/2,0,0, A/2,0,0
!!	LIN_ 0,0,0, 0,0,dTHeight-A/2
!!
!!	LIN_ 0.4*A,0,-0.8*pipeR, 0.4*A,0,0.8*pipeR
!!	LIN_ -0.4*A,0,-0.8*pipeR, -0.4*A,0,0.8*pipeR
!!	LIN_ 0.4*A,0,-0.8*pipeR, -0.4*A,0,0.8*pipeR
!!	LIN_ -0.4*A,0,-0.8*pipeR, 0.4*A,0,0.8*pipeR
!!
!!	IF ConStatus_3 THEN LIN_ 0,0,0, ConPosX_3, ConPosY_3, ConPosZ_3
!!
!!	fa = 0
!!	IF gs_AddConnections[1][1] < 2 THEN fa = 1
!!	IF gs_AddConnections[1][1] = 3 AND ABS(MEP_NumConnectionData[1][20]-2) < EPS THEN fa = 1
!!	IF gs_AddConnections[1][1] = 4 AND ABS(MEP_NumConnectionData[1][20]-2) < EPS THEN fa = 1
!!
!!	fb = 0
!!	IF gs_AddConnections[2][1] < 2 THEN fb = 1
!!	IF gs_AddConnections[2][1] = 3 AND ABS(MEP_NumConnectionData[2][20]-2) < EPS THEN fb = 1
!!	IF gs_AddConnections[2][1] = 4 AND ABS(MEP_NumConnectionData[2][20]-2) < EPS THEN fb = 1
!!
!!	IF fa OR fb THEN
!!		MODEL WIRE
!!		ROTy -90
!!		ADDz A/2
!!		IF fa THEN CIRCLE ConConnectorWidth_1/2
!!		DEL 1
!!		ADDz -A/2
!!		CIRCLE ConConnectorWidth_2/2
!!		DEL 2
!!		MODEL SOLID
!!	ENDIF
ELSE

	DIM StraightPosition[][]

	StraightPosition[1][1] = -A/2
	StraightPosition[1][2] = 0
	StraightPosition[1][3] = 0

	StraightPosition[2][1] = 1
	StraightPosition[2][2] = 0
	StraightPosition[2][3] = 0

	StraightPosition[3][1] = 0
	StraightPosition[3][2] = 1
	StraightPosition[3][3] = 0

	ROTy 180
	CUTPOLYA 2, 2, 0.001,
		0, 0, 913,
		ConWidth_3/2, 360, 4013
	DEL 1

	CALL "MEP_m_Pipe_4" PARAMETERS 	MEP_Position = StraightPosition,
									MEP_NumberConnections = 2,
									MEP_NumConnectionData = MEP_NumConnectionData,
									MEP_StrConnectionData = MEP_StrConnectionData,
									MEP_StraightLength = A,
									gs_AddConnections = gs_AddConnections,
									pipe_show = 1,
									gs_PipeMat = MainBoxMaterial,
									MEP_InsShow = 0,
									MEP_cline_show_3D = 0,
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_cont_pen = gs_cont_pen

	CUTEND

	ROTy 90
	CUTPOLYA 2, 2, 0,
		0, 0, 913,
		ConWidth_1/2, 360, 4013
	DEL 1

	CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
			ui_current_con = ui_current_con,
			MEP_NumberConnections = MEP_NumberConnections,
			MEP_NumConnectionData = gs_Connections,
			gs_AddConnections = gs_AddConnections,
			cShow3D = 1,
			gs_ConMat = MainBoxMaterial,
			MEP_InsShow = 0,
			MEP_cline_show_3D = 0,
			gs_cont_pen = gs_cont_pen,
			ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=0, ConShow_1 = 0, ConHotspotShow_1 = 0,
			ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=0, ConShow_2 = 0, ConHotspotShow_2 = 0,
			ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 1

	CYLIND ConWidth_1/2+0.25*actL, actR
	CUTEND

	ROTy -90
	ADDx ConWidth_1/2
	REVOLVE 6,360,1+2+4+8+16+32,
		0.25*actL, actR, 2,
		0.56*actL, actR/3, 2,
		0.56*actL, 0.8*actR, 2,
		0.75*actL, 0.8*actR, 2,
		0.75*actL, actR/3, 2,
		actL, actR/3, 2

	ADDx actL
	ROTy 90
	PRISM 4, boxHeight,
		-boxLen/2, -boxWidth/2,
		-boxLen/2,  boxWidth/2,
		 boxLen/2,  boxWidth/2,
		 boxLen/2, -boxWidth/2
	DEL 4
ENDIF
DEL 1

END
