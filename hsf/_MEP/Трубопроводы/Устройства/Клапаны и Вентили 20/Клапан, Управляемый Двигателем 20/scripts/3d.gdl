!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

call "2dmacro" parameters all, type_symb = type_symb, dirAngOnXYPlane = dirAngOnXYPlane
if show_axis then 
	hotspot 0,0,0
	end
endif

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


						! ---------------------- !
						! --- object rotator --- !
						! ---------------------- !

IF rot_conn THEN
	dist_x = A/2
	dist_z = ZZYZX-ConWidth_1/2

	CALL "MEP_m_object_rotator" PARAMETERS 	rot_conn = rot_conn,
											MEP_Position = MEP_Position,
											dist_x = dist_x,
											dist_y = dist_z,
											dist_z = dist_z,
											angleDiffFromDefaultStatus = 90,
											Rotation = Rotation,
											symbRotangle = SYMB_ROTANGLE,
											gs_Connections = gs_Connections,
											MEP_NumberConnections = MEP_NumberConnections,
											MEP_NumConnectionData = MEP_NumConnectionData,
											defaultMatrix = defaultMatrix
ENDIF


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


			! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX-ConWidth_1/2, unID: unID=unID+1
HOTSPOT 0, 0, -ConWidth_1/2, unID: unID=unID+1


IF gs_detlevel_3D_m = 0 THEN	! Wireframe
!!	LIN_ 0,-A/2,0, 0,A/2,0
!!	LIN_ 0,0,0, 0,0,ZZYZX-ConWidth_1/2
!!
!!	LIN_ 0, 0.4*A,-0.8*pipeR, 0, 0.4*A,0.8*pipeR
!!	LIN_ 0,-0.4*A,-0.8*pipeR, 0,-0.4*A,0.8*pipeR
!!	LIN_ 0, 0.4*A,-0.8*pipeR, 0,-0.4*A,0.8*pipeR
!!	LIN_ 0,-0.4*A,-0.8*pipeR, 0, 0.4*A,0.8*pipeR
!!
!!	IF ConStatus_3 THEN LIN_ 0,0,ConPosZ_3, ConPosY_3, ConPosX_3, ConPosZ_3
!!
!!	fa = 0
!!	IF gs_AddConnections[1][1] < 2 THEN fa = 1
!!	IF gs_AddConnections[1][1] = 3 AND ABS(MEP_NumConnectionData[1][20]-2) < EPS THEN fa = 1
!!	IF gs_AddConnections[1][1] = 4 AND ABS(MEP_NumConnectionData[1][20]-2) < EPS THEN fa = 1
!!
!!	fb = 0
!!	IF gs_AddConnections[2][1] < 2 THEN fb = 1
!!	IF gs_AddConnections[2][1] = 3 AND ABS(MEP_NumConnectionData[2][20]-2) < EPS THEN fb = 1
!!	IF gs_AddConnections[2][1] = 4 AND ABS(MEP_NumConnectionData[2][20]-2) < EPS THEN fb = 1
!!
!!	IF fa OR fb THEN
!!		MODEL WIRE
!!		ROTx 90
!!		ADDz A/2
!!		IF fa THEN CIRCLE ConConnectorWidth_1/2
!!		DEL 1
!!		ADDz -A/2
!!		CIRCLE ConConnectorWidth_2/2
!!		DEL 2
!!		MODEL SOLID
!!	ENDIF
ELSE

	DIM StraightPosition[][]

	StraightPosition[1][1] = -A/2
	StraightPosition[1][2] = 0
	StraightPosition[1][3] = 0

	StraightPosition[2][1] = 1
	StraightPosition[2][2] = 0
	StraightPosition[2][3] = 0

	StraightPosition[3][1] = 0
	StraightPosition[3][2] = 1
	StraightPosition[3][3] = 0

	CALL "MEP_m_Pipe_4" PARAMETERS 	MEP_Position = StraightPosition,
									MEP_NumberConnections = MEP_NumberConnections,
									MEP_NumConnectionData = MEP_NumConnectionData,
									MEP_StrConnectionData = MEP_StrConnectionData,
									MEP_StraightLength = A,
									gs_AddConnections = gs_AddConnections,
									pipe_show = 1,
									gs_PipeMat = MainBoxMaterial,
									MEP_InsShow = 0,
									MEP_cline_show_3D = 0,
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_cont_pen = gs_cont_pen


	ROTy -90
	ARMC ConWidth_1/2, pipeR*0.85, ConWidth_1/2+0.10*boxH, 0,0, 90
	ADDx ConWidth_1/2+0.10*boxH
	REVOLVE 2,360,1+2+4+8+16+32,
		0, pipeR, 2,
		0.10*boxH, pipeR, 2
	DEL 2

	ROTx 90
	CUTPOLY 4,
		-pipeR, 0,
		-pipeR, ConWidth_1/2+0.45*boxH,
		 pipeR, ConWidth_1/2+0.45*boxH,
		 pipeR, 0
	DEL 1

	alpha = MAX(ATN((0.4*boxH)/(boxLen/2 - pipeR-0.01)), ATN((0.4*boxH)/(boxWidth/2-0.01)))
	alpha = MIN(alpha, 89.99)

	ADDz ConWidth_1/2+0.75*boxH
	MULz -1
	FPRISM_ MainBoxMaterial,MainBoxMaterial,MainBoxMaterial,MainBoxMaterial,
		4, 0.65*boxH, alpha, 0.40*boxH,
		-boxDepth/2, -boxWidth/2, 15,
		-boxDepth/2,  boxWidth/2, 15,
		 boxDepth/2,  boxWidth/2, 15,
		 boxDepth/2, -boxWidth/2, 15
	DEL 1

	dx=0.20*boxH/TAN(85)

	FPRISM_ MainBoxMaterial,MainBoxMaterial,MainBoxMaterial,MainBoxMaterial,
		4, 0.20*boxH, 85, 0.20*boxH,
		-boxDepth/2, -boxWidth/2, 15,
		-boxDepth/2,  boxWidth/2, 15,
		 boxDepth/2,  boxWidth/2, 15,
		 boxDepth/2, -boxWidth/2, 15
	ADDz 0.20*boxH
	FPRISM_ MainBoxMaterial,MainBoxMaterial,MainBoxMaterial,MainBoxMaterial,
		4, 0.05*boxH, 45, 0.05*boxH,
		-boxDepth/2+dx, -boxWidth/2+dx, 15,
		-boxDepth/2+dx,  boxWidth/2-dx, 15,
		 boxDepth/2-dx,  boxWidth/2-dx, 15,
		 boxDepth/2-dx, -boxWidth/2+dx, 15
	DEL 2
	CUTEND

	RESOL 8
	ADDz ConWidth_1/2+0.20*boxH
	CYLIND 0.25*boxH, 0.25*pipeR
	DEL 1
ENDIF
DEL 1

END
