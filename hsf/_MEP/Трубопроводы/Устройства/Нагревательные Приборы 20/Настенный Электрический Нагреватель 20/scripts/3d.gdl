!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Electric Wall Heater


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res

				! --- MAIN BOX --- !

unID = 1
ADD -A/2, -frontTh, -ZZYZX/2+boxZ/2
	HOTSPOT 0, frontTh, ZZYZX, unID: unID=unID+1
	HOTSPOT A, frontTh, ZZYZX, unID: unID=unID+1
	HOTSPOT 0, frontTh, 0, unID: unID=unID+1
	HOTSPOT A, frontTh, 0, unID: unID=unID+1
	HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
	HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
	HOTSPOT 0, 0, 0, unID: unID=unID+1
	HOTSPOT A, 0, 0, unID: unID=unID+1

	BLOCK A, frontTh, ZZYZX

	IF gs_detlevel_3D_m = 2 THEN
		ROTx 90
			sd= 0.020
			nd= MAX (1, INT (boxX/0.15))
			xd= boxX/nd
			zd= boxZ-0.07
			FOR w=0 TO nd-1
				ADD dBox+sd/2+w*xd, dBox+sd/2, 0.0001
					PLANE 4,
						0, 0, 0,
						xd-sd, 0, 0,
						xd-sd, zd, 0,
						0, zd, 0
				DEL 1
			NEXT w
		DEL 1
	ENDIF

	ADD dBox, frontTh, dBox
		HOTSPOT 0, boxY, 0, unID: unID=unID+1
		HOTSPOT boxX, boxY, 0, unID: unID=unID+1
		HOTSPOT 0, boxY, boxZ, unID: unID=unID+1
		HOTSPOT boxX, boxY, boxZ, unID: unID=unID+1
		HOTSPOT 0, 0, 0, unID: unID=unID+1
		HOTSPOT boxX, 0, 0, unID: unID=unID+1
		HOTSPOT 0, 0, boxZ, unID: unID=unID+1
		HOTSPOT boxX, 0, boxZ, unID: unID=unID+1

		BLOCK boxX, boxY, boxZ
	DEL 1
DEL 1

END
