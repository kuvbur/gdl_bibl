!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el

! Steam Unit Heater



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
HOTSPOT2 0, 0, unID: unID=unID+1

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top


				! --- MAIN BOX --- !

	HOTSPOT2 A/2, dTDepth/2, unID: unID=unID+1
	HOTSPOT2 -A/2, dTDepth/2, unID: unID=unID+1
	HOTSPOT2 -A/2, -dTDepth/2, unID: unID=unID+1
	HOTSPOT2 A/2, -dTDepth/2, unID: unID=unID+1
	HOTSPOT2 -holeWidth/2, -dTDepth/2-0.025, unID: unID=unID+1
	HOTSPOT2 holeWidth/2, -dTDepth/2-0.025, unID: unID=unID+1

	PUT A/2, dTDepth/2, 1,
		-A/2, dTDepth/2, 1,
		-A/2, -dTDepth/2, 1,
		A/2, -dTDepth/2, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	PUT holeWidth/2, -dTDepth/2, 1,		! --- Louver ---
		-holeWidth/2, -dTDepth/2, 1,
		-holeWidth/2, -dTDepth/2-0.025, 1,
		holeWidth/2, -dTDepth/2-0.025, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)


	ADD2 0, dTDepth/2
	MUL2 A/0.42, (B-dTDepth-0.025)/0.37

	HOTSPOT2 -0.035, 0.37, unID: unID=unID+1
	HOTSPOT2 0.035, 0.37, unID: unID=unID+1
	HOTSPOT2 -0.12, 0.32, unID: unID=unID+1
	HOTSPOT2 0.12, 0.32, unID: unID=unID+1

	PUT -0.12, 0, 1,		! --- Engine ---
		0.12, 0, 1,
		0.12, 0.32, 1,
		0.085, 0.32, 1,
		0.035, 0.37, 1,
		-0.035, 0.37, 1,
		-0.085, 0.32, 1,
		-0.12, 0.32, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	LINE2 -0.085, 0.32, 0.085, 0.32

	DEL 2
ENDIF


				! --- OTHER VIEWS --- !

ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 5,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 5, MEP_StrConnectionData = MEP_StrConnectionData
DEL 1

END
