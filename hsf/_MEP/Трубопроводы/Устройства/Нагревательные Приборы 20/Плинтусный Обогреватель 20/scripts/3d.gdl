
! Baseboard Heater


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HOTSPOTS --- !

unID = 1
HOTSPOT A/2, B/2, 0, unID: unID=unID+1
HOTSPOT -A/2, B/2, 0, unID: unID=unID+1
HOTSPOT -A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT A/2, B/2, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, B/2, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, -B/2, ZZYZX, unID: unID=unID+1
HOTSPOT A/2, -B/2, ZZYZX, unID: unID=unID+1


				! --- MAIN BOX --- !

IF gs_detlevel_3D_m = 2 THEN
	GROUP "box"
		ROTy -90
		ADD 0, -B/2, -A/2
		PRISM_ 7, A,
			0, B, 15,
			ZZYZX, B, 15,
			ZZYZX, 0.015*dd, 15,
			ZZYZX-0.005*hh, 0.015*dd, 15,
			0.095*hh, 0.005*dd, 15,
			0.095*hh, 0, 15,
			0, 0, 15
		DEL 2
	ENDGROUP

	GROUP "2box"
		ROTy -90
		ADD 0, -B/2, -A/2+0.08
		PRISM_ 6, A-0.16,
			0.095*hh, 0, 15,
			0.095*hh, 0.005*dd, 15,
			0.085*hh, B-0.015*dd, 15,
			0.105*hh, B-0.015*dd, 15,
			0.145*hh, 0.015*dd, 15,
			0.145*hh, 0, 15
		DEL 2

		ADD -A/2+0.08, -B/2-0.015*dd, 0.015*hh
			BLOCK A-0.16, B, 0.03*hh
		DEL 1
	ENDGROUP

	result_=SUBGROUP("box","2box")

	PLACEGROUP result_

	KILLGROUP "box"
	KILLGROUP "2box"
ELSE
	ROTy -90
	ADD 0, -B/2, -A/2
	PRISM_ 5, A,
		0, B, 15,
		ZZYZX, B, 15,
		ZZYZX, 0.015*dd, 15,
		0.095*hh, 0, 15,
		0, 0, 15
	DEL 2
ENDIF

END
