
! Vert Pressure Operated Condensate Pump


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HORIZONTAL PRESSURE OPERATED CONDENSATE PUMP --- !

unID = 1
HOTSPOT -tankR,  0, 0, unID: unID=unID+1
HOTSPOT  0, -tankR, 0, unID: unID=unID+1
HOTSPOT  tankR, 0, 0, unID: unID=unID+1
HOTSPOT  0,  tankR, 0, unID: unID=unID+1

HOTSPOT -tankR,  0, tankL, unID: unID=unID+1
HOTSPOT  0, -tankR, tankL, unID: unID=unID+1
HOTSPOT  tankR, 0, tankL, unID: unID=unID+1
HOTSPOT  0,  tankR, tankL, unID: unID=unID+1


vx=ConPosX_3
vy=ConPosY_3
GOSUB 100
L=MIN(tankR-topR, SQR(vx^2+vy^2))


IF gs_detlevel_3D_m = 2 THEN	! Detailed

	CUTPOLYA 2,1,0,
		0,0, 913,
		tankR,360,4013

	RESOL 10

	ADD ConPosX_1,0,ConPosZ_1
	REVOLVE 7,360,1+2+4+16+32,
		0,ConWidth_1/2, 2,
		0,pipeR1, 2,
		0.45*pipeL1,pipeR1, 2,
		0.45*pipeL1,ConWidth_1/2, 2,
		pipeL1,ConWidth_1/2, 2,
		pipeL1,pipeR1, 2,
		ABS(ConPosX_1),pipeR1, 2
	DEL 1

	ADD ConPosX_2,0,ConPosZ_2
	MULx -1
	REVOLVE 7,360,1+2+4+16+32,
		0,ConWidth_2/2, 2,
		0,pipeR2, 2,
		0.45*pipeL2,pipeR2, 2,
		0.45*pipeL2,ConWidth_2/2, 2,
		pipeL2,ConWidth_2/2, 2,
		pipeL2,pipeR2, 2,
		ABS(ConPosX_2),pipeR2, 2
	DEL 2


	! Valves
	dR=ZZYZX/3

	RESOL 8
	ROTz -90
	ADDz pipeLowH + (tankL-pipeLowH)/2
	ROTy 90

	ADDx dR
	CYLIND 1.15*tankR+1.5*valveR, valveR
	ADDz 1.15*tankR+1.5*valveR
	CYLIND valveR,valveR/2
	ADDz valveR
	CYLIND valveR/5,2*valveR
	CUTPOLYA 2,1,0,
		0,0,913,
		valveR,360,4013
	DEL 3

	ADDx -dR
	CYLIND 1.15*tankR+1.5*valveR, valveR
	ADDz 1.15*tankR+1.5*valveR
	CYLIND valveR,valveR/2
	ADDz valveR
	CYLIND valveR/5,2*valveR
	CUTPOLYA 2,1,0,
		0,0,913,
		valveR,360,4013
	DEL 3

	DEL 1

	ADD 1.15*tankR,0,-dR-1.1*valveR
	CYLIND 2*dR+2.2*valveR,1.1*valveR
	DEL 1

	CUTEND
	CUTEND
	DEL 2


	CUTEND


	! Top
	RESOL res
	ADD L*COS(angle),L*SIN(angle), tankL
	CYLIND 0.05*ZZYZX, topR
	DEL 1


	! Body
	RESOL res
	ROTy -90
	REVOLVE 5,360,1+2+4+8+16+32,
		0, 0.85*tankR, 2,
		pipeLowH/2, 0.85*tankR, 2,
		3*pipeLowH/4, 0.95*tankR, 2,
		pipeLowH, tankR, 2,
		tankL, tankR, 2
	DEL 1

ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple

	CUTPOLYA 2,1,0,
		0,0, 913,
		tankR,360,4013

	RESOL 6

	ADD ConPosX_1,0,ConPosZ_1
	REVOLVE 2,360,1+2+4+16+32,
		0,ConWidth_1/2, 2,
		ABS(ConPosX_1),ConWidth_1/2, 2
	DEL 1

	ADD ConPosX_2,0,ConPosZ_2
	MULx -1
	REVOLVE 2,360,1+2+4+16+32,
		0,ConWidth_2/2, 2,
		ABS(ConPosX_2),ConWidth_2/2, 2
	DEL 2


	! Valves
	dR=ZZYZX/3

	RESOL 6
	ROTz -90
	ADDz pipeLowH + (tankL-pipeLowH)/2
	ROTy 90

	ADDx dR
	CYLIND 1.15*tankR+2.5*valveR, valveR
	ADDz 1.15*tankR+1.5*valveR
	ADDz valveR
	CYLIND valveR/5,2*valveR
	CUTPOLYA 2,1,0,
		0,0,913,
		valveR,360,4013
	DEL 3

	ADDx -dR
	CYLIND 1.15*tankR+2.5*valveR, valveR
	ADDz 1.15*tankR+1.5*valveR
	ADDz valveR
	CYLIND valveR/5,2*valveR
	CUTPOLYA 2,1,0,
		0,0,913,
		valveR,360,4013
	DEL 3

	DEL 1

	ADD 1.15*tankR,0,-dR-1.1*valveR
	CYLIND 2*dR+2.2*valveR,1.1*valveR
	DEL 1

	CUTEND
	CUTEND
	DEL 2


	CUTEND


	! Top
	RESOL res
	ADD L*COS(angle),L*SIN(angle), tankL
	CYLIND 0.05*ZZYZX, topR
	DEL 1


	! Body
	RESOL res
	ROTy -90
	REVOLVE 4,360,1+2+4+8+16+32,
		0, 0.85*tankR, 2,
		pipeLowH/2, 0.85*tankR, 2,
!		3*pipeLowH/4, 0.95*tankR, 2,
		pipeLowH, tankR, 2,
		tankL, tankR, 2
	DEL 1

ENDIF

DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 14,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 14,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 1,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 1

END

100:
IF ABS(vx) < EPS THEN
	IF vy > 0 THEN
		angle = 90
	ELSE
		angle = 270
	ENDIF
ELSE
	angle = ATN (vy / vx)
	IF vx >= 0 THEN
		IF angle >= 0 THEN
			angle = angle
		ELSE
			angle = 360 + angle
		ENDIF
	ELSE
		IF anglex > 0 THEN
			angle = 180 + angle
		ELSE
			angle = 180 + angle
		ENDIF
	ENDIF
ENDIF
RETURN
