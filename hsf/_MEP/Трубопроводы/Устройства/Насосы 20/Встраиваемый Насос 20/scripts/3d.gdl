!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Inline Pump


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple
IF GLOB_CONTEXT = 2 THEN res = 24

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


						! ---------------------- !
						! --- object rotator --- !
						! ---------------------- !

IF rot_conn THEN
	dist_x = A/2
	dist_z = 0.65*houseThk+motorL

	CALL "MEP_m_object_rotator" PARAMETERS 	rot_conn = rot_conn,
											MEP_Position = MEP_Position,
											dist_x = dist_x,
											dist_y = dist_z,
											dist_z = dist_z,
											angleDiffFromDefaultStatus = 90,
											Rotation = Rotation,
											symbRotangle = SYMB_ROTANGLE,
											gs_Connections = gs_Connections,
											MEP_NumberConnections = MEP_NumberConnections,
											MEP_NumConnectionData = MEP_NumConnectionData,
											defaultMatrix = defaultMatrix
ENDIF


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


			! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

HOTSPOT 0, 0, 0.65*houseThk+motorL, unID: unID=unID+1
HOTSPOT 0, 0, -0.6*houseThk, unID: unID=unID+1


IF gs_detlevel_3D_m = 0 THEN	! Wireframe
!!	LIN_ 0,-dLen/2,0, 0,dLen/2,0
!!
!!	RR = dTWidth/2
!!	yy = SQR( (2*RR*SIN(30))^2-(RR/2)^2 )
!!
!!	MODEL WIRE
!!	CIRCLE RR
!!	LIN_  yy,-RR/2,0, -yy,-RR/2,0
!!	LIN_  yy,-RR/2,0,  0,RR,0
!!	LIN_ -yy,-RR/2,0,  0,RR,0
!!
!!
!!	fa = 0
!!	IF gs_AddConnections[1][1] < 2 THEN fa = 1
!!	IF gs_AddConnections[1][1] = 3 AND ABS(MEP_NumConnectionData[1][20]-2) < EPS THEN fa = 1
!!	IF gs_AddConnections[1][1] = 4 AND ABS(MEP_NumConnectionData[1][20]-2) < EPS THEN fa = 1
!!
!!	fb = 0
!!	IF gs_AddConnections[2][1] < 2 THEN fb = 1
!!	IF gs_AddConnections[2][1] = 3 AND ABS(MEP_NumConnectionData[2][20]-2) < EPS THEN fb = 1
!!	IF gs_AddConnections[2][1] = 4 AND ABS(MEP_NumConnectionData[2][20]-2) < EPS THEN fb = 1
!!
!!	IF fa OR fb THEN
!!		MODEL WIRE
!!		ROTx 90
!!		ADDz dLen/2
!!		IF fa THEN CIRCLE ConConnectorWidth_1/2
!!		DEL 1
!!		ADDz -dLen/2
!!		CIRCLE ConConnectorWidth_2/2
!!		DEL 2
!!		MODEL SOLID
!!	ENDIF
ELSE

	DIM StraightPosition[][]

	StraightPosition[1][1] = -A/2
	StraightPosition[1][2] = 0
	StraightPosition[1][3] = 0

	StraightPosition[2][1] = 1
	StraightPosition[2][2] = 0
	StraightPosition[2][3] = 0

	StraightPosition[3][1] = 0
	StraightPosition[3][2] = 1
	StraightPosition[3][3] = 0

	CALL "MEP_m_Pipe_4" PARAMETERS 	MEP_Position = StraightPosition,
									MEP_NumberConnections = MEP_NumberConnections,
									MEP_NumConnectionData = MEP_NumConnectionData,
								MEP_StrConnectionData = MEP_StrConnectionData,
									MEP_StraightLength = A,
									gs_AddConnections = gs_AddConnections,
									pipe_show = 1,
									gs_PipeMat = MainBoxMaterial,
									MEP_InsShow = 0,
									MEP_cline_show_3D = 0,
								gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_cont_pen = gs_cont_pen


	ROTy 90
	CUTPOLYA 2, 2, 0,
		0, 0, 913,
		MEP_NumConnectionData[1][12]/2, 360, 4013
	DEL 1

	IF gs_detlevel_3D_m = 2 THEN

		ROTx 90
		ROTz 90

		REVOLVE 6,360,1+2+4+8+16+32,
			-0.6*houseThk, houseR/5, 2,
			-houseThk/2, houseR/5, 2,
			-houseThk/2, houseR-hR, 1,
			-houseThk/2+hR,houseR, 1,
			-hR,houseR, 1,
			 0, houseR-hR, 1

		REVOLVE 5,360,1+2+4+8+16+32,
			 0.65*houseThk, houseR-hR, 2,
			 houseThk/2, houseR-hR, 2,
			 houseThk/2-hR,houseR, 1,
			 hR,houseR, 1,
			 0, houseR-hR, 1

		ADDx 0.65*houseThk

		FOR i=1 TO 2
			CUTPOLYA 4,1,0,
				0.20*motorNeckL, motorR/4, 15,
				0.20*motorNeckL,-motorR/4, 15,
				0.85*motorNeckL,-motorR/3, 15,
				0.85*motorNeckL, motorR/3, 15
			ROTx 90
		NEXT i
		DEL 2

		! --- Motor

		REVOLVE 11,360, 1+2+4+8+16+32,
			0, 0.7*motorR, 2,
			motorNeckL, motorR, 2,

			motorNeckL, 1.2*motorR, 2,
			1.5*motorNeckL, 1.2*motorR, 2,
			2*motorNeckL, motorR, 2,

			motorL-motorR/8-motorBackL, motorR, 2,
			motorL-motorR/8-motorBackL, 1.05*motorR, 2,
			motorL-motorR/8-mR, 1.05*motorR, 1,
			motorL-motorR/8, 1.05*motorR-mR, 1001,
			motorL-motorR/8, motorR/5, 2,
			motorL, motorR/5, 2
		DEL 1

		CUTEND
		CUTEND

		DEL 2
	ELSE
		ROTx 90
		ROTz 90
		REVOLVE 4,360,1+2+4+8+16+32,
			-0.6*houseThk, houseR-hR, 1,
			-0.6*houseThk+hR,houseR, 1,
			 0.65*houseThk-hR, houseR, 1,
			 0.65*houseThk, houseR-hR, 2

		ADDx 0.65*houseThk

		! --- Motor

		REVOLVE 7,360, 1+2+4+8+16+32,
			0, 0.7*motorR, 2,
			motorNeckL, motorR, 2,
			motorNeckL, 1.2*motorR, 2,
			1.5*motorNeckL, 1.2*motorR, 2,
			2*motorNeckL, motorR, 2,
			motorL-mR, motorR, 2,
			motorL, motorR-mR, 1
		DEL 3
	ENDIF
	CUTEND
ENDIF

DEL 1

END
