
! Horizontal Split Case Pump


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

IF GLOB_CONTEXT = 23 THEN gs_detlevel_3D_m = 1
res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

IF useSysMat THEN MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- HORIZONTAL SPLIT CASE PUMP --- !

unID = 1
HOTSPOT -A/2, 0, 0, unID: unID=unID+1
HOTSPOT  A/2, 0, 0, unID: unID=unID+1
HOTSPOT -A/2, -B/2, 0, unID: unID=unID+1
HOTSPOT -A/2,  B/2, 0, unID: unID=unID+1
HOTSPOT  A/2,  B/2, 0, unID: unID=unID+1
HOTSPOT  A/2, -B/2, 0, unID: unID=unID+1


baseH = MIN(0.15*ZZYZX, 0.9*ConPosZ_1-flangeR1, 0.9*ConPosZ_2-flangeR2)
IF baseH < 0.001 THEN baseH = 0.001
baseThk=0.15*baseH
baseR = 0.1*baseH


IF gs_detlevel_3D_m = 2 THEN	! Detailed

	! Base
	ROTz 90
	ROTx 90
	ADDz -A/2
	PRISM_ 12, A,
		-B/2, 0, 15,
		-B/2, baseH-baseR, 79,
		-B/2+baseR, baseH, 79,
		 B/2-baseR, baseH, 79,
		 B/2, baseH-baseR, 79,
		 B/2, 0, 15,
		 B/2-baseThk, 0, 15,
		 B/2-baseThk, baseH-baseThk-baseR, 79,
		 B/2-baseThk-baseR, baseH-baseThk, 79,
		-B/2+baseThk+baseR, baseH-baseThk, 79,
		-B/2+baseThk, baseH-baseThk-baseR, 79,
		-B/2+baseThk, 0, 15
	DEL 3


	ADD ConPosX_1, 0, houseH + houseR - houseR/6
	PRISM_ 16,houseR/3,
		-0.7*houseLThk, 0.7*houseR, 79,
		-0.63*houseLThk, 0.7*houseR+hR/2, 79,
		-0.4*houseLThk, houseR+hR/6, 79,
		-houseThk/2+hR/2, houseR+hR/2, 79,
		 houseThk/2-hR/2, houseR+hR/2, 79,
		 0.4*houseLThk, houseR+hR/6, 79,
		 0.63*houseLThk, 0.7*houseR+hR/2, 79,
		 0.7*houseLThk, 0.7*houseR, 79,
		 0.7*houseLThk, -0.7*houseR, 79,
		 0.63*houseLThk, -0.7*houseR-hR/2, 79,
		 0.4*houseLThk, -houseR-hR/6, 79,
		 houseThk/2-hR/2, -houseR-hR/2, 79,
		-houseThk/2+hR/2, -houseR-hR/2, 79,
		-0.4*houseLThk, -houseR-hR/6, 79,
		-0.63*houseLThk, -0.7*houseR-hR/2, 79,
		-0.7*houseLThk, -0.7*houseR, 79
	DEL 1


	GROUP "pumpHouse1"
	ADD ConPosX_1, 0, houseH + houseR

	ROTx 90
	CUTPOLYA 4,1,0,
		-0.7*houseLThk, -houseR/6, 15,
		-0.7*houseLThk,  houseR/6, 15,
		 0.7*houseLThk,  houseR/6, 15,
		 0.7*houseLThk, -houseR/6, 15
	DEL 1

	REVOLVE 15,360, 1+2+4+8+16+32,
		-houseLThk, 0.2*houseR, 2,
		-0.9*houseLThk, 0.2*houseR, 2,
		-0.9*houseLThk, 0.6*houseR, 2,
		-0.7*houseLThk, 0.6*houseR, 2,
		-0.7*houseLThk, 0.7*houseR, 2,

		-houseThk/2, 0.7*houseR, 2,
		-houseThk/2, houseR-hR, 1,
		-houseThk/2+hR, houseR, 1001,
		 houseThk/2-hR, houseR, 1,
		 houseThk/2, houseR-hR, 1002,

		 0.7*houseLThk, 0.7*houseR, 2,
		 0.7*houseLThk, 0.6*houseR, 2,
		 0.9*houseLThk, 0.6*houseR, 2,
		 0.9*houseLThk, 0.2*houseR, 2,
		 houseLThk, 0.2*houseR, 2

	CUTEND
	DEL 1
	ENDGROUP

	GROUP "pumpBase1"
	hBaseThk = 0.1*houseR
	ADDx ConPosX_1
	FOR i=1 TO 2
		ROTx 90
		ROTy 90
		IF i=1 THEN
			ROTy 90
			ADDy ConPosZ_2
			CUTPOLYA 2,1,0,
				0,0, 979,
				pipeR2,360,4079
			DEL 2
		ENDIF
		IF i=2 THEN
			ROTy 90
			ADDy ConPosZ_1
			CUTPOLYA 2,1,0,
				0,0, 979,
				pipeR1,360,4079
			DEL 2
		ENDIF
		ADDz -0.7*houseLThk
		PRISM 6, 1.4*houseLThk,
			-1.1*houseR, baseH,
			-1.1*houseR, baseH+hBaseThk,
			-0.9*houseR, baseH+hBaseThk,
			-0.73*houseR, houseH + houseR - houseR/6,
			-0.73*houseR+hBaseThk, houseH + houseR - houseR/6,
			-0.9*houseR+hBaseThk, baseH

		CUTEND

		DEL 3
		MULy -1
	NEXT i
	DEL 3
	ENDGROUP

	PLACEGROUP ADDGROUP("pumpHouse1", "pumpBase1")
	KILLGROUP "pumpHouse1"
	KILLGROUP "pumpBase1"

	ADDz houseH + houseR
	ROTy 90
	CUTPOLYA 2,1,0,
		0,0,913,
		houseR,360,4013
	DEL 2

	ADD ConPosX_1, ConPosY_1, ConPosZ_1
	ROTz -90
	REVOLVE 2,360,1+2+4+8+16+32,
			MAX(ABS(ConPosY_1), EPS),	pipeR1, 2,
			0,			pipeR1, 2
	DEL 2

	ADD ConPosX_2, ConPosY_2, ConPosZ_2
	ROTz 90
	REVOLVE 2,360,1+2+4+8+16+32,
			MAX(ABS(ConPosY_2), EPS),	pipeR2, 2,
			0,			pipeR2, 2
	DEL 2

	CUTEND


	ADD ConPosX_1+houseLThk, 0, houseH + houseR
		REVOLVE 2,360, 1+2+4+8+16+32,
			0,	 cupR, 2,
			cupL,cupR, 2

	cupThk = 0.05*cupR
	basePosZ = houseH + houseR - baseH

	ROTy 90
	ROTz 90
	ADDz -0.25*cupL
	PRISM_ 13,1.5*cupL,
		-2.2*cupR, -basePosZ + cupThk, 15,
		-1.5*cupR, 0, 79,
		 1.5*cupR, 0, 1079,
		 2.2*cupR, -basePosZ + cupThk, 15,
		 2.7*cupR, -basePosZ + cupThk, 15,
		 2.7*cupR, -basePosZ, 15,
		 2.2*cupR-cupThk, -basePosZ, 15,
		 1.5*cupR-cupThk, 0, 79,
		-1.5*cupR+cupThk, 0, 1079,
		-2.2*cupR+cupThk, -basePosZ, 15,
		-2.7*cupR, -basePosZ, 15,
		-2.7*cupR, -basePosZ + cupThk, 15,
		-2.7*cupR, -basePosZ + cupThk, 15
	DEL 4


	! Motor
	ADDx ConPosX_1+houseLThk+cupL
	ADDz houseH + houseR
		REVOLVE 8,360, 1+2+4+8+16+32,
			0, 0.2*houseR, 2,
			0.1*motorL, 0.2*houseR, 2,
			0.1*motorL, motorR-mR, 1,
			0.1*motorL+mR, motorR, 1001,
			0.95*motorL-mR, motorR, 1,
			0.95*motorL, motorR-mR, 1001,
			0.95*motorL, 0.2*motorR, 2,
			motorL, 0.2*motorR, 2
	DEL 1

	mDepth = (0.95-0.1)*motorL
	mBaseThk = 0.05*motorR
	mBaseH = houseH + houseR - motorR - baseH

	ADD (0.1+(0.95-0.1)/2)*motorL,motorR, baseH
	ROTx 90
	PRISM_ 12, 2*motorR,
		-mDepth/2, 0, 15,
		-mDepth/2, mBaseH-baseR, 79,
		-mDepth/2+baseR, mBaseH, 79,
		 mDepth/2-baseR, mBaseH, 79,
		 mDepth/2, mBaseH-baseR, 79,
		 mDepth/2, 0, 15,
		 mDepth/2-mBaseThk, 0, 15,
		 mDepth/2-mBaseThk, mBaseH-mBaseThk-baseR, 79,
		 mDepth/2-mBaseThk-baseR, mBaseH-mBaseThk, 79,
		-mDepth/2+mBaseThk+baseR, mBaseH-mBaseThk, 79,
		-mDepth/2+mBaseThk, mBaseH-mBaseThk-baseR, 79,
		-mDepth/2+mBaseThk, 0, 15

	DEL 3

ENDIF

IF gs_detlevel_3D_m = 1 THEN	! Simple

	ROTz 90
	ROTx 90
	ADDz -A/2
	PRISM_ 8, A,
		-B/2, 0, 15,
		-B/2, baseH, 15,
		 B/2, baseH, 15,
		 B/2, 0, 15,
		 B/2-baseThk, 0, 15,
		 B/2-baseThk, baseH-baseThk, 15,
		-B/2+baseThk, baseH-baseThk, 15,
		-B/2+baseThk, 0, 15
	DEL 3

	ADD ConPosX_1, 0, houseH + houseR - houseR/6
	PRISM_ 8,houseR/3,
		-0.7*houseLThk, 0.7*houseR+hR/4, 79,
		-houseThk/2+hR/2, houseR+hR/2, 79,
		 houseThk/2-hR/2, houseR+hR/2, 79,
		 0.7*houseLThk, 0.7*houseR+hR/4, 79,

		 0.7*houseLThk, -0.7*houseR-hR/4, 79,
		 houseThk/2-hR/2, -houseR-hR/2, 79,
		-houseThk/2+hR/2, -houseR-hR/2, 79,
		-0.7*houseLThk, -0.7*houseR-hR/4, 79
	DEL 1


	GROUP "pumpHouse2"
	ADD ConPosX_1, 0, houseH + houseR

	ROTx 90
	CUTPOLYA 4,1,0,
		-0.7*houseLThk, -houseR/6, 15,
		-0.7*houseLThk,  houseR/6, 15,
		 0.7*houseLThk,  houseR/6, 15,
		 0.7*houseLThk, -houseR/6, 15
	DEL 1

	REVOLVE 15,360, 1+2+4+8+16+32,
		-houseLThk, 0.2*houseR, 2,
		-0.9*houseLThk, 0.2*houseR, 2,
		-0.9*houseLThk, 0.6*houseR, 2,
		-0.7*houseLThk, 0.6*houseR, 2,
		-0.7*houseLThk, 0.7*houseR, 2,

		-houseThk/2, 0.7*houseR, 2,
		-houseThk/2, houseR-hR, 1,
		-houseThk/2+hR, houseR, 1001,
		 houseThk/2-hR, houseR, 1,
		 houseThk/2, houseR-hR, 1002,

		 0.7*houseLThk, 0.7*houseR, 2,
		 0.7*houseLThk, 0.6*houseR, 2,
		 0.9*houseLThk, 0.6*houseR, 2,
		 0.9*houseLThk, 0.2*houseR, 2,
		 houseLThk, 0.2*houseR, 2

	CUTEND
	DEL 1
	ENDGROUP

	GROUP "pumpBase2"
	hBaseThk = 0.1*houseR
	ADDx ConPosX_1
	FOR i=1 TO 2
		ROTx 90
		ROTy 90
		IF i=1 THEN
			ROTy 90
			ADDy ConPosZ_2
			CUTPOLYA 2,1,0,
				0,0, 979,
				pipeR2,360,4079
			DEL 2
		ENDIF
		IF i=2 THEN
			ROTy 90
			ADDy ConPosZ_1
			CUTPOLYA 2,1,0,
				0,0, 979,
				pipeR1,360,4079
			DEL 2
		ENDIF
		ADDz -0.7*houseLThk
		PRISM 6, 1.4*houseLThk,
			-1.1*houseR, baseH,
			-1.1*houseR, baseH+hBaseThk,
			-0.9*houseR, baseH+hBaseThk,
			-0.73*houseR, houseH + houseR - houseR/6,
			-0.73*houseR+hBaseThk, houseH + houseR - houseR/6,
			-0.9*houseR+hBaseThk, baseH

		CUTEND

		DEL 3
		MULy -1
	NEXT i
	DEL 3
	ENDGROUP

	PLACEGROUP ADDGROUP("pumpHouse2", "pumpBase2")
	KILLGROUP "pumpHouse2"
	KILLGROUP "pumpBase2"

	ADDz houseH + houseR
	ROTy 90
	CUTPOLYA 2,1,0,
		0,0,913,
		houseR,360,4013
	DEL 2

	ADD ConPosX_1, ConPosY_1, ConPosZ_1
	ROTz -90
	REVOLVE 2,360,1+2+4+8+16+32,
			0,	pipeR1, 2,
			ABS(ConPosY_1),	pipeR1, 2
	DEL 2

	ADD ConPosX_2, ConPosY_2, ConPosZ_2
	ROTz 90
	REVOLVE 2,360,1+2+4+8+16+32,
			0,	pipeR2, 2,
			ABS(ConPosY_2),	pipeR2, 2
	DEL 2

	CUTEND


	ADD ConPosX_1+houseLThk, 0, houseH + houseR
		REVOLVE 2,360, 1+2+4+8+16+32,
			0,	 cupR, 2,
			cupL,cupR, 2

	cupThk = 0.05*cupR
	basePosZ = houseH + houseR - baseH

	ROTy 90
	ROTz 90
	ADDz -0.25*cupL
	EXTRUDE 6, 0,0,1.5*cupL, 16+32,
		-2.7*cupR, -basePosZ, 0,
		-2.2*cupR, -basePosZ + cupThk, 0,
		-1.5*cupR, 0, 1,
		 1.5*cupR, 0, 1001,
		 2.2*cupR, -basePosZ + cupThk, 0,
		 2.7*cupR, -basePosZ, 0
	DEL 4


	! Motor
	ADDx ConPosX_1+houseLThk+cupL
	ADDz houseH + houseR
		REVOLVE 8,360, 1+2+4+8+16+32,
			0, 0.2*houseR, 2,
			0.1*motorL, 0.2*houseR, 2,
			0.1*motorL, motorR-mR, 1,
			0.1*motorL+mR, motorR, 1,
			0.95*motorL-mR, motorR, 1,
			0.95*motorL, motorR-mR, 1,
			0.95*motorL, 0.2*motorR, 2,
			motorL, 0.2*motorR, 2
	DEL 1

	mDepth = (0.95-0.1)*motorL
	mBaseThk = 0.05*motorR
	mBaseH = houseH + houseR - motorR - baseH

	ADD (0.1+(0.95-0.1)/2)*motorL,motorR, baseH
	ROTx 90
	PRISM_ 8, 2*motorR,
		-mDepth/2, 0, 15,
		-mDepth/2, mBaseH, 15,
		 mDepth/2, mBaseH, 15,
		 mDepth/2, 0, 15,
		 mDepth/2-mBaseThk, 0, 15,
		 mDepth/2-mBaseThk, mBaseH-mBaseThk, 15,
		-mDepth/2+mBaseThk, mBaseH-mBaseThk, 15,
		-mDepth/2+mBaseThk, 0, 15

	DEL 3

ENDIF
DEL TOP
RESOL res


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16
END
