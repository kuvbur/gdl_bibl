!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el



IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- object --- !

unID = 1
HOTSPOT	   0, 0, 0, unID: unID=unID+1
HOTSPOT	   A, 0, 0, unID: unID=unID+1
HOTSPOT	   0, B, 0, unID: unID=unID+1
HOTSPOT	   A, B, 0, unID: unID=unID+1


IF gs_detlevel_3D_m = 2 THEN


	BLOCK A, B, bz						! base

	ADD A/2-MIN(2*hdr, A-0.05)/2, B-0.37*B-0.03, bz
		BLOCK MIN(MAX(2*hdr, 0.015), A-0.05), 0.35*B, bbz*(hdr<0.25)+(ConPosZ_1-0.25-0.1)*(hdr>=0.25)				! base under the back cylind
	DEL 1

	ADD A/2, ConPosY_1+5*B/10, bz
	ROTx 90
		EXTRUDE 5, 0, 0, B/10, 16+32,
			-ConWidth_1/2, 0, 0,
			-ConWidth_1/2, ConPosZ_1+ConWidth_1/2-bz+0.02, 0,
			 ConWidth_1/2, ConPosZ_1+ConWidth_1/2-bz+0.02, 0,
			 ConWidth_1/2, 0, 0,
			-ConWidth_1/2, 0, -1
	DEL 2

	GROUP "1"

		ADD A/2, B-0.39*B-0.03, bz+bbz+hdr
		ROTx -90
			CYLIND 0.39*B, MIN(MAX(hdr, 0.03), 0.25)					!  back cylind
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1, bz+bbz+hdr
		ROTx -90
			CYLIND 0.03, ConWidth_1
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1+0.03, bz+bbz+hdr
		ROTx -90
			CYLIND ConPosY_2+ConWidth_2/2-ConPosY_1, hdr+0.03					! engine 1
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1+0.03+B/10, bz+bbz+hdr
		ROTx -90
			CONE B/10, 0.9*hdr, 0.6*hdr, 90, 90					! engine 2
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1+0.03+B/10+B/10, bz+bbz+hdr
		ROTx -90
			CONE B/8, 0.5*hdr, 0.35*hdr, 90, 90					! engine 3
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1+B/10+B/10+2*B/10, bz+bbz+hdr
		ROTx -90
			CYLIND B/10,  ConWidth_1/2
		DEL 1
		DEL 1

	ENDGROUP

	GROUP "2"

		ADD A/2, 0.03, bz+bbz+hdr
		ROTx -90
			CYLIND B-0.03, ConWidth_1/3					! arbor
		DEL 1
		DEL 1

		ADD ConPosX_2, ConPosY_2, ConPosZ_1
			CYLIND ConPosZ_2-ConPosZ_1, ConWidth_2/2
		DEL 1

!		ADD ConPosX_2, ConPosY_2, ConPosZ_2-0.021
!			CYLIND 0.02, ConWidth_2
!		DEL 1

		ADD A/2-2*hdr/3, ConPosY_1+0.03+(ConPosY_2+ConWidth_2/2-ConPosY_1)/4, bz
			BLOCK 4*hdr/3, (ConPosY_2+ConWidth_2/2-ConPosY_1)/2, 	ConPosZ_1-hdr-bz				! base under the back cylind
		DEL 1

	ENDGROUP

	result_01=ADDGROUP ("1", "2")
	PLACEGROUP result_01

	KILLGROUP "1"
	KILLGROUP "2"
	KILLGROUP result_01

ELSE

	BLOCK A, B, bz						! base

	ADD A/2-MIN(2*hdr, A-0.05)/2, B-0.37*B-0.03, bz
		BLOCK MIN(2*hdr, A-0.05), 0.35*B, bbz*(hdr<0.25)+(ConPosZ_1-0.25-0.1)*(hdr>=0.25)				! base under the back cylind
	DEL 1


	GROUP "3"

		ADD A/2, B-0.39*B-0.03, bz+bbz+hdr
		ROTx -90
			CYLIND 0.39*B, MIN(hdr, 0.25)					!  back cylind
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1, bz+bbz+hdr
		ROTx -90
			CYLIND 0.03, ConWidth_1
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1+0.03, bz+bbz+hdr
		ROTx -90
			CYLIND ConPosY_2+ConWidth_2/2-ConPosY_1, hdr+0.03					! engine 1
		DEL 1
		DEL 1

		ADD A/2, ConPosY_1+0.03+B/10, bz+bbz+hdr
		ROTx -90
			CONE B/10+B/8, 0.9*hdr, 0.35*hdr, 90, 90					! engine 2
		DEL 1
		DEL 1


	ENDGROUP

	GROUP "4"

		ADD A/2, 0.03, bz+bbz+hdr
		ROTx -90
			CYLIND B-0.03, ConWidth_1/3					! arbor
		DEL 1
		DEL 1

		ADD ConPosX_2, ConPosY_2, ConPosZ_1
			CYLIND ConPosZ_2-ConPosZ_1, ConWidth_2/2
		DEL 1



		ADD A/2-2*hdr/3, ConPosY_1+0.03+(ConPosY_2+ConWidth_2/2-ConPosY_1)/4, bz
			BLOCK 4*hdr/3, (ConPosY_2+ConWidth_2/2-ConPosY_1)/2, 	ConPosZ_1-hdr-bz				! base under the back cylind
		DEL 1

	ENDGROUP

	result_02=ADDGROUP ("3", "4")
	PLACEGROUP result_02

	KILLGROUP "3"
	KILLGROUP "4"
	KILLGROUP result_02

ENDIF


				 !--- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	cShow3D = 1,
	gs_ConMat = MainBoxMaterial,
	MEP_InsShow = 0,
	MEP_cline_show_3D = 0,
	gs_cont_pen = gs_cont_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 14,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 14, MEP_StrConnectionData = MEP_StrConnectionData
END
