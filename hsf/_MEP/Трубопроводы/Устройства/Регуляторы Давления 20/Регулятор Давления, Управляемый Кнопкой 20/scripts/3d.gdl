!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple
IF GLOB_CONTEXT = 2 THEN res = 24

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


						! ---------------------- !
						! --- object rotator --- !
						! ---------------------- !

IF rot_conn THEN
	dist_x = A/2
	dist_z = 0.135*(ZZYZX/0.20)

	CALL "MEP_m_object_rotator" PARAMETERS 	rot_conn = rot_conn,
											MEP_Position = MEP_Position,
											dist_x = dist_x,
											dist_y = dist_z,
											dist_z = dist_z,
											angleDiffFromDefaultStatus = 90,
											Rotation = Rotation,
											symbRotangle = SYMB_ROTANGLE,
											gs_Connections = gs_Connections,
											MEP_NumberConnections = MEP_NumberConnections,
											MEP_NumConnectionData = MEP_NumConnectionData,
											defaultMatrix = defaultMatrix
ENDIF


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


			! --- HOTSPOTS --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1

HOTSPOT 0, 0, 0.135*(ZZYZX/0.20), unID: unID=unID+1
HOTSPOT 0, 0, -0.065*(ZZYZX/0.20), unID: unID=unID+1


				! --- MAIN BOX --- !

CUTPOLYA 2, 1, 0,
	0, 0, 913,
	0.05*(B/0.10), 360, 4013


DIM StraightPosition[][]

StraightPosition[1][1] = -A/2
StraightPosition[1][2] = 0
StraightPosition[1][3] = 0

StraightPosition[2][1] = 1
StraightPosition[2][2] = 0
StraightPosition[2][3] = 0

StraightPosition[3][1] = 0
StraightPosition[3][2] = 1
StraightPosition[3][3] = 0

CALL "MEP_m_Pipe_4" PARAMETERS 	MEP_Position = StraightPosition,
								MEP_NumberConnections = MEP_NumberConnections,
								MEP_NumConnectionData = MEP_NumConnectionData,
								MEP_StrConnectionData = MEP_StrConnectionData,
								MEP_StraightLength = A,
								gs_AddConnections = gs_AddConnections,
								pipe_show = 1,
								gs_PipeMat = MainBoxMaterial,
								MEP_InsShow = 0,
								MEP_cline_show_3D = 0,
								gs_detlevel_3D_m = gs_detlevel_3D_m,
								gs_cont_pen = gs_cont_pen

CUTEND

MUL B/0.10, B/0.10, ZZYZX/0.20
IF gs_detlevel_3D_m = 2 THEN
	ADDz -0.065
	RESOL 6
	PRISM_ 2, 0.015,
		0, 0, 915,
		0.015, 360, 4015
	RESOL res
	ADDz 0.015
	CYLIND 0.02, 0.025
	DEL 2

	ADDz -0.03
	CYLIND 0.09, 0.05
	ADDz 0.09
	CYLIND 0.025, 0.025
	ADDz 0.025
	CYLIND 0.050, 0.04
	DEL 3
ELSE
	ADDz -0.065
	CYLIND 0.20, 0.05
	DEL 1
ENDIF
DEL 2

END
