!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el

! 0.25-2.5 LPS Water Softener



PEN gs_cont_pen
SET FILL gs_fill_type

unID = 1
DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

				! --- WATER SOFTENER --- !

	HOTSPOT2  0, 0, unID: unID=unID+1
	HOTSPOT2 A - (mineralTankDiam + brineTankDiam) / 2,0, unID: unID=unID+1

	HOTSPOT2 -mineralTankDiam/2,  0, unID: unID=unID+1
	HOTSPOT2 0,  mineralTankDiam/2, unID: unID=unID+1
	HOTSPOT2 0, -mineralTankDiam/2, unID: unID=unID+1

	HOTSPOT2 A - mineralTankDiam / 2, 0, unID: unID=unID+1
	HOTSPOT2 A - (mineralTankDiam + brineTankDiam) / 2,  brineTankDiam/2, unID: unID=unID+1
	HOTSPOT2 A - (mineralTankDiam + brineTankDiam) / 2, -brineTankDiam/2, unID: unID=unID+1

	IF GLOB_CONTEXT <> 22 THEN
		POLY2_B 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 901,
			mineralTankDiam/2,360,4001

		ADD2 A - (mineralTankDiam + brineTankDiam) / 2, 0
		POLY2_B 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 901,
			brineTankDiam/2,360,4001
		DEL 1

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-valveWidth/2,-valveDepth/2, 1,
			-valveWidth/2, valveDepth/2, 1,
			 valveWidth/2, valveDepth/2, 1,
			 valveWidth/2,-valveDepth/2, 1

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			valveWidth/2, ConWidth_3/2, 1,
			A - mineralTankDiam/2 - brineTankDiam,  ConWidth_3/2, 1,
			A - mineralTankDiam/2 - brineTankDiam,  -ConWidth_3/2, 1,
			valveWidth/2, -ConWidth_3/2, 1

	ENDIF
ENDIF
DEL TOP


				! --- OTHER VIEWS --- !

ROT2 (SYMB_MIRRORED - NOT(SYMB_MIRRORED)) * (DrawingNum > 1) * SYMB_ROTANGLE

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180 * SYMB_MIRRORED,		 2	! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180 * NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 2,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 2,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 1,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConLength_4=ConLength_4, ConEdit_4 = 1, MEP_StrConnectionData = MEP_StrConnectionData
DEL 1

END
