!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el

! 5-20 LPS Water Softener



PEN gs_cont_pen
SET FILL gs_fill_type


unID = 1
DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

				! --- WATER SOFTENER --- !

	HOTSPOT2  0, 0, unID: unID=unID+1
	HOTSPOT2  mineralTankPos, 0, unID: unID=unID+1
	HOTSPOT2  brineTankPos, 0, unID: unID=unID+1

	HOTSPOT2 -mineralTankDiam/2, -mineralTankDiam/2, unID: unID=unID+1
	HOTSPOT2 -mineralTankDiam/2,  mineralTankDiam/2, unID: unID=unID+1
	HOTSPOT2  mineralTankPos+mineralTankDiam/2,  mineralTankDiam/2, unID: unID=unID+1
	HOTSPOT2  mineralTankPos+mineralTankDiam/2, -mineralTankDiam/2, unID: unID=unID+1

	IF GLOB_CONTEXT <> 22 THEN

		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-mineralTankDiam/2, -mineralTankDiam/2,	1,
			-mineralTankDiam/2,  mineralTankDiam/2,	1,
			 mineralTankPos+mineralTankDiam/2,  mineralTankDiam/2,	1,
			 mineralTankPos+mineralTankDiam/2, -mineralTankDiam/2,	1

		px = SQR((mineralTankDiam/2)^2 - (valveDepth/2)^2)
		dR = 0.03

		POLY2_B 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
			px, valveDepth/2, 1,
			mineralTankDiam/2+valveWidth-dR, valveDepth/2, 1,
			mineralTankDiam/2+valveWidth, valveDepth/2-dR, 1001,
			mineralTankDiam/2+valveWidth,-valveDepth/2+dR,1,
			mineralTankDiam/2+valveWidth-dR,-valveDepth/2,1001,
			px,-valveDepth/2, 0

		POLY2_B 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 901,
			mineralTankDiam/2,360,4001

		POLY2_B 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			mineralTankPos, 0, 901,
			mineralTankDiam/2,360,4001

		ADD2 brineTankPos, 0
		POLY2_B 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 901,
			brineTankDiam/2,360,4001
		DEL 1

	ENDIF
ENDIF
DEL TOP


				! --- OTHER VIEWS --- !

ROT2 (SYMB_MIRRORED - NOT(SYMB_MIRRORED)) * (DrawingNum > 1) * SYMB_ROTANGLE

IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
IF DrawingNum = 5 THEN PROJECT2 4, 180 * SYMB_MIRRORED,		 2	! Right
IF DrawingNum = 6 THEN PROJECT2 4, 180 * NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	ui_current_con = ui_current_con,
	MEP_NumberConnections = MEP_NumberConnections,
	MEP_NumConnectionData = gs_Connections,
	gs_AddConnections = gs_AddConnections,
	DrawingNum = DrawingNum,
	MEP_InsShow = 0,
	MEP_cline_show = 0,
	gs_cont_pen = gs_cont_pen,
	gs_fill_type=gs_fill_type,
	gs_fill_pen=gs_fill_pen,
	gs_back_pen=gs_back_pen,
	ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
	ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 1,
	ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 1,
	ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConLength_4=ConLength_4, ConEdit_4 = 1,
	ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConLength_5=ConLength_5, ConEdit_5 = 1,
	ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConLength_6=ConLength_6, ConEdit_6 = 1,
	ConPosX_7=ConPosX_7, ConPosY_7=ConPosY_7, ConLength_7=ConLength_7, ConEdit_7 = 1,
	ConPosX_8=ConPosX_8, ConPosY_8=ConPosY_8, ConLength_8=ConLength_8, ConEdit_8 = 1, MEP_StrConnectionData = MEP_StrConnectionData
DEL 1

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top
	IF GLOB_CONTEXT <> 22 THEN

		POLY2_B 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, pR/2, 1,
			brineTankPos+pR/2,  pR/2, 1,
			brineTankPos+pR/2,  -pR/2, 1001,
			0, -pR/2, 1,
			0, pR/2, 1001
	ENDIF
ENDIF

END
