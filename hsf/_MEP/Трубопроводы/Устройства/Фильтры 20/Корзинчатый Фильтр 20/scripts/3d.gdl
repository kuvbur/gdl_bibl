!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Basket Strainer


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple
IF GLOB_CONTEXT = 2 THEN res = 24

MainBoxMaterial = sMat
MATERIAL MainBoxMaterial
PEN gs_cont_pen
RESOL res


				! --- TRANSFORMATION --- !

XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
		MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
		MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]


				! --- HOTSPOT --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, 0, -ZZYZX*0.64+ConWidth_1/2, unID: unID=unID+1
HOTSPOT 0, 0, ZZYZX*0.36+ConWidth_1/2, unID: unID=unID+1


				! --- MAIN BOX --- !

DIM StraightPosition[][]

StraightPosition[1][1] = -A/2
StraightPosition[1][2] = 0
StraightPosition[1][3] = 0

StraightPosition[2][1] = 1
StraightPosition[2][2] = 0
StraightPosition[2][3] = 0

StraightPosition[3][1] = 0
StraightPosition[3][2] = 1
StraightPosition[3][3] = 0

CALL "MEP_m_Pipe_4" PARAMETERS 	MEP_Position = StraightPosition,
								MEP_NumberConnections = MEP_NumberConnections,
								MEP_NumConnectionData = MEP_NumConnectionData,
								MEP_StrConnectionData = MEP_StrConnectionData,
								MEP_StraightLength = A,
								gs_AddConnections = gs_AddConnections,
								pipe_show = 1,
								gs_PipeMat = MainBoxMaterial,
								MEP_InsShow = 0,
								MEP_cline_show_3D = 0,
								gs_detlevel_3D_m = gs_detlevel_3D_m,
								gs_cont_pen = gs_cont_pen



atma = 0.7*B/2
atmf = B/2

IF gs_detlevel_3D_m = 2 THEN
	ROTy 90
	CUTPOLYA 2, 2, 0,
		0, 0, 913,
		ConWidth_1/2, 360, 4013
	DEL 1

	ADDz -ZZYZX*0.64+ConWidth_1/2
	CONE ZZYZX*0.64, atma, atmf, 90, 90
	DEL 1

	CUTEND

	ADDz ConWidth_1/2
		CYLIND ZZYZX*0.09, 0.9*atma
	ADDz ZZYZX*0.09
		CYLIND ZZYZX*0.09, 1.1*atma
	ADDz ZZYZX*0.09
		CONE ZZYZX*0.09, 0.9*atma, 0.3*atma, 90, 90
	DEL 3

	GROUP "1"
		ADDz ZZYZX*0.27+ConWidth_1/2
			CYLIND ZZYZX*0.09, 0.3*atma
		DEL 1
	ENDGROUP

	GROUP "2"
		ADD -1.1*atmf, -0.2*atma/2, ZZYZX*0.3375+ConWidth_1/2
			BLOCK 2*1.1*atmf, 0.2*atma, ZZYZX*0.0225
		DEL 1
	ENDGROUP

	result_01=ADDGROUP ("1", "2")
	PLACEGROUP result_01

	KILLGROUP "1"
	KILLGROUP "2"
	KILLGROUP result_01
ELSE
	ROTy 90
	CUTPOLYA 2, 2, 0,
		0, 0, 913,
		ConWidth_1/2, 360, 4013
	DEL 1

	ADDz -ZZYZX*0.64+ConWidth_1/2
		CONE ZZYZX*0.64, atmf, atmf, 90, 90
	DEL 1

	CUTEND
	ADDz ConWidth_1/2
		CYLIND ZZYZX*0.18, 1.1*atma
	DEL 1

	ADDz ZZYZX*0.18+ConWidth_1/2
		CONE ZZYZX*0.18, 0.9*atma, 0.3*atma, 90, 90
	DEL 1
ENDIF
DEL 1

END
