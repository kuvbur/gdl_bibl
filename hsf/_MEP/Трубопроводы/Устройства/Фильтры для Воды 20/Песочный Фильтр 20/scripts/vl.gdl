
! Sand Filter


call "FM_types_MEP" parameters
DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 0		! Rectangle
	shape_typ_to_validation[2] = 1		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0


					! --- MAIN PARAMETERS --- !

IF NOT(SetProgram) THEN
	IF A < arcRad*2+0.01 THEN A = arcRad*2+0.01
	VALUES "A" RANGE [arcRad*2+0.01,]
	IF ZZYZX < arcRad+lowerArcHeight+footingHeight+ConWidth_1+ConWidth_2 THEN ZZYZX = arcRad+lowerArcHeight+footingHeight+ConWidth_1+ConWidth_2
	VALUES "ZZYZX" RANGE [arcRad+lowerArcHeight+footingHeight+ConWidth_1+ConWidth_2,]
ENDIF

B = A
PARAMETERS B = B


					! --- CONNECTIONS --- !

PARAMETERS MEP_NumberConnections = 2


					! --- PIPE INLET CONNECTION --- !

ConID = 1
ConName = `Труба Впускного Соединения `

ConDVecX = -1
ConDVecY = 0
ConDVecZ = 0

ConWVecX = 0
ConWVecY = -1
ConWVecZ = 0

IF NOT(SetProgram) THEN
	IF ConWidth_1 > MIN(A/2, ZZYZX-arcRad-lowerArcHeight-footingHeight-ConWidth_2) THEN ConWidth_1 = MIN(A/2, ZZYZX-arcRad-lowerArcHeight-footingHeight-ConWidth_2)
	VALUES "ConWidth_1" RANGE (0, MIN(A/2, ZZYZX-arcRad-lowerArcHeight-footingHeight-ConWidth_2)]
	PARAMETERS ConDepth_1 = ConWidth_1
	HIDEPARAMETER "ConDepth_1"

	IF ConPosZ_1 > ZZYZX-arcRad-ConWidth_1/2 THEN ConPosZ_1 = ZZYZX-arcRad-ConWidth_1/2
	IF ConStatus_2 THEN
		VALUES "ConPosZ_1" RANGE [ConPosZ_2+ConWidth_2/2+ConWidth_1/2, ZZYZX-arcRad-ConWidth_1/2]
	ELSE
		VALUES "ConPosZ_1" RANGE [footingHeight+lowerArcHeight+ConWidth_1/2, ZZYZX-arcRad-ConWidth_1/2]
	ENDIF
	VALUES "ConLength_1" RANGE [0, ]


	PARAMETERS ConPosX_1 = -B/2
	PARAMETERS ConPosY_1 = 0
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_1,
	ConPosY = ConPosY_1,
	ConPosZ = ConPosZ_1,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_1,
	ConNominalDepth = ConNominalDepth_1,
	ConWidth = ConWidth_1,
	ConDepth = ConDepth_1,
	ConLength = ConLength_1,
	ConWallThickness = ConWallThickness_1,
	ConConnectorWidth = ConConnectorWidth_1,
	ConConnectorDepth = ConConnectorDepth_1,
	ConConnectorWidth2 = ConConnectorWidth2_1,
	ConToolType = ConToolType_1,
	ConStatus = ConStatus_1,
	ConType_1 = ConType_1,
	ConConnectorType_1 = ConConnectorType_1,
	ConSystem_1 = ConSystem_1,
	ui_page_idx = ui_page_idx,
	ui_current_con = ui_current_con,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1,
	bUIConnectionStatusCheckbox = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"


					! --- UPPER SIDE CONNECTION --- !

ConID = 2
ConName = `Труба Выпускного Соединения`

ConDVecX = -1
ConDVecY = 0
ConDVecZ = 0

ConWVecX = 0
ConWVecY = -1
ConWVecZ = 0

IF NOT(SetProgram) THEN
	IF ConWidth_2 > MIN(A/2, ZZYZX-arcRad-lowerArcHeight-footingHeight-ConWidth_1) THEN ConWidth_2 = MIN(A/2, ZZYZX-arcRad-lowerArcHeight-footingHeight-ConWidth_1)
	VALUES "ConWidth_2" RANGE (0, MIN(A/2, ZZYZX-arcRad-lowerArcHeight-footingHeight-ConWidth_1)]
	PARAMETERS ConDepth_2 = ConWidth_2
	HIDEPARAMETER "ConDepth_2"

	IF ConStatus_1 THEN
		IF ConPosZ_2 > ConPosZ_1-ConWidth_1/2-ConWidth_2/2 THEN ConPosZ_2 = ConPosZ_1-ConWidth_1/2-ConWidth_2/2
		VALUES "ConPosZ_2" RANGE [footingHeight+lowerArcHeight+ConWidth_2/2, ConPosZ_1-ConWidth_1/2-ConWidth_2/2]
	ELSE
		IF ConPosZ_2 > ZZYZX-arcRad-ConWidth_2/2 THEN ConPosZ_2 = ZZYZX-arcRad-ConWidth_2/2
		VALUES "ConPosZ_2" RANGE [footingHeight+lowerArcHeight+ConWidth_2/2, ZZYZX-arcRad-ConWidth_2/2]
	ENDIF
	VALUES "ConLength_2" RANGE [0, ]


	PARAMETERS ConPosX_2 = -B/2
	PARAMETERS ConPosY_2 = 0
ENDIF


CALL "MEP_m_ConnectionsMEP_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_2,
	ConPosY = ConPosY_2,
	ConPosZ = ConPosZ_2,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_2,
	ConNominalDepth = ConNominalDepth_2,
	ConWidth = ConWidth_2,
	ConDepth = ConDepth_2,
	ConLength = ConLength_2,
	ConWallThickness = ConWallThickness_2,
	ConConnectorWidth = ConConnectorWidth_2,
	ConConnectorDepth = ConConnectorDepth_2,
	ConConnectorWidth2 = ConConnectorWidth2_2,
	ConToolType = ConToolType_2,
	ConStatus = ConStatus_2,
	ConType_2 = ConType_2,
	ConConnectorType_2 = ConConnectorType_2,
	ConSystem_2 = ConSystem_2,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1,
	bUIConnectionStatusCheckbox = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF setProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF
