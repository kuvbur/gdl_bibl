!-----------3d---------------------
if show_el < EPS then end
if GLOB_VIEW_TYPE=2 then gs_detlevel_3D_m = gs_detlevel_2D_m
if (GLOB_PREVIEW_MODE or GLOB_FEEDBACK_MODE or GLOB_SEO_TOOL_MODE) then gs_detlevel_3D_m = 1
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen
call "error_sphere" parameters is_error = is_error
_pTM = GLOB_SCALE / 1000
tfl = flange_t
if show_conventions and show_axis then end
!	flange_d = 1.5 * _pTM
!	flange_t = 0.5 * _pTM
!	flange_t1 = 0
!	flange_db2 = 0
!	flange_full_t = flange_t
!	tfl = 0.2 * _pTM
!endif

PEN gs_cont_pen
shadow off
material sMat
if SetProgram then
	XFORM   MEP_Position[2][1], MEP_Position[3][1], dZx, MEP_Position[1][1],
			MEP_Position[2][2], MEP_Position[3][2], dZy, MEP_Position[1][2],
			MEP_Position[2][3], MEP_Position[3][3], dZz, MEP_Position[1][3]
endif

if flange_mir then 
	roty -90
else
	roty 90
endif

hotspot 0,0,0
sc=13
if NSP>0 then tt = max(get(NSP))
RESOL res

group "fl"
	if flange_type = 14 and flange_subtype=2 then addz flange_t1
	hotspot flange_d/2,0,0
	hotspot 0,flange_d/2,0
	hotspot -flange_d/2,0,0
	hotspot 0,-flange_d/2,0
	CYLIND tfl, flange_d/2
	if flange_type = 14 and flange_subtype=2 then del 1
endgroup

if not(show_axis) and not(show_conventions) then
	group "otv"
			addz -flange_t1
!		if flange_type = 14 and flange_subtype=2 then
!			CYLIND flange_full_t+flange_t1, flange_dnf/2
!		else
			CYLIND flange_full_t+flange_t1, flange_dy/2
!		endif
			del 1
	endgroup
	
	group "bolt_hole"
		RESOL 8
		step_ang = 360/flange_nb
		ang = 0
		rb = flange_dab/2
		for i=1 to flange_nb
			add rb*cos(ang), rb*sin(ang), -0.02
			CYLIND flange_full_t+0.02, (flange_db+0.002)/2
			ang = ang + step_ang
			del 1
		next i
		RESOL res
	endgroup
	
	group "dop"
		if flange_type = 11 then
			addz flange_t
			CONE flange_t1, flange_db1/2, flange_db2/2, 90, 90
			hotspot 0,0,0
			hotspot flange_db1/2,0,0
			hotspot 0,flange_db1/2,0
			hotspot -flange_db1/2,0,0
			hotspot 0,-flange_db1/2,0
			addz flange_t1
			CYLIND flange_t2, flange_db2/2
			hotspot 0,0,flange_t2
			hotspot flange_db2/2,0,flange_t2
			hotspot 0,flange_db2/2,flange_t2
			hotspot -flange_db2/2,0,flange_t2
			hotspot 0,-flange_db2/2,flange_t2
			del 2
		endif
		if flange_type = 2 then
			addz -flange_t1
			CYLIND flange_t1, flange_db1/2
			hotspot 0,0,0
			hotspot flange_db1/2,0,0
			hotspot 0,flange_db1/2,0
			hotspot -flange_db1/2,0,0
			hotspot 0,-flange_db1/2,0
			del 1
		endif

		if flange_type = 14 and flange_subtype=2 and flange_t1>EPS and flange_t2>EPS and flange_db2>EPS and flange_db1>EPS then
			CYLIND flange_t1, flange_db2/2
			addz flange_t1+flange_t
			CYLIND flange_t2, flange_db1/2
			del 1
			hotspot flange_db2/2,0,0
			hotspot 0,flange_db2/2,0
			hotspot -flange_db2/2,0,0
			hotspot 0,-flange_db2/2,0
			hotspot flange_db1/2,0,flange_t1+flange_t+flange_t2
			hotspot 0,flange_db1/2,flange_t1+flange_t+flange_t2
			hotspot -flange_db1/2,0,flange_t1+flange_t+flange_t2
			hotspot 0,-flange_db1/2,flange_t1+flange_t+flange_t2
		endif

	endgroup
	r1 = ADDGROUP("fl", "dop")
	if gs_detlevel_3D_m = 2 then
		r2 = SUBGROUP(r1, "bolt_hole")
	else
		r2 = r1
	endif
	rez1 = SUBGROUP(r2, "otv")
	placegroup rez1
	killgroup rez1
	killgroup "otv"
	killgroup "dop"
	killgroup "bolt_hole"
else
	placegroup "fl"
endif


killgroup "fl"
if NSP>0 then tt = max(get(NSP))
del 1
