
! Cable Ladder


CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen

PEN gs_cont_pen
IF gs_fill_pen THEN SET FILL gs_fill_type

ladderNum = MAX(2, INT(MEP_StraightLength/(0.75*ConWidth_1)))
ladderDist = MEP_StraightLength/ladderNum
ladderHalfDist = ladderDist/2
ladderThk = MAX(0.03*(ladderDist > 0.06), (0.75*ConWidth_1)/10)

IF SGN(dz) = 0 THEN		! -----> the cable tray in the xy-plane <----- !

	ADD2 rx1, ry1
	ROT2 dirAngOnXYPlane

	IF connectedA + connectedB < 1 + EPS THEN
		HOTSPOT2 0, 0, hceID_1, MEP_StraightLength, 1+256*(connectedA < EPS)
		HOTSPOT2 -1, 0, HptsUnIDBeginForConnEnds, MEP_StraightLength, 3
		IF ABS(connectedA) < EPS AND ABS(connectedB - 1) < EPS THEN
			hID = hceID_2+1
			HOTSPOT2 MEP_StraightLength, 0, hceID_2
		ELSE
			hID = hceID_2
		ENDIF
		HOTSPOT2 MEP_StraightLength, 0, hID, MEP_StraightLength, 2+128*connectedB
	ELSE
		HOTSPOT2 0, 0, hceID_1
		HOTSPOT2 MEP_StraightLength, 0, hceID_2
	ENDIF

	IF ABS(ABS(MEP_Position[3][3]) - 1) < EPS THEN
		cableTrayHalfWidth = cableTrayHalfHeight
	ENDIF

	FOR i = 1 TO ladderNum						! -----> main ladders <----- !
		ADD2 ladderHalfDist+ladderDist*(i-1), 0
		PUT ladderThk/2, cableTrayHalfWidth, 1
		PUT -ladderThk/2, cableTrayHalfWidth, 1
		PUT -ladderThk/2, -cableTrayHalfWidth, 1
		PUT ladderThk/2, -cableTrayHalfWidth, 1
		PUT ladderThk/2, cableTrayHalfWidth, -1

		POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

			GET (NSP)

		DEL 1
	NEXT i

	PUT 0, cableTrayHalfWidth, 1
	PUT MEP_StraightLength, cableTrayHalfWidth, MEP_CoveredCableTray
	PUT MEP_StraightLength, -cableTrayHalfWidth, 1
	PUT 0, -cableTrayHalfWidth, MEP_CoveredCableTray

	POLY2_B NSP/3, 1 + 2*MEP_CoveredCableTray + 4 + 64, gs_fill_pen, gs_back_pen,

		GET (NSP)

	HOTLINE2 0, 0, MEP_StraightLength, 0

	IF MEP_cline_show THEN		! -----> center line <----- !
		PEN MEP_cline_pen
		SET LINE_TYPE MEP_cline_type

		LINE2 0, 0, MEP_StraightLength, 0
	ENDIF
	DEL 2
	END
ENDIF


!!SYMB_POS_Z = -0.165
!!GLOB_CSTORY_ELEV = 0
!!GLOB_CSTORY_HEIGHT = 0.84

IF rz1 > rz2 THEN
	dz = ABS(dz)

	SYMB_POS_Z_temp = SYMB_POS_Z
	SYMB_POS_Z 		= SYMB_POS_Z-dz
ENDIF

IF SGN(dx) = 0 AND SGN(dy) = 0 THEN		! -----> the cable tray on the xy-plane <----- !
	ADD2 rx1, ry1
	ROT2 dirAngOnXYPlane

	HOTSPOT2 0, 0, hceID_1
	IF GLOB_CONTEXT <> 5 AND AC_AutoOnStoryVisibility > 0.5 THEN
		IF  GLOB_CSTORY_ELEV - SYMB_POS_Z < EPS THEN
			IF (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT) - (SYMB_POS_Z  + MEP_StraightLength) > -EPS THEN
				GOSUB 10
!!				TEXT2 0, 0, 1
			ENDIF
			IF GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT > SYMB_POS_Z AND GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT < SYMB_POS_Z + MEP_StraightLength THEN
				PEN SYMB_SECT_PEN
				GOSUB 10
!!				TEXT2 0, 0, 21
			ENDIF
		ELSE
			IF GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT < SYMB_POS_Z + MEP_StraightLength THEN
				PEN SYMB_SECT_PEN
				GOSUB 10
!!				TEXT2 0, 0, 22
			ENDIF
			IF GLOB_CSTORY_ELEV < SYMB_POS_Z  + MEP_StraightLength AND (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT) - (SYMB_POS_Z  + MEP_StraightLength) > -EPS THEN
				GOSUB 10
!!				TEXT2 0, 0, 23
			ENDIF
		ENDIF
	ELSE
		GOSUB 10
	ENDIF

	SYMB_POS_Z = SYMB_POS_Z_temp

	DEL 2

	END
ENDIF


							! -----< other positions of the duct >----- !


IF GLOB_CSTORY_ELEV+GLOB_CSTORY_HEIGHT > SYMB_POS_Z AND GLOB_CSTORY_ELEV < SYMB_POS_Z+dz OR AC_AutoOnStoryVisibility < 0.5 THEN

	ladderNum = MAX(2, INT(MEP_StraightLength/(0.75*ConWidth_1)))
	ladderDist = MEP_StraightLength/ladderNum
	ladderHalfDist = ladderDist/2
	ladderThk = MAX(0.03*(ladderDist > 0.06), ladderDist/10)

	HOTLINE2 rx1, ry1, rx2, ry2

	Length=SQR(dx^2+dy^2)
	ar = ABS(dz)/MEP_StraightLength
	br = Length/MEP_StraightLength
	cr = ABS(dz)/Length

	IF ABS(MEP_Position[3][3]) < EPS THEN
		decreaseLen = cableTrayHalfHeight*br
	ELSE
		decreaseLen = cableTrayHalfWidth*br
	ENDIF

	dz = dz - decreaseLen*2
	specCase = 0
	IF dz < EPS OR AC_AutoOnStoryVisibility < 0.5 THEN specCase = 1

	SYMB_POS_Z_temp = SYMB_POS_Z
	SYMB_POS_Z		= SYMB_POS_Z+decreaseLen

	ADD2 rx1, ry1
	ROT2 dirAngOnXYPlane

	IF rz1 > rz2 THEN
		ADD2 Length, 0
		MUL2 -1, 1
	ENDIF

	IF ABS(MEP_Position[3][3]) > EPS THEN
		halfWidth = cableTrayHalfHeight
		halfHeight = cableTrayHalfWidth
	ELSE
		halfWidth = cableTrayHalfWidth
		halfHeight = cableTrayHalfHeight
	ENDIF

	HOTSPOT2 0, 0, hceID_1
	HOTSPOT2 Length, 0, hceID_2

	IF GLOB_CSTORY_ELEV - SYMB_POS_Z < EPS OR GLOB_CONTEXT = 5 OR specCase THEN
		IF (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT) - (SYMB_POS_Z  + dz) > -EPS OR GLOB_CONTEXT = 5 OR specCase THEN
!!			TEXT2 0, 0, "1. case"

			startSegment = halfHeight*ar
			endSegment = MEP_StraightLength*br+halfHeight*ar

			DRAWINDEX 10
			GOSUB 100
			ratio = ar
			cont_pen = gs_cont_pen
			GOSUB 12

			IF MEP_CoveredCableTray THEN
				startSegment = -halfHeight*ar
				endSegment = MEP_StraightLength*br-halfHeight*ar

				DRAWINDEX 20
				GOSUB 11
			ENDIF

			IF MEP_cline_show THEN		! -----> center line <----- !
				startSegment = 0
				endSegment = MEP_StraightLength*br

				PEN MEP_cline_pen
				SET LINE_TYPE MEP_cline_type

				DRAWINDEX 30
				LINE2 startSegment, 0, endSegment, 0
			ENDIF
			END
		ENDIF
		IF  GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT > SYMB_POS_Z AND GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT < SYMB_POS_Z + dz THEN
!!			TEXT2 0, 0, "2a. case"

			startSegment = halfHeight*ar
			endSegment = (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr + halfHeight/ar
!!			IF (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z) < (halfWidth*(1/ar-ar))*cr THEN
!!				endSegment = startSegment+(halfWidth*(1/ar-ar)) + halfHeight/ar
!!			ENDIF
			DRAWINDEX 10
			GOSUB 100

			IF MEP_CoveredCableTray THEN
				startSegment = -halfHeight*ar
				endSegment = (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr - halfHeight/ar
				MEP_CoveredCableTrayOn = 1

				DRAWINDEX 20
				GOSUB 11
			ENDIF

			ratio = ar
			cont_pen = gs_cont_pen
			DRAWINDEX 10
			GOSUB 12

			ADD2 (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr, 0
			ratio = 1/ar
			cont_pen = SYMB_SECT_PEN
			PEN SYMB_SECT_PEN
			DRAWINDEX 40
			GOSUB 12
			DEL 1

			IF MEP_cline_show THEN		! -----> center line <----- !
				startSegment = 0
				endSegment = (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr

				PEN MEP_cline_pen
				SET LINE_TYPE MEP_cline_type

				DRAWINDEX 30
				LINE2 startSegment, 0, endSegment, 0
			ENDIF
		ENDIF
	ELSE
		IF GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT < SYMB_POS_Z + dz THEN
!!			TEXT2 0, 0, "2b. case"

			startSegment = (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr + halfHeight/ar
			endSegment = (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr + halfHeight/ar
!!			IF (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z) < (halfWidth*(1/ar-ar))*cr THEN
!!				endSegment = startSegment+(halfWidth*(1/ar-ar)) + halfHeight/ar
!!			ENDIF
			DRAWINDEX 10
			GOSUB 100
			ratio = ar
			cont_pen = gs_cont_pen

			IF MEP_CoveredCableTray THEN
				startSegment = (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr - halfHeight/ar
				endSegment = (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr - halfHeight/ar
				MEP_CoveredCableTrayOn = 1

				DRAWINDEX 20
				GOSUB 11
			ENDIF

			ADD2 (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr, 0
			ratio = 1/ar
			cont_pen = SYMB_SECT_PEN
			DRAWINDEX 10
			GOSUB 12
			DEL 1

			ADD2 (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr, 0
			ratio = 1/ar
			cont_pen = SYMB_SECT_PEN
			PEN SYMB_SECT_PEN
			DRAWINDEX 40
			GOSUB 12
			DEL 1

			IF MEP_cline_show THEN		! -----> center line <----- !
				startSegment = (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr
				endSegment = (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT - SYMB_POS_Z_temp)/cr

				PEN MEP_cline_pen
				SET LINE_TYPE MEP_cline_type

				DRAWINDEX 30
				LINE2 startSegment, 0, endSegment, 0
			ENDIF
		ENDIF
		IF GLOB_CSTORY_ELEV < SYMB_POS_Z  + dz AND (GLOB_CSTORY_ELEV + GLOB_CSTORY_HEIGHT) - (SYMB_POS_Z  + dz) > -EPS THEN
!!			TEXT2 0, 0, "2c. case"

			startSegment = (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr + halfHeight/ar
!!			IF (GLOB_CSTORY_ELEV - SYMB_POS_Z_temp) < (halfWidth*(1/ar-ar))*cr THEN
!!				startSegment = startSegment+((halfWidth*(1/ar-ar))*cr-(GLOB_CSTORY_ELEV - SYMB_POS_Z_temp))/cr + halfHeight/ar
!!			ENDIF
			endSegment = MEP_StraightLength*br + halfHeight*ar
			DRAWINDEX 10
			GOSUB 100

			IF MEP_CoveredCableTray THEN
				startSegment = (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr - halfHeight/ar
				endSegment = MEP_StraightLength*br - halfHeight*ar

				DRAWINDEX 20
				GOSUB 11
			ENDIF

			ADD2 (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr, 0
			ratio = 1/ar
			cont_pen = SYMB_SECT_PEN
			DRAWINDEX 10
			GOSUB 12
			DEL 1

			IF MEP_cline_show THEN		! -----> center line <----- !
				startSegment = (GLOB_CSTORY_ELEV-SYMB_POS_Z_temp)/cr
				endSegment = MEP_StraightLength*br

				PEN MEP_cline_pen
				SET LINE_TYPE MEP_cline_type

				DRAWINDEX 30
				LINE2 startSegment, 0, endSegment, 0
			ENDIF

		ENDIF
	ENDIF

ENDIF

END

10:
	IF MEP_CoveredCableTray OR ABS(MEP_EmptySideID - 3) > EPS THEN LINE2 cableTrayHalfHeight, cableTrayHalfWidth, cableTrayHalfHeight, -cableTrayHalfWidth
	IF MEP_CoveredCableTray OR ABS(MEP_EmptySideID - 4) > EPS THEN LINE2 cableTrayHalfHeight, -cableTrayHalfWidth, -cableTrayHalfHeight, -cableTrayHalfWidth
	IF MEP_CoveredCableTray OR ABS(MEP_EmptySideID - 2) > EPS THEN LINE2 cableTrayHalfHeight, cableTrayHalfWidth, -cableTrayHalfHeight, cableTrayHalfWidth
	IF MEP_CoveredCableTray OR ABS(MEP_EmptySideID - 1) > EPS THEN LINE2 -cableTrayHalfHeight, cableTrayHalfWidth, -cableTrayHalfHeight, -cableTrayHalfWidth
RETURN

11:
	PUT startSegment, halfWidth, 1
	PUT endSegment, halfWidth, 1
	PUT endSegment, -halfWidth, 1
	PUT startSegment, -halfWidth, 1

	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

		GET (NSP)
RETURN

12:
	MUL2 ratio, 1
	LINE2 -halfHeight, halfWidth, halfHeight, halfWidth
	LINE2 -halfHeight, -halfWidth, halfHeight, -halfWidth
	PEN cont_pen
	IF MEP_CoveredCableTrayOn THEN LINE2 -halfHeight, halfWidth, -halfHeight, -halfWidth
	DEL 1
RETURN

100:
	LINE2 startSegment, halfWidth, endSegment, halfWidth
	LINE2 startSegment, -halfWidth, endSegment, -halfWidth

	FOR i = 1 TO ladderNum
		dist = ladderHalfDist+ladderDist*(i-1)
		startX = halfHeight*ar+(dist-ladderThk/2)*br
		endX = halfHeight*ar+(dist+ladderThk/2)*br

		IF startX > endSegment-EPS OR endX < startSegment+EPS THEN

		ELSE
			IF startX+EPS < startSegment AND startSegment < endX-EPS THEN
				startX = startSegment
				PEN SYMB_SECT_PEN
				LINE2 startX, halfWidth, startX, -halfWidth
				PEN gs_cont_pen
			ENDIF
			IF startX+EPS < endSegment AND endSegment < endX-EPS THEN
				endX = endSegment
				PEN SYMB_SECT_PEN
				LINE2 endX, halfWidth, endX, -halfWidth
				PEN gs_cont_pen
			ENDIF

			PUT startX, halfWidth, 1
			PUT endX, halfWidth, 1
			PUT endX, -halfWidth, 1
			PUT startX, -halfWidth, 1

			POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen,

				GET (NSP)
		ENDIF
	NEXT i
RETURN
