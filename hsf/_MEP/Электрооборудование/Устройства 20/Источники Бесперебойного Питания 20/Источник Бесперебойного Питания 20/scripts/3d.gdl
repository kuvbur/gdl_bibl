
! Uninterrupt. Power Supply


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res


DEFINE MATERIAL	"Shaft Black Plastic" 20,
	0.147059, 0.135294, 0.103922,
	0.9, 0.6, 0.2, 0, 10, 0,
	1, 0.978103, 0.968383,
	0, 0, 0,
	0, 0, 1,
	0

DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0

handleMaterial = "Shaft Aluminum"


				! --- SWITCHBOARD  --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, -B, 0, unID: unID=unID+1
HOTSPOT A, -B, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, -B, ZZYZX, unID: unID=unID+1
HOTSPOT A, -B, ZZYZX, unID: unID=unID+1


ADDy -B


ADDy 0.01
BLOCK A, B-0.01, ZZYZX
DEL 1

ROTx 90
ADDz -0.01
FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
	4, 0.01, 45, 0.01,
	0.005, ZZYZX-0.005, 15,
	A-0.005, ZZYZX-0.005, 15,
	A-0.005, 0, 15,
	0.005, 0, 15
DEL 2

IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed

	MATERIAL handleMaterial
	RESOL 8
	ADD 0.08, 0, ZZYZX/2
	ROTx 90
	CYLIND 0.05, 0.02
	ADDz 0.05
	PRISM 4, 0.01,
		-0.01, 0.015,
		-0.01,-0.12,
		 0.01,-0.12,
		 0.01, 0.015
	DEL 3
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple
	MATERIAL handleMaterial
	ADD 0.08-0.01, 0, ZZYZX/2
	ROTy 90
	PRISM 6, 0.02,
		-0.015, 0,
		-0.015,-0.06,
		 0.12, -0.06,
		 0.12, -0.05,
		 0.015,-0.05,
		 0.015, 0
	DEL 2
ENDIF

DEL 1
