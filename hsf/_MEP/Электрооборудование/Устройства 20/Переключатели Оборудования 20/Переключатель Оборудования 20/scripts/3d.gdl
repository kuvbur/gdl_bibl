
! Equipment Switch


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res


DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0

				! --- EQUIPMENT SWITCH --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT  A/2, 0, 0, unID: unID=unID+1
HOTSPOT -A/2, 0, 0, unID: unID=unID+1
HOTSPOT  A/2, -B, 0, unID: unID=unID+1
HOTSPOT -A/2, -B, 0, unID: unID=unID+1

HOTSPOT  A/2, 0, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, 0, ZZYZX, unID: unID=unID+1
HOTSPOT  A/2, -B, ZZYZX, unID: unID=unID+1
HOTSPOT -A/2, -B, ZZYZX, unID: unID=unID+1


IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed

	ADD -A/2, -B, 0

	ADDy 0.02
	BLOCK A, B-0.02, ZZYZX
	DEL 1
	ADD 0.005, 0, 0.005
	BLOCK A-0.01, 0.02, ZZYZX-0.01
	DEL 1

	RESOL 8
	dx = A / (numButtons)

	ADD dx/2, 0, ZZYZX/2
	ROTx 90
	FOR i=1 TO numButtons
		MATERIAL "Shaft Aluminum"
		PRISM_ 3, 0.01,
			0,0,979,
			0.022, 360,4079,
			0.018, 360,4079
		MATERIAL buttonMaterial
		CYLIND 0.005,0.018
		ADDx dx
	NEXT i
	DEL numButtons + 2

	ADD A/2,0,ZZYZX/4
	ROTx 90
	MATERIAL "Shaft Aluminum"
	PRISM_ 3, 0.01,
		0,0,979,
		0.022, 360,4079,
		0.018, 360,4079
	MATERIAL buttonMaterial
	CYLIND 0.005,0.018

	DEL 3
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple
	ADD -A/2, -B, 0

	BLOCK A, B, ZZYZX

	RESOL 6
	MATERIAL buttonMaterial
	dx = A / (numButtons)

	ADD dx/2, 0, ZZYZX/2
	ROTx 90
	FOR i=1 TO numButtons
		CYLIND 0.01,0.02
		ADDx dx
	NEXT i
	DEL numButtons + 2

	ADD A/2,0,ZZYZX/4
	ROTx 90
	CYLIND 0.01,0.02

	DEL 3
ENDIF
DEL TOP
