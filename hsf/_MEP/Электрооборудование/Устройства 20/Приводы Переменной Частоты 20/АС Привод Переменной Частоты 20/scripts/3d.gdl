
! Variable Frequency AC Drive


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res

DEFINE MATERIAL	"Shaft Black Plastic" 20,
	0.147059, 0.135294, 0.103922,
	0.9, 0.6, 0.2, 0, 10, 0,
	1, 0.978103, 0.968383,
	0, 0, 0,
	0, 0, 1,
	0

DEFINE MATERIAL	"Shaft White Plastic" 20,
	1, 1, 1,
	0.98, 0.8, 0.25, 0, 3, 0,
	1, 1, 1,
	0.654902, 0.654902, 0.654902,
	100, 0, -1,
	0

DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0


				! --- JUNCTION BOX --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, -B, 0, unID: unID=unID+1
HOTSPOT A, -B, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, -B, ZZYZX, unID: unID=unID+1
HOTSPOT A, -B, ZZYZX, unID: unID=unID+1


IF boxShapeID = 1 THEN
	ADDy -B+0.01
	BLOCK A, B-conDepth-0.01, ZZYZX
	DEL 1

	ROTx 90
	ADDz B-0.01
	FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
		4, 0.01, 45, 0.01,
		0.005, ZZYZX-0.005, 15,
		A-0.005, ZZYZX-0.005, 15,
		A-0.005, 0, 15,
		0.005, 0, 15
	DEL 2

	ADD 0.02, -conDepth, 0
	CUTPLANE -225
	ADDz -conDepth
	FOR i=1 TO 2
		SPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,
			6, 0,0, 1,0, ZZYZX+conDepth, -45,
			0,		0,			15,
			0,		conDepth,	15,
			4*conThk, conDepth, 15,
			4*conThk, conDepth-conThk, 15,
			conThk,	conDepth-conThk, 15,
			conThk,	0,			15

		ADDx -0.04+A
		MULx -1
	NEXT i
	CUTEND
	DEL 6
ENDIF
IF boxShapeID = 2 THEN
	ADDy -B+0.01
	BLOCK A, B-0.01, ZZYZX
	DEL 1

	ROTx 90
	ADDz B-0.01
	FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
		4, 0.01, 45, 0.01,
		0.005, ZZYZX-0.005, 15,
		A-0.005, ZZYZX-0.005, 15,
		A-0.005, 0, 15,
		0.005, 0, 15
	DEL 2
ENDIF

IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed
	ROTx 90
	ADD 0.7 * A, 0.4 * ZZYZX + 0.30, B+0.001
	MATERIAL "Shaft Aluminum"
	POLY_ 3,
		0,0, 901,
		0.03,360,4001,
		0.02,360,4001

	CYLIND 0.01,0.02
	ADDz 0.01
	PRISM 4, 0.01,
		-0.01, 0.02,
		-0.01,-0.06,
		 0.01,-0.06,
		 0.01, 0.02
	DEL 3

	MATERIAL	"Shaft White Plastic"
	ROTx 90
	ADD 0.7 * A, 0.4 * ZZYZX + 0.06, B + 0.001
	POLY_ 10,
		-0.04,0, 1,
		-0.04,0.12, 1,
		 0.04,0.12, 1,
		 0.04, 0, 1,
		-0.04,0, -1,
		-0.025,0.06, 1,
		-0.025,0.105, 1,
		 0.025,0.105, 1,
		 0.025,0.06, 1,
		-0.025,0.06, -1

	MATERIAL	"Shaft Black Plastic"
	POLY 4,
		-0.025,0.06,
		-0.025,0.105,
		 0.025,0.105,
		 0.025,0.06
	DEL 2

	MATERIAL	"Shaft White Plastic"
	ROTx 90
	ADD 0.7 * A, 0.3 * ZZYZX, B
	FOR i=1 TO 4
		FOR j=1 TO 2
			BLOCK 0.02,0.02,0.01
			ADDy 0.04
		NEXT j
		DEL 2
		ADDx -0.04
	NEXT i
	DEL 6
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple
	ROTx 90
	ADD 0.7 * A, 0.4 * ZZYZX + 0.30, B+0.001
	MATERIAL "Shaft Aluminum"
	ROTy 90
	ROTz -90
	ADDz -0.01
	PRISM 6, 0.02,
		-0.015, 0,
		-0.015,-0.02,
		 0.06, -0.02,
		 0.06, -0.01,
		 0.015,-0.01,
		 0.015, 0
	DEL 5


	MATERIAL	"Shaft White Plastic"
	ROTx 90
	ADD 0.7 * A, 0.4 * ZZYZX + 0.06, B + 0.001
	POLY_ 4,
		-0.04,0, 1,
		-0.04,0.12, 1,
		 0.04,0.12, 1,
		 0.04, 0, 1
	DEL 2

	MATERIAL	"Shaft White Plastic"
	ROTx 90
	ADD 0.7 * A, 0.3 * ZZYZX, B
	FOR i=1 TO 4
		FOR j=1 TO 2
			BLOCK 0.02,0.02,0.01
			ADDy 0.04
		NEXT j
		DEL 2
		ADDx -0.04
	NEXT i
	DEL 6
ENDIF

