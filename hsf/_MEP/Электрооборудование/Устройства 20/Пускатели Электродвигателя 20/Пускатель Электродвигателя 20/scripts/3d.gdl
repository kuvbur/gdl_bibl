
!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
! Motor Starter


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple

MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res


DEFINE MATERIAL	"Shaft Black Plastic" 20,
	0.147059, 0.135294, 0.103922,
	0.9, 0.6, 0.2, 0, 10, 0,
	1, 0.978103, 0.968383,
	0, 0, 0,
	0, 0, 1,
	0

DEFINE MATERIAL	"Shaft Aluminum" 20,
	0.854902, 0.913725, 0.905882,
	0.98, 0.7, 0.5, 0, 7, 0,
	0.907897, 0.907897, 0.907897,
	0, 0, 0,
	0, 0, 1,
	0


				! --- MOTOR STARTER --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT 0, -B, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT A, -B, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, -B, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, -B, ZZYZX, unID: unID=unID+1


ADDy -B+0.01
BLOCK A, B-0.01, ZZYZX
DEL 1

ROTx 90
ADDz B-0.01
FPRISM_ bodyMaterial,bodyMaterial,bodyMaterial,bodyMaterial,
	4, 0.01, 45, 0.01,
	0.005, ZZYZX-0.005, 15,
	A-0.005, ZZYZX-0.005, 15,
	A-0.005, 0, 15,
	0.005, 0, 15
DEL 2


IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed
	RESOL 8

	IF configTypeID = 1 THEN
		ADD A/4,-B-0.001,ZZYZX/2
		ROTx 90

		FOR i=1 TO 2
			GOSUB 100

			ADDy 0.1
			MATERIAL "Shaft Aluminum"
			POLY_ 10,
				-0.055, 0.025, 1,
				-0.055,-0.025, 1,
				 0.055,-0.025, 1,
				 0.055, 0.025, 1,
				-0.055, 0.025, -1,
				-0.045, 0.015, 1,
				-0.045,-0.015, 1,
				 0.045,-0.015, 1,
				 0.045, 0.015, 1,
				-0.045, 0.015, -1
			MATERIAL "Shaft Black Plastic"
			POLY_ 4,
				-0.045, 0.015, 1,
				-0.045,-0.015, 1,
				 0.045,-0.015, 1,
				 0.045, 0.015, 1
			ADDy 0.07
			MATERIAL "Shaft Aluminum"
			POLY_ 10,
				-0.055, 0.015, 1,
				-0.055,-0.015, 1,
				 0.055,-0.015, 1,
				 0.055, 0.015, 1,
				-0.055, 0.015, -1,
				-0.045, 0.005, 1,
				-0.045,-0.005, 1,
				 0.045,-0.005, 1,
				 0.045, 0.005, 1,
				-0.045, 0.005, -1
			MATERIAL "Shaft Black Plastic"
			POLY_ 4,
				-0.045, 0.005, 1,
				-0.045,-0.005, 1,
				 0.045,-0.005, 1,
				 0.045, 0.005, 1
			DEL 2

			ADDx A/2
		NEXT i
		DEL 4
	ENDIF
	IF configTypeID = 2 THEN
		ADD A/4,-B-0.001,ZZYZX/2
		ROTx 90

		FOR i=1 TO 2
			FOR j=1 TO 3
				ADDy -0.04
				GOSUB 100
				DEL 1
				ROTz 120
			NEXT j
			DEL 3
			ADDx A/2
		NEXT i
		DEL 4
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple
	RESOL 6

	IF configTypeID = 1 THEN
		ADD A/4,-B-0.001,ZZYZX/2
		ROTx 90

		FOR i=1 TO 2
			GOSUB 200

			ADDy 0.1
			MATERIAL "Shaft Black Plastic"
			POLY_ 4,
				-0.055, 0.025, 1,
				-0.055,-0.025, 1,
				 0.055,-0.025, 1,
				 0.055, 0.025, 1
			ADDy 0.07
			POLY_ 4,
				-0.055, 0.015, 1,
				-0.055,-0.015, 1,
				 0.055,-0.015, 1,
				 0.055, 0.015, 1
			DEL 2

			ADDx A/2
		NEXT i
		DEL 4
	ENDIF
	IF configTypeID = 2 THEN
		ADD A/4,-B-0.001,ZZYZX/2
		ROTx 90

		FOR i=1 TO 2
			FOR j=1 TO 3
				ADDy -0.04
				GOSUB 200
				DEL 1
				ROTz 120
			NEXT j
			DEL 3
			ADDx A/2
		NEXT i
		DEL 4
	ENDIF
ENDIF


END

100:
	MATERIAL "Shaft Aluminum"
	PRISM_ 3, 0.01,
		0,0,979,
		0.022, 360,4079,
		0.018, 360,4079
	MATERIAL buttonMaterial
	CYLIND 0.005,0.018
RETURN

200:
	MATERIAL buttonMaterial
	CYLIND 0.01,0.02
RETURN

