!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

! Automatic Transfer Switch


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

res = 24	! Detailed
IF gs_detlevel_3D_m = 1 THEN res = 8	! Simple


MATERIAL bodyMaterial
PEN gs_cont_pen
RESOL res


				! --- AUTOMATIC TRANSFER SWITCH --- !

unID = 1
HOTSPOT 0, 0, 0, unID: unID=unID+1
HOTSPOT A, 0, 0, unID: unID=unID+1
HOTSPOT 0, B, 0, unID: unID=unID+1
HOTSPOT A, B, 0, unID: unID=unID+1

HOTSPOT 0, 0, ZZYZX, unID: unID=unID+1
HOTSPOT A, 0, ZZYZX, unID: unID=unID+1
HOTSPOT 0, B, ZZYZX, unID: unID=unID+1
HOTSPOT A, B, ZZYZX, unID: unID=unID+1


IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT <> 23 THEN	! Detailed

	ADDy 0.02
	BLOCK A, B-0.02, ZZYZX
	DEL 1
	ADD 0.005, 0, 0.005
	BLOCK A-0.01, 0.02, ZZYZX-0.01
	DEL 1

	MATERIAL handleMaterial
	RESOL 8
	ADD 0.08, 0, ZZYZX/2
	ROTx 90
	CYLIND 0.05, 0.02
	ADDz 0.05
	PRISM 4, 0.01,
		-0.01, 0.015,
		-0.01,-0.12,
		 0.01,-0.12,
		 0.01, 0.015
	DEL 3

ENDIF

IF gs_detlevel_3D_m = 1 AND GLOB_CONTEXT <> 23  THEN	! Simple
	BLOCK A, B, ZZYZX

	MATERIAL handleMaterial
	ADD 0.08-0.01, 0, ZZYZX/2
	ROTy 90
	PRISM 6, 0.02,
		-0.015, 0,
		-0.015,-0.06,
		 0.12, -0.06,
		 0.12, -0.05,
		 0.015,-0.05,
		 0.015, 0
	DEL 2
ENDIF
