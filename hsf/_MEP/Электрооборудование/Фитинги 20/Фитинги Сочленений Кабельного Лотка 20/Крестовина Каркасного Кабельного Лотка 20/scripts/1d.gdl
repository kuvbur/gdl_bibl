
! Cable Ladder Cross


EPS = 0.00001

HptsUnIDBeginForConnEnds = 30000

hceID_1 = HptsUnIDBeginForConnEnds+100
hceID_2 = HptsUnIDBeginForConnEnds+200
hceID_3 = HptsUnIDBeginForConnEnds+300
hceID_4 = HptsUnIDBeginForConnEnds+400

DIM CrossTypeStr_dim[2]
	CrossTypeStr_dim[1] = `Радиус`
	CrossTypeStr_dim[2] = `Со Скосом`


IF GLOB_MODPAR_NAME = "MEP_CrossType" THEN
	FOR i = 1 TO 2
		IF MEP_CrossType = CrossTypeStr_dim[i] THEN
			MEP_CrossTypeID = i
			PARAMETERS MEP_CrossTypeID = MEP_CrossTypeID
			i = 2
		ENDIF
	NEXT i
ENDIF
IF MEP_CrossType <> CrossTypeStr_dim[MEP_CrossTypeID] THEN
	MEP_CrossType = CrossTypeStr_dim[MEP_CrossTypeID]
	PARAMETERS MEP_CrossType = MEP_CrossType
ENDIF

IF ABS(MEP_CrossTypeID - 1) < EPS THEN
	CrossRes = 12
	IF gs_detlevel_3D = `Простой` THEN CrossRes = 2
ENDIF


						! ------------------ !
						! --- Properties --- !
						! ------------------ !

cableTrayWidth			= MEP_NumConnectionData[1][12]
cableTrayWidthB			= MEP_NumConnectionData[3][12]
cableTrayHeight			= MEP_NumConnectionData[1][13]
cableTrayHalfWidth		= MEP_NumConnectionData[1][12]/2
cableTrayHalfWidthB		= MEP_NumConnectionData[3][12]/2
cableTrayHalfWidthC		= MEP_NumConnectionData[4][12]/2
cableTrayHalfHeight		= MEP_NumConnectionData[1][13]/2


						! ---------------- !
						! --- Z vector --- !
						! ---------------- !

dZx = -MEP_Position[3][2]*MEP_Position[2][3]+MEP_Position[3][3]*MEP_Position[2][2]
dZy = -MEP_Position[3][3]*MEP_Position[2][1]+MEP_Position[3][1]*MEP_Position[2][3]
dZz = -MEP_Position[3][1]*MEP_Position[2][2]+MEP_Position[3][2]*MEP_Position[2][1]


					!!! +++ Transformational Matrix +++ !!!

DIM tMatrix[3][3]

tMatrix[1][1] = MEP_Position[2][1]
tMatrix[1][2] = MEP_Position[2][2]
tMatrix[1][3] = MEP_Position[2][3]

tMatrix[2][1] = MEP_Position[3][1]
tMatrix[2][2] = MEP_Position[3][2]
tMatrix[2][3] = MEP_Position[3][3]

tMatrix[3][1] = dZx
tMatrix[3][2] = dZy
tMatrix[3][3] = dZz


						!!! +++ Input Coordinates +++ !!!

PUT ConPosX_1, ConPosY_1, ConPosZ_1
PUT gs_Connections[1][4], gs_Connections[1][5], gs_Connections[1][6]
PUT gs_Connections[1][7], gs_Connections[1][8], gs_Connections[1][9]

PUT ConPosX_2, ConPosY_2, ConPosZ_2
PUT gs_Connections[2][4], gs_Connections[2][5], gs_Connections[2][6]
PUT gs_Connections[2][7], gs_Connections[2][8], gs_Connections[2][9]

PUT ConPosX_3, ConPosY_3+ConLength_3, ConPosZ_3
PUT gs_Connections[3][4], gs_Connections[3][5], gs_Connections[3][6]
PUT gs_Connections[3][7], gs_Connections[3][8], gs_Connections[3][9]

PUT ConPosX_4, ConPosY_4-ConLength_4, ConPosZ_4
PUT gs_Connections[4][4], gs_Connections[4][5], gs_Connections[4][6]
PUT gs_Connections[4][7], gs_Connections[4][8], gs_Connections[4][9]

PUT ConPosX_3, 0, 0

Coordinates = NSP/3

DIM inCoords[20][3]

FOR cnum = 1 TO Coordinates
	inCoords[cnum][1] = GET(1)
	inCoords[cnum][2] = GET(1)
	inCoords[cnum][3] = GET(1)
NEXT cnum


! --- the Matrix Operation, what make the Output-Coordinates

DIM outCoords[20][3]

FOR cnum = 1 TO Coordinates
	outCoords[cnum][1] = inCoords[cnum][1]*tMatrix[1][1]+inCoords[cnum][2]*tMatrix[2][1]+inCoords[cnum][3]*tMatrix[3][1]
	outCoords[cnum][2] = inCoords[cnum][1]*tMatrix[1][2]+inCoords[cnum][2]*tMatrix[2][2]+inCoords[cnum][3]*tMatrix[3][2]
	outCoords[cnum][3] = inCoords[cnum][1]*tMatrix[1][3]+inCoords[cnum][2]*tMatrix[2][3]+inCoords[cnum][3]*tMatrix[3][3]
NEXT cnum


						! --------------------- !
						! --- Set Side Flag --- !
						! --------------------- !

MEP_EmptySideID = 1
IF SGN(dZz) = -1 THEN MEP_EmptySideID = 3
PARAMETERS MEP_EmptySideID = MEP_EmptySideID



						! ---------------------------- !
						! --- Top and Bottom Level --- !
						! ---------------------------- !

z = MEP_Position[1][3] + MEP_Position[2][3]*MEP_StraightLength/2
PARAMETERS ac_toplevel 		= z
PARAMETERS ac_bottomlevel 	= z
