
! CasingTrapezoid

PEN gs_frame_pen


material gs_trim_out_mat

! ==============================================================================
! Casing in Straight Wall
! ==============================================================================

if abs(WIDO_ORIG_DIST)<EPS then

	if abs(WALL_INCL)<EPS then

		! ------------------------------------------------------------------------------
		! Corner cuts
		! ------------------------------------------------------------------------------

		muly -1
		if WIDO_REVEAL_SIDE then
			mulz -1
			addz -WIDO_FRAME_THICKNESS
		endif

		numCuts = 0

		! --- Cut on board side

		! Right Corner
		if bRightCornerFunction then
			! --- Cut right side in case of Corner Window ---

			if bFitMode then
				add rightCwDx + rightCwDx2 + rightWidth - (WALL_THICKNESS - WIDO_SILL - WIDO_FRAME_THICKNESS + gs_trim_thk_in) * rx, 0, WALL_THICKNESS - WIDO_SILL + gs_trim_thk_in
				rotx 90

				maxDist  = WALL_THICKNESS - min(0, WIDO_SILL) + gs_trim_thk_out + gs_trim_thk_in

				if abs(maxDist) > EPS then
					cutpolya 4, 2, 0,
						0, 0, 10,
						maxDist / tan(rightCornerAngle / 2), -maxDist, 10,
						A + A, -maxDist, 8,
						A + A, 0, 8

					numCuts = numCuts + 1
				endif
				del 2
			else
				rotx 90

				cutpolya 5, 2, 0,
					A + A + gs_tw_right_in, 																								WALL_THICKNESS - WIDO_SILL + MAX(gs_board_nosing, gs_trim_thk_in),		8,
					rightCwDx + rightCwDx2 + rightWidth - (WALL_THICKNESS - WIDO_SILL - WIDO_FRAME_THICKNESS + MAX(gs_board_nosing, gs_trim_thk_in)) * rx,	WALL_THICKNESS - WIDO_SILL + MAX(gs_board_nosing, gs_trim_thk_in),		10,
					rightCwDx + rightWidth - rightCwDx2 * COS(rightCornerAngle),															WIDO_FRAME_THICKNESS + rightCwDx2 * SIN(rightCornerAngle),	8,
					rightCwDx + rightWidth,																										WIDO_FRAME_THICKNESS,						8,
					A + A + gs_tw_right_in,																									WIDO_FRAME_THICKNESS,						8

				numCuts = numCuts + 1
				del 1
			endif
		endif

		! Left Corner
		if bLeftCornerFunction then

			! --- Cut left side in case of Corner Window ---

			if bFitMode then
				add -leftCwDx - leftCwDx2 - leftWidth + (WALL_THICKNESS - WIDO_SILL- WIDO_FRAME_THICKNESS + gs_trim_thk_in) * lx, 0, WALL_THICKNESS - WIDO_SILL + gs_trim_thk_in
				rotx 90

				maxDist  = WALL_THICKNESS - min(0, WIDO_SILL) + gs_trim_thk_out + gs_trim_thk_in

				if abs(maxDist) > EPS then
					cutpolya 4, 1, 0,
						0, 0, 10,
						-maxDist / tan(leftCornerAngle / 2), -maxDist, 10,
						-A - A, -maxDist, 8,
						-A - A, 0, 8

					numCuts = numCuts + 1
				endif
				del 2
			else

				rotx 90

				cutpolya 5, 1, 0,
					-A - A - gs_tw_left_in, 																								WALL_THICKNESS - WIDO_SILL + MAX(gs_board_nosing, gs_trim_thk_in),		8,
					-leftCwDx - leftCwDx2 - leftWidth + (WALL_THICKNESS - WIDO_SILL - WIDO_FRAME_THICKNESS + MAX(gs_board_nosing, gs_trim_thk_in)) * lx,	WALL_THICKNESS - WIDO_SILL + MAX(gs_board_nosing, gs_trim_thk_in),		10,
					-leftCwDx - leftWidth + leftCwDx2 * COS(leftCornerAngle),															WIDO_FRAME_THICKNESS + leftCwDx2 * SIN(leftCornerAngle),	8,
					-leftCwDx - leftWidth,																										WIDO_FRAME_THICKNESS,						8,
					-A - A - gs_tw_left_in,																									WIDO_FRAME_THICKNESS,						8

				numCuts = numCuts + 1
				del 1
			endif
		endif

		! --- Cut on sill side ---

		! Right Corner
		if bRightCornerFunction then
			! --- Cut right side in case of Corner Window ---

			add rightCwDx + rightWidth + WIDO_FRAME_THICKNESS * rx, 0, 0
			rotx 90

			maxDist  = WIDO_SILL + gs_trim_thk_out

			if abs(maxDist) > EPS then
				cutpolya 4, 1, 0,
					0, 0, 8,
					maxDist / tan(rightCornerAngle / 2), -maxDist, 10,
					A + A, -maxDist, 8,
					A + A, 0, 8

				numCuts = numCuts + 1
			endif

			del 2
		endif

		! Left Corner
		if bLeftCornerFunction then
			! --- Cut left side in case of Corner Window ---

			add -leftCwDx - leftWidth - WIDO_FRAME_THICKNESS * lx, 0, 0
			rotx 90

			maxDist  = WIDO_SILL + gs_trim_thk_out		!!WALL_THICKNESS

			if abs(maxDist) > EPS then
				cutpolya 4, 1, 0,
					0, 0, 8,
					-maxDist / tan(leftCornerAngle / 2), -maxDist, 10,
					-A - A, -maxDist, 8,
					-A - A, 0, 8

				numCuts = numCuts + 1
			endif

			del 2
		endif

		del 1
		if WIDO_REVEAL_SIDE then del 2



		if WIDO_REVEAL_SIDE then
			addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS    ! Flip
		else
			addz -WIDO_SILL
		endif

		if WIDO_REVEAL_SIDE then
			mulz -1
			addz -WALL_THICKNESS
		endif

		if bDrawInsideTrim then
			sect_fill gs_trim_fill_in, gs_trim_pen_bg_in, gs_trim_pen_fg_in, gs_trim_pen_cont
			material gs_trim_in_mat

			numCutsCornerColumn = 0
			if gs_corner_column then
				matTrim = gs_trim_in_mat
				gosub 300
			endif

			!!! cut with board-----------------------------
			if bDrawBoard then
				add 0, -boardSideHeight, WIDO_FRAME_THICKNESS
				rotx -90 - boardSideAngle

				material gs_trim_in_mat
				cutplane 1, 1, 0, 1

				del 2
			endif

			! Casing in Wallhole
			if gs_trim_width_in > 0 then
				px1L = -leftWidth+gs_trim_width_in-overSizeLeftX
				px2L = px1L - gs_trim_width_in
				px1R = rightWidth-gs_trim_width_in+overSizeRightX
				px2R = px1R + gs_trim_width_in

				if iWindowShape = SHAPE_PARALLELOGRAM then
					py1BL =	ArchHeight + dytth1_in - overSizeLeftY
					py2BL = py1BL - dytth1_in

					py1BR =	dytth2_in - overSizeRightY
					py2BR = py1BR - dytth2_in
				else
					py1BL =	gs_trim_width_in*not(bDrawBoard)
					py2BL = py1BL-gs_trim_width_in*not(bDrawSill)

					py1BR =	py1BL
					py2BR = py2BL
				endif

				py1TL = b-dytth2_in+overSizeLeftY
				py2TL = py1TL + dytth2_in
				py1TR = b-ArchHeight-dytth1_in-overSizeRightY
				py2TR = py1TR + dytth1_in

				addz WIDO_SILL+WIDO_FRAME_THICKNESS
				prism_ 9, WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS,
					px2L, py2BL, 15,
					px1L, py1BL, jambExtInMask,
					px1L, py1TL, jambExtInMask,
					px1R, py1TR, jambExtInMask,
					px1R, py1BR, jambExtInMask,
					px2R, py2BR, 15,
					px2R, py2TR, 15,
					px2L, py2TL, 15,
					px2L, py2BL, -1

				if not(bDrawBoard) then
					base
					prism_ 5, WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS,
						px1L, py1BL, jambExtInMask,
						px1R, py1BR, jambExtInMask,
						px2R, py2BR, 15,
						px2L, py2BL, jambExtInMask,
						px1L, py1BL, -1
					vert 0, 0, 0
					vert 1, 0, 0
					vert 0, 1, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				endif

				del 1
			endif

			! Casing on Wall
			if gs_trim_thk_in > 0 then
				px1L = -leftWidth+gs_trim_width_in-gs_trim_offs_in-overSizeLeftX
				px2L = px1L - gs_tw_right_in
				px1R = rightWidth-gs_trim_width_in+gs_trim_offs_in+overSizeRightX
				px2R = px1R + gs_tw_right_in

				py1TL = b-dytis2+overSizeLeftY
				py2TL = py1TL + idytw2
				py1TR = b-ArchHeight-dytis1-overSizeRightY
				py2TR = py1TR + idytw1

				if iWindowShape = SHAPE_PARALLELOGRAM then
					py1BL =	ArchHeight + dytis1 + overSizeLeftY
					py2BL = py1BL - idytw1

					py1BR =	dytis2 - overSizeRightY
					py2BR = py1BR - idytw2
				else
					if bDrawBoard then
						py1BL = gs_board_thk
						py2BL = py1BL
					else
						py1BL = gs_trim_width_in - gs_trim_offs_in
						py2BL = py1BL - gs_tw_bottom_in
					endif

					py1BR =	py1BL
					py2BR = py2BL
				endif

				addz WALL_THICKNESS

				if not(bLeftCornerFunction) then
					base
					PRISM_ 4, gs_trim_thk_in,
						px1L, py1BL, 10 + 5 * bDrawBoard,
						px2L, py2BL, 15,
						px2L, py2TL, 10,
						px1L, py1TL, casInMask
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				endif

				if not(bRightCornerFunction) then
					base
					PRISM_ 4,gs_trim_thk_in,
						px1R, py1BR, 10 + 5 * bDrawBoard,
						px2R, py2BR, 15,
						px2R, py2TR, 10,
						px1R, py1TR, casInMask
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				endif

				base
				PRISM_ 4,gs_trim_thk_in,
					px2L, py2TL, 10,
					px1L, py1TL, casInMask,
					px1R, py1TR, 10,
					px2R, py2TR, 15
				vert 0, 0, 0
				vert cos(palpha), -sin(palpha), 0
				vert sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				if not(bDrawBoard) then
					base

					PRISM_ 4,gs_trim_thk_in,
						px2R, py2BR, 10,
						px1R, py1BR, casInMask,
						px1L, py1BL, 10,
						px2L, py2BL, 15

					if iWindowShape = SHAPE_PARALLELOGRAM then
						vert 0, 0, 0
						vert cos(palpha), -sin(palpha), 0
						vert sin(palpha), cos(palpha), 0
						vert 0, 0, 1
					else
						vert 0, 0, 0
						vert 1, 0, 0
						vert 0, 1, 0
						vert 0, 0, 1
					endif
					coor 2, -1, -2, -3, -4
					body 1
				endif
				del 1
			endif

			if bDrawBoard then cutend

			for i=1 to numCutsCornerColumn
				cutend
			next i
		endif

		if bDrawOutsideTrim then gosub "Outside Casing"

		if WIDO_REVEAL_SIDE then
			del 2
		endif

		del 1

		for i = 1 to numCuts
			cutend
		next i

	endif

endif


! ==============================================================================
! Casing in Inclined Wall
! ==============================================================================

gs_trim_offs = gs_trim_offs_out

if abs(WIDO_ORIG_DIST)<EPS then

	if abs(WALL_INCL)>EPS then

		if not(WIDO_REVEAL_SIDE) then addz -WIDO_SILL else addz WIDO_SILL-WALL_THICKNESS+WIDO_FRAME_THICKNESS    ! Flip

		if WIDO_REVEAL_SIDE then
			MULZ -1
			addz -WALL_THICKNESS
		endif

		if bDrawInsideTrim then
			sect_fill gs_trim_fill_in, gs_trim_pen_bg_in, gs_trim_pen_fg_in, gs_trim_pen_cont
			material gs_trim_in_mat

			!!! cut with board-----------------------------
			if bDrawBoard then
				add 0, -boardSideHeight, WIDO_FRAME_THICKNESS
				rotx -90 - boardSideAngle

				material gs_trim_in_mat
				cutplane 1, 1, 0, 1

				del 2
			endif

			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then MULX -1  ! Rotated or mirrored window

			ROTX 90
			if WALL_INCL > 0 then
				cutpolya 4, 1, 0,
				!prism_ 4, 1,
				   -leftWidth,  WALL_THICKNESS-TAN(WALL_INCL)*(leftWidth),  cutInMask,
					rightWidth,  WALL_THICKNESS+TAN(WALL_INCL)*(rightWidth),   cutInMask,
				   -leftWidth,  WALL_THICKNESS+TAN(WALL_INCL)*(rightWidth),   cutInMask,
				   -leftWidth,  WALL_THICKNESS-TAN(WALL_INCL)*(leftWidth),  -1
			endif

			if WALL_INCL < 0 then
				cutpolya 4, 1, 0,
				!!prism_ 4, 1,
				   -leftWidth,  WALL_THICKNESS-TAN(WALL_INCL)*(leftWidth),   cutInMask,
					rightWidth,  WALL_THICKNESS-TAN(WALL_INCL)*(leftWidth),   cutInMask,
					rightWidth,  WALL_THICKNESS+TAN(WALL_INCL)*(rightWidth),   cutInMask,
				   -leftWidth,  WALL_THICKNESS-TAN(WALL_INCL)*(leftWidth),  -1
			endif
			del 1

			addz WIDO_SILL+WIDO_FRAME_THICKNESS
			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then MULX -1

			! Casing in Wallhole
			if gs_trim_width_in > 0 then
				px1L = -leftWidth+gs_trim_width_in
				px2L = px1L - gs_trim_width_in
				px1R = rightWidth-gs_trim_width_in
				px2R = px1R + gs_trim_width_in

				py1TL = b-dytth2_in
				py2TL = py1TL + dytth2_in
				py1TR = b-ArchHeight-dytth1_in
				py2TR = py1TR + dytth1_in

				if iWindowShape = SHAPE_PARALLELOGRAM then
					py1BL =	ArchHeight + dytth1_in
					py2BL = py1BL - dytth1_in

					py1BR =	dytth2_in
					py2BR = py1BR - dytth2_in
				else
					if bDrawBoard then
						py1BL = 0
						py2BL = py1BL
					else
						py1BL = gs_trim_width_in
						py2BL = py1BL - gs_trim_width_in
					endif

					py1BR =	py1BL
					py2BR = py2BL
				endif

				prism_ 9, WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS+ABS(TAN(WALL_INCL))*(leftWidth),
					px2L, py2BL, 15,
					px1L, py1BL, jambExtInMask,
					px1L, py1TL, jambExtInMask,
					px1R, py1TR, jambExtInMask,
					px1R, py1BR, jambExtInMask,
					px2R, py2BR, 15,
					px2R, py2TR, 15,
					px2L, py2TL, 15,
					px2L, py2BL, -1

				if not(bDrawBoard) then
					base
					prism_ 5, WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS+ABS(TAN(WALL_INCL))*(leftWidth),
						px1L, py1BL, jambExtInMask,
						px1R, py1BR, jambExtInMask,
						px2R, py2BR, 15,
						px2L, py2BL, jambExtInMask,
						px1L, py1BL, -1
					vert 0, 0, 0
					vert 1, 0, 0
					vert 0, 1, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				endif
			endif

			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then del 1
			del 1

			cutend


			if WIDO_REVEAL_SIDE then lwam=3
			if abs(gs_trim_offs_in)>EPS then lwam=-1

			px1L = -leftWidth+gs_trim_width_in-gs_trim_offs_in
			px1R = rightWidth-gs_trim_width_in+gs_trim_offs_in

			py1TL = b-dytis2
			py1TR = b-ArchHeight-dytis1

			if iWindowShape = SHAPE_PARALLELOGRAM then
				py1BL =	ArchHeight + dytis1

				py1BR =	dytis2
			else
				if bDrawBoard then
					py1BL = 0
				else
					py1BL = gs_trim_width_in - gs_trim_offs_in
				endif

				py1BR =	py1BL
			endif

			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then MULX -1

			cutpolya 5, 1, 0,
				px1L, py1BL, cutInMask,
				px1R, py1BR, cutInMask,
				px1R, py1TR, cutInMask,
				px1L, py1TL, cutInMask,
				px1L, py1BL,-1

			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then del 1

			addz WALL_THICKNESS
			ROTY -WALL_INCL

			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then MULX -1

			! Casing on Wall
			if gs_trim_thk_in > 0 then
				! Need an offset because of the previous cutpolya
				dxInclined = abs(tan(WALL_INCL))*gs_trim_thk_in

				palpha_inclined=atn(ArchHeight / ((leftWidth + rightWidth))/cos(WALL_INCL))
				pbeta1_inclined=(90+palpha_inclined)/2
				pbeta2_inclined=(90-palpha_inclined)/2

				dytis1_inclined=(gs_trim_width_in-gs_trim_offs_in+dxInclined)/tan(pbeta1_inclined)   		 !dy at trim thickness
				dytis2_inclined=(gs_trim_width_in-gs_trim_offs_in+dxInclined)/tan(pbeta2_inclined)

				idytw1_inclined=(gs_tw_right_in+dxInclined)/tan(pbeta1_inclined)     !dy at inside trim width
				idytw2_inclined=(gs_tw_right_in+dxInclined)/tan(pbeta2_inclined)

				px1L = (-leftWidth+gs_trim_width_in)/cos(WALL_INCL) - gs_trim_offs_in + dxInclined
				px2L = px1L - gs_tw_right_in - dxInclined
				px1R = (rightWidth-gs_trim_width_in)/cos(WALL_INCL) + gs_trim_offs_in - dxInclined
				px2R = px1R + gs_tw_right_in + dxInclined

				py1TL = b-dytis2_inclined
				py2TL = py1TL + idytw2_inclined
				py1TR = b-ArchHeight-dytis1_inclined
				py2TR = py1TR + idytw1_inclined

				if iWindowShape = SHAPE_PARALLELOGRAM then
					py1BL =	ArchHeight + dytis1_inclined
					py2BL = py1BL - idytw1_inclined

					py1BR =	dytis2_inclined
					py2BR = py1BR - idytw2_inclined
				else
					if bDrawBoard then
						py1BL = gs_board_thk
						py2BL = py1BL
					else
						py1BL = gs_trim_width_in - gs_trim_offs_in
						py2BL = py1BL - gs_tw_bottom_in
					endif

					py1BR =	py1BL
					py2BR = py2BL
				endif

				base
				prism_ 4, gs_trim_thk_in,
					px1L, py1BL, 10 + 5 * bDrawBoard,
					px2L, py2BL, 15,
					px2L, py2TL, 10,
					px1L, py1TL, casInMask
				vert 0, 0, 0
				vert 0, 1, 0
				vert 1, 0, 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				base
				prism_ 4,gs_trim_thk_in,
					px1R, py1BR, 10 + 5 * bDrawBoard,
					px2R, py2BR, 15,
					px2R, py2TR, 10,
					px1R, py1TR, casInMask
				vert 0, 0, 0
				vert 0, 1, 0
				vert 1, 0, 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				base
				prism_ 4,gs_trim_thk_in,
					px2L, py2TL, 10,
					px1L, py1TL, casInMask,
					px1R, py1TR, 10,
					px2R, py2TR, 15
				vert 0, 0, 0
				vert cos(palpha), -sin(palpha), 0
				vert sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				if not(bDrawBoard) then
					base
					prism_ 4,gs_trim_thk_in,
						px2R, py2BR, 10,
						px1R, py1BR, casInMask,
						px1L, py1BL, 10,
						px2L, py2BL, 15

					if iWindowShape = SHAPE_PARALLELOGRAM then
						vert 0, 0, 0
						vert cos(palpha), -sin(palpha), 0
						vert sin(palpha), cos(palpha), 0
						vert 0, 0, 1
					else
						vert 0, 0, 0
						vert 1, 0, 0
						vert 0, 1, 0
						vert 0, 0, 1
					endif
					coor 2, -1, -2, -3, -4
					body 1
				endif
			endif

			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then del 1
			del 2

			cutend

			if not(SYMB_MIRRORED) AND abs(SYMB_ROTANGLE-180)<EPS OR SYMB_MIRRORED AND abs(SYMB_ROTANGLE)<EPS then del 1

			if bDrawBoard then cutend

		endif

		if bDrawOutsideTrim then gosub "Outside Casing"

		if WIDO_REVEAL_SIDE then
			del 2
		endif

		del 1

		BODY -1
	endif

endif


! ==============================================================================
! Casing in Curved Wall
! ==============================================================================

if abs(WIDO_ORIG_DIST)>EPS then

	if bDrawInsideTrim then
		sect_fill gs_trim_fill_in, gs_trim_pen_bg_in, gs_trim_pen_fg_in, gs_trim_pen_cont
		material gs_trim_in_mat

		!!! cut with board-----------------------------
		if bDrawBoard then
			add 0, -boardSideHeight, WIDO_FRAME_THICKNESS
			rotx -90 - boardSideAngle

			material gs_trim_in_mat
			cutplane 1, 1, 0, 1

			del 2
		endif

		! Casing on Wall

		off1=0-gs_trim_thk_in
		off2=0
		off3=WALL_THICKNESS
		off4=-WALL_THICKNESS-gs_trim_thk_in
		gosub "Curved Wall Calculation"

		addz k_weitoffset

		mulz mulstat
		addz -rfur
		roty -AC_Hole_Position_Angle
		addz rfur

		resol 2*WALL_RESOL

		if iWindowShape = SHAPE_PARALLELOGRAM then
			py1BL =	ArchHeight + dytth1_in
			py2BL = py1BL - idytw1

			py1BR =	dytth2_in
			py2BR = py1BR - idytw2
		else
			if bDrawBoard then
				py1BL = gs_board_thk
				py2BL = py1BL
			else
				py1BL = gs_trim_width_in - gs_trim_offs_in
				py2BL = py1BL - gs_tw_bottom_in
			endif
			py1BR =	py1BL
			py2BR = py2BL
		endif

		if SYMB_ROTANGLE > 0 exor WIDO_REVEAL_SIDE then
			sideBit = 0
		else
			sideBit = 2
		endif

		if gs_trim_thk_in > 0 then
			bprism_ gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,4,gs_trim_thk_in,rfur,
				xoffset-ax/2-gs_tw_right_in+gs_trim_width_in, py2BL,15,
				xoffset-ax/2-gs_tw_right_in+gs_trim_width_in, b-dytth2_in+idytw2,bitset(btim1, sideBit,0),
				xoffset-ax/2+gs_trim_width_in, b-dytth2_in,btim1,
				xoffset-ax/2+gs_trim_width_in, py1BL,bitset(btim1, sideBit,abs(py1BL - py2BL) < EPS)

			bprism_ gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,4,gs_trim_thk_in,rfur,
				xoffset-ax/2-gs_tw_right_in+gs_trim_width_in, b-dytth2_in+idytw2,bitset(btim1, sideBit,0),
				xoffset-ax/2+gs_trim_width_in, b-dytth2_in,btim1,
				xoffset+ax/2-gs_trim_width_in, b-ArchHeight-dytth1_in,bitset(btim1, sideBit,0),
				xoffset+ax/2+gs_tw_right_in-gs_trim_width_in, b-ArchHeight-dytth1_in+idytw1,15

			bprism_ gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,4,gs_trim_thk_in,rfur,
				xoffset+ax/2+gs_tw_right_in-gs_trim_width_in, py2BR,15,
				xoffset+ax/2+gs_tw_right_in-gs_trim_width_in, b-ArchHeight-dytth1_in+idytw1,bitset(btim1, sideBit,0),
				xoffset+ax/2-gs_trim_width_in, b-ArchHeight-dytth1_in,btim1,
				xoffset+ax/2-gs_trim_width_in, py1BR,bitset(btim1, sideBit,abs(py1BR - py2BR) < EPS)

			if abs(py1BL - py2BL) > EPS then
				bprism_ gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,4,gs_trim_thk_in,rfur,
					xoffset-ax/2-gs_tw_right_in+gs_trim_width_in, py2BL,bitset(btim1, sideBit,0),
					xoffset-ax/2+gs_trim_width_in, py1BL,btim1,
					xoffset+ax/2-gs_trim_width_in, py1BR,bitset(btim1, sideBit,0),
					xoffset+ax/2+gs_tw_right_in-gs_trim_width_in, py2BR,15
			endif
		endif

		del 5


		! Casing in Wallhole

		!cutplanes at frame

		if WIDO_REVEAL_SIDE>0 then
			cutplane 1,1,0,0
		else
			addz WIDO_FRAME_THICKNESS
			cutplane 1,1,0,1
			del 1
		endif

		off1=0
		off2=-WALL_THICKNESS
		off3=0
		off4=-WALL_THICKNESS
		gosub "Curved Wall Calculation"

		addz k_weitoffset
		mulz mulstat
		addz -rfur
		roty -AC_Hole_Position_Angle
		addz rfur

		resol 2*WALL_RESOL

		if iWindowShape = SHAPE_PARALLELOGRAM then
			py1BL =	ArchHeight + dytth1_in
			py2BL = py1BL - dytth1_in

			py1BR =	dytth2_in
			py2BR = py1BR - dytth2_in
		else
			py1BL = gs_trim_width_in*not(bDrawBoard)
			py2BL = py1BL-gs_trim_width_in*not(bDrawBoard)

			py1BR =	py1BL
			py2BR = py2BL
		endif

		if gs_trim_width_in > 0 then
			bprism_  gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,10,WALL_THICKNESS,rfur,
				xoffset-ax/2, py2BL, 15,
				xoffset+ax/2, py2BR, 15,
				xoffset+ax/2, b-ArchHeight, 15,
				xoffset-ax/2, b, 15,
				xoffset-ax/2, py2BL, -1,

				xoffset-ax/2+gs_trim_width_in, py1BL, btim2,
				xoffset+ax/2-gs_trim_width_in, py1BR, btim2,
				xoffset+ax/2-gs_trim_width_in, b-ArchHeight-dytth1_in, btim2,
				xoffset-ax/2+gs_trim_width_in, b-dytth2_in, btim2,
				xoffset-ax/2+gs_trim_width_in, py1BL, -1
		endif

		del 5

		cutend

		if bDrawBoard then cutend

	endif


	if bDrawOutsideTrim then
		sect_fill gs_trim_fill_out, gs_trim_pen_bg_out, gs_trim_pen_fg_out, gs_trim_pen_cont
		material gs_trim_out_mat


		!!! Cutting with sill---------
		if not(bCasingBottomOutside) then
			if bDrawSill then
				add 0, sillBottom + sillSideHeight, 0
				rotx 90 - sillSideAngle

				material gs_trim_out_mat
				cutplane !1, 1, 0, 1

				del 2
			else
				rotx 90
				material gs_trim_out_mat
				cutplane
				del 1
			endif
		endif


		! Casing on Wall

		off1=WALL_THICKNESS
		off2=-WALL_THICKNESS-gs_trim_thk_out
		off3=-gs_trim_thk_out
		off4=0
		gosub "Curved Wall Calculation"

		addz k_weitoffset
		mulz mulstat
		addz -rfur
		roty -AC_Hole_Position_Angle
		addz rfur

		resol 2*WALL_RESOL

		if iWindowShape = SHAPE_PARALLELOGRAM then
			py1BL =	ArchHeight + dytth1_out
			py2BL = py1BL - dytw1

			py1BR =	dytth2_out
			py2BR = py1BR - dytw2
		else
			if bDrawSill & not(bCasingBottomOutside) then
				py1BL = sillBottomOutside
				py2BL = py1BL
			else
				if bCasingBottomOutside then
					py1BL = (gs_trim_width_out - gs_trim_offs_out + widoThresDepth) * not(bDrawSill) + sillBottomOutside * bDrawSill
				else
					py1BL = (gs_trim_width_out - gs_trim_offs_out + widoThresDepth - (gs_trim_width_out+0.01))
				endif
				py2BL = py1BL - gs_tw_bottom_out
			endif

			py1BR =	py1BL
			py2BR = py2BL
		endif

		if gs_trim_thk_out > 0 then
			if bDrawSill & not(bCasingBottomOutside) then
				bprism_ gs_trim_out_mat,gs_trim_out_mat,gs_trim_out_mat,8,gs_trim_thk_out,rfur,
					xoffset+ax/2+gs_tw_right_out-gs_trim_width_out, py2BR,15,
					xoffset+ax/2+gs_tw_right_out-gs_trim_width_out, b-ArchHeight-dytth1_out+dytw1,15,
					xoffset-ax/2-gs_tw_right_out+gs_trim_width_out, b-dytth2_out+dytw2,15,
					xoffset-ax/2-gs_tw_right_out+gs_trim_width_out, py2BL,15,

					xoffset-ax/2+gs_trim_width_out, py1BL,btim3,
					xoffset-ax/2+gs_trim_width_out, b-dytth2_out,btim3,
					xoffset+ax/2-gs_trim_width_out, b-ArchHeight-dytth1_out,btim3,
					xoffset+ax/2-gs_trim_width_out, py1BR,btim3

			else
				bprism_ gs_trim_out_mat,gs_trim_out_mat,gs_trim_out_mat,10,gs_trim_thk_out,rfur,
					xoffset-ax/2-gs_tw_right_out+gs_trim_width_out, py2BL,15,
					xoffset+ax/2+gs_tw_right_out-gs_trim_width_out, py2BR,15,
					xoffset+ax/2+gs_tw_right_out-gs_trim_width_out, b-ArchHeight-dytth1_out+dytw1,15,
					xoffset-ax/2-gs_tw_right_out+gs_trim_width_out, b-dytth2_out+dytw2,15,
					xoffset-ax/2-gs_tw_right_out+gs_trim_width_out, py2BL,-1,

					xoffset-ax/2+gs_trim_width_out, py1BL,btim3,
					xoffset+ax/2-gs_trim_width_out, py1BR,btim3,
					xoffset+ax/2-gs_trim_width_out, b-ArchHeight-dytth1_out,btim3,
					xoffset-ax/2+gs_trim_width_out, b-dytth2_out,btim3,
					xoffset-ax/2+gs_trim_width_out, py1BL,-1
			endif
		endif

		del 5

		! Casing in Wallhole

		!cutplanes at frame

		if WIDO_REVEAL_SIDE>0 then
			addz WIDO_FRAME_THICKNESS
				cutplane 1,1,0,1
			del 1
		else
			CUTPLANE 1,1,0,0
		endif

		off1=0
		off2=-WALL_THICKNESS
		off3=0
		off4=-WALL_THICKNESS
		gosub "Curved Wall Calculation"


		addz k_weitoffset
		mulz mulstat
		addz -rfur
		roty -AC_Hole_Position_Angle
		addz rfur

		resol 2*WALL_RESOL


		if iWindowShape = SHAPE_PARALLELOGRAM then
			py1BL =	ArchHeight + dytth1_out
			py2BL = py1BL - dytth1_out

			py1BR =	dytth2_out
			py2BR = py1BR - dytth2_out
		else
			py1BL =	(gs_trim_width_out+widoThresDepth) * not(bDrawSill) \
					+ sillBottomOutside * bDrawSill \
					- (gs_trim_width_out+0.01)*(not(bDrawSill) & not(bCasingBottomOutside))
			py2BL = py1BL-gs_trim_width_out*not(bDrawSill)

			py1BR =	py1BL
			py2BR = py2BL
		endif

		if gs_trim_width_out > 0 then
			if bDrawSill then
				bprism_  gs_trim_out_mat,gs_trim_out_mat,gs_trim_out_mat,8,WALL_THICKNESS,rfur,
					xoffset+ax/2, py2BR,15,
					xoffset+ax/2, b-ArchHeight,15,
					xoffset-ax/2, b,15,
					xoffset-ax/2, py2BL,15,

					xoffset-ax/2+gs_trim_width_out, py1BR, btim4,
					xoffset-ax/2+gs_trim_width_out,  b-dytth2_out, btim4,
					xoffset+ax/2-gs_trim_width_out, b-ArchHeight-dytth1_out, btim4,
					xoffset+ax/2-gs_trim_width_out, py1BL, btim4

			else
				bprism_  gs_trim_out_mat,gs_trim_out_mat,gs_trim_out_mat,10,WALL_THICKNESS,rfur,
					xoffset-ax/2, py2BL,15,
					xoffset+ax/2, py2BR,15,
					xoffset+ax/2, b-ArchHeight,15,
					xoffset-ax/2, b,15,
					xoffset-ax/2, py2BL,-1,

					xoffset-ax/2+gs_trim_width_out, py1BL, btim4,
					xoffset+ax/2-gs_trim_width_out, py1BR, btim4,
					xoffset+ax/2-gs_trim_width_out, b-ArchHeight-dytth1_out, btim4,
					xoffset-ax/2+gs_trim_width_out, b-dytth2_out, btim4,
					xoffset-ax/2+gs_trim_width_out, py1BL,-1
			endif
		endif

		del 5

		cutend

		if not(bCasingBottomOutside) then cutend

	endif

endif


END

!! 3D-SUBROUTINES !!

"Curved Wall Calculation":

	A = refWidth

	r0=abs(WIDO_ORIG_DIST)

	if WIDO_ORIG_DIST<0 then                !Wall is convex from "outside"

		if WIDO_REVEAL_SIDE>0 then                            !Flip is on

			nframe=WIDO_FRAME_THICKNESS
			wsill=WIDO_SILL
			wthickness=WALL_THICKNESS

			mulstat=1
			rdpi=sqr(a*a/4+(r0+nframe)^2)
			r=sqr(a*a/4+r0*r0)
			kdpi=rdpi-r+WIDO_SILL
			rclo=r+kdpi                         !wall radius_2
			rfur=rclo-wthickness+off1           !wall radius_1
			calpha=asn(a/2/r)                   !window half angle

		else                                     !Flip is off

			nframe=0
			wsill=-WIDO_SILL
			wthickness=-WALL_THICKNESS

			mulstat=1
			r=sqr(a*a/4+r0*r0)
			rclo=r+wsill                         !wall radius_2
			rfur=rclo-wthickness+off2            !wall radius_1
			calpha=asn(a/2/r)                    !window half angle

		endif

	else                                      !Wall is concave from "outside"

		if WIDO_REVEAL_SIDE>0 then                              !Flip is on

			nframe=WIDO_FRAME_THICKNESS
			wsill=WIDO_SILL
			wthickness=WALL_THICKNESS

			mulstat=-1
			r0=r0-nframe
			r=sqr(a*a/4+r0*r0)
			rfur=r-wsill+off3                    !wall radius_1
			rclo=rfur+wthickness                 !wall radius_2
			calpha=asn(a/2/r)                    !window half angle

		else                                      !Flip is off

			nframe=WIDO_FRAME_THICKNESS
			wsill=-WIDO_SILL
			wthickness=-WALL_THICKNESS

			mulstat=-1
			rdpi=sqr(a*a/4+r0*r0)
			r0=r0-nframe
			r=sqr(a*a/4+r0*r0)
			kdpi=rdpi-r+WIDO_SILL
			rfur=r+kdpi+off4                     !wall radius_1
			rclo=rfur+wthickness                 !wall radius_2
			calpha=asn(a/2/r)                    !window half angle

		endif

	endif


	!Additional values

	k_weitoffset=rfur*mulstat+WIDO_ORIG_DIST	!gs_window_sill value at window centerpoint
	walpha=2*atn(a/2/r0)						!Window Opening angle
	xoffset=AC_Hole_Position_Angle/180*pi*rfur						!Window centerpoint distance from wall origin
	ax=walpha/180*pi*rfur						!"a" curve length (at further radius)


	!Oversize calculation (independent from "rfur")
	calphar=90+calpha

	!right oversize and gs_window_sill angle
	rosr=sqr(r^2+osr^2-2*r*osr*cos(calphar))
	cbetarr=asn(osr*sin(calphar)/rosr)          !Right oversize angle

	!left oversize and gs_window_sill angle
	rosl=sqr(r^2+osl^2-2*r*osl*cos(calphar))
	cbetarl=asn(osl*sin(calphar)/rosl)          !Left oversize angle

	A = leftWidth+rightWidth

RETURN


"Outside Casing":

	sect_fill gs_trim_fill_out, gs_trim_pen_bg_out, gs_trim_pen_fg_out, gs_trim_pen_cont
	material gs_trim_out_mat

	numCutsCornerColumn = 0
	if gs_corner_column & abs(WALL_INCL) < eps then
		matTrim = gs_trim_out_mat
		gosub 300
	endif

	!!! Cutting with sill---------
	if not(bCasingBottomOutside) then
		if bDrawSill then
			add 0, sillBottom + sillSideHeight, WIDO_SILL
			rotx 90 - sillSideAngle

			material gs_trim_out_mat
			cutplane !1, 1, 0, 1

			del 2
		else
			rotx 90
			material gs_trim_out_mat
			cutplane
			del 1
		endif
	endif


	! Casing in Wallhole
	if gs_trim_width_out > 0 then
		px1L = -leftWidth+gs_trim_width_out+widoLeftJamb-overSizeLeftX
		px2L = px1L - gs_trim_width_out
		px1R = rightWidth-gs_trim_width_out-widoRightJamb+overSizeRightX
		px2R = px1R + gs_trim_width_out

		if iWindowShape = SHAPE_PARALLELOGRAM then
			py1BL =	ArchHeight + dytth1_out + dyrev1 - overSizeLeftY
			py2BL = py1BL - dytth1_out

			py1BR =	dytth2_out + dyrev2 - overSizeRightY
			py2BR = py1BR - dytth2_out
		else
			py1BL =	(gs_trim_width_out+widoThresDepth) * not(bDrawSill) \
					+ sillBottomOutside * bDrawSill \
					- (gs_trim_width_out+0.01)*(not(bDrawSill) & not(bCasingBottomOutside))
			py2BL = py1BL-gs_trim_width_out*not(bDrawSill)

			py1BR =	py1BL
			py2BR = py2BL
		endif

		py1TL = b-dytth2_out-dyrev2+overSizeLeftY
		py2TL = py1TL + dytth2_out
		py1TR = b-ArchHeight-dytth1_out-dyrev1-overSizeRightY
		py2TR = py1TR + dytth1_out

		prism_ 9, WIDO_SILL,
			px2L, py2BL, 15,
			px1L, py1BL, jambExtOutMask,
			px1L, py1TL, jambExtOutMask,
			px1R, py1TR, jambExtOutMask,
			px1R, py1BR, jambExtOutMask,
			px2R, py2BR, 15,
			px2R, py2TR, 15,
			px2L, py2TL, 15,
			px2L, py2BL, -1

		if not(bDrawSill) then
			base
			prism_ 5, WIDO_SILL,
				px1L, py1BL, jambExtOutMask,
				px1R, py1BR, jambExtOutMask,
				px2R, py2BR, 15,
				px2L, py2BL, jambExtOutMask,
				px1L, py1BL, -1
			vert 0, 0, 0
			vert 1, 0, 0
			vert 0, 1, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1
		endif
	endif

	! Casing on Wall
	if gs_trim_thk_out > 0 then
		px1L = -leftWidth+gs_trim_width_out-gs_trim_offs_out+widoLeftJamb-overSizeLeftX
		px2L = px1L - gs_tw_right_out
		px1R = rightWidth-gs_trim_width_out+gs_trim_offs_out-widoRightJamb+overSizeRightX
		px2R = px1R + gs_tw_right_out

		py1TL = b-dytos2-dyrev2+overSizeLeftY
		py2TL = py1TL + dytw2
		py1TR = b-ArchHeight-dytos1-dyrev1-overSizeRightY
		py2TR = py1TR + dytw1

		if iWindowShape = SHAPE_PARALLELOGRAM then
			py1BL =	ArchHeight + dytos1 + dyrev1 + overSizeLeftY
			py2BL = py1BL - dytw1

			py1BR =	dytos2 + dyrev2 - overSizeRightY
			py2BR = py1BR - dytw2
		else
			if bDrawSill & not(bCasingBottomOutside) then
				! Only for Trapezoid 
				py1BL = sillBottomOutside
				py2BL = py1BL

				py1BR =	py1BL
				py2BR = py2BL

				casOutMask_start = 15
			else
				if bCasingBottomOutside then
					py1BL = (gs_trim_width_out - gs_trim_offs_out + widoThresDepth) * not(bDrawSill) + sillBottomOutside * bDrawSill
				else
					py1BL = (gs_trim_width_out - gs_trim_offs_out + widoThresDepth - (gs_trim_width_out+0.01))
				endif
				py2BL = py1BL - gs_tw_bottom_out

				py1BR =	py1BL
				py2BR = py2BL

				casOutMask_start = 10
			endif
		endif

		addz -gs_trim_thk_out
		if not(bLeftCornerFunction) then
			base
			PRISM_ 4,gs_trim_thk_out,
				px1L, py1BL, casOutMask_start,
				px2L, py2BL, 15,
				px2L, py2TL, 10,
				px1L, py1TL, casOutMask

			vert 0, 0, 0
			vert 0, 1, 0
			vert 1, 0, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1
		endif

		if not(bRightCornerFunction) then
			base
			PRISM_ 4,gs_trim_thk_out,
				px1R, py1BR, casOutMask_start,
				px2R, py2BR, 15,
				px2R, py2TR, 10,
				px1R, py1TR, casOutMask

			vert 0, 0, 0
			vert 0, 1, 0
			vert 1, 0, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1
		endif

		base
		PRISM_ 4,gs_trim_thk_out,
			px2L, py2TL, 10,
			px1L, py1TL, casOutMask,
			px1R, py1TR, 10,
			px2R, py2TR, 15
		vert 0, 0, 0
		vert cos(palpha), -sin(palpha), 0
		vert sin(palpha), cos(palpha), 0
		vert 0, 0, 1
		coor 2, -1, -2, -3, -4
		body 1

		if not(bDrawSill * not(bCasingBottomOutside)) then
			base
			PRISM_ 4,gs_trim_thk_out,
				px2R, py2BR, 10,
				px1R, py1BR, casOutMask,
				px1L, py1BL, 10,
				px2L, py2BL, 15

			if iWindowShape = SHAPE_PARALLELOGRAM then
				vert 0, 0, 0
				vert cos(palpha), -sin(palpha), 0
				vert sin(palpha), cos(palpha), 0
				vert 0, 0, 1
			else
				vert 0, 0, 0
				vert 1, 0, 0
				vert 0, 1, 0
				vert 0, 0, 1
			endif
			coor 2, -1, -2, -3, -4
			body 1
		endif
		del 1
	endif

	if not(bCasingBottomOutside) then cutend

	if abs(WALL_INCL) < eps then
		for i=1 to numCutsCornerColumn
			cutend
		next i
	endif

return


300:
	! ==============================================================================
	!	Cutting Corner Column
	! ==============================================================================

	if WIDO_REVEAL_SIDE then
		addz -WIDO_FRAME_THICKNESS
	else
		mulz -1
	endif

	rotx -90


	! Draw Right Column ------------------------------------------------------------

	if bRightCornerFunction then
		add rightCwDx + rightWidth + WIDO_FRAME_THICKNESS * rx, WIDO_SILL, 0

		if bMirrorRight then muly -1

		px1		= px1R
		px2		= px2R
		px3		= px3R
		pxFrame	= pxFrameR

		cutpolya 4, 1, 0,
			px1,			-gs_cc_pr,				15,
			px2 - pxFrame,	-gs_cc_pr,				15,
			px2 - pxFrame,	-gs_cc_pr + gs_cc_thk,	15,
			px3,			-gs_cc_pr + gs_cc_thk,	15

		numCutsCornerColumn = numCutsCornerColumn + 1

		del bMirrorRight + 1
	endif


	! Draw Left Column -------------------------------------------------------------

	if bLeftCornerFunction then
		add -leftCwDx - leftWidth - WIDO_FRAME_THICKNESS * lx, WIDO_SILL, 0

		if bMirrorLeft then muly -1
		add offsLeftX, offsLeftY, 0
		mulx -1

		px1		= px1L
		px2		= px2L
		px3		= px3L
		pxFrame	= pxFrameL

		cutpolya 4, 1, 0,
			px1,			-gs_cc_pr,				15,
			px2 - pxFrame,	-gs_cc_pr,				15,
			px2 - pxFrame,	-gs_cc_pr + gs_cc_thk,	15,
			px3,			-gs_cc_pr + gs_cc_thk,	15

		numCutsCornerColumn = numCutsCornerColumn + 1

		del bMirrorLeft + 3
	endif

	del 2

return
