

! =============================================================================
! Hide All parameters list
! =============================================================================
hideparameter all



![#COMPATIBILITY] - START
bDefaultElem = (GLOB_INTGUID = "" | GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")
if bDefaultElem then parameters gs_center_hpts_compatibility = 1
![#COMPATIBILITY] - END


hideparameter "ac_details"

if (iWindowShape <> SHAPE_RECTANGULAR) then
	if not(gs_arc_2D) then
		hideparameter "gs_brickw_lt"
		lock "gs_brickw_lt"
	endif
endif


! =============================================================================
! Show / Hide 2D Fills
! =============================================================================

if (iWindowShape = SHAPE_RECTANGULAR) & not(gs_bFills) then	! Hide
	hideparameter	"gs_trim_fill_in",
					"gs_trim_pen_fg_in",
					"gs_trim_pen_bg_in",
					"gs_trim_fill_out",
					"gs_trim_pen_fg_out",
					"gs_trim_pen_bg_out"

	lock	"gs_trim_fill_in",
			"gs_trim_pen_fg_in",
			"gs_trim_pen_bg_in",
			"gs_trim_fill_out",
			"gs_trim_pen_fg_out",
			"gs_trim_pen_bg_out"
endif


! =============================================================================
! Wall Contour Line Parameter Handling
! =============================================================================

if not(bEnableSill | bEnableThreshold) then
	lock "gs_wcont_hidden_line_out"
	hideparameter "gs_wcont_hidden_line_out"
endif
if not(bEnableThreshold) then
	lock "gs_wcont_hidden_line_in"
	hideparameter "gs_wcont_hidden_line_in"
endif


! =============================================================================
! Dimension Link
! =============================================================================

! Reveal Size -> Wallhole Size difference
revealDLeft		= gs_reveal_left
revealDRight	= gs_reveal_right
revealDUpper	= gs_reveal_top
revealDLower	= gs_reveal_bottom

if GLOB_MODPAR_NAME = "ac_wallhole_width" then
	A = ac_wallhole_width
	ac_unit_width = ac_wallhole_width
	ac_egress_width = ac_wallhole_width - revealDLeft - revealDRight

	parameters	A				= A,
				ac_egress_width	= ac_egress_width,
				ac_unit_width	= ac_unit_width
else
	if GLOB_MODPAR_NAME = "ac_egress_width" then
		ac_wallhole_width = ac_egress_width + revealDLeft + revealDRight
		ac_unit_width = ac_wallhole_width
		A = ac_wallhole_width

		parameters	A					= A,
					ac_wallhole_width	= ac_wallhole_width,
					ac_unit_width		= ac_unit_width
	else
		if GLOB_MODPAR_NAME = "ac_unit_width" then
			A = ac_unit_width
			ac_wallhole_width = ac_unit_width
			ac_egress_width = ac_unit_width - revealDLeft - revealDRight

			parameters	A				= A,
						ac_wallhole_width	= ac_wallhole_width,
						ac_egress_width	= ac_egress_width
		else
			ac_wallhole_width = A
			ac_unit_width = A
			ac_egress_width = A - revealDLeft - revealDRight

			parameters	ac_wallhole_width	= ac_wallhole_width,
						ac_egress_width		= ac_egress_width,
						ac_unit_width		= ac_unit_width
		endif
	endif
endif

if GLOB_MODPAR_NAME = "ac_wallhole_height" then
	B = ac_wallhole_height
	ac_unit_height = ac_wallhole_height
	ac_egress_height = ac_wallhole_height - revealDUpper - revealDLower

	parameters	B				= B,
				ac_egress_height	= ac_egress_height,
				ac_unit_height	= ac_unit_height
else
	if GLOB_MODPAR_NAME = "ac_egress_height" then
		ac_wallhole_height = ac_egress_height + revealDUpper + revealDLower
		ac_unit_height = ac_wallhole_height
		B = ac_wallhole_height

		parameters	B					= B,
					ac_wallhole_height	= ac_wallhole_height,
					ac_unit_height		= ac_unit_height
	else
		if GLOB_MODPAR_NAME = "ac_unit_height" then
			B = ac_unit_height
			ac_wallhole_height = ac_unit_height
			ac_egress_height = ac_unit_height - revealDUpper - revealDLower

			parameters	B				= B,
						ac_wallhole_height	= ac_wallhole_height,
						ac_egress_height	= ac_egress_height
		else
			ac_wallhole_height = B
			ac_unit_height = B
			ac_egress_height = B - revealDUpper - revealDLower

			parameters	ac_wallhole_height	= ac_wallhole_height,
						ac_egress_height		= ac_egress_height,
						ac_unit_height		= ac_unit_height
		endif
	endif
endif
! =============================================================================
! Wallhole Structure
! =============================================================================

call "WallholeStructure" parameters all A = A, B = B,
	refWidth 				= refWidth,
	leftWidth				= leftWidth,
	rightWidth				= rightWidth,
	openingHeight			= openingHeight,
	iRevealType				= iRevealType,
	iSillRevealType			= iSillRevealType,
	iBoardRevealType		= iBoardRevealType,
	leftWallholeRevealPnts	= leftWallholeRevealPnts,
	rightWallholeRevealPnts	= rightWallholeRevealPnts,
	iRevealPointsIdx		= iRevealPointsIdx,
	iClosureType			= iClosureType,
! --- Function Control Parameters
	bEnablePlasterLeftIn	= bEnablePlasterLeftIn,
	bEnablePlasterLeftOut	= bEnablePlasterLeftOut,
	bEnablePlasterRightIn	= bEnablePlasterRightIn,
	bEnablePlasterRightOut	= bEnablePlasterRightOut,
	bEnablePlasterTopIn		= bEnablePlasterTopIn,
	bEnablePlasterTopOut	= bEnablePlasterTopOut,
	bEnablePlasterBottomIn	= bEnablePlasterBottomIn,
	bEnablePlasterBottomOut	= bEnablePlasterBottomOut,
	bEnableParapetInset		= bEnableParapetInset,
	bEnableMasonryArch		= bEnableMasonryArch,
	bEnableFaschen			= bEnableFaschen,
	gs_RevealTypeEnable		= gs_RevealTypeEnable,
	gs_ClosureTypeEnable	= gs_ClosureTypeEnable,
	bEnableWallContourHiddenLine	= bEnableWallContourHiddenLine,
! --- Macro Control Parameters
	isWindow				= 0,
	iWindowShape			= iWindowShape,
	defaultSillDepth		= defaultSillDepth,
! --- Corner Functions
	bLeftCornerFunction		= bLeftCornerFunction,
!	leftCornerAngle			= leftCornerAngle,
!	leftConnWallThk			= leftConnWallThk,
	bRightCornerFunction	= bRightCornerFunction,
!	rightCornerAngle		= rightCornerAngle,
!	rightConnWallThk		= rightConnWallThk,
! --- Casing
	bDrawOutsideTrim		= bDrawOutsideTrim,
	bDrawInsideTrim			= bDrawInsideTrim,
! --- String Resources
	stRevealTypes					= stRevealTypes,
	stRevealTypesBottom				= stRevealTypesBottom,
	stRevealAngleEditBy				= stRevealAngleEditBy,
	stClosureTypes					= stClosureTypes,
	stWallContourLine				= stWallContourLine,
	stCavityClosureSide				= stCavityClosureSide,
	stCavityClosureTurnDir			= stCavityClosureTurnDir,
	stDutchClosureToSide			= stDutchClosureToSide,
	stCavityClosureEditedJambSide	= stCavityClosureEditedJambSide,
	stCavityClosureEditedWallSide	= stCavityClosureEditedWallSide,
	stCavityClosureEditedSkinNumber	= stCavityClosureEditedSkinNumber,
	stCavityClosureTurnInThickness	= stCavityClosureTurnInThickness,
	stCavityClosureHatchOrientation	= stCavityClosureHatchOrientation,
	stMasonryArchTypes				= stMasonryArchTypes,
	stMasonryArchStructure			= stMasonryArchStructure,
	gs_faschen_type					= gs_faschen_type


! =============================================================================
! Wallhole Framing
! =============================================================================

call "WallholeFraming" parameters all A = A,
	iRevealType				= iRevealType,
	leftWallFrameRevealPnts = leftWallholeRevealPnts,
	rightWallFrameRevealPnts= rightWallholeRevealPnts,
! --- Function Control Parameters
	bEnableSill				= bEnableSill,
	bEnableBoard			= bEnableBoard,
	bEnableThreshold		= bEnableThreshold,
	bEnableCasing			= bEnableCasing,
	bEnableShutter			= bEnableShutter,
	bEnableCustomCornerLeft		= bEnableCustomCornerLeft,
	bEnableCustomCornerRight	= bEnableCustomCornerRight,
! --- Macro Control Parameters
	isWindow				= 0,
	isEmptyOpening			= isEmptyOpening,
	iWindowShape			= iWindowShape,
	gs_casing_nor			= 0,
! --- Corner Functions
	bLeftCornerFunction		= bLeftCornerFunction,
!	leftCornerAngle			= leftCornerAngle,
!	leftConnWallThk			= leftConnWallThk,
	bRightCornerFunction	= bRightCornerFunction,
!	rightCornerAngle		= rightCornerAngle,
!	rightConnWallThk		= rightConnWallThk,
! --- String Resources
	stSillTypes				= stSillTypes,
	stCasingType			= stCasingType,
	stCasingHeadType		= stCasingHeadType,
	stArchitraveCapitalType	= stArchitraveCapitalType,
	stThresholdTypes		= stThresholdTypes,
	stBsilldetlevel 		= stBsilldetlevel


! =============================================================================
! Show / Hide 2D Fills
! =============================================================================

if not(gs_bFills) then	! Hide
	hideparameter	"gs_wallhole_fill",
					"gs_wallhole_pen_fg",
					"gs_wallhole_pen_bg"

	lock	"gs_wallhole_fill",
			"gs_wallhole_pen_fg",
			"gs_wallhole_pen_bg"
endif


! =============================================================================
! Demolition / New construction
! =============================================================================

if gs_bFills = 0 then
	lock "iTypeDemolitionFill"
endif

if gs_bShowDemolitionPar then
	values{2} "iTypeDemolitionFill" 	RENOVATION_FILL_EXIST, `Нет`,
										RENOVATION_FILL_DEMOLISHED, `Демонтаж`,
										RENOVATION_FILL_NEW, `Новая Конструкция - Пустой Символ`,
										RENOVATION_FILL_NEW_FULL, `Новая Конструкция - Полный Символ`

	if iTypeDemolitionFill = RENOVATION_FILL_DEMOLISHED then
		lock "gs_wallhole_fill"
		lock "gs_wallhole_pen_fg"
		lock "gs_wallhole_pen_bg"
	endif

	if	iTypeDemolitionFill = RENOVATION_FILL_NEW |\
		iTypeDemolitionFill = RENOVATION_FILL_NEW_FULL then

		lock "gs_wallhole_fill"
		lock "gs_wallhole_pen_fg"
		lock "gs_wallhole_pen_bg"
	endif
endif


! =============================================================================
! 2D Detail Level
! =============================================================================

if gs_2DDetEnable = 0 then
			values{2} "iDWDet2D" 	DETLEVEL_DW2D_MVO,		`по ПМВ`,
									DETLEVEL_DW2D_SCSENS,	`Зависит от Масштаба`,
									DETLEVEL_DW2D_50,		`1:50`,
									DETLEVEL_DW2D_100,		`1:100`,
									DETLEVEL_DW2D_200,		`1:200`,
									DETLEVEL_DW2D_OFF,		`Откл.`
else

	dim n2DScaleValueMask[]
	dim n2DScaleValueMask_m[]

	i=1
	if bittest( gs_2DDetEnable, 8) then                                              ! by MVO, 256
				n2DScaleValueMask[i] = stDetail2D[1]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_MVO
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 0) then                                              ! Scale Sensitive in Value list, 1
				n2DScaleValueMask[i] = stDetail2D[2]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_SCSENS
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 7) then                                              ! 1:20 in Value list,128
				n2DScaleValueMask[i] = stDetail2D[9]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_20
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 1) then                                              ! 1:50 in Value list,2
				n2DScaleValueMask[i] = stDetail2D[3]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_50
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 2) then                                              ! 1:100 in Value list,4
				n2DScaleValueMask[i] = stDetail2D[4]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_100
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 3) then                                              ! 1:200 in Value list,8
				n2DScaleValueMask[i] = stDetail2D[5]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_200
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 9) then                                              ! 1:500 in Value list, 512
				n2DScaleValueMask[i] = stDetail2D[10]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_500
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 4) then                                              ! Off in Value list,16
				n2DScaleValueMask[i] = stDetail2D[6]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_OFF
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 5) then                                              ! Chinese Simple in Value list,32
				n2DScaleValueMask[i] = stDetail2D[7]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_CHISMPL
				i = i + 1
	endif
	if bittest( gs_2DDetEnable, 6) then                                              ! Chinese Detailed in Value list,64
				n2DScaleValueMask[i] = stDetail2D[8]
				n2DScaleValueMask_m[i] = DETLEVEL_DW2D_CHIDET
				i = i + 1
	endif

			values{2} "iDWDet2D"  n2DScaleValueMask_m, n2DScaleValueMask
endif


! =============================================================================
! Show Reveal On Symbol
! =============================================================================

if bCanHaveReveal then
	values{2} "iRevealOnSymbol"	SHOWREV_MVO, stRevealOnSymbValues[1],
					SHOWREV_ALWAYS, stRevealOnSymbValues[2],
					SHOWREV_UPTO50, stRevealOnSymbValues[3],
					SHOWREV_UPTO100, stRevealOnSymbValues[4],
					SHOWREV_UPTO200, stRevealOnSymbValues[5],
					SHOWREV_NEVER, stRevealOnSymbValues[6]
					
else
	lock "iRevealOnSymbol"
	hideparameter "iRevealOnSymbol"
endif


! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif


! =============================================================================

values{2} "iTypeCurvedWallCut" 	CUT_PARALLEL, `Параллельно`,
								CUT_RADIAL, `Радиально`

! =============================================================================
! UK Listing Parameters
! =============================================================================
if bEnableListing then
	if not(gs_show_UKListParams) then
		hideparameter	"gs_list_NBSClause",
						"gs_list_doorcloser",
						"gs_list_visionpanel_type",
						"gs_list_elevationtype",
						"gs_list_frametype",
						"gs_list_holddevice",
						"gs_list_firesignage",
						"gs_list_comment"
	else

		if bEnableNEDFireRating & not(isEmptyOpening) then
			values "gs_list_firerating"			`Нет`, `Самозакрывающаяся`, `30 минут`, `60 минут`, `30 минут + самозакрывающаяся`, `60 минут + самозакрывающаяся`, CUSTOM

			if GLOB_MODPAR_NAME = "gs_list_firerating" then
				iNEDFireRating = 0
				if gs_list_firerating = `Самозакрывающаяся` then iNEDFireRating = 1
				if gs_list_firerating = `30 минут` then iNEDFireRating = 2
				if gs_list_firerating = `60 минут` then iNEDFireRating = 3
				if gs_list_firerating = `30 минут + самозакрывающаяся` then iNEDFireRating = 4
				if gs_list_firerating = `60 минут + самозакрывающаяся` then iNEDFireRating = 5
				parameters iNEDFireRating = iNEDFireRating
			else
				if iNEDFireRating = 1 then gs_list_firerating = `Самозакрывающаяся`
				if iNEDFireRating = 2 then gs_list_firerating = `30 минут`
				if iNEDFireRating = 3 then gs_list_firerating = `60 минут`
				if iNEDFireRating = 4 then gs_list_firerating = `30 минут + самозакрывающаяся`
				if iNEDFireRating = 5 then gs_list_firerating = `60 минут + самозакрывающаяся`
				parameters gs_list_firerating = gs_list_firerating
			endif
		else
			values "gs_list_firerating"			`Нет`, `FS30`, `FD30SC`, `FS30SC`, `FS60`, `FD60SC`, `FS60SC`, CUSTOM
		endif
		values "gs_list_acousticrating"		`Нет`, `32 dB/Rw`, `35 dB/Rw`, `38 dB/Rw`, CUSTOM
		values "gs_list_finish"				`Нет`, `Облицовка`, `Поливинилхлорид`, `Полированная Сталь`, CUSTOM
		values "gs_list_frametype"			`Нет`, `Сталь`, `Алюминий`, `Древесина`, CUSTOM
		values "gs_list_firesignage"		`Нет`, `FDKS`, `FDKLS`, `AFDKC`, CUSTOM
		values "gs_list_visionpanel_type"	stVisionPanelTypes
	endif
endif

! =============================================================================
end stDetail2D
! =============================================================================
