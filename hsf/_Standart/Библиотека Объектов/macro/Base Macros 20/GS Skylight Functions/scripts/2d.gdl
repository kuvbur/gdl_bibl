
! ==============================================================================
! 2D Detail Level
! ==============================================================================

iLoD2D = SYMBS_OFF

if bGetMVOValueDlev2D then								! by MVO
	iLoD2D	= iMVODetlevel2D

	if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
		iLoD2D = SYMBS_D_2
	endif
else
	if gs_detlevel_2D_m = LOD2D_SCALE then				! Scale Sensitive
		if	gs_iDisplayStandards = 1 |\		! INT Display standards
			gs_iDisplayStandards = 2 then	! GER Display standards
			iLoD2D = SYMBS_D_2								! Detailed 1
			if GLOB_SCALE > 21  then iLoD2D = SYMBS_D_1		! Detailed 2
			if GLOB_SCALE > 51 	then iLoD2D = SYMBS_M_1		! Middle
			if GLOB_SCALE > 101 then iLoD2D = SYMBS_S_2		! Simple 1
			if GLOB_SCALE > 201 then iLoD2D = SYMBS_S_1		! Simple 2
		endif
		if	gs_iDisplayStandards = 3 |\		! CHE Display standards
			gs_iDisplayStandards = 4 |\		! AUT Display standards
			gs_iDisplayStandards = 5 then	! USA Display standards
			iLoD2D = SYMBS_D_2								! Detailed 2
			if GLOB_SCALE > 21  then iLoD2D = SYMBS_M_1		! Middle
			if GLOB_SCALE > 51 	then iLoD2D = SYMBS_S_2		! Simple 1
			if GLOB_SCALE > 101 then iLoD2D = SYMBS_S_1		! Simple 2
		endif
	else
		if	gs_iDisplayStandards = 1 |\		! INT Display standards
			gs_iDisplayStandards = 2 then	! GER Display standards
			if gs_detlevel_2D_m = LOD2D_20 then		iLoD2D = SYMBS_D_2		! Detailed 1
			if gs_detlevel_2D_m = LOD2D_50 then		iLoD2D = SYMBS_D_1		! Detailed 2
			if gs_detlevel_2D_m = LOD2D_100 then	iLoD2D = SYMBS_M_1		! Middle
			if gs_detlevel_2D_m = LOD2D_200 then	iLoD2D = SYMBS_S_2		! Simple 1
		endif
		if	gs_iDisplayStandards = 3 |\		! CHE Display standards
			gs_iDisplayStandards = 4 |\		! AUT Display standards
			gs_iDisplayStandards = 5 then	! USA Display standards
			if gs_detlevel_2D_m = LOD2D_20 then		iLoD2D = SYMBS_D_2		! Detailed 1
			if gs_detlevel_2D_m = LOD2D_50 then		iLoD2D = SYMBS_M_1		! Middle
			if gs_detlevel_2D_m = LOD2D_100 then	iLoD2D = SYMBS_S_2		! Simple 1
		endif
	endif
endif

! ==============================================================================
! Opening Lines
! ==============================================================================

gs_opLinePen = 1
gs_iSwingType = 1
gs_opline_style_m = 1

call "OpeningOptions" parameters all	bSkylight			= 1,
										bWindow				= 1,
										bOverride_MVO_2D	= bOverride_MVO_2D,
										or_opLinePen		= or_opLinePen,
!										or_iSwingType		= or_iSwingType,
										or_swingLineType	= or_swingLineType,
										or_iOplineStyle		= or_iOplineStyle,
										bEnableSecondaryOpLineTypes = 0,
				returned_parameters gs_opLinePen,
									gs_iSwingType,
									gs_swingLineType,
									gs_opline_style_m


if gs_bDifferentSashAndSwing2D then
	gs_openedSashPen = gs_sash_pen
else
	gs_openedSashPen = gs_opLinePen
endif

! ==============================================================================

end iLoD2D,
	gs_opLinePen, gs_openedSashPen, gs_iSwingType, gs_swingLineType, gs_opline_style_m
