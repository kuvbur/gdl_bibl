
! ==============================================================================
! 3D Detail Level
! ==============================================================================

bProjected2DContext = (GLOB_VIEW_TYPE = 2) & not(bCall3DFrom2DScript)

iLoD3D = SYMBS_3D_OFF
if bSectionElevationContext then
	if bGetMVOValueDlevSE then												! by MVO then
		iLoD3D = iMVODetlevelSE
	else
		if gs_detlevel_SE_m = LODSE_SCALE then								! Scale Sensitive
			if	gs_iDisplayStandards = 1 |\									! INT Display standards
				gs_iDisplayStandards = 2 then								! GER Display standards
				iLoD3D = SYMBS_3D_D_1										! Detailed 1
				if GLOB_SCALE > 21  then iLoD3D = SYMBS_3D_D_2				! Detailed 2
				if GLOB_SCALE > 51 	then iLoD3D = SYMBS_3D_M_1				! Middle
				if GLOB_SCALE > 101 then iLoD3D = SYMBS_3D_S_1				! Simple 2
				if GLOB_SCALE > 201 then iLoD3D = SYMBS_3D_S_1				! Simple 1
			endif
			if	gs_iDisplayStandards = 3 |\									! CHE Display standards
				gs_iDisplayStandards = 4 |\									! AUT Display standards
				gs_iDisplayStandards = 5 then								! USA Display standards
				iLoD3D = SYMBS_3D_D_1										! Detailed 1
				if GLOB_SCALE > 21 	then iLoD3D = SYMBS_3D_M_1				! Middle
				if GLOB_SCALE > 51  then iLoD3D = SYMBS_3D_S_2				! Simple 2
				if GLOB_SCALE > 101 then iLoD3D = SYMBS_3D_S_1				! Simple 1
			endif
		endif
		if	gs_iDisplayStandards = 1 |\										! INT Display standards
			gs_iDisplayStandards = 2 then									! GER Display standards
			if gs_detlevel_SE_m = LODSE_20 then		iLoD3D = SYMBS_3D_D_1	! Detailed 1
			if gs_detlevel_SE_m = LODSE_50 then		iLoD3D = SYMBS_3D_D_2	! Detailed 2
			if gs_detlevel_SE_m = LODSE_100 then	iLoD3D = SYMBS_3D_M_1	! Middle
			if gs_detlevel_SE_m = LODSE_200 then	iLoD3D = SYMBS_3D_S_1	! Simple 1
			if gs_detlevel_SE_m = LODSE_500 then	iLoD3D = SYMBS_3D_S_1	! Simple 1
		endif
		if	gs_iDisplayStandards = 3 |\										! CHE Display standards
			gs_iDisplayStandards = 4 |\										! AUT Display standards
			gs_iDisplayStandards = 5 then									! USA Display standards
			if gs_detlevel_SE_m = LODSE_20 then		iLoD3D = SYMBS_3D_D_1	! Detailed 1
			if gs_detlevel_SE_m = LODSE_50 then		iLoD3D = SYMBS_3D_M_1	! Middle
			if gs_detlevel_SE_m = LODSE_100 then	iLoD3D = SYMBS_3D_S_2	! Simple 2
			if gs_detlevel_SE_m = LODSE_200 then	iLoD3D = SYMBS_3D_S_1	! Simple 1
			if gs_detlevel_SE_m = LODSE_500 then	iLoD3D = SYMBS_3D_S_1	! Simple 1
		endif
	endif
else
	if not(bProjected2DContext) then
		if (GLOB_VIEW_TYPE = 9 & GLOB_PREVIEW_MODE = 0) then					! in case of Surface IS Calculation
			iLoD3D = SYMBS_3D_D_1												! Detailed
		else
			if gs_detlevel_3D_m = LOD3D_DETAILED then	iLoD3D = SYMBS_3D_D_1	! Detailed
			if gs_detlevel_3D_m = LOD3D_SIMPLE then		iLoD3D = SYMBS_3D_S_2	! Simple
		endif
	else
		if gs_detlevel_2D_m = LOD2D_SCALE | gs_detlevel_2D_m = LOD2D_BYMVO then	! Scale Sensitive, by MVO
			if	gs_iDisplayStandards = 1 |\		! INT Display standards
				gs_iDisplayStandards = 2 then	! GER Display standards
				iLoD2D = SYMBS_D_1								! Detailed 1
				if GLOB_SCALE > 21  then iLoD2D = SYMBS_D_2		! Detailed 2
				if GLOB_SCALE > 51 	then iLoD2D = SYMBS_M_1		! Middle
				if GLOB_SCALE > 101 then iLoD2D = SYMBS_S_1		! Simple 1
			endif
			if	gs_iDisplayStandards = 3 |\		! CHE Display standards
				gs_iDisplayStandards = 4 |\		! AUT Display standards
				gs_iDisplayStandards = 5 then	! USA Display standards
				iLoD2D = SYMBS_D_1								! Detailed 1
				if GLOB_SCALE > 21  then iLoD2D = SYMBS_M_1		! Middle
				if GLOB_SCALE > 51 	then iLoD2D = SYMBS_S_2		! Simple 2
				if GLOB_SCALE > 101 then iLoD2D = SYMBS_S_1		! Simple 1
			endif
		else
			if	gs_iDisplayStandards = 1 |\		! INT Display standards
				gs_iDisplayStandards = 2 then	! GER Display standards
				if gs_detlevel_2D_m = LOD2D_20 then		iLoD2D = SYMBS_D_1		! Detailed 1
				if gs_detlevel_2D_m = LOD2D_50 then		iLoD2D = SYMBS_D_2		! Detailed 2
				if gs_detlevel_2D_m = LOD2D_100 then	iLoD2D = SYMBS_M_1		! Middle
				if gs_detlevel_2D_m = LOD2D_200 then	iLoD2D = SYMBS_S_1		! Simple 1
				if gs_detlevel_2D_m = LOD2D_500 then	iLoD2D = SYMBS_S_1		! Simple 1
			endif
			if	gs_iDisplayStandards = 3 |\		! CHE Display standards
				gs_iDisplayStandards = 4 |\		! AUT Display standards
				gs_iDisplayStandards = 5 then	! USA Display standards
				if gs_detlevel_2D_m = LOD2D_20 then		iLoD2D = SYMBS_D_1		! Detailed 1
				if gs_detlevel_2D_m = LOD2D_50 then		iLoD2D = SYMBS_M_1		! Detailed 2
				if gs_detlevel_2D_m = LOD2D_100 then	iLoD2D = SYMBS_S_2		! Middle
				if gs_detlevel_2D_m = LOD2D_200 then	iLoD2D = SYMBS_S_1		! Simple 1
				if gs_detlevel_2D_m = LOD2D_500 then	iLoD2D = SYMBS_S_1		! Simple 1
			endif
		endif

		iLoD3D = iLoD2D
	endif
endif

iOldResol = 4
if not(bSectionElevationContext) and not(bProjected2DContext) then
	if iLoD3D = SYMBS_3D_S_1 then iOldResol = 2
else
	if	gs_iDisplayStandards = 1 |\		! INT Display standards
		gs_iDisplayStandards = 2 then	! GER Display standards
		if iLoD3D = SYMBS_3D_M_1 then iOldResol = 3
		if iLoD3D = SYMBS_3D_S_2 | iLoD3D = SYMBS_3D_S_1 then iOldResol = 1
	endif
	if	gs_iDisplayStandards = 3 |\		! CHE Display standards
		gs_iDisplayStandards = 4 |\		! AUT Display standards
		gs_iDisplayStandards = 5 then	! USA Display standards
		if iLoD3D = SYMBS_3D_M_1 then iOldResol = 3
		if iLoD3D = SYMBS_3D_S_2 then iOldResol = 2
		if iLoD3D = SYMBS_3D_S_1 then iOldResol = 1
	endif
endif

lod3D_bOplines	= 0	! boolean - show 3D opening lines if checked on in MVO
lod3D_Casing	= 0	! 0, 1

if	gs_iDisplayStandards = 1 |\		! INT Display standards
	gs_iDisplayStandards = 2 then	! GER Display standards
	if bSectionElevationContext | bProjected2DContext then
		if iLoD3D = SYMBS_3D_D_2 then
			lod3D_Casing			= 1
		endif
		if iLoD3D = SYMBS_3D_D_1 then
			lod3D_Casing			= 1
		endif
	else
		if iLoD3D = SYMBS_3D_D_1 then
			lod3D_Casing			= 1
		endif
	endif
	lod3D_bOplines			= 1
endif

if	gs_iDisplayStandards = 3 |\		! CHE Display standards
	gs_iDisplayStandards = 4 |\		! AUT Display standards
	gs_iDisplayStandards = 5 then	! USA Display standards
	if bSectionElevationContext | bProjected2DContext then
		if iLoD3D = SYMBS_3D_D_2 then
			lod3D_Casing			= 1
		endif
		if iLoD3D = SYMBS_3D_D_1 then
			lod3D_Casing			= 1
		endif
	else
		if iLoD3D = SYMBS_3D_D_1 then
			lod3D_Casing			= 1
		endif
	endif
	lod3D_bOplines			= 1
endif


! ==============================================================================
! Lining
! ==============================================================================

! Edge:
! 1: Vertical
! 2: Perpendicular
! 3: Horizontal
! 4: Custom

if bEnableLining & lod3D_Casing = 1 & gs_egde_cover & ac_roofthk > EPS then

	rotx ac_roofang

	gs_edge_cover_thk = max(0.0005, gs_edge_cover_thk)

	material gs_edge_mat
	sect_fill gs_edge_cover_fill_type, gs_edge_cover_fill_pen_bg, gs_edge_cover_fill_pen_fg, SYMB_SECT_PEN

	if ac_edge_lower_type = 0 then alphaL = ac_roofang
	if ac_edge_lower_type = 1 then alphaL = 0
	if ac_edge_lower_type = 2 then alphaL = -90 + ac_roofang
	if ac_edge_lower_type = 3 then alphaL = 90 - ac_edge_lower_angle

	if ac_edge_upper_type = 0 then alphaU = ac_roofang
	if ac_edge_upper_type = 1 then alphaU = 0
	if ac_edge_upper_type = 2 then alphaU = -90 + ac_roofang
	if ac_edge_upper_type = 3 then alphaU = -90 + ac_edge_upper_angle

	roty 90
	addz -A / 2 + gs_edge_cover_thk

	prism 4, A - 2 * gs_edge_cover_thk,
		0,			 0,
		0,			 gs_edge_cover_thk / cos(alphaL),
		ac_roofthk,  gs_edge_cover_thk / cos(alphaL) - ac_roofthk * tan(alphaL),
		ac_roofthk, -ac_roofthk * tan(alphaL)

	addy B

	prism 4, A - 2 * gs_edge_cover_thk,
		0,			 0,
		0,			-gs_edge_cover_thk / cos(alphaU),
		ac_roofthk, -gs_edge_cover_thk / cos(alphaU) - ac_roofthk * tan(alphaU),
		ac_roofthk, -ac_roofthk * tan(alphaU)
	del 1

	prism 4, -gs_edge_cover_thk,
		0,			0,
		0,			B,
		ac_roofthk,	B - ac_roofthk * tan(alphaU),
		ac_roofthk, -ac_roofthk * tan(alphaL)

	addz A - gs_edge_cover_thk

	prism 4, -gs_edge_cover_thk,
		0,			0,
		0,			B,
		ac_roofthk,	B - ac_roofthk * tan(alphaU),
		ac_roofthk, -ac_roofthk * tan(alphaL)
	del 4
endif


! ==============================================================================
! Opening Lines
! ==============================================================================

bOplines3d				= 0
opLineType3dIn			= 1
opLineType3dOut			= 1
gs_opline_style_m		= 1
gs_opLinePen			= 1

call "OpeningOptions" parameters all	bSkylight			= 1,
										bWindow				= 1,
										gs_opening_dir_m	= 0,
										bOverride_MVO_3D	= bOverride_MVO_3D,
										or_bOplines3d		= or_bOplines3d,
										or_iOpLineType3dIn	= or_iOpLineType3dIn,
										or_iOpLineType3dOut	= or_iOpLineType3dOut,
										or_iOplineStyle		= or_iOplineStyle,
										or_opLinePen		= or_opLinePen,
										bEnableSecondaryOpLineTypes = 0,
						returned_parameters bOplines3d,
											opLineType3dOut,
											opLineType3dIn,
											dummy,
											gs_opline_style_m,
											gs_opLinePen


if bProjected2DContext then
	gs_opline_style_m = 1
	success = LIBRARYGLOBAL ("LibraryGlobals13", "gs_opline_style_m", reqIOplineStyleTemp)
	if success > 0 then
		gs_opline_style_m = reqIOplineStyleTemp
	endif

	gs_iSwingType = 1
	reqSwingType = -1

	success = LIBRARYGLOBAL ("LibraryGlobals13", "W_iSwingType", reqSwingType)
	if success > 0 and reqSwingType > -1 then
		gs_iSwingType = reqSwingType
	endif

	gs_opLinePen = 1
	success = LIBRARYGLOBAL ("LibraryGlobals13", "W_opLinePen", reqOplinePen)
	if success > 0 then
		gs_opLinePen = reqOplinePen
	endif

	gs_swingLineType = 1
	success = LIBRARYGLOBAL ("LibraryGlobals13", "W_swingLineType", reqSwingLineType)
	if success > 0 then
		gs_swingLineType = reqSwingLineType
	endif

	if bOverride_MVO_3D then
		gs_opLinePen		= or_opLinePen
		gs_opline_style_m	= or_iOplineStyle
	endif
endif

bOplines3d = (bOplines3d & lod3D_bOplines)


! ==============================================================================

end iLoD3D,
	lod3D_Casing,
	bOplines3d, opLineType3dIn, opLineType3dOut, gs_opline_style_m, gs_opLinePen, iOldResol

