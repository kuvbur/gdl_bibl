
! const values for parameter: iDWDet2D
if	LibraryLangCode = "AUT" |\
	LibraryLangCode = "CHE" |\
	LibraryLangCode = "GER" then

	DETLEVEL_DW2D_MVO    = 1
	DETLEVEL_DW2D_SCSENS = 2
	DETLEVEL_DW2D_50     = 3
	DETLEVEL_DW2D_100    = 4
	DETLEVEL_DW2D_200    = 5
	DETLEVEL_DW2D_OFF    = 8
	DETLEVEL_DW2D_20     = 9
	DETLEVEL_DW2D_500    = 10
else
	DETLEVEL_DW2D_MVO     = 1
	DETLEVEL_DW2D_SCSENS  = 2
	DETLEVEL_DW2D_50      = 3
	DETLEVEL_DW2D_100     = 4
	DETLEVEL_DW2D_MIDDET  = 5
	DETLEVEL_DW2D_SMPL2L  = 6
	DETLEVEL_DW2D_200     = 7
	DETLEVEL_DW2D_OFF     = 8
	DETLEVEL_DW2D_MIDUSA  = 9
	DETLEVEL_DW2D_CHISMPL = 10
	DETLEVEL_DW2D_CHIDET  = 11
	DETLEVEL_DW2D_20      = 12
endif


! const values for parameter: iTypeCurvedWallCut
CUT_PARALLEL = 1
CUT_RADIAL   = 2


! const values for parameter: iTypeDemolitionFill
RENOVATION_FILL_EXIST		= 1
RENOVATION_FILL_DEMOLISHED	= 2
RENOVATION_FILL_NEW			= 3
RENOVATION_FILL_NEW_FULL	= 4


! const values for parameter: iBoardInPolyWall
BOARD_PARALLEL_WINDOW      = 1
BOARD_PARALLEL_WALLPOLYGON = 2


! const values for parameter: iCustomCCEditWallSide
EDITED_WALL_FACE_OUTSIDE = 1
EDITED_WALL_FACE_INSIDE  = 2


! const values for parameter: iCustomCCEditJambSide
EDITED_JAMB_SIDE_BOTH  = 1
EDITED_JAMB_SIDE_LEFT  = 2
EDITED_JAMB_SIDE_RIGHT = 3


! const values for parameter: iCustomCCThicknessType
THICKNESS_TYPE_ORIGINAL = 1
THICKNESS_TYPE_CUSTOM   = 2


! const values for parameter: iCustomCCEditSkin
EDITED_SKIN_1 = 1
EDITED_SKIN_2 = 2
EDITED_SKIN_3 = 3
EDITED_SKIN_4 = 4


! const values for parameter: iCustomCCOrientation
ORIENTATION_ORIGINAL  = 1
ORIENTATION_TURNED_IN = 2


! const values for parameter: iSillType
SILL_TIMBER       = 1
SILL_BRICK        = 2
SILL_STONE        = 3
SILL_CERAMIC      = 4
SILL_ETHERNIT     = 5
SILL_CONCRETE_TUB = 6
SILL_ALUMINIUM    = 7
SILL_SOLID_11     = 8
SILL_SOLID_12     = 9
SILL_SOLID_22     = 10
SILL_METAL_1      = 11
SILL_PARALLEL     = 12
SILL_METAL_2      = 13
SILL_PARALLEL_2   = 14
SILL_SIMPLEBLOCK  = 15
SILL_BLOCK        = 16
SILL_DUTCH        = 17

! Contact person: <NJ>

EPS = 0.0001

! iRevealType:
REVEAL_EDGE				= 0
REVEAL_NO				= 1
REVEAL_POSITIVE			= 2
REVEAL_NEGATIVE			= 3
REVEAL_SLANTED			= 4
REVEAL_SPLAYED			= 5
REVEAL_HISTORIC			= 6
REVEAL_DOUBLE			= 7
REVEAL_DOUBLE_SPLAYED	= 8
REVEAL_OUTSIDE_SPLAYED	= 9

! iRevealOnSymbol
SHOWREV_MVO		= 1
SHOWREV_ALWAYS	= 2
SHOWREV_UPTO50	= 3
SHOWREV_UPTO100	= 4
SHOWREV_UPTO200	= 5
SHOWREV_NEVER	= 6

! iClosureType:
CLOSURE_NO					= 0
CLOSURE_TRADITIONAL			= 1
CLOSURE_PREFABRICATED		= 2
CLOSURE_CUSTOM				= 3
CLOSURE_SOLID_WALL			= 4
CLOSURE_STUD_WALL			= 5
CLOSURE_STUD_WALL_SIDING	= 6
CLOSURE_BRICK_VENEER		= 7
CLOSURE_VARIABLE			= 8
CLOSURE_PLASTER_REVEAL	 			= 9
CLOSURE_INSULATED_REVEAL			= 10
CLOSURE_INSULATED_FOR_RENOVATION	= 11
CLOSURE_FINISH						= 12
CLOSURE_AIR_SPACE					= 13
CLOSURE_DUTCH_VARIABLE				= 14

! Partial Stucture Display defines:
STRUCTURE_ENTIRE_STRUCTURE	= 0
STRUCTURE_CORE_ONLY			= 1
STRUCTURE_WITHOUT_FINISHES	= 2

! lod2d
DET2D_FS_OFF				= 0
DET2D_FS_SCALESENS			= -1
DET2D_FS_SIMPLE_ONE			= 11
DET2D_FS_SIMPLE_TWO			= 12
DET2D_FS_MIDDLE				= 21
DET2D_FS_MIDDLE_USA			= 22
DET2D_FS_DETAILED			= 30
DET2D_FS_DETAILED_FIX_GRID	= 31
DET2D_FS_DETAILED_USA		= 32
DET2D_FS_SIMPLE_CHI			= 51
DET2D_FS_DETAILED_CHI		= 52

! iLoD2D
SYMBW_OFF					= 0
SYMBW_SCALESENS				= 1
SYMBW_S_1LINE				= 11
SYMBW_S_2LINE				= 12
SYMBW_S_CENTERLINE			= 13
SYMBW_S_2LINE_ACC			= 14
SYMBW_S_5					= 15
SYMBW_S_6					= 16
SYMBW_S_7					= 17
SYMBW_S_8					= 18
SYMBW_S_9					= 19
SYMBW_M_2LINE_NOGLASS		= 21
SYMBW_M_2LINE_GLASS			= 22
SYMBW_M_FRAMESASH			= 23
SYMBW_M_2LINE_FRAME			= 24
SYMBW_M_5					= 25
SYMBW_M_6					= 26
SYMBW_M_7					= 27
SYMBW_M_8					= 28
SYMBW_M_9					= 29
SYMBW_D_SGLASS				= 31
SYMBW_D_2GLASS				= 32
SYMBW_D_2LINE_HS			= 33
SYMBW_D_NOGLASS				= 34
SYMBW_D_REBATEONGLASS		= 35
SYMBW_D_2GLASS_OVERLAP		= 36
SYMBW_D_7					= 37
SYMBW_D_8					= 38
SYMBW_D_9					= 39

! iWindowShape:
SHAPE_RECTANGULAR			= 0
SHAPE_ARCHED				= 1
SHAPE_ROUND					= 2
SHAPE_EYEBROWS				= 3
SHAPE_HALFCIRCLE			= 4 	! straight side is horizontal
SHAPE_MULTISIDE				= 5
SHAPE_OCTAGONAL				= 6
SHAPE_HALFARCH				= 7
SHAPE_PENTAGON				= 8
SHAPE_QUARTERROUND			= 9
SHAPE_HALFROUND				= 10 	! straight side is vertical
SHAPE_TRAPEZOID				= 11
SHAPE_TRIANGLE				= 12
SHAPE_GOTHICARCH			= 13
SHAPE_ELLIPSEARCH			= 14
SHAPE_ROMBUS				= 15
SHAPE_HORSESHOE				= 16
SHAPE_OGEECENTERED			= 17
SHAPE_SARACENIC				= 18
SHAPE_T						= 19
SHAPE_HALF_T_LEFT			= 20
SHAPE_ARCHED_T				= 21
SHAPE_ARCHED_HALF_T_LEFT	= 22
SHAPE_PARALLELOGRAM			= 23
SHAPE_ELLIPSE				= 24
SHAPE_CORNER_TRIANGLE		= 25
SHAPE_QUARTERROUND_NOFRAME	= 26
SHAPE_HALF_T_RIGHT			= 27
SHAPE_ARCHED_HALF_T_RIGHT	= 28

! Turn Skin on Side
CAVITY_SIDE_BOTH	= 1
CAVITY_SIDE_LEFT	= 2
CAVITY_SIDE_RIGHT	= 3
CAVITY_SIDE_NONE	= 4

! Wall Inset Sizes
PARAPET_SIZE_AUTO	= 1
PARAPET_SIZE_SYMM	= 2
PARAPET_SIZE_ASYMM	= 3

! Wall Inset Bottom at
PARAPET_BOTTOM_CURRENTSTORY	= 1
PARAPET_BOTTOM_WALLBASE		= 2

! Wall Contour Line - iWallContourLine
WALL_CONTOUR_OFF		= 1
WALL_CONTOUR_INSIDE		= 2
WALL_CONTOUR_OUTSIDE	= 3
WALL_CONTOUR_BOTHSIDES	= 4

! ==============================================================================
! Localization
! ==============================================================================

dim stDetail2D[13]
	stDetail2D[1]	= `по ПМВ`
	stDetail2D[2]	= `Зависит от Масштаба`
	stDetail2D[3]	= `1:50`
	stDetail2D[4]	= `1:100`
	stDetail2D[5]	= `Средняя Детализация - без Аксесс.`
	stDetail2D[6]	= `Простой - Две Линии`
	stDetail2D[7]	= `1:200`
	stDetail2D[8]	= `Откл.`
	stDetail2D[9]	= `Середина США`
	stDetail2D[10]	= `Китайский Простой`
	stDetail2D[11]	= `Китайский Детальный`
	stDetail2D[12]	= `1:20`
	stDetail2D[13]	= `1:500`

dim stRevealTypes[10]	! gs_reveal_type
	stRevealTypes[ 1] = `Без Четверти - Торец`
	stRevealTypes[ 2] = `Без Четверти`
	stRevealTypes[ 3] = `Четверть`
	stRevealTypes[ 4] = `Отрицательная Четверть`
	stRevealTypes[ 5] = `Четверть с Рассветами`
	stRevealTypes[ 6] = `Четверть с Откосом`
	stRevealTypes[ 7] = `Историческая Четверть`
	stRevealTypes[ 8] = `Специальная Четверть`
	stRevealTypes[ 9] = `Четверть с Двумя Откосами`
	stRevealTypes[10] = `Четверть с Откосом Снаружи`

dim stRevealTypesBottom[4]		! gs_reveal_type_bottom
	stRevealTypesBottom[1] = `Одинаковый`
	stRevealTypesBottom[2] = `Без Четверти`
	stRevealTypesBottom[3] = `Четверть`
	stRevealTypesBottom[4] = `Отрицательная Четверть`

dim stRevealAngleEditBy[2]	! gs_reveal_angle_edit_by
	stRevealAngleEditBy[1] = `Угол и Глубина`
	stRevealAngleEditBy[2] = `Ширина и Глубина`

dim stClosureTypes[15]	! gs_closure_type
	stClosureTypes[ 1] = `Без Замыкания`
	stClosureTypes[ 2] = `Традиционное Замыкание Слоев`
	stClosureTypes[ 3] = `Готовое Замыкание Слоев`
	stClosureTypes[ 4] = `Специальное Замыкание`
	stClosureTypes[ 5] = `Сплошная Стена`
	stClosureTypes[ 6] = `Деревянная Каркасная Стена`
	stClosureTypes[ 7] = `Деревянная Каркасная Стена с Наружной Обшивкой`
	stClosureTypes[ 8] = `Кирпичная Облицовка`
	stClosureTypes[ 9] = `Настраиваемое Замыкание`
	stClosureTypes[10] = `Штукатурная Четверть`
	stClosureTypes[11] = `Утепленная Четверть`
	stClosureTypes[12] = `Утепление при Реконструкции`
	stClosureTypes[13] = `Замыкание Отделкой`
	stClosureTypes[14] = `Замыкание с Воздушной Прослойкой`
	stClosureTypes[15] = `Датское Замыкание`

dim stRevealOnSymbValues[6]
	stRevealOnSymbValues[1] = `по ПМВ`
	stRevealOnSymbValues[2] = `Всегда`
	stRevealOnSymbValues[3] = `До 1:50`
	stRevealOnSymbValues[4] = `До 1:100`
	stRevealOnSymbValues[5] = `До 1:200`
	stRevealOnSymbValues[6] = `Никогда`

dim stWallContourLine[4]	! gs_wcont_line_sl
	stWallContourLine[1] = `Откл.`
	stWallContourLine[2] = `Внутри`
	stWallContourLine[3] = `Снаружи`
	stWallContourLine[4] = `Обе Стороны`

dim stCavityClosureSide[4]		! gs_cavity_side
	stCavityClosureSide[1] = `Обе`
	stCavityClosureSide[2] = `Слева`
	stCavityClosureSide[3] = `Справа`
	stCavityClosureSide[4] = `Нет`

dim stCavityClosureTurnDir[2]	! gs_nTurnDir
	stCavityClosureTurnDir[1] = `Снаружи`
	stCavityClosureTurnDir[2] = `Внутри`

dim stDutchClosureToSide[2]
	stDutchClosureToSide[1] = `Снаружи`
	stDutchClosureToSide[2] = `Внутри`

dim stCavityClosureEditedJambSide[3]
	stCavityClosureEditedJambSide[1] = `Редактирование Обоих Откосов`
	stCavityClosureEditedJambSide[2] = `Редактирование Левого Откоса`
	stCavityClosureEditedJambSide[3] = `Редактирование Правого Откоса`

dim stCavityClosureEditedWallSide[2]
	stCavityClosureEditedWallSide[1] = `Снаружи`
	stCavityClosureEditedWallSide[2] = `Внутри`

dim stCavityClosureEditedSkinNumber[4]
	stCavityClosureEditedSkinNumber[1] = `1. Слой`
	stCavityClosureEditedSkinNumber[2] = `2. Слой`
	stCavityClosureEditedSkinNumber[3] = `3. Слой`
	stCavityClosureEditedSkinNumber[4] = `4. Слой`

dim stCavityClosureTurnInThickness[2]
	stCavityClosureTurnInThickness[1] = `Исходная`
	stCavityClosureTurnInThickness[2] = `Специальная`

dim stCavityClosureHatchOrientation[2]
	stCavityClosureHatchOrientation[1] = `Исходная`
	stCavityClosureHatchOrientation[2] = `Поворот Внутрь`

dim stMasonryArchTypes[7]	! gs_masonry_arch_int
	stMasonryArchTypes[1] = `Откл.`
	stMasonryArchTypes[2] = `Прямая Перемычка`
	stMasonryArchTypes[3] = `Клинчатая Перемычка`
	stMasonryArchTypes[4] = `Клинчатая Перемычка с Замковым Камнем`
	stMasonryArchTypes[5] = `Лучковая Перемычка`
	stMasonryArchTypes[6] = `Лучковая Перемычка с Замковым Камнем`
	stMasonryArchTypes[7] = `Декоративная Арка`

dim gs_faschen_type[2]	! faschen
	gs_faschen_type[1] = `Штукатурка`
	gs_faschen_type[2] = `Кирпичная Кладка`

dim stMasonryArchStructure[2]	! gs_masonry_arch_structure
	stMasonryArchStructure[1] = `Штукатурная`
	stMasonryArchStructure[2] = `Кирпичная`

dim stSillTypes[17]	! gs_sill_type
	stSillTypes[ 1] = `Деревянный Отлив`			! Timber Sill
	stSillTypes[ 2] = `Кирпичный Отлив`
	stSillTypes[ 3] = `Каменный Отлив`
	stSillTypes[ 4] = `Керамический Отлив`
	stSillTypes[ 5] = `Этернитовый Отлив`
	stSillTypes[ 6] = `Бетонный Отлив`
	stSillTypes[ 7] = `Алюминиевый Отлив`
	stSillTypes[ 8] = `Монолитный Отлив 1.1`	! Massiv-Fensterbank 1.1
	stSillTypes[ 9] = `Монолитный Отлив 1.2`	! Massiv-Fensterbank 1.2
	stSillTypes[10] = `Монолитный Отлив`		! Massiv-Fensterbank 2.2
	stSillTypes[11] = `Металлический Лист`		! Metallfensterbank 1.1
	stSillTypes[12] = `Сплошной Параллельный`	! Massiv parallel
	stSillTypes[13] = `Металлический Лист 2`
	stSillTypes[14] = `Сплошной Параллельный 2`	! Massiv parallel 2
	stSillTypes[15] = `Простой Блок`
	stSillTypes[16] = `Блок Отлив`
	stSillTypes[17] = `Голландский Профилированный Отлив`

dim stBsilldetlevel[2]	! gs_brick_sill_detlev
	stBsilldetlevel[1] = `Простой`
	stBsilldetlevel[2] = `Детальный`

dim stOpsideType[2]	! gs_opside
	stOpsideType[1] = `Параллельно`
	stOpsideType[2] = `Радиально`

dim stParapetBottom[2]	! gs_parapet_bottom_st
	stParapetBottom[1] = `Текущий Этаж`
	stParapetBottom[2] = `Основание Стены`

dim stParapetSize[3] ! gs_parapet_sizes_st
	stParapetSize[1] = `Автоматически`
	stParapetSize[2] = `Симметрично`
	stParapetSize[3] = `Асимметрично`

dim stBoardInPolyWall[2] ! gs_boardInPolyWall
	stBoardInPolyWall[1] = `Параллельно Окну`
	stBoardInPolyWall[2] = `Параллельно Многоугольнику Стены`

dim stCasingType[3] ! gs_trimtype_in, gs_trimtype_out
	stCasingType[1] = `Простой`
	stCasingType[2] = `Простой с Выступом`
	stCasingType[3] = `Сложный с Выступом`

dim stSunShadeTypes[ 9]
	stSunShadeTypes[ 1] = `Нет`
	stSunShadeTypes[ 2] = `Декоративные`
	stSunShadeTypes[ 3] = `Обычные`
	stSunShadeTypes[ 4] = `Складывание к Четверти`
	stSunShadeTypes[ 5] = `Складывание к Поверхности Стены`
	stSunShadeTypes[ 6] = `Раздвижные`
	stSunShadeTypes[ 7] = `Рольставни`
	stSunShadeTypes[ 8] = `Наружные Жалюзи`
	stSunShadeTypes[ 9] = `Жалюзи в Перемычке`

dim stShutterPanelPos[11]
	stShutterPanelPos[ 1] = `Симметрично`
	stShutterPanelPos[ 2] = `Слева`
	stShutterPanelPos[ 3] = `Справа`
	stShutterPanelPos[ 4] = `Обычно Слева`
	stShutterPanelPos[ 5] = `Обычно Справа`
	stShutterPanelPos[ 6] = `Слева Вверху`
	stShutterPanelPos[ 7] = `Слева Внизу`
	stShutterPanelPos[ 8] = `Справа Вверху`
	stShutterPanelPos[ 9] = `Справа Внизу`
	stShutterPanelPos[10] = `Двойное Вверху`
	stShutterPanelPos[11] = `Двойное Внизу`

dim stFoldingShutterStacking[2]
	stFoldingShutterStacking[1] = `Внутри`
	stFoldingShutterStacking[2] = `Снаружи`

dim stSlidingShutterCover[3]
	stSlidingShutterCover[1] = `Нет`
	stSlidingShutterCover[2] = `Z-профиль`
	stSlidingShutterCover[3] = `Корпус`

dim stShutterPanelTypes[11]
	stShutterPanelTypes[ 1] = `Стиль 1`
	stShutterPanelTypes[ 2] = `Стиль 2`
	stShutterPanelTypes[ 3] = `Стиль 3`
	stShutterPanelTypes[ 4] = `Стиль 4`
	stShutterPanelTypes[ 5] = `Стиль 5`
	stShutterPanelTypes[ 6] = `Стиль 6`
	stShutterPanelTypes[ 7] = `Стиль 7`
	stShutterPanelTypes[ 8] = `Стиль 8`
	stShutterPanelTypes[ 9] = `Стиль 9`
	stShutterPanelTypes[10] = `Стиль 10`
	stShutterPanelTypes[11] = `Специальная`

dim stShutRollPlacement[3]
	stShutRollPlacement[1] = `На Фасаде`
	stShutRollPlacement[2] = `Перед Окном`
	stShutRollPlacement[3] = `За Фасадом`

dim stShutRollBoxType[2]
	stShutRollBoxType[1] = `Прямоугольник`
	stShutRollBoxType[2] = `Закругленный`

dim stShutRollBoxDefaultSize[2]
	stShutRollBoxDefaultSize[1] = `По Ширине Четверти`
	stShutRollBoxDefaultSize[2] = `По Ширине Проема`

dim stShutLamPlacement[3]
	stShutLamPlacement[1] = `На Фасаде`
	stShutLamPlacement[2] = `Перед Окном`
	stShutLamPlacement[3] = `За Фасадом`

dim stShutRollRailPos[2]
	stShutRollRailPos[1] = `На Наличнике`
	stShutRollRailPos[2] = `На Стене`

dim stShutLamRailPos[2]
	stShutLamRailPos[1] = `На Наличнике`
	stShutLamRailPos[2] = `На Стене`


! =============================================================================
! Array index definitions
! =============================================================================

iRevealType = gs_reveal_type_int - 1

if gs_bShowNewOnCC & iTypeDemolitionFill = RENOVATION_FILL_NEW then iRevealType = REVEAL_NO

iSillRevealType  = gs_reveal_doubleOuterTypeInt - 1
iBoardRevealType = gs_reveal_doubleInnerTypeInt - 1


! ==============================================================================
! Function Control
! ==============================================================================

bEnableParapetInset			= (iWindowShape = SHAPE_RECTANGULAR | iWindowShape = SHAPE_ARCHED | \
				iWindowShape = SHAPE_EYEBROWS     | iWindowShape = SHAPE_PENTAGON | \
				iWindowShape = SHAPE_QUARTERROUND | iWindowShape = SHAPE_TRAPEZOID | \
				iWindowShape = SHAPE_TRIANGLE     | iWindowShape = SHAPE_GOTHICARCH | \
				iWindowShape = SHAPE_ELLIPSEARCH  | iWindowShape = SHAPE_HORSESHOE | \
				iWindowShape = SHAPE_OGEECENTERED | iWindowShape = SHAPE_SARACENIC | \
				iWindowShape = SHAPE_QUARTERROUND_NOFRAME )

if bEnableMasonryArch < 0 then
	bEnableMasonryArch			= (iWindowShape = SHAPE_RECTANGULAR | iWindowShape = SHAPE_ARCHED | iWindowShape = SHAPE_ROUND | iWindowShape = SHAPE_ELLIPSE)
endif
bEnableFaschen				= 0
bEnableSill					= (iWindowShape = SHAPE_ARCHED | iWindowShape = SHAPE_ELLIPSEARCH | iWindowShape = SHAPE_GOTHICARCH | iWindowShape = SHAPE_PENTAGON)
bEnableBoard				= (iWindowShape = SHAPE_ARCHED | iWindowShape = SHAPE_ELLIPSEARCH | iWindowShape = SHAPE_GOTHICARCH | iWindowShape = SHAPE_PENTAGON)
bEnableThreshold			= 0
bEnableCasing				= (iWindowShape = SHAPE_ARCHED)
bEnableShutter				= (iWindowShape = SHAPE_ARCHED)
bEnableCustomCornerLeft		= 0
bEnableCustomCornerRight	= 0
bEnableVentilationProfile	= 0
bEnableWallContourHiddenLine	= 1

if isEmptyOpening	< 0 then isEmptyOpening = 1

if gs_RevealTypeEnable = 0 then
	gs_RevealTypeEnable	 = 1 + 2
	if iWindowShape = SHAPE_ARCHED | iWindowShape = SHAPE_RECTANGULAR then
		gs_RevealTypeEnable	= 1 + 2 + 4 + 8 + 16 + 32 + 64 + 128
	endif
	if iWindowShape = SHAPE_HALFARCH | iWindowShape = SHAPE_GOTHICARCH \
			| iWindowShape = SHAPE_ROUND | iWindowShape = SHAPE_ROMBUS | iWindowShape = SHAPE_PENTAGON \
			| iWindowShape = SHAPE_TRAPEZOID | iWindowShape = SHAPE_ELLIPSEARCH | iWindowShape = SHAPE_OCTAGONAL \
			| iWindowShape = SHAPE_MULTISIDE | iWindowShape = SHAPE_HALFROUND then
		gs_RevealTypeEnable	= 1 + 2 + 4 + 8 + 128
	endif
endif
if gs_ClosureTypeEnable = 0 then gs_ClosureTypeEnable = 1 + 2 + 4 + 8 + 256 + 512 + 1024 + 2048
if gs_SillTypeEnable = 0 then
	gs_SillTypeEnable	= 1 + 2 + 4 + 512 + 1024
		! Ceramic, Eternite, Concrete, Aluminum and German massiv sill types are hidden
endif

defaultSillDepth	= 0.120

bCasingTolerance	= 0
bSillPlaneCut		= 1
bBoardPlaneCut		= 1

! in case of SUNSHADE_ROLLING and SHUTROLL_BEHIND placement
if bEnableShutter & bSunshade & (gs_sunShade_iType = 6) & (gs_shutRoll_iPlacement = 3) & gs_shutRoll_bIncludeBoxHeight then
	_excludeRevealHeight	= 0
	_excludeCasingHeight	= 0
	
	if iRevealType = REVEAL_POSITIVE | iRevealType = REVEAL_SLANTED | iRevealType = REVEAL_HISTORIC | (iRevealType = REVEAL_DOUBLE & (iSillRevealType = 2 | gs_reveal_doubleOuterTypeInt = 3)) then
		if gs_reveal_uniform then
			_excludeRevealHeight = gs_reveal_right
		else
			_excludeRevealHeight = gs_reveal_top
		endif
	endif
	if (bEnableCasing & gs_trim_outside) then
		_excludeCasingHeight	= gs_trim_width_out
	endif

	_includeBoxHeight = (gs_shutRoll_boxHeight + gs_shutRoll_distFromHead - _excludeRevealHeight - _excludeCasingHeight)
else
	_includeBoxHeight = 0
endif


! ==============================================================================

curvedWall		= abs(WIDO_ORIG_DIST) > EPS
wallIsLeft		= (SYMB_ROTANGLE > EPS) exor WIDO_REVEAL_SIDE
bInclinedWall	= (abs(WALL_INCL) > EPS)

gs_IsCurved				= 0
bLeftCornerFunction		= 0
bRightCornerFunction	= 0


! =============================================================================
! Show Reveal On Symbol
! =============================================================================

bCanHaveReveal = (bittest(gs_RevealTypeEnable, REVEAL_POSITIVE)			| \
				  bittest(gs_RevealTypeEnable, REVEAL_NEGATIVE)			| \
				  bittest(gs_RevealTypeEnable, REVEAL_SLANTED)			| \
				  bittest(gs_RevealTypeEnable, REVEAL_SPLAYED)			| \
				  bittest(gs_RevealTypeEnable, REVEAL_HISTORIC)			| \
				  bittest(gs_RevealTypeEnable, REVEAL_DOUBLE)			| \
				  bittest(gs_RevealTypeEnable, REVEAL_DOUBLE_SPLAYED)	| \
				  bittest(gs_RevealTypeEnable, REVEAL_OUTSIDE_SPLAYED)	)


! =============================================================================
! Request globals for 2D (MVO Detail Level, MVO Reveal, Hotspots for IS)
! =============================================================================

iMVODetlevel2D			= 0
bMVOCasing2D			= 0
bShowAddHotspots		= 1
bMVODetlevelReveal		= 0
bShowAddHotspotsForIS	= 0

bGetMVOValueDlev2D		= (iDWDet2D = DETLEVEL_DW2D_MVO)
bGetMVOValueCasing2D	= (bGetMVOValueDlev2D and bNotShowCasingIn2DNOR)
bGetMVOValueDlevReveal	= (iRevealOnSymbol = SHOWREV_MVO)
if GLOB_SCRIPT_TYPE = 2 then
	bGetHotspotsForIS		= (GLOB_CONTEXT = 6)

	if bGetMVOValueDlev2D | bGetMVOValueDlevReveal | bGetHotspotsForIS then
		call "GetDWSymbSettings" parameters	bWindow					= 1,
											bGetMVOValueDlev2D		= bGetMVOValueDlev2D,
											bGetMVOValueCasing2D	= bGetMVOValueCasing2D,
											bGetMVOValueDlevReveal	= bGetMVOValueDlevReveal,
											bGetHotspotsForIS		= bGetHotspotsForIS,
						returned_parameters iMVODetlevel2D,
											bMVOCasing2D,
											bMVODetlevelReveal,
											bShowAddHotspotsForIS
	endif
endif


! =============================================================================
! Request globals for 3D (MVO SE Detail Level, Hotspots for IS)
! =============================================================================

iMVODetlevelSE			= 0
bGetMVOValueDlevSE		= 0					! not used in INT
if GLOB_SCRIPT_TYPE = 3 then
	bGetHotspotsForIS		= (GLOB_CONTEXT = 6)

	if bGetMVOValueDlevSE | bGetHotspotsForIS then
		call "GetDWSymbSettings" parameters	bWindow					= 1,
											bGetMVOValueDlevSE		= bGetMVOValueDlevSE,
											bGetHotspotsForIS		= bGetHotspotsForIS,
						returned_parameters iMVODetlevelSE,
											bShowAddHotspotsForIS
	endif
endif


! =============================================================================
! 2D detail level settings
! =============================================================================

iLoD2D	= SYMBW_SCALESENS
if GLOB_SCRIPT_TYPE = 2 then
	if bGetMVOValueDlev2D then													! by MVO
		iLoD2D	= iMVODetlevel2D
		if	iLoD2D = SYMBW_M_2LINE_FRAME |\
			iLoD2D = SYMBW_D_2GLASS_OVERLAP then

			iLoD2D = SYMBW_D_SGLASS
			if iMVODetlevel2D < SYMBW_D_SGLASS then				iLoD2D	= SYMBW_M_2LINE_NOGLASS
			if iMVODetlevel2D < SYMBW_M_2LINE_NOGLASS then		iLoD2D	= SYMBW_S_1LINE
		endif

		if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
			iLoD2D = SYMBW_D_SGLASS
		endif
	else
		if iDWDet2D = DETLEVEL_DW2D_50 then
			iLoD2D = SYMBW_D_SGLASS														! Detailed --> INT 50
			if bShowRebateOnFixGlass		then	iLoD2D = SYMBW_D_REBATEONGLASS		! Detailed --> NLD 50
		endif
		if iDWDet2D = DETLEVEL_DW2D_100 then
			iLoD2D = SYMBW_M_2LINE_NOGLASS												! Middle - with acc --> INT 100
			if bShowSashInMiddle2D			then	iLoD2D = SYMBW_M_FRAMESASH			! Middle --> NLD 100
		endif
		if iDWDet2D = DETLEVEL_DW2D_200		then	iLoD2D = SYMBW_S_1LINE				! Simple - One Line --> INT 200
		if iDWDet2D = DETLEVEL_DW2D_OFF		then	iLoD2D = SYMBW_OFF					! Off
		if iDWDet2D = DETLEVEL_DW2D_20		then	iLoD2D = SYMBW_D_2GLASS				! Detailed --> DAN 20

		if	LibraryLangCode = "AUT" |\
			LibraryLangCode = "CHE" |\
			LibraryLangCode = "GER" then
			if iDWDet2D = DETLEVEL_DW2D_500		then	iLoD2D = SYMBW_S_1LINE			! DACH 500
		else
			if iDWDet2D = DETLEVEL_DW2D_MIDDET	then	iLoD2D = SYMBW_M_2LINE_GLASS	! Middle - without acc
			if iDWDet2D = DETLEVEL_DW2D_SMPL2L	then	iLoD2D = SYMBW_S_2LINE_ACC		! Simple - Two Lines
			if iDWDet2D = DETLEVEL_DW2D_MIDUSA	then	iLoD2D = SYMBW_M_FRAMESASH		! Middle USA
			if iDWDet2D = DETLEVEL_DW2D_CHISMPL	then	iLoD2D = SYMBW_S_2LINE			! Chinese Simple
			if iDWDet2D = DETLEVEL_DW2D_CHIDET	then	iLoD2D = SYMBW_D_2LINE_HS		! Chinese Detailed
		endif
	endif
endif

! =============================================================================
! Show Reveal On Symbol
! =============================================================================

bShowReveal = 1
if GLOB_SCRIPT_TYPE = 2 then
	if bGetMVOValueDlevReveal then																				! by MVO
		bShowReveal = bMVODetlevelReveal
	else
		bShowReveal = 1
		if iRevealOnSymbol = SHOWREV_NEVER											then bShowReveal = 0		! Never
		if iLoD2D = SYMBW_SCALESENS then																		! Scale sensitive
			if iRevealOnSymbol = SHOWREV_UPTO50 & GLOB_SCALE > 75					then bShowReveal = 0		! Up to 1:50
			if iRevealOnSymbol = SHOWREV_UPTO100 & GLOB_SCALE > 150					then bShowReveal = 0		! Up to 1:100
		else
			if iRevealOnSymbol = SHOWREV_UPTO50 & iLoD2D < SYMBW_D_SGLASS			then bShowReveal = 0		! Up to 1:50
			if iRevealOnSymbol = SHOWREV_UPTO100 & iLoD2D < SYMBW_M_2LINE_NOGLASS	then bShowReveal = 0		! Up to 1:100
		endif
		if iRevealOnSymbol = SHOWREV_UPTO200 & GLOB_SCALE > 200						then bShowReveal = 0		! Up to 1:200
	endif
endif

! =============================================================================
! Hide Variable Closure Type NED, FLA
! =============================================================================

iClosureType = gs_closure_type_int - 1
if GLOB_SCRIPT_TYPE = 2 then
	if	bHideVariClosTypeInSimple2D &\
		iClosureType = CLOSURE_VARIABLE	&\
		((iLoD2D < SYMBW_M_2LINE_NOGLASS & iLoD2D > SYMBW_SCALESENS) | (iLoD2D = SYMBW_SCALESENS & GLOB_SCALE > 150)) then
		iClosureType = CLOSURE_NO
	endif
endif


! ==============================================================================
! Symbol at Middle
! ==============================================================================

signRevealSide = not(WIDO_REVEAL_SIDE) - WIDO_REVEAL_SIDE

if GLOB_SCRIPT_TYPE = 2 then
	if GLOB_CONTEXT = 2 | GLOB_CONTEXT = 22 then
		if	iLoD2D = SYMBW_S_2LINE | iLoD2D = SYMBW_D_2LINE_HS then bDWSymbolAtMiddle = 1
		if	iLoD2D = SYMBW_S_CENTERLINE |\
			(iLoD2D = SYMBW_SCALESENS and GLOB_SCALE > 150) then bDWSymbolAtMiddle2 = 1
	endif
endif
if GLOB_SCRIPT_TYPE = 3 then
	if GLOB_CONTEXT = 4 | GLOB_CONTEXT = 24 | GLOB_CONTEXT = 6 then
		if lod3D = 5 then bDWSymbolAtMiddle = 1
	endif
endif

SymbMid_LeftDx		= 0
SymbMid_RightDx		= 0
SymbolMid_shiftDy	= 0

if bDWSymbolAtMiddle then
	SymbolMid_shiftDy = (WIDO_SILL - WALL_THICKNESS / 2 + WIDO_FRAME_THICKNESS / 2) * signRevealSide
endif

if bDWSymbolAtMiddle2 then
	SymbolMid_shiftDy = (WIDO_SILL - WALL_THICKNESS / 2) * signRevealSide
endif


! =============================================================================
! Array index definitions
! =============================================================================

if not(bEnableSill) then
	iSillType = 0
endif

bBoardInPolyWall = 0
if iBoardInPolyWall = BOARD_PARALLEL_WINDOW then bBoardInPolyWall = 1 ! Parallel to Window


! =============================================================================
! Definitions
! =============================================================================

bDrawOutsideTrim	= (gs_trim_outside)
bDrawInsideTrim		= (gs_trim_inside)
bDrawSill			= (gs_sill_outside)
bDrawBoard			= (gs_board_inside)



! ==============================================================================
! Partial Stucture Display related calculations
! ==============================================================================

if GLOB_SCRIPT_TYPE = 2 | GLOB_SCRIPT_TYPE = 3 then
	if GLOB_STRUCTURE_DISPLAY = STRUCTURE_CORE_ONLY & \
	  (GLOB_CONTEXT = 2 | GLOB_CONTEXT = 22 | \		! Floor plan
	   GLOB_CONTEXT = 3 | GLOB_CONTEXT = 23 | \		! 3D
	   GLOB_CONTEXT = 4 | GLOB_CONTEXT = 24 | \		! Section
	   (GLOB_PREVIEW_MODE = 2) | \	! IS but not surface
	   GLOB_CONTEXT = 8 | GLOB_CONTEXT = 28) then	! Layout
		bDrawOutsideTrim	= 0
		bDrawInsideTrim		= 0
		bDrawSill			= 0
		bDrawBoard			= 0
	endif
endif


! =============================================================================
! Wallhole Oversize
! =============================================================================

overSize = 0

overSizeLeft	= 0
overSizeRight	= 0

if not(bDrawSill) & not(bDrawBoard) then
	overSizeLower = 0
else
	if not(bDrawSill) & bDrawBoard then
		overSizeLower = gs_sill_oversize * gs_board_thk
	else
		overSizeLower = 0
	endif
	if bDrawSill & not(bDrawBoard) then
		if iSillType = 1 then
			overSizeLower = gs_sill_oversize * (gs_sill_thk + gs_sill_elev)
		else
			overSizeLower = 0
		endif
	endif
	if bDrawSill & bDrawBoard then
		if iSillType = 1 then
			if gs_sill_oversize then
				overSizeLower = max((gs_sill_thk + gs_sill_elev), gs_board_thk)
			else
				overSizeLower = 0
			endif
		else
			overSizeLower = 0
		endif
	endif
endif
overSizeUpper	= 0


! ==============================================================================
! Hide Casing in Special Cases
! ==============================================================================

call "CasingEnable" parameters A=A,
	iRevealType		= iRevealType,
	iWindowShape	= iWindowShape,
	returned_parameters bInsideCasingNotAvailableByWallType,
						bInsideCasingNotAvailableByReveal,
						bOutsideCasingNotAvailableByReveal,
						dummy,
						dummy

if (bInsideCasingNotAvailableByWallType |\
	bInsideCasingNotAvailableByReveal) then
	gs_trim_inside	= 0
	bDrawInsideTrim	= 0
endif
if bOutsideCasingNotAvailableByReveal then
	gs_trim_outside	 = 0
	bDrawOutsideTrim = 0
endif


! ==============================================================================
! Retroactive Logic
! ==============================================================================

if gs_sill_outside then
	if iSillType = 2 | iSillType = 3 | iSillType = 4 | iSillType = 5 | iSillType = 6 | iSillType = 7 | iSillType = 15 | iSillType = 17 then
		WIDO_THRES_DEPTH = 0
		gs_reveal_bottom = 0
	endif
endif

if gs_board_inside then
	gs_reveal_bottom_angle = 0
endif

bEnablePlasterLeftIn	= 1
bEnablePlasterLeftOut	= 1
bEnablePlasterRightIn	= 1
bEnablePlasterRightOut	= 1
bEnablePlasterTopIn		= 1
bEnablePlasterTopOut	= 1
bEnablePlasterBottomIn	= 1
bEnablePlasterBottomOut	= 1

bOutsideAtRefLine = (wallIsLeft exor (AC_Wall_Direction_Type <> 1 & AC_Wall_Direction_Type <> 3))
if bOutsideAtRefLine then
	if bDrawInsideTrim then
		bEnablePlasterLeftOut	= 0
		bEnablePlasterRightOut	= 0
		bEnablePlasterTopOut	= 0
		bEnablePlasterBottomOut	= 0
	endif
	if bDrawOutsideTrim then
		bEnablePlasterLeftIn	= 0
		bEnablePlasterRightIn	= 0
		bEnablePlasterTopIn		= 0
		bEnablePlasterBottomIn	= 0
	endif
	if bDrawSill then
		bEnablePlasterBottomIn	= 0
	endif
	if bDrawBoard then
		bEnablePlasterBottomOut	= 0
	endif
	if bEnableMasonryArch & bMasonryArch then
		bEnablePlasterTopIn	= 0
	endif
else
	if bDrawOutsideTrim then
		bEnablePlasterLeftOut	= 0
		bEnablePlasterRightOut	= 0
		bEnablePlasterTopOut	= 0
		bEnablePlasterBottomOut	= 0
	endif
	if bDrawInsideTrim then
		bEnablePlasterLeftIn	= 0
		bEnablePlasterRightIn	= 0
		bEnablePlasterTopIn		= 0
		bEnablePlasterBottomIn	= 0
	endif
	if bDrawSill then
		bEnablePlasterBottomOut	= 0
	endif
	if bDrawBoard then
		bEnablePlasterBottomIn	= 0
	endif
	if bEnableMasonryArch & bMasonryArch then
		bEnablePlasterTopOut	= 0
	endif
endif


! =============================================================================
! Plaster Geometry
! =============================================================================

call "PlasterGeometry" parameters gs_macro_version = 12,
	AC_Wall_Direction_Type	= AC_Wall_Direction_Type,
	bEnablePlasterLeftIn	= bEnablePlasterLeftIn,
	bEnablePlasterLeftOut	= bEnablePlasterLeftOut,
	bEnablePlasterRightIn	= bEnablePlasterRightIn,
	bEnablePlasterRightOut	= bEnablePlasterRightOut,
	bEnablePlasterTopIn		= bEnablePlasterTopIn,
	bEnablePlasterTopOut	= bEnablePlasterTopOut,
	bEnablePlasterBottomIn	= bEnablePlasterBottomIn,
	bEnablePlasterBottomOut	= bEnablePlasterBottomOut,
	gs_useWallFinishSkin	= gs_useWallFinishSkin,
	gs_maxPlasterThk		= gs_maxPlasterThk,
	gs_bAutoTurnOut			= gs_bAutoTurnOut,
	gs_bOverOut				= gs_bOverOut,
	gs_bAutoTurnIn			= gs_bAutoTurnIn,
	gs_bOverIn				= gs_bOverIn,
	iClosureType			= iClosureType,
	gs_custom_cc_array_left	= gs_custom_cc_array_left,
	gs_custom_cc_array_right= gs_custom_cc_array_right,
returned_parameters	thkPlasterAtSillLeft,
					thkPlasterAtSillSlLeft,
					thkPlasterAtSillSlLeftBottom,
					thkPlasterAtSillRight,
					thkPlasterAtSillSlRight,
					thkPlasterAtSillSlRightBottom,
					thkPlasterAtSillTop,
					thkPlasterAtSillBottom,
					thkPlasterAtBoardLeft,
					thkPlasterAtBoardSlLeft,
					thkPlasterAtBoardSlLeftBottom,
					thkPlasterAtBoardRight,
					thkPlasterAtBoardSlRight,
					thkPlasterAtBoardSlRightBottom,
					thkPlasterAtBoardTop,
					thkPlasterAtBoardBottom,
					possibleSkinTurnIn,
					possibleSkinTurnOut,
					maxSkinTurnIn,
					maxSkinTurnOut,
					nSkinTurnIn,
					nSkinTurnOut


! =============================================================================
! Wallhole Geometry
! =============================================================================

refWidth	= A
leftWidth	= A / 2 + gs_left_oversize
rightWidth	= A / 2 + gs_right_oversize

openingHeight = B


! ==============================================================================
! Calculations for Curved Walls
! ==============================================================================

if curvedWall then
	WOD = abs(WIDO_ORIG_DIST) - (SYMB_ROTANGLE > EPS) * WIDO_FRAME_THICKNESS

	leftAlfa	= atn(leftWidth  / WOD)
	rightAlfa	= atn(rightWidth / WOD)
endif


! =============================================================================
! Wallhole Reveal Geometry
! =============================================================================

origRevealType					= iRevealType
origRevealLeft					= gs_reveal_left
origRevealRight					= gs_reveal_right
origRevealTop					= gs_reveal_top
origRevealBottom				= gs_reveal_bottom
origRevealLeftAngle				= gs_reveal_left_angle
origRevealRightAngle			= gs_reveal_right_angle
origRevealTopAngle				= gs_reveal_top_angle
origRevealBottomAngle			= gs_reveal_bottom_angle
origRevealSplayedInnerLeft		= gs_reveal_splayed_innerLeft
origRevealSplayedInnerRight		= gs_reveal_splayed_innerRight
origRevealSplayedInnerTop		= gs_reveal_splayed_innerTop
origRevealSplayedInnerBottom	= gs_reveal_splayed_innerBottom
origRevealOuterJambDepth		= gs_reveal_outer_jamb_depth
origRevealDoubleInnerLeft		= gs_reveal_double_innerLeft
origRevealDoubleInnerRight		= gs_reveal_double_innerRight
origRevealDoubleInnerTop		= gs_reveal_double_innerTop
origRevealDoubleInnerBottom		= gs_reveal_double_innerBottom
origRevealOuterLeftAngle		= 0 ! gs_reveal_outerLeft_angle
origRevealOuterRightAngle		= 0 ! gs_reveal_outerRight_angle
origRevealOuterTopAngle			= 0 ! gs_reveal_outerTop_angle
origRevealOuterBottomAngle		= 0 ! gs_reveal_outerBottom_angle
origRevealInnerWidthLeft		= 0 ! gs_reveal_innerLeft_width
origRevealInnerWidthRight		= 0 ! gs_reveal_innerRight_width
origRevealInnerWidthTop			= 0 ! gs_reveal_innerTop_width
origRevealInnerWidthBottom		= 0 ! gs_reveal_innerBottom_width
origRevealOuterWidthLeft		= 0 ! gs_reveal_outerLeft_width
origRevealOuterWidthRight		= 0 ! gs_reveal_outerRight_width
origRevealOuterWidthTop			= 0 ! gs_reveal_outerTop_width
origRevealOuterWidthBottom		= 0 ! gs_reveal_outerBottom_width

dim revealPointsArray[]
dim iRevealPointsIdx[20]

dim bAddPoint[4]

dim  leftWallholeRevealPnts[][2]
dim rightWallholeRevealPnts[][2]

call "RevealGeometry" parameters A	= A,
	AC_WallType						= AC_WallType,
	AC_WindowInWallContour			= AC_WindowInWallContour,
	AC_WallContourPolygon			= AC_WallContourPolygon,
	gs_macro_version				= 12,
	iWindowShape					= iWindowShape,
	refWidth						= refWidth,
	leftWidth						= leftWidth,
	rightWidth						= rightWidth,
	cornerPostX						= 0,
	bWndCurved						= gs_IsCurved,
	bParalellInCurvedWalls			= bParalellInCurvedWalls,
	bShowReveal						= bShowReveal,
	bDWSymbolAtMiddle				= bDWSymbolAtMiddle,
	iClosureType					= iClosureType,
	iRevealType						= origRevealType,
	gs_reveal_type_bottom_int		= gs_reveal_type_bottom_int,
	iSillRevealType					= iSillRevealType,
	iBoardRevealType				= iBoardRevealType,
	gs_wido_sill					= gs_wido_sill,
	gs_reveal_uniform				= gs_reveal_uniform,
	gs_reveal_left					= origRevealLeft,
	gs_reveal_right					= origRevealRight,
	gs_reveal_top					= origRevealTop,
	gs_reveal_bottom				= origRevealBottom,
	gs_reveal_splayed_innerLeft		= origRevealSplayedInnerLeft,
	gs_reveal_splayed_innerRight	= origRevealSplayedInnerRight,
	gs_reveal_splayed_innerTop		= origRevealSplayedInnerTop,
	gs_reveal_splayed_innerBottom	= origRevealSplayedInnerBottom,
	gs_reveal_double_innerLeft		= origRevealDoubleInnerLeft,
	gs_reveal_double_innerRight		= origRevealDoubleInnerRight,
	gs_reveal_double_innerTop		= origRevealDoubleInnerTop,
	gs_reveal_double_innerBottom	= origRevealDoubleInnerBottom,
	gs_reveal_outer_jamb_depth		= origRevealOuterJambDepth,
	gs_reveal_left_angle			= origRevealLeftAngle,
	gs_reveal_right_angle			= origRevealRightAngle,
	gs_reveal_top_angle				= origRevealTopAngle,
	gs_reveal_bottom_angle			= origRevealBottomAngle,
	gs_reveal_outerLeft_angle		= origRevealOuterLeftAngle,
	gs_reveal_outerRight_angle		= origRevealOuterRightAngle,
	gs_reveal_outerTop_angle		= origRevealOuterTopAngle,
	gs_reveal_outerBottom_angle		= origRevealOuterBottomAngle,
	gs_reveal_angle_edit_by_int		= 1,
	gs_reveal_innerLeft_width		= origRevealInnerWidthLeft,
	gs_reveal_innerRight_width		= origRevealInnerWidthRight,
	gs_reveal_innerTop_width		= origRevealInnerWidthTop,
	gs_reveal_innerBottom_width		= origRevealInnerWidthBottom,
	gs_reveal_outerLeft_width		= origRevealOuterWidthLeft,
	gs_reveal_outerRight_width		= origRevealOuterWidthRight,
	gs_reveal_outerTop_width		= origRevealOuterWidthTop,
	gs_reveal_outerBottom_width		= origRevealOuterWidthBottom,
	gs_reveal_left_sL				= 0,
	gs_reveal_right_sR				= 0,
	gs_reveal_bottom_sL				= 0,
	gs_reveal_bottom_sR				= 0,
	gs_reveal_double_innerLeft_sL	= 0,
	gs_reveal_double_innerRight_sR	= 0,
	gs_reveal_double_innerBottom_sL	= 0,
	gs_reveal_double_innerBottom_sR	= 0,
	gs_mountingFrame				= gs_mountingFrame,
	gs_mountingFrame_depth			= gs_mountingFrame_depth,
	thkPlasterAtSillLeft			= thkPlasterAtSillLeft,
	thkPlasterAtSillRight			= thkPlasterAtSillRight,
	thkPlasterAtBoardLeft			= thkPlasterAtBoardLeft,
	thkPlasterAtBoardRight			= thkPlasterAtBoardRight,
	bLeftCornerFunction				= bLeftCornerFunction,
!	leftCornerAngle					= leftCornerAngle,
!	leftConnWallThk					= leftConnWallThk,
	bRightCornerFunction			= bRightCornerFunction,
!	rightCornerAngle				= rightCornerAngle,
!	rightConnWallThk				= rightConnWallThk,
	bCornerWindow					= 0,
	bGangingLeft					= gs_stack_left,
	bGangingRight					= gs_stack_right,
	gs_unit_nominal					= 0,
	gs_reveal_nominal				= 0,
	gs_tolerance_left				= 0,
	gs_tolerance_right				= 0,
	gs_left_oversize				= gs_left_oversize,
	gs_right_oversize				= gs_right_oversize,
returned_parameters	iRevealType,
	iRevealTypeForDisplay,
	iClosureType,
	bShowReveal,
	leftWallholeWidth,
	rightWallholeWidth,
	originalOffsWindowX,
	offsWindowX,
	gs_reveal_left,
	gs_reveal_right,
	revealInnerLeft,
	revealInnerRight,
	gs_reveal_top,
	gs_reveal_bottom,
	gs_reveal_splayed_innerTop,
	gs_reveal_splayed_innerBottom,
	gs_reveal_double_innerLeft,
	gs_reveal_double_innerRight,
	gs_reveal_double_innerTop,
	gs_reveal_double_innerBottom,
	gs_reveal_outer_jamb_depth,
	gs_reveal_left_angle,
	gs_reveal_right_angle,
	gs_reveal_top_angle,
	gs_reveal_bottom_angle,
	gs_reveal_outerLeft_angle,
	gs_reveal_outerRight_angle,
	gs_reveal_outerTop_angle,
	gs_reveal_outerBottom_angle,
	gs_reveal_left_sL,
	gs_reveal_right_sR,
	gs_reveal_bottom_sL,
	gs_reveal_bottom_sR,
	gs_reveal_double_innerLeft_sL,
	gs_reveal_double_innerRight_sR,
	gs_reveal_double_innerBottom_sL,
	gs_reveal_double_innerBottom_sR,
	radSill,
	radBoard,
	WOD,
	iRevealPointsIdx,
	bAddPoint,
	revealPointsArray


for i=1 to iRevealPointsIdx[10]
	idx = 2 * (i - 1) + 1
	leftWallholeRevealPnts[i][1] = revealPointsArray[idx]
	leftWallholeRevealPnts[i][2] = revealPointsArray[idx + 1]
next i

for i=1 to iRevealPointsIdx[20]
	idx = 2 * (iRevealPointsIdx[10] + i - 1) + 1
	rightWallholeRevealPnts[i][1] = revealPointsArray[idx]
	rightWallholeRevealPnts[i][2] = revealPointsArray[idx + 1]
next i


! =============================================================================
! Wall Framing Reveal Geometry
! =============================================================================

dim lineSections[][7]
lineSections[1][1] = 0	! Initialization
lineSections[1][2] = 0
lineSections[1][3] = 0
lineSections[1][4] = 0
lineSections[1][5] = 0
lineSections[1][6] = 0
lineSections[1][7] = 0

if GLOB_SCRIPT_TYPE = 2 then

	bCompositeWall		= (WALL_SKINS_NUMBER > 0)

	bSkinTurnLeft	= 0
	bSkinTurnRight	= 0
	if bCompositeWall then
		if	iClosureType = CLOSURE_TRADITIONAL |\
			iClosureType = CLOSURE_VARIABLE |\
			iClosureType = CLOSURE_DUTCH_VARIABLE |\
			iClosureType = CLOSURE_PREFABRICATED then

			bSkinTurnLeft	= (iCavityClosureSide = CAVITY_SIDE_BOTH | iCavityClosureSide = CAVITY_SIDE_LEFT)
			bSkinTurnRight	= (iCavityClosureSide = CAVITY_SIDE_BOTH | iCavityClosureSide = CAVITY_SIDE_RIGHT)
		endif
		if iClosureType = CLOSURE_CUSTOM then
			bSkinTurnLeft	= 1
			bSkinTurnRight	= 1
		endif

		bPlasterTurnSillLeft	= (thkPlasterAtSillLeft > EPS)
		bPlasterTurnSillRight	= (thkPlasterAtSillRight > EPS)
		bPlasterTurnBoardLeft	= (thkPlasterAtBoardLeft > EPS)
		bPlasterTurnBoardRight	= (thkPlasterAtBoardRight > EPS)
	else
		bPlasterTurnSillLeft	= 0
		bPlasterTurnSillRight	= 0
		bPlasterTurnBoardLeft	= 0
		bPlasterTurnBoardRight	= 0
	endif

	dim tempLineSections[]
	tempLineSections[1] = 0	! Initialization

	call "FramingGeometry" parameters gs_macro_version = 12,
		refWidth				= refWidth,
		leftWidth				= leftWidth  + gs_left_oversize,
		rightWidth				= rightWidth + gs_right_oversize,
		bWndCurved				= gs_IsCurved,
		bParalellInCurvedWalls	= bParalellInCurvedWalls,
		radSill					= radSill,
		radBoard				= radBoard,
		WOD						= WOD,
		isEmptyOpening			= isEmptyOpening,
		bSkinTurnLeft			= bSkinTurnLeft,
		bSkinTurnRight			= bSkinTurnRight,
		bPlasterTurnSillLeft	= bPlasterTurnSillLeft,
		bPlasterTurnSillRight	= bPlasterTurnSillRight,
		bPlasterTurnBoardLeft	= bPlasterTurnBoardLeft,
		bPlasterTurnBoardRight	= bPlasterTurnBoardRight,
		bLeftCornerFunction		= bLeftCornerFunction,
		bRightCornerFunction	= bRightCornerFunction,
		iRevealType				= iRevealTypeForDisplay,
		gs_reveal_left			= gs_reveal_left,
		gs_reveal_right			= gs_reveal_right,
		gs_reveal_left_angle	= gs_reveal_left_angle,
		gs_reveal_right_angle	= gs_reveal_right_angle,
		leftRevealPnts			= leftWallholeRevealPnts,
		rightRevealPnts			= rightWallholeRevealPnts,
		iRevealPointsIdx		= iRevealPointsIdx,
		bGangingLeft			= gs_stack_left,
		bGangingRight			= gs_stack_right,
		gs_left_oversize		= gs_left_oversize,
		gs_right_oversize		= gs_right_oversize,
		gs_sill_outside			= gs_sill_outside,
		iSillType				= iSillType,
		gs_sill_nosing			= gs_sill_nosing,
		gs_sill_ovhg_left		= gs_sill_ovhg_left,
		gs_sill_ovhg_right		= gs_sill_ovhg_right,
		gs_board_inside			= gs_board_inside,
		gs_board_ovhg_left		= gs_board_ovhg_left,
		gs_board_ovhg_right		= gs_board_ovhg_right,
		bDrawOutsideTrim		= bDrawOutsideTrim,
		bDrawInsideTrim			= bDrawInsideTrim,
		gs_trim_outside_uniform	= gs_trim_outside_uniform,
		gs_trim_inside_uniform	= gs_trim_inside_uniform,
		gs_trim_width_in		= gs_trim_width_in,
		gs_trim_width_out		= gs_trim_width_out,
		gs_trim_offs_in			= gs_trim_offs_in,
		gs_trim_offs_out		= gs_trim_offs_out,
		gs_trim_thk_in			= gs_trim_thk_in,
		gs_trim_thk_out			= gs_trim_thk_out,
		gs_tw_left_in			= gs_tw_left_in,
		gs_tw_right_in			= gs_tw_right_in,
		gs_tw_left_out			= gs_tw_left_out,
		gs_tw_right_out			= gs_tw_right_out,
		gs_trim_atframe_out		= gs_trim_atframe_out,
		gs_trim_atframe_in		= gs_trim_atframe_in,
	returned_parameters	tempLineSections

	numLineSections = round_int(vardim1(tempLineSections) / 7)
	idx = 0
	for i=1 to numLineSections
		for j=1 to 7
			lineSections[i][j] = tempLineSections[idx + j]
		next j
		idx = idx + 7
	next i
endif

