

! --------------------------------------------------------------------------------
! Lock flag conditions
! --------------------------------------------------------------------------------
lockPicContour = 0
if bEnableSwingLineTurnOff then
	textFlag2DMVO = 4 - 4*(not(bDisable2DOverride) & bOverride_MVO_2D & or_bOplines2d)
	lockPicContour = 1-1*(bOverride_MVO_2D & or_bOplines2d)
	textFlagOpLinePen = 4 - 4*((not(bDisable2DOverride) & bOverride_MVO_2D & or_bOplines2d) | (not(bDisable3DOverride) & bOverride_MVO_3D & or_bOplines3d))
else
	textFlag2DMVO = 4 - 4*(bOverride_MVO_2D)
	if bSkylight then
		textFlagOpLinePen = 4 - 4*((not(bDisable2DOverride) & bOverride_MVO_2D) | (not(bDisable3DOverride) & bOverride_MVO_3D & or_bOplines3d))
	else
		if iLineShapeCategory = LINE_SHAPE_CATEGORY_SWING then
			textFlagOpLinePen = 4 - 4*((((bOverride_MVO_2D & not(or_iSwingType = LINE_SHAPE_SWING_NONE)) |\
								  (bOverride_MVO_3D & or_bOplines3d)) |\
								  (((bOverride_MVO_2D | bOverride_MVO_3D) & gs_opline_2D) & ((iOpeningParamType = 9) | (iOpeningParamType = 11)))) & \
								  (not(bDisable2DOverride) | not(bDisable3DOverride)))
		else
			textFlagOpLinePen = 4 - 4*((not(bDisable2DOverride) & bOverride_MVO_2D & not(or_iSlidingType = LINE_SHAPE_SLIDING_NONE)) | (not(bDisable3DOverride) & bOverride_MVO_3D & or_bOplines3d))
		endif
	endif
endif

if iOpeningParamType = 12 then
	textFlag2DMVO = 4 - 4 * (not(bDisable2DOverride) & bOverride_MVO_2D & gs_passdoor)
endif

textFlag3DMVO = 4 - 4*(not(bDisable3DOverride) & bOverride_MVO_3D & or_bOplines3D)
textFlagSideLight3D = 4 - 4*((bOverride_MVO_win_2D | (bOverride_MVO_win_3D & or_bWinOplines3d)) & (not(bDisable2DOverride) | not(bDisable3DOverride)))
textFlagWin2D = 4*(not(bOverride_MVO_win_2D) | bDisable2DOverride)		! Sidelights

! --------------------------------------------------------------------------------
! Swing Line Enable
! --------------------------------------------------------------------------------
if iLineShapeCategory = LINE_SHAPE_CATEGORY_SWING then
	numLineShapeType = (bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_NONE)		+\
						bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_CURVED)	+\
						bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_STRAIGHT)	+\
						bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_SIMPLE_LINE))
else
	numLineShapeType = (bittest(iLineShapeSlidingEnable, LINE_SHAPE_SLIDING_NONE)		+\
						bittest(iLineShapeSlidingEnable, LINE_SHAPE_SLIDING_ARROW)		+\
						bittest(iLineShapeSlidingEnable, LINE_SHAPE_SLIDING_SIMPLE_LINE))
endif

bUsePicForSwingLine = 1
if	iOpeningParamType = 6	|\
	iOpeningParamType = 61	|\
	iOpeningParamType = 8	|\
	iOpeningParamType = 11	|\
	iOpeningParamType = 12	|\
	iOpeningParamType = 2	|\
	(iOpeningParamType = 1 & bEnableSecondaryOpLineTypes)	|\
	(numLineShapeType > 3)	then

	bUsePicForSwingLine = 0
endif


! --------------------------------------------------------------------------------
! Page content
! --------------------------------------------------------------------------------

startPosX = 2
y = pageStartY
dy = 23
yMaxRight = y
yMaxLeft = y

bShowSameLineTypeonUI = bShowSameLineTypeForLeaves & bEnableSecondaryOpLineTypes & not(bDisableSwingLineType)

if bShowSameLineTypeonUI then
	ui_style 0, 0
	ui_infield{3}	"gs_bSameLineTypeForLeaves",	startPosX,	y,	442-startPosX, 19,
		7, "", 2, 1, 0, 0, 0, 0,
		"", stOpeningOptionsUI[38], 0,
		"", stOpeningOptionsUI[38], 1
	y = y + dy
	ui_separator 5, y-3, 440, y - 3
	yVertSepStart = y + 3
else
	yVertSepStart = pageStartY
endif


yVertSepEnd = y

if b2ColumnsOnOpLineUI then
	x1 = startPosX
	x3 = 217
	x2 = x3 - 90	! picWidth
	ui_style 0, 1
	ui_outfield stOpeningOptionsUI[1], startPosX, y+4, x3-x1, 15, 4*(bDisable2DOverride & iOpeningParamType = 0)
	y = y + dy
endif

ui_style 0, 0
! --------------------------------------------------------------------------------
! Opening angle parameters
! --------------------------------------------------------------------------------
if b2ColumnsOnOpLineUI then
	if iOpeningParamType = 1 then gosub "openingType1_2D"
	if iOpeningParamType = 2 then gosub "openingType2_2D"
	if iOpeningParamType = 3 then gosub "openingType3_2D"
	if iOpeningParamType = 5 then gosub "openingType5_2D"
	if iOpeningParamType = 19 then gosub "openingType19_2D"
	if iOpeningParamType = 20 then gosub "openingType20_2D"

	if iOpeningParamType = 6 then
		gosub "openingType1_2D"
		gosub "GhostLineParameters"		! ghost line params
	endif
	if iOpeningParamType = 61 then
		gosub "GhostLineParameters"		! ghost line params
	endif
	if iOpeningParamType = 7 then
		gosub "openingType1_2D"
		gosub "DoubleActingLineType"		! Double Acting Line Type
	endif
	if iOpeningParamType = 71 then
		gosub "DoubleActingLineType"		! Double Acting Line Type
	endif
	if iOpeningParamType = 8 then
		gosub "openingType1_2D"
		gosub "DoubleActingLineType"		! Double Acting Line Type
		gosub "GhostLineParameters"		! ghost line params
	endif
	if iOpeningParamType = 9 or iOpeningParamType = 10 or \
		iOpeningParamType = 13 or iOpeningParamType = 14 or \
		iOpeningParamType = 17 then		! D1 Garage 1 INT or USA or CHE
		gosub "GarageOpline2D"		! Garage 2D oplines
	endif
	if iOpeningParamType = 11 or iOpeningParamType = 12 then	! D1 Garage 4 INT or D1 Garage 3 USA with passdoor
		gosub "GarageOpline2D"		! Garage 2D oplines
		gosub "PassDoorAngle2D"		! Pass Door angle in 2D
		lockPicContour = (not(gs_passdoor) & (iOpeningParamType = 12))
	endif
	if iOpeningParamType = 16 or iOpeningParamType = 18 then		!Vertically rolling CHE garage doors
		gosub "closed2DControl"			!Closed in 2D
	endif
endif

! --------------------------------------------------------------------------------
! 2D control parameters
! --------------------------------------------------------------------------------
if b2ColumnsOnOpLineUI and not(bDisable2DOverride) then
	gosub "2DControlsMVO"
endif

yMaxRight = y + 5

! --------------------------------------------------------------------------------
! 3D control parameters
! --------------------------------------------------------------------------------

y = pageStartY + dy*bShowSameLineTypeonUI
if b2ColumnsOnOpLineUI then
	startPosX = 227
	x1 = startPosX
	x3 = 442
	x2 = x3 - 90	! picWidth
else
	x1 = startPosX
	x2 = startPosX + 205
	x3 = startPosX + 325
endif
ui_style 0, 1
ui_outfield stOpeningOptionsUI[2], startPosX, y+4, x3-x1, 15, 4*(bDisable3DOverride & iOpeningParamType = 0)
y = y + dy

ui_style 0, 0

if iOpeningParamType = 1 then gosub "openingType1_3D"
if iOpeningParamType = 2 then gosub "openingType2_3D"
if iOpeningParamType = 3 | iOpeningParamType = 21 then gosub "openingType3_3D"
if iOpeningParamType = 4 then gosub "openingType4_3D"
if iOpeningParamType = 5 then gosub "openingType5_3D"
if iOpeningParamType = 6 then gosub "openingType1_3D"		! Same as Type 1
if iOpeningParamType = 7 then gosub "openingType1_3D"		! Same as Type 1
if iOpeningParamType = 8 then gosub "openingType1_3D"		! Same as Type 1
if iOpeningParamType = 9 then gosub "openingType4_3D"		! same as Type 4
if iOpeningParamType = 10 then gosub "openingType4_3D"		! same as Type 4
if iOpeningParamType = 11 or iOpeningParamType = 12 then	! D1 Garage 3 INT or 4 USA with passdoor
	gosub "openingType11_3D"		! Pass Door angle in 2D
endif
if iOpeningParamType = 13 then gosub "openingType4_3D"
if iOpeningParamType = 14 then gosub "openingType14_3D"
if iOpeningParamType = 16 or iOpeningParamType = 18 then gosub "openingType16_3D"
if iOpeningParamType = 17 then gosub "openingType17_3D"
if iOpeningParamType = 19 then gosub "openingType19_3D"
if iOpeningParamType = 20 then gosub "openingType20_3D"

gosub "3DControlsMVO"

yMaxLeft = y + 5

! --------------------------------------------------------------------------------
! Pen control parameters and Vertical separator
! --------------------------------------------------------------------------------

y = max(yMaxRight, yMaxleft)
! if small space is left underneath, move pen to bottom line

if iOpeningParamType = 8 then
	! pen group is under 3D section
	ui_separator 220, yVertSepStart, 220, 265
	ui_separator 220 + 5, yMaxleft, 444, yMaxleft
	x1 = startPosX
	y = yMaxleft + 5
	gosub "linePenUnder3DColumn"
else
	if 266 - y < 2*dy then
		y = 266 - dy
	endif
	
	if b2ColumnsOnOpLineUI then
		yVertSepEnd = y - 9
		ui_separator 220, yVertSepStart, 220, yVertSepEnd
	endif
	
	startPosX = 3
	if b2ColumnsOnOpLineUI and not(bDisable2DOverride) then
		x1 = startPosX
		x2 = 232
		gosub "linePen2Dand3D"
	else
		x1 = startPosX
		x2 = startPosX + 225
		gosub "linePen3D"
	endif
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ==============================================================================


! ------------------------------------------------------------------------------
"3DControlsMVO":
! ------------------------------------------------------------------------------
	if bDoorWithSidelightSupport then
		ui_infield{3}	"bOverride_MVO_win_3D",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[3], 0,
				"", stOpeningOptionsUI[3], 1
		y = y + dy

		ui_infield{3}	"or_bWinOplines3D",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[4], 0,
				"", stOpeningOptionsUI[4], 1
		y = y + dy

		gosub "lineOrientationSub"

		ui_outfield stOpeningOptionsUI[5], x1, y+4, x2-x1-2, 15, textFlagSideLight3D
		ui_infield{3}	"or_iWinOplineType3DIn",	x2, y, x3-x2, 19,
				8, "", 3, 1, 0, 0, 0, 0,
				"", stOpeningLineType[1], 0,
				"", stOpeningLineType[2], 1,
				"", stOpeningLineType[3], 2
		y = y + dy

		ui_outfield stOpeningOptionsUI[6], x1, y+4, x2-x1-2, 15, textFlagSideLight3D
		ui_infield{3}	"or_iWinOplineType3DOut",	x2, y, x3-x2, 19,
				8, "", 3, 1, 0, 0, 0, 0,
				"", stOpeningLineType[1], 0,
				"", stOpeningLineType[2], 1,
				"", stOpeningLineType[3], 2
		y = y + dy + 14
	else
		ui_infield{3}	"bOverride_MVO_3D",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[3], 0,
				"", stOpeningOptionsUI[3], 1
		y = y + dy
		ui_infield{3}	"or_bOplines3D",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[4], 0,
				"", stOpeningOptionsUI[4], 1
		y = y + dy

		gosub "lineOrientationSub"

		ui_outfield stOpeningOptionsUI[5], x1, y+4, x2-x1-2, 15, textFlag3DMVO
		ui_infield{3}	"or_iOplineType3DIn",	x2, y, x3-x2, 19,
			8, "", 4, 1, 0, 0, 0, 0,
				"", stOpeningLineType[1], 0,
				"", stOpeningLineType[2], 1,
				"", stOpeningLineType[3], 2,
				"", stOpeningLineType[4], 3
		y = y + dy

		ui_outfield stOpeningOptionsUI[6], x1, y+4, x2-x1-2, 15, textFlag3DMVO
		ui_infield{3}	"or_iOplineType3DOut",	x2, y, x3-x2, 19,
			8, "", 4, 1, 0, 0, 0, 0,
				"", stOpeningLineType[1], 0,
				"", stOpeningLineType[2], 1,
				"", stOpeningLineType[3], 2,
				"", stOpeningLineType[4], 3
		y = y + dy

		if bEnableSecondaryOpLineTypes then
			if bWindow then
				ui_outfield stOpeningOptionsUI[7], x1, y+4, x2-x1-2, 15, textFlag3DMVO
			else
				textFlagSecondary3D = textFlag3DMVO
				if gs_bSameLineTypeForLeaves then
					textFlagSecondary3D = 4
				endif
				ui_outfield stOpeningOptionsUI[8], x1, y+4, x2-x1-2, 15, textFlagSecondary3D
			endif
			ui_infield{3}	"or_iOpLineType3dSecondary",	x2, y, x3-x2, 19,
					8, "", 5, 1, 0, 0, 0, 0,
					"", stOpeningLineTypeSec[1], 0,
					"", stOpeningLineTypeSec[2], 1,
					"", stOpeningLineTypeSec[3], 2,
					"", stOpeningLineTypeSec[4], 3,
					"", stOpeningLineTypeSec[5], 4

			y = y + dy
		endif
		y = y + 14
	endif
return


! ------------------------------------------------------------------------------
"2DControlsMVO":
! ------------------------------------------------------------------------------
	xPic = x3 - 90
	if bDoorWithSidelightSupport then
		ui_infield{3}	"bOverride_MVO_win_2D",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[3], 0,
				"", stOpeningOptionsUI[3], 1
	else
		ui_infield{3}	"bOverride_MVO_2D",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[3], 0,
				"", stOpeningOptionsUI[3], 1
	endif

	y = y + dy

	if bEnableSwingLineTurnOff then
		ui_infield{3}	"or_bOplines2d",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[4], 0,
				"", stOpeningOptionsUI[4], 1

		y = y + dy
	endif

	!positioning defaults
	yPicDefault = y
	_numRadButton = 0
	if not(bUseRadioButtons) then
		yPicDefault = y + dy
	endif

	if not(bSkylight) and not(bDisableSwingType) then
		if bUsePicForSwingLine then
			picHeight = 66
			picWidth = 90
			popWidth = picWidth

			if bDoorWithSidelightSupport then
				ui_outfield stOpeningOptionsUI[9], x1, y + 4, xPic-x1-2, 15, textFlagWin2D
			else
				ui_outfield stOpeningOptionsUI[9], x1, y + 4, xPic-x1-2, 15, textFlag2DMVO
			endif

			if bUseRadioButtons then y = y + dy

			if iLineShapeCategory = LINE_SHAPE_CATEGORY_SWING then

				if bUseRadioButtons then
					if bDoorWithSidelightSupport then
						ui_radiobutton "or_iWinSwingType", LINE_SHAPE_SWING_CURVED, stLineShapeSwingType[2], 	x1, y, xPic-x1-2, 19
						y = y + dy
						_numRadButton = _numRadButton + 1

						ui_radiobutton "or_iWinSwingType", LINE_SHAPE_SWING_STRAIGHT, stLineShapeSwingType[3], 	x1, y, xPic-x1-2, 19
						y = y + dy
						_numRadButton = _numRadButton + 1
					else
						if bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_NONE) then
							ui_radiobutton "or_iSwingType", LINE_SHAPE_SWING_NONE, stLineShapeSwingType[1], 	x1, y, xPic-x1-2, 19
							y = y + dy
							_numRadButton = _numRadButton + 1
						endif
	
						if bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_CURVED) then
							ui_radiobutton "or_iSwingType", LINE_SHAPE_SWING_CURVED, stLineShapeSwingType[2], 	x1, y, xPic-x1-2, 19
							y = y + dy
							_numRadButton = _numRadButton + 1
						endif
	
						if bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_STRAIGHT) then
							ui_radiobutton "or_iSwingType", LINE_SHAPE_SWING_STRAIGHT, stLineShapeSwingType[3], 	x1, y, xPic-x1-2, 19
							y = y + dy
							_numRadButton = _numRadButton + 1
						endif
	
						if bittest(iLineShapeSwingEnable, LINE_SHAPE_SWING_SIMPLE_LINE) then
							ui_radiobutton "or_iSwingType", LINE_SHAPE_SWING_SIMPLE_LINE, stLineShapeSwingType[4], 	x1, y, xPic-x1-2, 19
							y = y + dy
							_numRadButton = _numRadButton + 1
						endif
					endif
				else
					if bDoorWithSidelightSupport then
						ui_infield{3}	"or_iWinSwingType",	xPic, y, popWidth, 19,
							8, "", 5, 1, 0, 0, 0, 0,
							"", stLineShapeSwingType[2], LINE_SHAPE_SWING_CURVED,
							"", stLineShapeSwingType[3], LINE_SHAPE_SWING_STRAIGHT
					else
						ui_infield{3}	"or_iSwingType",	xPic, y, popWidth, 19,
							8, "", 5, 1, 0, 0, 0, 0,
							"", stLineShapeSwingType[1], LINE_SHAPE_SWING_NONE,
							"", stLineShapeSwingType[2], LINE_SHAPE_SWING_CURVED,
							"", stLineShapeSwingType[3], LINE_SHAPE_SWING_STRAIGHT,
							"", stLineShapeSwingType[4], LINE_SHAPE_SWING_SIMPLE_LINE,
							"", stLineShapeSwingType[5], LINE_SHAPE_SWING_LEAF_LINE
					endif
				endif

				! positioning swingline picture
				yPic = yPicDefault + (_numRadButton+1)*dy - 2 - picHeight
				yPic = max(yPic, yPicDefault)

				if bDoorWithSidelightSupport then
					if or_iWinSwingType = LINE_SHAPE_SWING_CURVED then
						ui_pict 9, xPic, yPic, picWidth, picHeight, 1				! ui_w_swing_curved.png
					endif
					if or_iWinSwingType = LINE_SHAPE_SWING_STRAIGHT then
						ui_pict 10, xPic, yPic, picWidth, picHeight, 1				! ui_w_swing_straight.png
					endif
				else
					if or_iSwingType = LINE_SHAPE_SWING_NONE then
						if bGarageDoorNoSideHung then
							ui_pict 15, xPic, yPic, picWidth, picHeight, 1			! ui_garage_none.png
						else
							if bDoubleOpeningDoor then
								ui_pict 22, xPic, yPic, picWidth, picHeight, 1		! ui_swing_none2.png
							else
								ui_pict 17, xPic, yPic, picWidth, picHeight, 1		! ui_swing_none.png
							endif
						endif
					endif
					if or_iSwingType = LINE_SHAPE_SWING_CURVED then
						if bWindow then
							ui_pict 9, xPic, yPic, picWidth, picHeight, 1			! ui_w_swing_curved.png
						else
							if bDoubleOpeningDoor then
								ui_pict 21, xPic, yPic, picWidth, picHeight, 1		! ui_swing_curved2.png
							else
								ui_pict 4, xPic, yPic, picWidth, picHeight, 1		! ui_swing_curved.png
							endif
						endif
					endif
					if or_iSwingType = LINE_SHAPE_SWING_STRAIGHT then
						if bWindow then
							ui_pict 10, xPic, yPic, picWidth, picHeight, 1			! ui_w_swing_straight.png
						else
							if bDoubleOpeningDoor then
								ui_pict 25, xPic, yPic, picWidth, picHeight, 1		! ui_swing_straight2.png
							else
								ui_pict 5, xPic, yPic, picWidth, picHeight, 1		! ui_swing_straight.png
							endif
						endif
					endif
					if or_iSwingType = LINE_SHAPE_SWING_SIMPLE_LINE then
						if bGarageDoorNoSideHung then
							ui_pict 16, xPic, yPic, picWidth, picHeight, 1			! ui_garage_simple_line.png
						else
							if bDoubleOpeningDoor then
								ui_pict 23, xPic, yPic, picWidth, picHeight, 1		! ui_swing_simple_line2.png
							else
								ui_pict 11, xPic, yPic, picWidth, picHeight, 1		! ui_swing_simple_line.png
							endif
						endif
					endif
					if or_iSwingType = LINE_SHAPE_SWING_LEAF_LINE then
						ui_pict 24, xPic, yPic, picWidth, picHeight, 1				! ui_swing_simple_line3.png
					endif
				endif
			else	! iLineShapeCategory # LINE_SHAPE_CATEGORY_SWING 
				if bUseRadioButtons then
					if bittest(iLineShapeSlidingEnable, LINE_SHAPE_SLIDING_NONE) then
						ui_radiobutton "or_iSlidingType", LINE_SHAPE_SLIDING_NONE, stLineShapeSlidingType[1], 	x1, y, xPic-x1-2, 19
						y = y + dy
						_numRadButton = _numRadButton + 1
					endif

					if bittest(iLineShapeSlidingEnable, LINE_SHAPE_SLIDING_ARROW) then
						ui_radiobutton "or_iSlidingType", LINE_SHAPE_SLIDING_ARROW, stLineShapeSlidingType[2], 	x1, y, xPic-x1-2, 19
						y = y + dy
						_numRadButton = _numRadButton + 1
					endif

					if bittest(iLineShapeSlidingEnable, LINE_SHAPE_SLIDING_SIMPLE_LINE) then
						ui_radiobutton "or_iSlidingType", LINE_SHAPE_SLIDING_SIMPLE_LINE, stLineShapeSlidingType[3], 	x1, y, xPic-x1-2, 19
						y = y + dy
						_numRadButton = _numRadButton + 1
					endif
				else
					ui_infield{3}	"or_iSlidingType",	xPic, y, popWidth, 19,
						8, "", 5, 1, 0, 0, 0, 0,
						"", stLineShapeSlidingType[1], LINE_SHAPE_SLIDING_NONE,
						"", stLineShapeSlidingType[2], LINE_SHAPE_SLIDING_ARROW,
						"", stLineShapeSlidingType[3], LINE_SHAPE_SLIDING_SIMPLE_LINE
				endif

				! positioning swingline picture
				yPic = yPicDefault + (_numRadButton+1)*dy - 2 - picHeight
				yPic = max(yPic, yPicDefault)

				if or_iSlidingType = LINE_SHAPE_SLIDING_NONE then
					ui_pict 12, xPic, yPic, picWidth, picHeight, 1				! ui_sliding_none.png
				endif
				if or_iSlidingType = LINE_SHAPE_SLIDING_ARROW then
					ui_pict 13, xPic, yPic, picWidth, picHeight, 1				! ui_sliding_arrow.png
				endif
				if or_iSlidingType = LINE_SHAPE_SLIDING_SIMPLE_LINE then
					ui_pict 14, xPic, yPic, picWidth, picHeight, 1				! ui_sliding_simple_line.png
				endif
			endif

			!positioning controls after picture
			y = yPic + picHeight + 2
		else
			xPopup = x3 - 90
			if iOpeningParamType = 12 then
				ui_outfield stOpeningOptionsUI[12], x1, y+4, xPopup-x1-2, 15, textFlag2DMVO
			else
				if bDoorWithSidelightSupport then
					ui_outfield stOpeningOptionsUI[9], x1, y+4, xPopup-x1-2, 15, textFlagWin2D
				else
					ui_outfield stOpeningOptionsUI[9], x1, y+4, xPopup-x1-2, 15, textFlag2DMVO
				endif
			endif
			
			if iLineShapeCategory = LINE_SHAPE_CATEGORY_SWING then
				ui_infield{3}	"or_iSwingType",	xPopup, y, x3-xPopup, 19,
					8, "", 5, 1, 0, 0, 0, 0,
					"", stLineShapeSwingType[1], 0,
					"", stLineShapeSwingType[2], 1,
					"", stLineShapeSwingType[3], 2,
					"", stLineShapeSwingType[4], 3
			else
				ui_infield{3}	"or_iSlidingType",	xPopup, y, x3-xPopup, 19,
					8, "", 5, 1, 0, 0, 0, 0,
					"", stLineShapeSlidingType[1], 0,
					"", stLineShapeSlidingType[2], 1,
					"", stLineShapeSlidingType[3], 2
			endif
			y = y + dy
		endif
	endif
	if not(bDisableSwingLineType) then
		xLineType = x3 - 98
		if bEnableSecondaryOpLineTypes then
			gosub "mainLineTypeControl"
			gosub "secondaryLineTypeControl"
		else
			gosub "mainLongerLineTypeControl"
		endif
	endif
return

! ------------------------------------------------------------------------------
"mainLongerLineTypeControl":
! ------------------------------------------------------------------------------


	if bDoorWithSidelightSupport then
		if bOverride_MVO_win_2D & not(lockPicContour) then
			ui_pict "ui_list_dw_OpLineType_flat(1)", x1, y+6, 32, 15, 1
		else
			ui_pict "ui_list_dw_OpLineType_lock_flat(1)", x1, y+6, 32, 15, 1
		endif
		ui_infield "or_winSwingLineType",	x1+40, y,	x3-x1-40, 27
	else
		if bOverride_MVO_2D & not(lockPicContour) then
			ui_pict "ui_list_dw_OpLineType_flat(1)", x1, y+6, 32, 15, 1
		else
			ui_pict "ui_list_dw_OpLineType_lock_flat(1)", x1, y+6, 32, 15, 1
		endif
		ui_infield "or_swingLineType",		x1+40, y,	x3-x1-40, 27
	endif
	y = y + dy + 13
return


! ------------------------------------------------------------------------------
"mainLineTypeControl":
! ------------------------------------------------------------------------------
	if bEnableSecondaryOpLineTypes then
		if	bWindow | \
			bDoorWithSidelightSupport then
			ui_outfield stOpeningOptionsUI[13], x1, y+4, xLineType-x1-2, 15, 0	! Main Sash
		else
			ui_outfield stOpeningOptionsUI[14], x1, y+4, xLineType-x1-2, 15, 0	! Main Leaf
		endif
	endif
	if bDoorWithSidelightSupport then
		ui_infield "or_winSwingLineType", 	xLineType, y+3,	x3-xLineType, 27
	else
		ui_infield "or_swingLineType", 	xLineType, y+3,	x3-xLineType, 27
	endif
	y = y + dy + 10
return


! ------------------------------------------------------------------------------
"secondaryLineTypeControl":
! ------------------------------------------------------------------------------
	if bEnableSecondaryOpLineTypes then	
		if bWindow then
			ui_outfield stOpeningOptionsUI[15], x1, y+4, xLineType-x1-2, 15, textFlag2DMVO	! Secondary Sash
		else
			textFlagSecondary2D = textFlag2DMVO
			if gs_bSameLineTypeForLeaves then
				textFlagSecondary2D = 4
			endif
			ui_outfield stOpeningOptionsUI[16], x1, y+4, xLineType-x1-2, 15, textFlagSecondary2D		! Secondary Leaf
		endif
		ui_infield "or_swingLineType2", xLineType, y+3,	x3-xLineType, 27
		y = y + dy + 10
	endif
return


! ------------------------------------------------------------------------------
"linePen3D":
! ------------------------------------------------------------------------------
	ui_separator 5, y-6, 440, y-6
	ui_style 0, 1

	picWidth = 32

	if bDoorWithSidelightSupport then
		if textFlagSideLight3D = 0 then
			ui_pict "ui_list_contourPen_flat(1)", x2, y+2, picWidth, 15, 1
		else
			ui_pict "ui_list_contourPen_lock_flat(1)", x2, y+2, picWidth, 15, 1
		endif
		ui_outfield stOpeningOptionsUI[17], x1,							y+4,	x2-x1-1,	15, textFlagSideLight3D
		ui_infield "or_iWinOpLinePen", 		x2 + picWidth + 2,			y, 		30,			19		ui_tooltip stOpeningOptionsUI[17]
		ui_infield "or_winOpLinePen", 		x2 + picWidth + 2 + 30 + 2,	y, 		33,			19		ui_tooltip stOpeningOptionsUI[17]
		y = y + dy
	else
		if textFlagOpLinePen = 0 then
			ui_pict "ui_list_contourPen_flat(1)", x2, y+2, picWidth, 15, 1
		else
			ui_pict "ui_list_contourPen_lock_flat(1)", x2, y+2, picWidth, 15, 1
		endif
		ui_outfield stOpeningOptionsUI[17],	x1,							y+4,	x2-x1-1,	15, textFlagOpLinePen
		ui_infield "or_iOpLinePen", 		x2 + picWidth + 2,			y, 		30,			19		ui_tooltip stOpeningOptionsUI[17]
		ui_infield "or_opLinePen", 			x2 + picWidth + 2 + 30 + 2,	y, 		33,			19		ui_tooltip stOpeningOptionsUI[17]
		y = y + dy
	endif
return


! ------------------------------------------------------------------------------
"linePen2Dand3D":
! ------------------------------------------------------------------------------
	ui_separator 5, y-6, 440, y-6

	ui_style 0, 1
	picWidth = 32

	if bDoorWithSidelightSupport then
		if textFlagSideLight3D = 0 then
			ui_pict "ui_list_contourPen_flat(1)", x2, y+2, picWidth, 15, 1
		else
			ui_pict "ui_list_contourPen_lock_flat(1)", x2, y+2, picWidth, 15, 1
		endif
		ui_outfield stOpeningOptionsUI[20], x1,						y+4,	x2-x1-1,	15, textFlagSideLight3D

		ui_style 0, 0
		ui_infield "or_iWinOpLinePen", 	x2 + picWidth + 2,			y, 		30,			19		ui_tooltip stOpeningOptionsUI[19]
		ui_infield "or_winOpLinePen", 	x2 + picWidth + 2 + 30 + 2,	y, 		33,			19		ui_tooltip stOpeningOptionsUI[19]
	else
		if textFlagOpLinePen = 0 then
			ui_pict "ui_list_contourPen_flat(1)", x2, y+2, picWidth, 15, 1
		else
			ui_pict "ui_list_contourPen_lock_flat(1)", x2, y+2, picWidth, 15, 1
		endif
		ui_outfield stOpeningOptionsUI[20], x1,						y+4,	x2-x1-1,	15, textFlagOpLinePen

		ui_style 0, 0
		ui_infield "or_iOpLinePen",		x2 + picWidth + 2,			y, 		30,			19		ui_tooltip stOpeningOptionsUI[19]
		ui_infield "or_opLinePen",		x2 + picWidth + 2 + 30 + 2,	y, 		33,			19		ui_tooltip stOpeningOptionsUI[19]
	endif
return


! ------------------------------------------------------------------------------
"linePenUnder3DColumn":
! ------------------------------------------------------------------------------
	ui_style 0, 1
	picWidth = 32

	if bDoorWithSidelightSupport then
		if textFlagSideLight3D = 0 then
			ui_pict "ui_list_contourPen_flat(1)", x1, y+dy+2, picWidth, 15, 1
		else
			ui_pict "ui_list_contourPen_lock_flat(1)", x1, y+dy+2, picWidth, 15, 1
		endif
		ui_outfield stOpeningOptionsUI[20], x1,						y+4,	444-x1-1,	15, textFlagSideLight3D
		y = y + dy

		ui_style 0, 0
		ui_infield "or_iWinOpLinePen",	x1 + picWidth + 2,			y, 		30,			19		ui_tooltip stOpeningOptionsUI[19]
		ui_infield "or_winOpLinePen", 	x1 + picWidth + 2 + 30 + 2,	y, 		33,			19		ui_tooltip stOpeningOptionsUI[19]
	else
		if textFlagOpLinePen = 0 then
			ui_pict "ui_list_contourPen_flat(1)", x1, y+dy+2, picWidth, 15, 1
		else
			ui_pict "ui_list_contourPen_lock_flat(1)", x1, y+dy+2, picWidth, 15, 1
		endif
		ui_outfield stOpeningOptionsUI[20], x1,						y+4,	444-x1-1,	15, textFlagOpLinePen
		y = y + dy

		ui_style 0, 0
		ui_infield "or_iOpLinePen",		x1 + picWidth + 2,			y, 		30,			19		ui_tooltip stOpeningOptionsUI[19]
		ui_infield "or_opLinePen",		x1 + picWidth + 2 + 30 + 2,	y, 		33,			19		ui_tooltip stOpeningOptionsUI[19]
	endif
return


! ------------------------------------------------------------------------------
"lineOrientationSub":
! ------------------------------------------------------------------------------
	if not(bHideOplineStyle) then
		if (b2ColumnsOnOpLineUI and bDisable2DOverride) or bDoubleActing then
			gosub "lineOrientationText"
		else
			if iOpeningParamType = 11 or iOpeningParamType = 12 | iOpeningParamType = 20 then
				gosub "lineOrientationRadButton"
			else
				gosub "lineOrientationPic"
			endif
		endif
	endif
return


! ------------------------------------------------------------------------------
"lineOrientationRadButton":
! ------------------------------------------------------------------------------
	textFlagOpLine = textFlag3DMVO
	if bLockOplineStyle then textFlagOpLine = 4
	ui_outfield stOpeningOptionsUI[21], x1, y+4, x2-x1-1, 15, textFlagOpLine

	ui_radiobutton "or_iOplineStyle", 1, stOpeningLineStyle[1], 	x2, y, x3-x2-1, 19
	y = y + dy
	ui_radiobutton "or_iOplineStyle", 2, stOpeningLineStyle[2], 	x2, y, x3-x2-1, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"lineOrientationPic":
! ------------------------------------------------------------------------------
	xPic = x3 - 90
	picWidth	= 90
	picHeight	= 66

	if bDoorWithSidelightSupport then
		if not(or_bWinOplines3D) then
			ui_pict 8, xPic, y, picWidth, picHeight, 1		! ui_w_opline3d_off.png
		else
			if or_iWinOplineStyle = 1 then
				ui_pict 6, xPic, y, picWidth, picHeight, 1		! ui_w_opline3d_handle.png
			else
				ui_pict 7, xPic, y, picWidth, picHeight, 1		! ui_w_opline3d_hinges.png
			endif
		endif
		textFlagOpLine = textFlagSideLight3D
		if bLockOplineStyle then textFlagOpLine = 4
		ui_outfield stOpeningOptionsUI[21], x1, y+4, xPic-x1-2, 15, textFlagOpLine
		y = y + dy
		ui_radiobutton "or_iWinOplineStyle", 1, stOpeningLineStyle[1], 	x1, y, xPic-x1-2, 19
		y = y + dy
		ui_radiobutton "or_iWinOplineStyle", 2, stOpeningLineStyle[2], 	x1, y, xPic-x1-2, 19
		y = y + dy
	else
		if not(or_bOplines3D) then
			if bWindow then
				ui_pict 8, xPic, y, picWidth, picHeight, 1		! ui_w_opline3d_off.png
			else
				if bDoubleOpeningDoor then
					ui_pict 20, xPic, y, picWidth, picHeight, 1		! ui_opline3d_off2.png
				else
					ui_pict 3, xPic, y, picWidth, picHeight, 1		! ui_opline3d_off.png
				endif
			endif
		else
			if or_iOplineStyle = 1 then
				if bWindow then
					ui_pict 6, xPic, y, picWidth, picHeight, 1		! ui_w_opline3d_handle.png
				else
					if bDoubleOpeningDoor then
						ui_pict 18, xPic, y, picWidth, picHeight, 1		! ui_opline3d_handle2.png
					else
						ui_pict 1, xPic, y, picWidth, picHeight, 1		! ui_opline3d_handle.png
					endif
				endif
			else
				if bWindow then
					ui_pict 7, xPic, y, picWidth, picHeight, 1		! ui_w_opline3d_hinges.png
				else
					if bDoubleOpeningDoor then
						ui_pict 19, xPic, y, picWidth, picHeight, 1		! ui_opline3d_hinges2.png
					else
						ui_pict 2, xPic, y, picWidth, picHeight, 1		! ui_opline3d_hinges.png
					endif
				endif
			endif
		endif

		textFlagOpLine = textFlag3DMVO
		if bLockOplineStyle then textFlagOpLine = 4
		ui_outfield stOpeningOptionsUI[21], x1, y+4, xPic-x1-2, 15, textFlagOpLine
		y = y + dy

		ui_radiobutton "or_iOplineStyle", 1, stOpeningLineStyle[1], 	x1, y, xPic-x1-2, 19
		y = y + dy
		ui_radiobutton "or_iOplineStyle", 2, stOpeningLineStyle[2], 	x1, y, xPic-x1-2, 19
		y = y + dy
	endif
return


! ------------------------------------------------------------------------------
"lineOrientationText":
! ------------------------------------------------------------------------------
	textFlagOpLine = textFlag3DMVO
	if bLockOplineStyle then textFlagOpLine = 4

	ui_outfield stOpeningOptionsUI[21], x1, y+4, x2-x1-2, 15, textFlagOpLine
	ui_infield{3}	"or_iOplineStyle",	x2, y, x3-x2, 19,
		8, "", 2, 1, 0, 0, 0, 0,
		"", stOpeningLineStyle[1], 1,
		"", stOpeningLineStyle[2], 2

	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType1_2D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[22], x1, y+4, x2-x1-2, 15
	ui_infield	"gs_open_2d",	x2, y, x3-x2, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType1_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[22], x1, y+4, x2-x1-2, 15
	ui_infield	"gs_open_3d",	x2, y, x3-x2, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType2_2D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[23], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open_2d",	x2+20, y, x3-x2-20, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[24], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open_2d_s",	x2+20, y, x3-x2-20, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType2_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[23], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open_3d",	x2+20, y, x3-x2-20, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[24], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open_3d_s",	x2+20, y, x3-x2-20, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType3_2D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[25], x1, y+4, x2-x1-2, 15
	if NORSlidingType = 1 then
		ui_infield	"gs_open2D_1",	x2, y, x3-x2, 19
	else
		if NORSlidingType = 2 then
			ui_infield	"gs_open2D_2",	x2, y, x3-x2, 19
		else
			ui_infield	"gs_open2D",	x2, y, x3-x2, 19
		endif
	endif
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType3_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[25], x1, y+4, x2-x1-2, 15
	if NORSlidingType = 1 then
		ui_infield	"gs_open3D_1",	x2, y, x3-x2, 19
	else
		if NORSlidingType = 2 then
			ui_infield	"gs_open3D_2",	x2, y, x3-x2, 19
		else
			ui_infield	"gs_open3D",	x2, y, x3-x2, 19
		endif
	endif
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType4_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[26], x1, y+4, x2-x1-2, 15
	ui_infield	"gs_open3D",	x2, y, x3-x2, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType5_2D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[27], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open2D",	x2+20, y, x3-x2-25, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[28], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open2D_s",	x2+20, y, x3-x2-25, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType5_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[27], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open3D",	x2+20, y, x3-x2-20, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[28], x1, y+4, x2-x1-2+20, 15
	ui_infield	"gs_open3D_s",	x2+20, y, x3-x2-20, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType11_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[26], x1, y+4, x2-x1-2, 15
	ui_infield	"gs_open3D",	x2, y, x3-x2, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[29], x1, y+4, x3-x1, 15, 4 - 4 * gs_passdoor
	y = y + dy
	ui_infield	"gs_open_3d",	x2, y, x3-x2, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType14_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[26], x1, y+4, x2-x1-2, 15
	ui_infield	"hotzpar",	x2, y, x3-x2, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType16_3D":
! ------------------------------------------------------------------------------
	if iOpeningParamType = 16 then
		ui_outfield stOpeningOptionsUI[26], x1, y+4, x2-x1-2, 15
	else
		ui_outfield stOpeningOptionsUI[25], x1, y+4, x2-x1-2, 15
	endif
	ui_infield	"op",	x2, y, x3-x2, 19
	y = y + dy
return

! ------------------------------------------------------------------------------
"openingType17_3D":
! ------------------------------------------------------------------------------
	ui_infield{3}	"H",	x1,	y,	x3-x1, 19,
			7, "", 2, 1, 0, 0, 0, 0,
			"", stOpeningOptionsUI[37], 0,		!Closed
			"", stOpeningOptionsUI[37], 1		!Closed
	y = y + dy
return



! ------------------------------------------------------------------------------
"openingType19_2D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[22], x1, y+4, x2-x1-2, 15
	ui_infield	"openingAngle2D_01",	x2, y, x3-x2, 19
	y = y + dy
return



! ------------------------------------------------------------------------------
"openingType19_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[22], x1, y+4, x2-x1-2, 15
	ui_infield	"openingAngle3D_01",	x2, y, x3-x2, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType20_2D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[23], x1, y+4, x2-x1-2+20, 15
	ui_infield	"openingAngle2D_01",	x2+20, y, x3-x2-20, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[24], x1, y+4, x2-x1-2+20, 15
	ui_infield	"openingAngle2D_02",	x2+20, y, x3-x2-20, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"openingType20_3D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[23], x1, y+4, x2-x1-2+20, 15
	ui_infield	"openingAngle3D_01",	x2+20, y, x3-x2-20, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[24], x1, y+4, x2-x1-2+20, 15
	ui_infield	"openingAngle3D_02",	x2+20, y, x3-x2-20, 19
	y = y + dy
return


! ------------------------------------------------------------------------------
"GhostLineParameters":
! ------------------------------------------------------------------------------
	ui_infield{3}	"gs_gang",	x1,	y,	x3-x1, 19,
			7, "", 2, 1, 0, 0, 0, 0,
			"", stOpeningOptionsUI[30], 0,
			"", stOpeningOptionsUI[30], 1

	y = y + dy

	ui_outfield stOpeningOptionsUI[31], x1, y+4, x2-x1-2, 15, 4 - 4 * gs_gang
	ui_infield	"gs_gopen_2d",	x2, y, x3-x2, 19
	y = y + dy

	ui_outfield stOpeningOptionsUI[32], x1, y+4, x1+69, 15, 4 - 4 * gs_gang
	ui_infield "gs_gline", 	x1+70, 	y+3, 	x3-x1-70, 	27
	y = y + dy + 10
return


! ------------------------------------------------------------------------------
"DoubleActingLineType":			!Double acting secondary linetype INT
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[33], x1, y+4, x3-x1, 15
	y = y + dy
	ui_infield "gs_swing_pen_line", 	x1+70, 	y, 	x3-x1-70, 	27
	y = y + dy + 10
return


! ------------------------------------------------------------------------------
"GarageOpline2D":
! ------------------------------------------------------------------------------
	if iOpeningParamType = 13 or iOpeningParamType = 14 or iOpeningParamType = 17 then
		lineFlag = 0
	endif

	if iOpeningParamType <> 13 and iOpeningParamType <> 14 and iOpeningParamType <> 17 then
		ui_infield{3}	"gs_opline_2D",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[34], 0,
				"", stOpeningOptionsUI[34], 1
		y = y + dy
		lineFlag = 4 - 4 * gs_opline_2D
	endif

	if iOpeningParamType = 10 or iOpeningParamType = 12 then
		ui_infield{3}	"gs_onlyPanelContour",	x1,	y,	x3-x1, 19,
				7, "", 2, 1, 0, 0, 0, 0,
				"", stOpeningOptionsUI[35], 0,
				"", stOpeningOptionsUI[35], 1
		y = y + dy
	endif

	ui_outfield stOpeningOptionsUI[32], x1, y+4, x2-x1-42, 15, lineFlag
	ui_infield "gs_opline_type_2D", 	x2-40, 	y+3, 	x3-x2+40, 	27
	y = y + dy + 10
return


! ------------------------------------------------------------------------------
"PassDoorAngle2D":
! ------------------------------------------------------------------------------
	ui_outfield stOpeningOptionsUI[29], x1, y+4, x3-x1, 15, 4 - 4 * gs_passdoor
	y = y + dy
	ui_infield	"gs_open_2d",	x2, y, x3-x2, 19
	y = y + dy
return

! ------------------------------------------------------------------------------
"closed2DControl":
! ------------------------------------------------------------------------------
	ui_infield{3}	"H",	x1,	y,	x3-x1, 19,
			7, "", 2, 1, 0, 0, 0, 0,
			"", stOpeningOptionsUI[36], 0,		!`Closed in 2D`
			"", stOpeningOptionsUI[36], 1		!`Closed in 2D`
	y = y + dy
return



