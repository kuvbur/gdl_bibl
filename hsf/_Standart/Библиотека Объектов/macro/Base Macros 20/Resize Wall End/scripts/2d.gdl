
! --- hotspots at the ends ---
hotspot2 0, 0				! fix hotspot at the wall-end
hotspot2 0, WALL_THICKNESS	! fix hotspot at the wall-end

unID = GS_HotspotUnIDBegin

if r0 < EPS then
	xMoveTo = AC_CutWidth
	gosub "move the coorinate system to X"

	hotspot2 -AC_CutWidth, additionIfMirr, unID, AC_CutWidth, 1+128				! base
	unID = unID + 1
	hotspot2 -AC_CutWidth-0.1, additionIfMirr, unID, AC_CutWidth, 3				! reference
	unID = unID + 1
	hotspot2 0, additionIfMirr, unID, AC_CutWidth, 2						! moving
	unID = unID + 1

	hotspot2 -AC_CutWidth, WALL_THICKNESS+additionNotMirr, unID, AC_CutWidth, 1+128	! base
	unID = unID + 1
	hotspot2 -AC_CutWidth-0.1, WALL_THICKNESS+additionNotMirr, unID, AC_CutWidth, 3	! reference
	unID = unID + 1
	hotspot2 0, WALL_THICKNESS+additionNotMirr, unID, AC_CutWidth, 2			! moving
	unID = unID + 1

	del moveStepNum
else
	revAngleExtra = 0
	if not(onWallBeg) then
		revAngleExtra = 512
	endif

	hotspot2 0, 0, unID, cutWidthAngle, 4+128			! base
	unID = unID + 1
	xMoveTo = -0.1
	gosub "move the coorinate system to X"
	hotspot2 0, 0, unID, cutWidthAngle, 7				! reference
	del moveStepNum
	unID = unID + 1
	xMoveTo = AC_CutWidth
	gosub "move the coorinate system to X"
	hotspot2 0, 0, unID, cutWidthAngle, 5, AC_CutWidth		! moving - display param: AC_CutWidth
	del moveStepNum
	unID = unID + 1
	if onWallBeg then
		hotspot2 0, r0 + WALL_THICKNESS, unID, cutWidthAngle, 6+revAngleExtra	! centerpoint
	else
		hotspot2 0, -r0, unID, cutWidthAngle, 6+revAngleExtra				! centerpoint
	endif
	unID = unID + 1

	hotspot2 0, WALL_THICKNESS, unID, cutWidthAngle, 4+128			! base
	unID = unID + 1
	xMoveTo = -0.1
	gosub "move the coorinate system to X"
	hotspot2 0, WALL_THICKNESS, unID, cutWidthAngle, 7				! reference
	del moveStepNum
	unID = unID + 1
	xMoveTo = AC_CutWidth
	gosub "move the coorinate system to X"
	hotspot2 0, WALL_THICKNESS, unID, cutWidthAngle, 5, AC_CutWidth		! moving - display param: AC_CutWidth
	del moveStepNum
	unID = unID + 1
	if onWallBeg then
		hotspot2 0, r0 + WALL_THICKNESS, unID, cutWidthAngle, 6+revAngleExtra	! centerpoint
	else
		hotspot2 0, -r0, unID, cutWidthAngle, 6+revAngleExtra				! centerpoint
	endif
	unID = unID + 1

endif

end


!**************************** SUBROUTINES ****************************!

! ===== move the coorinate system to a given X coordinate =====
"move the coorinate system to X":

additionIfMirr = 0
additionNotMirr = 0
if r0 > EPS then

	if onWallBeg then
		add2 0, r0+WALL_THICKNESS
		rot2 (xMoveTo / (r0 + WALL_THICKNESS/2)) / PI * 180
		add2 0, -r0-WALL_THICKNESS
	else
		add2 0, -r0
		rot2 -(xMoveTo / (r0 + WALL_THICKNESS/2)) / PI * 180
		add2 0, r0
	endif

	moveStepNum = 3

else

	if SYMB_MIRRORED then
		additionIfMirr = xMoveTo * tan(wallIncl)
	else
		additionNotMirr = xMoveTo * tan(wallIncl)
	endif

	add2 xMoveTo, 0
	moveStepNum = 1

endif

return
