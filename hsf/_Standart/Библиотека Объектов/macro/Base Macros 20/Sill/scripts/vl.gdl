
! Contact person: <NJ>

! ==============================================================================
! This macro draws the Sill
! ------------------------------------------------------------------------------
! Input parameters:
!	A:						Window nominal width (length)
!	B:						Window nominal height (length)
!	gs_IsCurved:			Does the window follow the curvature of the wall? (0 / 1)
!	iWindowShape:			Shape of the window (integer)
!								0 = rectangular (default)
!								1 = arched
!							Note: only the rectangular shape can be a Corner Window
! Corner Function --------------------------------------------------------------
!	bLeftCornerFunction:		Turn macro in Corner Window mode at the left side (0 / 1)
!	leftCornerAngle:		Angle between connected walls in case of Corner Window (angle)
!	leftConnWallThk:		Thickness of the connected wall in case of Corner Window (length)
!	bRightCornerFunction:	Turn macro in Corner Window mode at the right side (0 / 1)
!	rightCornerAngle:		Angle between connected walls in case of Corner Window (angle)
!	rightConnWallThk:		Thickness of the connected wall in case of Corner Window (length)
! Sill -------------------------------------------------------------------------
!	iSillType:				Identifies the type of the Sill. (intger)
!	gs_sill_thk:			Thickness of the Sill. (length)
!	gs_sill_nosing:			Nosing of the Sill. (length)
!	gs_sill_angle:			Sill slope. (angle)
!	gs_sill_ovhg_left:		Sill overhang on the left side. (length)
!	gs_sill_ovhg_right:		Sill overhang on the right side. (length)
!	gs_sill_slip_in:		Slip in of the Sill. Only effective when iSillType = 2 or iSillType = 3 (length)
!	gs_sill_width:			Width of the Brick or Timber Sill.
!	gs_sill_topWidth:		Sill Top Width
!	gs_sill_topHeight:		Sill Top Height
!	gs_sill_curved:			Sill edge is curved or straight. (0 / 1)
!	gs_sill_endthk:			Outer thickness (CHE profile parameters)
!	gs_sill_elev:			Insulation thickness under the sill (CHE profile parameters)
!	gs_sill_dripnos:		Drin hanging height (CHE profile parameters)
!	gs_sill_dripwidth:		Drin width (CHE profile parameters)!
!	gs_sill_edge:			Parallel sill outer edge (only CHE - perpendicular, vertical)
!	gs_sill_element_thk:	Sill element thk (only CHE - solid parallel)
! Ganging ----------------------------------------------------------------------
!	gs_stack_left:			Align to door/window on left side (0 / 1)
!	gs_stack_right:			Align to door/window on right side (0 / 1)
!	gs_stack_bottom:		Align to door/window on bottom side (0 / 1)!
!
! Related Global Variables:
!	GLOB_SCRIPT_TYPE
!	GLOB_CONTEXT
!	WALL_THICKNESS
!	WALL_INCL
!	WIDO_FRAME_THICKNESS
!	WIDO_SILL
!	WIDO_LEFT_JAMB
!	WIDO_RIGHT_JAMB
!	WIDO_ORIG_DIST
!	WIDO_REVEAL_SIDE
!	SYMB_MIRRORED
!	SYMB_ROTANGLE
! ==============================================================================


! ==============================================================================
! Sill and Board
! ==============================================================================

dim SillTypeValueMask[]
dim iSillTypeValueMask[]

i=1
if bittest( gs_SillTypeEnable, 0) then 				! Timber Sill in Value list
	SillTypeValueMask[i]	= stSillTypes[1]
	iSillTypeValueMask[i]	= SILL_TIMBER
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 1) then 				! Brick Sill in Value list
	SillTypeValueMask[i]	= stSillTypes[2]
	iSillTypeValueMask[i]	= SILL_BRICK
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 2) then 				! Stone Sill in Value list
	SillTypeValueMask[i]	= stSillTypes[3]
	iSillTypeValueMask[i]	= SILL_STONE
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 3) then 				! Ceramic Sill in Value list
	SillTypeValueMask[i]	= stSillTypes[4]
	iSillTypeValueMask[i]	= SILL_CERAMIC
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 4) then 				! Eternite Sill in Value list
	SillTypeValueMask[i]	= stSillTypes[5]
	iSillTypeValueMask[i]	= SILL_ETHERNIT
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 5) then 				! Concrete Tub in Value list
	SillTypeValueMask[i]	= stSillTypes[6]
	iSillTypeValueMask[i]	= SILL_CONCRETE_TUB
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 6) then 				! Aluminum in Value list
	SillTypeValueMask[i]	= stSillTypes[7]
	iSillTypeValueMask[i]	= SILL_ALUMINIUM
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 7) then 				! Solid Sill 1.1 / Massiv-Fensterbank 1.1 in Value list
	SillTypeValueMask[i]	= stSillTypes[8]
	iSillTypeValueMask[i]	= SILL_SOLID_11
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 8) then 				! Solid Sill 1.2 / Massiv-Fensterbank 1.2 in Value list
	SillTypeValueMask[i]	= stSillTypes[9]
	iSillTypeValueMask[i]	= SILL_SOLID_12
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 9) then 				! Solid Sill 2.2 / Massiv-Fensterbank 2.2 in Value list
	SillTypeValueMask[i]	= stSillTypes[10]
	iSillTypeValueMask[i]	= SILL_SOLID_22
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 10) then 			! Metal Sheet / Metallfensterbank 1.1 in Value list
	SillTypeValueMask[i]	= stSillTypes[11]
	iSillTypeValueMask[i]	= SILL_METAL_1
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 11) then 			! Solid parallel / massiv parallel in Value list
	SillTypeValueMask[i]	= stSillTypes[12]
	iSillTypeValueMask[i]	= SILL_PARALLEL
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 12) then 			! Metal Sheet 2 in Value list
	SillTypeValueMask[i]	= stSillTypes[13]
	iSillTypeValueMask[i]	= SILL_METAL_2
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 13) then 			! Solid parallel 2 / massiv parallel in Value list
	SillTypeValueMask[i]	= stSillTypes[14]
	iSillTypeValueMask[i]	= SILL_PARALLEL_2
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 14) then 			! simple block in Value list
	SillTypeValueMask[i]	= stSillTypes[15]
	iSillTypeValueMask[i]	= SILL_SIMPLEBLOCK
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 15) then 			! block in Value list
	SillTypeValueMask[i]	= stSillTypes[16]
	iSillTypeValueMask[i]	= SILL_BLOCK
	i = i + 1
endif
if bittest( gs_SillTypeEnable, 16) then 			! dutch in Value list
	SillTypeValueMask[i]	= stSillTypes[17]
	iSillTypeValueMask[i]	= SILL_DUTCH
	i = i + 1
endif

if gs_IsCurved then
	values{2} "iSillType" 	SILL_TIMBER, stSillTypes[1]! Timber Sill
else
	values{2} "iSillType" iSillTypeValueMask, SillTypeValueMask
endif


values{2} "iBrickSilldetlevel" 	BRICKSILL_SIMPLE,	stBsilldetlevel[1],
								BRICKSILL_DETAILED,	stBsilldetlevel[2]

if bDoorWithSidelightSupport then
	if not(gs_sidelight_left & gs_sidelight_parapet_hgt_left > EPS) then
		hideparameter "gs_sill_outside_sl_left"
		lock "gs_sill_outside_sl_left"
		if gs_sidelight_left & gs_sidelight_parapet_hgt_left < EPS then
			gs_sill_outside_sl_left = 0
			parameters gs_sill_outside_sl_left = 0
		endif
	endif
	if not(gs_sidelight_right & gs_sidelight_parapet_hgt_right > EPS) then
		hideparameter "gs_sill_outside_sl_right"
		lock "gs_sill_outside_sl_right"
		if gs_sidelight_right & gs_sidelight_parapet_hgt_right < EPS then
			gs_sill_outside_sl_right = 0
			parameters gs_sill_outside_sl_right = 0
		endif
	endif
endif

hideparameter "gs_sill_topHeight"	! can be used if needed by design

if not(bHasAnySillOn) | gs_stack_bottom then						! Off
	hideparameter	"iSillType",
					"gs_sill_thk",
					"gs_sill_nosing",
					"gs_sill_angle",
					"gs_sill_ovhg_left",
					"gs_sill_ovhg_right",
					"gs_sill_slip_in",
					"gs_sill_width",
					"gs_sill_pen_3D",
					"gs_sill_pen_sectcont",
					"gs_sill_fill",
					"gs_sill_pen_fg",
					"gs_sill_pen_bg",
					"gs_sill_pen_ins_sectcont",
					"gs_sill_fill_ins_fg",
					"gs_sill_pen_ins_fg",
					"gs_sill_pen_ins_bg",
					"gs_sill_mat",
					"iBrickSilldetlevel",
					"gs_brick_sill_br_thk",
					"gs_brick_sill_mort_thk",
					"gs_brick_sill_mort_mat",
					"gs_sill_topWidth"

	lock	"iSillType",
			"gs_sill_thk",
			"gs_sill_nosing",
			"gs_sill_angle",
			"gs_sill_ovhg_left",
			"gs_sill_ovhg_right",
			"gs_sill_slip_in",
			"gs_sill_width",
			"gs_sill_pen_3D",
			"gs_sill_pen_sectcont",
			"gs_sill_fill",
			"gs_sill_pen_fg",
			"gs_sill_pen_bg",
			"gs_sill_pen_ins_sectcont",
			"gs_sill_fill_ins_fg",
			"gs_sill_pen_ins_fg",
			"gs_sill_pen_ins_bg",
			"gs_sill_mat",
			"gs_sill_topWidth",
			"gs_sill_topHeight",
			"iBrickSilldetlevel",
			"gs_brick_sill_br_thk",
			"gs_brick_sill_mort_thk",
			"gs_brick_sill_mort_mat"

	if	bittest( gs_SillTypeEnable, 7) | \			! Solid Sill 1.1 / Massiv-Fensterbank 1.1 in Value list
		bittest( gs_SillTypeEnable, 8) | \			! Solid Sill 1.2 / Massiv-Fensterbank 1.1 in Value list
		bittest( gs_SillTypeEnable, 9) | \			! Solid Sill 2.2 / Massiv-Fensterbank 2.2 in Value list
		bittest( gs_SillTypeEnable, 10) | \			! Metal Sheet / Metallfensterbank 1.1 in Value list
		bittest( gs_SillTypeEnable, 12) | \			! Metal Sheet 2 in Value list
		bittest( gs_SillTypeEnable, 16) then		! dutch in Value list

		hideparameter	"gs_sill_endthk"
		lock	"gs_sill_endthk"
	endif

	if	bittest( gs_SillTypeEnable, 0) then			! Timber Sill in Value list
		hideparameter	"gs_sill_curved"
		lock	"gs_sill_curved"
	endif

	if	bittest( gs_SillTypeEnable, 0) | \			! Timber Sill in Value list
		bittest( gs_SillTypeEnable, 7) | \			! Solid Sill 1.1 / Massiv-Fensterbank 1.1 in Value list
		bittest( gs_SillTypeEnable, 8) | \			! Solid Sill 1.2 / Massiv-Fensterbank 1.2 in Value list
		bittest( gs_SillTypeEnable, 9) then			! Solid Sill 2.2 / Massiv-Fensterbank 2.2 in Value list

		hideparameter	"gs_sill_elev"
		lock	"gs_sill_elev"
	endif

	if	bittest( gs_SillTypeEnable, 9) | \			! Solid Sill 2.2 / Massiv-Fensterbank 2.2 in Value list
		bittest( gs_SillTypeEnable, 10) | \			! Metal Sheet / Metallfensterbank 1.1 in Value list
		bittest( gs_SillTypeEnable, 12) then		! Metal Sheet 2 in Value list

		hideparameter	"gs_sill_dripnos"
		lock	"gs_sill_dripnos"
	endif

	if bittest( gs_SillTypeEnable, 9) then			! Solid Sill 2.2 / Massiv-Fensterbank 2.2 in Value list
		hideparameter	"gs_sill_dripwidth"
		lock	"gs_sill_dripwidth"
	endif

	if bittest( gs_SillTypeEnable, 13) then			! Solid parallel 2 / massiv parallel in Value list
		hideparameter	"gs_sill_ins_below",
						"gs_sill_ins_use_wall_skin"
		lock	"gs_sill_ins_below",
				"gs_sill_ins_use_wall_skin"
	endif

	if	bittest( gs_SillTypeEnable, 11) |\ 			! SILL_PARALLEL
		bittest( gs_SillTypeEnable, 13) then		! SILL_PARALLEL_2

		hideparameter	"gs_sill_edge",
						"gs_sill_element_thk"

		lock	"gs_sill_edge",
				"gs_sill_element_thk"
	endif
else
	if iSillType <> SILL_PARALLEL_2 then
		hideparameter "gs_sill_ins_below", "gs_sill_ins_use_wall_skin"
		lock "gs_sill_ins_below", "gs_sill_ins_use_wall_skin"
	endif

	if bHasAnySillOn then			! Outside, Both Sides
		if iSillType <> SILL_BRICK then
			hideparameter	"iBrickSilldetlevel",
							"gs_brick_sill_br_thk",
							"gs_brick_sill_mort_thk",
							"gs_brick_sill_mort_mat"

			lock	"iBrickSilldetlevel",
					"gs_brick_sill_br_thk",
					"gs_brick_sill_mort_thk",
					"gs_brick_sill_mort_mat"
		endif

		maxSillThk = ac_reveal_height - bEnableCasing * gs_trim_outside * gs_trim_thk_out

! Timber Sill ------------------------------------------------------------------

		if iSillType = SILL_TIMBER then

			if GLOB_MODPAR_NAME = "iSillType" then		! Setup Timber Sill Defaults
				gs_sill_thk		= 0.025
				gs_sill_nosing	= max(0.030, minSillNosing)
				gs_sill_angle	= 3
				gs_sill_holeDepthBehind = 0

				parameters	gs_sill_thk		= gs_sill_thk,
							gs_sill_nosing	= gs_sill_nosing,
							gs_sill_angle	= gs_sill_angle,
							gs_sill_holeDepthBehind = gs_sill_holeDepthBehind
			endif

			hideparameter	"gs_sill_slip_in",
							"gs_board_slip_in"
			lock	"gs_sill_slip_in",
					"gs_board_slip_in"

			values "gs_sill_thk" 	range (0, maxSillThk]
			values "gs_sill_elev"	range [0, maxSillThk - gs_sill_thk]

			if maxSillAngle > EPS then
				if gs_sill_angle > maxSillAngle - EPS then
					gs_sill_angle = maxSillAngle - EPS
					parameters gs_sill_angle = maxSillAngle - EPS
				endif
				values "gs_sill_angle" range [0, maxSillAngle)
			else
				values "gs_sill_angle" range [0,)
			endif

			if curvedWall & abs(radSill) > EPS then
				if wallIsLeft then
					maxSillLength = abs(radSill - abs(WOD))
				else
					alfaR = alfaRight - alfaRightJamb + alfaSillRightOvhg
					pyR = ySign * radSill * cos(alfaR)

					alfaL = -alfaLeft + alfaLeftJamb - alfaSillLeftOvhg
					pyL = ySign * radSill * cos(alfaL)

					maxSillLength = max((abs(WOD) - pyL), (abs(WOD) - pyR))
				endif
			else
				maxSillLength = WIDO_SILL
			endif

			values "gs_sill_width"	range[maxSillLength,]
			if GLOB_MODPAR_NAME = "gs_sill_width" then
				gs_sill_nosing = gs_sill_width - maxSillLength
				parameters gs_sill_nosing = gs_sill_nosing
			else
				gs_sill_width = maxSillLength + gs_sill_nosing
				parameters gs_sill_width = gs_sill_width
			endif
		endif

! Brick Sill -------------------------------------------------------------------

		if iSillType = SILL_BRICK then

			if GLOB_MODPAR_NAME = "iSillType" then		! Setup Brick Sill Defaults
				gs_sill_thk			= 0.120
				gs_sill_nosing		= max(0.020, minSillNosing)
				gs_sill_angle		= 10
				gs_sill_ovhg_left	= 0
				gs_sill_ovhg_right	= 0
				gs_sill_slip_in		= min(0.020, WIDO_FRAME_THICKNESS / 3)
				gs_sill_holeDepthBehind = sin(gs_sill_angle) * gs_sill_thk

				parameters	gs_sill_thk			= gs_sill_thk,
							gs_sill_nosing		= gs_sill_nosing,
							gs_sill_angle		= gs_sill_angle,
							gs_sill_ovhg_left	= gs_sill_ovhg_left,
							gs_sill_ovhg_right	= gs_sill_ovhg_right,
							gs_sill_slip_in		= gs_sill_slip_in,
							gs_sill_holeDepthBehind = gs_sill_holeDepthBehind
			endif

			if not(bSillUSA) | (bSillUSA & isWindow) then values "gs_sill_holeDepthBehind" range [sin(gs_sill_angle) * gs_sill_thk,)

			hideparameter "gs_sill_curved"
			lock "gs_sill_curved"

			if curvedWall then
				if wallIsLeft then
					maxSillLength = abs(radSill - abs(WOD))
				else
					alfaR = alfaRight - alfaRightJamb + alfaSillRightOvhg
					pyR = ySign * radSill * cos(alfaR)

					alfaL = -alfaLeft + alfaLeftJamb - alfaSillLeftOvhg
					pyL = ySign * radSill * cos(alfaL)

					maxSillLength = max((abs(WOD) - pyL), (abs(WOD) - pyR))
				endif
			else
				maxSillLength = WIDO_SILL
			endif

			if GLOB_MODPAR_NAME = "gs_sill_width" then
				gs_sill_nosing = gs_sill_width * cos(gs_sill_angle) - gs_sill_thk * sin(gs_sill_angle) - maxSillLength - gs_sill_slip_in
				parameters gs_sill_nosing = gs_sill_nosing
			else
				gs_sill_width = gs_sill_thk * tan(gs_sill_angle) + (maxSillLength + gs_sill_slip_in + gs_sill_nosing) / cos(gs_sill_angle)
				parameters gs_sill_width = gs_sill_width
			endif

			values "gs_sill_thk"		range (0, maxSillThk]
			if maxSillAngle > EPS then
				if gs_sill_angle > maxSillAngle - EPS then
					gs_sill_angle = maxSillAngle - EPS
					parameters gs_sill_angle = maxSillAngle - EPS
				endif
				values "gs_sill_angle"		range [0, maxSillAngle)
			else
				values "gs_sill_angle"		range [0, )
			endif
			values "gs_sill_slip_in"	range [0, WIDO_FRAME_THICKNESS]
			values "gs_sill_width"		range [gs_sill_thk * tan(gs_sill_angle) + (maxSillLength + gs_sill_slip_in + minSillNosing) / cos(gs_sill_angle), )

			if bEnableBoard then
				values "gs_board_slip_in"	range [0, WIDO_FRAME_THICKNESS]
				if GLOB_MODPAR_NAME = "gs_sill_slip_in" then
					maxBoardSlipIn = WIDO_FRAME_THICKNESS - gs_sill_slip_in
					if gs_board_slip_in > maxBoardSlipIn then parameters gs_board_slip_in = maxBoardSlipIn
				endif
				if GLOB_MODPAR_NAME = "gs_board_slip_in" then
					maxSillSlipIn = WIDO_FRAME_THICKNESS - gs_board_slip_in
					if gs_sill_slip_in > maxSillSlipIn then parameters gs_sill_slip_in = maxSillSlipIn
				endif
			endif

			if iBrickSilldetlevel = BRICKSILL_SIMPLE then
				hideparameter	"gs_brick_sill_br_thk",
								"gs_brick_sill_mort_thk",
								"gs_brick_sill_mort_mat"
				lock			"gs_brick_sill_br_thk",
								"gs_brick_sill_mort_thk",
								"gs_brick_sill_mort_mat"
			endif

			if isWindow & not(gs_brick_sill) & bSillUSA then
				lock 			"gs_sill_mat"
				hideparameter	"gs_sill_mat"
			endif
		endif


! Simple Block Sill -------------------------------------------------------------------

		if iSillType = SILL_SIMPLEBLOCK then

			if GLOB_MODPAR_NAME = "iSillType" then		! Setup Simple Block Sill Defaults
				gs_sill_thk			= 0.1
				gs_sill_nosing		= 0.03
				gs_sill_angle		= 0
				gs_sill_ovhg_left	= 0
				gs_sill_ovhg_right	= 0
				gs_sill_slip_in		= 0.035
				gs_sill_holeDepthBehind = 0

				parameters	gs_sill_thk			= gs_sill_thk,
							gs_sill_nosing		= gs_sill_nosing,
							gs_sill_angle		= gs_sill_angle,
							gs_sill_ovhg_left	= gs_sill_ovhg_left,
							gs_sill_ovhg_right	= gs_sill_ovhg_right,
							gs_sill_slip_in		= gs_sill_slip_in,
							gs_sill_holeDepthBehind = gs_sill_holeDepthBehind
			endif

			values "gs_sill_holeDepthBehind" range [0,)

			hideparameter	"gs_sill_curved",
							"gs_sill_angle",
							"iBrickSilldetlevel",
							"gs_sill_endthk"
			lock	"gs_sill_curved",
					"gs_sill_angle",
					"iBrickSilldetlevel",
					"gs_sill_endthk"

			iBrickSilldetlevel = BRICKSILL_SIMPLE
			parameters iBrickSilldetlevel = iBrickSilldetlevel

			if curvedWall then
				if wallIsLeft then
					maxSillLength = abs(radSill - abs(WOD))
				else
					alfaR = alfaRight - alfaRightJamb + alfaSillRightOvhg
					pyR = ySign * radSill * cos(alfaR)

					alfaL = -alfaLeft + alfaLeftJamb - alfaSillLeftOvhg
					pyL = ySign * radSill * cos(alfaL)

					maxSillLength = max((abs(WOD) - pyL), (abs(WOD) - pyR))
				endif
			else
				maxSillLength = WIDO_SILL
			endif

			if GLOB_MODPAR_NAME = "gs_sill_width" then
				gs_sill_nosing = gs_sill_width * cos(gs_sill_angle) - gs_sill_thk * sin(gs_sill_angle) - maxSillLength - gs_sill_slip_in
				parameters gs_sill_nosing = gs_sill_nosing
			else
				gs_sill_width = gs_sill_thk * tan(gs_sill_angle) + (maxSillLength + gs_sill_slip_in + gs_sill_nosing) / cos(gs_sill_angle)
				parameters gs_sill_width = gs_sill_width
			endif

			values "gs_sill_thk"		range (0, maxSillThk]
			if maxSillAngle > EPS then
				if gs_sill_angle > maxSillAngle - EPS then
					gs_sill_angle = maxSillAngle - EPS
					parameters gs_sill_angle = maxSillAngle - EPS
				endif
				values "gs_sill_angle"		range [0, maxSillAngle)
			else
				values "gs_sill_angle"		range [0, )
			endif
			values "gs_sill_slip_in"	range [0, WIDO_FRAME_THICKNESS]
			values "gs_sill_width"		range [gs_sill_thk * tan(gs_sill_angle) + (maxSillLength + gs_sill_slip_in + minSillNosing) / cos(gs_sill_angle), )

			if bEnableBoard then
				values "gs_board_slip_in"	range [0, WIDO_FRAME_THICKNESS]
				if GLOB_MODPAR_NAME = "gs_sill_slip_in" then
					maxBoardSlipIn = WIDO_FRAME_THICKNESS - gs_sill_slip_in
					if gs_board_slip_in > maxBoardSlipIn then parameters gs_board_slip_in = maxBoardSlipIn
				endif
				if GLOB_MODPAR_NAME = "gs_board_slip_in" then
					maxSillSlipIn = WIDO_FRAME_THICKNESS - gs_board_slip_in
					if gs_sill_slip_in > maxSillSlipIn then parameters gs_sill_slip_in = maxSillSlipIn
				endif
			endif

			hideparameter	"gs_brick_sill_br_thk",
							"gs_brick_sill_mort_thk",
							"gs_brick_sill_mort_mat"
			lock	"gs_brick_sill_br_thk",
					"gs_brick_sill_mort_thk",
					"gs_brick_sill_mort_mat"
		endif


! Stone Sill -------------------------------------------------------------------

		if iSillType = SILL_STONE then

			if GLOB_MODPAR_NAME = "iSillType" then	! Setup Stone Sill Defaults
				minUpperWdt = 0.02

				gs_sill_thk			= 0.100
				gs_sill_nosing		= max(0.045, minSillNosing)
				gs_sill_angle		= 10
				gs_sill_ovhg_left	= 0
				gs_sill_ovhg_right	= 0
				gs_sill_slip_in		= 0.035
				gs_sill_holeDepthBehind = 0
				gs_sill_topWidth    = 0.01
				gs_sill_topHeight	= 0.01

				parameters	gs_sill_thk			= gs_sill_thk,
							gs_sill_nosing		= gs_sill_nosing,
							gs_sill_angle		= gs_sill_angle,
							gs_sill_ovhg_left	= gs_sill_ovhg_left,
							gs_sill_ovhg_right	= gs_sill_ovhg_right,
							gs_sill_slip_in		= gs_sill_slip_in,
							gs_sill_holeDepthBehind = gs_sill_holeDepthBehind,
							gs_sill_topWidth	= gs_sill_topWidth,
							gs_sill_topHeight	= gs_sill_topHeight
			endif

			if not(bSillUSA) | (bSillUSA & isWindow) then values "gs_sill_holeDepthBehind"	range [0,)
			values "gs_sill_topWidth"			range [0,)

			hideparameter "gs_sill_curved"
			lock "gs_sill_curved"

			if curvedWall then
				if wallIsLeft then
					maxSillLength = abs(radSill - abs(WOD))
				else
					alfaR = alfaRight - alfaRightJamb + alfaSillRightOvhg
					pyR = ySign * radSill * cos(alfaR)

					alfaL = -alfaLeft + alfaLeftJamb - alfaSillLeftOvhg
					pyL = ySign * radSill * cos(alfaL)

					maxSillLength = max((abs(WOD) - pyL), (abs(WOD) - pyR))
				endif
			else
				maxSillLength = WIDO_SILL
			endif

			if GLOB_MODPAR_NAME = "gs_sill_width" then
				gs_sill_nosing = gs_sill_width  - gs_sill_slip_in - maxSillLength
				parameters gs_sill_nosing = gs_sill_nosing
			else
				gs_sill_width = gs_sill_slip_in + maxSillLength + gs_sill_nosing
				parameters gs_sill_width = gs_sill_width
			endif

			values "gs_sill_thk"		range (0, maxSillThk]
			if maxSillAngle > EPS then
				if gs_sill_angle > maxSillAngle - EPS then
					gs_sill_angle = maxSillAngle - EPS
					parameters gs_sill_angle = maxSillAngle - EPS
				endif
				values "gs_sill_angle"		range [0, maxSillAngle)
			else
				values "gs_sill_angle"		range [0, )
			endif
			values "gs_sill_slip_in"	range [0, WIDO_FRAME_THICKNESS]
			values "gs_sill_width"		range [gs_sill_slip_in + maxSillLength + minSillNosing, )

			if bEnableBoard then
				values "gs_board_slip_in"	range [0, max(0, WIDO_FRAME_THICKNESS - gs_sill_topWidth - minUpperWdt)]
				if GLOB_MODPAR_NAME = "gs_sill_slip_in" then
					maxBoardSlipIn = WIDO_FRAME_THICKNESS - gs_sill_slip_in
					if gs_board_slip_in > maxBoardSlipIn then parameters gs_board_slip_in = maxBoardSlipIn
				endif
				if GLOB_MODPAR_NAME = "gs_board_slip_in" then
					maxSillSlipIn = WIDO_FRAME_THICKNESS - gs_board_slip_in
					if gs_sill_slip_in > maxSillSlipIn then parameters gs_sill_slip_in = maxSillSlipIn
				endif
			endif
		endif


! NED Sill Profiles ------------------------------------------------------------

		if iSillType = SILL_CERAMIC | iSillType = SILL_ETHERNIT | iSillType = SILL_CONCRETE_TUB | iSillType = SILL_ALUMINIUM then

			if curvedWall then
				if wallIsLeft then
					maxSillLength = abs(radSill - abs(WOD))
				else
					alfaR = alfaRight - alfaRightJamb + alfaSillRightOvhg
					pyR = ySign * radSill * cos(alfaR)

					alfaL = -alfaLeft + alfaLeftJamb - alfaSillLeftOvhg
					pyL = ySign * radSill * cos(alfaL)

					maxSillLength = max((abs(WOD) - pyL), (abs(WOD) - pyR))
				endif
			else
				maxSillLength = WIDO_SILL
			endif

			if GLOB_MODPAR_NAME = "iSillType" then
				if iSillType = SILL_CERAMIC then
					gs_sill_width		= 0.158
					gs_sill_nosing		= 0.03		!!!gs_sill_width - gs_sill_slip_in - maxSillLength
					gs_sill_thk			= 0.073
					gs_sill_endthk		= 0.027
					gs_sill_topHeight	= gs_sill_thk - gs_sill_endthk
					gs_sill_topWidth	= 0.03
					gs_sill_angle		= 8
					gs_sill_slip_in		= 0.035		!!!gs_sill_topHeight * sin(gs_sill_angle)
					gs_sill_holeDepthBehind = 0
				endif
				if iSillType = SILL_ETHERNIT then
					gs_sill_nosing		= 0.03
					gs_sill_thk			= 0.065
					gs_sill_endthk		= 0.03
					gs_sill_topWidth	= 0.02
					gs_sill_topHeight	= 0.01
					gs_sill_angle		= 10
					gs_sill_slip_in		= 0.035
					gs_sill_width		= gs_sill_nosing + WIDO_SILL + gs_sill_slip_in
					gs_sill_holeDepthBehind = 0
				endif
				if iSillType = SILL_CONCRETE_TUB then
					gs_sill_nosing	= 0.03
					gs_sill_thk		= 0.185
					gs_sill_slip_in	= 0.035
					gs_sill_width	= WIDO_SILL + gs_sill_nosing + gs_sill_slip_in
					gs_sill_angle	= 0
					gs_sill_holeDepthBehind = 0
				endif
				if iSillType = SILL_ALUMINIUM then
					gs_sill_width		= max(0.13, WIDO_SILL + 0.015)
					gs_sill_nosing		= gs_sill_width - gs_sill_slip_in - maxSillLength
					gs_sill_thk			= 0.05
					gs_sill_slip_in		= 0
					gs_sill_angle		= 15
					gs_sill_topHeight	= 0.018
					gs_sill_dripnos		= 0.02
					gs_sill_dripwidth 	= min(0.015, gs_sill_width - WIDO_SILL)
					gs_sill_holeDepthBehind = 0
					parameters	gs_sill_dripnos		= gs_sill_dripnos,
								gs_sill_dripwidth	= gs_sill_dripwidth
				endif
				parameters	gs_sill_width	= gs_sill_width,
							gs_sill_nosing	= gs_sill_nosing,
							gs_sill_thk		= gs_sill_thk,
							gs_sill_endthk	= gs_sill_endthk,
							gs_sill_angle	= gs_sill_angle,
							gs_sill_slip_in	= gs_sill_slip_in,
							gs_sill_holeDepthBehind = gs_sill_holeDepthBehind,
							gs_sill_topWidth	= gs_sill_topWidth,
							gs_sill_topHeight	= gs_sill_topHeight
			endif

			values "gs_sill_holeDepthBehind" range [0,)

			if iSillType = SILL_CERAMIC then
				parameters gs_sill_topHeight = gs_sill_thk - gs_sill_endthk
				lock "gs_sill_angle", "gs_sill_topHeight", "gs_sill_topWidth", "gs_sill_slip_wall"
				hideparameter "gs_sill_topWidth", "gs_sill_slip_wall"

				values "gs_sill_width"		range [WIDO_SILL, )
				values "gs_sill_thk"		range [0.03, maxSillThk]
				values "gs_sill_slip_in"	range [0, )
				values "gs_sill_angle"		range [0,90)
			endif

			if iSillType = SILL_ETHERNIT then
				hideparameter	"gs_sill_slip_wall"
				lock			"gs_sill_topHeight", "gs_sill_slip_wall"

				values "gs_sill_width"		range [WIDO_SILL, )
				values "gs_sill_thk"		range [gs_sill_topHeight, maxSillThk]
				values "gs_sill_slip_in"	range [0, )
				values "gs_sill_topWidth"	range [0,)

				if maxSillAngle > EPS then
					if gs_sill_angle > maxSillAngle - EPS then
						gs_sill_angle = maxSillAngle - EPS
						parameters gs_sill_angle = maxSillAngle - EPS
					endif
					values "gs_sill_angle" range [0, maxSillAngle)
				else
					values "gs_sill_angle" range [0,)
				endif
			endif

			if iSillType = SILL_CONCRETE_TUB then
				hideparameter 	"gs_sill_endthk",
								"gs_sill_angle",
								"gs_sill_slip_wall",
								"gs_sill_topWidth"
				lock 			"gs_sill_endthk",
								"gs_sill_angle",
								"gs_sill_slip_wall",
								"gs_sill_topWidth",
								"gs_sill_topHeight"

				values "gs_sill_width"		range [WIDO_SILL, )
				values "gs_sill_thk"		range (0, maxSillThk]
				values "gs_sill_slip_in"	range [0, )
			endif

			if iSillType = SILL_ALUMINIUM then
				hideparameter	"gs_sill_endthk",
								"gs_sill_angle",
								"gs_sill_slip_wall",
								"gs_sill_topWidth"
				lock			"gs_sill_endthk",
								"gs_sill_angle",
								"gs_sill_slip_wall",
								"gs_sill_topWidth"

				! can be used if needed by design:
				hideparameter	"gs_sill_dripnos",
								"gs_sill_dripwidth"
				lock			"gs_sill_dripnos",
								"gs_sill_dripwidth"

				values "gs_sill_width"		range [WIDO_SILL, )
				values "gs_sill_thk"		range [gs_sill_dripnos + gs_sill_topHeight, maxSillThk]
				values "gs_sill_slip_in"	range [0, )
			endif


			hideparameter "gs_sill_curved"
			lock "gs_sill_curved"

			if GLOB_MODPAR_NAME = "gs_sill_width" then
				gs_sill_nosing = gs_sill_width  - gs_sill_slip_in - maxSillLength
				parameters gs_sill_nosing = gs_sill_nosing
			else
				if GLOB_MODPAR_NAME = "gs_sill_angle" then
					if iSillType = SILL_ALUMINIUM then
						parameters gs_sill_thk = gs_sill_width * tan(gs_sill_angle) + gs_sill_topHeight
					endif
				else
					gs_sill_width = gs_sill_slip_in + maxSillLength + gs_sill_nosing
					parameters gs_sill_width = gs_sill_width
					if iSillType = SILL_ALUMINIUM then
						thk = 0.002
						parameters gs_sill_angle = atn((gs_sill_thk - gs_sill_topHeight - gs_sill_dripnos) / (gs_sill_width - thk))
					endif
				endif
			endif

		endif

! CHE Sill Profiles ------------------------------------------------------------

		if	iSillType = SILL_SOLID_11 | \
			iSillType = SILL_SOLID_12 | \
			iSillType = SILL_SOLID_22 | \
			iSillType = SILL_METAL_1  | \
			iSillType = SILL_PARALLEL | \
			iSillType = SILL_METAL_2  | \
			iSillType = SILL_PARALLEL_2 then

			if curvedWall & abs(radSill) > EPS then
				if wallIsLeft then
					maxSillLength = abs(radSill - abs(WOD))
				else
					alfaR = alfaRight - alfaRightJamb + alfaSillRightOvhg
					pyR = ySign * radSill * cos(alfaR)

					alfaL = -alfaLeft + alfaLeftJamb - alfaSillLeftOvhg
					pyL = ySign * radSill * cos(alfaL)

					maxSillLength = max((abs(WOD) - pyL), (abs(WOD) - pyR))
				endif
			else
				maxSillLength = WIDO_SILL
			endif
			maxSillLength = max(0, maxSillLength - (iSillType = SILL_PARALLEL_2) * gs_mountingFrame_depth * gs_mountingFrame)

			if GLOB_MODPAR_NAME = "iSillType" then
				gs_sill_width = WIDO_SILL + 0.02
				if gs_sill_width < 0.15 then gs_sill_width = 0.15
				parameters gs_sill_width = gs_sill_width
				if iSillType = SILL_SOLID_11 | iSillType = SILL_SOLID_12 | iSillType = SILL_SOLID_22 then
					if iSillType = SILL_SOLID_22 then
						gs_sill_thk			= 0.045
						gs_sill_endthk		= 0.025
					else
						gs_sill_thk			= 0.050
						gs_sill_endthk		= 0.030
					endif
					gs_sill_topHeight	= 0.010
					gs_sill_topWidth	= 0.010
					gs_sill_dripnos		= 0.010
					gs_sill_dripwidth	= 0.030
					gs_sill_slip_in		= 0
				endif
				if iSillType = SILL_METAL_1 then
					gs_sill_thk			= 0.050
					gs_sill_endthk		= 0.030
					gs_sill_topHeight	= 0.010
					gs_sill_dripnos		= 0.020
					gs_sill_slip_in		= 0
				endif
				if iSillType = SILL_PARALLEL | iSillType = SILL_PARALLEL_2 then
					gs_sill_thk			= 0.080
					gs_sill_angle		= 3
					gs_sill_width		= 0.200
					gs_sill_element_thk = 0.020
					gs_sill_slip_in		= 0
				endif
				if iSillType = SILL_METAL_2 then
					gs_sill_thk			= 0.080
					gs_sill_endthk		= 0.070
					gs_sill_topHeight	= 0.010
					gs_sill_topWidth	= 0.010
					gs_sill_dripnos		= 0.110
					gs_sill_dripwidth	= 0.030
					gs_sill_slip_in		= 0
				endif
				gs_sill_holeDepthBehind = 0
				parameters	gs_sill_thk			= gs_sill_thk,
							gs_sill_endthk		= gs_sill_endthk,
							gs_sill_topHeight	= gs_sill_topHeight,
							gs_sill_topWidth	= gs_sill_topWidth,
							gs_sill_dripnos		= gs_sill_dripnos,
							gs_sill_dripwidth	= gs_sill_dripwidth,
							gs_sill_slip_in		= gs_sill_slip_in,
							gs_sill_element_thk = gs_sill_element_thk,
							gs_sill_angle		= gs_sill_angle,
							gs_sill_width		= gs_sill_width,
							gs_sill_holeDepthBehind = gs_sill_holeDepthBehind

			endif

			if	iSillType = SILL_SOLID_11 | \
				iSillType = SILL_SOLID_12 | \
				iSillType = SILL_SOLID_22 then
				values "gs_sill_elev"		range[0, maxSillThk - gs_sill_thk]
			else
				values "gs_sill_elev"		range[0,]
			endif

			if iSillType = SILL_SOLID_22 then
				values "gs_sill_width"		range[maxSillLength + gs_sill_dripwidth,]
			else
				values "gs_sill_width"		range[maxSillLength,]
			endif
			values "gs_sill_endthk"		range[0,]
			values "gs_sill_thk"		range[gs_sill_endthk + gs_sill_topHeight, maxSillThk]
			values "gs_sill_dripnos"	range[0,]
			values "gs_sill_dripwidth"	range[0,]

			if iSillType = SILL_PARALLEL | iSillType = SILL_PARALLEL_2 then
				values "gs_sill_edge"			stSillEdge
				values "gs_sill_angle"			range[0,90)
				values "gs_sill_element_thk"	range(0,]

				hideparameter "gs_sill_curved", "gs_sill_slip_in"
				lock "gs_sill_curved", "gs_sill_slip_in"

				if iSillType = SILL_PARALLEL_2 then
					hideparameter "gs_sill_angle"
					lock "gs_sill_angle"

					if gs_mountingFrame | gs_reveal_bottom < EPS then
						hideparameter "gs_sill_ins_below"
						lock "gs_sill_ins_below"
					endif

					bDefaultElem		= (GLOB_INTGUID = "" | GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")
					if not(bDefaultElem) & not(bCompositeWall) then
						lock "gs_sill_ins_use_wall_skin"
						hideparameter	"gs_sill_ins_use_wall_skin"
					endif

					if not(gs_sill_ins_below) then
						hideparameter	"gs_sill_ins_use_wall_skin",
										"gs_sill_pen_ins_sectcont",
										"gs_sill_fill_ins_fg",
										"gs_sill_pen_ins_fg",
										"gs_sill_pen_ins_bg"

						lock 	"gs_sill_ins_use_wall_skin",
								"gs_sill_pen_ins_sectcont",
								"gs_sill_fill_ins_fg",
								"gs_sill_pen_ins_fg",
								"gs_sill_pen_ins_bg"
					else
						if gs_sill_ins_use_wall_skin & bCompositeWall then
							hideparameter	"gs_sill_pen_ins_sectcont",
											"gs_sill_fill_ins_fg",
											"gs_sill_pen_ins_fg",
											"gs_sill_pen_ins_bg"

							lock 	"gs_sill_pen_ins_sectcont",
									"gs_sill_fill_ins_fg",
									"gs_sill_pen_ins_fg",
									"gs_sill_pen_ins_bg"
						endif
					endif
				else
					hideparameter "gs_sill_ins_below", "gs_sill_ins_use_wall_skin"
					lock "gs_sill_ins_below", "gs_sill_ins_use_wall_skin"
				endif
			else
				hideparameter "gs_sill_angle", "gs_sill_curved", "gs_sill_slip_in"
				lock "gs_sill_angle", "gs_sill_curved", "gs_sill_slip_in"
			endif


			if iSillType = SILL_PARALLEL | iSillType = SILL_PARALLEL_2 then
				if iSillType = SILL_PARALLEL then
					sillAngle 	= gs_sill_angle
					sillWido 	= WIDO_SILL
				else
					sillAngle = gs_reveal_outerBottom_angle
					sillWido 	= WIDO_SILL - gs_mountingFrame_depth * gs_mountingFrame
				endif
				if GLOB_MODPAR_NAME = "gs_sill_width" then
					if gs_sill_edge = stSillEdge[1] then		! Perpendicular
						gs_sill_nosing = gs_sill_width * cos (sillAngle) - sillWido
					else			! Vertical
						gs_sill_nosing = (gs_sill_width - gs_sill_element_thk * tan (sillAngle)) * cos (sillAngle) - sillWido
					endif
					parameters gs_sill_nosing = gs_sill_nosing
				else
					if gs_sill_edge = stSillEdge[1] then		! Perpendicular
						gs_sill_width = (sillWido + gs_sill_nosing) / cos (sillAngle)
					else			! Vertical
						gs_sill_width = (sillWido + gs_sill_nosing + gs_sill_element_thk * tan (sillAngle)) / cos (sillAngle)
					endif
					parameters gs_sill_width = gs_sill_width
				endif
			else
				if GLOB_MODPAR_NAME = "gs_sill_width" then
					gs_sill_nosing = gs_sill_width - WIDO_SILL
					parameters gs_sill_nosing = gs_sill_nosing
				else
					gs_sill_width = WIDO_SILL + gs_sill_nosing
					parameters gs_sill_width = gs_sill_width
				endif
			endif
		endif

! Block Sill -------------------------------------------------------------------

		if iSillType = SILL_BLOCK then
			if GLOB_MODPAR_NAME = "iSillType" then
				gs_sill_nosing	= 0
				gs_sill_thk		= 0.012

				parameters gs_sill_nosing = gs_sill_nosing, gs_sill_thk = gs_sill_thk
			endif

			gs_sill_angle		= 0
			gs_sill_width		= WIDO_FRAME_THICKNESS + gs_sill_nosing
			gs_sill_ovhg_left	= 0
			gs_sill_ovhg_right	= 0
			gs_sill_slip_in		= WIDO_FRAME_THICKNESS

			values "gs_sill_thk" range(0, maxSillThk]

			parameters gs_sill_anlge	= gs_sill_angle,
					gs_sill_width		= gs_sill_width,
					gs_sill_ovhg_left	= gs_sill_ovhg_left,
					gs_sill_ovhg_right	= gs_sill_ovhg_right,
					gs_sill_slip_in		= gs_sill_slip_in

			hideparameter	"gs_sill_angle",
							"gs_sill_ovhg_left",
							"gs_sill_ovhg_right",
							"gs_brick_sill_br_thk",
							"gs_brick_sill_mort_thk",
							"gs_brick_sill_mort_mat",
							"gs_sill_endthk"
			lock	"gs_sill_angle",
					"gs_sill_width",
					"gs_sill_slip_in",
					"gs_sill_ovhg_left",
					"gs_sill_ovhg_right",
					"gs_brick_sill_br_thk",
					"gs_brick_sill_mort_thk",
					"gs_brick_sill_mort_mat",
					"gs_sill_endthk"
		endif


! Dutch Profiled Sill ----------------------------------------------------------

		if iSillType = SILL_DUTCH then
			if GLOB_MODPAR_NAME = "iSillType" then
				gs_sill_width	= WIDO_FRAME_THICKNESS
				gs_sill_thk		= 0.1
				gs_sill_endthk	= 0.07
				gs_sill_nosing	= 0
				gs_sill_angle	= 10
				gs_sill_topWidth	= 0.042
				gs_sill_topHeight	= 0.02

				parameters gs_sill_width	= gs_sill_width,
						gs_sill_thk			= gs_sill_thk,
						gs_sill_endthk		= gs_sill_endthk,
						gs_sill_nosing		= gs_sill_nosing,
						gs_sill_angle		= gs_sill_angle,
						gs_sill_topWidth	= gs_sill_topWidth,
						gs_sill_topHeight	= gs_sill_topHeight
			endif

			if GLOB_MODPAR_NAME = "gs_sill_width" then
				gs_sill_nosing = gs_sill_width - WIDO_FRAME_THICKNESS
				parameters gs_sill_nosing = gs_sill_nosing
			else
				gs_sill_width = gs_sill_nosing + WIDO_FRAME_THICKNESS
				parameters gs_sill_width = gs_sill_width
			endif
			values "gs_sill_width"	range [WIDO_FRAME_THICKNESS, )
			values "gs_sill_nosing"	range [0, )

			values "gs_sill_thk" 		range (0, maxSillThk]
			values "gs_sill_endthk"		range [0, gs_sill_thk]
			gs_sill_endthk = min(gs_sill_endthk, gs_sill_thk)
			values "gs_sill_topHeight"	range [0, gs_sill_thk - gs_sill_endthk]
			if abs(gs_sill_width - gs_sill_topWidth) > EPS then
				values "gs_sill_angle"		range [0, atn((gs_sill_thk - gs_sill_topHeight) / (gs_sill_width - gs_sill_topWidth))]
			endif
			values "gs_sill_topWidth"	range (0, gs_sill_width)

			if GLOB_MODPAR_NAME = "gs_sill_angle" then
				gs_sill_endthk = gs_sill_thk - gs_sill_topHeight - (gs_sill_width - gs_sill_topWidth) * tan(gs_sill_angle)
				parameters gs_sill_endthk = gs_sill_endthk
			else
				if abs(gs_sill_width - gs_sill_topWidth) > EPS then
					gs_sill_angle = atn((gs_sill_thk - gs_sill_endthk - gs_sill_topHeight) / (gs_sill_width - gs_sill_topWidth))
					parameters gs_sill_angle = gs_sill_angle
				endif
			endif

			gs_sill_ovhg_left = 0
			gs_sill_ovhg_right = 0
			gs_sill_slip_in	= WIDO_FRAME_THICKNESS

			parameters 	gs_sill_ovhg_left	= gs_sill_ovhg_left,
						gs_sill_ovhg_right	= gs_sill_ovhg_right,
						gs_sill_slip_in		= gs_sill_slip_in

			hideparameter "gs_sill_ovhg_left", "gs_sill_ovhg_right"
			lock "gs_sill_ovhg_left", "gs_sill_ovhg_right", "gs_sill_slip_in"
		endif


! Common Settings for All Sill Types -------------------------------------------

		if not(iSillType = SILL_TIMBER | iSillType = SILL_SOLID_11 | iSillType = SILL_SOLID_12 | iSillType = SILL_SOLID_22) then
			lock "gs_sill_elev"
			hideparameter "gs_sill_elev"
		endif
		if not(iSillType = SILL_ALUMINIUM | iSillType = SILL_SOLID_22 | iSillType = SILL_METAL_1 | iSillType = SILL_METAL_2) then
			lock "gs_sill_dripnos"
			hideparameter "gs_sill_dripnos"
		endif
		if not(iSillType = SILL_ALUMINIUM | iSillType = SILL_SOLID_22) then
			lock "gs_sill_dripwidth"
			hideparameter "gs_sill_dripwidth"
		endif

		values "gs_sill_nosing"		range [minSillNosing,]
		values "gs_sill_ovhg_left"	range [0,]
		values "gs_sill_ovhg_right"	range [0,]
	endif

	if	bittest( gs_SillTypeEnable, 11) | \			! Solid parallel / massiv parallel in Value list
		bittest( gs_SillTypeEnable, 13) then		! Solid parallel 2 / massiv parallel in Value list

		if iSillType <> SILL_PARALLEL & iSillType <> SILL_PARALLEL_2 then
			hideparameter	"gs_sill_edge", "gs_sill_element_thk"
			lock 			"gs_sill_edge", "gs_sill_element_thk"
		endif
	endif

	if gs_stack_left then
		hideparameter "gs_sill_ovhg_left"
		lock "gs_sill_ovhg_left"
	endif
	if gs_stack_right then
		hideparameter "gs_sill_ovhg_right"
		lock "gs_sill_ovhg_right"
	endif
endif

if bSillUSA then						! USA Sill parameter handling
	if isWindow then
		if not( gs_win_type = 83 |\		! WIN_METAL_CURVED
				gs_win_type = 58 |\		! WIN_BOW
				gs_win_type = 59 |\		! WIN_SPLAYBAY
				gs_win_type = 60) then	! WIN_SQUAREBAY
	
			hideparameter "gs_sill_thk"
			lock "gs_sill_thk"
		endif

		if ((gs_wt_m = 4) & not(gs_brick_sill) & gs_fsill) then
			hideparameter	"gs_sill_ovhg_left",
							"gs_sill_ovhg_right"
	
			lock	"gs_sill_ovhg_left",
					"gs_sill_ovhg_right"
		endif
	endif
endif

if not(iSillType = SILL_BRICK |iSillType = SILL_STONE | iSillType = SILL_CERAMIC | \
		iSillType = SILL_ETHERNIT | iSillType = SILL_CONCRETE_TUB | iSillType = SILL_ALUMINIUM | iSillType = SILL_SIMPLEBLOCK) then
	hideparameter "gs_sill_holeDepthBehind"
	lock "gs_sill_holeDepthBehind"
endif

if not(iSillType = SILL_STONE | iSillType = SILL_CERAMIC | iSillType = SILL_ETHERNIT | \
		iSillType = SILL_CONCRETE_TUB | iSillType = SILL_ALUMINIUM | iSillType = SILL_DUTCH) then
	hideparameter	"gs_sill_topWidth"
	lock			"gs_sill_topWidth", "gs_sill_topHeight"
endif

if	bittest( gs_SillTypeEnable, 3) |\		! SILL_CERAMIC
	bittest( gs_SillTypeEnable, 4) |\		! SILL_ETHERNIT
	bittest( gs_SillTypeEnable, 5) |\		! SILL_CONCRETE_TUB
	bittest( gs_SillTypeEnable, 6) then		! SILL_ALUMINIUM

	if not(iSillType = SILL_CERAMIC | iSillType = SILL_ETHERNIT | iSillType = SILL_CONCRETE_TUB | \
			iSillType = SILL_ALUMINIUM) then
		hideparameter	"gs_sill_slip_wall"
		lock			"gs_sill_slip_wall"
	endif
endif
