
bSlidingDoor = (iDoorType = 6 | iDoorType = 7 | iDoorType = 8 | iDoorType = 9)

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

! call all available page fix IDs 

call "ui_tabIDlistHierarchy_m" parameters bFixtureFittingTabIDsOnly = 1,
				returned_parameters iUiID_Root,
									iUiID_FixtureFitting,	iParID_FixtureFitting,
									iUiID_MountingFrame_DACH, iParID_MountingFrame_DACH,
									iUiID_CustomCorner,		iParID_CustomCorner,
									iUiID_Threshold,		iParID_Threshold,
									iUiID_ThresholdSill,	iParID_ThresholdSill,
									iUiID_Sill,				iParID_Sill,
									iUiID_Sill_USA,			iParID_Sill_USA,
									iUiID_Board,			iParID_Board,
									iUiID_CasingOut,		iParID_CasingOut,
									iUiID_CasingIn,			iParID_CasingIn,
									iUiID_Casing_USA,		iParID_Casing_USA,
									iUiID_Shutter,			iParID_Shutter,
							        iUiID_BayBowSillBoard,	iParID_BayBowSillBoard,
									iUiID_FixFittingModAttr, iParID_FixFittingModAttr


! =============================================================================
! Display actual tabpages
! =============================================================================
bCasingOutPage	= 0
bCasingInPage	= 0
bCasingPageUSA	= 0
bSillPage		= 0
bSillPageUSA	= 0
bThresholdSillPage = 0

for iTabs = 1 to vardim1(uiUsedPageIDs)
	if bEnableCasing then
		if uiUsedPageIDs[iTabs][1]= iUiID_CasingOut then
			ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
			if gs_ui_current_page = iUiID_CasingOut then
				bCasingOutPage = 1
				gosub "pageCallCasing"
			endif
		endif
		if uiUsedPageIDs[iTabs][1]= iUiID_CasingIn then
			ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
			if gs_ui_current_page = iUiID_CasingIn then
				bCasingInPage = 1
				gosub "pageCallCasing"
			endif
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_Casing_USA then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Casing_USA then
			bCasingPageUSA = 1
			gosub "pageCallCasingUSA"
		endif
	endif
	if bEnableBoard then
		if uiUsedPageIDs[iTabs][1]= iUiID_Board then
			ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
			if gs_ui_current_page = iUiID_Board then
				gosub "pageCallBoard"
			endif
		endif
	endif
	if bEnableSill then
		if uiUsedPageIDs[iTabs][1]= iUiID_Sill then
			ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
			if gs_ui_current_page = iUiID_Sill then
				bSillPage = 1
				gosub "pageCallSill"
			endif
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_Sill_USA then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Sill_USA then
			bSillPageUSA = 1
			gosub "pageCallSill"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_ThresholdSill then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_ThresholdSill then
			bThresholdSillPage = 1
			gosub "pageThreshold"
		endif
	endif
	if bEnableThreshold then
		if uiUsedPageIDs[iTabs][1]= iUiID_Threshold then
			ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
			if gs_ui_current_page = iUiID_Threshold then
				bThresholdPage = 1
				gosub "pageThreshold"
			endif
		endif
	endif
	if bEnableShutter then
		if uiUsedPageIDs[iTabs][1]= iUiID_Shutter then
			ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
			if gs_ui_current_page = iUiID_Shutter then
				gosub "pageShutter"
			endif
		endif
	endif
	if bEnableCustomCornerLeft | bEnableCustomCornerRight then
		if uiUsedPageIDs[iTabs][1]= iUiID_CustomCorner then
			ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
			if gs_ui_current_page = iUiID_CustomCorner then
				gosub "pageCustomCorner"
			endif
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_FixFittingModAttr then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_FixFittingModAttr then
			gosub "pageFixFittingModAttributes"
		endif
	endif
next iTabs

! =============================================================================
end
! =============================================================================


! --------------------------------------------------------------------------------
! Casing Settings/Window
! --------------------------------------------------------------------------------
"pageCallCasing":
	call "Casing" parameters all	bCasingOutPage					= bCasingOutPage,
									bCasingInPage					= bCasingInPage,
									bEnableSwitchableBottomCasing	= bEnableSwitchableBottomCasing,
									isEmptyOpening					= isEmptyOpening,
									Suppress_isWindow				= Suppress_isWindow,
									iDoorType						= iDoorType,
									iRevealType						= iRevealType,
									stCasingUI						= stCasingUI,
									stCasingUITooltip				= stCasingUITooltip,
									stSubTabpageToolTip				= stSubTabpageToolTip,
									stNORCasingTypes				= stNORCasingTypes,
									stCasingType					= stCasingType,
									stCasingHeadType				= stCasingHeadType,
									stArchitraveCapitalType			= stArchitraveCapitalType
return


! --------------------------------------------------------------------------------
! Casing Settings USA Window/Door
! --------------------------------------------------------------------------------
"pageCallCasingUSA":
	if isWindow then
		call "Casing" parameters all bCasingPageUSA 	= bCasingPageUSA,
									iRevealType			= iRevealType,
									stCasingUI			= stCasingUI,
									stSubTabpageToolTip	= stSubTabpageToolTip
	else
		call "Casing" parameters all bCasingPageUSA			= bCasingPageUSA,
									iDoorType				= iDoorType,
									bshowCasbSill			= bshowCasbSill,
									stCasingUI				= stCasingUI,
									stSubTabpageToolTip		= stSubTabpageToolTip,
									bTelescopic				= bTelescopic,
									gs_frame_style_m		= iFrameStyle
	endif
return



! --------------------------------------------------------------------------------
! Sill Settings - Window & CHE Door
! --------------------------------------------------------------------------------
"pageCallSill":
	call "Sill" parameters all bSillPage = bSillPage,
			bSillPageUSA			= bSillPageUSA,
			iWindowShape			= iWindowShape,
			bOpeningDirection		= bOpeningDirection,
			bDoorWithSidelightSupport	= bDoorWithSidelightSupport,
			stBsilldetlevel			= stBsilldetlevel,
			stSillUI				= stSillUI,
			stSillTypes				= stSillTypes,
			stSubTabpageToolTip		= stSubTabpageToolTip,
			iThresholdType			= iThresholdType,
			iSillType				= iSillType,
			iPanelType				= iPanelType,
			bDrawOutsideTrim		= bDrawOutsideTrim,
			bDrawSill				= bDrawSill,
			bShowSillUICHE			= bShowSillUICHE
return



! --------------------------------------------------------------------------------
! Board Settings - Window
! --------------------------------------------------------------------------------

"pageCallBoard":
	call "Board" parameters all iWindowShape = iWindowShape,
			bOpeningDirection			= bOpeningDirection,
			bDoorWithSidelightSupport	= bDoorWithSidelightSupport,
			iPanelType					= iPanelType,
			stBoardUI					= stBoardUI,
			stSubTabpageToolTip			= stSubTabpageToolTip
return



! --------------------------------------------------------------------------------
! Threshold and Sill - Door
! --------------------------------------------------------------------------------
"pageThreshold":

	pageStartY = 5

	dy = 22

	if bThresholdNotAvailableByOpTpye then
		ui_pict 138, 7, 8 + pageStartY, 12, 12, 1	! ui_warningsign.png
		ui_style 0,0

		ui_outfield stThresholdUI[21], 28, 8 + pageStartY, 410, 17
		end
	endif

	if bThresholdSillPage then
		x1 = 0
		x2 = 219
	else
		x1 = 5
		x2 = 295
	endif
	x3 = 230
	x4 = 440
	y = pageStartY

	ui_style 0,1
	ui_infield{3} "bThreshold", x1, y+5, x2-x1-45, 18,
			7, "",
			2,
			1, 20, 20, 20, 20,
			0, stThresholdUI[1], 0,
			1, stThresholdUI[1], 1

	ui_style 0,0
	ui_infield{3} "iThresholdType", x2-40, y, 40, 26,
		2, 89,	! ui_threshold_type.png
		17, 1,
		30, 20, 30, 20,
		2,	stThresholdTypes[2],	TRESHOLD_NORMAL,		! Normal Threshold
		3,	stThresholdTypes[3],	TRESHOLD_EXTENDED,		! Extended Threshold
		3,	stThresholdTypes[18],	TRESHOLD_EXTENDED_SWE,	! Extended to Opening Side
		4,	stThresholdTypes[4],	TRESHOLD_BOTHEXTENDED,	! Extended Both Sides
		5,	stThresholdTypes[5],	TRESHOLD_BENEATHPANEL,	! Beneath Panel
		6,	stThresholdTypes[6],	TRESHOLD_PROFILED,		! Profiled
		7,	stThresholdTypes[7],	TRESHOLD_FLAT,			! Flat Terskel
		8,	stThresholdTypes[8],	TRESHOLD_ANSLAG,		! Anslagterskel
		9,	stThresholdTypes[9],	TRESHOLD_GUMMI,			! Gummiterskel m slepelist
		10,	stThresholdTypes[10],	TRESHOLD_HEVE,			! Heveterskel
		11,	stThresholdTypes[11],	TRESHOLD_HC,			! H-C Terskel
		12,	stThresholdTypes[12],	TRESHOLD_BRANN,			! Brannterskel
		13,	stThresholdTypes[13],	TRESHOLD_BADEROMS,		! Baderomsterskel
		14,	stThresholdTypes[14],	TRESHOLD_AKKUSTIKK,		! Akkustikkterskel
		15,	stThresholdTypes[15],	TRESHOLD_SIMPLE,		! Simple Threshold
		16,	stThresholdTypes[16],	TRESHOLD_SLOPINGEXT,	! Sloping Extended
		17,	stThresholdTypes[17],	TRESHOLD_METAL,			! Metal
		1,	stThresholdTypes[19],	TRESHOLD_NOTAVAILABLE	! Not Available

	if bThresholdSillPage then
		ui_style 0,1
		ui_infield{3} "gs_sill_outside", x3, y+5, x4-x3-45, 18,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stSillUI[1], 0,
				1, stSillUI[1], 1

		ui_style 0,0
		ui_infield{3} "iSillType", x4-40, y, 40, 26,
			2, 118,	! ui_sill_type_door.png
			5, 1,
			30, 20, 30, 20,
			1,	stSillTypes[1], SILL_TIMBER,	! Off, Timber Sill
			2,	stSillTypes[2], SILL_BRICK,		! Brick Sill
			3,	stSillTypes[3], SILL_STONE,		! Stone Sill
			4,	stSillTypes[16], SILL_BLOCK,	! Block Sill
			5,	stSillTypes[17], SILL_DUTCH		! Dutch Profiled Sill
	endif

	bFlag1 = 1 * not(bThreshold & (iThresholdType = TRESHOLD_EXTENDED | iThresholdType = TRESHOLD_BOTHEXTENDED))
	iFlag1 = 4 * bFlag1
	bFlag2 = (not(gs_sill_outside) | iSillType = SILL_TIMBER | iSillType = SILL_BLOCK)
	iFlag2 = 4 * bFlag2

	y = y + dy + 10

	if not(gs_threshold_nor) then
		ui_outfield stThresholdUI[2],	x1, 	y, 	x2-x1-50-1, 	15, iFlag1		! Side Overhang Opening Side
		ui_infield "gs_treshold_ovhg_2",	x2-50, 	y, 	50, 	18

		if bThresholdSillPage then
			if iSillType = SILL_DUTCH then
				ui_outfield stSillUI[35],	x3, y+3, x4-x3-90-1, 15, iFlag2	! Upper Thickness
				ui_infield "gs_sill_topWidth",	x4-90, y, 90, 18
			else
				ui_outfield stSillUI[3],	x3, y+3, x4-x3-90-1, 15, iFlag2	! Overhang Left
				ui_infield "gs_sill_ovhg_left",	x4-90, y, 90, 18
			endif
		endif
	else
		if bThresholdSillPage then
			picWidth	= 60
			pic1Height1	= 18
			picHeight2	= 52

			picPosXLft	= 300
			picPosXRgt	= picPosXLft + picWidth + 10
			picPosY1	= 126
			picPosY2	= picPosY1 + pic1Height1

			if not(gs_sill_outside) then
				ui_pict 145, picPosXLft, picPosY2, picWidth, picHeight2, 1			! uiSillPlanLeftovhgDoorOutNoboard.png
				ui_pict 148, picPosXRgt, picPosY2, picWidth, picHeight2, 1			! uiSillPlanRightovhgDoorOutNoboard.png
			else
				ui_infield "gs_sill_ovhg_left",		picPosXLft-10,	picPosY1-21, picWidth+10, 19
				ui_infield "gs_sill_ovhg_Right",	picPosXRgt,		picPosY1-21, picWidth+10, 19

				if iSillType = SILL_BRICK & iBrickSilldetlevel = BRICKSILL_DETAILED then
					ui_pict 140, picPosXLft, picPosY1, picWidth, pic1Height1, 1		! ui_sill_leftovhg_2_br.png
					ui_pict 142, picPosXRgt, picPosY1, picWidth, pic1Height1, 1		! ui_sill_rightovhg_2_br.png
					ui_pict 144, picPosXLft, picPosY2, picWidth, picHeight2, 1		! uiSillPlanLeftovhgDoorOutBr.png
					ui_pict 147, picPosXRgt, picPosY2, picWidth, picHeight2, 1		! uiSillPlanRightovhgDoorOutBr.png
				else
					ui_pict 139, picPosXLft, picPosY1, picWidth, pic1Height1, 1		! ui_sill_leftovhg_2.png
					ui_pict 141, picPosXRgt, picPosY1, picWidth, pic1Height1, 1		! ui_sill_rightovhg_2.png
					ui_pict 143, picPosXLft, picPosY2, picWidth, picHeight2, 1		! uiSillPlanLeftovhgDoorOut.png
					ui_pict 146, picPosXRgt, picPosY2, picWidth, picHeight2, 1		! uiSillPlanRightovhgDoorOut.png
				endif
			endif
		endif
	endif

	y = y + dy

	bFlag1 = not(bThreshold & (iThresholdType = TRESHOLD_BOTHEXTENDED | iThresholdType = TRESHOLD_EXTENDED_SWE))
	iFlag1 = 4 * bFlag1

	if not(gs_threshold_nor) then
		ui_outfield stThresholdUI[3],	x1, y, x2-x1-50-1, 15, iFlag1		! Side Overhang Opposite Side
		ui_infield "gs_treshold_ovhg",	x2-50, y, 50, 20

		if bThresholdSillPage then
			if iSillType = SILL_DUTCH then
				ui_outfield stSillUI[36],	x3, y+3, x4-x3-90-1, 15	! Upper Width
				ui_infield "gs_sill_topHeight",	x4-90, y, 90, 18
			else
				ui_outfield stSillUI[4], x3, y+3, x4-x3-90-1, 15, iFlag2	! Overhang Right
				ui_infield "gs_sill_ovhg_right", x4-90, y, 90, 18
			endif
		endif
	endif

	y = y + dy
	if bEnableThreshold & bThreshold & iThresholdType = TRESHOLD_BOTHEXTENDED then
		if not(bThresholdSillPage) | iSillType = SILL_TIMBER then	! Off
		else
			ui_outfield stThresholdUI[5],	x1, y, x2-x1-50-1, 15, iFlag1			! Threshold Nosing Outside
			ui_infield "gs_treshold_nosing",  x2-50,	y, 50, 18
		endif
	endif

	bFlag3 = (not(gs_sill_outside) | iSillType = SILL_TIMBER | iSillType = SILL_BLOCK | iSillType = SILL_DUTCH)
	iFlag3 = 4 * bFlag3

	if bThresholdSillPage then
		y = y - (gs_threshold_nor) * 2 * dy

		ui_outfield stSillUI[5],	x3, y+3, x4-x3-90-1, 15, iFlag3	! Slip in Depth
		ui_infield "gs_sill_slip_in",	x4-90, y, 90, 20
	endif
	y = y + dy

	if bThresholdSillPage then
		if iSillType = SILL_BRICK then
			ui_outfield stSillUI[14],	x3, y+3, x2-100, 15, 4 * (not(gs_sill_outside) | iSillType <> SILL_BRICK)
			ui_infield{3} "iBrickSilldetlevel",  	x4-90,	y, 90, 19,
					8, "",
					2,
					1, 20, 20, 20, 20,
					1, stBsilldetlevel[1],	BRICKSILL_SIMPLE,
					2, stBsilldetlevel[2],	BRICKSILL_DETAILED

		endif
		y = y + dy
	endif

	ui_separator 0, y, 444, y
	if bThresholdSillPage then ui_separator 222, pageStartY, 222, y-2

	! --- Threshold section picture ----------------------------------------------------------------

	x = 100 - 10 - (gs_threshold_nor) * 80
	y = 35 + pageStartY - 30 * (gs_threshold_nor)

	thresThkPosX		= x + 205
	thresThkPosY		= y + 137
	thresThkInFldWdt	= 90 - 20 * (gs_threshold_nor)

	if not(bEnableThreshold) | not(bThreshold) | (bThreshold & (iThresholdType = TRESHOLD_HEVE | iThresholdType = TRESHOLD_HC)) then
		ui_pict 103, x+25+79,y+90, 82,61, 1		! ui_thres_sect_1.png
		ui_pict 104, x+25+72,y+90, 7,61, 1		! ui_thres_sect_1_1.png
	endif

	if bEnableThreshold & bThreshold then
		if iThresholdType = TRESHOLD_NORMAL then
			ui_pict 105, x+25+79,y+90, 82,61, 1		! ui_thres_sect_2.png
			ui_pict 104, x+25+72,y+90, 7,61, 1		! ui_thres_sect_1_1.png
			ui_pict 96, x+25+164,y+143, 13,9, 1		! ui_arrow_v_9.png

			ui_infield "gs_treshold_thk", thresThkPosX, thresThkPosY, thresThkInFldWdt,20				ui_tooltip stThresholdUI[4]		! Threshold Thickness
		endif

		if	iThresholdType = TRESHOLD_EXTENDED |\
			iThresholdType = TRESHOLD_EXTENDED_SWE then

			ui_pict 97, x+25+79,y+90, 53,61, 1		! ui_thres_sect_3.png
			ui_pict 99, x+25+132,y+90, 29,61, 1	! ui_thres_sect_3_T2.png

			ui_pict 104, x+25+72,y+90, 7,61, 1		! ui_thres_sect_1_1.png
			ui_pict 96, x+25+164,y+143, 13,9, 1		! ui_arrow_v_9.png

			if iThresholdType = TRESHOLD_EXTENDED_SWE then
				ui_infield "gs_treshold_nosing", x+188,y+110,90,20		ui_tooltip stThresholdUI[5]		! Threshold Nosing Opening Side
			else
				ui_infield "gs_treshold_nosing_2", x+188,y+110,90,20	ui_tooltip stThresholdUI[6]		! Threshold Nosing Opposite Side
			endif
			ui_infield "gs_treshold_thk",	 thresThkPosX, thresThkPosY, thresThkInFldWdt,20			ui_tooltip stThresholdUI[4]		! Threshold Thickness
		endif

		if iThresholdType = TRESHOLD_BOTHEXTENDED then
			ui_pict 100, x+25+79, y+90,  53,61, 1	! ui_thres_sect_4.png
			ui_pict 99, x+25+132,y+90, 29,61, 1	! ui_thres_sect_3_T2.png

			if not(bThresholdSillPage) | iSillType = SILL_TIMBER then	! Off
				ui_pict 101, x+25+63,y+90, 16,61, 1		! ui_thres_sect_4_1.png
			else
				ui_pict 102, x+25+72,y+90, 7,61, 1		! ui_thres_sect_4_2.png
			endif
			ui_pict 96, x+25+164,y+143, 13,9, 1			! ui_arrow_v_9.png

			if not(bThresholdSillPage) | iSillType = SILL_TIMBER then	! Off
				ui_infield "gs_treshold_nosing",  x,		y+110,60,20 ui_tooltip stThresholdUI[5]		! Threshold Nosing Opening Side
			endif
			ui_infield "gs_treshold_nosing_2",	x+188,	y+110,90,20 	ui_tooltip stThresholdUI[6]		! Threshold Nosing Opposite Side
			ui_infield "gs_treshold_thk",		thresThkPosX,	 thresThkPosY, thresThkInFldWdt,20 	ui_tooltip stThresholdUI[4]		! Threshold Thickness
		endif

		thresWdtPosX		= x - 30 + (gs_threshold_nor) * 20
		thresWdtPosY		= y + 110 - 23
		thresWdtInFldWdt	= 90 - (gs_threshold_nor) * 20

		if iThresholdType = TRESHOLD_FLAT then
			ui_pict 123, x+25+63,y+90, 98,62, 1		! ui_nor_thres_2.png
			ui_pict 96, x+25+164,y+143, 13,9, 1		! ui_arrow_v_9.png
			ui_infield "gs_treshold_thk",	thresThkPosX,	thresThkPosY,	thresThkInFldWdt,	20	ui_tooltip stThresholdUI[4]		! Threshold Thickness
			ui_infield "gs_treshold_width",	thresWdtPosX,	thresWdtPosY,	thresWdtInFldWdt,	20	ui_tooltip stThresholdUI[18]	! Threshold Width
		endif

		if iThresholdType = TRESHOLD_SIMPLE then
			ui_pict 129, x+25+63,y+90, 98,62, 1		! ui_simple_threshold.png
			ui_pict 96, x+25+164,y+143, 13,9, 1		! ui_arrow_v_9.png
			ui_infield "gs_treshold_thk", thresThkPosX, thresThkPosY, thresThkInFldWdt,20				ui_tooltip stThresholdUI[4]		! Threshold Thickness
		endif

		if	iThresholdType = TRESHOLD_ANSLAG |\
			iThresholdType = TRESHOLD_BRANN |\
			iThresholdType = TRESHOLD_BADEROMS |\
			iThresholdType = TRESHOLD_AKKUSTIKK then

			ui_pict 124, x+25+63,y+90, 98,62, 1		! ui_nor_thres_3.png
			ui_pict 96, x+25+164,y+143, 13,9, 1		! ui_arrow_v_9.png
			ui_infield "gs_treshold_thk",	thresThkPosX,	thresThkPosY,	thresThkInFldWdt,	20	ui_tooltip stThresholdUI[4]		! Threshold Thickness
			ui_infield "gs_treshold_width",	thresWdtPosX,	thresWdtPosY,	thresWdtInFldWdt,	20	ui_tooltip stThresholdUI[18]	! Threshold Width
		endif

		if iThresholdType = TRESHOLD_GUMMI then
			ui_pict 125, x+25+63,y+90, 98,62, 1		! ui_nor_thres_6.png
			ui_pict 96, x+25+164,y+143, 13,9, 1		! ui_arrow_v_9.png
			ui_infield "gs_treshold_thk",	thresThkPosX,	thresThkPosY,	thresThkInFldWdt,	20	ui_tooltip stThresholdUI[4]		! Threshold Thickness
			ui_infield "gs_treshold_width",	thresWdtPosX,	thresWdtPosY,	thresWdtInFldWdt,	20	ui_tooltip stThresholdUI[18]	! Threshold Width
		endif

		if iThresholdType = TRESHOLD_SLOPINGEXT then
			ui_pict 136, x+8+63,y+90, 120,75, 1		! ui_sloping_ext_threshold.png

			ui_infield "gs_treshold_thk", x+195,y+131,90,20		ui_tooltip stThresholdUI[4]			! Threshold Thickness
			ui_infield "gs_treshold_width", x+195,y+155,90,20	ui_tooltip stThresholdUI[18]		! Threshold Width
			ui_infield "gs_treshold_angle", x+6,y+105,90,20		ui_tooltip stThresholdUI[19]		! Threshold Angle
			ui_infield "gs_treshold_ext_hgt", x+6,y+131,90,20	ui_tooltip stThresholdUI[20]		! Threshold External Thickness
			ui_infield "gs_treshold_nosing", x+6,y+155,90,20	ui_tooltip stThresholdUI[5]			! Threshold Nosing Opposite Side
		endif

		if iThresholdType = TRESHOLD_METAL then
			ui_pict 137, x+8+63,y+90, 120,75, 1		! ui_metal_threshold.png

			ui_infield "gs_treshold_thk", x - 30+195,y+136,90,20	ui_tooltip stThresholdUI[4]			! Threshold Thickness
			ui_infield "gs_treshold_angle", x - 30+6,y+105,90,20	ui_tooltip stThresholdUI[19]		! Threshold Angle
			ui_infield "gs_treshold_ext_hgt", x - 30+6,y+131,90,20	ui_tooltip stThresholdUI[20]		! Threshold External Thickness
			ui_infield "gs_treshold_width", x - 30+6,y+155,90,20	ui_tooltip stThresholdUI[18]		! Threshold Width
		endif
	endif

	bFlag = (iBrickSilldetlevel = BRICKSILL_SIMPLE)
	if bThresholdSillPage & gs_sill_outside & iSillType = SILL_BRICK then
		if not(gs_threshold_nor) then
			ui_outfield stSillUI[17],			x1,		245,	x2-x1-90-1,	15,  4* bFlag		! Brick Thickness
			ui_infield "gs_brick_sill_br_thk",	x2-90,	245-4,	90,		19
		else
			ui_outfield stSillUI[17],			x3,		222,	x4-x3-90-1,	15,  4* bFlag		! Brick Thickness
			ui_infield "gs_brick_sill_br_thk",  x4-90,	222-4,	90,		19
		endif

		ui_outfield stSillUI[18],				x3,		245,	x4-x3-90-1,	15,  4* bFlag		! Mortar Thickness
		ui_infield "gs_brick_sill_mort_thk",  	x4-90,	245-4,	90,		19
	endif

	ui_pict 95, x+25+107,y+151, 54,1			! ui_sill_right.png
	if bThresholdSillPage & gs_sill_outside & iSillType <> SILL_TIMBER then
		sillInFldWdt = 90 - (gs_threshold_nor) * 20
		xPos1 = x - 30 + (gs_threshold_nor) * 20

		if iSillType = SILL_BRICK | iSillType = SILL_STONE then
			ui_infield "gs_sill_angle",		xPos1, y+110, sillInFldWdt, 19		ui_tooltip stSillUI[19]			! Sill Angle
			ui_infield "gs_sill_nosing",	xPos1, y+133, sillInFldWdt, 19		ui_tooltip stSillUI[21]			! Sill Nosing
			ui_infield "gs_sill_thk",		xPos1, y+156, sillInFldWdt, 19		ui_tooltip stSillUI[20]			! Sill Thickness
			ui_infield "gs_sill_width", 	xPos1, y+179, sillInFldWdt, 19		ui_tooltip stSillUI[22]			! Sill Width
		endif

		if iSillType = SILL_BRICK then
			ui_pict 90, x+25+36,y+112, 36,82, 1	! ui_sill_brick_1.png
			ui_pict 91, x+25+72,y+151, 35,43, 1	! ui_sill_brick_2.png
		endif

		if iSillType = SILL_STONE then
			ui_pict 92, x+25+36,y+112, 36,82, 1	! ui_sill_stone_1.png
			ui_pict 93, x+25+72,y+151, 35,43, 1	! ui_sill_stone_2.png

			ui_infield "gs_sill_topWidth",	x+25+36+72, y+169, sillInFldWdt, 19	ui_tooltip stSillUI[35]			! Sill Top Width
		endif

		if iSillType = SILL_BLOCK then
			xp = x + 64
			yp = y + 151
			ui_pict 130, xp, yp-17, 33, 39, 1	! ui_sill_block_1.png
			ui_pict 131, xp+33, yp, 35, 11, 1	! ui_sill_block_2.png

			ui_infield "gs_sill_nosing",	xPos1, y+133, sillInFldWdt, 20	ui_tooltip stSillUI[21]			! Sill Nosing
			ui_infield "gs_sill_thk",		xPos1, y+162, sillInFldWdt, 20	ui_tooltip stSillUI[20]			! Sill Thickness
		endif

		if iSillType = SILL_DUTCH then
			xp = x + 63
			yp = y + 151
			ui_pict 132, xp, yp-38, 33, 75, 1	! ui_sill_dutch_1.png
			ui_pict 133, xp+33, yp, 59, 37, 1	! ui_sill_dutch_2.png

			ui_infield "gs_sill_angle",		xPos1, y+110, sillInFldWdt, 20		ui_tooltip stSillUI[19]			! Sill Angle
			ui_infield "gs_sill_nosing",	xPos1, y+133, sillInFldWdt, 20		ui_tooltip stSillUI[21]			! Sill Nosing
			ui_infield "gs_sill_endthk",	xPos1, y+156, sillInFldWdt, 20		ui_tooltip stSillUI[37]			! Sill Front Side Thickness
			ui_infield "gs_sill_width", 	xPos1, y+179, sillInFldWdt, 20		ui_tooltip stSillUI[22]			! Sill Width
			ui_infield "gs_sill_thk",		x+158, y+179, sillInFldWdt, 20	ui_tooltip stSillUI[20]			! Sill Thickness
		endif
	else
		ui_pict 94, x+25+58,y+151, 49,1			! ui_sill_left.png
	endif
return


! --------------------------------------------------------------------------------
! Custom Corner
! --------------------------------------------------------------------------------
"pageCustomCorner":

	pageStartY = 4

	bCornerUIDisable = 0
	if bInclinedWall or curvedWall then
		bCornerUIDisable = 1
	endif

	if isEmptyOpening then				!-----Custom Corner in openings-------

		x1 = 10
		y1 = pageStartY
		x2 = 232
		y2 = 68 + pageStartY + 20*ac_cw_function

		ui_style 0,1
		if ac_cw_function then
			ui_infield{3} "ac_corner_window", 5, y1, 430, 19,
						7, "",
						1, 1,
						0, 0, 0, 0,
						"", stCustomCornerUI[14], 0	,		! Corner Window Function
						"", stCustomCornerUI[14], 1 ui_tooltip stCustomCornerUI[15]
			y1 = y1 + 20
		endif

		ui_separator x2-10,	y1+35, x2-10, 225 + pageStartY -bCornerUIDisable*22

		! Left Side============================================
		if iWindowShape = SHAPE_RECTANGULAR then

			ui_style 0,1
				ui_infield{3} "bLeftCornerFunction", x1+5, y1+35, 194, 16,
						7, "",
						2,
						1, 20, 20, 20, 20,
						0, stCustomCornerUI[2], 0,
						1, stCustomCornerUI[2], 1

			ui_style 0,0

			if bLeftCornerFunction then
				ui_pict 120,	x1+75, y2,    118, 63, 1				!ui_corner_opening_rect_left
				ui_pict 88,	x1+75, y2+66, 49,  16, 1

				ui_infield "leftCornerAngle", x1+135, y2+65, 60, 20 ui_tooltip stCustomCornerUI[8]
				ui_infield "leftConnWallThk", x1+70,  y2+90, 60, 20 ui_tooltip stCustomCornerUI[10]

				ui_infield{3} "bLeftDiffConnWall", x1+5, y2+118-bCornerUIDisable*20, 200, 40,
							7, "",
							2,
							1, 20, 20, 20, 20,
							0, stCustomCornerUI[4], 0,
							1, stCustomCornerUI[4], 1
			else
				ui_pict 119,	x1+75, y2,    118, 49, 1				!ui_corner_opening_left
			endif

		else
			ui_pict 119,	x1+75, y2,    118, 49, 1				!ui_corner_opening_left
		endif

! Right Side============================================

		ui_style 0,1
		ui_infield{3} "bRightCornerFunction", x2+5, y1+35, 193, 16,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stCustomCornerUI[3], 0,
				1, stCustomCornerUI[3], 1
		ui_style 0,0

		if bRightCornerFunction then

			ui_pict 121,	x2+10, y2,    118, 63, 1			!ui_corner_opening_right.png
			ui_pict 88,	x2+80, y2+66, 49,  16, 1

			ui_infield "rightCornerAngle", x2+10, y2+65, 60, 20 ui_tooltip stCustomCornerUI[9]
			ui_infield "rightConnWallThk", x2+80, y2+90, 60, 20 ui_tooltip stCustomCornerUI[11]

			ui_infield{3} "bRightDiffConnWall", x2+5, y2+118-bCornerUIDisable*20, 200, 40,
					7, "",
					2,
					1, 20, 20, 20, 20,
					0, stCustomCornerUI[4], 0,
					1, stCustomCornerUI[4], 1
		else
			ui_pict 122,	x2+10, y2,    118, 49, 1				!ui_corner_opening_rect_right.png
		endif
	else

		x1 = 10
		y1 = pageStartY - 4*ac_cw_function
		x2 = 232
		y2 = 91 + pageStartY + 20*ac_cw_function

		ui_style 0,1
		if ac_cw_function then
			ui_infield{3} "ac_corner_window", 5, y1, 430, 19,
						7, "",
						1, 1,
						0, 0, 0, 0,
						"", stCustomCornerUI[14], 0	,		! Corner Window Function
						"", stCustomCornerUI[14], 1 ui_tooltip stCustomCornerUI[15]
			y1 = y1 + 20
		endif

		ui_style 0,1
		if bShowCornerMitreLineUI then
			ui_outfield	stCustomCornerUI[1], 5,  y1+3, 184, 16, 4*(bCornerUIDisable or not(bRightCornerFunction or bLeftCornerFunction))

			ui_style 0,0

			ui_infield{3}	"iCornerConnTypeLeft",	190, y1-1, 112, 20,
				8, "", 3, 1, 0, 0, 0, 0,
				1, stCornerConnType[1], CONNECTION_TYPE_FRAME,
				2, stCornerConnType[2], CONNECTION_TYPE_COLUMN,
				3, stCornerConnType[3], CONNECTION_TYPE_GLASS

			ui_infield{3} "gs_line_corn", 190+128, y1+7, 125, 16,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stCustomCornerUI[12], 0,
				1, stCustomCornerUI[12], 1
		else
			ui_outfield	stCustomCornerUI[1], 5,  y1+3, 187, 16, 4*(bCornerUIDisable or not(bRightCornerFunction or bLeftCornerFunction))

			ui_style 0,0

			ui_infield{3}	"iCornerConnTypeLeft",	x2-10, y1-1, 166, 20,
					8, "", 3, 1, 0, 0, 0, 0,
					1, stCornerConnType[1], CONNECTION_TYPE_FRAME,
					2, stCornerConnType[2], CONNECTION_TYPE_COLUMN,
					3, stCornerConnType[3], CONNECTION_TYPE_GLASS

		endif

		y1 = y1 + 4

		ui_separator 1,	y1+20, 443, y1+20


! Left Side============================================
		if iWindowShape = SHAPE_RECTANGULAR or iWindowShape = SHAPE_TRAPEZOID then
			ui_style 0,1
			ui_infield{3} "bLeftCornerFunction", 5, y1+30, 194, 16,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stCustomCornerUI[2], 0,
				1, stCustomCornerUI[2], 1

			ui_style 0,0
			ui_infield{3} "bLeftDiffConnWall", x1+5, y2+115-bCornerUIDisable*20, 200, 30,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stCustomCornerUI[4], 0,
				1, stCustomCornerUI[4], 1
		endif

		if bLeftCornerFunction then

! Frame-------------------------------
			if iCornerConnTypeLeft = CONNECTION_TYPE_FRAME then			!Frame
				ui_pict 82,	x1+75, y2,    118, 63, 1				!ui_cornerw_frame_left
				ui_pict 88,	x1+75, y2+66, 49,  16, 1

				ui_infield "leftCornerAngle", x1+135, y2+65, 60, 20 ui_tooltip stCustomCornerUI[8]
				ui_infield "leftConnWallThk", x1+70,  y2+90, 60, 20 ui_tooltip stCustomCornerUI[10]
			endif

! Corner Coloumn---------------------
			if iCornerConnTypeLeft = CONNECTION_TYPE_COLUMN then			!Corner Coloumn
				ui_pict 84,	x1+68, y2-5,  125, 70, 1				!ui_cornerw_column_left
				ui_pict 88,	x1+75, y2+68, 49,  16, 1

				ui_infield "gs_cc_thk",       x1,     y2+14, 60, 20 ui_tooltip stCustomCornerUI[6]
				ui_infield "gs_cc_width",     x1+48,  y2-30, 60, 20 ui_tooltip stCustomCornerUI[5]
				ui_infield "gs_cc_pr",        x1+115, y2-30, 60, 20 ui_tooltip stCustomCornerUI[7]
				ui_infield "leftCornerAngle", x1+135, y2+65, 60, 20 ui_tooltip stCustomCornerUI[8]
				ui_infield "leftConnWallThk", x1+70,  y2+90, 60, 20 ui_tooltip stCustomCornerUI[10]
			endif

! Glass-------------------------------
			if iCornerConnTypeLeft = CONNECTION_TYPE_GLASS then			!Glass
				ui_pict 86,	x1+75, y2,    118, 63, 1				!ui_cornerw_glass_left
				ui_pict 88,	x1+75, y2+66, 49,  16, 1

				ui_infield "leftCornerAngle", x1+135, y2+65, 60, 20 ui_tooltip stCustomCornerUI[8]
				ui_infield "leftConnWallThk", x1+70,  y2+90, 60, 20 ui_tooltip stCustomCornerUI[10]
			endif

		else
			ui_pict 80,	x1+75, y2, 118, 49, 1						!ui_cornerw_nocorner_left
		endif

		ui_separator x2-10,	y1+24, x2-10, 235 + pageStartY - bCornerUIDisable*22

! Right Side============================================

		ui_style 0,1
		ui_infield{3} "bRightCornerFunction", x2-5, y1+30, 193, 16,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stCustomCornerUI[3], 0,
				1, stCustomCornerUI[3], 1
		ui_style 0,0

		ui_infield{3} "bRightDiffConnWall", x2+5, y2+115-bCornerUIDisable*20, 200, 30,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stCustomCornerUI[4], 0,
				1, stCustomCornerUI[4], 1

		if bRightCornerFunction then

! Frame-------------------------------
			if iCornerConnTypeLeft = CONNECTION_TYPE_FRAME then		!Frame
				ui_pict 83,	x2+10, y2,    118, 63, 1			!ui_cornerw_frame_righ
				ui_pict 88,	x2+80, y2+66, 49,  16, 1

				ui_infield "rightCornerAngle", x2+10, y2+65, 60, 20 ui_tooltip stCustomCornerUI[9]
				ui_infield "rightConnWallThk", x2+80, y2+90, 60, 20 ui_tooltip stCustomCornerUI[11]
			endif

! Corner Coloumn---------------------
			if iCornerConnTypeLeft = CONNECTION_TYPE_COLUMN then		!Corner Coloumn
				ui_pict 85,	x2+10, y2-5,  125, 70, 1			!ui_cornerw_column_right
				ui_pict 88,	x2+80, y2+68, 49,  16, 1

				if bleftCornerFunction then
					n = request("Working_length_unit", "", format_string)
					outStringThk   = str(format_string, gs_cc_thk)
					outStringWidth = str(format_string, gs_cc_width)
					outStringPr    = str(format_string, gs_cc_pr)
					ui_outfield outStringThk,   x2+140, y2+17, 60, 16    ui_tooltip stCustomCornerUI[6]
					ui_outfield outStringWidth, x2+77,  y2-27, 60, 16, 2 ui_tooltip stCustomCornerUI[5]
					ui_outfield outStringPr,    x2+10,  y2-27, 60, 16, 1 ui_tooltip stCustomCornerUI[7]
				else
					ui_infield "gs_cc_thk",   x2+145, y2+14, 60, 20 ui_tooltip stCustomCornerUI[6]
					ui_infield "gs_cc_width", x2+95,  y2-30, 60, 20 ui_tooltip stCustomCornerUI[5]
					ui_infield "gs_cc_pr",    x2+25,  y2-30, 60, 20 ui_tooltip stCustomCornerUI[7]
				endif

				ui_infield "rightCornerAngle", x2+10, y2+65, 60, 20 ui_tooltip stCustomCornerUI[9]
				ui_infield "rightConnWallThk", x2+80, y2+90, 60, 20 ui_tooltip stCustomCornerUI[11]
			endif

! Glass-------------------------------
			if iCornerConnTypeLeft = CONNECTION_TYPE_GLASS then		!Glass
				ui_pict 87,	x2+10, y2,    118, 63, 1			!ui_cornerw_glass_right
				ui_pict 88,	x2+80, y2+66, 49,  16, 1

				ui_infield "rightCornerAngle", x2+10, y2+65, 60, 20 ui_tooltip stCustomCornerUI[9]
				ui_infield "rightConnWallThk", x2+80, y2+90, 60, 20 ui_tooltip stCustomCornerUI[11]
			endif
		else
			ui_pict 81,	x2+10, y2, 118, 49, 1					!ui_cornerw_nocorner_right
		endif
	endif

	if bCornerUIDisable then
		ui_pict 138, x1, 210 + pageStartY, 12, 12, 1
		ui_outfield stCustomCornerUI[13], x1+15, 210 + pageStartY, 440-x1-15, 20
	endif
return


! --------------------------------------------------------------------------------
! Shutter
! --------------------------------------------------------------------------------
"pageShutter":

	if iRevealType = REVEAL_DOUBLE_SPLAYED | iRevealType = REVEAL_OUTSIDE_SPLAYED then
		leftCornerX		= leftWallFrameRevealPnts[idxSillStartPlasterLeft+1][1]
		rightCornerX	= rightWallFrameRevealPnts[idxSillStartPlasterRight+1][1]
	else
		leftCornerX		= leftWallFrameRevealPnts[idxSillStartPlasterLeft][1]
		rightCornerX	= rightWallFrameRevealPnts[idxSillStartPlasterRight][1]
	endif

	sunShadeLength = rightCornerX - leftCornerX

	casingThkAtWindow	= 0
	casingThkOnFacade	= 0

	if gs_trim_outside then
		casingThkAtWindow = gs_trim_width_out
	endif

	call "SunShade" parameters a				= sunShadeLength - casingThkAtWindow * 2,
				bSunshade						= bSunshade,
				gs_sunShade_iType				= gs_sunShade_iType,
				gs_shutter_nPanels				= gs_shutter_nPanels,
				gs_shutter_nPanels2				= gs_shutter_nPanels2,
				gs_sunShade_bAutomaticHeight	= gs_sunShade_bAutomaticHeight,
				gs_shutter_bExtraFrame			= gs_shutter_bExtraFrame,
				gs_slidingShutter_iCover		= gs_slidingShutter_iCover,
				gs_foldingShutter_iStacking		= gs_foldingShutter_iStacking,
				gs_foldingShutter_ovhg			= gs_foldingShutter_ovhg,
				gs_sunShade_bShowIn2D			= gs_sunShade_bShowIn2D,
				gs_shutPanel_iType				= gs_shutPanel_iType,
				gs_shutPanel_thk				= gs_shutPanel_thk,
				gs_shutPanel_bAutomaticWidth	= gs_shutPanel_bAutomaticWidth,
				gs_shutPanel_panelWidth			= gs_shutPanel_panelWidth,
				gs_shutRoll_iPlacement			= gs_shutRoll_iPlacement,
				gs_shutRoll_iBoxType			= gs_shutRoll_iBoxType,
				gs_shutRoll_cutWallBehind		= gs_shutRoll_cutWallBehind,
				gs_shutLam_iPlacement			= gs_shutLam_iPlacement,
				gs_shutLam_cutWallBehind		= gs_shutLam_cutWallBehind,
				gs_shutLam_bRail				= gs_shutLam_bRail,

				stSunShadeTypes					= stSunShadeTypes,
				stShutterPanelPos				= stShutterPanelPos,
				stFoldingShutterStacking		= stFoldingShutterStacking,
				stSlidingShutterCover			= stSlidingShutterCover,
				stShutterPanelTypes				= stShutterPanelTypes,
				stShutRollPlacement				= stShutRollPlacement,
				stShutRollBoxType				= stShutRollBoxType,
				stShutRollBoxDefaultSize		= stShutRollBoxDefaultSize,
				stShutRollRailPos				= stShutRollRailPos,
				stShutLamPlacement				= stShutLamPlacement,
				stShutLamRailPos				= stShutLamRailPos,
				stSunShadeUI					= stSunShadeUI,
				stSunshadeTooltip				= stSunshadeTooltip,

				gs_ui_sub_tabpage				= gs_ui_sub_tabpage,
				iWindowShape					= iWindowShape,
				iRevealType						= iRevealType,
				bEnableCasing					= bEnableCasing
return


! --------------------------------------------------------------------------------
! Fixtures and Fittings Model Attributes
! --------------------------------------------------------------------------------
"pageFixFittingModAttributes":
	__bHasListItem = 0
	if bSillUSA then
		_bBrickSillDetailed	= (not(isWindow) & iSillType = SILL_BRICK & iBrickSilldetlevel = BRICKSILL_DETAILED)
	else
		_bBrickSillDetailed	= (iSillType = SILL_BRICK & iBrickSilldetlevel = BRICKSILL_DETAILED)
	endif
	
	listFieldConnStrAttrID = iUiID_FixFittingModAttr
	itemID = 1
	
	if bEnableThreshold & bThreshold then
		__bHasListItem = 1
		ui_listitem itemID, listFieldConnStrAttrID, "gs_treshold_mat",	0,	"ui_list_surface_flat(1)", stThresholdUI[1]	: itemID = itemID + 1		! Threshold
	endif

	if bEnableSill then
		itemID = 5
		if bSillUSA then
			if	(isWindow & iClosureType <> CLOSURE_BRICK_VENEER & not(gs_fsill)) |\
				not(isWindow) then
				ui_listitem itemID, listFieldConnStrAttrID, "",		0,	"", `Покрытия Отлива`	: itemID = itemID + 1
				if gs_sill_outside then
					__bHasListItem = 1
					ui_listitem itemID, listFieldConnStrAttrID, "gs_sill_pen_3D", 			1,	"ui_list_contourPen_flat(1)",	stSillUI[29]	: itemID = itemID + 1
					ui_listitem itemID, listFieldConnStrAttrID, "gs_sill_mat",				1,	"ui_list_surface_flat(1)", 		stSillUI[1]	: itemID = itemID + 1	! Sill
					if _bBrickSillDetailed then
						ui_listitem itemID, listFieldConnStrAttrID, "gs_brick_sill_mort_mat",	1,	"ui_list_surface_flat(1)", 		stSillUI[13]	: itemID = itemID + 1	! Brick Sill Mortar
					endif
				endif
			endif
		else
			if bDoorWithSidelightSupport then
				_bHasAnySillOn = (gs_sill_outside | \
								(gs_sidelight_left  & gs_sidelight_parapet_hgt_left > EPS  & gs_sill_outside_sl_left) | \
								(gs_sidelight_right & gs_sidelight_parapet_hgt_right > EPS & gs_sill_outside_sl_right))
			else
				_bHasAnySillOn = (gs_sill_outside)
			endif

			if _bHasAnySillOn then
				__bHasListItem = 1
				if _bBrickSillDetailed then
					ui_listitem itemID, listFieldConnStrAttrID, "",							0,	"", 					`Покрытия Отлива`	: itemID = itemID + 1
					ui_listitem itemID, listFieldConnStrAttrID, "gs_sill_mat",				1,	"ui_list_surface_flat(1)", 	stSillUI[1]	: itemID = itemID + 1	! Sill
					ui_listitem itemID, listFieldConnStrAttrID, "gs_brick_sill_mort_mat",	1,	"ui_list_surface_flat(1)", 	stSillUI[13]	: itemID = itemID + 1	! Brick Sill Mortar
				else
					ui_listitem itemID, listFieldConnStrAttrID, "gs_sill_mat",	0,	"ui_list_surface_flat(1)", 		stSillUI[8]		: itemID = itemID + 1	! Sill
				endif
			endif
		endif
	endif

	if bEnableBoard & bDrawBoard then
		itemID = 10
		__bHasListItem = 1
		ui_listitem itemID, listFieldConnStrAttrID,	 	"gs_board_mat",		0,	"ui_list_surface_flat(1)", stBoardUI[1]			: itemID = itemID + 1	! Board
		if bSillUSA & bEnableCasing & gs_apron then
			ui_listitem itemID, listFieldConnStrAttrID, "gs_apron_mat",		0,	"ui_list_surface_flat(1)", stCasingTextUSA[7]	: itemID = itemID + 1	! Apron
		endif
	endif

	if bEnableCasing then
		itemID = 20
		__bHasListItem = 1
		ui_listitem itemID, listFieldConnStrAttrID, "",		0,	"", `Покрытия Наличника`	: itemID = itemID + 1
		if gs_trim_outside | gs_trim_inside then
			ui_listitem itemID, listFieldConnStrAttrID, "bLinkCasingSurfaces",		1,	"ui_list_surface_flat(1)"	: itemID = itemID + 1

			ui_listitem itemID, listFieldConnStrAttrID, "gs_trim_out_mat",		1,	"ui_list_surface_flat(1)", `Наружный Наличник`	: itemID = itemID + 1
			ui_listitem itemID, listFieldConnStrAttrID, "gs_trim_in_mat",		1,	"ui_list_surface_flat(1)", `Внутренний Наличник`	: itemID = itemID + 1
		else
			itemID = itemID + 3
		endif
	endif

	if bEnableRail then
		__bHasListItem = 1
		itemID = 30
		if bUSARail then
			ui_listitem itemID, listFieldConnStrAttrID, "",					0,	"", 				`Направляющая`		: itemID = itemID + 1
		else
			ui_listitem itemID, listFieldConnStrAttrID, "",					0,	"", 				`Ограждение`				: itemID = itemID + 1
		endif
		if gs_door_rail then
			ui_listitem itemID, listFieldConnStrAttrID, "gs_rail_mat", 		1,	"ui_list_surface_flat(1)",	`Покрытие`		: itemID = itemID + 1
		endif
	endif

	if bEnableShutter then
		itemID = 40
		ui_listitem itemID, listFieldConnStrAttrID, "",		0,	"", `Покрытия Ставней`	: itemID = itemID + 1
		if bSunshade then
			__bHasListItem = 1
			ui_listitem itemID, listFieldConnStrAttrID, "gs_sunShade_unCutLinesPen", 		1,	"ui_list_contourPen_flat(1)",	stSunShadeUI[34]	: itemID = itemID + 1
	
			if gs_sunShade_iType = SUNSHADE_ROLLING then
				ui_listitem itemID, listFieldConnStrAttrID, "gs_shutRoll_matBox",		1,	"ui_list_surface_flat(1)", stSunShadeUI[44]	: itemID = itemID + 1	! Box
				ui_listitem itemID, listFieldConnStrAttrID, "gs_shutRoll_matSegm",		1,	"ui_list_surface_flat(1)", stSunShadeUI[45]	: itemID = itemID + 1	! Blades
				ui_listitem itemID, listFieldConnStrAttrID, "gs_shutRoll_matRail",		1,	"ui_list_surface_flat(1)", stSunShadeUI[31]	: itemID = itemID + 1	! Rail
			else
				if gs_sunShade_iType = SUNSHADE_VENETIAN | gs_sunShade_iType = SUNSHADE_EXTVENETIAN then
					ui_listitem itemID, listFieldConnStrAttrID, "gs_shutLam_matBox",	1,	"ui_list_surface_flat(1)", stSunShadeUI[44]	: itemID = itemID + 1	! Box
					ui_listitem itemID, listFieldConnStrAttrID, "gs_shutLam_matLam",	1,	"ui_list_surface_flat(1)", stSunShadeUI[45]	: itemID = itemID + 1	! Blades
				else
					ui_listitem itemID, listFieldConnStrAttrID, "gs_shutPanel_materialAcc",		1,	"ui_list_surface_flat(1)", stSunShadeUI[29]	: itemID = itemID + 1	! Frame
					ui_listitem itemID, listFieldConnStrAttrID, "gs_shutPanel_materialMain",	1,	"ui_list_surface_flat(1)", stSunShadeUI[30]	: itemID = itemID + 1	! Filling
				endif
			endif
			if gs_sunShade_iType = SUNSHADE_SLIDING then
				ui_listitem itemID, listFieldConnStrAttrID, "gs_shutSliding_matRail",	1,	"ui_list_surface_flat(1)", stSunShadeUI[31]	: itemID = itemID + 1	! Rail
			endif
		endif
	endif

	if bEnableCustomCornerLeft | bEnableCustomCornerRight then
		itemID = 50
		if (bLeftCornerFunction | bRightCornerFunction) & iCornerConnTypeLeft = CONNECTION_TYPE_COLUMN then
			__bHasListItem = 1
			ui_listitem itemID, listFieldConnStrAttrID, "matCornerColumn", 				0,	"ui_list_surface_flat(1)",	`Покрытие Углового Импоста`	: itemID = itemID + 1
		endif
	endif
	
	! --------------------------------------------------------------------------------
	! Show listfield only if there are items enabled in it (at least 1 item is displayed)
	! --------------------------------------------------------------------------------

 	if __bHasListItem then
		ui_listfield listFieldConnStrAttrID, 0, 0, 444, 265
	else
		ui_outfield `Дополнительные элементы не выбраны.`, 2, 10, 440, 15
	endif
return

