

reqBKnobs = 0
success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
if success > 0 and reqBKnobs then
	end
endif


IF gs_detlevel_3D_m=0 THEN END

MATERIAL gs_latch_mat
rotx 90

!!! ************************Style 1************************************
IF gs_latch_typ_m = 1 THEN
	IF gs_detlevel_3D_m = 2 THEN		!!!Detailed
		!!!frame
				prism_ 8, th1,
					-aa1/2, -bb1/2, 11,
					aa1/2, -bb1/2, 11,
					aa1/2, -rnh/2, 13,
					aa1/2, rnh/2, 9,
					aa1/2, bb1/2, 11,
					-aa1/2, bb1/2, 11,
					-aa1/2, rnh/2, 13,
					-aa1/2, -rnh/2, 9

			CUTPOLY 4,
				-aa1/2+0.005, lth,
				-aa1/2+0.005, -lth,
				aa1/2-0.005, -lth,
				aa1/2-0.005, lth

			ADD aa1/2, 0, th1
			ROTy -90
				prism_ 12, aa1,
					0, -bb1/2, 10,
					0, -rnh/2, 15,
					th1, -rkh/2, 15,
					th1, rkh/2, 15,
					0, rnh/2, 10,
					0, bb1/2, 13,
					th1, bb1/2, 15,
					th1, rnh/2+(th1/tan(bet1)), 15,
					2*th1, rkh/2+(th1/tan(bet1)), 15,
					2*th1, -rkh/2-(th1/tan(bet1)), 15,
					th1, -rnh/2-(th1/tan(bet1)), 15,
					th1, -bb1/2, 15
			DEL 2
			CUTEND

		!!latch
		ADDz th1
			prism 4, th1-0.0003,
				-aa1/2-0.01, rkh/2-0.001,
				-aa1/2-0.01, -rkh/2+0.001,
				aa1/2, -rkh/2+0.001,
				aa1/2, rkh/2-0.001

		RESOL 8
		ADD -aa1/2+0.012,0,2*th1
			cylind clh, clr
		DEL 2

	ELSE							!!!Simple
		ADDx aa1/2
		ROTy -90

			Prism 8, aa1,
				0, -bb1/2,
				0, bb1/2,
				2*th1, bb1/2,
				2*th1, rnh/2,
				3*th1, rkh/2,
				3*th1, -rkh/2,
				2*th1, -rnh/2,
				2*th1, -bb1/2

		ADDz aa1
			Prism 4, 0.01,
				th1, rkh/2-0.001,
				2*th1, rkh/2-0.001,
				2*th1, -rkh/2+0.001,
				th1, -rkh/2+0.001
		DEL 3

		ADD -aa1/2+0.012,0,3*th1
			cylind clh, clr
		DEL 1
	ENDIF


		GOSUB 100		!!!Display

ENDIF

!!! ************************Style 2************************************
IF gs_latch_typ_m = 2 THEN
	!!! handle
	ROTx -90

	ADDZ -0.0015
	Prism_ 14, 0.003,
		-0.003, -th21-th22, 79,
		-0.003, -0.012, 79,
		-0.02, -0.014, 79,
		-0.022, -0.016, 79,
		-0.022, -0.017, 79,
		-0.02, -0.019, 79,
		-0.003, -0.021, 79,

		0.003, -0.021, 79,
		0.02, -0.019, 79,
		0.022, -0.017, 79,
		0.022, -0.016, 79,
		0.02, -0.014, 79,
		0.003, -0.012, 79,
		0.003, -th21-th22, 79

	DEL 2

		GOSUB 200		!!!base

	ADDz -gs_door_thck-dpd
	ROTy 180
		GOSUB 250
	DEL 2
ENDIF

!!! ************************Style 3************************************
IF gs_latch_typ_m = 3 THEN
	IF gs_detlevel_3D_m = 2 THEN
		resol 28
	ELSE
		resol 18
	ENDIF

	!!! handle
	ROTx -90

	ADDZ -0.0015
	Prism_ 7, 0.003,
		-0.002, -th21-th22, 79,
		-0.002, -th21-th22-0.001, 79,
		0, 0.003, 979,
		-diami/2, -th21-th22-0.001, 79,
		diami/2, -th21-th22-0.001, 3079,
		0.002, -th21-th22-0.001, 79,
		0.002, -th21-th22, 79
	DEL 2

		GOSUB 200		!!!base

	ADDz -gs_door_thck-dpd
	ROTy 180
		GOSUB 250
	DEL 2
ENDIF

!!! ************************Style 4************************************
IF gs_latch_typ_m = 4 THEN

	!!! handle
	ROTx -90

	ROTz -90
	ROTx -90
	ADDZ -0.0015

	Prism_ 10, 0.003,
		th21+th22, 0.003, 79,
		th21+th22+0.002, 0.003, 79,
		th21+th22+0.002, diamo/2+0.024, 79,
		th21+th22+0.004, diamo/2+0.026, 79,
		th21+th22+0.005, diamo/2+0.026, 79,
		th21+th22+0.008, 0.028, 79,
		th21+th22+0.009, 0.01, 79,
		th21+th22+0.009, -0.002, 79,
		th21+th22+0.002, -0.003, 79,
		th21+th22, -0.003, 79
	DEL 4


		GOSUB 200		!!!base

	ADDz -gs_door_thck-dpd
	ROTy 180
		GOSUB 250
	DEL 2
ENDIF

END




!!!________________Subroutins___________________________________________
100:
	!!! display
	ADDz -gs_door_thck-dpd
		Prism_ 4, dpd,
			-aa1/2, dph/2, 14,
			aa1/2, dph/2, 14,
			aa1/2, -dph/2, 14,
			-aa1/2, -dph/2, 14
	ADDz -th1
		Prism_ 10, th1,
			-aa1/2, dph/2, 11,
			aa1/2, dph/2, 11,
			aa1/2, -dph/2, 11,
			-aa1/2, -dph/2, 11,
			-aa1/2, dph/2, -1,
			-kjw/2, kjh/2, 15,
			kjw/2, kjh/2, 15,
			kjw/2, -kjh/2, 15,
			-kjw/2, -kjh/2, 15,
			-kjw/2, kjh/2,-1
	DEL 1

	MATERIAL "Green1"
		Poly 4,
			-kjw/2, kjh/2,
			kjw/2, kjh/2,
			kjw/2, -kjh/2,
			-kjw/2, -kjh/2
RETURN


200:
	IF gs_detlevel_3D_m = 2 THEN		!!!Detailed
		resol 18
		ROTy -90
			Revolve 3, 360, 2,
				0, diamo/2, 1,
				th21, diamo/2, 0,
				th21+th22, diami/2, 1
		DEL 1
	ELSE								!!!Simple
		resol 10
			cylind th21+th22, diamo/2
	ENDIF
RETURN



250:
	IF gs_detlevel_3D_m = 2 THEN		!!!Detailed
		resol 18
		prism_ 2, th21,
			0,0,911+64,
			diamo/2, 360, 4011+64


		Cutpoly 4,
			dp2w/2, 0.01+dp2h/2,
			-dp2w/2, 0.01+dp2h/2,
			-dp2w/2, 0.01-dp2h/2,
			dp2w/2, 0.01-dp2h/2
		ROTy -90
		Rotx 90

			Revolve 2, 360, 1+2,
				th21, diamo/2, 1,
				th21+th22, diami/2, 1
		DEL 2
		Cutend


	MATERIAL "Green1"
		ADDz th21+0.00001
		Poly 4,
			dp2w/2, 0.01+dp2h/2,
			-dp2w/2, 0.01+dp2h/2,
			-dp2w/2, 0.01-dp2h/2,
			dp2w/2, 0.01-dp2h/2
		DEL 1

	ELSE								!!!Simple
		resol 10
			cylind th21+th22, diamo/2

	MATERIAL "Green1"
		ADDz th21+th22+0.00001
		Poly 4,
			dp2w/2, 0.01+dp2h/2,
			-dp2w/2, 0.01+dp2h/2,
			-dp2w/2, 0.01-dp2h/2,
			dp2w/2, 0.01-dp2h/2
		DEL 1
	ENDIF
RETURN


