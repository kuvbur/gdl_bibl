
! Skewed_window_frame


if gs_frame_mat = gs_frame_mat2 then				!!! One body, in case of similar materials
	in_out_cikl = 1

	var_mater = gs_frame_mat
	var_mater2 = gs_frame_mat

	out_thk = 0
	part_thk =gs_frame_thk

	in_out_mask = 15
	in_out_mask2 = 13
	if gs_stack_bottom then bot_mask = 8 else bot_mask = 15
else												!!!two bodies
	in_out_cikl = 2

	!!! Inner-outer Material Control-----------
	temp_frame_mat = gs_frame_mat
	temp_frame_mat2 = gs_frame_mat2
	if WIDO_REVEAL_SIDE then
		gs_frame_mat=temp_frame_mat
		gs_frame_mat2=temp_frame_mat2
	else
		gs_frame_mat=temp_frame_mat2
		gs_frame_mat2=temp_frame_mat
	endif

	var_mater = gs_frame_mat
	var_mater2 = gs_frame_mat2

	if lod3D = 2 then	! Detailed
		out_thk = gs_frame_thk - gs_rebate_depth
		part_thk = out_thk
	else
		if lod3D = 5 then
			out_thk = gs_frame_thk/2
		else
			if WIDO_REVEAL_SIDE then
				out_thk = gs_frame_thk/2 - gs_glass_thk/2
			else
				out_thk = gs_frame_thk/2 + gs_glass_thk/2
			endif
		endif
		part_thk = out_thk
	endif

	in_out_mask = 11
	in_out_mask2 = 9
	if gs_stack_bottom then bot_mask = 8 else bot_mask = 11
endif


!! FRAME !!

group "frame"

	sect_fill gs_fillFr, gs_penFr_bg, gs_penFr_fg, gs_frame_pen_cont
	pen gs_frame_pen

	! --- Cutting in case of corner window -----------------------------------------
	halfA = A / 2
	cutLeft = 0
	cutRight = 0
	if abs(gs_frame_ovhg_left_out - gs_frame_ovhg_left_in) > EPS then
		addx -halfA - gs_frame_ovhg_left_out
		rotz 90
		cutplane -90 + atn((gs_frame_ovhg_left_out - gs_frame_ovhg_left_in) / gs_frame_thk)
		del 2
		cutLeft = 1
	endif
	if abs(gs_frame_ovhg_right_out - gs_frame_ovhg_right_in) > EPS then
		addx halfA + gs_frame_ovhg_right_out
		rotz 90
		cutplane 90 - atn((gs_frame_ovhg_right_out - gs_frame_ovhg_right_in) / gs_frame_thk)
		del 2
		cutRight = 1
	endif

	for ijk = 1 to in_out_cikl
		material var_mater

		if gs_SashText_typ_num = 1 then
			base
			cprism_ var_mater2,var_mater,var_mater,
				4, part_thk,
				-a/2+gs_frame_width+simp_mod,		frameBottomMiddleY+dysw1,	10,
				-a/2-leftOvhg,						frameBottomMiddleY+dy1,		in_out_mask,
				-a/2-leftOvhg,						frameTopMiddleY+dy2,		10,
				-a/2+gs_frame_width+simp_mod,		frameTopMiddleY+dysw2,		in_out_mask
			vert 0, 0, 0
			vert 0, 1, 0
			vert 1, 0, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1

			base
			cprism_ var_mater2,var_mater,var_mater,
				4, part_thk,
				a/2-gs_frame_width-simp_mod,	frameBottomMiddleY-dysw2,	10,
				a/2+rightOvhg,					frameBottomMiddleY-dy2,		in_out_mask,
				a/2+rightOvhg,					frameTopMiddleY-dy1,		10,
				a/2-gs_frame_width-simp_mod,	frameTopMiddleY-dysw1,		in_out_mask
			vert 0, 0, 0
			vert 0, 1, 0
			vert 1, 0, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1

			base
			cprism_ var_mater2,var_mater,var_mater,
				4, part_thk,
				-a/2-leftOvhg,					frameTopMiddleY+dy2,	10,
				-a/2+gs_frame_width+simp_mod,	frameTopMiddleY+dysw2,	in_out_mask,
				a/2-gs_frame_width-simp_mod,	frameTopMiddleY-dysw1,	10,
				a/2+rightOvhg,					frameTopMiddleY-dy1,	in_out_mask
			vert 0, 0, 0
			vert cos(topAngle), -sin(topAngle), 0
			vert sin(topAngle), cos(topAngle), 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1

			base
			cprism_ var_mater2,var_mater,var_mater,
				4, part_thk,
				a/2+rightOvhg,					frameBottomMiddleY-dy2,		10,
				a/2-gs_frame_width-simp_mod,	frameBottomMiddleY-dysw2,	in_out_mask,
				-a/2+gs_frame_width+simp_mod,	frameBottomMiddleY+dysw1,	10,
				-a/2-leftOvhg,					frameBottomMiddleY+dy1,		bot_mask
			vert 0, 0, 0
			vert cos(topAngle), -sin(topAngle), 0
			vert sin(topAngle), cos(topAngle), 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1
		endif

		if gs_SashText_typ_num = 2 then
			base
			cprism_ var_mater2,var_mater,var_mater,
				4, part_thk,
				-a/2+gs_frame_width+simp_mod,	frameBottomMiddleY+dysw1,	10,
				-a/2-leftOvhg,					frameBottomMiddleY+dysw1,	in_out_mask-2,
				-a/2-leftOvhg,					frameTopMiddleY+dysw2,		8,
				-a/2+gs_frame_width+simp_mod,	frameTopMiddleY+dysw2,		in_out_mask
			vert 0, 0, 0
			vert 0, 1, 0
			vert 1, 0, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1

			base
			cprism_ var_mater2,var_mater,var_mater,
				4, part_thk,
				a/2-gs_frame_width-simp_mod,	frameBottomMiddleY-dysw2,	8,
				a/2+rightOvhg,					frameBottomMiddleY-dysw2,	in_out_mask-2,
				a/2+rightOvhg,					frameTopMiddleY-dysw1,		8,
				a/2-gs_frame_width-simp_mod,	frameTopMiddleY-dysw1,		in_out_mask
			vert 0, 0, 0
			vert 0, 1, 0
			vert 1, 0, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1

			base
			cprism_ var_mater2,var_mater,var_mater,
				6, part_thk,
				-a/2-leftOvhg,					frameTopMiddleY+dy2,	in_out_mask,
				-a/2-leftOvhg,					frameTopMiddleY+dysw2,	8,
				-a/2+gs_frame_width+simp_mod,	frameTopMiddleY+dysw2,	in_out_mask,
				a/2-gs_frame_width-simp_mod,	frameTopMiddleY-dysw1,	10,
				a/2+rightOvhg,					frameTopMiddleY-dysw1,	in_out_mask2,
				a/2+rightOvhg,					frameTopMiddleY-dy1,	in_out_mask
			vert 0, 0, 0
			vert cos(topAngle), -sin(topAngle), 0
			vert sin(topAngle), cos(topAngle), 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1

			base
			cprism_ var_mater2,var_mater,var_mater,
				6, part_thk,
				a/2+rightOvhg,					frameBottomMiddleY-dy2,		in_out_mask,
				a/2+rightOvhg,					frameBottomMiddleY-dysw2,	8,
				a/2-gs_frame_width-simp_mod,	frameBottomMiddleY-dysw2,	in_out_mask,
				-a/2+gs_frame_width+simp_mod,	frameBottomMiddleY+dysw1,	10,
				-a/2-leftOvhg,					frameBottomMiddleY+dysw1,	in_out_mask2,
				-a/2-leftOvhg,					frameBottomMiddleY+dy1,		bot_mask
			vert 0, 0, 0
			vert cos(topAngle), -sin(topAngle), 0
			vert sin(topAngle), cos(topAngle), 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body 1
		endif

		addz part_thk
		part_thk= gs_frame_thk-out_thk

		var_mater = gs_frame_mat2
		var_mater2 = gs_frame_mat2

		in_out_mask = 14
		in_out_mask2 = 12
		if gs_stack_bottom then bot_mask = 8 else bot_mask = 14

	next ijk
	del in_out_cikl

	if cutLeft then cutend
	if cutRight then cutend
endgroup


! ==============================================================================
! Sash cavities in Frame
! ==============================================================================

group "harcsi"
	if lod3D = 2 then
		if GLOB_CONTEXT = 4 | GLOB_CONTEXT = 6 then
			pen gs_frame_pen_cont
		else
			pen gs_frame_pen
		endif
		sect_fill gs_fillFr, gs_penFr_bg, gs_penFr_fg, gs_frame_pen_cont

		if iSashCutType = SASHCUT_REBATE then
			addz gs_frame_thk - gs_rebate_depth
			material gs_frame_mat2
			prism_ 5, gs_frame_thk,
				-a/2+gs_frame_width-gs_rebate_width, frameBottomMiddleY+dyrw1,	15,
				 a/2-gs_frame_width+gs_rebate_width, frameBottomMiddleY-dyrw2,	15,
				 a/2-gs_frame_width+gs_rebate_width, frameTopMiddleY-dyrw1,		15,
				-a/2+gs_frame_width-gs_rebate_width, frameTopMiddleY+dyrw2,		15,
				-a/2+gs_frame_width-gs_rebate_width, frameBottomMiddleY+dyrw1,	-1
			body 1
			del 1
		endif

		if iSashCutType = SASHCUT_MIDDLE then
			addz gs_frame_thk/2
			material gs_frame_mat2
			prism_ 5, gs_frame_thk,
				-a/2+gs_frame_width, frameBottomMiddleY+dyfw1,	15,
				 a/2-gs_frame_width, frameBottomMiddleY-dyfw2,	15,
				 a/2-gs_frame_width, frameTopMiddleY-dyfw1,		15,
				-a/2+gs_frame_width, frameTopMiddleY+dyfw2,		15,
				-a/2+gs_frame_width, frameBottomMiddleY+dyfw1,	-1
			body 1
			material gs_frame_mat
			prism_ 5, -gs_frame_thk,
				-a/2+gs_frame_width, frameBottomMiddleY+dyfw1,	15,
				 a/2-gs_frame_width, frameBottomMiddleY-dyfw2,	15,
				 a/2-gs_frame_width, frameTopMiddleY-dyfw1,		15,
				-a/2+gs_frame_width, frameTopMiddleY+dyfw2,		15,
				-a/2+gs_frame_width, frameBottomMiddleY+dyfw1,	-1
			body 1
			del 1
		endif
	endif
endgroup

pen gs_frame_pen
sect_fill gs_fillFr, gs_penFr_bg, gs_penFr_fg, gs_frame_pen_cont

if lod3D = 2 then	! Detailed
	placegroup subgroup("frame","harcsi")
else
	placegroup "frame"
endif

end
