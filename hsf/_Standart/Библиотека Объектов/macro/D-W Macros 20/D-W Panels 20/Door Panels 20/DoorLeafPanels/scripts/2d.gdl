
pen gs_leaf_pen_2D

drawindex 40

LeafIsSimple50 = 0
if bSimpleLeafin50 & lod2D = DET2D_FS_DETAILED then
	lod2D = DET2D_FS_MIDDLE		!simple leaf in detailed symbol
	LeafIsSimple50 = 1
endif

if lod2D = DET2D_FS_SIMPLE | lod2D = DET2D_FS_MIDDLE | lod2D = DET2D_FS_SIMPLE_CHI then		! 1:200 / 1:100
	line2 -A/2, 0, A/2, 0
endif

if lod2D = DET2D_FS_DETAILED then		! 1:50
	gosub "FillInLeaf"
endif


! --- Chinese -----------------------------------------------------------------
if lod2D = DET2D_FS_DETAILED_CHI then
	gosub "FillInLeaf"
endif

! CHI SPEC
if bSlidingOpLine2D then
	opLine2DDist = 0.03

	add2 0, opLine2DDist
	mul2 -1, 1

	if lod2D <> DET2D_FS_SIMPLE and lod2D <> DET2D_FS_SIMPLE_CHI then
		poly2_b 4, 1+32, gs_penLeaf_fg, gs_penLeaf_bg,
			0.11, 0, 1,
			0.02, 0.02, 1,
			0.02, 0, 1,
			0.11, 0, 1
		line2 	-0.11, 0, 0.02, 0
	endif

	del 2
endif

end


! ------------------------------------------------------------------------------
"FillInLeaf":
! ------------------------------------------------------------------------------

fill gs_fillLeaf

!	add2 0, leafOffsetForPivot
	if bShowAirtightPanel then 		!!! Chinese additional symbols
		fill gs_fillAirtight
		poly2_b 4, 1+2+4+32, gs_penAirtight_fg, gs_penAirtight_bg,
			 A/2,	0, 1+32,
			-A/2, 0, 1-bHideInnerContour+32,
			-A/2, -airtightWidth,	1+32,
			 A/2,	-airtightWidth, 1+32
		fill gs_fillLeaf
		poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
			 A/2,	-airtightWidth, 1+32,
			-A/2, -airtightWidth, 1-bHideInnerContour+32,
			-A/2, -B,	1+32,
			 A/2,	-B, 1+32
	else
		gosub "leaf_2D"

		if bShowGlassPanelOnSymbol then		!!! Chinese additional symbols
			glassdiv_n=int(A/glassPanelWidth)
			glassdiv_w=A/glassdiv_n

			add2 A/2, 0
			line_property 2
			for gls=1 to glassdiv_n-1
				line2 -glassdiv_w, 0, -glassdiv_w, -B
				add2 -glassdiv_w, 0
			next gls
			del glassdiv_n
			line_property 0
		endif
	endif

	if bShowHandleOnSymbol and bShowHandleonActualLeaf then	!!! Chinese additional symbols
		add2 A/2 + hand2DWidth/2 - hand2DDist, 0
		for hon=1 to 2
			fill gs_fillLeaf
			poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
				0,			 0,			1+32,
				-hand2DWidth, 0,			1+32,
				-hand2DWidth, hand2DThk,	1+32,
				0,			 hand2DThk, 1+32
			mul2 1, -1
			add2 0, B
		next hon
		del 4+1
	endif


!	del 1

return


! ------------------------------------------------------------------------------
"leaf_2D":
! ------------------------------------------------------------------------------
if bEnableShowGlassPanel | _bSimplePanel then !_bSimplePanel is used, when the panel would be too narrow for the style, we use simple panel instead
	gosub "leafWithOutGlass"
else
	if  iDoorPanelType = 15 |\
		iDoorPanelType = 17 |\
		iDoorPanelType = 18 |\
		iDoorPanelType = 19 |\
		iDoorPanelType = 20 |\
		iDoorPanelType = 21 |\
		iDoorPanelType = 39 |\
		iDoorPanelType = 40 |\
		iDoorPanelType = 45 |\
		iDoorPanelType = 46 |\
		iDoorPanelType = 47 |\
		iDoorPanelType = 48 |\
		iDoorPanelType = 54 |\
		iDoorPanelType = 64 |\
		iDoorPanelType = 75 then

		pen gs_leaf_pen_2D

		gosub "set_frame_width"
		gosub "leafCutPart"
		gosub "leafUncutPart"

	else
		if	iDoorPanelType = 36 |\
			iDoorPanelType = 37 |\
			iDoorPanelType = 38 |\
			iDoorPanelType = 39 |\
			iDoorPanelType = 40 then

			gosub "leafWithGlassStorefront"

		else

			gosub "leafWithOutGlass"

		endif
	endif
endif

return

! ------------------------------------------------------------------------------
"set_frame_width":
! ------------------------------------------------------------------------------
	if iDoorPanelType = 45 then
		_left_frame		= gs_fw_side_left
		_right_frame	= gs_fw_side_right
	else
		if 	iDoorPanelType = 46 |\
			iDoorPanelType = 47 |\
			iDoorPanelType = 48 |\
			iDoorPanelType = 54 then

			_left_frame		= gs_fw_side
			_right_frame	= gs_fw_side
		else

			if	iDoorPanelType = 39 |\
				iDoorPanelType = 40 then
				if bStorefrontClassic & (iDoorPanelType = 39) then
					_left_frame		= gs_fw_side
					_right_frame	= gs_fw_side
				else
					_left_frame		= 0.10
					_right_frame	= 0.10
				endif
			else
				_left_frame		= 0.08
				_right_frame	= 0.08
			endif
		endif
	endif
	if bHideInnerContour then
		_left_frame	= 0
	endif
return


! ------------------------------------------------------------------------------
"leafCutPart":
! ------------------------------------------------------------------------------
	fill gs_fillLeaf

	poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
		A/2,				 0, 1+32,
		A/2-_right_frame,	 0, 1-bHideInnerContour+32,
		A/2-_right_frame,	-B,	1+32,
		A/2,				-B, 1+32

	poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
		-A/2,			 	 0, 1+32,
		-A/2+_left_frame,	 0, 1-bHideInnerContour+32,
		-A/2+_left_frame,	-B,	1+32,
		-A/2,				-B, 1-bHideInnerContour+32

return

! ------------------------------------------------------------------------------
"leafUncutPart":
! ------------------------------------------------------------------------------
	if iSymbolGlassType	= SYMBGLASS_DOUBLEGLASS then
		__glass_pos = (B - gs_glass_thk) / 2
	else
		__glass_pos = B / 2
	endif

	! ----- Uncut Frame -----
	fill gs_FillSdl
	for i=1 to 2
		poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
			 A/2-_right_frame,	 0,				1,
			-A/2+_left_frame,	 0,				1-bHideInnerContour,
			-A/2+_left_frame,	-__glass_pos,	1,
			 A/2-_right_frame,	-__glass_pos,	1

		add2 0, -B+__glass_pos
	next i
	del 2

	! ----- Glass -----
	if iSymbolGlassType	= SYMBGLASS_DOUBLEGLASS then
		pen gs_glass_pen_cont
		fill gs_gsect
		poly2_b 4, 1+2+4+32,gs_penGlass_fg, gs_penGlass_bg,
			A/2-_right_frame,	-__glass_pos, 1+32,
			-A/2+_left_frame,	-__glass_pos, 1-bHideInnerContour+32,
			-A/2+_left_frame,	-B+__glass_pos,	1+32,
			A/2-_right_frame,	-B+__glass_pos, 1+32
	else
		pen gs_leaf_pen_2D
		line_property 2
		line2 A/2-_right_frame, -B/2, -A/2+_left_frame, -B/2
		line_property 0
	endif
return

! ------------------------------------------------------------------------------
"leafWithGlassStorefront":
! ------------------------------------------------------------------------------
	if iSymbolGlassType	= SYMBGLASS_DOUBLEGLASS then
		__glass_pos = (B - gs_glass_thk) / 2
	else
		__glass_pos = B / 2
	endif

	! ----- Frame -----
	fill gs_FillSdl
	for i=1 to 2
		if iDoorPanelType = 38 then
			poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
				-A/2+0.2,	 0, 1,
				-A/2,		 0, 1-bHideInnerContour,
				-A/2,		-__glass_pos,	1,
				-A/2+0.2,	-__glass_pos, 1
		else
			poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
				 A/2,	 0, 1,
				-A/2,	 0, 1-bHideInnerContour,
				-A/2,	-__glass_pos,	1,
				 A/2,	-__glass_pos, 1
		endif
		add2 0, -B+__glass_pos
	next i
	del 2

	! ----- Glass -----
	if iSymbolGlassType	= SYMBGLASS_DOUBLEGLASS then
		pen gs_glass_pen_cont
		fill gs_gsect
		poly2_b 4, 1+2+4+32, gs_penGlass_fg, gs_penGlass_bg,
			A/2,	-__glass_pos, 1+32,
			-A/2,	-__glass_pos, 1-bHideInnerContour+32,
			-A/2,	-B+__glass_pos,	1+32,
			A/2,	-B+__glass_pos, 1+32
	else
		pen gs_leaf_pen_2D
		line_property 2
		line2 A/2, -__glass_pos, -A/2, -__glass_pos
		line_property 0
	endif
return

! ------------------------------------------------------------------------------
"leafWithOutGlass":
! ------------------------------------------------------------------------------
	fill gs_fillLeaf
	poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
		 A/2,	0, 1+32,
		-A/2, 0, 1-bHideInnerContour+32,
		-A/2, -B,	1+32,
		 A/2,	-B, 1+32
return
