values "gs_doorcode_" "Gar1", "Gar2", "Gar3", "Gar4", "HSD", "HSRD"


! =============================================================================
! Size Limitations
! =============================================================================

minDoorLeafWidth	= 0.2 + gs_frame_width_left + gs_frame_width_right
minDoorLeafHeight	= 0.4 + gs_frame_width_upper

minUnitWidth		= minDoorLeafWidth
minUnitHeight		= minDoorLeafHeight


! =============================================================================
! GS Door Functions
! =============================================================================

if gs_garage | gs_highspeedgar then
	gs_sunShade_typeDisableByObject	= 2+4+8+16+32+64+128+256
else
	gs_sunShade_typeDisableByObject	= 0
endif

call "GS Door Functions",
	parameters all	iWindowShape				= iWindowShape,
					bEnableParapetInset			= bEnableParapetInset,
					bEnableMasonryArch			= bEnableMasonryArch,
					bEnableSill					= bEnableSill,
					bEnableBoard				= bEnableBoard,
					bEnableFaschen				= bEnableFaschen,
					bEnableThreshold			= bEnableThreshold,
					bEnableCasing				= bEnableCasing,
					bEnableShutter				= bEnableShutter,
					bEnableCustomCornerLeft		= bEnableCustomCornerLeft,
					bEnableCustomCornerRight	= bEnableCustomCornerRight,
					bEnableVentilationProfile	= bEnableVentilationProfile,
					gs_pivotdoor				= 0,
					gs_garage					= gs_garage,
					gs_sectgar 					= gs_sectgar,
					gs_highspeedgar				= gs_highspeedgar,
					minUnitWidth				= minUnitWidth,
					minUnitHeight				= minUnitHeight,
					gs_sunShade_typeDisableByObject	= gs_sunShade_typeDisableByObject,
					b2ColumnsOnOpLineUI			= b2ColumnsOnOpLineUI,
					bHideOplineStyle			= bHideOplineStyle,
					bLockOplineStyle			= bLockOplineStyle,
					bDisable2DOverride			= bDisable2DOverride,
					bDisable3DOverride			= bDisable3DOverride,
					bDisableSwingType			= bDisableSwingType,
					bDisableSwingLineType 		= bDisableSwingLineType,
					iOpeningParamType			= iOpeningParamType,
					bDoorEmptyPanel				= bDoorEmptyPanel


! =============================================================================
! Sidelight Widths for Schedules
! =============================================================================

gs_sidelight_width_left		= 0
gs_sidelight_width_right	= 0

parameters	gs_sidelight_width_left		= gs_sidelight_width_left,
			gs_sidelight_width_right	= gs_sidelight_width_right


! =============================================================================
! Door Type
! =============================================================================

if gs_garage then

	if gs_sectgar then

		! ------------------------------------------------------------------------------
		! Panel Types for Sectional Garage Doors
		! ------------------------------------------------------------------------------

		dim stDoorTypes[5]
			stDoorTypes[1]	= `Стиль 1`
			stDoorTypes[2]	= `Стиль 2`
			stDoorTypes[3]	= `Стиль 3`
			stDoorTypes[4]	= `Специальное Полотно Двери`
			stDoorTypes[5]	= `Пусто`

		dim iDoorTypes[5]
			iDoorTypes[1]	= 1
			iDoorTypes[2]	= 2
			iDoorTypes[3]	= 3
			iDoorTypes[4]	= 31
			iDoorTypes[5]	= 0
	else

		! ------------------------------------------------------------------------------
		! Panel Types for Garage Doors
		! ------------------------------------------------------------------------------

		dim stDoorTypes[11]
			stDoorTypes[1]	= `Стиль 1`
			stDoorTypes[2]	= `Стиль 2`
			stDoorTypes[3]	= `Стиль 3`
			stDoorTypes[4]	= `Стиль 4`
			stDoorTypes[5]	= `Стиль 5`
			stDoorTypes[6]	= `Стиль 6`
			stDoorTypes[7]	= `Стиль 7`
			stDoorTypes[8]	= `Стиль 8`
			stDoorTypes[9]	= `Стиль 9`
			stDoorTypes[10]	= `Специальное Полотно Двери`
			stDoorTypes[11]	= `Пусто`

		dim iDoorTypes[11]
			iDoorTypes[1]	= 1
			iDoorTypes[2]	= 2
			iDoorTypes[3]	= 3
			iDoorTypes[4]	= 4
			iDoorTypes[5]	= 5
			iDoorTypes[6]	= 10
			iDoorTypes[7]	= 11
			iDoorTypes[8]	= 12
			iDoorTypes[9]	= 14
			iDoorTypes[10]	= 31
			iDoorTypes[11]	= 0
	endif

	values{2} "iDoorPanelType" iDoorTypes, stDoorTypes
endif


! =============================================================================
! Frame Style
! =============================================================================

if gs_garage | gs_highspeedgar then
	gs_frame_style = gs_stDoorFrame[1]
	gs_frame_style_m = 1
	parameters	gs_frame_style = gs_frame_style,
				gs_frame_style_m = gs_frame_style_m

	lock "gs_frame_style"
	hideparameter "gs_frame_style"
endif


! =============================================================================
! Ventilation Profile
! =============================================================================

if bEnableVentilationProfile then
	call "Ventilation_profile" parameters	isWindow						= 0,
											iWindowType						= 0,
											bVentilationProfile				= bVentilationProfile,
											iVentilationProfileType			= iVentilationProfileType,
											gs_VentilationProfileTypeEnable	= gs_VentilationProfileTypeEnable
endif


! ==============================================================================
! Uniform Surfaces
! ==============================================================================

dim	surfaceParNames[8]						:	dim	surfaceParameters[8]
	surfaceParNames[1]	= "gs_frame_mat"	:		surfaceParameters[1]	= gs_frame_mat
	surfaceParNames[2]	= "gs_frame_mat2"	:		surfaceParameters[2]	= gs_frame_mat2
	surfaceParNames[3]	= "gs_leaf_mat"		:		surfaceParameters[3]	= gs_leaf_mat
	surfaceParNames[4]	= "gs_leaf_matin"	:		surfaceParameters[4]	= gs_leaf_matin
	surfaceParNames[5]	= "gs_leaf_mat2"	:		surfaceParameters[5]	= gs_leaf_mat2
	surfaceParNames[6]	= "gs_leaf_mat3"	:		surfaceParameters[6]	= gs_leaf_mat3
	surfaceParNames[7]	= "gs_grill_mat"	:		surfaceParameters[7]	= gs_grill_mat
	surfaceParNames[8]	= "gs_grill_matin"	:		surfaceParameters[8]	= gs_grill_matin

_lastModSurfaceParameter = gs_frame_mat

if stored_material[1][1] <> -9999 then
	for i = 1 to vardim1(surfaceParNames)
		if GLOB_MODPAR_NAME = surfaceParNames[i] then
			_lastModSurfaceParameter	= surfaceParameters[i]
			stored_material[1][i]	= surfaceParameters[i]
			stored_material[2][i]	= 1

			for j = 1 to i-1
				stored_material[2][j]	= 0
				parameters stored_material[2][j]	= stored_material[2][j]
			next j

			for k = i+1 to vardim1(surfaceParNames)
				stored_material[2][k]	= 0
				parameters stored_material[2][k]	= stored_material[2][k]
			next k
		endif

		parameters	stored_material[1][i]	= stored_material[1][i],
					stored_material[2][i]	= stored_material[2][i]

		if stored_material[2][i] = 1 then
			_lastModSurfaceParameter = stored_material[1][i]
		endif
	next i
else
	gosub "preloadSurfaces"
endif

if bLinkSurfacesFrameSash then
	parameters	gs_frame_mat		= _lastModSurfaceParameter,
				gs_frame_mat2		= _lastModSurfaceParameter,
				gs_leaf_mat			= _lastModSurfaceParameter,
				gs_leaf_matin		= _lastModSurfaceParameter,
				gs_leaf_mat2		= _lastModSurfaceParameter,
				gs_leaf_mat3		= _lastModSurfaceParameter,
				gs_grill_mat		= _lastModSurfaceParameter,
				gs_grill_matin		= _lastModSurfaceParameter
else
	if GLOB_MODPAR_NAME = "bLinkSurfacesFrameSash" then
		parameters	gs_frame_mat		= stored_material[1][1],
					gs_frame_mat2		= stored_material[1][2],
					gs_leaf_mat			= stored_material[1][3],
					gs_leaf_matin		= stored_material[1][4],
					gs_leaf_mat2		= stored_material[1][5],
					gs_leaf_mat3		= stored_material[1][6],
					gs_grill_mat		= stored_material[1][7],
					gs_grill_matin		= stored_material[1][8]
	else
		gosub "preloadSurfaces"
	endif
endif


! =============================================================================
! Orientation
! =============================================================================

values{2} "gs_list_orientationDef_m"	0, `Автоматически`,
										1, `Специальный`

if gs_list_orientationDef_m = 0 then
	ac_OpeningSide = ""
	if gs_leaf_optype_m = LEAF_GAR_2L_SIDE_HUNG	then
		if SYMB_MIRRORED then
			ac_OpeningSide = stOpeningSide[2]
		else
			ac_OpeningSide = stOpeningSide[1]
		endif
	endif
	parameters ac_OpeningSide = ac_OpeningSide

	lock "ac_OpeningSide"
	hideparameter "ac_OpeningSide"
endif


! =============================================================================
! Door Leaf Widths for Listing
! =============================================================================

if	(gs_leaf_optype_m = LEAF_GAR_2L_SIDE_HUNG) then

	_formatStr = ""
	rrr = REQUEST ("Window_door_dimension", "", _formatStr)

	gs_list_doorleafwidths = STR{2}(_formatStr, ((gs_rttl/100) * ac_leaf_width)) + " / " + STR{2}(_formatStr, ((1 - (gs_rttl/100)) * ac_leaf_width))

	parameters gs_list_doorleafwidths = gs_list_doorleafwidths
endif


! =============================================================================
! IFC Parameters
! =============================================================================

ifc_optype = DOOR_OP_USER_DEFINED

if iDoorPanelType = 0 then
	ifc_optype = DOOR_OP_NOT_DEFINED
else
	if gs_leaf_optype_m = LEAF_GAR_2L_SIDE_HUNG  then
		ifc_optype = DOOR_OP_DOUBLE_DOOR_SINGLE_SWING
	endif

	if	gs_leaf_optype_m = LEAF_GAR_1L_UP_AND_OVER |\
		gs_leaf_optype_m = LEAF_GAR_1L_UP_SLIDING |\
		gs_leaf_optype_m = LEAF_GAR_1L_SECTIONAL |\
		gs_leaf_optype_m = LEAF_GAR_1L_ROLLUP then

		ifc_optype = DOOR_OP_ROLLING_UP
	endif
endif

parameters ifc_optype = ifc_optype

call "IFC_optype_door" parameters ifc_optype = ifc_optype

lock "ifc_optypestr"

if gs_frame_wthk then
	ifc_LiningDepth = 0
else
	ifc_LiningDepth = WIDO_FRAME_THICKNESS
endif
parameters ifc_LiningDepth = ifc_LiningDepth

ifc_LiningThickness	= gs_frame_width
ifc_LiningOffset	= WIDO_FRAME_THICKNESS * WIDO_REVEAL_SIDE
parameters	ifc_LiningThickness	= ifc_LiningThickness,
			ifc_LiningOffset	= ifc_LiningOffset

ifc_TransomOffset		= 0
ifc_TransomThickness 	= 0
parameters	ifc_TransomOffset		= ifc_TransomOffset,
			ifc_TransomThickness	= ifc_TransomThickness

dim ifc_DoorPanel[][4]
if ifc_optype = DOOR_OP_NOT_DEFINED | ifc_optype = DOOR_OP_USER_DEFINED then
	ifc_DoorPanel[1][1] = 0
	ifc_DoorPanel[1][2] = 0
	ifc_DoorPanel[1][3] = 0		! NOTDEFINED
	ifc_DoorPanel[1][4] = 0		! NOTDEFINED
else
	iDoorPanelOperation = 0		! NOTDEFINED
	if gs_leaf_optype_m = LEAF_GAR_2L_SIDE_HUNG then
		iDoorPanelOperation = 1		! SWINGING
	endif
	if	gs_leaf_optype_m = LEAF_GAR_1L_UP_AND_OVER |\
		gs_leaf_optype_m = LEAF_GAR_1L_UP_SLIDING |\
		gs_leaf_optype_m = LEAF_GAR_1L_SECTIONAL |\
		gs_leaf_optype_m = LEAF_GAR_1L_ROLLUP then

		iDoorPanelOperation = 6		! ROLLINGUP
	endif
	if gs_leaf_optype_m = LEAF_GAR_2L_ROLLSIDE then
		iDoorPanelOperation = 7		! USERDEFINED
	endif


	if	gs_leaf_optype_m = LEAF_GAR_1L_UP_AND_OVER |\
		gs_leaf_optype_m = LEAF_GAR_1L_UP_SLIDING |\
		gs_leaf_optype_m = LEAF_GAR_1L_SECTIONAL |\
		gs_leaf_optype_m = LEAF_GAR_1L_ROLLUP then

		ifc_DoorPanel[1][1] = gs_leaf_thk
		ifc_DoorPanel[1][2] = ac_leaf_width/ac_egress_width
		ifc_DoorPanel[1][3] = iDoorPanelOperation
		ifc_DoorPanel[1][4] = 2		! MIDDLE
	endif

	if	gs_leaf_optype_m = LEAF_GAR_2L_SIDE_HUNG |\
		gs_leaf_optype_m = LEAF_GAR_2L_ROLLSIDE then

		ifc_DoorPanel[1][1] = gs_leaf_thk
		ifc_DoorPanel[1][2] = lal/ac_egress_width
		ifc_DoorPanel[1][3] = iDoorPanelOperation
		ifc_DoorPanel[1][4] = 1		! LEFT

		ifc_DoorPanel[2][1] = gs_leaf_thk
		ifc_DoorPanel[2][2] = lar/ac_egress_width
		ifc_DoorPanel[2][3] = iDoorPanelOperation
		ifc_DoorPanel[2][4] = 3		! RIGHT
	endif
endif
parameters ifc_DoorPanel = ifc_DoorPanel


! =============================================================================
! FM Types
! =============================================================================

call "FM_types" parameters all


! =============================================================================
! Door Specific Parameters
! =============================================================================

values{2} "gs_SashText_typ_num" 	MITERED_JOINT, `Угловое Соединение`,
									BUTT_JOINT, `Соединение Встык`


if (gs_sectgar | gs_highspeedgar) & gs_leaf_nominal then
	gs_egress_nominal = 1
	parameters gs_egress_nominal = gs_egress_nominal
endif


glassarea_counter = 0
if gs_garage then
	aaa = ac_unit_width-(gs_frame_width_left+gs_frame_width_right)+2*gs_rebate_width
	bbb = ac_unit_height-gs_frame_width_upper+gs_rebate_width

	if gs_doorcode = "Gar3" then
		aaa = lar
		call "dp_garage_panels" parameters all a			= aaa,
								b							= bbb,
								iDoorPanelType				= iDoorPanelType, !5, 14
								bEnableKickBoard			= bEnableKickBoard,
								bKickBoardOut				= bKickBoardOut,
								bKickBoardIn				= bKickBoardIn,
								kickBoardHeight				= kickBoardHeight,
								matKickBoardOut				= matKickBoardOut,
								matKickBoardIn				= matKickBoardIn,
								gs_hor_pnum					= gs_hor_pnum,
								gs_ver_pnum					= gs_ver_pnum,
								returned_parameters	PanelGlassArea

		glassarea_counter = glassarea_counter + PanelGlassArea

		aaa = lal
	endif

	call "dp_garage_panels" parameters all a			= aaa,
							b							= bbb,
							iDoorPanelType				= iDoorPanelType, !5, 14
							bEnableKickBoard			= bEnableKickBoard,
							bKickBoardOut				= bKickBoardOut,
							bKickBoardIn				= bKickBoardIn,
							kickBoardHeight				= kickBoardHeight,
							matKickBoardOut				= matKickBoardOut,
							matKickBoardIn				= matKickBoardIn,
							gs_hor_pnum					= gs_hor_pnum,
							gs_ver_pnum					= gs_ver_pnum,
							returned_parameters	PanelGlassArea

	! =============================================================================
	! Total Glazed Area Listing Parameters
	! =============================================================================
	if NOT(gs_list_glassarea_override) then
		if GLOB_MODPAR_NAME = "gs_list_glassarea" then
			gs_list_glassarea_tmp = gs_list_glassarea
			parameters gs_list_glassarea_tmp = gs_list_glassarea_tmp
		else
			gs_list_glassarea = gs_list_glassarea_tmp
			parameters gs_list_glassarea = gs_list_glassarea
		endif

		glassarea_counter = glassarea_counter + PanelGlassArea
		parameters gs_list_glassarea = glassarea_counter
		lock "gs_list_glassarea"
	else
		if GLOB_MODPAR_NAME = "gs_list_glassarea" then
			gs_list_glassarea_tmp = gs_list_glassarea
			parameters gs_list_glassarea_tmp = gs_list_glassarea_tmp
		else
			gs_list_glassarea = gs_list_glassarea_tmp
			parameters gs_list_glassarea = gs_list_glassarea
		endif
	endif
endif

if gs_sectgar then
	gs_tolerance_left	= 0
	gs_tolerance_right	= 0
	gs_tolerance_upper	= 0
	gs_stack_left		= 0
	gs_stack_right		= 0
	gs_stack_top		= 0

	call "sectionalGarageDoor" parameters all a	= ac_leaf_width,
								b				= ac_leaf_height,
								gs_frwt			= gs_frame_width_upper,
								iDoorPanelType 	= iDoorPanelType,
								bEnableKickBoard	= bEnableKickBoard,
								bKickBoardOut		= bKickBoardOut,
								bKickBoardIn		= bKickBoardIn,
								kickBoardHeight		= kickBoardHeight,
								matKickBoardOut		= matKickBoardOut,
								matKickBoardIn		= matKickBoardIn

	parameters	gs_tolerance_left	= gs_tolerance_left,
				gs_tolerance_right	= gs_tolerance_right,
				gs_tolerance_upper	= gs_tolerance_upper,
				gs_stack_left		= gs_stack_left,
				gs_stack_right		= gs_stack_right,
				gs_stack_top		= gs_stack_top

	if iDoorPanelType = 3 then
		lock "gs_passdoorWidth"
		hideparameter"gs_passdoorWidth"
	endif

	if iDoorPanelType = 1 | iDoorPanelType = 2 then
		if gs_bGlazedDoor then
			lock "gs_passdoorWidth"
			hideparameter"gs_passdoorWidth"
		endif
	endif
endif

if gs_highspeedgar then
	gs_tolerance_left	= 0
	gs_tolerance_right	= 0
	gs_tolerance_upper	= 0
	gs_stack_left		= 0
	gs_stack_right		= 0
	gs_stack_top		= 0

	call "d_highspeed" parameters all gs_glass_section_width	= gs_glass_section_width,
										gs_glass_section 		= gs_bGlazedDoor,
										ac_egress_width			= ac_egress_width

	parameters	gs_tolerance_left	= gs_tolerance_left,
				gs_tolerance_right	= gs_tolerance_right,
				gs_tolerance_upper	= gs_tolerance_upper,
				gs_stack_left		= gs_stack_left,
				gs_stack_right		= gs_stack_right,
				gs_stack_top		= gs_stack_top

	if iDoorType = 13 then		! High-Speed Door
		parameters	gs_unit_extra_left		= -gs_frame_width_out + gs_frame_side_overhang,
					gs_unit_extra_right		= -gs_frame_width_out + gs_frame_side_overhang,
					gs_unit_extra_upper		= -0.33 + gs_frame_side_overhang,
					gs_frame_width_upper 	= 0.33

		values "gs_open2D" range[0, ac_egress_width/2]
		values "gs_open3D" range[0, ac_egress_width/2]
		values "gs_frame_width_out" range(0,)
		values "gs_frame_width" range(gs_frame_width_out,)
	endif
	if iDoorType = 14 then		! High-Speed Roll Up Door
		parameters	gs_unit_extra_left		= -gs_frame_width + gs_frame_side_overhang,
					gs_unit_extra_right		= -gs_frame_width + gs_frame_side_overhang,
					gs_unit_extra_upper		= -0.32 + gs_frame_side_overhang,
					gs_frame_width_upper 	= 0.32,
					gs_cust_on				= 0

		values "gs_open3D" range[0, b]
		values "gs_frame_width" range(0,)
	endif

	gs_frame_width_left		= gs_frame_width
	gs_frame_width_right	= gs_frame_width
	gs_frame_wthk			= 0
	parameters	gs_frame_width_left 	= gs_frame_width_left,
				gs_frame_width_right	= gs_frame_width_right,
				gs_frame_wthk			= gs_frame_wthk

	if gs_cust_on then
		lock	"gs_bGlazedDoor",
				"gs_glass_section_width",
				"gs_glass_section_elev",
				"gs_glass_pen",
				"gs_glass_mat",
				"gs_bottom_part_mat"
		hideparameter	"gs_bGlazedDoor",
						"gs_glass_section_width",
						"gs_glass_section_elev",
						"gs_glass_pen",
						"gs_glass_mat",
						"gs_bottom_part_mat"
	else
		lock "gs_cust_panel"
		hideparameter "gs_cust_panel"

		if not(gs_bGlazedDoor) then
			lock	"gs_glass_section_width",
					"gs_glass_section_elev",
					"gs_glass_pen",
					"gs_glass_mat"
			hideparameter	"gs_glass_section_width",
							"gs_glass_section_elev",
							"gs_glass_pen",
							"gs_glass_mat"
		else
			values "gs_glass_section_width" range(0, )
			values "gs_glass_section_elev" range[0.10, )
		endif
	endif
endif

! =============================================================================

values "gs_frame_thk" range(0,)


!******** Parameter Lock ********

if not(gs_highspeedgar) & not(bINTHandles) then
	if gs_handle_type_m =  0 then lock "gs_han_height", "gs_han_pos"
	if gs_handle_typ_m2 =  0 then lock "gs_han_height2", "gs_han_pos2"

	!*** NOT custom handle
	if gs_handle_type_m<>99 then
		lock "gs_cust_handle"
		hideparameter "gs_cust_handle"
	endif
	if gs_handle_typ_m2<>99 then
		lock "gs_cust_handle2"
		hideparameter "gs_cust_handle2"
	endif
endif

if gs_segmented = 1 then
	lock "gs_left_oversize", "gs_right_oversize",
		"gs_upper_oversize", "gs_lower_oversize"
endif

if gs_doorcode = "Gar1" | gs_doorcode = "Gar4" then
	if not(gs_opline_2D) then lock "gs_opline_type_2D"
endif

!********** Lock END ************


!******** max. / min. of door panels widths proportion in double doors

if gs_door_num_m = 2 then
	if gs_rttl<20 then parameters gs_rttl=20
	if gs_rttl>80 then parameters gs_rttl=80
endif


!************* leaf frame thickness

if gs_frame_wthk then
	parameters gs_fthk = WALL_THICKNESS
else
	parameters gs_fthk = gs_frame_thk
endif


!**************** Swedish list parameter control **********************
if gs_SWE_listParams_contr > 0 then
	call "SWE_ListParameters_control" parameters gs_SWE_listParams_contr = gs_SWE_listParams_contr,
			swe_list_d1				= swe_list_d1
endif

! =============================================================================
! Japanese list parameter control
! =============================================================================

if gs_JPN_listParams_contr then
	call "ListValuesJPN" parameters all gs_JPNObjectType = 1,		!COMMPARS_INTDOORS
			gs_is_p2	= 0,
			gs_is_p6	= 0
endif


! =============================================================================
! CHI
! =============================================================================


if (gs_doorcode = "Gar3") then
	if not(bEnableShowHandle) | (gs_handle_type_m = 0 and gs_handle_typ_m2 = 0) | \
		iDWDet2D = DETLEVEL_DW2D_100 | iDWDet2D = DETLEVEL_DW2D_200 | \
		iDWDet2D = DETLEVEL_DW2D_OFF | iDWDet2D = DETLEVEL_DW2D_CHISMPL  then
		lock "bShowHandleOnSymbol"
		hideparameter "bShowHandleOnSymbol"
	endif
endif
if (gs_doorcode = "Gar1") | (gs_doorcode = "Gar3") then
	if not(bEnableShowAirtightPanel) | iDWDet2D = DETLEVEL_DW2D_100 | \
		iDWDet2D = DETLEVEL_DW2D_200 | iDWDet2D = DETLEVEL_DW2D_OFF | iDWDet2D = DETLEVEL_DW2D_CHISMPL then
		lock "bShowAirtightPanel"
		hideparameter "bShowAirtightPanel"
	endif
	if not(bEnableShowAirtightPanel & bShowAirtightPanel) | iDWDet2D = DETLEVEL_DW2D_100 | \
		iDWDet2D = DETLEVEL_DW2D_200 | iDWDet2D = DETLEVEL_DW2D_OFF | iDWDet2D = DETLEVEL_DW2D_CHISMPL then
		lock "gs_fillAirtight", "gs_penAirtight_fg", "gs_penAirtight_bg"
		hideparameter "gs_fillAirtight", "gs_penAirtight_fg", "gs_penAirtight_bg"
	endif
endif
if not(gs_highspeedgar) then
	if not(bEnableShowGlassPanel) | iDWDet2D = DETLEVEL_DW2D_100 | \
		iDWDet2D = DETLEVEL_DW2D_200 | iDWDet2D = DETLEVEL_DW2D_OFF | iDWDet2D = DETLEVEL_DW2D_CHISMPL then
		lock "bShowGlassPanelOnSymbol"
		hideparameter "bShowGlassPanelOnSymbol"
	endif
endif
if GLOB_MODPAR_NAME="bShowAirtightPanel" and bShowAirtightPanel then
	bShowGlassPanelOnSymbol=0
	parameters bShowGlassPanelOnSymbol=0
endif
if GLOB_MODPAR_NAME="bShowGlassPanelOnSymbol" and bShowGlassPanelOnSymbol then
	bShowAirtightPanel=0
	parameters bShowAirtightPanel=0
endif

! =============================================================================
! 3D Detail Level
! =============================================================================


if gs_3DDetEnable =0 then

    values{2} "iDWDet3D" 	DETLEVEL_DW3D_DETAILED,		`Детальный`,
						    DETLEVEL_DW3D_SIMPLE,		`Простой`,
						    DETLEVEL_DW3D_OFF,			`Откл.`

else
	dim n3DScaleValueMask[]
	dim n3DScaleValueMask_m[]

	i=1
	if bittest( gs_3DDetEnable, 0) then                                              ! Scale Sensitive in Value list, 1
				n3DScaleValueMask[i] = stDetail[1]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_SCALESENS
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 1) then                                              ! Detailed in Value list,2
				n3DScaleValueMask[i] = stDetail[2]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_DETAILED
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 2) then                                              ! Simple in Value list,4
				n3DScaleValueMask[i] = stDetail[3]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_SIMPLE
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 3) then                                              ! Off in Value list,8
				n3DScaleValueMask[i] = stDetail[4]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_OFF
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 4) then                                              ! Chinese Simple in Value list,16
				n3DScaleValueMask[i] = stDetail[5]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_CHI_SIMPLE
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 5) then                                              ! Chinese Detailed in Value list,32
				n3DScaleValueMask[i] = stDetail[6]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_CHI_DETAILED
				i = i + 1
	endif

	values{2} "iDWDet3D" n3DScaleValueMask_m, n3DScaleValueMask
endif



!********************** Door Panel

if gs_garage then
	gosub "garage doors"
	goto 1000
endif

if gs_highspeedgar then
	goto 1000
endif


!*** H-V Grid

if gs_grill_typ_m = 2 then
	if gs_grid_w<=0 then  parameters gs_grid_w=0.0001
	if gs_grid_w>a-gs_frame_width_left-gs_frame_width_right-gs_panel_fw then  parameters gs_grid_w=a-gs_frame_width_left-gs_frame_width_right-gs_panel_fw
		if gs_grill_pos_m=3 then
			if gs_grid_thk<=0 then  parameters gs_grid_thk=0.0001
			if gs_grid_thk>gs_leaf_thk then  parameters gs_grid_thk=gs_leaf_thk
		else
			if gs_grid_thk<=0 then  parameters gs_grid_thk=0.0001
			if gs_grid_thk>(gs_leaf_thk-gs_glass_thk)/2 then  parameters gs_grid_thk=(gs_leaf_thk-gs_glass_thk)/2
		endif
	if gs_ver_gnum<1 then  parameters gs_ver_gnum=1
	if gs_hor_gnum<1 then  parameters gs_hor_gnum=1
endif

!*** Prairie Style

if gs_grill_typ_m = 3 then
	if a<gs_grid_w*5+6"+gs_panel_fw+gs_frame_width_left+gs_frame_width_right then  parameters a=gs_grid_w*5+6"+gs_panel_fw+gs_frame_width_left+gs_frame_width_right+0.005"
	if b<gs_grid_w*8+6"+gs_panel_fw+gs_frame_width_left+gs_frame_width_right then  parameters b=gs_grid_w*8+6"+gs_panel_fw+gs_frame_width_left+gs_frame_width_right+0.005"
	if gs_grid_thk<=0 then  parameters gs_grid_thk=0.0001
	if gs_grid_thk>gs_leaf_thk then  parameters gs_grid_thk=gs_leaf_thk
endif


!********************* hide and lock door panel parameters ************************
!**********************************************************************************

!*** leaf material 2

if iDoorPanelType<>2 and iDoorPanelType<>3 and iDoorPanelType<>4 and iDoorPanelType<>8 and\
	 iDoorPanelType<>9 and iDoorPanelType<>10 and iDoorPanelType<>11 and\
	 iDoorPanelType<>13 and iDoorPanelType<>16 and iDoorPanelType<>22 and\
	 iDoorPanelType<>23 and iDoorPanelType<>24 and iDoorPanelType<>25 and\
	 iDoorPanelType<>26 and iDoorPanelType<>27 and iDoorPanelType<>28 then
	hideparameter "gs_leaf_mat2"
    lock "gs_leaf_mat2"
endif

!*** leaf material 3

if iDoorPanelType<>2 and iDoorPanelType<>8 and iDoorPanelType<>9 and\
	 iDoorPanelType<>10 and iDoorPanelType<>11 and iDoorPanelType<>28 then
	hideparameter "gs_leaf_mat3"
    lock "gs_leaf_mat3"
endif


!*** grid parameters

if gs_grill_typ_m <> 2 then
	hideparameter "gs_hor_gnum4","gs_ver_gnum4"
	lock "gs_hor_gnum4","gs_ver_gnum4"
endif

if iDoorPanelType<>29 and iDoorPanelType<>30 then
	hideparameter "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
		"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
	lock "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
		"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
else
	if gs_grill_typ_m = 1 then
		!*** No Grid
		hideparameter "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
			"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
		lock "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
			"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
	endif
	if gs_grill_typ_m = 3 then
		!*** Prairie Style
		hideparameter "gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
		lock "gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
	endif
endif


!************* leaf frame width settings

!*** frame internal width

if iDoorPanelType<2 or iDoorPanelType>4 then
	hideparameter "gs_int_fw"
    lock "gs_int_fw"
endif

!*** frame side width

if iDoorPanelType<>29 then
	hideparameter "gs_fw_side"
    lock "gs_fw_side"
endif

!*** frame lower/upper width

if iDoorPanelType<>29 and iDoorPanelType<>30 then
	hideparameter "gs_fw_lower", "gs_fw_upper"
    lock "gs_fw_lower", "gs_fw_upper"
endif

!*** not french panel

if iDoorPanelType<>30 then
	hideparameter "gs_fw_side_left", "gs_fw_side_right"
	lock "gs_fw_side_left", "gs_fw_side_right"
endif


!*** glass parameters

if iDoorPanelType=1 or iDoorPanelType=5 or iDoorPanelType=6 or iDoorPanelType=7 or iDoorPanelType=12 or \
	iDoorPanelType=8 or iDoorPanelType=9 or iDoorPanelType=10 or iDoorPanelType=11 or iDoorPanelType=28 then
	hideparameter "gs_glass_thk","gs_glass_pen", "gs_glass_mat"
	lock "gs_glass_thk","gs_glass_pen", "gs_glass_mat"
endif


!*** empty panel
if not(gs_highspeedgar) & iDoorPanelType=0 then
	hideparameter "gs_handle_type", "gs_han_height", "gs_han_pos"
	lock "gs_handle_type", "gs_han_height", "gs_han_pos"
	gs_handle_type_m = 0	! Force to lock and hide handle parameters
endif


!*** Empty or Custom
if iDoorPanelType=0 or iDoorPanelType=31 then
	hideparameter "gs_fw_side","gs_fw_lower","gs_fw_upper","gs_glass_thk",
	"gs_leaf_pen","gs_glass_pen","gs_leaf_mat",
	"gs_leaf_matin","gs_leaf_mat2","gs_leaf_mat3","gs_glass_mat","gs_grill_mat"
	lock "gs_fw_side","gs_fw_lower","gs_fw_upper","gs_glass_thk",
	"gs_leaf_pen","gs_glass_pen","gs_leaf_mat",
	"gs_leaf_matin","gs_leaf_mat2","gs_leaf_mat3","gs_glass_mat","gs_grill_mat"
endif


1000:


!*** bigger default resolution set for panel style 12

if GLOB_MODPAR_NAME="iDoorPanelType" and gs_door_typ_num = 12 then
	parameters res = 32
endif
if GLOB_MODPAR_NAME="iDoorPanelType" and gs_door_typ_num <> 12 then
	parameters res = 12
endif


if gs_frame_wthk then hideparameter "gs_frame_thk"
if gs_frame_style_m <> 2 then hideparameter "gs_ds_thk", "gs_ds_w"
if gs_frame_style_m <> 3 then hideparameter "gs_rebate_width"


!************************* 3D Handle Definition

if bINTHandles then
	if iDoorPanelType = 0 then	! Empty
		hideparameter "gs_handle_type", "gs_handle_type_m"
		lock "gs_handle_type", "gs_handle_type_m"
		gs_handle_type_m = 0	! Force to lock and hide handle parameters
	endif

	if gs_leaf_optype_m = LEAF_GAR_2L_SIDE_HUNG then
		iHandleCategoryEnable	= 1 + 2 + 4
	else
		! LEAF_GAR_1L_UP_AND_OVER
		! LEAF_GAR_1L_UP_SLIDING
		! LEAF_GAR_1L_SECTIONAL
		! LEAF_GAR_2L_ROLLSIDE
		! LEAF_GAR_1L_ROLLUP
		iHandleCategoryEnable	= 1 + 8 + 16
	endif

	call "DoorHandle" parameters A=A,
		bDifferentHandles		= 0,
		bSecondHandle			= 0,
		iHandleCategoryEnable	= iHandleCategoryEnable,
		bPassDoor				= gs_sectgar,
		bINTHandles				= 1,
		gs_handle_type			= gs_handle_type,
		gs_handle_type_m		= gs_handle_type_m,
		gs_handle_typ_in_m		= gs_handle_typ_in_m,
		gs_handle_typ_out_m		= gs_handle_typ_out_m
endif

if not(gs_highspeedgar) & not(bINTHandles) then
	if gs_handle_type_m = 0 or (iDoorPanelType+gs_door_typ_num=0) then
		 hideparameter "gs_handle_mat", "gs_handle_ac_mat", "gs_han_height", "gs_han_pos"
		 lock "gs_handle_mat", "gs_handle_ac_mat", "gs_han_height", "gs_han_pos"
	endif
endif


!*************************** RESOLUTION

if res<3 then parameters res=3


!*************************** PENS

if gs_pen_2D<1 then parameters gs_pen_2D=3
if gs_leaf_pen_2D<1 then parameters gs_leaf_pen_2D=3
if gs_frame_pen<1 then parameters gs_frame_pen=2
if gs_leaf_pen<1 then parameters gs_leaf_pen=2
if gs_glass_pen<1 then parameters gs_glass_pen=2


!************************** OPENING ANGLE IN 2D/3D

if gs_doorcode = "Gar1" or gs_doorcode = "Gar2" then values "gs_open3D" range [0, ac_egress_height]
if gs_doorcode <> "D1DA" and gs_doorcode <> "D2DA" then values "gs_open_3D" range [0, 180]
values "gs_open_2D" range [0, 180]


!************************** LIMITATIONs of PARAMETERs

if iDoorPanelType=3 then ax=3*gs_fw_side+4*gs_int_fw
if iDoorPanelType=4 then ax=4*gs_fw_side+6*gs_int_fw
if iDoorPanelType<>3 and iDoorPanelType<>4 then ax=3*gs_fw_side

if iDoorPanelType>=2 and iDoorPanelType<29 then
	by=7*gs_fw_side+12*gs_int_fw+0.1
else
	by=gs_fw_lower+gs_fw_upper
endif


! =============================================================================
! For DoorPanel AddOn
! =============================================================================

if iDoorPanelType = 31 then
	dim gs_pt[]
	call "Door_Panel_Collection" parameters returned_parameters gs_pt
	values "gs_cust_panel" gs_pt
else
	hideparameter "gs_cust_panel"
endif


! =============================================================================
! For CustomHandle AddOn
! =============================================================================

if gs_handle_type_m=99 then
	dim gs_pt_handle[]
	call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle
	values "gs_cust_handle" gs_pt_handle
endif
if gs_handle_typ_m2=99 then
	dim gs_pt_handle2[]
	call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle2
	values "gs_cust_handle2" gs_pt_handle2
endif
if gs_sectgar then
	if gs_handle_typ_in_m = 99 then
		dim gs_pt_handle_in[]
		call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle_in
		values "gs_cust_handle_in" gs_pt_handle_in
	endif
	if gs_handle_typ_out_m = 99 then
		dim gs_pt_handle_out[]
		call "DW_Hardware_Collection" parameters returned_parameters gs_pt_handle_out
		values "gs_cust_handle_out" gs_pt_handle_out
	endif
endif



!******************** french panel calculations ********************
!*******************************************************************

!*** panel width

fl_width = ac_leaf_width

if gs_door_num_m = 2 then
	fl_width = lar
	fl_width2 = lal
endif


!*** panel height

fl_height = ac_leaf_height


if iDoorPanelType = 30 or gs_door_typ_num = 30 then

	f_rench_h=0
	if GLOB_MODPAR_NAME="gs_fw_lower" then f_rench_h=1
	if GLOB_MODPAR_NAME="gs_fw_upper" then f_rench_h=1
	if GLOB_MODPAR_NAME="gs_hor_gnum" then f_rench_h=1
	if GLOB_MODPAR_NAME="iDoorPanelType" then f_rench_h=1
	if GLOB_MODPAR_NAME="B" then f_rench_h=1

	f_rench_v=0
	if GLOB_MODPAR_NAME="gs_fw_side_left" then f_rench_v=1
	if GLOB_MODPAR_NAME="gs_fw_side_right" then f_rench_v=1
	if GLOB_MODPAR_NAME="gs_ver_gnum" then f_rench_v=1
	if GLOB_MODPAR_NAME="iDoorPanelType" then f_rench_v=1
	if GLOB_MODPAR_NAME="A" then f_rench_v=1
	if GLOB_MODPAR_NAME="gs_rttl" then f_rench_v=1


	if gs_hor_gnum>0 then
	if f_rench_h=1 then
		dim temp[]
		last=0
		dist=(fl_height-gs_fw_lower-gs_fw_upper+gs_grid_w)/(gs_hor_gnum+1)
		for i=1 to gs_hor_gnum
			temp[i]=last+dist
			last=temp[i]
		next i

		parameters gs_hor_d=temp
		gs_hor_d=temp
	endif
	endif

	if gs_ver_gnum>0 then
	if f_rench_v=1 then
		dim temp2[]
		last2=0
		dist2=(fl_width-gs_fw_side_left-gs_fw_side_right+gs_grid_w)/(gs_ver_gnum+1)
		for HJ=1 to gs_ver_gnum
			temp2[HJ]=last2+dist2
			last2=temp2[HJ]
		next HJ

		parameters gs_ver_d=temp2
		gs_ver_d=temp2
	endif
	endif

	if gs_door_num_m = 2 then

		!************** double leaf doors

		if gs_ver_gnum>0 then
		if f_rench_v=1 then
			dim temp4[]
			last4=0
			dist4=(fl_width2-gs_fw_side_left-gs_fw_side_right+gs_grid_w)/(gs_ver_gnum+1)
			for HJ=1 to gs_ver_gnum
				temp4[HJ]=last4+dist4
				last4=temp4[HJ]
			next HJ

			parameters gs_ver_d4=temp4
			gs_ver_d4=temp4
		endif
		endif

	endif

endif

if GLOB_MODPAR_NAME="iDoorPanelType" then
	if	iDoorPanelType=29 or\
		iDoorPanelType=2 or iDoorPanelType=3 or iDoorPanelType=4 or\
		iDoorPanelType=13 or iDoorPanelType=14 or iDoorPanelType=15 or\
		iDoorPanelType=16 or iDoorPanelType=17 or iDoorPanelType=18 or\
		iDoorPanelType=19 or iDoorPanelType=20 or iDoorPanelType=21 or\
		iDoorPanelType=25 or iDoorPanelType=26 or iDoorPanelType=30 then
		parameters bShowGlassPanelOnSymbol = 1
	else
		parameters bShowGlassPanelOnSymbol = 0
	endif
endif

! =============================================================================
! Zone Recognitions for Doors - Parapet wall Inset value calculation
! at gs_sectgar, gs_highspeedgar: (others in DoorFrame macro)
!		GLOB_ELEVATION		- UNconditional - Zone`s always flowing
!		WIDO_REVEAL_SIDE	- conditional
!		REVEAL (WIDO_SILL)	- conditional
!
! ParapetWallInset Values:	flowing: zero or more from the outside (reveal side)
!		-EPS < >= WALL_THICKNESS
! =============================================================================

ZONE_RECOGNITION_NONE 		= 0
ZONE_RECOGNITION_FULL 		= 1
ZONE_RECOGNITION_OUTSIDE 	= 2
ZONE_RECOGNITION_INSIDE 	= 3

! gs_sectgar, gs_highspeedgar special
if gs_leaf_optype_m = LEAF_GAR_2L_ROLLSIDE |\
	gs_leaf_optype_m = LEAF_GAR_1L_ROLLUP |\
	gs_leaf_optype_m = LEAF_GAR_1L_SECTIONAL then
	pwi = ZONE_RECOGNITION_FULL
else
	if WIDO_REVEAL_SIDE then
		pwi = ZONE_RECOGNITION_INSIDE
	else
		pwi = ZONE_RECOGNITION_OUTSIDE
	endif
endif
parameters pwi  = pwi

end

"preloadSurfaces":
	parameters	stored_material[1][1]	= gs_frame_mat,
				stored_material[1][2]	= gs_frame_mat2,
				stored_material[1][3]	= gs_leaf_mat,
				stored_material[1][4]	= gs_leaf_matin,
				stored_material[1][5]	= gs_leaf_mat2,
				stored_material[1][6]	= gs_leaf_mat3,
				stored_material[1][7]	= gs_grill_mat,
				stored_material[1][8]	= gs_grill_matin
return


2000:

!******************************* for entrance doors ************************
!***************************************************************************

!**************** Parameter hide according to Panel Style

if gs_door_typ_num > 0 then

	!*** originally internal panels

	!*** H-V Grid
	if gs_grill_typ_m = 2 then
		if gs_grid_w<=0 then  parameters gs_grid_w=0.0001
		if gs_grid_w>a-(gs_frame_width_left+gs_frame_width_right)-gs_panel_fw then\
				parameters gs_grid_w=a-(gs_frame_width_left+gs_frame_width_right)-gs_panel_fw
			if gs_grill_pos_m=3 then
				if gs_grid_thk<=0 then  parameters gs_grid_thk=0.0001
				if gs_grid_thk>gs_leaf_thk then  parameters gs_grid_thk=gs_leaf_thk
			else
				if gs_grid_thk<=0 then  parameters gs_grid_thk=0.0001
				if gs_grid_thk>(gs_leaf_thk-gs_glass_thk)/2 then\
					parameters gs_grid_thk=(gs_leaf_thk-gs_glass_thk)/2
			endif
		if gs_ver_gnum<1 then  parameters gs_ver_gnum=1
		if gs_ver_gnum>(a-(gs_frame_width_left+gs_frame_width_right)-gs_panel_fw)/gs_grid_w then\
			parameters gs_ver_gnum=(a-(gs_frame_width_left+gs_frame_width_right)-gs_panel_fw)/gs_grid_w
		if gs_hor_gnum<1 then  parameters gs_hor_gnum=1
		if gs_hor_gnum>(b-(gs_frame_width_left+gs_frame_width_right)-gs_panel_fw)/gs_grid_w then\
			parameters gs_hor_gnum=(b-(gs_frame_width_left+gs_frame_width_right)-gs_panel_fw)/gs_grid_w
	endif

	!*** Prairie
	if gs_grill_typ_m = 3 then
		if a<gs_grid_w*5+6"+gs_panel_fw+(gs_frame_width_left+gs_frame_width_right) then\
			parameters a=gs_grid_w*5+6"+gs_panel_fw+(gs_frame_width_left+gs_frame_width_right)+0.005"
		if b<gs_grid_w*8+6"+gs_panel_fw+(gs_frame_width_left+gs_frame_width_right) then\
			parameters b=gs_grid_w*8+6"+gs_panel_fw+(gs_frame_width_left+gs_frame_width_right)+0.005"
		if gs_grid_thk<=0 then  parameters gs_grid_thk=0.0001
		if gs_grid_thk>gs_leaf_thk then  parameters gs_grid_thk=gs_leaf_thk
	endif

endif


!********************* hide and lock door panel materials ************************
!*********************************************************************************

!*** Original External Panels
if gs_door_typ_num=0 then
	hideparameter "gs_fw_side","gs_fw_lower","gs_fw_upper","gs_int_fw",
		"gs_fw_side_left","gs_fw_side_right","gs_glass_thk",
		"gs_leaf_mat2","gs_leaf_mat3","gs_grill_mat", "gs_grill_pos",
		"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
	lock "gs_fw_side","gs_fw_lower","gs_fw_upper","gs_int_fw",
		"gs_fw_side_left","gs_fw_side_right","gs_glass_thk",
		"gs_leaf_mat2","gs_leaf_mat3","gs_grill_mat", "gs_grill_pos",
		"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
endif


!*** leaf material 2

if gs_door_typ_num<>2 and gs_door_typ_num<>3 and gs_door_typ_num<>4 and\
	gs_door_typ_num<>8 and gs_door_typ_num<>9 and gs_door_typ_num<>10 and \
	gs_door_typ_num<>11 and gs_door_typ_num<>13 and gs_door_typ_num<>16 and\
	gs_door_typ_num<>22 and gs_door_typ_num<>23 and gs_door_typ_num<>24 and\
	gs_door_typ_num<>25 and gs_door_typ_num<>26 and gs_door_typ_num<>27 and \
	gs_door_typ_num<>28 then
	hideparameter "gs_leaf_mat2"
    lock "gs_leaf_mat2"
endif

!*** leaf material 3

if gs_door_typ_num<>2 and gs_door_typ_num<>8 and gs_door_typ_num<>9 and\
	 gs_door_typ_num<>10 and gs_door_typ_num<>11 and gs_door_typ_num<>28 then
	hideparameter "gs_leaf_mat3"
    lock "gs_leaf_mat3"
endif

!*** grid parameters

if gs_door_typ_num<>29 and gs_door_typ_num<>30 then
	hideparameter "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
		"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
	lock "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
		"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
else
	if gs_grill_typ_m = 1 then
		!*** No Grid
		hideparameter "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
			"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
		lock "gs_grill_mat", "gs_grill_matin", "gs_grill_pos",
			"gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
	endif
	if gs_grill_typ_m = 3 then
		!*** Prairie Style
		hideparameter "gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
		lock "gs_grid_w", "gs_grid_thk", "gs_hor_gnum", "gs_ver_gnum"
	endif
endif


!************* leaf frame width settings

!*** frame internal width

if gs_door_typ_num<2 or gs_door_typ_num>4 then
	hideparameter "gs_int_fw"
    lock "gs_int_fw"
endif

!*** frame side width

if gs_door_typ_num<>29 then
	hideparameter "gs_fw_side"
    lock "gs_fw_side"
endif

!*** frame lower/upper width

if gs_door_typ_num<>29 and gs_door_typ_num<>30 then
	hideparameter "gs_fw_lower", "gs_fw_upper"
    lock "gs_fw_lower", "gs_fw_upper"
endif

!*** not french panel

if gs_door_typ_num<>30 then
	hideparameter "gs_fw_side_left", "gs_fw_side_right"
	lock "gs_fw_side_left", "gs_fw_side_right"
endif

!*** glass parameters

if gs_door_typ_num=1 or gs_door_typ_num=5 or gs_door_typ_num=6 or gs_door_typ_num=7 or\
	gs_door_typ_num=12 or gs_door_typ_num=8 or gs_door_typ_num=9 or \
	gs_door_typ_num=10 or gs_door_typ_num=11 or gs_door_typ_num=28 then
	hideparameter "gs_glass_thk","gs_glass_pen", "gs_glass_mat"
	lock "gs_glass_thk","gs_glass_pen", "gs_glass_mat"
endif


!*** Empty
if iDoorPanelType+gs_door_typ_num=0 then
	hideparameter "gs_handle_type"
    lock "gs_handle_type"
endif

if GLOB_MODPAR_NAME="iDoorPanelType" then
	if	iDoorPanelType=4 or iDoorPanelType=6 or iDoorPanelType=7 or\
		gs_door_typ_num=2 or gs_door_typ_num=3 or gs_door_typ_num=4 or\
		gs_door_typ_num=13 or gs_door_typ_num=14 or gs_door_typ_num=15 or\
		gs_door_typ_num=16 or gs_door_typ_num=17 or gs_door_typ_num=18 or\
		gs_door_typ_num=19 or gs_door_typ_num=20 or gs_door_typ_num=21 or\
		gs_door_typ_num=25 or gs_door_typ_num=26 or gs_door_typ_num=29 or\
		gs_door_typ_num=30 then
		parameters bShowGlassPanelOnSymbol = 1
	else
		parameters bShowGlassPanelOnSymbol = 0
	endif
endif

!************************** Door Panel END ***********************************
!*****************************************************************************


return


3000:

!************************* for metal / sidefront doors *********************
!***************************************************************************

!**************** Parameter hide according to Panel Style

!*** style 1
if iDoorPanelType=1 then
	hideparameter "gs_leaf_mat2","gs_glass_mat","gs_glass_pen"
	lock "gs_leaf_mat2","gs_glass_mat","gs_glass_pen"
endif


!*** empty panel
if iDoorPanelType=0 then
	hideparameter "gs_leaf_mat","gs_leaf_matin","gs_leaf_mat2","gs_glass_mat","gs_leaf_thk",
		"gs_glass_pen","gs_leaf_pen"
	lock "gs_leaf_mat","gs_leaf_matin","gs_leaf_mat2","gs_glass_mat",
		"gs_glass_pen","gs_leaf_pen", "gs_leaf_thk","gs_handle_type",
		"gs_han_height", "gs_han_pos"
endif


!*** custom panel
if iDoorPanelType=31 then
	hideparameter "gs_leaf_mat","gs_leaf_matin", "gs_leaf_mat2","gs_glass_mat",
		"gs_glass_pen","gs_leaf_pen"
	lock "gs_leaf_mat","gs_leaf_matin","gs_leaf_mat2","gs_glass_mat",
		"gs_glass_pen","gs_leaf_pen"
endif

!*** NOT custom panel
if iDoorPanelType<>31 then hideparameter "gs_cust_panel"

!******************** Door Panel END


return



! ------------------------------------------------------------------------------
! garage doors
! ------------------------------------------------------------------------------
"garage doors":


	if iDoorPanelType < 31 then hideparameter "gs_cust_panel"

	if iDoorPanelType=0 then
		hideparameter "gs_leaf_thk","gs_ver_pnum","gs_hor_pnum",
			"gs_handle_type","gs_leaf_pen","gs_glass_pen","gs_leaf_mat","gs_leaf_matin",
			"gs_leaf_mat2","gs_glass_mat"
		lock "gs_leaf_thk","gs_ver_pnum","gs_hor_pnum",
			"gs_handle_type","gs_leaf_pen","gs_glass_pen","gs_leaf_mat","gs_leaf_matin",
			"gs_leaf_mat2","gs_glass_mat"
	endif

	if not(gs_sectgar) then

		if iDoorPanelType=1 or iDoorPanelType=2 or iDoorPanelType=3 or \
				iDoorPanelType=4 or iDoorPanelType=5 then
			hideparameter "gs_ver_pnum","gs_hor_pnum"
			lock "gs_ver_pnum","gs_hor_pnum"
		endif

		if iDoorPanelType=10 then
			hideparameter "gs_hor_pnum"
            lock "gs_hor_pnum"
		endif

		if iDoorPanelType<>5 and iDoorPanelType<>14 then
			hideparameter "gs_glass_pen","gs_glass_mat"
            lock "gs_glass_pen","gs_glass_mat"
		endif

		if iDoorPanelType=10 or iDoorPanelType=11 or iDoorPanelType=12 or iDoorPanelType=14 then
			hideparameter "gs_leaf_mat2"
            lock "gs_leaf_mat2"
		endif
	else

		values "gs_section_num" 1, 2, 3, 4

		parameters gs_unit_extra_left	= -gs_frame_width_left + gs_frame_side_overhang
		parameters gs_unit_extra_right	= -gs_frame_width_right + gs_frame_side_overhang
		parameters gs_unit_extra_upper	= -gs_frame_width_upper

		if not(gs_passdoor) then
			hideparameter 	"gs_passdoorWidth",
							"gs_handle_typ_in_m",
							"gs_handle_typ_out_m",
							"gs_cust_handle_in",
							"gs_cust_handle_out",
							"gs_han_height",
							"gs_han_pos"
			lock	"gs_passdoorWidth",
					"gs_handle_typ_in_m",
					"gs_handle_typ_out_m",
					"gs_cust_handle_in",
					"gs_cust_handle_out",
					"gs_han_height",
					"gs_han_pos"
		endif

		if gs_handle_type_m = 0 then
			lock "gs_main_han_height"
			hideparameter "gs_main_han_height"
		endif

		if gs_handle_type_m <> 99 then
			lock "gs_cust_handle"
			hideparameter "gs_cust_handle"
		endif

		if gs_handle_typ_in_m = 0 & gs_handle_typ_out_m = 0 then
			lock "gs_han_height", "gs_han_pos"
			hideparameter "gs_han_height", "gs_han_pos"
		endif

		if gs_handle_typ_in_m <> 99 then
			lock "gs_cust_handle_in"
			hideparameter "gs_cust_handle_in"
		endif
		if gs_handle_typ_out_m <> 99 then
			lock "gs_cust_handle_out"
			hideparameter "gs_cust_handle_out"
		endif

		if iDoorPanelType <> 3 then
			lock "gs_hor_pnum"
			hideparameter "gs_hor_pnum"
		else
			values "gs_hor_pnum" range[1, ]
		endif

		lock "gs_leaf_mat2", "gs_ver_pnum"
		hideparameter "gs_leaf_mat2", "gs_ver_pnum"

		if not(gs_bGlazedDoor) | iDoorPanelType = 31 then
			lock "gs_glass_mat", "gs_glass_pen"
			hideparameter "gs_glass_mat", "gs_glass_pen"
		endif

		if not(gs_passdoor) then
			lock "gs_open_2D", "gs_open_3D"
			hideparameter "gs_open_2D", "gs_open_3D"
		endif
		values "gs_open3D" range[0, ]

		if iDoorPanelType = 0 | iDoorPanelType = 31 then
			hideparameter	"gs_section_num",
							"gs_bGlazedDoor"

			lock	"gs_section_num",
					"gs_bGlazedDoor"

			if iDoorPanelType = 0 then
				hideparameter	"gs_passdoor",
								"gs_passdoorWidth"

				lock	"gs_passdoor",
						"gs_passdoorWidth"
			endif
		endif
	endif
return


