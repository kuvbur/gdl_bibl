
! --- gs_UI_CasementRect

dim gs_UI_CasementRect[119]
	gs_UI_CasementRect[1] = `Одинаковые Створки`
	gs_UI_CasementRect[2] = `Выбор Створки`
	gs_UI_CasementRect[3] = `Створка`
	gs_UI_CasementRect[4] = `Вертикаль`
	gs_UI_CasementRect[5] = `По Вертик.`
	gs_UI_CasementRect[6] = `Верт.`
	gs_UI_CasementRect[7] = `Гор.`
	gs_UI_CasementRect[8] = `Верт. Вверху`
	gs_UI_CasementRect[9] = `Гориз. Вверху`
	gs_UI_CasementRect[10] = `Вертик. Внизу`
	gs_UI_CasementRect[11] = `Гориз. Внизу`
	gs_UI_CasementRect[12] = `Раскладка`
	gs_UI_CasementRect[13] = `Ширина`
	gs_UI_CasementRect[14] = `Высота`
	gs_UI_CasementRect[15] = `Толщина`
	gs_UI_CasementRect[16] = `Расположение`
	gs_UI_CasementRect[17] = `Стекол`
	gs_UI_CasementRect[18] = `Имя Створки Окна`
	gs_UI_CasementRect[19] = `Переплет Створки`
	gs_UI_CasementRect[20] = ``
	gs_UI_CasementRect[21] = ``
	gs_UI_CasementRect[22] = ``
	gs_UI_CasementRect[23] = ``
	gs_UI_CasementRect[24] = `Основная Створка`
	gs_UI_CasementRect[25] = `Основная Средняя`
	gs_UI_CasementRect[26] = `Основная Боковая 1`
	gs_UI_CasementRect[27] = `Основная Боковая 2`
	gs_UI_CasementRect[28] = `Створка Фрамуги`
	gs_UI_CasementRect[29] = `Створка Фрамуги 1`
	gs_UI_CasementRect[30] = `Створка Фрамуги 2`
	gs_UI_CasementRect[31] = `Средняя Фрамуга`
	gs_UI_CasementRect[32] = `Боковая Фрамуга 1`
	gs_UI_CasementRect[33] = `Боковая Фрамуга 2`
	gs_UI_CasementRect[34] = `Нижняя Боковая Панель`
	gs_UI_CasementRect[35] = `Нижняя Створка 1`
	gs_UI_CasementRect[36] = `Нижняя Створка 2`
	gs_UI_CasementRect[37] = `Нижняя Фрамуга`
	gs_UI_CasementRect[38] = `Нижняя Боковая Панель 1`
	gs_UI_CasementRect[39] = `Нижняя Боковая Панель 2`
	gs_UI_CasementRect[40] = `Боковая Панель`
	gs_UI_CasementRect[41] = `Боковые Створки`
	gs_UI_CasementRect[42] = `Боковая Створка 1`
	gs_UI_CasementRect[43] = `Боковая Створка 2`
	gs_UI_CasementRect[44] = `Фрамуга`
	gs_UI_CasementRect[45] = `Основная Створка 1`
	gs_UI_CasementRect[46] = `Основная Створка 2`
	gs_UI_CasementRect[47] = `Тип и Угол/Ширина Открывания в 3D`
	gs_UI_CasementRect[48] = `Угол Открывания в 2D`
if bSlidingUIalternative then
	gs_UI_CasementRect[49] = `Выбор Створки`
else
	gs_UI_CasementRect[49] = `Редактирование Створки`
endif
	gs_UI_CasementRect[50] = `Стекло`
	gs_UI_CasementRect[51] = `Покрытие`
	gs_UI_CasementRect[52] = `Перо в 3D`
	gs_UI_CasementRect[53] = `Снаружи`
	gs_UI_CasementRect[54] = `Внутри`
	gs_UI_CasementRect[55] = `Ширина Коробки`
	gs_UI_CasementRect[56] = `Основные Створки`
	gs_UI_CasementRect[57] = `Створки Фрамуги`
	gs_UI_CasementRect[58] = `Верхняя Створка`
	gs_UI_CasementRect[59] = `Фиксированная Створка`
	gs_UI_CasementRect[60] = `Раздвижная Створка`
	gs_UI_CasementRect[61] = `Сторона 1`
	gs_UI_CasementRect[62] = `Сторона 2`
	gs_UI_CasementRect[63] = `Тип Штриховки Сечения`
	gs_UI_CasementRect[64] = `Перо`
	gs_UI_CasementRect[65] = `Перо Фона`
	gs_UI_CasementRect[66] = `Перо 3D`
	gs_UI_CasementRect[67] = `Внутри`
	gs_UI_CasementRect[68] = `Снаружи`
	gs_UI_CasementRect[69] = `Створка`
	gs_UI_CasementRect[70] = `Боковая Панель и Фрамуга`
	gs_UI_CasementRect[71] = `Реквизиты`
	gs_UI_CasementRect[72] = `Тип Штриховки`
	gs_UI_CasementRect[73] = `Перо Штриховки`
	gs_UI_CasementRect[74] = `Перо Фона Штриховки`
	gs_UI_CasementRect[75] = `Раскладка Полотна и Фрамуги`
	gs_UI_CasementRect[76] = `Боковая Панель 1`
	gs_UI_CasementRect[77] = `Боковая Панель 2`
	gs_UI_CasementRect[78] = `Основная Фрамуга`
	gs_UI_CasementRect[79] = `Средняя Фрамуга`
	gs_UI_CasementRect[80] = `Боковая Фрамуга`
	gs_UI_CasementRect[81] = `Фрамуга 1`
	gs_UI_CasementRect[82] = `Фрамуга 2`
	gs_UI_CasementRect[83] = `Реквизиты Полотна и Фрамуги`
	gs_UI_CasementRect[84] = `Остекление Полотна и Фрамуги`
	gs_UI_CasementRect[85] = `Средник Боковой Панели`
	gs_UI_CasementRect[86] = `Покрытия`
	gs_UI_CasementRect[87] = `Сплошная Панель Снаружи`
	gs_UI_CasementRect[88] = `Сплошная Панель Внутри`
	gs_UI_CasementRect[89] = `Сплошное Остекление возможно только при деактивации Одинаковых Створок.`
	gs_UI_CasementRect[90] = `Разделенная Рама возможна только при деактивации Одинаковых Створок.`
	gs_UI_CasementRect[91] = `Тип створки боковой панели был изменен.`
	gs_UI_CasementRect[92] = `Вверху`
	gs_UI_CasementRect[93] = `Внизу`
	gs_UI_CasementRect[94] = ""	! Unused
	gs_UI_CasementRect[95] = ""	! Unused
	gs_UI_CasementRect[96] = ""	! Unused
	gs_UI_CasementRect[97] = `Реквизиты Полотна`
	gs_UI_CasementRect[98] = `Остекление Полотна и Боковой Панели`
	gs_UI_CasementRect[99] = `Остекление Полотна, Боковой Панели и Фрамуги`
	gs_UI_CasementRect[100] = ""
	gs_UI_CasementRect[101] = ""
	gs_UI_CasementRect[102] = ""
	gs_UI_CasementRect[103] = ""
	gs_UI_CasementRect[104] = ""
	gs_UI_CasementRect[105] = ""
	gs_UI_CasementRect[106] = ""
	gs_UI_CasementRect[107] = ""
	gs_UI_CasementRect[108] = ""
	gs_UI_CasementRect[109] = ""
	gs_UI_CasementRect[110] = ""
	gs_UI_CasementRect[111] = ""
	gs_UI_CasementRect[112] = ""
	gs_UI_CasementRect[113] = `Стиль Ламели`
	gs_UI_CasementRect[114] = `Толщина Ламелей`
	gs_UI_CasementRect[115] = `Ширина Ламелей`
	gs_UI_CasementRect[116] = `Поворот Ламелей`
	gs_UI_CasementRect[117] = `Жалюзи`
	gs_UI_CasementRect[118] = `Прим: параметры применены ко всем вент-створкам.`
	gs_UI_CasementRect[119] = `Расположение Фиксированного Стекла`

dim stDoorGeneralOpeningUI[21]
	stDoorGeneralOpeningUI[1] = `Дверь`
	stDoorGeneralOpeningUI[2] = `Тип Открывания`

if	gs_leaf_optype_m = LEAF_2L_SIDE_HUNG		|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_ACTING	|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS	|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS_M	then

	stDoorGeneralOpeningUI[3] = `Угол Открывания в 2D со Стороны 1`
	stDoorGeneralOpeningUI[4] = `Угол Открывания в 3D со Стороны 1`
else
	stDoorGeneralOpeningUI[3] = `Угол Открывания в 2D`
	stDoorGeneralOpeningUI[4] = `Угол Открывания в 3D`
endif

	stDoorGeneralOpeningUI[5] = `Ширина Открывания в 2D`
	stDoorGeneralOpeningUI[6] = `Ширина Открывания в 3D`
	stDoorGeneralOpeningUI[7] = `Стиль Коробки был изменен на Гладкую.`
	stDoorGeneralOpeningUI[8] = `Порог был изменен.`
	stDoorGeneralOpeningUI[9] = `Отлив был изменен.`
	stDoorGeneralOpeningUI[10] = `Открывание Складыванием`
	stDoorGeneralOpeningUI[11] = `Ширина в 3D`
	stDoorGeneralOpeningUI[12] = `Ширина в 2D`
	stDoorGeneralOpeningUI[13] = `Открывание Конечной Панели`
	stDoorGeneralOpeningUI[14] = `Полотно Открывания`
	stDoorGeneralOpeningUI[15] = `Угол в 2D`
	stDoorGeneralOpeningUI[16] = `Угол в 3D`
	stDoorGeneralOpeningUI[17] = `Сторона 1`
	stDoorGeneralOpeningUI[18] = `Сторона 2`
	stDoorGeneralOpeningUI[19] = `Полотно Открывания в Конце`
	stDoorGeneralOpeningUI[20] = `Угол Открывания в 2D со Стороны 2`
	stDoorGeneralOpeningUI[21] = `Угол Открывания в 3D со Стороны 2`

dim st_uiDoorSettModAttr[5]
	st_uiDoorSettModAttr[1] = `Покрытия Коробки, Полотна и Створки`
	st_uiDoorSettModAttr[2] = `Покрытия Ручки`
	st_uiDoorSettModAttr[3] = `Покрытие Защитной Пластины`
	st_uiDoorSettModAttr[4] = `Перья Коробки и Створок в 3D`
	st_uiDoorSettModAttr[5] = `Приточный Клапан`

	bSingleDoor = 0
	bShapeTabpage = (gs_sidelight_left	|\
	gs_sidelight_right	|\
	gs_door_transom	|\
	bArchedTop			|\
	gs_leaf_optype_m = LEAF_2L_SIDE_HUNG		|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_ACTING	|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS	|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS_M )& not(bBombShelter)

if (gs_leaf_optype_m = LEAF_1L_SIDE_HUNG		|\
	gs_leaf_optype_m = LEAF_1L_SIDE_HUNG_M		|\
	gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING	|\
	gs_leaf_optype_m = LEAF_1L_DOUBLE_ACTING_M	|\
	gs_leaf_optype_m = LEAF_2L_SIDE_HUNG		|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_ACTING	|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS	|\
	gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS_M	) then

	if bStorefrontDoor then
		ui_dialog `Параметры Двери Витрины`
	else
		if bBombShelter then
			ui_dialog `Параметры Двери Бомбоубежища`
		else
			ui_dialog `Параметры Распашной Двери`
		endif
	endif
else
	if (gs_leaf_optype_m = LEAF_GAR_1L_UP_AND_OVER	|\
		gs_leaf_optype_m = LEAF_GAR_1L_UP_SLIDING	|\
		gs_leaf_optype_m = LEAF_GAR_1L_SECTIONAL	|\
		gs_leaf_optype_m = LEAF_GAR_2L_SIDE_HUNG	|\
		gs_leaf_optype_m = LEAF_GAR_2L_ROLLSIDE		|\
		gs_leaf_optype_m = LEAF_GAR_1L_ROLLUP		) then

		ui_dialog `Параметры Гаражных Ворот`
	else
		if (gs_leaf_optype_m = LEAF_REVOLVING	|\
			gs_leaf_optype_m = LEAF_1L_PIVOT	|\
			gs_leaf_optype_m = LEAF_1L_PIVOT_M	) then

			ui_dialog `Параметры Вращающейся Двери`
		else
			if (gs_leaf_optype_m = LEAF_1L_MULTIPANEL	|\
				gs_leaf_optype_m = LEAF_1L_MULTIPANEL_M	|\
				gs_leaf_optype_m = LEAF_2L_MULTIPANEL	) then
				ui_dialog `Параметры Раздвижной Складывающейся Двери`
			else
				ui_dialog `Параметры Раздвижной Двери`
			endif
		endif
	endif
endif

! --------------------------------------------------------------------------------
! Custom UI Pages Titles
! --------------------------------------------------------------------------------

titleFrame		= `Коробка Двери`
if (iOpeningMethod = OPENMETHOD_OPEN_SIMP |\
	iOpeningMethod = OPENMETHOD_OPEN_DOUB |\
	iOpeningMethod = OPENMETHOD_PIVOT )	then

	titleOpening	= `Тип и Угол Открывания`
else
	titleOpening	= `Тип и Ширина Открывания`
endif
titlePanel2			= `Параметры 2D-символа Полотна Двери `

if iOpeningMethod = OPENMETHOD_SLIDE_EXT_SIMP | iOpeningMethod = OPENMETHOD_SLIDE_EXT_DOUB then
	titleMaterials	= `Покрытия Полотна и Ручки`
else
	titleMaterials	= `Покрытия Двери и Ручки`
endif

if gs_door_transom then
	if (gs_sidelight_left | gs_sidelight_right) then
		titleSide = `Типы Створок Боковой Панели и Фрамуги`
	else
		titleSide = `Тип Створки Фрамуги`
	endif
else
	titleSide = `Тип Створки Боковой Панели`
endif
if gs_sidelight_left | gs_sidelight_right | gs_door_transom then
	titleFrameStyle	= `Стиль и Размеры Коробки`
else
	titleFrameStyle	= `Коробка и Полотно`
endif
titleLeafSash	= `Размеры Полотна и Створки`


! =============================================================================
! Tabpage Structure Definition
! =============================================================================

! call all available page fix IDs
call "ui_tabIDlistHierarchy_m" parameters  iUITreeStandardType = 10,		!INT_STANDARD_DOOR_UI
		returned_parameters iUiID_Root,
							iUiID_Shape,			iParID_Shape,
							iUiID_NominalSizes, 	iParID_NominalSizes,
							iUiID_DoorSettOpening,	iParID_DoorSettOpening,
							iUiID_FrameSettings,	iParID_FrameSettings,
							iUiID_VentProfile,		iParID_VentProfile,
							iUiID_FrameSettingsGar,	iParID_FrameSettingsGar,
							iUiID_VentProfileGar,	iParID_VentProfileGar,
							iUiID_LeafSettingsGar,	iParID_LeafSettingsGar,
							iUiID_LeafSettingsGar2,	iParID_LeafSettingsGar2,
							iUiID_LeafTypeHandle,	iParID_LeafTypeHandle,
							iUiID_LeafSymbol_CHI,	iParID_LeafSymbol_CHI,
							iUiID_SidePanel,		iParID_SidePanel,
							iUiID_HandleAndKickBoard,	iParID_HandleAndKickBoard,
							iUiID_LeafTypeHandle2,	iParID_LeafTypeHandle2,
							iUiID_PassDoorHandle,	iParID_PassDoorHandle,
							iUiID_LeafSashDimensions,	iParID_LeafSashDimensions,
							iUiID_SideSashes,		iParID_SideSashes,
							iUiID_OpeningType,		iParID_OpeningType,
							iUiID_OpLinesDoor,		iParID_OpLinesDoor,
							iUiID_OpLinesSideSashes,	iParID_OpLinesSideSashes,
							iUiID_OpeningGar,		iParID_OpeningGar,
							iUiID_DoorSettOpModAttr,	iParID_DoorSettOpModAttr,
							
							! Wall Opening group
							iUiID_WallOpening,		iParID_WallOpening,
							iUiID_Reveal,			iParID_Reveal,
							iUiID_Closure,			iParID_Closure,
							iUiID_MasonryArch,		iParID_MasonryArch,
							iUiID_Tolerances,		iParID_Tolerances,
							iUiID_WallOpModelAttr,	iParID_WallOpModelAttr,
							
							! Fixtures and Fittings group
							iUiID_FixtureFitting,	iParID_FixtureFitting,
							iUiID_Threshold,		iParID_Threshold,
							iUiID_ThresholdSill,	iParID_ThresholdSill,
							iUiID_Sill,				iParID_Sill,
							iUiID_Board,			iParID_Board,
							iUiID_CasingOut,		iParID_CasingOut,
							iUiID_CasingIn,			iParID_CasingIn,
							iUiID_Shutter,			iParID_Shutter,
							iUiID_FixFittingModAttr, iParID_FixFittingModAttr,
				
							iUiID_FloorPlanSection,	iParID_FloorPlanSection,
							iUiID_MinSpace,			iParID_MinSpace,
							iUiID_Descriptions,		iParID_Descriptions


dim uiUsedPageIDs[][2]
dim uiUsedPageNames[][2]

idxPage = 1

! --------------------------------------------------------------------------------
! Independent pages
! --------------------------------------------------------------------------------
if bShapeTabpage then
	uiUsedPageIDs[idxPage][1]	= iUiID_Shape		: uiUsedPageIDs[idxPage][2]		= iParID_Shape
	uiUsedPageNames[idxPage][1]	= `Форма`			: uiUsedPageNames[idxPage][2]	= "uiTab_DoorShape_flat(1)"
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_NominalSizes		: uiUsedPageIDs[idxPage][2]		= iParID_NominalSizes
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

! --------------------------------------------------------------------------------
! Door Settings and Opening group
! --------------------------------------------------------------------------------

uiUsedPageIDs[idxPage][1]	= iUiID_DoorSettOpening			: uiUsedPageIDs[idxPage][2]		= iParID_DoorSettOpening
uiUsedPageNames[idxPage][1]	= `Параметры Двери и Открывания`	: uiUsedPageNames[idxPage][2]	= "uiTab_DoorSettings_flat(1)"
idxPage = idxPage + 1

if iOpeningMethod <> OPENMETHOD_SLIDE_EXT_SIMP & iOpeningMethod <> OPENMETHOD_SLIDE_EXT_DOUB then
	uiUsedPageIDs[idxPage][1]	= iUiID_FrameSettings		: uiUsedPageIDs[idxPage][2]		= iParID_FrameSettings
	uiUsedPageNames[idxPage][1]	= titleFrameStyle			: uiUsedPageNames[idxPage][2]	= "uiTab_DoorFrameStyle_flat(1)"
	idxPage = idxPage + 1
endif

if bEnableVentilationProfile then
	uiUsedPageIDs[idxPage][1]	= iUiID_VentProfile		: uiUsedPageIDs[idxPage][2]		= iParID_VentProfile
	uiUsedPageNames[idxPage][1]	= `Приточная Вентиляция`	: uiUsedPageNames[idxPage][2]	= "uiTab_OpeningVentilation_flat(1)"
	idxPage = idxPage + 1
endif

if not(bBombShelter) then
	uiUsedPageIDs[idxPage][1]	= iUiID_LeafTypeHandle		: uiUsedPageIDs[idxPage][2]		= iParID_LeafTypeHandle
	uiUsedPageNames[idxPage][1]	= `Тип Полотна Двери`			: uiUsedPageNames[idxPage][2]	= "uiTab_DoorLeaf_flat(1)"
	idxPage = idxPage + 1

	if bAsymmetricLeafs then
		uiUsedPageIDs[idxPage][1]	= iUiID_SidePanel		: uiUsedPageIDs[idxPage][2]		= iParID_SidePanel
		uiUsedPageNames[idxPage][1]	= `Боковая Панель`			: uiUsedPageNames[idxPage][2]	= "uiTab_DoorLeaf_flat(1)"
		idxPage = idxPage + 1

		! Change ParentID in Assymmetric Door - Original: iParID_HandleAndKickBoard
		_parIDAssym = iUiID_DoorSettOpening
	else
		_parIDAssym = iParID_HandleAndKickBoard
	endif
	
	uiUsedPageIDs[idxPage][1]	= iUiID_HandleAndKickBoard	: uiUsedPageIDs[idxPage][2]		= _parIDAssym
	uiUsedPageNames[idxPage][1]	= `Ручка и Защитная Пластина`	: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontHandle_flat(1)"
	idxPage = idxPage + 1
endif

if (bEnableShowHandle 			|\			! if any of them is enabled 
	bEnableShowGlassPanel 		|\			
	bEnableShowChiSpringSymbol 	|\
	bEnableShowAirtightPanel)	 then
	
	! Display tabpage for Chinese Leaf Symbol parameters
	if bBombShelter then
	   ! override iParID_LeafSymbol_CHI, different hierarchy
	   iParID_LeafSymbol_CHI = iUiID_DoorSettOpening
	endif
	
	uiUsedPageIDs[idxPage][1]	= iUiID_LeafSymbol_CHI				: uiUsedPageIDs[idxPage][2]		= iParID_LeafSymbol_CHI
	uiUsedPageNames[idxPage][1]	= `Параметры 2D-символа Полотна Двери `	: uiUsedPageNames[idxPage][2]	= "uiTab_DoorLeaf_flat(1)"
	idxPage = idxPage + 1
endif

if gs_sidelight_left | gs_sidelight_right | gs_door_transom then
	uiUsedPageIDs[idxPage][1]	= iUiID_LeafSashDimensions		: uiUsedPageIDs[idxPage][2]		= iParID_LeafSashDimensions
	uiUsedPageNames[idxPage][1]	= titleLeafSash					: uiUsedPageNames[idxPage][2]	= "uiTab_DoorLeafSash_flat(1)"
	idxPage = idxPage + 1

	uiUsedPageIDs[idxPage][1]	= iUiID_SideSashes		: uiUsedPageIDs[idxPage][2]		= iParID_SideSashes
	uiUsedPageNames[idxPage][1]	= titleSide				: uiUsedPageNames[idxPage][2]	= "uiTab_DoorConnectingSidelight_flat(1)"
	idxPage = idxPage + 1
endif

if not(gs_pocket | gs_slidingext) then
	uiUsedPageIDs[idxPage][1]	= iUiID_OpeningType			: uiUsedPageIDs[idxPage][2]		= iParID_OpeningType
	uiUsedPageNames[idxPage][1]	= titleOpening				: uiUsedPageNames[idxPage][2]	= "uiTab_WindowOpening_flat(1)"
	idxPage = idxPage + 1
endif

! Change ParentID of Opening Lines page for Pocket and Ext Sliding Doors - Original: iParID_OpLinesDoor
if gs_pocket | gs_slidingext then
	_parIDOpLines = iUiID_DoorSettOpening
else
	_parIDOpLines = iParID_OpLinesDoor
endif

uiUsedPageIDs[idxPage][1]	= iUiID_OpLinesDoor			: uiUsedPageIDs[idxPage][2]		= _parIDOpLines
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

if gs_sidelight_left | gs_sidelight_right | gs_door_transom then
	uiUsedPageIDs[idxPage][1]	= iUiID_OpLinesSideSashes	: uiUsedPageIDs[idxPage][2]		= iParID_OpLinesSideSashes
	uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_DoorSettOpModAttr	: uiUsedPageIDs[idxPage][2]	= iParID_DoorSettOpModAttr
uiUsedPageNames[idxPage][1]	= `Реквизиты Модели`		: uiUsedPageNames[idxPage][2]	= "uiTab_3DRepr_flat(1)"
idxPage = idxPage + 1

! --------------------------------------------------------------------------------
! Wall Opening group
! --------------------------------------------------------------------------------
uiUsedPageIDs[idxPage][1]	= iUiID_WallOpening			: uiUsedPageIDs[idxPage][2]		= iParID_WallOpening
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

if iOpeningMethod <> OPENMETHOD_POCKET_SIMP & iOpeningMethod <> OPENMETHOD_POCKET_DOUB then
	uiUsedPageIDs[idxPage][1]	= iUiID_Reveal				: uiUsedPageIDs[idxPage][2]		= iParID_Reveal
	uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_Closure				: uiUsedPageIDs[idxPage][2]		= iParID_Closure
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

if gs_sidelight_left | gs_sidelight_right then
	uiUsedPageIDs[idxPage][1]	= iUiID_Tolerances		: uiUsedPageIDs[idxPage][2]		= iParID_Tolerances
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

if bEnableMasonryArch then
	uiUsedPageIDs[idxPage][1]	= iUiID_MasonryArch		: uiUsedPageIDs[idxPage][2]		= iParID_MasonryArch
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1

	uiUsedPageIDs[idxPage][1]	= iUiID_WallOpModelAttr		: uiUsedPageIDs[idxPage][2]		= iParID_WallOpModelAttr
	uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

! --------------------------------------------------------------------------------
! Fixtures and Fittings group
! --------------------------------------------------------------------------------
uiUsedPageIDs[idxPage][1]	= iUiID_FixtureFitting		: uiUsedPageIDs[idxPage][2]		= iParID_FixtureFitting
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

if bEnableThreshold | bForceToShowThresholdUI then
	uiUsedPageIDs[idxPage][1]	= iUiID_Threshold		: uiUsedPageIDs[idxPage][2]		= iParID_Threshold
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif
if bEnableCasing then
	uiUsedPageIDs[idxPage][1]	= iUiID_CasingOut		: uiUsedPageIDs[idxPage][2]		= iParID_CasingOut
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1

	uiUsedPageIDs[idxPage][1]	= iUiID_CasingIn		: uiUsedPageIDs[idxPage][2]		= iParID_CasingIn
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif
if bEnableSill then
	uiUsedPageIDs[idxPage][1]	= iUiID_Sill			: uiUsedPageIDs[idxPage][2]		= iParID_Sill
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif
if bEnableBoard then
	uiUsedPageIDs[idxPage][1]	= iUiID_Board			: uiUsedPageIDs[idxPage][2]		= iParID_Board
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif
if bEnableShutter then
	uiUsedPageIDs[idxPage][1]	= iUiID_Shutter			: uiUsedPageIDs[idxPage][2]		= iParID_Shutter
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_FixFittingModAttr	: uiUsedPageIDs[idxPage][2]		= iParID_FixFittingModAttr
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

! --------------------------------------------------------------------------------
! Additional independent pages
! --------------------------------------------------------------------------------

uiUsedPageIDs[idxPage][1]	= iUiID_FloorPlanSection	: uiUsedPageIDs[idxPage][2]		= iParID_FloorPlanSection
uiUsedPageNames[idxPage][1]	= `План Этажа и Разрез`	: uiUsedPageNames[idxPage][2]	= "uiTab_2DRepr_flat(1)"
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_MinSpace		: uiUsedPageIDs[idxPage][2]		= iParID_MinSpace
uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_Descriptions	: uiUsedPageIDs[idxPage][2]		= iParID_Descriptions
uiUsedPageNames[idxPage][1] = `Характеристики`		: uiUsedPageNames[idxPage][2]	= "uiTab_Descriptions_flat(1)"
idxPage = idxPage + 1


! =============================================================================
! Display actual tabpages
! =============================================================================

ui_current_page gs_ui_current_page

for iTabs = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[iTabs][1]= iUiID_DoorSettOpening then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_DoorSettOpening then
			gosub "pageCallDoorSettOpening"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_FloorPlanSection then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_FloorPlanSection then
			gosub "pageCallFloorPlanAndSect"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_Descriptions then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Descriptions then
			gosub "pageCallListing"
		endif
	endif	
next iTabs

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_DoorShape_flat(1)"
file_dependence "uiTab_DoorSettings_flat(1)"
file_dependence "uiTab_DoorFrameStyle_flat(1)"
file_dependence "uiTab_DoorLeafSash_flat(1)"
file_dependence "uiTab_DoorFrameStyle_flat(1)"
file_dependence "uiTab_DoorLeaf_flat(1)"
file_dependence "uitab_WindowSash_flat(1)"
file_dependence "uiTab_DoorConnectingSidelight_flat(1)"
file_dependence "uiTab_WindowOpening_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"
file_dependence "uiTab_OpeningVentilation_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"

! --------------------------------------------------------------------------------
! GS Door Functions
! --------------------------------------------------------------------------------
call "GS Door Functions",
	parameters all	iWindowShape				= iWindowShape,
					bEnableMasonryArch			= bEnableMasonryArch,
					bEnableSill					= bEnableSill,
					bEnableBoard				= bEnableBoard,
					bEnableThreshold			= bEnableThreshold,
					bForceToShowThresholdUI		= bForceToShowThresholdUI,
					bThresholdNotAvailableByOpTpye	= bThresholdNotAvailableByOpTpye,
					bEnableShutter				= bEnableShutter,
					bEnableCasing				= bEnableCasing,
					bEnableGanging				= bEnableGanging,
					bEnableVentilationProfile	= bEnableVentilationProfile,
					bDoorWithSidelightSupport	= 1,
					bOpeningDirection			= WIDO_REVEAL_SIDE,
					bOpeningDirectionSL			= bOpeningDirectionSL,
					bMiddleStacking				= bMiddleStacking,
					stOpeningDir				= stOpeningDir,
					stLeafOpeningType			= stLeafOpeningType,
					iDoorType					= iDoorType,
					b2ColumnsOnOpLineUI			= b2ColumnsOnOpLineUI,
					bHideOplineStyle			= bHideOplineStyle,
					bLockOplineStyle			= bLockOplineStyle,
					bDisable2DOverride			= bDisable2DOverride,
					bDisableSwingType			= bDisableSwingType,
					bDisableSwingLineType 		= bDisableSwingLineType,
					iOpeningParamType			= iOpeningParamType,
					bArchedTop					= bArchedTop,
					bDoorEmptyPanel				= bDoorEmptyPanel,
					uiUsedPageIDs				= uiUsedPageIDs,
					uiUsedPageNames				= uiUsedPageNames

! --------------------------------------------------------------------------------
! General Door Unit
! --------------------------------------------------------------------------------
call "GeneralDoorUnit" parameters all A=A,
	bEnableVentilationProfile	= bEnableVentilationProfile,
	gs_UI_CasementRect			= gs_UI_CasementRect,
	stDoorGeneralOpeningUI		= stDoorGeneralOpeningUI,
	stSidelightTransomRelation	= stSidelightTransomRelation,
	stLeavesConfiguration		= stLeavesConfiguration,
	stSashType					= stSashType,
	stMullionPosition			= stMullionPosition,
	stOpeningType				= stOpeningType,
	stOpenLeaf					= stOpenLeaf,
	stLouversinFrame			= stLouversinFrame,
	stOpeningDir				= stOpeningDir,
	stFrameElemJoin				= stFrameElemJoin,
	gs_stDoorFrame				= gs_stDoorFrame,
	stGrillPos					= stGrillPos,
	stLeafOpeningType			= stLeafOpeningType,
	stDoorTypes					= stDoorTypes,
	stRailTypes					= stRailTypes,
	st_uiDoorSettModAttr		= st_uiDoorSettModAttr,
	iOpeningMethod 				= iOpeningMethod,
	bSlidingUIalternative		= bSlidingUIalternative,
	isRebateWidth				= isRebateWidth,
	uiUsedPageIDs				= uiUsedPageIDs,
	uiUsedPageNames				= uiUsedPageNames,
	CasementIDArray				= CasementIDArray,
	bKickBoardOut				= bKickBoardOut,
	bKickBoardIn				= bKickBoardIn,
	kickBoardHeight				= kickBoardHeight,
	bKickBoardOut2				= bKickBoardOut2,
	bKickBoardIn2				= bKickBoardIn2,
	kickBoardHeight2			= kickBoardHeight2


! =============================================================================
end
! =============================================================================


! --------------------------------------------------------------------------------
! Door Settings and Opening
! --------------------------------------------------------------------------------
"pageCallDoorSettOpening":
	call "ui_DoorSettOpening_m" parameters		iOpeningMethod			= iOpeningMethod,
												iUiID_OpeningType 		= iUiID_OpeningType,
												iUiID_LeafTypeHandle 	= iUiID_LeafTypeHandle,
												iUiID_HandleAndKickBoard	= iUiID_HandleAndKickBoard,
												iUiID_FrameSettings		= iUiID_FrameSettings,
												iUiID_VentProfile		= iUiID_VentProfile,
												stDoorTypes 			= stDoorTypes,
												bMetalDoor				= bMetalDoor,
												bBombShelter			= bBombShelter,
												bStoreFrontDoor			= bStoreFrontDoor,
												bFoldingDoor			= bFoldingDoor,
												iDoorPanelType			= iDoorPanelType,
												stHandleTypes 			= stHandleTypes,
												gs_handle_type_m		= gs_handle_type_m, 
												stLeafOpeningType		= stLeafOpeningType,
												gs_leaf_optype_m		= gs_leaf_optype_m,
												gs_leaf_num				= gs_leaf_num,
												gs_leaf_num2			= gs_leaf_num2,
												bMiddleStacking			= bMiddleStacking,
												gs_pivotOffset			= gs_pivotOffset,
												gs_pocket_door_frame	= gs_pocket_door_frame,
												gs_pocket_frame			= gs_pocket_frame,
												gs_frame_wthk			= gs_frame_wthk,
												gs_leaf_center			= gs_leaf_center,
												gs_stDoorFrame			= gs_stDoorFrame,
												bForceToShowFrameStyle	= bForceToShowFrameStyle,
												gs_frame_style_m		= gs_frame_style_m,
												bEnableVentilationProfile = bEnableVentilationProfile,
												stVentilationProfileType = stVentilationProfileType,
												bVentilationProfile		= bVentilationProfile,
												iVentilationProfileType = iVentilationProfileType,
												iDWDet2D				= iDWDet2D,
												iDWDet3D				= iDWDet3D,
												bFrameWOutRebate		= bFrameWOutRebate,
												bEnableNEDFrameStyles	= bEnableNEDFrameStyles

return


! --------------------------------------------------------------------------------
! Floor Plan and Section
! --------------------------------------------------------------------------------
"pageCallFloorPlanAndSect":
	if (gs_sidelight_left & gs_sidelight_right) then
		iWindowType = 102	! DOOR_TwoSidelight
	else
		if (gs_sidelight_left | gs_sidelight_right) then
			iWindowType = 101	! DOOR_OneSidelight
		else
			iWindowType = 100	! DOOR_NoSidelight
		endif
	endif
	_bDoorWithSidelightSupport = (gs_sidelight_left | gs_sidelight_right)

	_bShowGlassSectionPars = bStorefrontDoor & (iDoorPanelType <> 1 & iDoorPanelType <> 49 & iDoorPanelType <> 0)

	call "ui_FloorPlanSection_m" parameters isWindow	= 0,
						iWindowShape					= iWindowShape,
						iWallContourLine				= iWallContourLine,
						gs_bFills						= gs_bFills,
						gs_bShowDemolitionPar			= gs_bShowDemolitionPar,
						bShowDemolitionLineType			= 1,
						iTypeDemolitionFill				= iTypeDemolitionFill,
						iRevealType						= gs_reveal_type_int - 1,
						gs_frame_style_m 				= gs_frame_style_m,
						bDoorWithSidelightSupport		= _bDoorWithSidelightSupport,
						gs_stack_top					= gs_stack_top,
						gs_stack_bottom					= 0,
						iWindowType						= iWindowType,
!						gs_bLeftSLight					= gs_bLeftSLight,		! only for windows
!						gs_bSLight						= gs_bSLight,
!						gs_UTrans						= gs_UTrans,
!						gs_UTrans_Div					= gs_UTrans_Div,
!						gs_LTrans						= gs_LTrans,
!						gs_LTrans_Div					= gs_LTrans_Div,
!						iUTrans							= iUTrans,
						gs_sash_uniform					= gs_sash_uniform,
!						gs_panes_number					= gs_panes_number,		! only for multi-sash windows
						CasementIDArray					= CasementIDArray,
						iMullionStyle_00				= iMullionStyle_00,
!						iMullionStyle_01				= iMullionStyle_01,
						iMullionStyle_02				= iMullionStyle_02,
!						iMullionStyle_03				= iMullionStyle_03,
!						iMullionStyle_04				= iMullionStyle_04,
						iMullionStyle_05				= iMullionStyle_05,
						iMullionStyle_06				= iMullionStyle_06,
						iMullionStyle_07				= iMullionStyle_07,
!						iMullionStyle_08				= iMullionStyle_08,
						iMullionStyle_09				= iMullionStyle_09,
						iMullionStyle_10				= iMullionStyle_10,
						iMullionStyle_11				= iMullionStyle_11,
!						gs_optype_m_01					= gs_optype_m_01,		! only for windows
						gs_optype_m_02					= gs_optype_m_02,
!						gs_optype_m_03					= gs_optype_m_03,		! only for windows
!						gs_optype_m_04					= gs_optype_m_04,
						gs_optype_m_05					= gs_optype_m_05,
						gs_optype_m_06					= gs_optype_m_06,
						gs_optype_m_07					= gs_optype_m_07,
!						gs_optype_m_08					= gs_optype_m_08,
						gs_optype_m_09					= gs_optype_m_09,
						gs_optype_m_10					= gs_optype_m_10,
						gs_optype_m_11					= gs_optype_m_11,
						bDisable2DOverride				= bDisable2DOverride,
						bDisable3DOverride				= bDisable3DOverride,
						bOverride_MVO_2D				= bOverride_MVO_2D,
						bOverride_MVO_3D				= bOverride_MVO_3D,
						bOverride_MVO_win_3D			= bOverride_MVO_win_3D,
						iClosureType					= gs_closure_type_int - 1,
						gs_bJoint						= gs_bJoint,
						bEnableParapetInset				= 0,
						gs_parapet_wall_inset			= 0,
						bEnableMasonryArch				= bEnableMasonryArch,
						bMasonryArch					= bMasonryArch,
						bEnableThreshold				= bEnableThreshold,
						bThreshold						= bThreshold,
						bEnableCasing					= bEnableCasing,
						gs_trim_inside					= gs_trim_inside,
						gs_trim_outside					= gs_trim_outside,
						bEnableSill						= bEnableSill,
						gs_sill_outside					= gs_sill_outside,
						iSillType						= iSillType,
						gs_sill_outside_sl_left			= gs_sill_outside_sl_left,
						gs_sidelight_left				= gs_sidelight_left,
						gs_sidelight_parapet_hgt_left	= gs_sidelight_parapet_hgt_left,
						gs_sill_outside_sl_right		= gs_sill_outside_sl_right,
						gs_sidelight_right				= gs_sidelight_right,
						gs_sidelight_parapet_hgt_right	= gs_sidelight_parapet_hgt_right,
						gs_sill_ins_below				= gs_sill_ins_below,
						gs_sill_ins_use_wall_skin		= gs_sill_ins_use_wall_skin,
						bEnableBoard					= bEnableBoard,
						gs_board_inside					= gs_board_inside,
						gs_board_inside_sl_left			= gs_board_inside_sl_left,
						gs_board_inside_sl_right		= gs_board_inside_sl_right,
						bEnableVentilationProfile		= bEnableVentilationProfile,
						bVentilationProfile				= bVentilationProfile,
						iVentilationProfileType			= iVentilationProfileType,
						isVentilationProfileInSash_01	= isVentilationProfileInSash_01,
						isVentilationProfileInSash_02	= isVentilationProfileInSash_02,
						isVentilationProfileInSash_03	= isVentilationProfileInSash_03,
						isVentilationProfileInSash_04	= isVentilationProfileInSash_04,
						isVentilationProfileInSash_05	= isVentilationProfileInSash_05,
						isVentilationProfileInSash_06	= isVentilationProfileInSash_06,
						isVentilationProfileInSash_07	= isVentilationProfileInSash_07,
						isVentilationProfileInSash_08	= isVentilationProfileInSash_08,
						isVentilationProfileInSash_09	= isVentilationProfileInSash_09,
						isVentilationProfileInSash_10	= isVentilationProfileInSash_10,
						isVentilationProfileInSash_11	= isVentilationProfileInSash_11,
						bEnableShutter					= bEnableShutter,
						bSunshade						= bSunshade,
						gs_sunShade_iType				= gs_sunShade_iType,
						listFieldID		 				= iUiID_FloorPlanSection,
						isEmptyOpening					= 0,
						gs_leaf_optype_m				= gs_leaf_optype_m,
						bShowGlassSectionPars			= _bShowGlassSectionPars
return


! --------------------------------------------------------------------------------
! Descriptions
! --------------------------------------------------------------------------------
"pageCallListing":
	call "ui_Description_m" parameters all	iDWObjectType				= 2, 	! DOOR_OBJECT
											gs_show_UKListParams		= gs_show_UKListParams,
											listFieldID 				= iUiID_Descriptions,
											gs_bOpeningToZone			= gs_bOpeningToZone,
											gs_list_cost				= gs_list_cost,
											gs_list_manufacturer		= gs_list_manufacturer,
											gs_list_note				= gs_list_note,
											gs_list_location			= gs_list_location,
											gs_list_location_as			= gs_list_location_as,
											gs_list_accessories			= gs_list_accessories,
											gs_list_glazing				= gs_list_glazing,
											gs_list_headdet				= gs_list_headdet,
											gs_list_jambdet				= gs_list_jambdet,
											gs_list_silldet				= gs_list_silldet,
											gs_list_hwsetno				= gs_list_hwsetno,
											gs_list_hwkeyside			= gs_list_hwkeyside,
											gs_list_optype				= gs_list_optype,
											gs_list_finish				= gs_list_finish,
											gs_list_elevation			= gs_list_elevation,
											gs_list_hings				= gs_list_hings,
											gs_list_lockset				= gs_list_lockset,
											gs_list_glassarea_tmp		= gs_list_glassarea_tmp,
											gs_list_glassarea_override	= gs_list_glassarea_override,
											gs_list_glassarea			= gs_list_glassarea,
											gs_list_glassperimeter		= gs_list_glassperimeter,
											gs_list_heattransfer		= gs_list_heattransfer,
											gs_list_firerating			= gs_list_firerating,
											gs_list_acousticrating		= gs_list_acousticrating,
											FM_Type						= FM_Type,
											iFMType						= iFMType,
											FM_InventoryNumber			= FM_InventoryNumber,
											FM_SerialNumber				= FM_SerialNumber,
											FM_ProductionYear			= FM_ProductionYear,
											FM_ObjectWeight				= FM_ObjectWeight,
											FM_ObjectWeightUnit			= FM_ObjectWeightUnit,
											gs_list_orientationDef_m	= gs_list_orientationDef_m,
											gs_list_NBSClause			= gs_list_NBSClause,
											gs_list_doorcloser			= gs_list_doorcloser,
											gs_list_visionpanel_type	= gs_list_visionpanel_type,
											gs_list_elevationtype		= gs_list_elevationtype,
											gs_list_frametype			= gs_list_frametype,
											gs_list_holddevice			= gs_list_holddevice,
											gs_list_firesignage			= gs_list_firesignage,
											gs_list_comment				= gs_list_comment,
											ifc_optypestr				= ifc_optypestr,
											ifc_optype					= ifc_optype
return


