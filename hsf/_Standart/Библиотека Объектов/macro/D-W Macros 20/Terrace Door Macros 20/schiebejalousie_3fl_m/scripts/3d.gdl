
pen gs_sc_pen

!********* abstract 3D

if GLOB_CONTEXT <> 4 & not(GLOB_PREVIEW_MODE = 2) then
	if det_3 = "Abstrakt" then
		scale3=100
		goto 1001
	endif
endif



!********* section, elevation

if GLOB_CONTEXT = 4 | (GLOB_PREVIEW_MODE = 2) then
	if det_sa = 100 then goto 1000
	if det_sa = 50 then goto 1000
	if det_sa = 20 then goto 1
endif


1:

!****************************************** Bedeckung

	lb_b=(a+0.02+2*(a+0.02)/3)-0.02

if bedec=1 then

	if iLayoutType = LAYOUT_LEFT_TOP or iLayoutType = LAYOUT_LEFT_BOT then mulx -1

	material matsc


	bed_bet=(ras_s+0.04)*tan(15)

	lb_b=(a+0.02+2*(a+0.02)/3)-0.02
	bet_brei=0.04+3*ras_s/2+sc/2+0.02

	sc_tk=0.005			!!!schiene thickness
	bed_ob=0.05
	bed_unt=vsh+sc+0.03

		addy b+bed_unt+bed_bet+bed_ob
		addx -lb/2
		addz -0.02
		roty 90


		prism_ 9, lb_b,
			0,0,15,
			-sc_tk,0,15,
			-sc_tk,-bed_ob,15,
			-sc_tk-bet_brei,-bed_ob-bed_bet,15,
			-sc_tk-bet_brei,-bed_ob-bed_bet-bed_unt,15,
			-bet_brei,-bed_ob-bed_bet-bed_unt,15,
			-bet_brei,-bed_ob-bed_bet-sc_tk*tan(30),15,
			0,-bed_ob-sc_tk*tan(30), 15,
			0,0,-1

		del 4

	if iLayoutType = LAYOUT_LEFT_TOP or iLayoutType = LAYOUT_LEFT_BOT then del 1

endif


!****************************************** type, offnung



if iLayoutType = LAYOUT_RIGHT_TOP then

	gosub 601

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 1

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 2

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 3

	del 2

	endif



if iLayoutType = LAYOUT_RIGHT_BOT then

	gosub 602

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 2

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 1

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 3

	del 1

	endif



if iLayoutType = LAYOUT_LEFT_TOP then

	mulx -1

	gosub 601

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 1

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 2

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 3

	del 2

	endif



if iLayoutType = LAYOUT_LEFT_BOT then

	mulx -1

	gosub 602

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 1

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 2

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 10
	if loc_type=1	then gosub 20
	if loc_type=3	then gosub 30
	if loc_type=2	then gosub 40
	if loc_type=5	then gosub 50

	del 3

	del 1

	del 1

	endif




end




!****************************************** Schiebeladen "mono"



10:

!****************************************** type Gelocht

material mats

gps=0.003   !!!gelocht starke

!********************   Rahmen

addz gps

prism_ 5, ras_s-gps,
	-lb/2, 0, 10,
	-lb/2+rab_s, rab_s, 15,
	-lb/2+rab_s, b-rab_s, 10,
	-lb/2, b, 15,
	-lb/2, 0, -1

prism_ 5, ras_s-gps,
	lb/2, 0, 10,
	lb/2-rab_s, rab_s, 15,
	lb/2-rab_s, b-rab_s, 10,
	lb/2, b, 15,
	lb/2, 0, -1

gosub 100

prism_ 5, ras_s-gps,
	-lb/2, 0, 10,
	-lb/2+rab_s, rab_s, 15,
	 lb/2-rab_s, rab_s, 10,
	 lb/2, 0, 15,
	-lb/2, 0, -1

prism_ 5, ras_s-gps,
	-lb/2, b, 10,
	-lb/2+rab_s, b-rab_s, 15,
	 lb/2-rab_s, b-rab_s, 10,
	 lb/2, b, 15,
	-lb/2, b, -1

del 1

gosub 200

gosub 500

!********************   Laden

material matl

!mm=14		!!! vertikal
!nn=14		!!! horizontal
!r=0.03		!!! hole radius

r=rsc

a1=lb-2*rab_s
b1=b-2*rab_s

!res=4
dalph=360/res

dx=(a1-(nnsc*2*r))/(nnsc+1)
dy=(b1-(mmsc*2*r))/(mmsc+1)



alph=0

dyy=0

for ii=1 to mmsc

	for j=1 to nnsc

		for i=1 to res+1

			put -a1/2+r*cos(alph)+dx+r+dxx, r*sin(alph)+r+dy+rab_s+dyy, 79

			if i=res+1 then put -a1/2+r*cos(alph)+dx+r+dxx, r*sin(alph)+r+dy+rab_s+dyy, -1

			alph=alph+dalph

		next i

	dxx=(dx+2*r)*j

	next j


dxx=0
dyy=(dy+2*r)*ii

next ii


prism_ 5+nsp/3, gps,
	-lb/2, 0, 15,
	-lb/2, b, 5,
	lb/2, b, 15,
	lb/2, 0, 15,
	-lb/2, 0, -1,

	get (nsp)

return



20:

!****************************************** type Lamellen_fest

material mats

!********************   Rahmen

prism_ 5, ras_s,
	-lb/2, 0, 15,
	-lb/2+rab_s, 0, 15,
	-lb/2+rab_s, b-rab_s, 15,
	-lb/2, b-rab_s, 15,
	-lb/2, 0, -1

prism_ 5, ras_s,
	lb/2, 0, 15,
	lb/2-rab_s, 0, 15,
	lb/2-rab_s, b-rab_s, 15,
	lb/2, b-rab_s, 15,
	lb/2, 0, -1

gosub 100

prism_ 5, ras_s,
	-lb/2+rab_s, 0, 15,
	-lb/2+rab_s, rabu_s, 15,
	 lb/2-rab_s, rabu_s, 15,
	 lb/2-rab_s, 0, 15,
	-lb/2+rab_s, 0, -1

prism_ 5, ras_s,
	-lb/2, b, 15,
	-lb/2, b-rab_s, 15,
	 lb/2, b-rab_s, 15,
	 lb/2, b, 15,
	-lb/2, b, -1

gosub 200

gosub 500

!********************   Laden

material matl

las_s=ras_s/3
b1=b-rab_s-rabu_s

!hlb=0.08

nn=int(b1/hlb)
if (nn+1)-b1/hlb<0.5 then nn=nn+1
hlb=b1/nn


	for i=1 to nn

		addy b-rab_s
		roty -90
		addz-(lb-2*rab_s)/2

			prism_ 5, lb-2*rab_s,
					0,0,15,
					las_s,0,15,
					ras_s,-hlb,15,
					ras_s-las_s,-hlb,15,
					0,0,-1


		del 3

		addy -hlb

	next i

	del nn


return



30:

!****************************************** type Rahmen

material mats

!********************   Rahmen

rabuo_s=0.008			!***breite unter
rabuu_s=rabu_s-rabuo_s
pas_s=ras_s/3			!***panel breite
pab=0.01				!***panel anlagern


prism_ 5, ras_s,
	-lb/2, 0, 15,
	-lb/2+rab_s, 0, 15,
	-lb/2+rab_s, b-rab_s, 15,
	-lb/2, b-rab_s, 15,
	-lb/2, 0, -1

prism_ 5, ras_s,
	lb/2, 0, 15,
	lb/2-rab_s, 0, 15,
	lb/2-rab_s, b-rab_s, 15,
	lb/2, b-rab_s, 15,
	lb/2, 0, -1

gosub 100

prism_ 5, ras_s,
	-lb/2+rab_s, 0, 15,
	-lb/2+rab_s, rabuu_s, 15,
	 lb/2-rab_s, rabuu_s, 15,
	 lb/2-rab_s, 0, 15,
	-lb/2+rab_s, 0, -1

addy rabuu_s
roty 90
addz-(lb-2*rab_s)/2
addx -ras_s/2

prism_ 5, lb-2*rab_s,
	-ras_s/2, 0, 15,
	-pas_s/2, rabuo_s, 15,
	pas_s/2, rabuo_s, 15,
	ras_s/2, 0, 15,
	-ras_s/2, 0, -1

del 4

prism_ 5, ras_s,
	-lb/2, b, 15,
	-lb/2, b-rab_s, 15,
	 lb/2, b-rab_s, 15,
	 lb/2, b, 15,
	-lb/2, b, -1

gosub 200

gosub 500

!********************   Laden

material matl

addz ras_s/2-pas_s/2

prism_ 5, pas_s,
	-lb/2+rab_s-pab, rabu_s, 15,
	-lb/2+rab_s-pab, b-rab_s+pab, 15,
	lb/2-rab_s+pab, b-rab_s+pab, 15,
	lb/2-rab_s+pab, rabu_s, 15,
	-lb/2+rab_s-pab, rabu_s, -1

del 1

return



40:

!****************************************** type Lamellen_beweglich

material mats

!********************   Rahmen

prism_ 5, ras_s,
	-lb/2, 0, 10,
	-lb/2+rab_s, rab_s, 15,
	-lb/2+rab_s, b-rab_s, 10,
	-lb/2, b, 15,
	-lb/2, 0, -1

prism_ 5, ras_s,
	lb/2, 0, 10,
	lb/2-rab_s, rab_s, 15,
	lb/2-rab_s, b-rab_s, 10,
	lb/2, b, 15,
	lb/2, 0, -1

gosub 100

prism_ 5, ras_s,
	-lb/2, 0, 10,
	-lb/2+rab_s, rab_s, 15,
	 lb/2-rab_s, rab_s, 10,
	 lb/2, 0, 15,
	-lb/2, 0, -1

prism_ 5, ras_s,
	-lb/2, b, 10,
	-lb/2+rab_s, b-rab_s, 15,
	 lb/2-rab_s, b-rab_s, 10,
	 lb/2, b, 15,
	-lb/2, b, -1

gosub 200

gosub 500

!********************   Laden

material matl

lab_s=ras_s/6
b1=b-2*rab_s

!hlb=0.1

nn=int(b1/hlb)
if (nn+1)-b1/hlb<0.5 then nn=nn+1
hlb=b1/nn


	for i=1 to nn


addy b-(rab_s+hlb/2)
addz ras_s-1.5*lab_s

rotx -alpha

addy -(b-(rab_s+hlb/2))
addz -(ras_s-1.5*lab_s)



		addy b-rab_s
		roty -90
		addz-(lb-2*rab_s)/2



			prism_ 9, lb-2*rab_s,
					ras_s-lab_s,0,15,
					ras_s-2*lab_s,0,15,
					ras_s-2*lab_s+2*lab_s/3,-hlb/3,13,
					ras_s-2*lab_s+3*lab_s/3,-2*hlb/3,13,
					ras_s-lab_s,-hlb,15,
					ras_s,-hlb,15,
					ras_s,-2*hlb/3,13,
					ras_s-lab_s/3,-hlb/3,13,
					ras_s-lab_s,0,-1


		del 3

		del 5

		addy -hlb

	next i

	del nn


return



50:

!****************************************** type Vollflachig

!********************   Rahmen

material mats

prism_ 5, ras_s,
	-lb/2, 0, 15,
	-lb/2, b, 15,
	lb/2, b, 15,
	lb/2, 0, 15,
	-lb/2, 0, -1


gosub 500

return



500:

!****************************************** Schiene vertikal

material matsc

vsv=vsh+sc_tk	!!!schiene vertikal hight

for i=1 to 2

	addy b
	addx -lb/2+rab_s+0.05
	addz (ras_s-vsc)/2

	prism_ 5, vsc,
		-vsc/2,0, 15,
		-vsc/2,vsv, 15,
		vsc/2,vsv, 15,
		vsc/2,0, 15,
		-vsc/2,0, -1

	del 3

mulx -1

next i

del 2

return




601:

!****************************************** Schiene horizontal oben (r)

material matsc

lb_sc=a+0.02	!!!schiene length
sc_tk=0.005			!!!schiene thickness

	addy b+vsh
	addx -lb/2
	addz ras_s/2
	roty 90


	prism_ 13, lb_sc,
		-sc/2,0, 15,
		-sc/2,sc, 15,
		sc/2,sc, 15,
		sc/2,0, 15,
		vsc/2,0, 15,
		vsc/2,sc_tk, 15,
		sc/2-sc_tk,sc_tk, 15,
		sc/2-sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc_tk, 15,
		-vsc/2,sc_tk, 15,
		-vsc/2,0, 15,
		-sc/2,0, -1

	del 4

lb_sc=2*(a+0.02)/3	!!!schiene length

	addy b+vsh
	addx -lb/2
	addz ras_s/2+ras_s+0.02
	roty 90


	prism_ 13, lb_sc,
		-sc/2,0, 15,
		-sc/2,sc, 15,
		sc/2,sc, 15,
		sc/2,0, 15,
		vsc/2,0, 15,
		vsc/2,sc_tk, 15,
		sc/2-sc_tk,sc_tk, 15,
		sc/2-sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc_tk, 15,
		-vsc/2,sc_tk, 15,
		-vsc/2,0, 15,
		-sc/2,0, -1

	del 4

lb_sc=2*(a+0.02)/3	!!!schiene length

	addy b+vsh
	addx -lb/2+lb_b-lb_sc
	addz ras_s/2+ras_s+0.02
	roty 90


	prism_ 13, lb_sc,
		-sc/2,0, 15,
		-sc/2,sc, 15,
		sc/2,sc, 15,
		sc/2,0, 15,
		vsc/2,0, 15,
		vsc/2,sc_tk, 15,
		sc/2-sc_tk,sc_tk, 15,
		sc/2-sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc_tk, 15,
		-vsc/2,sc_tk, 15,
		-vsc/2,0, 15,
		-sc/2,0, -1

	del 4

return



602:

!****************************************** Schiene horizontal unter (r)

material matsc

lb_sc=a+0.02	!!!schiene length
sc_tk=0.005			!!!schiene thickness

	addy b+vsh
	addx -lb/2
	addz ras_s/2+ras_s+0.02
	roty 90


	prism_ 13, lb_sc,
		-sc/2,0, 15,
		-sc/2,sc, 15,
		sc/2,sc, 15,
		sc/2,0, 15,
		vsc/2,0, 15,
		vsc/2,sc_tk, 15,
		sc/2-sc_tk,sc_tk, 15,
		sc/2-sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc_tk, 15,
		-vsc/2,sc_tk, 15,
		-vsc/2,0, 15,
		-sc/2,0, -1

	del 4

lb_sc=2*(a+0.02)/3	!!!schiene length

	addy b+vsh
	addx -lb/2
	addz ras_s/2
	roty 90


	prism_ 13, lb_sc,
		-sc/2,0, 15,
		-sc/2,sc, 15,
		sc/2,sc, 15,
		sc/2,0, 15,
		vsc/2,0, 15,
		vsc/2,sc_tk, 15,
		sc/2-sc_tk,sc_tk, 15,
		sc/2-sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc_tk, 15,
		-vsc/2,sc_tk, 15,
		-vsc/2,0, 15,
		-sc/2,0, -1

	del 4

lb_sc=2*(a+0.02)/3	!!!schiene length

	addy b+vsh
	addx -lb/2+lb_b-lb_sc
	addz ras_s/2
	roty 90


	prism_ 13, lb_sc,
		-sc/2,0, 15,
		-sc/2,sc, 15,
		sc/2,sc, 15,
		sc/2,0, 15,
		vsc/2,0, 15,
		vsc/2,sc_tk, 15,
		sc/2-sc_tk,sc_tk, 15,
		sc/2-sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc-sc_tk, 15,
		-sc/2+sc_tk,sc_tk, 15,
		-vsc/2,sc_tk, 15,
		-vsc/2,0, 15,
		-sc/2,0, -1

	del 4

return



!************** coors for textures ***********
!*********************************************

100:

rotz 90
vert 0, 0, 0
vert 0.1, 0, 0
vert 0, 0.1, 0
vert 0, 0, 0.1
del 1

coor 2, -1, -2, -3, -4

base

body -1

return



200:

vert 0, 0, 0
vert 0.1, 0, 0
vert 0, 0.1, 0
vert 0, 0, 0.1

coor 2, -1, -2, -3, -4

base

body -1

return




!*******************************************************
!************************************ Section, elevation
!*******************************************************


1000:

if GLOB_SCALE<100 then
	scale3=50
else
	scale3=100
endif

if det_sa = 100 then scale3=100
if det_sa = 50 then scale3=50


1001:

!********************* Fenster

!	if e~=1 then
!		mulz -1
!		addz -f~
!	endif

if scale3=100 then

	gosub 3000

endif

if scale3=50 then

		gosub 3000

		addz ras_s
		gosub 3000
		del 1

		gosub 2000

	for i=1 to 2

			addy b
			gosub 2000
			del 1

			addy b+vsh
			gosub 2000
			del 1

			addy b+vsh+sc
			gosub 2000
			del 1

	addz ras_s

	next i

	del 2

endif

!	if e~=1 then
!		del 2
!	endif


end




2000:

pen gs_sc_pen


prism_ 5,ras_s,
	-lb/2, 0, 15,
	-lb/2, 0.0001, 15,
	 lb/2, 0.0001, 15,
	 lb/2, 0, 15,
	-lb/2, 0, -1


return




3000:

!************************ Window polygons

pen gs_sc_pen

!************************ schiene

lb_sc2=a+0.02	!!!schiene length
lb_sc=2*(a+0.02)/3	!!!schiene length

lb_b=(a+0.02+2*(a+0.02)/3)-0.02

if iLayoutType = LAYOUT_LEFT_TOP or iLayoutType = LAYOUT_LEFT_BOT then mulx-1

if iLayoutType = LAYOUT_RIGHT_TOP or iLayoutType = LAYOUT_LEFT_TOP then
	addz ras_s+0.02
endif

	addy b+vsh
	addx (lb_sc-lb)/2

	prism_ 5, 0.0001,
		-lb_sc/2, 0, 15,
		-lb_sc/2, sc, 15,
		lb_sc/2, sc, 15,
		lb_sc/2, 0, 15,
		-lb_sc/2, 0, -1

	del 1

	addx lb_b-(lb_sc+lb)/2

	prism_ 5, 0.0001,
		-lb_sc/2, 0, 15,
		-lb_sc/2, sc, 15,
		lb_sc/2, sc, 15,
		lb_sc/2, 0, 15,
		-lb_sc/2, 0, -1

	del 1
	del 1

if iLayoutType = LAYOUT_RIGHT_TOP or iLayoutType = LAYOUT_LEFT_TOP then del 1


if iLayoutType = LAYOUT_RIGHT_BOT or iLayoutType = LAYOUT_LEFT_BOT then
	addz ras_s+0.02
endif

	addy b+vsh
	addx (lb_sc2-lb)/2

	prism_ 5, 0.0001,
		-lb_sc2/2, 0, 15,
		-lb_sc2/2, sc, 15,
		lb_sc2/2, sc, 15,
		lb_sc2/2, 0, 15,
		-lb_sc2/2, 0, -1

del 2

if iLayoutType = LAYOUT_RIGHT_BOT or iLayoutType = LAYOUT_LEFT_BOT then del 1

if iLayoutType = LAYOUT_LEFT_TOP or iLayoutType = LAYOUT_LEFT_BOT then del 1


!************************ rahmen

if iLayoutType = LAYOUT_RIGHT_TOP then

	lb_sc=a+0.02	!!!schiene length

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 1

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02
!	addz 0.0001

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 2

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02
!	addz 0.0001

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 3

	del 2

	endif


if iLayoutType = LAYOUT_RIGHT_BOT then

	lb_sc=a+0.02	!!!schiene length

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02
!	addz 0.0001

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 2

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 1

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 3

	del 1

	endif


if iLayoutType = LAYOUT_LEFT_TOP then

	lb_sc=a+0.02	!!!schiene length

	mulx -1

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 1

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02
!	addz 0.0001

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 2

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02
!	addz 0.0001

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 3

	del 2

	del 1

	endif


if iLayoutType = LAYOUT_LEFT_BOT then

	lb_sc=a+0.02	!!!schiene length

	mulx -1

	addx ((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 1

	addx ((a+0.02)-(a+0.08)/3)*(offladen/100)
	addz ras_s+0.02
!	addz 0.0001

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 2

	addx -((2*(a+0.02)/3)-(a+0.08)/3)*(offladen/100)

	addx lb_b/2-lb/2
	mulx -1
	addx -(lb_b/2-lb/2)

	if loc_type=4	then gosub 3010
	if loc_type=1	then gosub 3020
	if loc_type=3	then gosub 3030
	if loc_type=2	then gosub 3020
	if loc_type=5	then gosub 3010

	del 3

	del 1

	del 1

	endif


RETURN

3010:

!!!**********   type="Gelocht" or type="Vollflachig" then

prism_ 5, 0.0001,
	-lb/2, 0, 15,
	-lb/2, b, 15,
	lb/2, b, 15,
	lb/2, 0, 15,
	-lb/2, 0, -1


return


3020:

!!!**********   type="Lamellen_fest" or type="Lamellen_beweglich" then

prism_ 5, 0.0001,
	-lb/2, 0, 15,
	-lb/2, b, 15,
	lb/2, b, 15,
	lb/2, 0, 15,
	-lb/2, 0, -1

b1=b
hlb=0.08

nn=int(b1/hlb)
if (nn+1)-b1/hlb<0.5 then nn=nn+1
hlb=b1/nn

addz 0.0001

	for i=1 to nn

	lin_ -lb/2, 0, 0, lb/2, 0, 0

		addy hlb

	next i

	del nn+1


return


3030:

!!!**********   type="Rahmen" then

prism_ 5, 0.0001,
	-lb/2, 0, 15,
	-lb/2, b, 15,
	lb/2, b, 15,
	lb/2, 0, 15,
	-lb/2, 0, -1

addx -(lb-2*rab_s)/2
addy rab_s
addz 0.0001
rect lb-2*rab_s,b-2*rab_s
del 3


return












