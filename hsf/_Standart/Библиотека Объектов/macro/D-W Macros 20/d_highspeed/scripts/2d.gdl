
if lod2D = DET2D_DETAILED_CHI then		! CHI
	CFrameWallThk	= 0
endif

unID_Opening = 1000

line_type gs_panelLineType

if lod2D = DET2D_OFF then end

if lod2D = DET2D_SCALESENS then
	lod2D = DET2D_SIMPLE
	if GLOB_SCALE < 150 then lod2D = DET2D_MIDDLE
	if GLOB_SCALE < 75 then lod2D = DET2D_DETAILED
endif

symb_2d = 0
if lod2D = DET2D_SIMPLE | lod2D = DET2D_SIMPLE_CHI then symb_2d = 200
if lod2D = DET2D_MIDDLE then symb_2d = 100
if lod2D = DET2D_DETAILED | lod2D = DET2D_DETAILED_CHI then symb_2d = 50


!********************* coordinates according to size types

unitleft = -ac_unit_width / 2
unitright = ac_unit_width / 2
leafleft = -ac_leaf_width / 2
leafright = ac_leaf_width / 2
egressleft = -ac_egress_width / 2
egressright = ac_egress_width / 2


!************ Hotspots on Unit Size

if bShowUnitHotspots then
	add2 0, WIDO_SILL-WALL_THICKNESS
	unID = 100
	hotspot2 unitleft,	 0,           	unID
	hotspot2 unitright,	 0,           	unID + 1
	hotspot2 unitleft,	-gs_frame_thk,	unID + 2
	hotspot2 unitright,	-gs_frame_thk,	unID + 3
	del 1
endif


!************ Hotspots on Egress Size

if bShowLeafHotspots then
	add2 0, WIDO_SILL-WALL_THICKNESS
	unID = 200
	hotspot2 egressleft,	(-gs_frame_thk + frameAddPartThick)*(iHighSpeedDoorType = HIGHSPEED_SIMPLE),	unID
	hotspot2 egressright,	(-gs_frame_thk + frameAddPartThick)*(iHighSpeedDoorType = HIGHSPEED_SIMPLE),	unID + 1
	hotspot2 egressleft,	-gs_frame_thk,																	unID + 2
	hotspot2 egressright,	-gs_frame_thk,																	unID + 3
	del 1
endif


!************ Hotspots on Leaf Size

if bShowLeafHotspots then
	add2 0, WIDO_SILL-WALL_THICKNESS
	unID = 300
	hotspot2 leafleft,	(-gs_frame_thk + frameAddPartThick)*(iHighSpeedDoorType = HIGHSPEED_SIMPLE),	unID
	hotspot2 leafright,	(-gs_frame_thk + frameAddPartThick)*(iHighSpeedDoorType = HIGHSPEED_SIMPLE),	unID + 1
	hotspot2 leafleft,	-gs_frame_thk,																	unID + 2
	hotspot2 leafright,	-gs_frame_thk,																	unID + 3
	del 1
endif


if bShowAddHotspots & (iHighSpeedDoorType = HIGHSPEED_SIMPLE) then
	add2 0, WIDO_SILL-WALL_THICKNESS
	unID = 400
	hotspot2 unitleft + gs_frame_width_out,	 	0,          			 			unID
	hotspot2 unitright - gs_frame_width_out,	0,           						unID + 1
	hotspot2 unitleft + gs_frame_width_out,		-gs_frame_thk + frameAddPartThick,	unID + 2
	hotspot2 unitright - gs_frame_width_out,	-gs_frame_thk + frameAddPartThick,	unID + 3
	del 1
endif


if lod2D <> DET2D_SIMPLE_CHI & lod2D <> DET2D_DETAILED_CHI then add2 0, WIDO_SILL-WALL_THICKNESS

if iHighSpeedDoorType = HIGHSPEED_SIMPLE then

	if gs_full_edit then
		unID = 500
		y = (-CFrameShortThick-0.01) * (lod2D = DET2D_MIDDLE | lod2D = DET2D_DETAILED)	
		hotspot2 0, y, unID, gs_open2D, 1+128 : unID=unID+1
		hotspot2 gs_open2D, y, unID, gs_open2D, 2 : unID=unID+1
		hotspot2 -1, y, unID, gs_open2D, 3 : unID=unID+1
	endif
	gs_open2D = gs_open2D*2

	! --------- Frame

	drawindex 30
	pen gs_frame_pen_cont
	fill gs_fillFr

	if lod2D = DET2D_DETAILED then
		for i = 1 to -1 step -2
			mul2 i, 1
			add2 a/2, 0
			circle2 gs_frame_width_out/2, -gs_frame_width_out/2, r
			poly2_b 13, 1+2+4+16+32, gs_penFr_fg, gs_penFr_bg,
					0,													-CFrameShortThick,					32 + 1,
					0,		 											0,									32 + 1,
					gs_frame_width_out,									0,									32 + 1,
					gs_frame_width_out,									-gs_frame_thk,						32 + 1,
					gs_frame_width_out-CFrameShortWidth,				-gs_frame_thk,						32 + 1,
					gs_frame_width_out-CFrameShortWidth,				-gs_frame_thk+CFrameShorterThick,	32 + 1,
					gs_frame_width_out-CFrameShortWidth+CFrameWallThk,	-gs_frame_thk+CFrameShorterThick,	32 + 1,
					gs_frame_width_out-CFrameShortWidth+CFrameWallThk,	-gs_frame_thk+CFrameWallThk,		32 + 1,
					gs_frame_width_out-CFrameWallThk,					-gs_frame_thk+CFrameWallThk,		32 + 1,
					gs_frame_width_out-CFrameWallThk,					-CFrameWallThk,						32 + 1,
					CFrameWallThk,										-CFrameWallThk,						32 + 1,
					CFrameWallThk,										-CFrameShortThick,					32 + 1,
					0,													-CFrameShortThick,					-1

			add2 -diffFrameWidths, -gs_frame_thk+frameAddPartThick

			poly2_b 6, 1+2+4+16+32, gs_penFr_fg, gs_penFr_bg,
				0,		0,			32 + 1,
				frameAddPartWidth+inclinedPartSize,	0,										32 + 1,
				frameAddPartWidth,					-(frameAddPartThick-inclinedPartSize),	32 + 1,
				frameAddPartWidth,					-frameAddPartThick,						32 + 1,
				0,									-frameAddPartThick,						32 + 1,
				0,									0,										-1

			del 3
		next i
	endif

	if symb_2d = 100 then
		for i = 1 to -1 step -2
			mul2 i, 1
			add2 a/2, 0
			poly2_b 7, 1+2+4+16+32, gs_penFr_fg, gs_penFr_bg,
					0,						0,									32 + 1,
					gs_frame_width_out,		0,									32 + 1,
					gs_frame_width_out,		-gs_frame_thk,						32 + 1,
					-diffFrameWidths,		-gs_frame_thk,						32 + 1,
					-diffFrameWidths,		-gs_frame_thk+frameAddPartThick,	32 + 1,
					0,						-gs_frame_thk+frameAddPartThick,	32 + 1,
					0,						0,									-1
			del 2
		next i
	endif

	! --------- Leaf

	drawindex 20
	pen gs_leaf_pen_2D
	fill gs_fillLeaf

	if symb_2d = 50 then
		for i = 1 to -1 step -2
			mul2 i, 1
			if lod2D = DET2D_DETAILED then
				add2 0, -CFrameShortThick-0.01
			else
				add2 0, -gs_leaf_thk/2
			endif
			poly2_b 5, 1+2+4+16+32, gs_penLeaf_fg, gs_penLeaf_bg,
				gs_open2D/2,	0,				32 + 1,
				gs_open2D/2,	gs_leaf_thk,	32 + 1,
				a/2,			gs_leaf_thk,	32 + 1,
				a/2,			0,				32 + 1,
				gs_open2D/2,	0,				-1

			del 2
		next i
	endif

	if symb_2d = 100 then
		add2 0, -CFrameShortThick-0.01
		line2 gs_open2D/2, 0, a/2, 0
		line2 -gs_open2D/2, 0, -a/2, 0
		del 1
	endif

	if symb_2d = 200 then
		line2 gs_open2D/2, 0, a/2, 0
		line2 -gs_open2D/2, 0, -a/2, 0
	endif

	if gs_iSwingType = 3 | gs_iSwingType = 4 then	! LINE_SHAPE_SWING_SIMPLE_LINE or LINE_SHAPE_SWING_LEAF_LINE
		drawindex 10

		pen gs_opLinePen
		line_type gs_swingLineType

		add2 0, (-gs_frame_thk/2) * (lod2D = DET2D_MIDDLE | lod2D = DET2D_DETAILED)
		hotspot2 0, -0.50, unID_Opening
		hotspot2 0, 0.50, unID_Opening + 1
		line2 0, -0.50, 0, 0.50
		del 1
	endif
endif


if iHighSpeedDoorType = HIGHSPEED_ROLL_UP then

	! --------- Frame

	drawindex 20
	pen gs_frame_pen_cont
	fill gs_fillFr

	if lod2D = DET2D_DETAILED then
		for i = 1 to -1 step -2
			mul2 i, 1
			add2 a/2, 0
			poly2_b 13, 1+2+4+16+32, gs_penFr_fg, gs_penFr_bg,
					0,								-gs_frame_thk/2+CFrameOpeningSize/2,	32 + 1,
					0,	 							0,										32 + 1,
					gs_frame_width,					0,										32 + 1,
					gs_frame_width,					-gs_frame_thk,							32 + 1,
					0,								-gs_frame_thk,							32 + 1,
					0,								-gs_frame_thk/2-CFrameOpeningSize/2,	32 + 1,
					CFrameWallThk,					-gs_frame_thk/2-CFrameOpeningSize/2,	32 + 1,
					CFrameWallThk,					-gs_frame_thk+CFrameWallThk,			32 + 1,
					gs_frame_width-CFrameWallThk,	-gs_frame_thk+CFrameWallThk,			32 + 1,
					gs_frame_width-CFrameWallThk,	-CFrameWallThk,							32 + 1,
					CFrameWallThk,					-CFrameWallThk,							32 + 1,
					CFrameWallThk,					-gs_frame_thk/2+CFrameOpeningSize/2,	32 + 1,
					0,								-gs_frame_thk/2+CFrameOpeningSize/2,	-1
			del 2
		next i

		add2 a/2+gs_frame_width+sideBoxWidth, 0

		rot2 90
		fill 0
		poly2_b   7, 1+2+64,      0,     0,
			0,									sideBoxWidth,						1,
			-sideBoxDepth+sideBoxInclPartSize,	sideBoxWidth,						1,
			-sideBoxDepth,						sideBoxWidth-sideBoxInclPartSize,	1,
			-sideBoxDepth,						sideBoxInclPartSize,				1,
			-sideBoxDepth+sideBoxInclPartSize,	0,									1,
			0,									0,									1,
			0,									sideBoxWidth,						-1
		del 2
	endif

	if symb_2d = 100 then
		for i = 1 to -1 step -2
			mul2 i, 1
			add2 a/2, 0
			poly2_b 5, 1+2+4+16+32, gs_penFr_fg, gs_penFr_bg,
					0,					0,				32 + 1,
					gs_frame_width,		0,				32 + 1,
					gs_frame_width,		-gs_frame_thk,	32 + 1,
					0,					-gs_frame_thk,	32 + 1,
					0,					0,				-1
			del 2
		next i
	endif

	! --------- Leaf

	drawindex 20
	pen gs_leaf_pen_2D
	fill gs_fillLeaf

	if symb_2d = 50 then
		if lod2D = DET2D_DETAILED then
			add2 0, -gs_frame_thk/2
		else
			add2 0, -gs_leaf_thk/2
		endif
		poly2_b 5, 1+2+4+16+32, gs_penLeaf_fg, gs_penLeaf_bg,
			-a/2-CFrameWallThk,	0,				32 + 1,
			-a/2-CFrameWallThk,	gs_leaf_thk,	32 + 1,
			a/2+CFrameWallThk,	gs_leaf_thk,	32 + 1,
			a/2+CFrameWallThk,	0,				32 + 1,
			-a/2-CFrameWallThk,	0,				-1
		del 1
	endif

	if symb_2d = 100 then
		add2 0, -gs_frame_thk/2
		line2 -a/2, 0, a/2, 0
		del 1
	endif

	if symb_2d = 200 then
		line2 -a/2, 0, a/2, 0
	endif

	if gs_iSwingType = 3 | gs_iSwingType = 4 then	! LINE_SHAPE_SWING_SIMPLE_LINE or LINE_SHAPE_SWING_LEAF_LINE
		drawindex 10

		pen gs_opLinePen
		line_type gs_swingLineType

		add2 0, (-gs_frame_thk/2) * (lod2D = DET2D_MIDDLE | lod2D = DET2D_DETAILED)
		hotspot2 0, -0.50, unID_Opening
		hotspot2 0, 0.50, unID_Opening + 1
		line2 0, -0.50, 0, 0.50
		del 1
	endif
endif
if lod2D <> DET2D_SIMPLE_CHI & lod2D <> DET2D_DETAILED_CHI then del 1

