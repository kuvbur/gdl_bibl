
ui_dialog `Параметры Стола`

ui_current_page gs_ui_current_page



iPageIDRoot 			=-1
iPageIDStyle			= 1
iPageIDAccessories		= 2
iPageIDLeg				= 3
iPageIDMaterials		= 4
iPageIDRepMinSpace		= 5

pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

	UI_PAGE iPageIDStyle, iPageIDRoot, `Стиль Стола`, "uiTab_Style_flat(1)"
	gosub "stylePage"

	UI_PAGE iPageIDAccessories, iPageIDRoot, `Аксессуары`, "uiTab_Drawer_flat(1)"
		gosub "accessoriesPage"

	UI_PAGE iPageIDLeg, iPageIDRoot, `Ножки`, "uiTab_Leg_flat(1)"
		gosub "legPage"

	UI_PAGE iPageIDMaterials, iPageIDRoot, `Покрытия`, "uiTab_3DRepr_flat(1)"
		gosub "materialPage"

	UI_PAGE iPageIDRepMinSpace, iPageIDRoot, `Отображение и Минимальное Пространство`, "uiTab_2DRepr_flat(1)"
		gosub "repMinSpacePage"


! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"stylePage":
! ------------------------------------------------------------------------------

if iTableCode = FORM_ROUND then
	idxPicture = 2		! RoundTableTypes.png
else
	idxPicture = 1		! RectTableTypes.png
endif

ui_infield{3} "iPresetTableStyle", 0, pageStartY, 443, 225,
		1, idxPicture, 6, 2,
		110, 110, 88, 88,
		1, listPresetTable[1], TABLE_PRESET_DINING,
		2, listPresetTable[2], TABLE_PRESET_KITCHEN,
		3, listPresetTable[3], TABLE_PRESET_SMALL,
		4, listPresetTable[4], TABLE_PRESET_LOBBY,
		5, listPresetTable[5], TABLE_PRESET_GARDEN,
		6, listPresetTable[6], TABLE_PRESET_CUSTOM


return

! ------------------------------------------------------------------------------
"accessoriesPage":
! ------------------------------------------------------------------------------

file_dependence "ui_frlowrect(14)"
file_dependence	"ui_frupperrect(14)"
file_dependence	"ui_shrect(14)"
file_dependence	"ui_frlowshrect(14)"
file_dependence	"ui_drrfrect(14)"
file_dependence	"ui_drshrfrect(14)"
file_dependence	"ui_drshrect(14)"
file_dependence	"ui_drawrect(14)"
file_dependence	"ui_fruppershrect(14)"
file_dependence	"ui_alaprect(14)"

structurePictName = "ui_frlowrect(14)"
if frame then
	if drawer then
		if shelf then
			structurePictName = "ui_drshrfrect(14)"
		else
			structurePictName = "ui_drrfrect(14)"
		endif
	else
		if iPositionFrame = FRAMEPOS_UPPER and not(shelf) then
			structurePictName = "ui_frupperrect(14)"
		endif
		if iPositionFrame = FRAMEPOS_LOWER and not(shelf) then
			structurePictName = "ui_frlowrect(14)"
		endif
		if iPositionFrame = FRAMEPOS_LOWER and shelf then
				structurePictName = "ui_fruppershrect(14)"
		endif
		if iPositionFrame = FRAMEPOS_UPPER and shelf then
				structurePictName = "ui_frlowshrect(14)"
		endif
	endif
else
	if drawer then
		if shelf then
			structurePictName = "ui_drshrect(14)"
		else
			structurePictName = "ui_drawrect(14)"
		endif
	else
		if shelf then
				structurePictName = "ui_frupperrect(14)"
		else
			structurePictName = "ui_alaprect(14)"
		endif
	endif
endif

	ui_pict	structurePictName,	50,	pageStartY + 15,	95,	91,3

dim stAccPars[10]
	stAccPars[1] = "numDrawer"
	stAccPars[2] = "heightDrawer"
	stAccPars[3] = "gs_knob_type"
	stAccPars[4] = "positionFrame"
	stAccPars[5] = "heightFrame"
	stAccPars[6] = "thickFrame"

	if iTableCode = FORM_RECT then
		stAccPars[7] = "frameSection"
	else
		stAccPars[7] = "styleFrame"
	endif

	stAccPars[8] = "depthFrame"
	stAccPars[9] = "heightShelf"
	stAccPars[10] = "thickShelf"

dim stAccNames[10]
	stAccNames[1] = `Количество Ящиков`
	stAccNames[2] = `Высота Ящиков`
	stAccNames[3] = `Стиль Ручки`
	stAccNames[4] = `Расположение Царги`
	stAccNames[5] = `Высота Царги`

if iTableCode = FORM_RECT then
	stAccNames[6] = `Толщина Царги`
	stAccNames[7] = `Сечение Царги`
else
	stAccNames[6] = `Толщина Царги`
	stAccNames[7] = `Форма Царги`
endif

	stAccNames[8] = `Ширина Царги`
	stAccNames[9] = `Высота Полки`
	stAccNames[10] = `Толщина Полки`

x1=12
x2=150
	yCurrent = pageStartY + 135

ui_infield{3} "drawer", x1, yCurrent-2, x2-x1-2, 18,			! Drawer
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, `Ящики`, 0,
				1, `Ящики`, 1

yCurrent = yCurrent + 25

for iAccessories = 1 to 3
	ui_outfield	stAccNames[iAccessories],	x1,	yCurrent,		x2-x1-2,	13
	ui_infield	stAccPars[iAccessories],	x2,	yCurrent-4,		215-x2,		22
	yCurrent = yCurrent + 25
next iAccessories

x1=230
x2=365
	yCurrent = pageStartY + 10

ui_infield{3} "frame", x1, yCurrent-2, x2-x1-2, 18,			! Frame
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, `Царга`, 0,
				1, `Царга`, 1

yCurrent = yCurrent + 25

for iAccessories = 4 to 8
	ui_outfield	stAccNames[iAccessories],	x1,	yCurrent,		x2-x1-2,	13
	ui_infield	stAccPars[iAccessories],	x2,	yCurrent-4,		430-x2,		22
	yCurrent = yCurrent + 25
next iAccessories

ui_infield{3} "shelf", x1, yCurrent-2, x2-x1-2, 18,			! Shelf
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, `Полка`, 0,
				1, `Полка`, 1

yCurrent = yCurrent + 25

for iAccessories = 9 to 10
	ui_outfield	stAccNames[iAccessories],	x1,	yCurrent,		x2-x1-2,	13
	ui_infield	stAccPars[iAccessories],	x2,	yCurrent-4,		430-x2,		22
	yCurrent = yCurrent + 25
next iAccessories


return

! ------------------------------------------------------------------------------
"legPage":
! ------------------------------------------------------------------------------

x1 = 300
x2 = 375
	yCurrent = pageStartY

	ui_infield{3} "iStyleLeg", 0, pageStartY, 295, 225,
		1, "tablelegsSmall(1)", 8, 2,
		63, 110, 60, 90,
		2, `Круглая`, LEG_ROUND,
		1, `Квадратные`, LEG_BLOCK,
		3, `Конические`, LEG_CONIC,
		4, `Фигурные`, LEG_BALUSTER,
		5, `Специальные`, LEG_DESIGN,
		6, `Скрещенные`, LEG_FOLDING,
		7, `Сплошные`, LEG_SOLID,
		8, `Грубые`, LEG_RUSTIC

ui_style 0, 1
ui_outfield `Ножки`, x1, yCurrent, 142, 15
yCurrent = yCurrent + 21
ui_style 0, 0

if iTableCode = FORM_ROUND then 
	ui_outfield `Кол-во Ножек`, x1, yCurrent, x2-x1, 15, 4 * (iStyleLeg = LEG_FOLDING | iStyleLeg = LEG_SOLID | iStyleLeg = LEG_RUSTIC)
	ui_infield{3} "numLeg", x2, yCurrent-4, 68, 19,
		8, "", 0, 0, 0, 0, 0, 0,
		"", "1", 1,
		"", "2", 2,
		"", "3", 3,
		"", "4", 4
yCurrent = yCurrent + 22
endif

if iTableCode = FORM_ROUND then 
	ui_outfield `Длина`, x1, yCurrent, x2-x1, 15, 4 * (iStyleLeg <> LEG_FOLDING and iStyleLeg <> LEG_BLOCK)
else
	ui_outfield `Длина`, x1, yCurrent, x2-x1, 15, 4 * (iStyleLeg <> LEG_SOLID and iStyleLeg <> LEG_BLOCK)
endif

ui_infield "depthLeg", x2, yCurrent-4, 68, 19
yCurrent = yCurrent + 21

ui_outfield `Толщина/Диаметр`, x1, yCurrent, 142, 15
yCurrent = yCurrent + 22

ui_infield "widthLeg", x2, yCurrent-4, 68, 19
yCurrent = yCurrent + 22

ui_style 0, 1
ui_outfield `Столешница`, x1, yCurrent, x2-x1, 15
ui_style 0, 0
if iTableCode = FORM_ROUND then
	ui_infield{3} "iTypeTableEnd", x2, yCurrent-4, 68, 19,
		8, "", 0, 0, 0, 0, 0, 0,
		"", `Дугообразная`, TABLEEND_ARC,
		"", `Эллиптическая`, TABLEEND_ELLIP
endif
yCurrent = yCurrent + 22

ui_outfield `Свес`, x1, yCurrent, x2-x1, 15
ui_infield "widthOverhang", x2, yCurrent-4, 67, 19
yCurrent = yCurrent + 22

ui_outfield `Толщина`, x1, yCurrent, x2-x1, 15
ui_infield "thickTabletop", x2, yCurrent-4, 67, 19
yCurrent = yCurrent + 22

ui_outfield `Ширина Царги`, x1, yCurrent, x2-x1, 15
ui_infield "widthTabletopFrame", x2, yCurrent-4, 67, 19
yCurrent = yCurrent + 21

ui_outfield `Толщина Царги`, x1, yCurrent, 142, 15
yCurrent = yCurrent + 21
ui_infield "thickTabletopFrame", x2, yCurrent-4, 67, 19

return

! ------------------------------------------------------------------------------
"materialPage":
! ------------------------------------------------------------------------------

x1=12
x2=280

UI_STYLE 0,1
	UI_OUTFIELD	`Покрытия`,	x1,	pageStartY,	x2-x1,	13

UI_STYLE 0,0

dim stMatPars[7]
	stMatPars[1] = "gs_top_mat"
	stMatPars[2] = "gs_topedge_mat"
	stMatPars[3] = "gs_leg_mat"
	stMatPars[4] = "fmat"
	stMatPars[5] = "smat"
	stMatPars[6] = "dmat"
	stMatPars[7] = "gs_knob_mat"

dim stMatNames[7]
	stMatNames[1] = `Столешница`
	stMatNames[2] = `Торец Столешницы`
	stMatNames[3] = `Ножки`
	stMatNames[4] = `Царга`
	stMatNames[5] = `Полка`
	stMatNames[6] = `Ящики`
	stMatNames[7] = `Ручка`

	yCurrent = pageStartY + 25
for iMat = 1 to 7
	UI_OUTFIELD	stMatNames[iMat],	x1,	yCurrent,		x2-x1-2,	13
	UI_INFIELD	stMatPars[iMat],	x2,	yCurrent-4,		430-x2,		26
	yCurrent = yCurrent + 30
next iMat


return

! ------------------------------------------------------------------------------
"repMinSpacePage":
! ------------------------------------------------------------------------------

	UI_SEPARATOR	5,		pageStartY + 135,		440,	pageStartY + 135
	UI_SEPARATOR	220,	pageStartY,				220,	pageStartY + 130


x1 = 12
x2 = 175
x3 = 128
x4 = 235
x5 = 350

	yCurrent = pageStartY + 21

UI_STYLE 0,1
	UI_OUTFIELD	`2D-отображение`,	x1,		pageStartY,		215-x1,	15

UI_STYLE 0,0
UI_OUTFIELD	`Перо Контура`,		x1,		yCurrent,		x2-x1-2,	15
UI_INFIELD "gs_cont_pen",		x2,		yCurrent-4,		215-x2,		22
yCurrent = yCurrent + 27

UI_OUTFIELD	`Тип Штриховки`,		x1,		yCurrent,		x3-x1-2,	15
UI_INFIELD "gs_fill_type",		x3,		yCurrent-4,		215-x3,		22
yCurrent = yCurrent + 27

UI_OUTFIELD	`Перо Штриховки`,			x1,		yCurrent,		x2-x1-2,	15
UI_INFIELD "gs_fill_pen",		x2,		yCurrent-4,		215-x2,		22
yCurrent = yCurrent + 27

UI_OUTFIELD	`Перо Фона`,	x1,		yCurrent,		x2-x1-2,	15
UI_INFIELD "gs_back_pen",		x2,		yCurrent-4,		215-x2,		22


UI_STYLE 0,1
	UI_OUTFIELD	`3D-отображение`,	x1,	pageStartY + 139,	215-x1,	13

	yCurrent = pageStartY + 163
UI_STYLE 0,0
UI_OUTFIELD	`3D`,				x1,	yCurrent,	x3-x1-2,	13
UI_INFIELD	"gs_detlevel_3D",	x3,	yCurrent-4,	215-x3,		22
yCurrent = yCurrent + 24

UI_OUTFIELD	`Разрешение`,		x1,	yCurrent,	x3-x1-2,	15
UI_INFIELD	"gs_resol",			x3,	yCurrent-4,	215-x3,		22
yCurrent = yCurrent + 24

UI_INFIELD{3} "gs_shadow",		x1, yCurrent-2, x2-x1-2,	18,			! Shadow
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, `Отбрасывание Теней`, 0,
				1, `Отбрасывание Теней`, 1

	yCurrent = pageStartY + 21

UI_STYLE 0,1
	UI_OUTFIELD	`Минимальное Пространство`,	x4,		pageStartY,			430-x4,		15
UI_STYLE 0,0

if iTableCode = FORM_RECT then
	UI_OUTFIELD	`Спереди`,			x4,		yCurrent,	x5-x4-2,	15
	UI_INFIELD	"MSFront",			x5,		yCurrent-4,	215-x5,		22
	yCurrent = yCurrent + 27
	
	UI_OUTFIELD	`По Бокам`,		x4,		yCurrent,	x5-x4-2,	13
	UI_INFIELD	"MSSide",			x5,		yCurrent-4,	215-x5,	22
	yCurrent = yCurrent + 27
	
	UI_OUTFIELD	`Сзади`,			x4,		yCurrent,	x5-x4-2,	13
	UI_INFIELD	"MSRear",			x5,		yCurrent-4,	215-x5,	22
else
	UI_OUTFIELD	`Вокруг`,			x4,		yCurrent,	x5-x4-2,	15
	UI_INFIELD	"MSFront",			x5,		yCurrent-4,	215-x5,		22
endif

return

