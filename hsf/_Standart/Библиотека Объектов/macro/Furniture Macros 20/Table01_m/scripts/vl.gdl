
values "iTableCode" 1, 2

call "FM_types" parameters all

values "gs_ui_current_page" 1, 2, 3, 4, 5

values "A" range [0.3, )
values "B" range [0.3, )
values "zzyzx" range [.2, )
values "thickTabletop" range (0, zzyzx/2]
values "widthTabletopFrame" range [0, min(A, B)/2-0.1]
values "gs_resol" RANGE [3,)

values "gs_detlevel_3D" stDetLevels
values "gs_detlevel_3D_m" DETLEV_DETAILED, DETLEV_SIMPLE, DETLEV_OFF
values "gs_ui_current_page" 1, 2, 3, 4, 5

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DETLEV_DETAILED
	if gs_detlevel_3D = stDetLevels[2] then gs_detlevel_3D_m = DETLEV_SIMPLE
	if gs_detlevel_3D = stDetLevels[3] then gs_detlevel_3D_m = DETLEV_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetLevels[1]
	if gs_detlevel_3D_m = DETLEV_SIMPLE then gs_detlevel_3D = stDetLevels[2]
	if gs_detlevel_3D_m = DETLEV_OFF then gs_detlevel_3D = stDetLevels[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

values "positionFrame" stFramePos
values "iPositionFrame" FRAMEPOS_LOWER, FRAMEPOS_UPPER

if GLOB_MODPAR_NAME = "positionFrame" then
	iPositionFrame = FRAMEPOS_LOWER
	if positionFrame = stFramePos[2] then iPositionFrame = FRAMEPOS_UPPER
	parameters iPositionFrame = iPositionFrame
else
	positionFrame = stFramePos[1]
	if iPositionFrame = FRAMEPOS_UPPER then positionFrame = stFramePos[2]
	parameters positionFrame = positionFrame
endif

if iTableCode = FORM_RECT then
	values "frameSection" stFrameSect
	values "iFrameSection" FRAMESECT_RECT, FRAMESECT_ROUND
	
	if GLOB_MODPAR_NAME = "frameSection" then
		iFrameSection = FRAMESECT_RECT
		if frameSection = stFrameSect[2] then iFrameSection = FRAMESECT_ROUND
		parameters iFrameSection = iFrameSection
	else
		frameSection = stFrameSect[1]
		if iFrameSection = FRAMESECT_ROUND then frameSection = stFrameSect[2]
		parameters frameSection = frameSection
	endif
endif

values "styleLeg" stLegStyle
values "iStyleLeg" LEG_ROUND, LEG_BLOCK, LEG_CONIC, LEG_BALUSTER, LEG_DESIGN, LEG_FOLDING, LEG_SOLID, LEG_RUSTIC

if GLOB_MODPAR_NAME = "styleLeg" then
	iStyleLeg = LEG_ROUND
	for i = 2 to vardim1(stLegStyle)
		if styleLeg = stLegStyle[i] then iStyleLeg = i
	next i
	parameters iStyleLeg = iStyleLeg
else
	styleLeg = stLegStyle[max(1, min(iStyleLeg, vardim1(stLegStyle)))]
	parameters styleLeg = styleLeg
endif

if iTableCode = FORM_ROUND then
	values "styleFrame" stFrameStyle
	values "iStyleFrame" FRAMESTYLE_ARC, FRAMESTYLE_STRAIGHT	
	if GLOB_MODPAR_NAME = "styleFrame" then
		iStyleFrame =  FRAMESTYLE_ARC
		if styleFrame = stFrameStyle[2] then iStyleFrame = FRAMESTYLE_STRAIGHT
		parameters iStyleFrame = iStyleFrame
	else
		styleFrame = stFrameStyle[1]	! arc
		if iStyleFrame = FRAMESTYLE_STRAIGHT then styleFrame = stFrameStyle[2]
		parameters styleFrame = styleFrame
	endif
	
	if iStyleFrame =  FRAMESTYLE_ARC then
		lock "thickFrame"
		lock "depthFrame"
	endif
	
	values "typeTableEnd" stTableEnd
	values "iTypeTableEnd" TABLEEND_ARC, TABLEEND_ELLIP
	
	if GLOB_MODPAR_NAME = "typeTableEnd" then
		iTypeTableEnd = TABLEEND_ARC
		if typeTableEnd = stTableEnd[2] then iTypeTableEnd = TABLEEND_ELLIP
		parameters iTypeTableEnd = iTypeTableEnd
	else
		typeTableEnd = stTableEnd[1]
		if iTypeTableEnd = TABLEEND_ELLIP then typeTableEnd = stTableEnd[2]
		parameters typeTableEnd = typeTableEnd
	endif
	
	if GLOB_MODPAR_NAME="styleLeg" or GLOB_MODPAR_NAME = "iStyleLeg" then
		if iStyleLeg = LEG_BALUSTER or iStyleLeg = LEG_DESIGN then iPositionFrame =  FRAMEPOS_UPPER
		parameters iPositionFrame = iPositionFrame
		lock "positionFrame"
	endif

	if iStyleLeg = LEG_FOLDING then
		values "widthOverhang" range [0, min(A, B)/4]
	else
		values "widthOverhang" range (, min(A, B)/2-0.1]
	endif
	
	if iStyleLeg = LEG_CONIC then
		values "heightFrame" range [0, (zzyzx-thickTabletop)/2-thickFrame]
	else
		values "heightFrame" range [0, zzyzx-thickTabletop-thickFrame]
	endif
	if iStyleLeg = LEG_DESIGN or iStyleLeg = LEG_BALUSTER then
		values "thickFrame" range (0, 0.07]
	else
		values "thickFrame" range (0, zzyzx-thickTabletop]
	endif
	if numLeg = 3 then
		values "depthFrame" range (0, min(a,b)/6]
	else
		values "depthFrame" range (0, min(a,b)/3]
	endif

	values "depthLeg" range [0.02, (min(a,b)/2 - widthOverhang) * sin(45)]	!dx/2

	if (numLeg = 3 or numLeg = 4 )  then
		values "widthLeg" range [0.02, min(a,b)/2 - widthOverhang]
	else
		values "widthLeg" range [0.02, max(a,b)/2 - widthOverhang]
	endif
else
	values "widthOverhang" range (, min(A, B)/2-0.1]
	values "heightFrame" range [0, zzyzx-thickTabletop-thickFrame]
	values "thickFrame" range [0, zzyzx-thickTabletop-heightFrame]
endif

values "heightDrawer" range (thickTabletop+0.04, ZZYZX-thickTabletop]


dim stKnobTypes[9]
	stKnobTypes[1] = `Нет`
	stKnobTypes[2] = `Стиль 1`
	stKnobTypes[3] = `Стиль 2`
	stKnobTypes[4] = `Стиль 3`
	stKnobTypes[5] = `Стиль 4`
	stKnobTypes[6] = `Стиль 5`
	stKnobTypes[7] = `Стиль 6`
	stKnobTypes[8] = `Стиль 7`
	stKnobTypes[9] = `Стиль 8`

values "gs_knob_type" stKnobTypes
values "gs_knob_type_m" 0,1,2,3,4,5,6,7,8

if GLOB_MODPAR_NAME = "gs_knob_type" then
	for iType = 1 to 9
		if gs_knob_type = stKnobTypes[iType] then
			gs_knob_type_m = iType-1
			parameters gs_knob_type_m = gs_knob_type_m
		endif
	next iType
else
	actKnobIndex = max (1, min (gs_knob_type_m + 1, vardim1(stKnobTypes) ) )
	gs_knob_type = stKnobTypes[actKnobIndex]
	parameters gs_knob_type = gs_knob_type
endif

values{2} "iPresetTableStyle" 	TABLE_PRESET_DINING, listPresetTable[1],
								TABLE_PRESET_KITCHEN, listPresetTable[2],
								TABLE_PRESET_SMALL, listPresetTable[3],
								TABLE_PRESET_LOBBY, listPresetTable[4],
								TABLE_PRESET_GARDEN, listPresetTable[5],
								TABLE_PRESET_CUSTOM, listPresetTable[6]

if widthTabletopFrame < EPS then lock "gs_topedge_mat"

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19

IF gs_detlevel_3D_m = DETLEV_SIMPLE THEN HIDEPARAMETER "gs_resol"
IF gs_detlevel_3D_m = DETLEV_OFF THEN HIDEPARAMETER "gs_resol", "gs_shadow"

if iTableCode = FORM_RECT then
	if GLOB_MODPAR_NAME = "iPresetTableStyle" then
		! Dining Table
		! ------------------------------------------------------------
		if iPresetTableStyle = TABLE_PRESET_DINING then
			parameters 	a 	= 	1,
						b 	= 	1,
						zzyzx = 0.75,
						iStyleLeg = LEG_BLOCK,
						gs_leg_mat = 11,
						widthOverhang = 0.08,
						frame =	1,
						iFrameSection = FRAMESECT_RECT,
						heightFrame = 0.5,
						fmat 	= 	11,
						shelf 	= 	0,
						drawer 	= 	0
		else
			! Kitchen Table
			! --------------------------------------------------------
			if iPresetTableStyle = TABLE_PRESET_KITCHEN then
				parameters 	a = 	1.5,
							b = 	1,
							zzyzx = 	0.9,
							iStyleLeg = LEG_BLOCK,
							gs_leg_mat = 15,
							widthOverhang = 0.04,
							frame = 		0,
							shelf = 		0,
							drawer = 		1,
							numDrawer = 	2,
							heightDrawer = 	0.25,
							gs_knob_type_m = 5
			else
				! Small Table
				! ----------------------------------------------------
				if iPresetTableStyle = TABLE_PRESET_SMALL then
					parameters 	a 				=	1.2,
								b 				= 	0.8,
								zzyzx 			= 	0.625,
								iStyleLeg		= LEG_CONIC,
								gs_leg_mat 		=	11,
								widthOverhang 	=	0.1,
								frame 			= 	1,
								iFrameSection	= FRAMESECT_ROUND,
								heightFrame 	= 	0.285,
								shelf			= 	1,
								heightShelf		= 	0.3,
								thickShelf		= 	0.002,
								smat			= 	29,
								drawer			= 	0
				else
					! Lobby Table
					! ------------------------------------------------
					if iPresetTableStyle = TABLE_PRESET_LOBBY then
						parameters 	a 			= 		0.6,
									b 			= 		0.6,
									zzyzx 		=		1,
									iStyleLeg 	= LEG_ROUND,
									gs_leg_mat 	=		11,
									widthOverhang =		0.05,
									frame 		= 		0,
									shelf 		= 		0,
									drawer 		=		1,
									numDrawer 	=		1,
									heightDrawer 	=	0.15,
									gs_knob_type_m 	=	6
	
					else
						! Garden Table
						! --------------------------------------------
						if iPresetTableStyle = TABLE_PRESET_GARDEN then
							parameters 	a = 			1.5,
										b = 			1.2,
										zzyzx = 		0.75,
										iStyleLeg = LEG_FOLDING,
										widthOverhang = 0.08,
										frame = 		0,
										shelf = 		0,
										drawer = 		0
						endif
					endif
				endif
			endif
		endif
	endif
else
	if GLOB_MODPAR_NAME = "iPresetTableStyle" then
		! Dining Table
		! ------------------------------------------------------------
		if iPresetTableStyle = TABLE_PRESET_DINING then
			if LibraryLangCode = "USA" then
				a				= 	1.2192
				b				= 	0.9144
				zzyzx			=	0.762
				widthOverhang	=	0.1016
				heightFrame		=	0.508
			else
				a				= 	1
				b				= 	1
				zzyzx			=	0.75
				widthOverhang	=	0.08
				heightFrame		=	0.5
			endif

			iStyleLeg		=	LEG_BLOCK
			numLeg			=	4
			gs_leg_mat		=	11
			frame			=	1
			fmat			=	11
			shelf			=	0
			drawer			=	0
		endif
	
		! Kitchen Table
		! --------------------------------------------------------
		if iPresetTableStyle = TABLE_PRESET_KITCHEN then
			if LibraryLangCode = "USA" then
				a				=	1.524
				b				=	0.9906
				zzyzx			=	0.9144
				widthOverhang	=	0.0508
				heightDrawer	=	0.254
			else
				a				=	1.5
				b				=	1
				zzyzx			=	0.9
				widthOverhang	=	0.04
				heightDrawer	=	0.25
			endif

			iStyleLeg		=	LEG_BLOCK
			numLeg			=	4
			gs_leg_mat		=	15
			frame			= 	0
			shelf			=	0
			drawer			=	1
			numDrawer		=	2
			gs_knob_type_m	=	5
		endif
	
		! Small Table
		! ----------------------------------------------------
		if iPresetTableStyle = TABLE_PRESET_SMALL then
			if LibraryLangCode = "USA" then
				a 				=	1.2192
				b 				= 	0.8128
				zzyzx 			= 	0.6223
				widthOverhang 	=	0.1016
				heightFrame 	= 	0.2794
				heightShelf		= 	0.3048
				thickShelf		= 	0.003175
			else
				a 				=	1.2
				b 				= 	0.8
				zzyzx 			= 	0.625
				widthOverhang 	=	0.1
				heightFrame 	= 	0.285
				heightShelf		= 	0.3
				thickShelf		= 	0.002
			endif

			iStyleLeg		=	LEG_CONIC
			numLeg          =   4
			gs_leg_mat 		=	11
			frame 			= 	1
			shelf			= 	1
			smat			= 	29
			drawer			= 	0
		endif
	
		! Lobby Table
		! ------------------------------------------------
		if iPresetTableStyle = TABLE_PRESET_LOBBY then
			if LibraryLangCode = "USA" then
				a				= 	0.6096
				b				= 	0.6096
				zzyzx			=	0.9906
				widthOverhang	=	0.0508
				heightDrawer	=	0.1524
			else
				a				= 	0.6
				b				= 	0.6
				zzyzx			=	1
				widthOverhang	=	0.05
				heightDrawer	=	0.15
			endif

			iStyleLeg		=	LEG_ROUND
			numLeg			=	4
			gs_leg_mat		=	11
			frame			= 	0
			shelf			=	0
			drawer			=	1
			numDrawer		=	1
			gs_knob_type_m	=	6
		endif
	
		! Garden Table
		! --------------------------------------------
		if iPresetTableStyle = TABLE_PRESET_GARDEN then
			if LibraryLangCode = "USA" then
				a				=	1.524
				b				=	1.2192
				zzyzx			=	0.762
				widthOverhang	=	0.0762
			else
				a				=	1.5
				b				=	1.2
				zzyzx			=	0.75
				widthOverhang	=	0.08
			endif

			iStyleLeg		=	LEG_FOLDING
			gs_leg_mat		=	11
			frame			=	0
			shelf			=	0
			drawer			=	0
		endif
	
		parameters	A				= A,
					B				= B,
					ZZYZX			= ZZYZX,
					iStyleLeg		= iStyleLeg,
					numLeg			= numLeg,
					gs_leg_mat		= gs_leg_mat,
					widthOverhang	= widthOverhang,
					frame			= frame,
					heightFrame		= heightFrame,
					fmat			= fmat,
					shelf			= shelf,
					drawer			= drawer,
					shelf			= shelf,
					heightShelf		= heightShelf,
					thickShelf		= thickShelf,
					smat			= smat,
					drawer			= drawer,
					heightDrawer	= heightDrawer,
					numDrawer		= numDrawer,
					gs_knob_type_m	= gs_knob_type_m
	endif
endif

! set back to custom if main parameters are changed
if iPresetTableStyle <> TABLE_PRESET_CUSTOM then
	if iTableCode = FORM_RECT then
		if GLOB_MODPAR_NAME = "frame" or GLOB_MODPAR_NAME = "drawer" or GLOB_MODPAR_NAME = "shelf" or GLOB_MODPAR_NAME = "styleLeg" or GLOB_MODPAR_NAME = "iStyleLeg" then
			iPresetTableStyle = TABLE_PRESET_CUSTOM
			parameters iPresetTableStyle = iPresetTableStyle
		endif
	else
		if GLOB_MODPAR_NAME = "frame" or GLOB_MODPAR_NAME = "drawer" or GLOB_MODPAR_NAME = "shelf" or \
		GLOB_MODPAR_NAME = "styleLeg" or GLOB_MODPAR_NAME = "iStyleLeg" or GLOB_MODPAR_NAME = "numLeg" then
			iPresetTableStyle = TABLE_PRESET_CUSTOM
			parameters iPresetTableStyle = iPresetTableStyle
		endif
	endif
endif

if iTableCode = FORM_RECT then
	if GLOB_MODPAR_NAME = "styleLeg" or GLOB_MODPAR_NAME = "iStyleLeg" then
		if iStyleLeg <> LEG_SOLID then
			widthLeg = 0.05
			parameters widthLeg = widthLeg
			depthLeg = 0.05
			parameters depthLeg = depthLeg
		else
			widthLeg = 0.03
			parameters widthLeg = widthLeg
			depthLeg = max(0.200, B - 0.200)
			parameters depthLeg = depthLeg
		endif
	endif
	
	if GLOB_MODPAR_NAME = "drawer"		then
		if drawer  THEN iPositionFrame = FRAMEPOS_LOWER
		parameters iPositionFrame = iPositionFrame
		lock "positionFrame", "iPositionFrame"
	endif
	
	if iStyleLeg = LEG_SOLID then
		lock "frame"
		frame = 0
		parameters frame = frame
	endif
	
	if iStyleLeg = LEG_FOLDING then
		frame = 0
		parameters frame = frame
		lock "frame"
		shelf = 0
		parameters shelf = shelf
		lock "shelf"
		drawer = 0
		parameters drawer = drawer
		lock "drawer"
	endif
	
	if iStyleLeg = LEG_RUSTIC then
		lock "frame"
		lock "frameSection", "iFrameSection"
		lock "positionFrame", "iPositionFrame"
		lock "heightFrame"
		lock "depthFrame"
		shelf = 0
		parameters shelf = shelf
		lock "shelf"
		hideparameter "depthLeg"
		iFrameSection = FRAMESECT_RECT
		parameters iFrameSection = iFrameSection
	endif
	
	
	if iStyleLeg <> LEG_BLOCK and iStyleLeg <> LEG_SOLID then
		lock "depthLeg"
	endif
	
	if iStyleLeg = LEG_DESIGN or iStyleLeg = LEG_BALUSTER then
		lock "positionFrame", "iPositionFrame"
		lock "heightFrame"
		iFrameSection = FRAMESECT_RECT
		parameters iFrameSection = iFrameSection
		lock "frameSection", "iFrameSection"
		hideparameter "depthLeg"
		values "thickFrame" range (, 0.07]
	endif
	
	if iStyleLeg = LEG_ROUND or iStyleLeg = LEG_CONIC then
		hideparameter "depthLeg"
	endif
	if iFrameSection = FRAMESECT_ROUND then lock "depthFrame"
	
	if not(drawer) then
		lock "numDrawer"
		lock "heightDrawer"
		lock "gs_knob_type"
		lock "dmat"
	endif
else
	if iStyleLeg = LEG_SOLID then
		numLeg = 2	: parameters numLeg = numLeg
		frame = 0	: parameters frame  = frame
		lock "depthLeg"
		lock "numLeg"
		lock "frame"
	else
		if iStyleLeg = LEG_RUSTIC then
			numLeg = 2 : parameters numLeg = numLeg
			depthLeg = 2 * (min(a,b)/2 - widthOverhang) * sin(45)
			parameters depthLeg = depthLeg  !depthLeg = dx
			lock "depthLeg"
			lock "numLeg"
			lock "positionFrame", "iPositionFrame"
			lock "heightFrame"
			lock "frame"
			lock "styleFrame", "iStyleFrame"
			lock "shelf"
			lock "depthFrame"
		else
			if iStyleLeg = LEG_FOLDING then
				numLeg = 2 : parameters numLeg = numLeg
				frame  = 0 : parameters frame  = frame
				shelf  = 0 : parameters shelf  = shelf
				lock "numLeg"
				lock "frame"
				lock "shelf"
			else
				if iStyleLeg = LEG_DESIGN then
					values "numLeg" 3,4
				else
					values "numLeg" 1,3,4
				endif
			endif
		endif
	endif
	
	if iStyleLeg <> LEG_SOLID and iStyleLeg <> LEG_RUSTIC and iStyleLeg <> LEG_FOLDING and iStyleLeg <> LEG_BLOCK then
		lock "depthLeg"
		hideparameter "depthLeg"
	endif
	
	if numLeg = 1 then
		drawer = 0
		parameters drawer = drawer
		lock "drawer"
		frame = 0
		parameters frame = frame
		lock "frame"
		shelf = 0
		parameters shelf= shelf
		lock "shelf"
	endif
	
	if numLeg = 3 then
		drawer = 0
		parameters drawer = drawer
		lock "drawer"
		shelf = 0
		parameters shelf = shelf
		lock "shelf"
	endif
	
	
	if drawer then
		parameters iPositionFrame = FRAMEPOS_LOWER
		lock "positionFrame", "iPositionFrame"
	else
		lock "numDrawer"
		lock "heightDrawer"
		lock "gs_knob_type"
		lock "dmat"
	endif
endif

if gs_knob_type_m = 0 or not(drawer) THEN
	HIDEPARAMETER "gs_knob_mat"
	LOCK "gs_knob_mat"
endif

if not(frame) then
	lock "depthFrame"
	if iTableCode = FORM_RECT then
		lock "frameSection", "iFrameSection"
	else
		lock "styleFrame", "iStyleFrame"
	endif
	lock "positionFrame", "iPositionFrame"
	lock "heightFrame"
	lock "thickFrame"
	lock "fmat"
endif

if not(shelf) then
	lock "heightShelf"
	lock "thickShelf"
	lock "smat"
endif

if iTableCode = FORM_RECT then
	if iPositionFrame = FRAMEPOS_UPPER then
		lock "heightFrame"
	endif
else
	if iStyleLeg = LEG_DESIGN or iStyleLeg = LEG_BALUSTER then
		PositionFrame = stFramePos[2]
		iPositionFrame = FRAMEPOS_UPPER
		parameters positionFrame = positionFrame, iPositionFrame = iPositionFrame
		lock "PositionFrame"
	endif
endif

if iPositionFrame = FRAMEPOS_UPPER then
	heightFrame  =zzyzx-thickTabletop-thickFrame
	parameters heightFrame = heightFrame
	lock "heightFrame"
endif


! Minimal Space
if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif

if iTableCode = FORM_RECT then
	if MSSide < 0 then
		MSSide = 0
		parameters MSSide = MSSide
	endif
	if MSRear < 0 then
		MSRear = 0
		parameters MSRear = MSRear
	endif
endif


