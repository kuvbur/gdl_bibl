
if ABS(completeTable_unID - 1) < EPS then
	unID = 100
else
	unID = completeTable_unID
endif

! ======================================================================
! SCALE
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D


! ======================================================================
! TableTop
! ======================================================================

drawindex 40
call "tableTops_m" parameters all	tableTopAngle		= tableTopAngle,
									tableTops_unID		= unID,
									returned_parameters tableTops_unID
unID = tableTops_unID



! ======================================================================
! Chair
! ======================================================================

if (bPartOfLayout or bPartOfWsSolo) and bChair then
	add2 deskChairOrigPosX, deskChairOrigPosY

	if NOT(bCalledAsExtension) then
		add2 tableChairDistX * (iTableTopType = TTOP_STRAIGHT) + xPosDeskChair * (iTableTopType = TTOP_L_SHAPED), tableChairDistY
	else
		add2 extraDeskChairDistX, extraDeskChairDistY
	endif
	rot2 -chairAngle
	add2 -chairWidth / 2, -chairDepth / 2

	call "officeChair_m" parameters all	a					= chairWidth,
										b					= chairDepth,
										zzyzx				= chairHeight,
										bPartOfLayout		= 1,
										gs_back_mat2		= gs_frame_mat,
										gs_back_mat			= gs_seat_mat,
										gs_arm_mat			= gs_frame_mat,
										gs_headrest_mat		= gs_seat_mat,
										gs_legCylind_mat	= gs_frame_mat,
										gs_legStar_mat		= gs_frame_mat,
										rollmat				= gs_frame_mat,
										officeChair_unID	= unID,
										returned_parameters officeChair_unID
	unID = officeChair_unID

	del 3

	if bEnableChairHsEditing then
		if NOT(bCalledAsExtension) then
			if iTableTopType = TTOP_L_SHAPED then
				hotspot2 0, tableChairDistY, unID, xPosDeskChair, 1 + 128, tableChairDistX:			unID = unID + 1
				hotspot2 -1, tableChairDistY, unID, xPosDeskChair, 3 + 128, tableChairDistX:		unID = unID + 1
				hotspot2 xPosDeskChair, tableChairDistY, unID, xPosDeskChair, 2, tableChairDistX:	unID = unID + 1

				hotspot2 xPosDeskChair, 0, unID, tableChairDistY, 1 + 128:							unID = unID + 1
				hotspot2 xPosDeskChair, -1, unID, tableChairDistY, 3 + 128:							unID = unID + 1
				hotspot2 xPosDeskChair, tableChairDistY, unID, tableChairDistY, 2:					unID = unID + 1
			endif
			if iTableTopType = TTOP_STRAIGHT then
				hotspot2 0, tableChairDistY, unID, tableChairDistX, 1 + 128:			unID = unID + 1
				hotspot2 -1, tableChairDistY, unID, tableChairDistX, 3 + 128:			unID = unID + 1
				hotspot2 tableChairDistX, tableChairDistY, unID, tableChairDistX, 2:	unID = unID + 1

				hotspot2 tableChairDistX, 0, unID, tableChairDistY, 1 + 128:			unID = unID + 1
				hotspot2 tableChairDistX, -1, unID, tableChairDistY, 3 + 128:			unID = unID + 1
				hotspot2 tableChairDistX, tableChairDistY, unID, tableChairDistY, 2:	unID = unID + 1
			endif
		else
			hotspot2 extraDeskChairDistX, 0, unID, extraDeskChairDistY, 1 + 128:				unID = unID + 1
			hotspot2 extraDeskChairDistX, -1, unID, extraDeskChairDistY, 3 + 128:				unID = unID + 1
			hotspot2 extraDeskChairDistX, extraDeskChairDistY, unID, extraDeskChairDistY, 2:	unID = unID + 1
		endif
	endif

	del 1
endif



! ======================================================================
! Computer
! ======================================================================

if (bPartOfLayout or bPartOfWsSolo) and NOT(bCalledAsExtension) then
	drawindex 50

	if (det2D = 2 or det2D = 3) and bComputer then
		add2 computerOrigPosX, computerOrigPosY
		rot2 -chairAngle + 180
		mul2 1 - 2 * (SYMB_MIRRORED), 1
		mul2 1 - 2 * (bMirroredComputer), 1

		if bShowKeyboard then
			add2 -0.47 / 2, 0
			call "keyboard_m" parameters all	a					= 0.47,
												b					= 0.17,
												zzyzx				= 0.02,
												gs_detlevel_2D_m	= 1,
												bButtonText			= 0
			del 1
		endif

		if bShowMonitor then
			add2 -0.4 / 2, 0.17 + 0.05
			call "lcdMonitor_m" parameters all	a					= 0.4,
												b					= 0.13,
												zzyzx				= 0.35,
												bPartOfLayout		= 1,
												iMonitorType		= 1,
												pic					= 0
			del 1
		endif

		del 4
	endif
endif



! ======================================================================
! Office Cabinet
! ======================================================================

drawindex 50

if (det2D = 3) and (bOfficeCab1 or bOfficeCab2) then
	gosub "officeCabinet"
endif

! ======================================================================
completeTable_unID = unID
end completeTable_unID
! ======================================================================



"officeCabinet":
	if (iTableTopType = TTOP_L_SHAPED and bMirroredCabPos) then
		add2 tableDepth2 - 0.015, tableLength2
		mul2 1, -1
		rot2 90
		call "OfficeCabinet01_m" parameters all	a		= cabWidth1,
									b					= tableDepth2 - 0.015,
									bPartOfLayout		= 1,
									bTopCover			= 0,
									drawer2d			= 0,
									iCabIndex			= 1,
									gs_knobHorPos		= gs_knobHorPos,
									officeCab01_unID	= unID,
									returned_parameters officeCab01_unID
		unID = officeCab01_unID
		del 3
	else
		add2 -tableLength1 / 2 * bCalledAsExtension, 0

		for ii = 1 + NOT(bOfficeCab1) to 1 + bOfficeCab2
			add2 tableLength1 * (ii = 1), tableDepth1 - 0.015
			mul2 1 - 2 * (ii = 2), 1
			rot2 180
			if NOT(bPartOfLayout or bPartOfWsSolo) then
				call "OfficeCabinet01_m" parameters all	a		= cabWidth1 * (ii = 1) + cabWidth2 * (ii = 2),
											b					= tableDepth1 - 0.015,
											bPartOfLayout		= 1,
											bTopCover			= 0,
											drawer2d			= 0,
											iCabIndex			= ii,
											gs_knobHorPos		= gs_knobHorPos,
											officeCab01_unID	= unID,
											returned_parameters officeCab01_unID
				unID = officeCab01_unID
			else
				call "OfficeCabinet01_m" parameters all	a		= cabWidth1 * (ii = 1) + cabWidth2 * (ii = 2),
											b					= tableDepth1 - 0.015,
											bPartOfLayout		= 1,
											bTopCover			= 0,
											drawer2d			= 0,
											iCabIndex			= ii,
											gs_knobHorPos		= gs_knobHorPos,
											drawer_num_2		= drawer_num,
											drawerThk2			= drawerThk,
											gs_drawerScheme_2	= gs_drawerScheme,
											gs_drawerScheme_m_2	= gs_drawerScheme_m,
											gs_topDrawerH_2		= gs_topDrawerH,
											gs_bottomDrawerH_2	= gs_bottomDrawerH,
											gs_drawerH_1_2		= gs_drawerH_1,
											gs_drawerH_2_2		= gs_drawerH_1,
											gs_drawerH_3_2		= gs_drawerH_1,
											gs_drawerH_4_2		= gs_drawerH_1,
											gs_drawerH_5_2		= gs_drawerH_1,
											gs_drawerH_6_2		= gs_drawerH_1,
											gs_drawerHeights_2	= gs_drawerHeights,
											gs_knobHorPos_2		= gs_knobHorPos,
											officeCab01_unID	= unID,
											returned_parameters officeCab01_unID
				unID = officeCab01_unID
			endif

			del 3
		next ii

		del 1
	endif
return



