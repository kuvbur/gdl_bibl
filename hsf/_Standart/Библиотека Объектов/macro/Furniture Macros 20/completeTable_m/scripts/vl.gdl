
! ======================================================================
! 2D Detail Level
! ======================================================================

if bPartOfLayout or bPartOfWsSolo then
	call "2dDetailLevel" parameters	gs_detlevel_2D		= gs_detlevel_2D,
									gs_detlevel_2D_m	= gs_detlevel_2D_m,
									enable2dDetLevels	= 2 + 4 + 8 + 16 + 32
endif



! ======================================================================
! TableTop
! ======================================================================

call "tableTops_m" parameters all	tableTopAngle			= tableTopAngle,
									enableTableTopTypes		= 2 + 4,
									bOfficeCab1 			= bOfficeCab1,
									bOfficeCab2 			= bOfficeCab2

if NOT(bPartOfLayout) and NOT(bPartOfWsSolo) and iTableTopType = TTOP_L_SHAPED then
	values "deskAngle" stDeskAngle
	values "iDeskAngle" 90, 120

	if GLOB_MODPAR_NAME = "deskAngle" then
		if deskAngle = stDeskAngle[1] then iDeskAngle = 90
		if deskAngle = stDeskAngle[2] then iDeskAngle = 120
		parameters iDeskAngle = iDeskAngle
	else
		if iDeskAngle = 90 then deskAngle = stDeskAngle[1]
		if iDeskAngle = 120 then deskAngle = stDeskAngle[2]
		parameters deskAngle = deskAngle
	endif
endif

if bPartOfLayout or bPartOfWsSolo then
	values "tableTopHeight" range[0.1, )
endif

if bCalledAsExtension then
	minExtDeskCabWidth1	= 0.2 * bOfficeCabForExtDesk1 * bShowCabParams
	minExtDeskCabWidth2	= 0.2 * bOfficeCabForExtDesk2 * bShowCabParams
	minExtDeskWidth		= 0.2 + minExtDeskCabWidth1 + minExtDeskCabWidth2
	minExtDeskDepth		= 0.2

	if GLOB_MODPAR_NAME = "halfExtraDeskWidth" then
		parameters extraDeskWidth = halfExtraDeskWidth * 2
	else
		parameters halfExtraDeskWidth = extraDeskWidth / 2
	endif

	values "extraDeskWidth" range[minExtDeskWidth, )
	values "extraDeskDepth" range[minExtDeskDepth, )

	lock			"halfExtraDeskWidth"
	hideparameter	"halfExtraDeskWidth"
endif



! ======================================================================
! Chair
! ======================================================================

if bPartOfLayout or bPartOfWsSolo then
	call "officeChair_m" parameters all	a						= chairWidth,
										b						= chairDepth,
										zzyzx					= chairHeight,
										bPartOfLayout			= 1,
										bEnableFullSurfaceEdit	= 0,
										gs_back_mat2			= gs_frame_mat,
										gs_back_mat				= gs_seat_mat,
										gs_arm_mat				= gs_frame_mat,
										gs_headrest_mat			= gs_seat_mat,
										gs_legCylind_mat		= gs_frame_mat,
										gs_legStar_mat			= gs_frame_mat,
										rollmat					= gs_frame_mat

	if NOT(bCalledAsExtension) then
		if iTableTopType = TTOP_L_SHAPED then
			if GLOB_MODPAR_NAME = "xPosDeskChair" then
				parameters tableChairDistX = tableChairDistY / (COS(tableTopAngle - 90) / TAN(tableTopAngle - 90 + ATN(xPosDeskChair / tableChairDistY)) + SIN(tableTopAngle - 90))
			else
				parameters xPosDeskChair = -tableChairDistY * TAN(betaForChairPos)
			endif

			if chairPos = CHAIR_POS_L_CORNER then values "tableChairDistX" range [0.1, )
			values "tableChairDistY" range [0.1, )
		else
			values "tableChairDistY" range [0, )
		endif
	else
		values "extraDeskChairDistY" range [0, )

		lock			"extraDeskChairDistX"
		hideparameter	"extraDeskChairDistX"
	endif

	values "chairWidth" range [0.3, )
	values "chairDepth" range [0.3, )
endif

if bPartOfLayout then
	lock			"xPosDeskChair"
	hideparameter	"xPosDeskChair"
endif

! ======================================================================
! Leg
! ======================================================================

if NOT(bCalledAsExtension) then call "fa_desklegtyp" parameters all	enableLegTypes			= 2 + 4 + 8 + 16 + 32 + 64,
																	bEnableLegbaseSurface	= 1

if iTableTopType = TTOP_STRAIGHT and NOT(bPartOfLayout) and NOT(bPartOfWsSolo) then
	if bOfficeCab1 and bOfficeCab2 then
		lock			"gs_leg_type",
						"gs_leg_type_m",
						"gs_leg_mat",
						"legbase_mat"

		hideparameter	"gs_leg_type",
						"gs_leg_type_m",
						"gs_leg_mat",
						"legbase_mat"
	endif
endif

if gs_detlevel_3D_m < 2 or ((gs_knob_type_m = 0 or NOT(bShowCommonCabParams)) and (gs_leg_type_m = 2 or gs_leg_type_m = 4)) then
	lock			"gs_leg_resol"
	hideparameter	"gs_leg_resol"
else
	values "gs_leg_resol" range[3,)
endif

if not(bCalledAsExtension) then
	if iTableTopType = TTOP_L_SHAPED and iInnerEdge = ROUNDED and gs_detlevel_3D_m = 2 then
		values "gs_resol" range[3,)
	else
		lock			"gs_resol"
		hideparameter	"gs_resol"
	endif
endif


! ======================================================================
! Computer
! ======================================================================

if bPartOfLayout or bPartOfWsSolo then
	if NOT(bComputer) then
		lock			"box_mat",
						"feetmat",
						"dispmat",
						"cover_mat",
						"keyb_mat"
		hideparameter	"box_mat",
						"feetmat",
						"dispmat",
						"cover_mat",
						"keyb_mat"
	endif
endif



! ======================================================================
! Front panel
! ======================================================================

if NOT(bCalledAsExtension) then
	if gs_bFrontPanel then
		values "gs_frontPanelTop" range(, zzyzx - topThk]
		values "gs_frontPanelHeight" range(, gs_frontPanelTop]
	else
		lock "gs_frontPanelHeight", "gs_frontPanelTop", "gs_frontPanel_mat"
		hideparameter "gs_frontPanelHeight", "gs_frontPanelTop", "gs_frontPanel_mat"
	endif
endif



! ======================================================================
! Office cabinet
! ======================================================================

if NOT(bCalledAsExtension) then
	if iTableTopType = TTOP_STRAIGHT then
		values "gs_officeCab" stOfficeCab
		values "gs_officeCab_m" 0, 1, 2, 3

		if GLOB_MODPAR_NAME = "gs_officeCab" then
			for iOfficeCab = 1 to vardim1(stOfficeCab)
				if gs_officeCab = stOfficeCab[iOfficeCab] then gs_officeCab_m = iOfficeCab - 1
			next iOfficeCab
			parameters gs_officeCab_m = gs_officeCab_m
		else
			gs_officeCab = stOfficeCab[gs_officeCab_m + 1]
			parameters gs_officeCab = gs_officeCab
		endif
	endif


	bShowDrawer1Params			= 0
	bShowDrawer2Params			= 0
	bShowDrawer1WidthParam		= 0
	bShowDrawer2WidthParam		= 0
	bEnable2ndDrawerCabinet		= (iTableTopType = TTOP_STRAIGHT)
	if bPartOfLayout or bPartOfWsSolo then
		if bShowCabParams or bShowExtDeskCabParams then
			bShowDrawer1Params			= 1
			bShowDrawer1WidthParam		= 1
		endif
	else
		if bOfficeCab1 then
			bShowDrawer1Params		= 1
			bShowDrawer1WidthParam	= 1
		endif
		if bOfficeCab2 then
			bShowDrawer2Params		= 1
			bShowDrawer2WidthParam	= 1
		endif
	endif

	call "OfficeCabinet01_m" parameters all a	= gs_officeCab_width,
					b							= b - 0.015,
					bShowDrawer1Params			= bShowDrawer1Params,
					bShowDrawer2Params			= bShowDrawer2Params,
					bShowDrawer1WidthParam		= bShowDrawer1WidthParam,
					bShowDrawer2WidthParam		= bShowDrawer2WidthParam,
					bPartOfLayout				= bPartOfLayout,
					bEnable2ndDrawerCabinet		= bEnable2ndDrawerCabinet,
					bKnobNoneType				= bKnobNoneType,
					enableDrawerSchemes			= 2 + 4 + 8 + 16 + 32 * (NOT(bPartOfLayout) and NOT(bPartOfWsSolo))
endif

if bPartOfLayout and bCalledAsExtension then
	values "extraDeskCab" stOfficeCab
	values "iExtraDeskCab" 0, 1, 2, 3
	if GLOB_MODPAR_NAME = "extraDeskCab" then
		for iOfficeCab = 1 to vardim1(stOfficeCab)
			if extraDeskCab = stOfficeCab[iOfficeCab] then iExtraDeskCab = iOfficeCab - 1
		next iOfficeCab
		parameters iExtraDeskCab = iExtraDeskCab
	else
		extraDeskCab = stOfficeCab[iExtraDeskCab + 1]
		parameters extraDeskCab = extraDeskCab
	endif
endif









