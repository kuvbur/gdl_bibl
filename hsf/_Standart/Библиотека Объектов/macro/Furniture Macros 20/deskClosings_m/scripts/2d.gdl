
if ABS(deskClosings_unID - 1) < EPS then
	unID = 300
else
	unID = deskClosings_unID
endif

if iExtensionType = EXT_NONE then
	! ======================================================================
	deskClosings_unID = unID
	end deskClosings_unID
	! ======================================================================
endif



! ======================================================================
! SCALE
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D



! ======================================================================
! Extensions (Rounded, Desk, Desk with Chair, Cabinet, Partition)
! ======================================================================

if iExtensionType = EXT_ROUNDED then
	rot2 -180 * NOT(bSymmetricalDeskEnd) - 90 * bSymmetricalDeskEnd
	call "deskExtensions_m" parameters all	iTableTopType			= 6 * NOT(bSymmetricalDeskEnd) + 4 * bSymmetricalDeskEnd,
											b						= extRoundedRad,
											zzyzx					= tableTopHeight,
											bPartOfLayout			= 1,
											gs_leg_type_m			= gs_leg_type_m * (gs_leg_type_m < 5) + (gs_leg_type_m = 5 or gs_leg_type_m = 6)
	del 1
endif
if iExtensionType = EXT_DESK or iExtensionType = EXT_DESK_WCHAIR then
	call "completeTable_m" parameters all	iTableTopType			= TTOP_STRAIGHT,
											tableLength1			= extraDeskWidth,
											tableDepth1				= extraDeskDepth,
											zzyzx					= tableTopHeight,
											bCalledAsExtension		= 1,
											bChair					= (iExtensionType = EXT_DESK_WCHAIR),
											bPartOfLayout			= 1,
											bPartOfWsSolo			= 0,
											bMirroredCabPos			= 0,
											chairPos				= 1,
											bEnableChairHsEditing	= (iExtensionType = EXT_DESK_WCHAIR),
											gs_officeCab_m			= iExtraDeskCab,
											gs_officeCab_width		= gs_officeCab_width,
											gs_officeCab_width_2	= gs_officeCab_width,
											completeTable_unID		= unID,
											returned_parameters completeTable_unID
	unID = completeTable_unID
endif
if iExtensionType = EXT_CABINET then
	for cabNum = 1 to 1 + bSymmetricalDeskEnd
		mul2 -1, 1
		rot2 -90
		call "officeCabinet02_m" parameters all	a					= extCabinetWidth,
												b					= extCabinetDepth,
												zzyzx				= extCabinetHeight,
												bPartOfLayout		= 1,
												toe_mat				= gs_base_mat,
												gs_upper_mat		= gs_door_mat,
												gs_drawer_mat		= gs_door_mat,
												matShelf			= gs_cabinet_mat,
												matDrawerKnob		= gs_knob_mat,
												xDrawerKnobPos		= 0,
												zDrawerKnobPos		= 0,
												drawerKnobSize		= 0.1,
												gs_knob_type_m_up	= gs_knob_type_m,
												gs_knob_p_m			= 0,
												knob_size			= 0.1,
												knob_pl_x			= 0.05,
												officeCab02_unID	= unID,
												returned_parameters officeCab02_unID
		unID = officeCab02_unID
		del 2
	mul2 -1, 1
	next cabNum
	del 1 + bSymmetricalDeskEnd
endif
if iExtensionType = EXT_PARTITION then
	call "partitions_m" parameters all	bCalledAsExtension	= 1,
										partitions_unID		= unID,
										returned_parameters partitions_unID
	unID = partitions_unID
endif



! ======================================================================
! Desk Partitions
! ======================================================================

if iExtensionType = DESK_PARTITION then
	call "partitions_m" parameters all	partitions_unID		= unID,
										returned_parameters partitions_unID
	unID = partitions_unID
endif

! ======================================================================
deskClosings_unID = unID
end deskClosings_unID
! ======================================================================

