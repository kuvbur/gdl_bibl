
if gs_detlevel_3D_m = 0 then end

if not(gs_shadow) then shadow off

if bPartOfLayout then
	if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
		if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then
			iResol = 3
		else
			iResol = 12
		endif
	else
		iResol = gs_resol * 3
	endif
else
	if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
		iResol = 12
	else
		iResol = gs_resol * 3
	endif
endif


! ======================================================================
! TableTop
! ======================================================================

call "tableTops_m" parameters all	gs_resol = iResol



resol iResol

if gs_leg_type_m = 0 then end

! ======================================================================
! Legs
! ======================================================================

if gs_leg_type_m = 1 then
	legDistFromEdge = 0.06
	leg_a = 0.025
	leg_h = zzyzx - topThk - 0.01
endif
if gs_leg_type_m = 2 then
	legDistFromEdge = 0.06
	leg_a = 0.04
	leg_h = zzyzx - topThk - 0.01
endif
if gs_leg_type_m = 3 then
	legDistFromEdge = 0.035 + 0.06
	leg_h = zzyzx - topThk - 0.09
endif
if gs_leg_type_m = 4 then
	legDistFromEdge = 0.06
	leg_h = zzyzx - topThk
endif
if gs_leg_type_m = 5 then
	legDistFromEdge = 0.015
	leg_h = zzyzx - topThk
endif
if gs_leg_type_m = 6 then
	legDistFromEdge = 0.025
	leg_h = zzyzx - topThk
endif

leg_b = 0.04 * (gs_leg_type_m = 2)

if	iTableTopType = TTOP_ARCHED_90 or\
	iTableTopType = TTOP_ARCHED_135 or\
	iTableTopType = TTOP_ARCHED_180 or\
	iTableTopType = TTOP_ARCHED_360 or\
	iTableTopType = TTOP_ARCHED_P_TOP then gosub "legsForArchedDesks"

! ======================================================================
end
! ======================================================================



"legsForArchedDesks":
	if gs_leg_type_m = 1 or gs_leg_type_m = 2 then
		legYpos = legDistFromEdge
	endif
	if gs_leg_type_m = 3 or gs_leg_type_m = 4 then
		leg_a = b - 0.2
		legYpos = b / 2
	endif
	if iTableTopType = TTOP_ARCHED_90 then
		rotz -45 * (gs_leg_type_m = 3 or gs_leg_type_m = 4)
		add -b / 2 * (gs_leg_type_m = 1 or gs_leg_type_m = 2), -b / 2, 0
	else
		add -b / 2 * (iTableTopType = TTOP_ARCHED_180) - b / 2 * SIN(135 / 2) * (iTableTopType = TTOP_ARCHED_135), -b / 2 * COS(135 / 2) * (iTableTopType = TTOP_ARCHED_135), 0
		rotz 90 * (gs_leg_type_m = 3 or gs_leg_type_m = 4)
	endif
		gosub "leg"
	del 2

return

"leg":
	call "fa_desklegtyp" parameters	all a			 = leg_a,
										b			 = leg_b,
										leg_h		 = leg_h,
										gs_resol	 = iResol,
										bFullPanel	 = 0

return

1000:
	roty 90
	gosub 5000
	del 1
return

5000:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	base
	body -1
return

