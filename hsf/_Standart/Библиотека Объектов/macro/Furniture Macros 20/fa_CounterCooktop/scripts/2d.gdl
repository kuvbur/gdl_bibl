
PEN gs_cont_pen

if CountCooktop_unID = 1 then
	unID=200
else
	unID=CountCooktop_unID
endif

!!! *** Sink Streching/ 2D Hotspot Editing
ADD2 temp_aa, temp_bb
	HOTSPOT2 0, CooktopDistY, unID, CooktopDistX, 1+128 : unID = unID+1
	HOTSPOT2 CooktopDistX, CooktopDistY, unID, CooktopDistX, 2 :unID = unID+1
	HOTSPOT2 -1,CooktopDistY, unID, CooktopDistX, 3 : unID = unID+1

	HOTSPOT2 CooktopDistX, 0, unID, CooktopDistY, 1+128 : unID = unID+1
	HOTSPOT2 CooktopDistX, CooktopDistY, unID, CooktopDistY,2 : unID = unID+1
	HOTSPOT2 CooktopDistX, -1, unID, CooktopDistY, 3 : unID = unID+1
DEL 1

unID = 100

IF OverhangHtps2D=1 THEN

	HOTSPOT2 0, b, unID : unID = unID+1
	HOTSPOT2 a/2, b, unID : unID = unID+1

	IF cab_size=0 THEN

		!!! *** Left Overhang of Cabinet
		HOTSPOT2 0,front_cab,unID, left_cab, 1+128 :unID=unID+1
		HOTSPOT2 -1,front_cab,unID, left_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,front_cab,unID, left_cab, 2: unID=unID+1

		HOTSPOT2 0,B-rear_cab,unID, left_cab, 1+128 :unID=unID+1
		HOTSPOT2 -1,B-rear_cab,unID, left_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,B-rear_cab,unID, left_cab, 2: unID=unID+1

		!!! *** Right Overhang of Cabinet
		HOTSPOT2 A,front_cab,unID, right_cab, 1+128 :unID=unID+1
		HOTSPOT2 A+1,front_cab,unID, right_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,front_cab,unID, right_cab, 2: unID=unID+1

		HOTSPOT2 A,B-rear_cab,unID, right_cab, 1+128 :unID=unID+1
		HOTSPOT2 A+1,B-rear_cab,unID, right_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,B-rear_cab,unID, right_cab, 2: unID=unID+1

		!!! *** Front Overhang of Cabinet
		HOTSPOT2 left_cab,0,unID, front_cab, 1+128 :unID=unID+1
		HOTSPOT2 left_cab,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,front_cab,unID, front_cab, 2: unID=unID+1

		HOTSPOT2 A-right_cab,0,unID, front_cab, 1+128 :unID=unID+1
		HOTSPOT2 A-right_cab,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,front_cab,unID, front_cab, 2: unID=unID+1

		!!! *** Rear Overhang of Cabinet
		HOTSPOT2 left_cab,B,unID, rear_cab, 1+128 :unID=unID+1
		HOTSPOT2 left_cab,B+1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

		HOTSPOT2 A-right_cab,B,unID, rear_cab, 1+128 :unID=unID+1
		HOTSPOT2 A-right_cab,B+1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

	ELSE

		!!! *** Hotspots on bounding box
		HOTSPOT2 0,B,unID :unID=unID+1
		HOTSPOT2 0,0,unID :unID=unID+1
		HOTSPOT2 A,0,unID :unID=unID+1
		HOTSPOT2 A,B,unID :unID=unID+1

		!! *** Left Overhang of Cabinet
		HOTSPOT2 0,front_cab+temp_b/2,unID, left_cab, 1+256 :unID=unID+1
		HOTSPOT2 -1,front_cab+temp_b/2,unID, left_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,front_cab+temp_b/2,unID, left_cab, 2: unID=unID+1

		!! *** Right Overhang of Cabinet
		HOTSPOT2 left_cab+temp_a,front_cab+temp_b/2,unID, right_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+temp_a-1,front_cab+temp_b/2,unID, right_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+temp_a+right_cab,front_cab+temp_b/2,unID, right_cab, 2: unID=unID+1

		!! *** Front Overhang of Cabinet
		HOTSPOT2 left_cab+temp_a/2,0,unID, front_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,front_cab,unID, front_cab, 2: unID=unID+1

		!! *** Rear Overhang of Cabinet
		HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b,unID, rear_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b-1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b+rear_cab,unID, rear_cab, 2: unID=unID+1

	ENDIF
ENDIF

unID = 200

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!! *** COUNTER and BACKSPLASH*** !!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
1:

LINE2 -lftmod,0,a+rgtmod,0
LINE2 -lftmod,b+bckmod,a+rgtmod,b+bckmod

! === ===
if drawer2d=1 then
	for I=1 to drawer_num
				line2 0, -0.02, a, -0.02
				add2 0, -0.02
	next I
	del drawer_num
endif

if drawer2d=1 and drawer=1 then
		add2 0,-((drawer_num+1)*0.02)
		line2 0,0,a,0
		del 1
endif


if bBacksplash then
	! Backsplash
	LINE2 0,b-0.02,a,b-0.02
endif

if side_vis2D_m=2 or side_vis2D_m=4 then LINE2 -lftmod,0,-lftmod,b+bckmod
if side_vis2D_m=3 or side_vis2D_m=4 then LINE2 a+rgtmod,0,a+rgtmod,b+bckmod

drawindex 10
FILL gs_fill_type

poly2_b 5, 2+64,gs_fill_pen,gs_back_pen,
		-lftmod,0,1,
		a+rgtmod,0,1,
		a+rgtmod,b+bckmod,1,
		-lftmod,b+bckmod,1,
		-lftmod,0,-1

if drawer2d = 1 then
	poly2_b 5, 2+64,gs_fill_pen,gs_back_pen,
			-lftmod,0-(drawer_num*0.02),1,
			a+rgtmod,0-(drawer_num*0.02),1,
			a+rgtmod,b,1,
			-lftmod,b,1,
			-lftmod,0-(drawer_num*0.02),-1

endif

if drawer2d=1 and drawer=1 then
	poly2_b 5, 2+64,gs_fill_pen,gs_back_pen,
			-lftmod,0-((drawer_num+1)*0.02),1,
			a+rgtmod,0-((drawer_num+1)*0.02),1,
			a+rgtmod,b,1,
			-lftmod,b,1,
			-lftmod,0-((drawer_num+1)*0.02),-1
endif

HOTLINE2 -lftmod,0,a,0
HOTLINE2 a+rgtmod,0,a,b
HOTLINE2 a+rgtmod,b+bckmod,0,b
HOTLINE2 -lftmod,b+bckmod,0,0

!!! *** Minimal Space
reqBMinSpace = 0
call "fa_minimalspace" parameters all MSType = 20, unID = 3000,
							returned_parameters reqBMinSpace,
												MSFillType,
												MSFillPen,
												MSFillBackPen,
												MSContLineType,
												MSContLinePen

FILL gs_fill_type
PEN gs_cont_pen
ADD2 CooktopDistX,CooktopDistY
ADD2 a/2,b
IF CooktopType_m<>0 THEN CALL "fa_CooktopType" PARAMETERS ALL gs_symbol_type_m = 1,
								b2DSymbol = b2DSymbol,
								iCooktopSymbolType = iCooktopSymbolType,
								gs_symbol_pen = gs_symbol_pen,
								gs_symbol_fill_type = gs_symbol_fill_type
								 !gs_hotspotUnIdBegin = 300
DEL 1
DEL 1

! === opening line ===
line_type gs_open_line

if gs_panel=1 then
	line2 0,0+front_cab,0,-a+front_cab
	arc2 0,0+front_cab,a,270,360
else
	if gs_panel=2 then
		line2 0,0+front_cab,0,-a/2+front_cab
		arc2 0,0+front_cab,a/2,270,360
		line2 a,0+front_cab,a,-a/2+front_cab
		arc2 a,0+front_cab,a/2,180,270
	else
		if gs_panel=3 then
			line2 0,0+front_cab,0,-a/3+front_cab
			arc2 0,0+front_cab,a/3,270,360
			line2 a/3,0+front_cab,a/3,-a/3+front_cab
			arc2 a/3,0+front_cab,a/3,270,360
			line2 a,0+front_cab,a,-a/3+front_cab
			arc2 a,0+front_cab,a/3,180,270
		endif
	endif
endif

end
