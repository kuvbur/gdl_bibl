
if CountCooktop_unID = 1 then
	unID=200
else
	unID=CountCooktop_unID
endif

if gs_detlevel_3D_m=0 then END

if ABS(count_th)<EPS then END

if gs_shadow=0 then SHADOW OFF


PEN gs_cont_pen

RESOL gs_resol


! -- Cooktop Streching/ 3D Hotspot Editing -------------------------------------

unID = unID + 12	! skip old hotspots
add	temp_aa,temp_bb,0

hotspot 0,CooktopDistY,hCountertop,unID,CooktopDistX,1+128 : unID=unID+1
hotspot CooktopDistX,CooktopDistY,hCountertop,unID,CooktopDistX,2 : unID=unID+1
hotspot -1,CooktopDistY,hCountertop,unID,CooktopDistX,3 : unID=unID+1

hotspot CooktopDistX,0,hCountertop,unID,CooktopDistY,1+128 : unID=unID+1
hotspot CooktopDistX,CooktopDistY,hCountertop,unID,CooktopDistY,2 : unID=unID+1
hotspot CooktopDistX,-1,hCountertop,unID,CooktopDistY,3 : unID=unID+1

del 1



1:
! --- Counter and Cooktop  -----------------------------------------------------

	if count_th>0 then
		MATERIAL count_mat
		addz hCountertop-count_th

		if gs_count_type_m=1 OR gs_detlevel_3D_m=1 then
			PRISM_ 4,count_th,
					0,0,13+2*(side_vis_m=2 OR side_vis_m=4),
					a,0, 8+7*(side_vis_m>2),
					a,b,13+2*(side_vis_m>2),
					0,b,8+7*(side_vis_m=2 OR side_vis_m=4)
		endif
		if gs_count_type_m=2 and gs_detlevel_3D_m=2 then
			gosub 6000
			gosub 2000
		endif
		DEL 1
		GOSUB 300
	endif


	! --- Cooktop ---
	add A/2,B,hCountertop
	if CooktopType_m<>0 then GOSUB 400
	DEL 1

	CountCooktop_unID=unID
	END CountCooktop_unID
END


300:
! --- BACK SPLASH --------------------------------------------------------------
	if dob_face=0 then
		if bBacksplash then

			hotspot -lftmod,b+bckmod,hCountertop+bs_h,unID, bs_h, 2 : unID=unID+1
			hotspot -lftmod,b+bckmod,hCountertop,unID, bs_h, 1+128 : unID=unID+1
			hotspot -lftmod,b+bckmod,-1,unID, bs_h, 3 : unID=unID+1


			hotspot a+rgtmod,b+bckmod,hCountertop+bs_h,unID, bs_h, 2 : unID=unID+1
			hotspot a+rgtmod,b+bckmod,hCountertop,unID, bs_h, 1+128 : unID=unID+1
			hotspot a+rgtmod,b+bckmod,-1,unID, bs_h, 3 : unID=unID+1

			add 0,b-0.02+bckmod, hCountertop
			MATERIAL bs_mat
			PRISM_ 4,bs_h,
				-lftmod,0,13+2*(side_vis_m=2 OR side_vis_m=4),
				a+rgtmod,0, 8+7*(side_vis_m>2),
				a+rgtmod,0.02,13+2*(side_vis_m>2),
				-lftmod,0.02,8+7*(side_vis_m=2 OR side_vis_m=4)
			DEL 1
		endif
	endif

	return


400:
! --- COOKTOP ------------------------------------------------------------------

	add CooktopDistX,CooktopDistY,0
	CALL "fa_CooktopType" PARAMETERS ALL
	DEL 1
	return


! --- TEXTURE COORDINATION -----------------------------------------------------
1000:
	roty 90
	gosub 5000
	del 1
	return

2000:
	gosub 5000
	return

5000:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return


6000:
! --- Counter Type = Rounded ---------------------------------------------------

	rr=min (count_th*4/5, 0.02)
	maskk=8+(side_vis_m=2)*1+(side_vis_m=3)*4+(side_vis_m=4)*5

	rotz 90
	rotx 90
	if dob_face = 0 then
		prism_ 9, a,
			0, 0, 15,
			0, 0.0001, maskk,
			0, count_th-rr, 64+maskk,
			0, 10, 800,
			rr, count_th-rr, 900,
			rr, count_th, 3000+maskk,
			b, count_th, 15,
			b, 0, 15,
			0, 0, -1
	else
		prism_ 14, a,
			0, 0, 15,
			0, 0.0001, maskk,
			0, count_th-rr, 64+maskk,
			0, 10, 800,

			rr, count_th-rr, 900,
			rr, count_th, 3000+maskk,
			b-rr, count_th, 64+maskk,
			b-rr+10, count_th, 800,
			b-rr, count_th-rr, 900,
			b, count_th-rr, 3000+maskk,
			b, count_th-rr, 64+maskk,

			b, 0.0001, maskk,
			b, 0, 15,
			0, 0, -1
	endif
	del 2

	return
