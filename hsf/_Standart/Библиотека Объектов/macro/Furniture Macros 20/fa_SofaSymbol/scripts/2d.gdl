

SOFASYMBOL_RECTSIMPLE   = 1
SOFASYMBOL_RECT1        = 2
SOFASYMBOL_RECT2        = 3
SOFASYMBOL_RECT3        = 4
SOFASYMBOL_RECT4        = 5
SOFASYMBOL_CURVEDSIMPLE = 6
SOFASYMBOL_CURVED       = 7

wBackRest = 0.15
dist1=(A-2*wArmRest)/gs_SeatNum_m	!! Seat Width, 1/100
dist2=(A)/gs_SeatNum_m				!! Seat Width, 1/200
DRAWINDEX 30

FILL gs_fill_type
PEN gs_cont_pen

if iSofaSymbolType = SOFASYMBOL_RECTSIMPLE		then gosub "SymbolType1"	!! Type 1 >1/200, rectangle simple
if iSofaSymbolType = SOFASYMBOL_RECT1			then gosub "SymbolType2"	!! Type 2 	Rectangle 1
if iSofaSymbolType = SOFASYMBOL_RECT2			then gosub "SymbolType3"	!! Type 3	Rectangle 2
if iSofaSymbolType = SOFASYMBOL_RECT3			then gosub "SymbolType4"	!! Type 4	Rectangle 3
if iSofaSymbolType = SOFASYMBOL_RECT4			then gosub "SymbolType5"	!! Type 5	Rectangle 4
if iSofaSymbolType = SOFASYMBOL_CURVEDSIMPLE	then gosub "SymbolType6"	!! Type 6 >1/200, Curved simple
if iSofaSymbolType = SOFASYMBOL_CURVED			then gosub "SymbolType7"	!! Type 7	Curved

gosub "minimalSpaceSofa"

! ------------------------------------------------------------------------------
! Hotspots
! ------------------------------------------------------------------------------

IF EditHotspots THEN

	CALL "Resize_A_B_ZZYZX" Parameters ALL

	unID = gs_hotspotUnIdBegin + 20
	HOTSPOT2 A/2,0,unID: unID=unID+1
	HOTSPOT2 A/2,B/2,unID: unID=unID+1
ELSE

	HOTSPOT2 0,0, gs_hotspotUnIdBegin
	HOTSPOT2 A,0, gs_hotspotUnIdBegin + 1
	HOTSPOT2 A,B, gs_hotspotUnIdBegin + 2
	HOTSPOT2 0,B, gs_hotspotUnIdBegin + 3
	unID = gs_hotspotUnIdBegin + 20
!	HOTSPOT2 A/2,0,unID: unID=unID+1
!	HOTSPOT2 A/2,B/2,unID: unID=unID+1
ENDIF

! ==============================================================================
END	!END	!END	!END	!END	!END	!END	!END	!END	!END
! ==============================================================================


! ------------------------------------------------------------------------------
"SymbolType1":
! ------------------------------------------------------------------------------

	gosub "sofaRectPoly"

	LINE2 0,B-wBackRest, A,B-wBackRest

	IF gs_2DSeat = 1 THEN GOSUB 101
RETURN

! ------------------------------------------------------------------------------
"SymbolType2":
! ------------------------------------------------------------------------------

	gosub "sofaRectPoly"

	LINE2 wArmRest,0,           wArmRest,B-wBackRest
	LINE2 A-wArmRest,0,         A-wArmRest,B-wBackRest
	LINE2 wArmRest,B-wBackRest, A-wArmRest,B-wBackRest

	IF gs_2DSeat=1 THEN GOSUB 100
RETURN

! ------------------------------------------------------------------------------
"SymbolType3":
! ------------------------------------------------------------------------------

	gosub "sofaRectPoly"

	LINE2 wArmRest,0,    wArmRest,B-wBackRest
	LINE2 A-wArmRest,0,  A-wArmRest,B-wBackRest
	LINE2 0,B-wBackRest, A,B-wBackRest

	IF gs_2DSeat = 1 THEN GOSUB 100
RETURN

! ------------------------------------------------------------------------------
"SymbolType4":
! ------------------------------------------------------------------------------

	gosub "sofaRectPoly"

	LINE2 wArmRest,0,           wArmRest,B
	LINE2 A-wArmRest,0,         A-wArmRest,B
	LINE2 wArmRest,B-wBackRest, A-wArmRest,B-wBackRest

	IF gs_2DSeat=1 THEN GOSUB 100
RETURN

! ------------------------------------------------------------------------------
"SymbolType5":
! ------------------------------------------------------------------------------

	POLY2_B 13, 1+2+64, gs_fill_pen, gs_back_pen,
			0,            wBackRest/2,1,
			wArmRest/2,   wBackRest/2,1,
			wArmRest/2,   0,1,
			A-wArmRest/2, 0,1,
			A-wArmRest/2, wBackRest/2,1,
			A,            wBackRest/2,1,
			A,            b-wBackRest,1,
			A-wArmRest/2, b-wBackRest,1,
			A-wArmRest/2, b,1,
			wArmRest/2,   b,1,
			wArmRest/2,   b-wBackRest,1,
			0,            b-wBackRest,1,
			0,            wBackRest/2,-1

	LINE2 0,wBackRest/2,           wArmRest,wBackRest/2
	LINE2 A,wBackRest/2,           A-wArmRest,wBackRest/2
	LINE2 wArmRest,wBackRest/2,    wArmRest,B-wBackRest
	LINE2 A-wArmRest,wBackRest/2,  A-wArmRest,B-wBackRest
	LINE2 wArmRest/2,B-wBackRest,  A-wArmRest/2,B-wBackRest
	LINE2 wArmRest/2,wBackRest/2,  wArmRest/2,0
	LINE2 A-wArmRest/2,wBackRest/2,A-wArmRest/2,0

	IF gs_2DSeat=1 THEN GOSUB 100
RETURN

! ------------------------------------------------------------------------------
"SymbolType6":
! ------------------------------------------------------------------------------

	gosub "sofaCurvedPoly"

	IF gs_2DSeat=1 THEN GOSUB 101
RETURN

! ------------------------------------------------------------------------------
"SymbolType7":
! ------------------------------------------------------------------------------

	gosub "sofaCurvedPoly"

	LINE2 wArmRest,0,            wArmRest,B-(wBackRest+wArmRest)
	LINE2 A-wArmRest,0,          A-wArmRest,B-(wBackRest+wArmRest)
	LINE2 2*wArmRest,B-wBackRest,A-2*wArmRest,B-wBackRest

	ARC2 A-2*wArmRest,b-(wBackRest+wArmRest),wArmRest,0,90
	ARC2 2*wArmRest,b-(wBackRest+wArmRest),wArmRest,90,180

	IF gs_2DSeat=1 THEN GOSUB 100
RETURN

! ------------------------------------------------------------------------------
100:
! ------------------------------------------------------------------------------

	FOR k=1 to gs_SeatNum_m-1
		ADD2 wArmRest+dist1,0
		LINE2 0,0, 0,b-wBackRest
		DEL 1

		ADD2 dist1,0
	NEXT k

	DEL gs_SeatNum_m-1
RETURN

! ------------------------------------------------------------------------------
101:
! ------------------------------------------------------------------------------

	FOR k=1 to gs_SeatNum_m-1
		ADD2 dist2,0
		LINE2 0,0,0,b
		DEL 1

		ADD2 dist2,0
	NEXT k

	DEL gs_SeatNum_m-1
RETURN

! ------------------------------------------------------------------------------
"sofaRectPoly":
! ------------------------------------------------------------------------------

	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,b,1,
			0,b,1,
			0,0,-1
return

! ------------------------------------------------------------------------------
"sofaCurvedPoly":
! ------------------------------------------------------------------------------

	POLY2_B 10, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,b-2*wArmRest,1,
			A-2*wArmRest,b-2*wArmRest,901,
			0,90,4001,
			2*wArmRest,B,1,
			2*wArmRest,b-2*wArmRest,901,
			0,90,4001,
			0,b-2*wArmRest,1,
			0,0,-1
return

! ------------------------------------------------------------------------------
"minimalSpaceSofa":
! ------------------------------------------------------------------------------

	DRAWINDEX 10
	reqBMinSpace = 0	!don't draw minimal space if calledAsMacro

	if iSofaSymbolType > 4 then MSType = 0	!self-draw minimal space for these types

	if not(calledAsMacro) and (MSFront > EPS or MSSide > EPS or MSRear > EPS) then
		call "fa_minimalspace" parameters all MSType = MSType, unID = 3000,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen,
													MSFillBackPen,
													MSContLineType,
													MSContLinePen
	endif

	if reqBMinSpace <> 0 and iSofaSymbolType > 4 then
		FILL MSFillType
		PEN MSContLinePen
		line_type  MSContLineType

		if iSofaSymbolType = SOFASYMBOL_RECT4 then
			POLY2_B 18, 1+2+4+64, MSFillPen, MSFillBackPen,
					-MSSide, 	  -MSFront, 	1,
					-MSSide, 	  B+MSRear, 	1,
					A+MSSide, 	  B+MSRear, 	1,
					A+MSSide, 	  -MSFront, 	1,
					-MSSide, 	  -MSFront, 	-1,
					0,			  wBackRest/2, 0,
					wArmRest/2,   wBackRest/2, 0,
					wArmRest/2,   0,           0,
					A-wArmRest/2, 0,           0,
					A-wArmRest/2, wBackRest/2, 0,
					A,            wBackRest/2, 0,
					A,            b-wBackRest, 0,
					A-wArmRest/2, b-wBackRest, 0,
					A-wArmRest/2, b,           0,
					wArmRest/2,   b,           0,
					wArmRest/2,   b-wBackRest, 0,
					0,            b-wBackRest, 0,
					0,            wBackRest/2, -1

			unID = gs_hotspotUnIdBegin + 30
			HOTSPOT2 -MSSide, -MSFront, unID: unID=unID+1
			HOTSPOT2 -MSSide, B+MSRear, unID: unID=unID+1
			HOTSPOT2  A+MSSide, -MSFront, unID: unID=unID+1
			HOTSPOT2  A+MSSide, B+MSRear, unID: unID=unID+1
			HOTSPOT2  A/2, -MSFront, unID: unID=unID+1
		endif

		if iSofaSymbolType = SOFASYMBOL_CURVEDSIMPLE or iSofaSymbolType = SOFASYMBOL_CURVED then
			POLY2_B 15, 1+2+4+64,MSFillPen,MSFillBackPen,
					-MSSide, 	-MSFront, 	1,
					-MSSide, 	B+MSRear, 	1,
					A+MSSide, 	B+MSRear, 	1,
					A+MSSide, 	-MSFront, 	1,
					-MSSide, 	-MSFront, 	-1,
					0,            0,			0,
					A,            0,			0,
					A,            b-2*wArmRest,	0,
					A-2*wArmRest, b-2*wArmRest,	900,
					0,            90,			4000,
					2*wArmRest,   B,			0,
					2*wArmRest,   b-2*wArmRest,	900,
					0,            90,			4000,
					0,            b-2*wBackRest, 0,
					0,            0,			-1
		endif
	endif
return
