
eps = 0.00001

material mat

cushX = A
cushY = B
cushZ = zzyzx

num = 6

sectSizeX = (cushX-0.016)/sectX
sectSizeY = sectSizeX
hh = waveZ/2

alpha = 180-2*(ATN((sectSizeX/2)/hh))
rra = (sectSizeX/2)/SIN(alpha)
beta = 180-2*(ATN(sectSizeY/hh))
rrb = sectSizeY/SIN(beta)

if cushAng > eps then
	if gs_detlevel_3D_m = 2 then
		gosub 100

		add 0.008, 0.004, cushZ-waveZ
		roty cushAng_i/2
		rotx 90
		mulz -1

		xx2 = (sectSizeX/2)*COS(cushAng_i/2)
		zz2 = (sectSizeX/2)*SIN(cushAng_i/2)
		put xx2, zz2, 13
		for isectX = 1 to sectX-1
			xx = cushR*SIN(cushAng_i*isectX)
			zz = cushR-cushR*COS(cushAng_i*isectX)
			put xx, zz, 79
		next isectX
		xx3 = xx+(sectSizeX/2)*COS(cushAng_i*(sectX-0.5))
		zz3 = zz+(sectSizeX/2)*SIN(cushAng_i*(sectX-0.5))
		put xx3, zz3, 8
		put xx3+(cushZ-waveZ)*SIN(cushAng_i*(sectX-0.5)), zz3-(cushZ-waveZ)*COS(cushAng_i*(sectX-0.5)), 13

		cushR2 = cushR + (cushZ-waveZ)/COS(cushAng_i/2)
		for isectX = sectX-1 to 1 step -1
			xx = cushR2*SIN(cushAng_i*isectX)
			zz = cushR-cushR2*COS(cushAng_i*isectX)
			put xx, zz, 79
		next isectX
		put xx2+(cushZ-waveZ)*SIN(cushAng_i/2), zz2-(cushZ-waveZ)*COS(cushAng_i/2), 8
		put xx2, zz2, -1

		prism_ NSP/3, cushY-0.008,
			get(NSP)

		del 4

		add 0.008, 0.004, cushZ-waveZ
		gosub 150
		del 1

		add 0.008, cushY-0.004, cushZ-waveZ
		gosub 150
		del 1

		gosub 140

		xx = cushR*SIN(cushAng)
		zz = cushR-cushR*COS(cushAng)

		add 0.008, 0, cushZ-waveZ
		roty cushAng_i/2
		add xx, 0, zz
		roty -cushAng+cushAng_i/2
		add 0.008, 0, -cushZ+waveZ
		mulx -1
		gosub 140
		del top
	else
		cushAng_i = cushAng/sectX
		sectAng = -cushAng_i/2
		cushR = (sectSizeX/2)/SIN(abs(sectAng))

		add 0.008, 0, cushZ-waveZ
		roty cushAng_i/2
		rotx 90
		mulz -1

		xx2 = (-0.008)*COS(cushAng_i/2)
		zz2 = (-0.008)*SIN(cushAng_i/2)
		put xx2-waveZ*SIN(cushAng_i/2), zz2+waveZ*COS(cushAng_i/2), 15
		cushR2 = cushR - waveZ/COS(cushAng_i/2)
		for isectX = 1 to sectX-1
			xx = cushR2*SIN(cushAng_i*isectX)
			zz = cushR-cushR2*COS(cushAng_i*isectX)
			put xx, zz, 79
		next isectX
		xx = cushR*SIN(cushAng_i*(sectX-1))
		zz = cushR-cushR*COS(cushAng_i*(sectX-1))
		xx3 = xx+(sectSizeX+0.008)*COS(cushAng_i*(sectX-0.5))
		zz3 = zz+(sectSizeX+0.008)*SIN(cushAng_i*(sectX-0.5))
		put xx3-waveZ*SIN(cushAng_i*(sectX-0.5)), zz3+waveZ*COS(cushAng_i*(sectX-0.5)), 15
		put xx3+(cushZ-waveZ)*SIN(cushAng_i*(sectX-0.5)), zz3-(cushZ-waveZ)*COS(cushAng_i*(sectX-0.5)), 15

		cushR2 = cushR + (cushZ-waveZ)/COS(cushAng_i/2)
		for isectX = sectX-1 to 1 step -1
			xx = cushR2*SIN(cushAng_i*isectX)
			zz = cushR-cushR2*COS(cushAng_i*isectX)
			put xx, zz, 79
		next isectX
		put xx2+(cushZ-waveZ)*SIN(cushAng_i/2), zz2-(cushZ-waveZ)*COS(cushAng_i/2), 15
		put xx2-waveZ*SIN(cushAng_i/2), zz2+waveZ*COS(cushAng_i/2), -1

		prism_ NSP/3, cushY,
			get(NSP)

		del 4
	endif
else
	if gs_detlevel_3D_m = 2 then
		gosub 100

		if cutAngle > eps then
			roty -90
			addz -0.004
			put 0, cushY/2, 8
			put cushZ-waveZ, cushY/2, 13
			put cushZ-waveZ, cushY-0.004, 15
			put 0, cushY-(cushZ-waveZ)*TAN(cutAngle)-0.004, 15

			prism_ NSP/3, -cushX+0.008,
				get(NSP)
			del 2

			gosub 121
		else
			ss = 79
			gosub 120
		endif

		prism_ NSP/3, cushZ-waveZ,
			get(NSP)
	else
		if cutAngle > eps then
			addy cushY-(cushZ-waveZ)*TAN(cutAngle)-0.001
				cutplane 270-cutAngle
			del 1
		endif
		block cushX, cushY, cushZ

		if cutAngle > eps then cutend
	endif
endif

end

100:
	add 0.008, 0.008, cushZ-waveZ
	muly (cushY-0.016)/(sectSizeY*sectY)

	sectAng = 0
	if cushAng > eps then
		cushAng_i = cushAng/sectX
		sectAng = -cushAng_i/2
		cushR = (sectSizeX/2)/SIN(abs(sectAng))
		roty -sectAng
	endif

	xx = 0
	zz = 0
	for isectX = 1 to sectX
		add xx, 0, zz
		roty sectAng
		yy = 0
		for isectY = 1 to sectY
			type = 0
			if isectX = 1 and isectY = 1 then
				type = 2
				posID = 0
			endif
			if isectX = sectX and isectY = 1 then
				type = 2
				posID = 1
			endif
			if isectX = sectX and isectY = sectY then
				type = 2
				posID = 2
			endif
			if isectX = 1 and isectY = sectY then
				type = 2
				posID = 3
			endif
			if isectX = 1 and isectY > 1 and isectY < sectY then
				type = 1
				posID = 0
			endif
			if isectX > 1 and isectX < sectX and isectY = 1 then
				type = 1
				posID = 1
			endif
			if isectX = sectX and isectY > 1 and isectY < sectY then
				type = 1
				posID = 2
			endif
			if isectX > 1 and isectX < sectX and isectY = sectY then
				type = 1
				posID = 3
			endif

			addy yy
			gosub 110
			del 1
			yy = yy + sectSizeY
		next isectY
		del 2
		if cushAng > eps then
			sectAng = sectAng - cushAng_i
			xx = cushR*SIN(cushAng_i*isectX)
			zz = cushR-cushR*COS(cushAng_i*isectX)
		else
			xx = xx + sectSizeX
		endif
	next isectX
	if cushAng > eps then del 1
	del 2
return

110:
	alpha_j = -alpha
	for j = 1 to 5
		alpha_i = -alpha
		for i = 1 to 5
			mm = 1
			if type = 1 then
				if i = 1 then mm = 0
				if i = 2 then mm = 0.9
			endif
			if type = 2 then
				if i = 1 or j = 1 then mm = 0
				if i = 2 and j <> 1 or j = 2 and i <> 1 then mm = 0.9
			endif
			put (-rra+hh+rra*cos(alpha_i)-rra+hh+rra*cos(alpha_j))*mm
			alpha_i = alpha_i + alpha/2
		next i
		alpha_j = alpha_j + alpha/2
	next j

	add sectSizeX/2, sectSizeY/2, 0
	rotz 90*(posID)
	add -sectSizeX/2, -sectSizeY/2, 0
	mesh sectSizeX, sectSizeY, 5, 5, 1+4+16+32,
		get(NSP)
	del 3
return

120:
	alpha_i = 90/num
	alpha = 0
	for i = 1 to num+1
		put 0.02-0.016*COS(alpha), 0.02-0.016*SIN(alpha), ss
		alpha = alpha + alpha_i
	next i
	alpha = 0
	for i = 1 to num+1
		put cushX-0.02+0.016*SIN(alpha), 0.02-0.016*COS(alpha), ss
		alpha = alpha + alpha_i
	next i
	alpha = 0
	for i = 1 to num+1
		put cushX-0.02+0.016*COS(alpha), cushY-0.02+0.016*SIN(alpha), ss
		alpha = alpha + alpha_i
	next i
	alpha = 0
	for i = 1 to num+1
		put 0.02-0.016*SIN(alpha), cushY-0.02+0.016*COS(alpha), ss
		alpha = alpha + alpha_i
	next i
return

121:
	alpha_i = 90/num
	alpha = 0
	for i = 1 to num+1
		put 0.02-0.016*COS(alpha), 0.02-0.016*SIN(alpha), 79
		alpha = alpha + alpha_i
	next i
	alpha = 0
	for i = 1 to num+1
		put cushX-0.02+0.016*SIN(alpha), 0.02-0.016*COS(alpha), 79
		alpha = alpha + alpha_i
	next i
	put cushX-0.004, cushY/2, 8
	put 0.004, cushY/2, 13
return

122:
	alpha_i = 90/num
	alpha = 0
	for i = 1 to num+1
		put 0.02-0.016*COS(alpha), 0.02-0.016*SIN(alpha), ss
		alpha = alpha + alpha_i
	next i
	alpha = 0
	for i = 1 to num+1
		put cushX-0.02+0.016*SIN(alpha), 0.02-0.016*COS(alpha), ss
		alpha = alpha + alpha_i
	next i
	put cushX-0.004, dist, ss
	put 0.004, dist, ss
return

130:
	alpha_i = 90/num
	put 0.008+sectSizeX/2, cushY-0.004, ss13
	alpha = 0
	for i = 1 to num+1
		put 0.02-0.016*SIN(alpha), cushY-0.02+0.016*COS(alpha), ss
		alpha = alpha + alpha_i
	next i
	alpha = 0
	for i = 1 to num+1
		put 0.02-0.016*COS(alpha), 0.02-0.016*SIN(alpha), ss
		alpha = alpha + alpha_i
	next i
	put 0.008+sectSizeX/2, 0.004, ss8

return

140:
	ss = 79
	ss8 = 8
	ss13 = 13
	gosub 130

	prism_ NSP/3, cushZ-waveZ,
		get(NSP)
return

150:
return
