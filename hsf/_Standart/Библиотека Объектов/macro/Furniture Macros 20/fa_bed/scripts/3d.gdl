
IF gs_detlevel_3D_m = 0 THEN END
IF not(gs_shadow) THEN SHADOW OFF

if iBedType = 2 then frh = frhLeg


!----- Knob MVO ----------------------------------------------------------------------

reqBKnobs = 0
knob = 1
success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
if success > 0 and reqBKnobs then
	knob = 0
endif


RESOL 2*gs_resol
PEN gs_cont_pen

MATERIAL frmat

if HeadboardEdge then
	eh=0.02
	et=0.04
else
	eh=0
	et=0
endif

ADDZ leg_h

if bHeadboard then
	if bFrontHeadBoard then
		CALL "fa_bed_headboard" PARAMETERS a=a, hbh=frh+hbh1, hbt=0.03,
				frh=frh, hboard_type_m=hboard_type_m, gs_detlevel_3D_m=gs_detlevel_3D_m, hbmat=hbmat, eh=eh , et=et,gs_resol=8*gs_resol
	
		IF HeadboardEdge THEN
			hbz1=0.035
			hbzz1=0.005
		ELSE
			hbz1=0.03
			hbzz1=0.00
		ENDIF
	
		IF gs_detlevel_3D_m = 1 THEN hbz1=0.03
	else
		hbz1 = 0
		hbzz1= 0
	endif
	if bRearHeadBoard then
			IF HeadboardEdge AND gs_detlevel_3D_m = 2 THEN
				hbz2=0.04
				hbzz2=0.005
			ELSE
				hbz2=0.03
				hbzz2=0
			ENDIF
		
			IF gs_detlevel_3D_m = 2 THEN ADDY b-hbz2
			IF gs_detlevel_3D_m = 1 THEN ADDY b-0.03
		
			CALL "fa_bed_headboard" PARAMETERS a=a, hbh=frh+hbh2, hbt=0.03,
				frh=frh, hboard_type_m=hboard_type_m, gs_detlevel_3D_m=gs_detlevel_3D_m, hbmat=hbmat, eh=eh , et=et,gs_resol=8*gs_resol: DEL 1
		
			IF gs_detlevel_3D_m = 1 THEN
				hbz2 = 0.03
				hbzz2 = 0
			ENDIF
	
		else
			hbz2 = 0
			hbzz2= 0
	endif
	DEL 1
endif

if not(bHeadboard) then 
		hbz1 = 0
		hbzz1= 0
		hbz2=0.03
		hbzz2=0
endif

if iBedType = 1 then
	ADDY hbz1
	BLOCK a, b-hbz1-hbz2+hbzz2, frh

	IF iFrameType = FRAME_DRAWER AND gs_detlevel_3D_m = 2 AND frh>=0.12 THEN		! with Drawers
		FOR i=1 TO 2
			ADD a, 0.01+((b-hbz1-0.61)/2)*(i-1), 0.03
			BLOCK 0.005, (b-hbz1-0.63)/2, frh-0.06
			if knob then
				ADD 0.005, (b-hbz1-0.63)/4-0.045, (frh-0.06)/2-0.01
				BLOCK 0.03, 0.09, 0.02
				del 1
			endif
			del 1
			if bDoubleBed then
				ADD -0.005, 0.01+((b-hbz1-0.61)/2)*(i-1), 0.03
				BLOCK 0.005, (b-hbz1-0.63)/2, frh-0.06
				if knob then
					ADD -0.035, (b-hbz1-0.63)/4-0.045, (frh-0.06)/2-0.01
					BLOCK 0.03, 0.09, 0.02
					del 1
				endif
				del 1
			endif
		NEXT i
	ENDIF
else	! iBedType = 2
	xx=0
	IF iBedLegType=2 OR iBedLegType=3 THEN xx=0.03
	dd=0.04
	IF iBedLegType=3 THEN dd=0.08
	ADD xx, xx+hbzz1 * not (bDoubleBed), 0
	if bLeg then
		CALL "fa_bedlegtyp" PARAMETERS a=a-xx*2, gs_detlevel_3D_m=gs_detlevel_3D_m, iBedLegType=iBedLegType,
				d=LegWidth, leg_h=leg_h, gs_resol=gs_resol,gs_leg_mat=gs_leg_mat
	endif
	DEL 1
	ADD xx, b-LegWidth-xx-hbzz2, 0
	if bLeg then
		CALL "fa_bedlegtyp" PARAMETERS a=a-xx*2, gs_detlevel_3D_m=gs_detlevel_3D_m, iBedLegType=iBedLegType,
				d=LegWidth, leg_h=leg_h, gs_resol=gs_resol,gs_leg_mat=gs_leg_mat
	endif
	DEL 1

	ADD 0, hbz1, leg_h
	BLOCK a, b-hbz1-hbz2+hbzz2, frh
endif

ADDZ frh

IF pw = 1 and gs_detlevel_3D_m = 2 THEN
	if bDoubleBed then
		ADD a*0.275, b-0.32, mrh+0.001
		FOR i=1 TO 2
			CALL "fa_pillow" PARAMETERS  a=a*0.35, gs_detlevel_3D_m=gs_detlevel_3D_m,
								gs_resol=4*gs_resol,pillow_mat=pillow_mat

			ADDX a*0.45
		NEXT i
		DEL 3
	else
		ADD a/2, b-0.32, mrh+0.001
		CALL "fa_pillow" PARAMETERS  a=a*0.60, gs_detlevel_3D_m=gs_detlevel_3D_m,
								gs_resol=4*gs_resol,pillow_mat=pillow_mat
		DEL 1
	endif
ENDIF

!!! *** Mattress Dist. from Frame Edge
if bHeadboard then
	IF not (bFrontHeadBoard) THEN
		MattressDistFront=MattressDist
	ELSE
		MattressDistFront=0
	ENDIF
	
	IF not (bRearHeadBoard) THEN
		MattressDistRear=MattressDist
	ELSE
		MattressDistRear=0
	ENDIF
else
	MattressDistFront=MattressDist
	MattressDistRear=MattressDist
endif

CUTPLANE 180
ADDZ -0.06
ADDx MattressDist
ADDy MattressDistFront

CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-2*MattressDist,
	cush_b=b-hbz1-hbz2+hbzz2-MattressDistFront-MattressDistRear,cush_h=mrh+0.06,r2=RadCushEdge,r1=RadCushCorner,gs_resol=gs_resol,cush_mat=mmat

CUTEND
DEL 4

DEL TOP

!!! *** 3D HOTSPOTs
Unid=1
if bCalledAsMacro then
	HOTSPOT 0,0,0,unID : unID=unID+3
	HOTSPOT 0,B,0,unID : unID=unID+3
	HOTSPOT A,0,0,unID : unID=unID+3
	HOTSPOT A,B,0,unID : unID=unID+3
else
	!!! *** X Dimension
	HOTSPOT 0,0,0,unID, A,1+256 : unID=unID+1
	HOTSPOT -1,0,0,unID, A,3 : unID=unID+1
	HOTSPOT A,0,0,unID, A,2 : unID=unID+1

	HOTSPOT 0,B,0,unID, A,1+256 : unID=unID+1
	HOTSPOT -1,B,0,unID, A,3 : unID=unID+1
	HOTSPOT A,B,0,unID, A,2 : unID=unID+1

	!!! *** Y Dimension
	HOTSPOT 0,0,0,unID, B,1+256 : unID=unID+1
	HOTSPOT 0,-1,0,unID, B,3 : unID=unID+1
	HOTSPOT 0,B,0,unID, B,2 : unID=unID+1

	HOTSPOT A,0,0,unID, B,1+256 : unID=unID+1
	HOTSPOT A,-1,0,unID, B,3 : unID=unID+1
	HOTSPOT A,B,0,unID, B,2 : unID=unID+1
endif


if iBedType = 2 and not (bCalledAsMacro) then
	!!! *** Leg Height

	HOTSPOT 0,0,0,unID, leg_h,1+128 : unID=unID+1
	HOTSPOT 0,0,-1,unID, leg_h,3 : unID=unID+1
	HOTSPOT 0,0,leg_h,unID, leg_h,2 : unID=unID+1

	HOTSPOT A,0,0,unID, leg_h,1+128 : unID=unID+1
	HOTSPOT A,0,-1,unID, leg_h,3 : unID=unID+1
	HOTSPOT A,0,leg_h,unID, leg_h,2 : unID=unID+1

	HOTSPOT A,B,0,unID, leg_h,1+128 : unID=unID+1
	HOTSPOT A,B,-1,unID, leg_h,3 : unID=unID+1
	HOTSPOT A,B,leg_h,unID, leg_h,2 : unID=unID+1

	HOTSPOT 0,B,0,unID, leg_h,1+128 : unID=unID+1
	HOTSPOT 0,B,-1,unID, leg_h,3 : unID=unID+1
	HOTSPOT 0,B,leg_h,unID, leg_h,2 : unID=unID+1
endif

!!! *** Frame Height
if not (bCalledAsMacro) then
	if iBedType = 2 then
		addz leg_h

		HOTSPOT 0,	0,	0,		unID, frhLeg,	1+128	: unID = unID+1
		HOTSPOT 0,	0,	-1,		unID, frhLeg,	3		: unID = unID+1
		HOTSPOT 0,	0,	frh,	unID, frhLeg,	2		: unID = unID+1

		HOTSPOT A,	0,	0,		unID, frhLeg,	1+128	: unID = unID+1
		HOTSPOT A,	0,	-1,		unID, frhLeg,	3		: unID = unID+1
		HOTSPOT A,	0,	frh,	unID, frhLeg,	2		: unID = unID+1

		HOTSPOT A,	B,	0,		unID, frhLeg,	1+128	: unID = unID+1
		HOTSPOT A,	B,	-1,		unID, frhLeg,	3		: unID = unID+1
		HOTSPOT A,	B,	frh,	unID, frhLeg,	2		: unID = unID+1

		HOTSPOT 0,	B,	0,		unID, frhLeg,	1+128	: unID = unID+1
		HOTSPOT 0,	B,	-1,		unID, frhLeg,	3		: unID = unID+1
		HOTSPOT 0,	B,	frh,	unID, frhLeg,	2		: unID = unID+1

		del 1
	else
		HOTSPOT 0,	0,	0,		unID, frh,		1+128	: unID = unID+1
		HOTSPOT 0,	0,	-1,		unID, frh,		3		: unID = unID+1
		HOTSPOT 0,	0,	frh,	unID, frh,		2		: unID = unID+1

		HOTSPOT A,	0,	0,		unID, frh,		1+128	: unID = unID+1
		HOTSPOT A,	0,	-1,		unID, frh,		3		: unID = unID+1
		HOTSPOT A,	0,	frh,	unID, frh,		2		: unID = unID+1

		HOTSPOT A,	B,	0,		unID, frh,		1+128	: unID = unID+1
		HOTSPOT A,	B,	-1,		unID, frh,		3		: unID = unID+1
		HOTSPOT A,	B,	frh,	unID, frh,		2		: unID = unID+1

		HOTSPOT 0,	B,	0,		unID, frh,		1+128	: unID = unID+1
		HOTSPOT 0,	B,	-1,		unID, frh,		3		: unID = unID+1
		HOTSPOT 0,	B,	frh,	unID, frh,		2		: unID = unID+1
	endif
endif

!!! *** Mattress Height

if not (bCalledAsMacro) then
	ADDZ leg_h+frh

	ADD MattressDist,MattressDistFront+hbz1,0
		HOTSPOT 0,0,0,unID, mrh,1+128 : unID=unID+1
		HOTSPOT 0,0,-1,unID, mrh,3 : unID=unID+1
		HOTSPOT 0,0,mrh,unID, mrh,2 : unID=unID+1
	DEL 1
	ADD -MattressDist,MattressDistFront+hbz1,0
		HOTSPOT A,0,0,unID, mrh,1+128 : unID=unID+1
		HOTSPOT A,0,-1,unID, mrh,3 : unID=unID+1
		HOTSPOT A,0,mrh,unID, mrh,2 : unID=unID+1
	DEL 1
	ADD MattressDist,-MattressDistRear-hbz2,0
		HOTSPOT 0,B,0,unID, mrh,1+128 : unID=unID+1
		HOTSPOT 0,B,-1,unID, mrh,3 : unID=unID+1
		HOTSPOT 0,B,mrh,unID, mrh,2 : unID=unID+1
	DEL 1
	ADD -MattressDist,-MattressDistRear-hbz2,0
		HOTSPOT A,B,0,unID, mrh,1+128 : unID=unID+1
		HOTSPOT A,B,-1,unID, mrh,3 : unID=unID+1
		HOTSPOT A,B,mrh,unID, mrh,2 : unID=unID+1
	DEL 1

	DEL 1
endif

!!! *** Frontal Headboard Height
if bHeadboard then
	IF bFrontHeadBoard and not (bCalledAsMacro) THEN
		ADDZ leg_h+frh
			HOTSPOT A/2,0,0,unID, hbh1,1+128 : unID=unID+1
			HOTSPOT A/2,0,-1,unID, hbh1,3 : unID=unID+1
			HOTSPOT A/2,0,hbh1,unID, hbh1,2 : unID=unID+1
		DEL 1
	ENDIF
endif

!!! *** Rear Headboard Height
if bHeadboard then
	IF bRearHeadBoard and not (bCalledAsMacro) THEN
		ADDZ leg_h+frh
			HOTSPOT A/2,B,0,unID, hbh2,1+128 : unID=unID+1
			HOTSPOT A/2,B,-1,unID, hbh2,3 : unID=unID+1
			HOTSPOT A/2,B,hbh2,unID, hbh2,2 : unID=unID+1
		DEL 1
	ENDIF
endif
END
