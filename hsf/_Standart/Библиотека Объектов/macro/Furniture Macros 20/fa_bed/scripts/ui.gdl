

iPageIDRoot 				=-1
iPageIDHeadMattPillow		= 1
iPageIDLeg					= 2
iPageID2DRep				= 3
iPageID3DRep				= 4
iPageIDMinimalSpace			= 5
iPageIDDescriptions			= 6

dim tabIcons[6]
	tabIcons[1] = "uiTab_FurnitureAccessories1_flat(1)"
	tabIcons[2] = "uiTab_Leg_flat(1)"
	tabIcons[3] = "uiTab_2DRepr_flat(1)"
	tabIcons[4] = "uiTab_3DRepr_flat(1)"
	tabIcons[5] = "uiTab_MinSpace_flat(1)"
	tabIcons[6] = "uiTab_Descriptions_flat(1)"


pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

UI_PAGE iPageIDHeadMattPillow, iPageIDRoot, stTabTitles[1], tabIcons[1]
	gosub "HeadMattPillowPage"

UI_PAGE iPageIDLeg, iPageIDRoot, stTabTitles[2], tabIcons[2]
	gosub "legPage"

UI_PAGE iPageID2DRep, iPageIDRoot, stTabTitles[3], tabIcons[3]
	gosub "2DRepPage"

UI_PAGE iPageID3DRep, iPageIDRoot, stTabTitles[4], tabIcons[4]
	gosub "3DRepPage"

UI_PAGE iPageIDMinimalSpace, iPageIDRoot, stTabTitles[5], tabIcons[5]
	gosub "minimalSpacePage"

UI_PAGE iPageIDDescriptions, iPageIDRoot, stTabTitles[6], tabIcons[6]
	gosub "descriptionPage"

FILE_DEPENDENCE "uiTab_FurnitureAccessories1_flat(1)"
FILE_DEPENDENCE "uiTab_Leg_flat(1)"
FILE_DEPENDENCE "uiTab_2DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_3DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_MinSpace_flat(1)"
FILE_DEPENDENCE "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"HeadMattPillowPage":
! ------------------------------------------------------------------------------
	call "ui_furniture_parts" parameters all 	bIsHeadboard	= 1

pageStartY	= 5
pageEndY	= 266
ui_separator 222, pageStartY - 5, 222, pageEndY

	call "ui_furniture_parts" parameters all 	bIsMattresAndPillow	= 1,
												pageStartX			= 232
return

! ------------------------------------------------------------------------------
"legPage":
! ------------------------------------------------------------------------------

	call "ui_furniture_parts" parameters all 	bIsFrameAndLeg	= 1

return

! ------------------------------------------------------------------------------
"2DRepPage":
! ------------------------------------------------------------------------------
	SYMBOLCLAS_BED			= 2
	SYMBOLCLAS_BED_DOUBLE	= 3

	if bDoubleBed then
		iSymbolClass = SYMBOLCLAS_BED_DOUBLE
	else
		iSymbolClass = SYMBOLCLAS_BED
	endif

	call "ui_object_2DRepresentation" parameters all	iSymbolClass		= iSymbolClass,
														iFillListID			= iPageID2DRep

return

! ------------------------------------------------------------------------------
"3DRepPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1
	if bHeadboard then
		surfacesArray[SurfItemID]	= "hbmat"
		SurfItemID = SurfItemID + 1
	endif
	
	surfacesArray[SurfItemID]	= "frmat"
	SurfItemID = SurfItemID + 1

	if iBedType = 2 then 
		surfacesArray[SurfItemID]	= "gs_leg_mat"
		SurfItemID = SurfItemID + 1
	endif

	surfacesArray[SurfItemID]	= "mmat"
	
	if pw then
		SurfItemID = SurfItemID + 1
		surfacesArray[SurfItemID]	= "pillow_mat"
	endif
		
	dim childFlagArray[]
	for k = 1 to SurfItemID
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters all	surfacesArray		= surfacesArray,
														childFlagArray		= childFlagArray,
														iSurfaceListID		= iPageID3DRep
return

! ------------------------------------------------------------------------------
"minimalSpacePage":
! ------------------------------------------------------------------------------
	if bDoubleBed then
		MSType = 14		!MINSPACE_DOUBLE_BED
	else
		MSType = 2		!MINSPACE_SIMPLE_BED
	endif

	call "fa_minimalspace" parameters MSType = MSType

return

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = iPageIDDescriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
