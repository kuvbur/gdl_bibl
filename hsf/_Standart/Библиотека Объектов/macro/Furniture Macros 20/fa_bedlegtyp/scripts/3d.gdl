
!! fa_bedgs_leg_type_m/Macro


MATERIAL gs_leg_mat


IF iBedLegType=1 OR gs_detlevel_3D_m=1 THEN GOSUB 100

IF gs_detlevel_3D_m=2 THEN

IF iBedLegType=2 THEN GOSUB 110


IF iBedLegType=3 THEN
	ADD d/2, d/2, 0
	GOSUB 120
	ADDX a-d
	GOSUB 120
	DEL 2
ENDIF


IF iBedLegType=4 THEN
	GOSUB 100
	rh=leg_h/3: m=SQR((a/2-d)^2+rh^2)
	alpha=ATN((a/2-d)/rh): r=m/2/COS(alpha)
	RESOL 6*gs_resol
	ADDY d/3
	ROTX 90
	PRISM_ 6, -d/3,
		d, leg_h/3, 79,
		a/2, leg_h*2/3-r, 915,
		a-d, leg_h/3, 3015,
		a-d, leg_h, 15,
		d, leg_h, 15,
		d, leg_h/3, -1
	gosub 2000
	DEL 2
ENDIF


IF iBedLegType=5 THEN
	GOSUB 110
	rh=leg_h/2: m=SQR((a/2-d/2)^2+rh^2)
	alpha=ATN((a/2-d/2)/rh): r=m/2/COS(alpha): beta=180-2*alpha
	toler leg_h / (6 * gs_resol)
	ADD d/2, d/2, leg_h/2-d/4
	ROTY 90-beta
	ELBOW r, beta*2, d/4
	gosub 1000
	resol gs_resol
	DEL 2
ENDIF


IF iBedLegType=6 THEN
	GOSUB 100
	ADD d, d/4, leg_h-d
	ROTY 90
	resol gs_resol
	BLOCK    d/2, d/2, a-2*d
	gosub 1000
	DEL 2
ENDIF: ENDIF


END

!---------------->!!! RETURN !!!<----------------
100:
	BLOCK d, d, leg_h
	ADDX a-d
	BLOCK d, d, leg_h
	gosub 1000
	DEl 1
RETURN

110:
	RESOL gs_resol
	ADD d/2, d/2, 0
	CYLIND leg_h, d/2
	ADDX a-d
	CYLIND leg_h, d/2
	gosub 1000
	DEl 2
RETURN

120:
	RESOL 1.5*gs_resol
	MULZ leg_h/(d+d*2/3)

	CUTPLANE 180
	ADDZ d+d*2/3
	CUTPLANE 0
	DEL 1

	ADDZ d/2+d/3
	SPHERE d/2
	gosub 1000
	DEL 1
	ADDZ d/6
	SPHERE d/3
	gosub 1000
	ADDZ d+d/3
	SPHERE d/3
	gosub 1000
	DEL 3

	CUTEND
	CUTEND
RETURN

1000:

roty 90
vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1
del 1

coor 2, -1, -2, -3, -4

base

body -1

return



2000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

