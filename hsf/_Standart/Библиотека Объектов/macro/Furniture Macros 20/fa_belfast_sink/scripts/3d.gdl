
if gs_shadow = 0 then shadow off

material gs_sink_mat
pen gs_cont_pen

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
	iResol = 3
else
	iResol = gs_resol
endif

resol iResol

add a / 2, b, 0
	gosub gs_sink_type_m * 10
del 1

end


10:
! ==============================================================================
! Sink Type 1
! ==============================================================================

group "sink1"
	gosub "sinkGeometry"
endgroup

group "bowl1"
	valuesForDrain = 0
	bMirroredBowl = 0
	gosub "nBowl_1"
endgroup

group "drain1"
	valuesForDrain = 1
	gosub "nBowl_1"
endgroup

placegroup subgroup ("sink1", "bowl1")

placegroup "drain1"

gosub "taps"

return



20:
! ==============================================================================
! Sink Type 2
! ==============================================================================

group "sink2"
	gosub "sinkGeometry"
endgroup
group "bowl2"
	valuesForDrain = 0
	bMirroredBowl = 0
	gosub "nBowl_1"
endgroup

group "drain2"
	valuesForDrain = 1
	gosub "nBowl_1"
endgroup

placegroup subgroup ("sink2", "bowl2")

placegroup "drain2"

gosub "taps"

return



30:
! ==============================================================================
! Sink Type 3
! ==============================================================================

group "sink3"
	gosub "sinkGeometry"
endgroup

group "bowl3"
	valuesForDrain = 0
	bMirroredBowl = 0
	gosub "nBowl_1"
endgroup

group "drain3"
	valuesForDrain = 1
	gosub "nBowl_1"
endgroup

group "border3"
	gosub "border"
endgroup

if gs_detlevel_3D_m = 2 then
	placegroup subgroup (subgroup (("sink3"), "bowl3"), "border3")

	placegroup "drain3"
else
	placegroup subgroup ("sink3", "bowl3")
endif

gosub "taps"

return



40:
! ==============================================================================
! Sink Type 4
! ==============================================================================

group "sink4"
	gosub "sinkGeometry"
endgroup

group "bowl4"
	valuesForDrain = 0
	for i = 1 to 2
		if i = 1 then
			bMirroredBowl = 0
			gosub "nBowl_1"
		else
			bMirroredBowl = 1
			gosub "nBowl_2"
		endif
	next i
endgroup

group "drain4"
	valuesForDrain = 1
	gosub "nBowl_1"
	gosub "nBowl_2"
endgroup

group "border4"
	gosub "border"
endgroup

if gs_detlevel_3D_m = 2 then
	placegroup subgroup (subgroup (("sink4"), "bowl4"), "border4")

	placegroup "drain4"
else
	placegroup subgroup ("sink4", "bowl4")
endif

gosub "taps"

return



50:
! ==============================================================================
! Sink Type 5
! ==============================================================================

group "sink5"
	gosub "sinkGeometry"
endgroup

group "bowl5"
	valuesForDrain = 0
	bMirroredBowl = 0
	gosub "nBowl_1"
	gosub "nBowl_2"
	gosub "nBowl_3"
endgroup

group "drain5"
	valuesForDrain = 1
	gosub "nBowl_1"
	gosub "nBowl_2"
	gosub "nBowl_3"
endgroup

group "border5"
	gosub "border"
endgroup

group "bowlCut"
	material gs_sink_mat

	bowlCutWidth1	= 0.06
	bowlCutWidth2	= 0.08
	bowlCutDepth	= 0.02

	add a / 2 - sinkWallThkSide - bowlWidth3 / 2, -drainBoardHeight2 + sinkWallThkSide / 2, zzyzx - bowlCutDepth
	rotx 90
	prism_ 5, sinkWallThkSide * 2,
		-bowlCutWidth1 / 2, 0, 15,
		bowlCutWidth1 / 2, 0, 15,
		bowlCutWidth2 / 2, bowlCutDepth, 15,
		-bowlCutWidth2 / 2, bowlCutDepth, 15,
		-bowlCutWidth1 / 2, 0, -1
	del 2
endgroup

if gs_detlevel_3D_m = 2 then
	placegroup subgroup (subgroup (subgroup (("sink5"), "bowl5"), "border5"), "bowlCut")

	placegroup "drain5"
else
	placegroup subgroup ("sink5", "bowl5")
endif

gosub "drainBoardReed"

gosub "taps"

return



60:
! ==============================================================================
! Sink Type 6
! ==============================================================================

group "sink6"
	gosub "sinkGeometry"
endgroup

group "bowl6"
	valuesForDrain = 0
	for i = 1 to 2
		if i = 1 then
			bMirroredBowl = 0
			gosub "nBowl_1"
		else
			bMirroredBowl = 1
			gosub "nBowl_2"
		endif
	next i
endgroup

group "drain6"
	valuesForDrain = 1
	gosub "nBowl_1"
	gosub "nBowl_2"
endgroup

group "border6"
	gosub "border"
endgroup

if gs_detlevel_3D_m = 2 then
	placegroup subgroup (subgroup (("sink6"), "bowl6"), "border6")

	placegroup "drain6"
else
	placegroup subgroup ("sink6", "bowl6")
endif

for i = 1 to 2
	gosub "drainBoardReed"
mulx -1
next i
del 2

gosub "taps"

return



70:
! ==============================================================================
! Sink Type 7
! ==============================================================================

group "sink7"
	gosub "sinkGeometry"
endgroup

group "bowl7"
	valuesForDrain = 0
	bMirroredBowl = 0
	gosub "nBowl_1"
endgroup

group "drain7"
	valuesForDrain = 1
	gosub "nBowl_1"
endgroup

group "border7"
	gosub "border"
endgroup

if gs_detlevel_3D_m = 2 then
	placegroup subgroup (subgroup (("sink7"), "bowl7"), "border7")

	placegroup "drain7"
else
	placegroup subgroup ("sink7", "bowl7")
endif

gosub "drainBoardReed"

gosub "taps"

return



"sinkGeometry":
! ==============================================================================
! Sink Geometry
! ==============================================================================

material gs_sink_mat

if iSinkGeometry = 1 then
	put	0, 0, 79,
		 a / 2 - sinkCornerRadiusOut, 0, 79,
		 a / 2, -sinkCornerRadiusOut, 1079,
		 a / 2, -b + sinkCornerRadiusOut,	79,
		 a / 2 - sinkCornerRadiusOut,	-b, 1079,
		-a / 2 + sinkCornerRadiusOut,	-b, 79,
		-a / 2, -b + sinkCornerRadiusOut, 1079,
		-a / 2, -sinkCornerRadiusOut, 79,
		-a / 2 + sinkCornerRadiusOut, 0, 1079
else
	put	0, 0, 79,
		a / 2 - sinkCornerRadiusOut, 0, 79,
		a / 2, -sinkCornerRadiusOut, 1079,
		a / 2, -b + sinkCornerRadiusOut + sinkCornerDistY, 79,
		sinkCornerRadiusOut, -arch2Angle, 2079

	for i = 1 to iSinkBigResolOut * 2
		put	 sinkCornerRadiusOut2, -(90 - arch2Angle) / iSinkBigResolOut, 2079
	next i

	put	sinkCornerRadiusOut, -arch2Angle, 2079,
		-a / 2, -sinkCornerRadiusOut, 79,
		-a / 2 + sinkCornerRadiusOut, 0, 1079
endif

if GLOB_CONTEXT = 4 then
	prism_ nsp / 3, zzyzx,
		get (nsp)
else
	hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
			nsp / 3, zzyzx, 0, 0.005, 1,
			get (nsp)
endif

gosub 1000

return



"nBowl_1":
! ==============================================================================
! 1st Bowl, Drain Values
! ==============================================================================

bowlWidth			= bowlWidth1
bowlDepth			= bowlDepth1
drainPosXFromCenter	= drainPosFromBowlCenterX_1
drainPosYFromCenter	= drainPosFromBowlCenterY_1
bowlHeight			= bowlHeight1
add bowlPosX_1, bowlPosY_1, 0
	if valuesForDrain = 1 then
		gosub "drain"
	else
		gosub "bowlGeometry"
	endif
del 1

return



"nBowl_2":
! ==============================================================================
! 2nd Bowl, Drain Values
! ==============================================================================

bowlWidth			= bowlWidth2
bowlDepth			= bowlDepth2
drainPosXFromCenter	= drainPosFromBowlCenterX_2
drainPosYFromCenter	= drainPosFromBowlCenterY_2
bowlHeight			= bowlHeight1
add bowlPosX_2, bowlPosY_2, 0
	if valuesForDrain = 1 then
		gosub "drain"
	else
		gosub "bowlGeometry"
	endif
del 1

return



"nBowl_3":
! ==============================================================================
! 3rd Bowl, Drain Values
! ==============================================================================

bowlWidth			= bowlWidth3
bowlDepth			= bowlDepth3
drainPosXFromCenter	= drainPosFromBowlCenterX_3
drainPosYFromCenter	= drainPosFromBowlCenterY_3
drainDia1			= 0.04
drainDia2			= drainDia1 + 0.02
bowlHeight			= bowlHeight2
add bowlPosX_3, bowlPosY_3, 0
	if valuesForDrain = 1 then
		gosub "drain"
	else
		gosub "bowlGeometry"
	endif
del 1
drainDia1			= 0.07
drainDia2			= drainDia1 + 0.02

return



"bowlGeometry":
! ==============================================================================
! Bowl
! ==============================================================================

if iSinkGeometry = 1 then
	put	0, bowlDepth / 2, 79,
		bowlWidth / 2 - bowlCornerRadius * (gs_sink_type_m <> 4 or bMirroredBowl) - bowlCornerRadius2 * (gs_sink_type_m = 4 and not(bMirroredBowl)), bowlDepth / 2, 79,
		bowlWidth / 2, bowlDepth / 2 - bowlCornerRadius * (gs_sink_type_m <> 4 or bMirroredBowl) - bowlCornerRadius2 * (gs_sink_type_m = 4 and not(bMirroredBowl)), 1079,
		bowlWidth / 2, -bowlDepth / 2 + bowlCornerRadius, 79,
		bowlWidth / 2 - bowlCornerRadius, -bowlDepth / 2, 1079,
		-bowlWidth / 2 + bowlCornerRadius, -bowlDepth / 2, 79,
		-bowlWidth / 2, -bowlDepth / 2 + bowlCornerRadius, 1079,
		-bowlWidth / 2, bowlDepth / 2 - bowlCornerRadius * (gs_sink_type_m <> 4 or not(bMirroredBowl)) - bowlCornerRadius2 * (gs_sink_type_m = 4 and bMirroredBowl), 79,
		-bowlWidth / 2 + bowlCornerRadius * (gs_sink_type_m <> 4 or not(bMirroredBowl)) + bowlCornerRadius2 * (gs_sink_type_m = 4 and bMirroredBowl), bowlDepth / 2, 1079
else
	put	0, bowlDepth / 2, 79,
		bowlWidth / 2 - bowlCornerRadius, bowlDepth / 2, 79,
		bowlWidth / 2, bowlDepth / 2 - bowlCornerRadius, 1079,
		bowlWidth / 2, -bowlDepth / 2 + bowlCornerRadius + sinkCornerDistY, 79,
		bowlCornerRadius, -arch2Angle, 2079

	for i = 1 to iSinkBigResolOut * 2
		put	 sinkCornerRadiusOut2 - sinkWallThkSide, -(90 - arch2Angle) / iSinkBigResolOut, 2079
	next i

	put	bowlCornerRadius, -arch2Angle, 2079,
		-bowlWidth / 2, bowlDepth / 2 - bowlCornerRadius, 79,
		-bowlWidth / 2 + bowlCornerRadius, bowlDepth / 2, 1079
endif

addz zzyzx
mulz -1
if GLOB_CONTEXT = 4 then
	prism_ nsp / 3, bowlHeight,
		get (nsp)
else
	hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
		nsp / 3, bowlHeight, 0, bowlCornerRadius - 0.01, 1,
		get (nsp)
endif
del 2

! ==============================================================================
! Drain Cut
! ==============================================================================

if gs_detlevel_3D_m = 2 then
	material gs_sink_mat

	add drainPosXFromCenter, drainPosYFromCenter, zzyzx - bowlHeight - drainHeight
	roty -90
	revolve 2, 360, 1 + 2,
		0, drainDia1 / 2, 0,
		drainHeight, drainDia2 / 2, 0
	del 2
endif

return



"drainBoardReed":
! ==============================================================================
! Drainboard Reed
! ==============================================================================

if iDrainBoardDir = 1 then
	reedLength	= drainBoardHeight - drainPlateHeight - reedDist_temp
	reedNum		= INT((drainBoardWidth - reedDist_temp * 2) / reedDist_temp)
	reedDist	= (drainBoardWidth - reedDist_temp * 2) / (reedNum - 1)
else
	reedLength	= drainBoardWidth - drainPlateHeight - reedDist_temp
	reedNum		= INT((b - sinkWallThkSide * 2 - reedDist_temp * 2) / reedDist_temp)
	reedDist	= (b - sinkWallThkSide * 2 - reedDist_temp * 2) / (reedNum - 1)
endif

if gs_detlevel_3D_m = 2 then

	resol 8

	put	-reedWidth / 2, reedLength / 2 - reedWidth / 2, 79,
		0, b, 800,
		reedWidth / 2, reedLength / 2 - reedWidth / 2, 1079,
		reedWidth / 2, -reedLength / 2 + reedWidth / 2, 79,
		0, -b, 800,
		-reedWidth / 2, -reedLength / 2 + reedWidth / 2, 1079

	if iDrainBoardDir = 1 then
		add -a / 2 + sinkWallThkSide + reedDist_temp, -sinkWallThkSide - (drainBoardHeight - drainPlateHeight) / 2 - drainPlateHeight, zzyzx - drainPlateHeight
			for re = 1 to reedNum
				if re = reedNum then
					prism_ nsp / 3, reedThk,
						get (nsp)
				else
					prism_ nsp / 3, reedThk,
						use (nsp)
				endif
			addx reedDist
			next re
			del reedNum
		del 1
	else
		add a / 2 - sinkWallThkSide - (drainBoardWidth - drainPlateHeight) / 2 - drainPlateHeight, -b + sinkWallThkSide + reedDist_temp, zzyzx - drainPlateHeight
			for re = 1 to reedNum
				rotz 90
					if re = reedNum then
						prism_ nsp / 3, reedThk,
							get (nsp)
					else
						prism_ nsp / 3, reedThk,
							use (nsp)
					endif
				del 1
			addy reedDist
			next re
			del reedNum
		del 1
	endif

	resol iResol

endif

return



"taps":
! ==============================================================================
! Taps
! ==============================================================================

if gs_tap_type_m <> 0 then
	add -a / 2 + xOrigTapPos, -b + yOrigTapPos, zzyzx + count_th

	call "tapType_m" parameters all	gs_tap_type_m		= tempTapType,
									bEnableTapEdit		= 1,
									bSinkTap			= 1,
									useOldSimpleTaps	= 0

	del 1
endif

return



"drain":
! ==============================================================================
! Drain
! ==============================================================================

if gs_detlevel_3D_m = 2 then
	material gs_tap_mat

	add drainPosXFromCenter, drainPosYFromCenter, zzyzx - bowlHeight - drainHeight
	cylind drainHeight, drainDia1 / 2
	del 1
endif

return



"border":
! ==============================================================================
! Border
! ==============================================================================

if gs_sink_type_m = 3 then
	addz zzyzx
	mulz -1
	hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
		7, drainPlateHeight, 45, drainPlateHeight, 1,

		0, -sinkWallThkSide, 79,
		a / 2 - sinkCornerRadiusOut,	 -sinkWallThkSide,	79,
		a / 2 - sinkWallThkSide, -sinkCornerRadiusOut, 1079,
		a / 2 - sinkWallThkSide, -drainBoardHeight - sinkWallThkSide - bowlCornerRadius,	79,
		-a / 2 + sinkWallThkSide,	-drainBoardHeight - sinkWallThkSide - bowlCornerRadius,	79,
		-a / 2 + sinkWallThkSide, -sinkCornerRadiusOut, 79,
		-a / 2 + sinkCornerRadiusOut,	 -sinkWallThkSide,	1079
	del 2
endif

if gs_sink_type_m = 4 then
	addz zzyzx
	mulz -1
	hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
		13, drainPlateHeight, 45, drainPlateHeight, 1,

		-sinkWallThkMiddle / 2 - bowlCornerRadius2, -sinkWallThkSide, 79,
		-sinkWallThkMiddle / 2 - bowlCornerRadius2, -sinkWallThkSide - bowlCornerRadius2, 900,
		0, -45, 4079,
		sinkWallThkMiddle / 2 + bowlCornerRadius2 - sin(45) * bowlCornerRadius2, -sinkWallThkSide - bowlCornerRadius2 + sin(45) * bowlCornerRadius2, 1079,
		sinkWallThkMiddle / 2 + bowlCornerRadius2, -sinkWallThkSide, 1079,
		 a / 2 - sinkCornerRadiusOut, -sinkWallThkSide, 79,
		 a / 2 - sinkWallThkSide, -sinkCornerRadiusOut, 1079,
		 a / 2 - sinkWallThkSide, -b + sinkCornerRadiusOut, 79,
		 a / 2 - sinkCornerRadiusOut,	-b + sinkWallThkSide, 1079,
		-a / 2 + sinkCornerRadiusOut,	-b + sinkWallThkSide, 79,
		-a / 2 + sinkWallThkSide, -b + sinkCornerRadiusOut, 1079,
		-a / 2 + sinkWallThkSide, -sinkCornerRadiusOut, 79,
		-a / 2 + sinkCornerRadiusOut, -sinkWallThkSide, 1079
	del 2
endif

if gs_sink_type_m = 5 then
	add -a / 2 + drainBoardWidth / 2 + sinkWallThkSide, 0, zzyzx
	mulz -1
	hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
		11, drainPlateHeight, 45, drainPlateHeight, 1,

		0, -sinkWallThkSide, 79,
		drainBoardWidth / 2 - bowlCornerRadius, -sinkWallThkSide,	79,
		drainBoardWidth / 2, -sinkCornerRadiusOut, 1079,

		drainBoardWidth / 2, -drainBoardHeight2 - sinkWallThkSide + bowlCornerRadius,	79,
		drainBoardWidth / 2 + bowlCornerRadius, -drainBoardHeight2 - sinkWallThkSide,	1079,

		drainBoardWidth / 2 + bowlCornerRadius * 2 + sinkWallThkMiddle + bowlCornerRadius, -drainBoardHeight2 - sinkWallThkSide,	79,
		drainBoardWidth / 2 + bowlCornerRadius * 2 + sinkWallThkMiddle + bowlCornerRadius, -b + sinkWallThkSide,	79,
		-drainBoardWidth / 2 + bowlCornerRadius, -b + sinkWallThkSide,	79,
		-drainBoardWidth / 2, -b + sinkCornerRadiusOut,	1079,
		-drainBoardWidth / 2, -sinkCornerRadiusOut, 79,
		-drainBoardWidth / 2 + bowlCornerRadius, -sinkWallThkSide,	1079
	del 2
endif

if gs_sink_type_m = 6 then
	for i = 1 to 2
		add -a / 2 + drainBoardWidth / 2 + sinkWallThkSide, 0, zzyzx
		mulz -1
		hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
			9, drainPlateHeight, 45, drainPlateHeight, 1,

			0, -sinkWallThkSide, 79,
			drainBoardWidth / 2 - bowlCornerRadius, -sinkWallThkSide,	79,
			drainBoardWidth / 2, -sinkCornerRadiusOut, 1079,
			drainBoardWidth / 2, -drainBoardHeight - sinkWallThkSide + bowlCornerRadius,	79,
			drainBoardWidth / 2 + bowlCornerRadius, -drainBoardHeight - sinkWallThkSide,	1079,
			drainBoardWidth / 2 + bowlCornerRadius * 2, -drainBoardHeight - sinkWallThkSide - bowlCornerRadius,	1079,
			-drainBoardWidth / 2, -drainBoardHeight - sinkWallThkSide - bowlCornerRadius,	79,
			-drainBoardWidth / 2, -sinkCornerRadiusOut, 79,
			-drainBoardWidth / 2 + bowlCornerRadius, -sinkWallThkSide,	1079
		del 2
	mulx -1
	next i
	del 2
endif

if gs_sink_type_m = 7 then
	addz zzyzx
	mulz -1
	hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
		6, drainPlateHeight, 45, drainPlateHeight, 1,

		a / 2 - sinkWallThkSide - drainBoardWidth - bowlCornerRadius, -sinkWallThkSide, 79,
		a / 2 - sinkCornerRadiusOut,	 -sinkWallThkSide, 79,
		a / 2 - sinkWallThkSide, -sinkCornerRadiusOut, 1079,
		a / 2 - sinkWallThkSide, -b + sinkCornerRadiusOut, 79,
		a / 2 - sinkCornerRadiusOut,	-b + sinkWallThkSide, 1079,
		a / 2 - sinkWallThkSide - drainBoardWidth - bowlCornerRadius,	-b + sinkWallThkSide, 79
	del 2
endif

return

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4


base

body -1

return
