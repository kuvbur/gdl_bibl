
! fa_bookshelf/Macro


RESOL gs_resol
MATERIAL bpmat
PEN gs_cont_pen

!!! Side panels full widht
IF gs_lining_m=4 THEN spfw=a-2*spt
IF gs_lining_m=2 OR gs_lining_m=3 THEN spfw=a-cooh-2*spt
IF gs_lining_m=1 THEN spfw=a-2*cooh-2*spt

!!Shelf, Horizontal Frame starting point
if gs_lining_m=4 or gs_lining_m=2 then startcooh=0
if gs_lining_m=1 or gs_lining_m=3 then startcooh=cooh

!!spfw=a-2*cooh-2*spt           !!! Side panels full widht
sfh = zzyzx - cot - leg_h - 2 * shelfThk      !!! Shelves full height

if bVerticalPanels then		! One shelf, horizontal frame widht
	aa = (spfw - _iNumVertPanels * vpt) / (_iNumVertPanels + 1)
else
	aa = spfw
endif

if bShelves then	! Distance between shelves
	bb = (sfh + shelfThk) / (_iNumShelves + 1)
else
	bb = sfh + shelfThk
endif

!!! *** Back Panel

addz leg_h + shelfThk

		if bpt>0 and gs_lining_m=4 then

			!* no side cornice
			prism_ 4, zzyzx - cot - shelfThk - leg_h - shelfThk,
					-a/2+spt,0,15,
					-a/2+spt,-bpt,15,
					a/2-spt,-bpt,15,
					a/2-spt,0,15
			gosub 1000
		endif
		if bpt>0 and gs_lining_m=3 then

			!* left cornice
			prism_ 4, zzyzx - cot - shelfThk - leg_h - shelfThk,
					-a/2+cooh+spt,0,15,
					-a/2+cooh+spt,-bpt,15,
					a/2-spt,-bpt,15,
					a/2-spt,0,15
			gosub 1000
		endif
		if bpt>0 and gs_lining_m=2 then

			!*right cornice
			prism_ 4, zzyzx - cot - shelfThk - leg_h - shelfThk,
					-a/2+spt,0,15,
					-a/2+spt,-bpt,15,
					a/2-cooh-spt,-bpt,15,
					a/2-cooh-spt,0,15
			gosub 1000
		endif
		if bpt>0 and gs_lining_m=1 then

			!*both side cornice
			prism_ 4, zzyzx - cot - shelfThk - leg_h - shelfThk,
					-a/2+cooh+spt,0,15,
					-a/2+cooh+spt,-bpt,15,
					a/2-cooh-spt,-bpt,15,
					a/2-cooh-spt,0,15
			gosub 1000
		endif

DEL 1

!!! *** Frame Panels

ADDZ leg_h

MATERIAL spmat

	!!! *** Bottom and Top Panel

	addx -a / 2 + spt
	for kk = 1 to 2
		for k=1 TO _iNumVertPanels + 1
			add startcooh, -bpt, 0
			prism_ 4, horFrameThk,
				0,	0,					15,
				0,	-b + cooh + bpt,	15,
				aa,	-b + cooh + bpt,	15,
				aa,	0,					15
				gosub 2000
			del  1
			addx  aa + vpt
		next  k
		del  _iNumVertPanels + 1
		addz  zzyzx - cot - horFrameThk - leg_h
	next kk
	del 2
	del 1

	!!! *** Side Panels Panel

	if gs_lining_m=4 then

		PRISM_ 4, zzyzx-cot-leg_h,
				a/2-spt,0,15,
				a/2,0,15,
				a/2,-b+cooh,15,
				a/2-spt,-b+cooh,11

		PRISM_ 4, zzyzx-cot-leg_h,
				-a/2+spt,0,15,
				-a/2,0,15,
				-a/2,-b+cooh,15,
				-a/2+spt,-b+cooh,11

		gosub 1000
	endif

	if gs_lining_m=3 then

		PRISM_ 4, zzyzx-cot-leg_h,
				a/2-spt,0,15,
				a/2,0,15,
				a/2,-b+cooh,15,
				a/2-spt,-b+cooh,11

		PRISM_ 4, zzyzx-cot-leg_h,
				-a/2+cooh+spt,0,15,
				-a/2+cooh,0,15,
				-a/2+cooh,-b+cooh,15,
				-a/2+cooh+spt,-b+cooh,11

		gosub 1000
	endif

	if gs_lining_m=2 then

		PRISM_ 4, zzyzx-cot-leg_h,
				a/2-cooh-spt,0,15,
				a/2-cooh,0,15,
				a/2-cooh,-b+cooh,15,
				a/2-cooh-spt,-b+cooh,11

		PRISM_ 4, zzyzx-cot-leg_h,
				-a/2+spt,0,15,
				-a/2,0,15,
				-a/2,-b+cooh,15,
				-a/2+spt,-b+cooh,11

		gosub 1000
	endif

	if gs_lining_m=1 then

		PRISM_ 4, zzyzx-cot-leg_h,
				a/2-cooh-spt,0,15,
				a/2-cooh,0,15,
				a/2-cooh,-b+cooh,15,
				a/2-cooh-spt,-b+cooh,11

		PRISM_ 4, zzyzx-cot-leg_h,
				-a/2+cooh+spt,0,15,
				-a/2+cooh,0,15,
				-a/2+cooh,-b+cooh,15,
				-a/2+cooh+spt,-b+cooh,11

		gosub 1000
	endif

DEL 1

ADDx -a/2

		!!! *** Vertical Inner Panels
		if bVerticalPanels then
			material vpmat	
			add 0,-bpt, leg_h
			for i = 1 to _iNumVertPanels
				addx startcooh + spt - vpt + ((spfw + vpt) / (_iNumVertPanels + 1))
				prism_ 4, zzyzx - cot - leg_h,
					vpt,	0,					15,
					0,		0,					15,
					0,		-b + cooh + bpt,	15,
					vpt,	-b + cooh + bpt,	15
				del 1
				gosub 1000
				addx (spfw + vpt) / (_iNumVertPanels + 1)
			next i
			del _iNumVertPanels
			del 1
		endif

		!!! *** Shelves
		if bShelves then
			material shmat
			for r=1 to _iNumVertPanels + 1
				addx startcooh + spt
				add 0,-bpt,leg_h + bb
				for i=1 to _iNumShelves
					prism_ 4, shelfThk,
						0,	0,					15,
						0,	-b + cooh + bpt,	15,
						aa,	-b + cooh + bpt,	15,
						aa,	0,					15
					gosub 2000
					addz bb
				next i
				del _iNumShelves
				del 1
				del 1
				addx aa+vpt
			next r
			del _iNumVertPanels+1
		endif
DEL 1

END


1000:

	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return



2000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return



