
!! fa_cabinet_credenza/Macro

material gs_cabinet_mat
pen gs_cont_pen

!-----Definitions-----
FORM_SQUARE   = 1
FORM_CORNER_L = 2
FORM_CORNER_C = 3

EPS=0.0001


! ==============================================================================
! FORM_SQUARE
! ==============================================================================
if iCabinetForm = FORM_SQUARE then
	addz leg_h+boardThk

	if gs_lining_m=1 then
		vv=a/2-side_overhang
		vv2=-a/2+side_overhang
	endif
	if gs_lining_m=3 then
		vv=a/2
		vv2=-a/2+side_overhang
	endif
	if gs_lining_m=2 then
		vv=a/2-side_overhang
		vv2=-a/2
	endif
	if gs_lining_m=4 then
		vv=a/2
		vv2=-a/2
	endif
! -------- back panel ----------------------------------------------------------
	prism_ 4, zzyzx-cot-boardThk-leg_h-boardThk,
			vv2+boardThk,0,8,
			vv2+boardThk,-boardThk,15,
			vv-boardThk,-boardThk,10,
			vv-boardThk,0,8

	gosub "textureVertical_03"
	del 1

! -------- side panels ---------------------------------------------------------
	addz leg_h

    add vv-boardThk, -(b-cooh),0
    block boardThk,b-cooh , zzyzx-cot-leg_h
    del 1
    gosub "textureVertical_03"
  
    add vv2, -(b-cooh),0
    block boardThk,b-cooh , zzyzx-cot-leg_h
    del 1
    gosub "textureVertical_03"
    
    add vv2+boardThk, -(b-cooh),0
    block vv-vv2-boardThk*2,b-cooh, boardThk
    del 1
    gosub "textureHorizontal_03"
    
    add vv2+boardThk, -(b-cooh),zzyzx-cot-leg_h-boardThk
    block vv-vv2-boardThk*2,b-cooh, boardThk
    del 1
    gosub "textureHorizontal_03"
    
    del 1

else
! ==============================================================================
! FORM_CORNER_L,  FORM_CORNER_C
! ==============================================================================
	sideDepth1 = sideDepth1 - side_overhang
	sideDepth2 = sideDepth2 - cooh
	if gs_lining_m=3 or gs_lining_m=1 then b = b - cooh
	if gs_lining_m=2 or gs_lining_m=1 then a = a - side_overhang

	if abs(sideDepth1-a)< EPS or abs(sideDepth2-b) < EPS then
		alpha = 90
	else
		alpha = atn((b-sideDepth2)/(a-sideDepth1))
	endif


	!-----Backpanel-----
	put 0, 0, 10
	put a, 0, 10
	put a, -sideDepth2, 10
	if iCabinetForm = FORM_CORNER_L then
		put a-boardThk, -sideDepth2, 15
	else
		if abs(90-alpha)>eps then
			put a-boardThk, -sideDepth2-boardThk*tan(alpha), 15
		else
			put a-boardThk, -sideDepth2, 15
		endif
	endif
	put a-boardThk, -boardThk, 15
	put boardThk, -boardThk, 15
	put boardThk, -b+boardThk, 15
	if iCabinetForm = FORM_CORNER_L then
		put sideDepth1, -b+boardThk, 10
	else
		if abs(90-alpha)>eps then
			put sideDepth1+boardThk/tan(alpha), -b+boardThk, 10
		else
			put sideDepth1, -b+boardThk, 10
		endif
	endif
	put sideDepth1, -b, 10
	put 0, -b, 10

	addz leg_h+boardThk
	prism_ nsp/3, zzyzx-cot-boardThk-leg_h-boardThk,
			get (nsp)

	gosub "textureVertical_01"
	del 1

	!-----Top, Bottom-----
	maskTop = 0
	maskBottom = 1
	addz leg_h
	for i = 1 to 2
		if iCabinetForm = FORM_CORNER_L then
			put 0, 0, 10+maskBottom+4*maskTop
			put a, 0, 10+maskBottom+4*maskTop
			put a, -sideDepth2, 10+maskBottom+4*maskTop
			put a-boardThk, -sideDepth2, 13
			put sideDepth1, -sideDepth2, 8

			prism_ nsp/3, boardThk,
					get (nsp)
			gosub "textureHorizontal_02"

			put 0, 0, 8
			put sideDepth1, -sideDepth2, 15
			put sideDepth1, -b+boardThk, 8+maskBottom+4*maskTop
			put sideDepth1, -b, 10+maskBottom+4*maskTop
			put 0, -b, 10+maskBottom+4*maskTop

			prism_ nsp/3, boardThk,
					get (nsp)
			gosub "textureVertical_02"

			addz zzyzx-cot-boardThk-leg_h
			maskTop = 1
			maskBottom = 0
		else
			put 0, 0, 10+maskBottom+4*maskTop
			put a, 0, 10+maskBottom+4*maskTop
			put a, -sideDepth2, 10+maskBottom+4*maskTop
			if abs(90-alpha)>eps then
				put a-boardThk, -sideDepth2-boardThk*tan(alpha), 13
				put sideDepth1+boardThk/tan(alpha), -b+boardThk, 8+maskBottom+4*maskTop
			endif
			put sideDepth1, -b, 10+maskBottom+4*maskTop
			put 0, -b, 10+maskBottom+4*maskTop

			prism_ nsp/3, boardThk,
					get (nsp)
			gosub "textureAlha"

			addz zzyzx-cot-boardThk-leg_h
			maskTop = 1
			maskBottom = 0
		endif
	next i
	del 3
endif

END

! ======================================================================
! SubRoutines
! ======================================================================

"textureVertical_01":
	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return

"textureVertical_02":
	roty 90
	rotz 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 2

	coor 2, -1, -2, -3, -4

	base

	body -1
return

"textureVertical_03":
	vert 0, 0, 0
	vert 0, 0, 1
	vert 0, 1, 0
	vert 1, 0, 0

	coor 2+1024, -1, -2, -3, -4

	base

	body -1
return

"textureHorizontal_01":
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return


"textureHorizontal_02":
	rotx 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return

"textureHorizontal_03":
	
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2+1024, -1, -2, -3, -4

	base

	body -1
return


"textureAlha":
	rotz alpha+90
	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 2

	coor 2, -1, -2, -3, -4

	base

	body -1
return
