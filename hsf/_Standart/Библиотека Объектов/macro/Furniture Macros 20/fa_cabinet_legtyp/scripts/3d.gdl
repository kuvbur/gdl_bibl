
!! fa_cabinet_legtyp/Macro

!-----Definitions-----
FORM_SQUARE   = 1
FORM_CORNER_L = 2
FORM_CORNER_C = 3

EPS = 0.0001
thickness = 0.02

if leg_h < EPS then end

PEN gs_cont_pen
MATERIAL gs_leg_mat


! ==============================================================================
! FORM_SQUARE
! ==============================================================================
if iCabinetForm = FORM_SQUARE then
	IF gs_detlevel_3D_m=1 then gs_leg_type_m=1

	!!!*** For Style 1,5
	IF gs_lining_m=1 THEN dist=-(a-2*side_overhang-leg_w)/2
	IF gs_lining_m=2 THEN dist=-(a-leg_w)/2
	IF gs_lining_m=3 THEN dist=-(a-2*side_overhang-leg_w)/2
	IF gs_lining_m=4 THEN dist=-(a-leg_w)/2

	IF gs_lining_m=1 THEN dd=a-2*side_overhang-leg_w
	IF gs_lining_m=2 THEN dd=a-side_overhang-leg_w
	IF gs_lining_m=3 THEN dd=a-side_overhang-leg_w
	IF gs_lining_m=4 THEN dd=a-leg_w


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 1/rectangle

	IF gs_leg_type_m=1 THEN

		ADD dist,-leg_w/2,0

			FOR r=1 TO 2
				FOR i=1 TO 2
					gosub "LegStyle_01"
				ADDX dd
				NEXT i
				DEL 2
			ADDY -b+cooh+leg_w
			NEXT r
			DEL 2

		DEL 1

	ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 2/round


	IF gs_leg_type_m=2 OR gs_leg_type_m=3 THEN

		RR=leg_w/2
		tt=0.01

		IF gs_lining_m=1 THEN dist2=-(a-2*side_overhang-2*RR)/2
		IF gs_lining_m=2 THEN dist2=-(a-2*RR)/2
		IF gs_lining_m=3 THEN dist2=-(a-2*side_overhang-2*RR)/2
		IF gs_lining_m=4 THEN dist2=-(a-2*RR)/2

		IF gs_lining_m=1 THEN dd2=(a-2*side_overhang-2*RR-2*tt)
		IF gs_lining_m=2 THEN dd2=(a-side_overhang-2*RR-2*tt)
		IF gs_lining_m=3 THEN dd2=(a-side_overhang-2*RR-2*tt)
		IF gs_lining_m=4 THEN dd2=(a-2*RR-2*tt)

			RESOL gs_resol
			ADD dist2,-RR,0
				FOR r=1 TO vl
					FOR i=1 TO hl
						ADD tt,-tt,0
						gosub "LegStyle_02_03"
						DEL 1
						ADDX dd2/(hl-1)
					NEXT i
					DEL hl
					ADDY (-b+cooh+2*RR+2*tt)/(vl-1)
				NEXT r
				DEL vl
			DEL 1

	ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 4

	IF gs_leg_type_m=4 and gs_lining_m=4 THEN
		PRISM_ 4,leg_h,
				-a/2+leg_w,0,15,
				-a/2+leg_w,-leg_w,15,
				a/2-leg_w,-leg_w,15,
				a/2-leg_w,0,15

		PRISM_ 4,leg_h,
				-a/2,0,15,
				-a/2,-b+cooh,15,
				-a/2+leg_w,-b+cooh,15,
				-a/2+leg_w,0,15

		PRISM_ 4,leg_h,
				a/2,0,15,
				a/2,-b+cooh,15,
				a/2-leg_w,-b+cooh,15,
				a/2-leg_w,0,15

		ADDY -b+cooh+leg_w+leg_w
			PRISM_ 4,leg_h,
					-a/2+leg_w,0,15,
					-a/2+leg_w,-leg_w,15,
					a/2-leg_w,-leg_w,15,
					a/2-leg_w,0,15
		DEL 1
	ENDIF

	IF gs_leg_type_m=4 and gs_lining_m=2 THEN
		PRISM_ 4,leg_h,
				-a/2+leg_w,0,15,
				-a/2+leg_w,-leg_w,15,
				a/2-side_overhang-leg_w,-leg_w,15,
				a/2-side_overhang-leg_w,0,15

		PRISM_ 4,leg_h,
				-a/2,0,15,
				-a/2,-b+cooh,15,
				-a/2+leg_w,-b+cooh,15,
				-a/2+leg_w,0,15

		PRISM_ 4,leg_h,
				a/2-side_overhang,0,15,
				a/2-side_overhang,-b+cooh,15,
				a/2-side_overhang-leg_w,-b+cooh,15,
				a/2-side_overhang-leg_w,0,15

		ADDY -b+cooh+leg_w+leg_w
			PRISM_ 4,leg_h,
					-a/2+leg_w,0,15,
					-a/2+leg_w,-leg_w,15,
					a/2-side_overhang-leg_w,-leg_w,15,
					a/2-side_overhang-leg_w,0,15
		DEL 1
	ENDIF

	IF gs_leg_type_m=4 and gs_lining_m=3 THEN
		PRISM_ 4,leg_h,
				-a/2+side_overhang+leg_w,0,15,
				-a/2+side_overhang+leg_w,-leg_w,15,
				a/2-leg_w,-leg_w,15,
				a/2-leg_w,0,15

		PRISM_ 4,leg_h,
				-a/2+side_overhang,0,15,
				-a/2+side_overhang,-b+cooh,15,
				-a/2+side_overhang+leg_w,-b+cooh,15,
				-a/2+side_overhang+leg_w,0,15

		PRISM_ 4,leg_h,
				a/2,0,15,
				a/2,-b+cooh,15,
				a/2-leg_w,-b+cooh,15,
				a/2-leg_w,0,15

		ADDY -b+cooh+leg_w+leg_w
			PRISM_ 4,leg_h,
					-a/2+side_overhang+leg_w,0,15,
					-a/2+side_overhang+leg_w,-leg_w,15,
					a/2-leg_w,-leg_w,15,
					a/2-leg_w,0,15
		DEL 1
	ENDIF

	IF gs_leg_type_m=4 and gs_lining_m=1 THEN
		PRISM_ 4,leg_h,
				-a/2+side_overhang+leg_w,0,15,
				-a/2+side_overhang+leg_w,-leg_w,15,
				a/2-side_overhang-leg_w,-leg_w,15,
				a/2-side_overhang-leg_w,0,15

		PRISM_ 4,leg_h,
				-a/2+side_overhang,0,15,
				-a/2+side_overhang,-b+cooh,15,
				-a/2+side_overhang+leg_w,-b+cooh,15,
				-a/2+side_overhang+leg_w,0,15

		PRISM_ 4,leg_h,
				a/2-side_overhang,0,15,
				a/2-side_overhang,-b+cooh,15,
				a/2-side_overhang-leg_w,-b+cooh,15,
				a/2-side_overhang-leg_w,0,15

		ADDY -b+cooh+leg_w+leg_w
			PRISM_ 4,leg_h,
					-a/2+side_overhang+leg_w,0,15,
					-a/2+side_overhang+leg_w,-leg_w,15,
					a/2-side_overhang-leg_w,-leg_w,15,
					a/2-side_overhang-leg_w,0,15
		DEL 1
	ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 5

	IF gs_leg_type_m=5 THEN

		IF gs_lining_m=1 THEN Temp=cooh
		IF gs_lining_m=2 THEN Temp=cooh/2
		IF gs_lining_m=3 THEN Temp=cooh/2
		IF gs_lining_m=4 THEN Temp=0

		IF gs_lining_m=1 THEN Temp2=side_overhang
		IF gs_lining_m=2 THEN Temp2=side_overhang/2
		IF gs_lining_m=3 THEN Temp2=side_overhang/2
		IF gs_lining_m=4 THEN Temp2=0

		IF gs_lining_m=1 THEN TempX=0
		IF gs_lining_m=2 THEN TempX=-side_overhang/2
		IF gs_lining_m=3 THEN TempX=side_overhang/2
		IF gs_lining_m=4 THEN TempX=0

		RESOl 8*gs_resol

		hura=SQR((0.5*leg_h*3/4)**2+(a/2-Temp2-leg_w)**2)
		ra=(hura*hura/2)/(0.5*leg_h*3/4)

		hurb=SQR((0.5*leg_h*3/4)**2+(b/2-Temp/2-leg_w)**2)
		rb=(hurb*hurb/2)/(0.5*leg_h*3/4)

		FOR i=1 TO 2		!* Front, Rear

				ROTX 90
				ADD TempX,leg_h*1/4,leg_w/2-leg_w/4
					PRISM_ 5,leg_w/2,
							-a/2+Temp2+leg_w,0,79,
							0,-ra+0.5*leg_h*3/4,900,
							a/2-Temp2-leg_w,0,3015,
							a/2-Temp2-leg_w,leg_h*3/4,15,
							-a/2+Temp2+leg_w,leg_h*3/4,15
				gosub "textureHorizontal_01"
				DEL 1
				DEL 1

		ADDY -b+cooh+leg_w
		NEXT i
		DEL 2


		FOR i=1 TO 2			!* Left, Right

				ADDX dist
				ROTZ 90
				ROTX 90
				ADD -leg_w,leg_h*1/4,-leg_w/4
				PRISM_ 5,leg_w/2,
						-b+cooh+leg_w+leg_w,0,79,
						(-b+cooh+leg_w+leg_w)/2,-rb+0.5*leg_h*3/4,900,
						0,0,3015,
						0,leg_h*3/4,15,
						-b+cooh+leg_w+leg_w,leg_h*3/4,15
				gosub "textureHorizontal_01"
				DEL 1
				DEL 1
				DEL 1
				DEL 1

		ADDX dd
		NEXT i
		DEL 2

		ADD dist,-leg_w/2,0

			FOR r=1 TO 2
				FOR i=1 TO 2
					gosub "LegStyle_01"
				ADDX dd
				NEXT i
				DEL 2
			ADDY -b+cooh+leg_w
			NEXT r
			DEL 2

		DEL 1

	ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 6

	IF gs_leg_type_m=6 THEN

		IF gs_lining_m=1 THEN Temp=A-side_overhang*2
		IF gs_lining_m=2 THEN Temp=A-side_overhang
		IF gs_lining_m=3 THEN Temp=A-side_overhang
		IF gs_lining_m=4 THEN Temp=A

		IF gs_lining_m=1 THEN TempX=side_overhang
		IF gs_lining_m=2 THEN TempX=0
		IF gs_lining_m=3 THEN TempX=side_overhang
		IF gs_lining_m=4 THEN TempX=0

		addx -A/2
		RESOl 8*gs_resol
		legRadius = leg_h / 4
		legW = leg_w - thickness

		for i=1 to 2			!* Left, Right panel
				prism_ 4,leg_h,
						TempX, -B+cooh, 15,
						TempX+thickness, -B+cooh+thickness, 15,
						TempX+thickness, -thickness, 15,
						TempX, 0, 15
				gosub "textureHorizontal_01"
				if legW > EPS then
					prism_ 4, leg_h,		! Leg
							TempX+thickness, -B+cooh+thickness, 15,
							TempX+thickness, -B+cooh+thickness+legW, 15,
							TempX+thickness+legW, -B+cooh+thickness+legW, 15,
							TempX+thickness+legW, -B+cooh+thickness, 15
					gosub "textureVertical_01"

					prism_ 4, leg_h,		! Leg
							TempX+thickness, -thickness, 15,
							TempX+thickness, -thickness-legW, 15,
							TempX+thickness+legW, -thickness-legW, 15,
							TempX+thickness+legW, -thickness, 15
					gosub "textureVertical_01"
				endif
				mulx -1
				addx -Temp-2*TempX
		next i
		del 4

		prism_ 4,leg_h,			!* Rear
				TempX, 0, 15,
				Temp+TempX, 0, 15,
				Temp+TempX-thickness, -thickness, 15,
				TempX+thickness, -thickness, 15

		if Temp > 2*leg_w + 2*legRadius then
			rotx 90
			cutpolya 8, 0, 0,
				TempX+leg_w, 0, 15,
				TempX+leg_w, legRadius, 79,
				TempX+leg_w+legRadius, legRadius, 900,
				TempX+leg_w+legRadius, 2*legRadius, 3079,
				Temp+TempX-leg_w-legRadius,  2*legRadius, 79,
				Temp+TempX-leg_w-legRadius,  legRadius, 900,
				Temp+TempX-leg_w,  legRadius, 3079,
				Temp+TempX-leg_w,  0, 15
			del 1
		endif

		prism_ 4,leg_h,			!* Front
				TempX, -B+cooh, 15,
				Temp+TempX, -B+cooh, 15,
				Temp+TempX-thickness, -B+cooh+thickness, 15,
				TempX+thickness, -B+cooh+thickness, 15

		if Temp > 2*leg_w + 2*legRadius then cutend

		del 1

	ENDIF
else
! ==============================================================================
! FORM_CORNER_L,  FORM_CORNER_C
! ==============================================================================
	sideDepth1 = sideDepth1 - side_overhang
	sideDepth2 = sideDepth2 - cooh
	if gs_lining_m=3 or gs_lining_m=1 then b = b - cooh
	if gs_lining_m=2 or gs_lining_m=1 then a = a - side_overhang

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 1/rectangle

	if gs_leg_type_m=1 or gs_leg_type_m=5 then
		add leg_w/2,-leg_w/2,0
		gosub "LegStyle_01"
		del 1

		add A-leg_w/2,-leg_w/2,0
		gosub "LegStyle_01"
		del 1

		add leg_w/2, -B+leg_w/2,0
		gosub "LegStyle_01"
		del 1

		add sideDepth1-leg_w/2, -B+leg_w/2,0
		gosub "LegStyle_01"
		del 1

		add A-leg_w/2, -sideDepth2+leg_w/2,0
		gosub "LegStyle_01"
		del 1

		if iCabinetForm = FORM_CORNER_L then
			add sideDepth1-leg_w/2, -sideDepth2+leg_w/2,0
			gosub "LegStyle_01"
			del 1
		endif
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 2/3 round


	if gs_leg_type_m=2 OR gs_leg_type_m=3 THEN
		RR=leg_w/2
		tt=0.01+RR

		add tt,-tt,0
		gosub "LegStyle_02_03"
		del 1

		add A-tt,-tt,0
		gosub "LegStyle_02_03"
		del 1

		add tt, -B+tt,0
		gosub "LegStyle_02_03"
		del 1

		add sideDepth1-tt, -B+tt,0
		gosub "LegStyle_02_03"
		del 1

		add A-tt, -sideDepth2+tt,0
		gosub "LegStyle_02_03"
		del 1

		if iCabinetForm = FORM_CORNER_L then
			add sideDepth1-tt, -sideDepth2+tt,0
			gosub "LegStyle_02_03"
			del 1
		endif
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 4

	if gs_leg_type_m=4 then
		prism_ 4,leg_h,
				0,0,15,
				a-leg_w,0,15,
				a-leg_w,-leg_w,15,
				leg_w,-leg_w,15
		gosub "textureHorizontal_01"

		prism_ 4,leg_h,
				0,0,15,
				leg_w,-leg_w,15,
				leg_w,-b+leg_w,15,
				0,-b+leg_w,15
		gosub "textureHorizontal_02"

		if iCabinetForm = FORM_CORNER_L then
			prism_ 4,leg_h,
					0, -b+leg_w,15,
					sideDepth1, -b+leg_w,15,
					sideDepth1, -b,15,
					0,-b,15
			gosub "textureHorizontal_01"

			prism_ 4,leg_h,
					a,0,15,
					a,-sideDepth2,15,
					a-leg_w,-sideDepth2,15,
					a-leg_w,0,15
			gosub "textureHorizontal_02"

			prism_ 4,leg_h,
					sideDepth1-2*leg_w, -sideDepth2+2*leg_w,15,
					sideDepth1-leg_w, -sideDepth2+leg_w,15,
					sideDepth1-leg_w, -B+leg_w,15,
					sideDepth1-2*leg_w, -B+leg_w,15
			gosub "textureHorizontal_02"

			prism_ 4,leg_h,
					sideDepth1-2*leg_w, -sideDepth2+2*leg_w,15,
					sideDepth1-leg_w, -sideDepth2+leg_w,15,
					a-leg_w, -sideDepth2+leg_w,15,
					a-leg_w, -sideDepth2+2*leg_w,15
			gosub "textureHorizontal_01"
		else
			if abs(sideDepth1-a)< EPS or abs(sideDepth2-b) < EPS then
				alpha = 90
			else
				alpha = atn((b-sideDepth2)/(a-sideDepth1))
			endif

			prism_ 4,leg_h,
					0, -b+leg_w,15,
					sideDepth1+leg_w/tan(alpha), -b+leg_w,15,
					sideDepth1, -b,15,
					0,-b,15
			gosub "textureHorizontal_01"

			prism_ 4,leg_h,
					a,0,15,
					a,-sideDepth2,15,
					a-leg_w,-sideDepth2-leg_w*tan(alpha),15,
					a-leg_w,0,15
			gosub "textureHorizontal_02"

			beta = (180-alpha)/2
			gamma = (90-alpha)/2

			prism_ 4,leg_h,
					sideDepth1-leg_w/tan(beta), -B+leg_w,15,
					sideDepth1-leg_w/tan(beta) - leg_w/sin(alpha), -B+leg_w,15,
					a-leg_w,-sideDepth2+leg_w*tan(gamma)+leg_w/sin(90-alpha),15,
					a-leg_w,-sideDepth2+leg_w*tan(gamma),15
			gosub "textureAlha"

		endif
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 5

	if gs_leg_type_m=5 then
		resol 8*gs_resol

		TempX = (B-2*leg_w)/2
		rotz -90
		add b/2, leg_w/2+leg_w/4, 0
		gosub "LegStyle_05"
		del 2

		TempX = (sideDepth2-2*leg_w)/2
		rotz -90
		add sideDepth2/2, A-leg_w/2+leg_w/4, 0
		gosub "LegStyle_05"
		del 2

		TempX = (A-2*leg_w)/2
		add A/2, -leg_w/4, 0
		gosub "LegStyle_05"
		del 1

		TempX = (sideDepth1-2*leg_w)/2
		add sideDepth1/2, -B+leg_w/4+leg_w/2, 0
		gosub "LegStyle_05"
		del 1

		if iCabinetForm = FORM_CORNER_L then
			TempX = (B-sideDepth2-leg_w)/2
			rotz -90
			add B-(B-sideDepth2+leg_w)/2, sideDepth1-leg_w/2+leg_w/4, 0
			gosub "LegStyle_05"
			del 2

			TempX = (A-sideDepth1-leg_w)/2
			add sideDepth1+(A-sideDepth1-leg_w)/2, -sideDepth2+leg_w/2+leg_w/4, 0
			gosub "LegStyle_05"
			del 1
		else
			if abs(sideDepth1-a)< EPS or abs(sideDepth2-b) < EPS then
				alpha = 90
			else
				alpha = atn((b-sideDepth2)/(a-sideDepth1))
			endif

			TempX = ((B-sideDepth2-(leg_w/4)/sin(90-alpha) ) / sin (alpha) ) /2
			add sideDepth1+cos(alpha)*Tempx, -B+(leg_w/4)/sin(90-alpha)+sin(alpha)*Tempx, 0
			rotz alpha
			muly -1
			gosub "LegStyle_05"
			del 3
		endif
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!Style 6

	if gs_leg_type_m=6 then
		resol 8*gs_resol
		legRadius = leg_h / 4
		legW = leg_w - thickness

		if abs(sideDepth1-a)< EPS or abs(sideDepth2-b) < EPS then
			alpha = 90
		else
			alpha = atn((b-sideDepth2)/(a-sideDepth1))
		endif
		beta = (180-alpha)/2
		gamma = (90-alpha)/2

		put 0, 0, 15
		put a, 0, 15
		put a, -sideDepth2, 15
		if iCabinetForm = FORM_CORNER_L then
			put sideDepth1, -sideDepth2, 15
		endif
		put sideDepth1, -b, 15
		put 0, -b, 15
		put 0, 0, -1

		put a-thickness, -thickness, 15
		if iCabinetForm = FORM_CORNER_L then
			put a-thickness, -sideDepth2+thickness, 15
			put sideDepth1-thickness, -sideDepth2+thickness, 15
			put sideDepth1-thickness, -B+thickness,15
		else
			put a-thickness, -sideDepth2+thickness*tan(gamma), 15
			put sideDepth1-thickness/tan(beta), -B+thickness,15
		endif
		put thickness, -B+thickness,15
		put thickness, -thickness,15
		put a-thickness, -thickness, -1


		if iCabinetForm = FORM_CORNER_L then
			TempX = (B-sideDepth2)
			TempX1 = TempX
			rotz -90
			add B-(B-sideDepth2), sideDepth1, 0
			if TempX > 2*leg_w + 2*legRadius then gosub "LegStyle_06_Cut"
			del 2

			TempX = (A-sideDepth1)
			TempX2 = TempX
			add sideDepth1+TempX, -sideDepth2, 0
			mulx -1
			if TempX > 2*leg_w + 2*legRadius then gosub "LegStyle_06_Cut"
			del 2
		else
			TempX = (B-sideDepth2) / sin (alpha)
			add sideDepth1, -B, 0
			rotz alpha+180
			addx -TempX
			if TempX > 2*leg_w + 2*legRadius then gosub "LegStyle_06_Cut"
			del 3
		endif

		prism_ nsp/3, leg_h,
				get (nsp)
		gosub "textureHorizontal_01"

		if iCabinetForm = FORM_CORNER_L then
			if TempX1 > 2*leg_w + 2*legRadius then cutend
			if TempX2 > 2*leg_w + 2*legRadius then cutend
		else
			if TempX > 2*leg_w + 2*legRadius then cutend
		endif
	endif
endif


END

! ======================================================================
! SubRoutines
! ======================================================================
"LegStyle_01":
	prism_ 4,leg_h,
			-leg_w/2,-leg_w/2,15,
			-leg_w/2,leg_w/2,15,
			 leg_w/2,leg_w/2,15,
			 leg_w/2,-leg_w/2,15
	gosub "textureVertical_01"

return


"LegStyle_02_03":
	if gs_leg_type_m=2 then
		prism_ 2,leg_h,
				0,0,979,
				RR,360,4079
	else
		addz leg_h
		roty 90
		revolve 6,360,1+2,
				0,RR,0,
				leg_h/5-0.0001,RR,0,
				leg_h/5,RR,1,
				leg_h/2,-leg_h*leg_h,900,
				leg_h*4/5,RR,3000,
				leg_h,RR,0
		del 2
	endif
	!gosub "textureVertical_01"

return


"LegStyle_05":
	!alpha2=1/ATN((0.5*leg_h*3/4)/(TempX))
	hura=sqr((0.5*leg_h*3/4)**2+(TempX)**2)
	ra=(hura*hura/2)/(0.5*leg_h*3/4)

	rotx 90
	addy leg_h*1/4
	prism_ 5,leg_w/2,
			-TempX,0,79,
			0,-ra+0.5*leg_h*3/4,900,
			TempX,0,3015,
			TempX,leg_h*3/4,15,
			-TempX,leg_h*3/4,15
	gosub "textureHorizontal_01"
	del 2
return


"LegStyle_06_Cut":
	rotx 90
	cutpolya 8, 0, -thickness,
		leg_w, 0, 15,
		leg_w, legRadius, 79,
		leg_w+legRadius, legRadius, 900,
		leg_w+legRadius, 2*legRadius, 3079,
		TempX-leg_w-legRadius,  2*legRadius, 79,
		TempX-leg_w-legRadius,  legRadius, 900,
		TempX-leg_w,  legRadius, 3079,
		TempX-leg_w,  0, 15,
		0,0,-1
	del 1
return


"textureVertical_01":
	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return

"textureVertical_02":
	roty 90
	rotz 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 2

	coor 2, -1, -2, -3, -4

	base

	body -1
return


"textureHorizontal_01":
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return


"textureHorizontal_02":
	rotz 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return

"textureAlha":
	rotz alpha
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return


