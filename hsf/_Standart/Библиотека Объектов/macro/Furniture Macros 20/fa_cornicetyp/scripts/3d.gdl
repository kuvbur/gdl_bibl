
!! fa_cornicetyp/Macro

EPS = 0.0001
if cot < EPS then end


PEN gs_cont_pen
RESOL max(6, gs_resol)
MATERIAL gs_cornice_mat

!-----Definitions-----
FORM_SQUARE   = 1
FORM_CORNER_L = 2
FORM_CORNER_C = 3
!! LINING: 1=None, 2=Left, 3=Right, 4=Both


IF gs_detlevel_3D_m=1 then gs_cornice_type_m=CORN_STYLE_1


! ==============================================================================
! FORM_SQUARE
! ==============================================================================
if iCabinetForm = FORM_SQUARE then

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 1
	IF gs_cornice_type_m=CORN_STYLE_1 THEN

			PRISM_ 4, cot,
					-a/2,0,15,
					-a/2,-b,15,
					a/2,-b,15,
					a/2,0,15

			gosub "textureHorizontal_01"

	ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 2
	IF gs_cornice_type_m=CORN_STYLE_2 THEN

		IF gs_lining_m=1 then
			LEnd=cot/2
			REnd=cot/2
			LCorn=0
			RCorn=0
		ENDIF

		IF gs_lining_m=2 then

			LEnd=cot/2
			REnd=0
			LCorn=0
			RCorn=cot/2

		ENDIF

		IF gs_lining_m=3 then
			LEnd=0
			REnd=cot/2
			LCorn=cot/2
			RCorn=0

		ENDIF

		IF gs_lining_m=4 then
			LEnd=0
			REnd=0
			LCorn=cot/2
			RCorn=cot/2
		ENDIF

		IF gs_lining_m=2 OR gs_lining_m=4 then

			ADDx a/2
			ROTy 90
				CUTPLANE
			DEL 2

		ENDIF

		IF gs_lining_m=3 OR gs_lining_m=4 then

			ADDx -a/2
			ROTy -90
				CUTPLANE
			DEL 2

		ENDIF

		r=((cot/4)**2+(cot/2)**2)/(cot/2)

			TUBE 3,6,1+2+16+32,
					0,0,1,
					cot/2,r-cot/4,800,
					0,cot,1000,

					a/2+RCorn-cot/4,1,0,0,
					a/2+RCorn-cot/4,0,0,0,
					a/2+RCorn-cot/4,-b+cot/4,0,0,
					-a/2-LCorn+cot/4,-b+cot/4,0,0,
					-a/2-LCorn+cot/4,0,0,0,
					-a/2-LCorn+cot/4,1,0,0

			gosub "TextureCylind"

		vv=a/2-side_overhang/4
		vv2=-a/2+side_overhang/4

			PRISM_ 4, cot,
					-a/2+LEnd-cot/4,0,15,
					-a/2+LEnd-cot/4,-b+cot/4,15,
					a/2-REnd+cot/4,-b+cot/4,15,
					a/2-REnd+cot/4,0,15

		IF gs_lining_m=2 OR gs_lining_m=4 then CUTEND
		IF gs_lining_m=3 OR gs_lining_m=4 then CUTEND


	ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 3
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF gs_cornice_type_m=CORN_STYLE_3 THEN

		IF gs_lining_m=1 then
			LEnd=cot*3/8
			REnd=cot*3/8
			LCorn=0
			RCorn=0
		ENDIF

		IF gs_lining_m=2 then

			LEnd=cot*3/8
			REnd=0
			LCorn=0
			RCorn=cot*3/8

		ENDIF

		IF gs_lining_m=3 then
			LEnd=0
			REnd=cot*3/8
			LCorn=cot*3/8
			RCorn=0

		ENDIF

		IF gs_lining_m=4 then
			LEnd=0
			REnd=0
			LCorn=cot*3/8
			RCorn=cot*3/8
		ENDIF

		IF gs_lining_m=2 OR gs_lining_m=4 then

			ADDx a/2
			ROTy 90
				CUTPLANE
			DEL 2

		ENDIF

		IF gs_lining_m=3 OR gs_lining_m=4 then

			ADDx -a/2
			ROTy -90
				CUTPLANE
			DEL 2

		ENDIF

			ADDZ cot/4
				TUBE 3,6,1+2+16+32,
					0,0,1,
					1,0,800,
					0,cot*3/4,1000,

					a/2+RCorn-cot*3/8,1,0,0,
					a/2+RCorn-cot*3/8,0,0,0,
					a/2+RCorn-cot*3/8,-b+cot*3/8,0,0,
					-a/2-LCorn+cot*3/8,-b+cot*3/8,0,0,
					-a/2-LCorn+cot*3/8,0,0,0,
					-a/2-LCorn+cot*3/8,1,0,0
			DEL 1

			PRISM_ 4, cot,
					-a/2+LEnd,0,15,
					-a/2+LEnd,-b+cot*3/8,15,
					a/2-REnd,-b+cot*3/8,15,
					a/2-REnd,0,15

		IF gs_lining_m=2 OR gs_lining_m=4 then CUTEND
		IF gs_lining_m=3 OR gs_lining_m=4 then CUTEND

	ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 4
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF gs_cornice_type_m=CORN_STYLE_4 THEN

		IF gs_lining_m=2 OR gs_lining_m=1 then
			ADDX -a/2 !-LEnd
			ROTX 90
			CUTPOLYA 4,1,0,			!! left-bottom
				   0,0,15,
				   cot*3/4,0,15,
				   cot*3/4,cot/4,15,
				   0,cot/4,15

			CUTPOLYA  2,1,0,		!! left - arc
				   0,cot/4,979,
				   cot/2,360,4015
			DEL 2
		ENDIF

		!!!!!!!!!!!!!!

		IF gs_lining_m=3 OR gs_lining_m=1 then
			ADDX a/2 !+REnd
			MULX -1
			ROTX 90
			CUTPOLYA 4,1,0,		!! right-bottom
				   0,0,15,
				   cot*3/4,0,15,
				   cot*3/4,cot/4,15,
				   0,cot/4,15

			CUTPOLYA 2,1,0,		!! right - arc
				   0,cot/4,979,
				   cot/2,360,4015
			DEL 3
		ENDIF

		!!!!!!!!!!!!!
		ROTY 90
		ROTZ 90
		ADDX -b
		CUTPOLYA 4,1,0,		!! front-bottom
				0,0,15,
				cot*3/4,0,15,
				cot*3/4,cot/4,15,
				0,cot/4,15

		CUTPOLYA 2,1,0,		!! front - arc
				0,cot/4,979,
				cot/2,360,4015
		DEL 1
		DEL 1
		DEL 1
		!!!!!!!!!!!!!!!

		PRISM_ 4, cot,
				-a/2,0,15,
				-a/2,-b,15,
				a/2,-b,15,
				a/2,0,15

		IF gs_lining_m=2 OR gs_lining_m=1 then CUTEND
		IF gs_lining_m=2 OR gs_lining_m=1 then CUTEND
		IF gs_lining_m=3 OR gs_lining_m=1 then 	CUTEND
		IF gs_lining_m=3 OR gs_lining_m=1 then 	CUTEND

		CUTEND
		CUTEND

	ENDIF
else
! ==============================================================================
! FORM_CORNER_L,  FORM_CORNER_C
! ==============================================================================
	if abs(sideDepth1-a)< EPS or abs(sideDepth2-b) < EPS then
		alpha = 90
	else
		alpha = atn((b-sideDepth2)/(a-sideDepth1))
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 1
	if gs_cornice_type_m=CORN_STYLE_1 then
		plateHeight = cot
		gosub "cornicePlate"
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 2
	if gs_cornice_type_m=CORN_STYLE_2 then
		profileWidth = cot/4
		gosub "profileCalculation"

		plateHeight = cot
		gosub "cornicePlate"
		gosub "profilePath"

		r=((cot/4)**2+(cot/2)**2)/(cot/2)
		tube 3,nsp/4,1+2+16+32+128,
				0,0,1,
				cot/2,r-cot/4,800,
				0,cot,1000,
				get(nsp)

		gosub "TextureCylind"
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 3
	if gs_cornice_type_m=CORN_STYLE_3 then
		profileWidth = cot/2
		gosub "profileCalculation"

		plateHeight = cot
		gosub "cornicePlate"
		gosub "profilePath"

		addz cot/4
		tube 3,nsp/4,1+2+16+32+128,
				0,0,1,
				1,0,800,
				0,cot*3/4,1000,
				get(nsp)
		del 1

		gosub "TextureCylind"
	endif


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Style 4
	if gs_cornice_type_m=CORN_STYLE_4 then
		addz cot*3/4
		plateHeight = cot/4
		gosub "cornicePlate"
		del 1

		profileWidth = cot
		gosub "profileCalculation"

		plateHeight = cot*3/4
		gosub "cornicePlate"
		gosub "profilePath"


		tube 7,nsp/4,1+2+16+32+128,
				0,0,0,
				cot/4,0,0,
				cot/4,cot/4,0,
				cot/2,cot/4,1,
				cot,cot/4,900,
				cot,cot*3/4,3000,
				0, cot*3/4, 0,
				get(nsp)

		gosub "TextureCylind"
	endif

endif


END

! ======================================================================
! SubRoutines
! ======================================================================

"cornicePlate":
	if iCabinetForm = FORM_CORNER_L then
		put 0, 0, 15
		put a, 0, 15
		put a, -sideDepth2, 15
		put sideDepth1, -sideDepth2, 10

		prism_ nsp/3, plateHeight,
				get (nsp)
		gosub "textureHorizontal_02"

		put 0, 0, 8
		put sideDepth1, -sideDepth2, 15
		put sideDepth1, -b, 15
		put 0, -b, 15

		prism_ nsp/3, plateHeight,
				get (nsp)
		gosub "textureVertical_02"

	else
		put 0, 0, 15
		put a, 0, 15
		put a, -sideDepth2, 15
		put sideDepth1, -b, 15
		put 0, -b, 15

		prism_ nsp/3, plateHeight,
				get (nsp)
		gosub "textureAlpha"
	endif
return


"profileCalculation":
	if iCabinetForm = FORM_CORNER_L then
		sideDepth1 = sideDepth1 - profileWidth
		sideDepth2 = sideDepth2 - profileWidth
	else
		if gs_lining_m=1 then
			sideDepth1 = sideDepth1 - profileWidth/tan((180-alpha)/2)
			sideDepth2 = sideDepth2 - profileWidth*tan((90-alpha)/2)
		endif
		if gs_lining_m=2 then
			sideDepth1 = sideDepth1 - profileWidth/cos(90-alpha)
			sideDepth2 = sideDepth2 - profileWidth*tan((90-alpha)/2)
		endif
		if gs_lining_m=3 then
			sideDepth1 = sideDepth1 - profileWidth/tan((180-alpha)/2)
			sideDepth2 = sideDepth2 - profileWidth/sin(90-alpha)
		endif
		if gs_lining_m=4 then
			sideDepth1 = sideDepth1 - profileWidth/cos(90-alpha)
			sideDepth2 = sideDepth2 - profileWidth/sin(90-alpha)
		endif
	endif
	if gs_lining_m=3 or gs_lining_m=1 then b = b - profileWidth
	if gs_lining_m=2 or gs_lining_m=1 then a = a - profileWidth
return


"profilePath":
	if gs_lining_m=1 then
		put a, 1, 0, 0
		put a, 0, 0, 0
		put a, -sideDepth2, 0, 0
		if iCabinetForm = FORM_CORNER_L then
			put sideDepth1, -sideDepth2, 0, 0
		endif
		put sideDepth1, -b, 0, 0
		put 0, -b, 0, 0
		put -1, -b, 0, 0
	endif

	if gs_lining_m=2 then
		put a, 1, 0, 0
		put a, 0, 0, 0
		put a, -sideDepth2, 0, 0
		if iCabinetForm = FORM_CORNER_L then
			put sideDepth1, -sideDepth2, 0, 0
		endif
		put sideDepth1, -b, 0, 0
		if iCabinetForm = FORM_CORNER_L then
			put sideDepth1, -b-1, 0, 0
		else
			put sideDepth1+cos(alpha), -b-sin(alpha), 0, 0
		endif
	endif

	if gs_lining_m=3 then
		if iCabinetForm = FORM_CORNER_L then
			put a+1, -sideDepth2, 0, 0
		else
			put a+sin(90-alpha), -sideDepth2-cos(90-alpha), 0, 0
		endif
		put a, -sideDepth2, 0, 0
		if iCabinetForm = FORM_CORNER_L then
			put sideDepth1, -sideDepth2, 0, 0
		endif
		put sideDepth1, -b, 0, 0
		put 0, -b, 0, 0
		put -1, -b, 0, 0
	endif

	if gs_lining_m=4 then
		if iCabinetForm = FORM_CORNER_L then
			put a+1, -sideDepth2, 0, 0
		else
			put a+sin(90-alpha), -sideDepth2-cos(90-alpha), 0, 0
		endif
		put a, -sideDepth2, 0, 0
		if iCabinetForm = FORM_CORNER_L then
			put sideDepth1, -sideDepth2, 0, 0
		endif
		put sideDepth1, -b, 0, 0
		if iCabinetForm = FORM_CORNER_L then
			put sideDepth1, -b-1, 0, 0
		else
			put sideDepth1+cos(alpha), -b-sin(alpha), 0, 0
		endif
	endif

return


"textureVertical_01":
	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return


"textureVertical_02":
	roty 90
	rotz 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 2

	coor 2, -1, -2, -3, -4

	base

	body -1
return


"textureHorizontal_01":
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return


"textureHorizontal_02":
	rotx 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return


"TextureCylind":
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 3, -1, -2, -3, -4

	base

	body -1
return


"textureAlpha":
	rotz alpha+90
	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 2

	coor 2, -1, -2, -3, -4

	base

	body -1
return

"textureSphere":
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 4, -1, -2, -3, -4

	base

	body -1
return

