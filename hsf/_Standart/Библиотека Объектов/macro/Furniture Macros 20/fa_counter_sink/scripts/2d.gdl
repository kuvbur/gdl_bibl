
PEN gs_cont_pen

unID=1

HOTSPOT2 0,b,unID: unID=unID+1
HOTSPOT2 a/2,b,unID: unID=unID+1

IF cab_size=0 THEN

	!!! *** Left Overhang of Cabinet
	HOTSPOT2 0,front_cab,unID, left_cab, 1+128 :unID=unID+1
	HOTSPOT2 -1,front_cab,unID, left_cab, 3: unID=unID+1
	HOTSPOT2 left_cab,front_cab,unID, left_cab, 2: unID=unID+1

	HOTSPOT2 0,B-rear_cab,unID, left_cab, 1+128 :unID=unID+1
	HOTSPOT2 -1,B-rear_cab,unID, left_cab, 3: unID=unID+1
	HOTSPOT2 left_cab,B-rear_cab,unID, left_cab, 2: unID=unID+1

	!!! *** Right Overhang of Cabinet
	HOTSPOT2 A,front_cab,unID, right_cab, 1+128 :unID=unID+1
	HOTSPOT2 A+1,front_cab,unID, right_cab, 3: unID=unID+1
	HOTSPOT2 A-right_cab,front_cab,unID, right_cab, 2: unID=unID+1

	HOTSPOT2 A,B-rear_cab,unID, right_cab, 1+128 :unID=unID+1
	HOTSPOT2 A+1,B-rear_cab,unID, right_cab, 3: unID=unID+1
	HOTSPOT2 A-right_cab,B-rear_cab,unID, right_cab, 2: unID=unID+1

	!!! *** Front Overhang of Cabinet
	HOTSPOT2 left_cab,0,unID, front_cab, 1+128 :unID=unID+1
	HOTSPOT2 left_cab,-1,unID, front_cab, 3: unID=unID+1
	HOTSPOT2 left_cab,front_cab,unID, front_cab, 2: unID=unID+1

	HOTSPOT2 A-right_cab,0,unID, front_cab, 1+128 :unID=unID+1
	HOTSPOT2 A-right_cab,-1,unID, front_cab, 3: unID=unID+1
	HOTSPOT2 A-right_cab,front_cab,unID, front_cab, 2: unID=unID+1

	!!! *** Rear Overhang of Cabinet
	HOTSPOT2 left_cab,B,unID, rear_cab, 1+128 :unID=unID+1
	HOTSPOT2 left_cab,B+1,unID, rear_cab, 3: unID=unID+1
	HOTSPOT2 left_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

	HOTSPOT2 A-right_cab,B,unID, rear_cab, 1+128 :unID=unID+1
	HOTSPOT2 A-right_cab,B+1,unID, rear_cab, 3: unID=unID+1
	HOTSPOT2 A-right_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

ELSE

	!!! *** Hotspots on bounding box
	HOTSPOT2 0,B,unID :unID=unID+1
	HOTSPOT2 0,0,unID :unID=unID+1
	HOTSPOT2 A,0,unID :unID=unID+1
	HOTSPOT2 A,B,unID :unID=unID+1

	!! *** Left Overhang of Cabinet
	HOTSPOT2 0,front_cab+temp_b/2,unID, left_cab, 1+256 :unID=unID+1
	HOTSPOT2 -1,front_cab+temp_b/2,unID, left_cab, 3: unID=unID+1
	HOTSPOT2 left_cab,front_cab+temp_b/2,unID, left_cab, 2: unID=unID+1

	!! *** Right Overhang of Cabinet
	HOTSPOT2 left_cab+temp_a,front_cab+temp_b/2,unID, right_cab, 1+256 :unID=unID+1
	HOTSPOT2 left_cab+temp_a-1,front_cab+temp_b/2,unID, right_cab, 3: unID=unID+1
	HOTSPOT2 left_cab+temp_a+right_cab,front_cab+temp_b/2,unID, right_cab, 2: unID=unID+1

	!! *** Front Overhang of Cabinet
	HOTSPOT2 left_cab+temp_a/2,0,unID, front_cab, 1+256 :unID=unID+1
	HOTSPOT2 left_cab+temp_a/2,-1,unID, front_cab, 3: unID=unID+1
	HOTSPOT2 left_cab+temp_a/2,front_cab,unID, front_cab, 2: unID=unID+1

	!! *** Rear Overhang of Cabinet
	HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b,unID, rear_cab, 1+256 :unID=unID+1
	HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b-1,unID, rear_cab, 3: unID=unID+1
	HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b+rear_cab,unID, rear_cab, 2: unID=unID+1

ENDIF


! ==============================================================================
! COUNTER and BACKSPLASH
! ==============================================================================

drawindex 5

line2 -lftmod,0,a+rgtmod,0
line2 -lftmod,b+bckmod,a+rgtmod,b+bckmod


if gs_panel=1 then
	line_type gs_open_line
	line2 0,0+front_cab,0,-a+front_cab
	arc2 0,0+front_cab,a,270,360
else
	if gs_panel=2 then
		line_type gs_open_line
		line2 0,0+front_cab,0,-a/2+front_cab
		arc2 0,0+front_cab,a/2,270,360
		line2 a,0+front_cab,a,-a/2+front_cab
		arc2 a,0+front_cab,a/2,180,270
	else
		if gs_panel=3 then
			line_type gs_open_line
			line2 0,0+front_cab,0,-a/3+front_cab
			arc2 0,0+front_cab,a/3,270,360
			line2 a/3,0+front_cab,a/3,-a/3+front_cab
			arc2 a/3,0+front_cab,a/3,270,360
			line2 a,0+front_cab,a,-a/3+front_cab
			arc2 a,0+front_cab,a/3,180,270
		endif
	endif
endif

line_type corp_line

if drawer2d then
	lengthReduce = 0
	if bReduceLineLengthin2D then lengthReduce = 0.02
	for i=1 to drawer_num
		line2 (i-1) * lengthReduce, 0-0.02, a - (i-1) * lengthReduce, 0-0.02
		add2 0,-0.02
	next i
	del drawer_num
endif

if drawer2d and drawer then
	add2 0,-((drawer_num+1)*0.02)
	line2 drawer_num * 0.02 * bReduceLineLengthin2D, 0, a - drawer_num * 0.02 * bReduceLineLengthin2D, 0
	del 1
endif

if bs_h > 0 and dob_face=0 then LINE2 0, b-backSplashThk, a, b-backSplashThk	! Backsplash

if side_vis2D_m=2 or side_vis2D_m=4 then LINE2 -lftmod,0,-lftmod,b+bckmod
if side_vis2D_m=3 or side_vis2D_m=4 then LINE2 a+rgtmod,0,a+rgtmod,b+bckmod

drawindex 10
FILL gs_fill_type

poly2_b 5, 2+64,gs_fill_pen,gs_back_pen,
	   -lftmod,0,1,
	   a+rgtmod,0,1,
	   a+rgtmod,b+bckmod,1,
	   -lftmod,b+bckmod,1,
	   -lftmod,0,-1

if drawer2d then
	poly2_b 5, 2+64,gs_fill_pen,gs_back_pen,
			-lftmod,0-(drawer_num*0.02),1,
			a+rgtmod,0-(drawer_num*0.02),1,
			a+rgtmod,b,1,
			-lftmod,b,1,
			-lftmod,0-(drawer_num*0.02),-1
endif

if drawer2d and drawer then
	poly2_b 5, 2+64,gs_fill_pen,gs_back_pen,
			-lftmod,0-((drawer_num+1)*0.02),1,
			a+rgtmod,0-((drawer_num+1)*0.02),1,
			a+rgtmod,b,1,
			-lftmod,b,1,
			-lftmod,0-((drawer_num+1)*0.02),-1
endif

hotline2 -lftmod,0,a,0
hotline2 a+rgtmod,0,a,b
hotline2 a+rgtmod,b+bckmod,0,b
hotline2 -lftmod,b+bckmod,0,0

! === Minimal space ============================================================
reqBMinSpace = 0
call "fa_minimalspace" parameters all MSType = 20, unID = 3000,
							returned_parameters reqBMinSpace,
												MSFillType,
												MSFillPen,
												MSFillBackPen,
												MSContLineType,
												MSContLinePen

! === Sink =====================================================================
PEN gs_cont_pen
line_type corp_line

if gs_sink_type_m <> 0 and not(bBelfastSink) then

	add2 temp_aa, temp_bb - backSplashThk * (sink_b > 0.5) * NOT(cab_size)

		! === Sink Streching / 2D Hotspot Editing ===============================

		hotspot2 0,			sink_disty, unID, sink_dist, 1 + 128	: unID = unID + 1
		hotspot2 sink_dist,	sink_disty, unID, sink_dist, 2			: unID = unID + 1
		hotspot2 -1,		sink_disty, unID, sink_dist, 3			: unID = unID + 1

		hotspot2 sink_dist,	0,			unID, sink_disty, 1 + 128	: unID = unID + 1
		hotspot2 sink_dist,	sink_disty,	unID, sink_disty, 2			: unID = unID + 1
		hotspot2 sink_dist,	-1,			unID, sink_disty, 3			: unID = unID + 1

		add2 -sink_a / 2 + sink_dist, -sink_b / 2 + sink_disty

			call "fa_sinktyp" parameters all	a				= sink_a,
												b				= sink_b,
												zzyzx			= sink_z,
												bEnableTapEdit	= 1

			if gs_sink_type_m = 1 then
				! round sink
				hotarc2 sink_a/2,sink_b/2,sink_a/2,0,180
				hotarc2 sink_a/2,sink_b/2,sink_a/2,180,360
			else
				hotline2 0, 0, sink_a,0
				hotline2 sink_a,0,sink_a,sink_b
				hotline2 sink_a,sink_b,0,sink_b
				hotline2 0,sink_b,0,0
			endif

		del 1

	del 1

endif

END
