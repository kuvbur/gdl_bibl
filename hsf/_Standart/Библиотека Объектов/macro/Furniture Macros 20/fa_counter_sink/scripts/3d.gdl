
if countsink_unID < 1 then
	unID=200
else
	unID=countsink_unID
endIf

if	gs_detlevel_3D_m = 0	|\
	ABS(count_th) < EPS		then 

	countsink_unID = unID
	end countsink_unID
endif

if not(gs_shadow) then SHADOW OFF


MATERIAL gs_sink_mat
PEN gs_cont_pen

RESOL gs_resol


! --- 3D Hotspot in Bounding Boxs ---

if zzyzx_edit then
	hotspot a+rgtmod, b+bckmod, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a+rgtmod, b+bckmod, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a+rgtmod, b+bckmod, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot -lftmod, b+bckmod, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot -lftmod, b+bckmod, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot -lftmod, b+bckmod, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot a+rgtmod, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a+rgtmod, 0, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a+rgtmod, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot -lftmod, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot -lftmod, 0, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot -lftmod, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1
endif

if gs_sink_type_m <> 0 and not(bBelfastSink) then

	add temp_aa, temp_bb - backSplashThk * (sink_b > 0.5) * NOT(cab_size), zzyzx

	if gs_full_edit = 1 then
		! === Sink Streching / 3D Hotspot Editing ===============================

		hotspot 0,			sink_disty, 0, unID, sink_dist, 1 + 128	: unID = unID + 1
		hotspot sink_dist,	sink_disty, 0, unID, sink_dist, 2			: unID = unID + 1
		hotspot -1,			sink_disty, 0, unID, sink_dist, 3			: unID = unID + 1

		hotspot sink_dist,	0,			0, unID, sink_disty, 1 + 128	: unID = unID + 1
		hotspot sink_dist,	sink_disty,	0, unID, sink_disty, 2			: unID = unID + 1
		hotspot sink_dist,	-1,			0, unID, sink_disty, 3			: unID = unID + 1
	endif

	add -sink_a / 2 + sink_dist, -sink_b / 2 + sink_disty, -sink_z -count_th * sunder

	call "fa_sinktyp" parameters all	a				= sink_a,
										b				= sink_b,
										zzyzx			= sink_z,
										bEnableTapEdit	= 1

	del 1

	del 1

	gosub "COUNTER and BACKSPLASH"

	countsink_unID=unID

	END countsink_unID

endif

! --- Counter without Hole -----------------------------------------------------
IF count_th>0 THEN

	MATERIAL count_mat
	ADDZ zzyzx-count_th

	if gs_count_type_m=1 then
		PRISM_ 4,count_th,
				0,0,13+2*(side_vis_m=2 OR side_vis_m=4),
				a,0, 8+7*(side_vis_m>2),
				a,b,13+2*(side_vis_m>2),
				0,b,8+7*(side_vis_m=2 OR side_vis_m=4)
	endif
	if gs_count_type_m=2 then
		if gs_detlevel_3D_m=1 then gs_resol=4
		resol gs_resol
		gosub 6000
		gosub 5000
	Endif
	DEL 1
	GOSUB 300

ENDIF

countsink_unID = unID
end countsink_unID

! ==============================================================================
! *** COUNTER and BACKSPLASH
! ==============================================================================
"COUNTER and BACKSPLASH":

IF count_th>0 THEN
	MATERIAL count_mat
	ADDZ zzyzx-count_th
	if gs_count_type_m=1 or gs_detlevel_3D_m=1 then		!rectangular edge or simple 3D
		put 0,0,13+2*(side_vis_m=2 OR side_vis_m=4),
			a,0, 8+7*(side_vis_m>2),
			a,b,13+2*(side_vis_m>2),
			0,b,8+7*(side_vis_m=2 OR side_vis_m=4),
			0,0,79,
			0,0,-1
	endif

	IF gs_sink_type_m=1 then
		if sunder then
			walt=sink_a/2-0.02
		else
			walt=sink_a/2-0.015
		endif
		if gs_detlevel_3D_m=1 then resol 12
		put	temp_aa+sink_dist,temp_bb+sink_disty,979,
			walt,360,4079

	else	! gs_sink_type_m<>1
		if sunder then
			radCorner = 0.055
			overhangLower = 0.045
			overhangUpper = 0.095
			overhangLeft = 0.045
			overhangRight = 0.045
		else
			overhangLower = 0.025
			overhangUpper = 0.04
			overhangLeft = 0.025
			overhangRight = 0.025
		endif
		if gs_detlevel_3D_m=2 and sunder then
			put	temp_aa-sink_a/2+sink_dist+radCorner+overhangLeft, 	temp_bb-sink_b/2+sink_disty+overhangLower, 	15,
				temp_aa+sink_a/2+sink_dist-radCorner-overhangRight, temp_bb-sink_b/2+sink_disty+overhangLower, 	15,
				temp_aa+sink_a/2+sink_dist-overhangRight, 			temp_bb-sink_b/2+sink_disty+radCorner+overhangLower, 1015,
				temp_aa+sink_a/2+sink_dist-overhangRight, 			temp_bb+sink_b/2+sink_disty-radCorner-overhangUpper, 15,
				temp_aa+sink_a/2+sink_dist-radCorner-overhangRight,	temp_bb+sink_b/2+sink_disty-overhangUpper, 	1015,
				temp_aa-sink_a/2+sink_dist+radCorner+overhangLeft,	temp_bb+sink_b/2+sink_disty-overhangUpper, 	15,
				temp_aa-sink_a/2+sink_dist+overhangLeft, 			temp_bb+sink_b/2+sink_disty-radCorner-overhangUpper, 1015,
				temp_aa-sink_a/2+sink_dist+overhangLeft, 			temp_bb-sink_b/2+sink_disty+radCorner+overhangLower, 15,
				temp_aa-sink_a/2+sink_dist+radCorner+overhangLeft,	temp_bb-sink_b/2+sink_disty+overhangLower, 	1015,
				temp_aa-sink_a/2+sink_dist+radCorner+overhangLeft,	temp_bb-sink_b/2+sink_disty+overhangLower, 	-1
		else			! same as detlevel simple

			put	temp_aa-sink_a/2+sink_dist+overhangLeft,	temp_bb-sink_b/2+sink_disty+overhangLower,15,
				temp_aa+sink_a/2+sink_dist-overhangRight,	temp_bb-sink_b/2+sink_disty+overhangLower,15,
				temp_aa+sink_a/2+sink_dist-overhangRight,	temp_bb+sink_b/2+sink_disty-overhangUpper,15,
				temp_aa-sink_a/2+sink_dist+overhangLeft,	temp_bb+sink_b/2+sink_disty-overhangUpper,15,
				temp_aa-sink_a/2+sink_dist+overhangLeft,	temp_bb-sink_b/2+sink_disty+overhangLower,-1
		endif
	endif
	if gs_count_type_m=1 or gs_detlevel_3D_m=1	then   	!rectangular edge or simple

		PRISM_ NSP/3,count_th,
				get(NSP)
		gosub 5000
	else
		cutpolya nsp/3, 1, 0,
				get(nsp)
		gosub 6000
		gosub 5000
		cutend
	endif
	DEL 1
	GOSUB 300
ENDIF
RETURN
!!!!!!!!!!!!!!!!!!!!!!!!! *** BACK SPLASH *** !!!!!!!!!!!!!!!!!!!!!!!!
300:

	MATERIAL bs_mat

	IF dob_face=0 THEN
		IF bs_h>0 THEN

		hotspot -lftmod,b+bckmod,ZZYZX+bs_h,unID, bs_h, 2 : unID=unID+1
		hotspot -lftmod,b+bckmod,ZZYZX,unID, bs_h, 1+128 : unID=unID+1
		hotspot -lftmod,b+bckmod,-1,unID, bs_h, 3 : unID=unID+1


		hotspot a+rgtmod,b+bckmod,ZZYZX+bs_h,unID, bs_h, 2 : unID=unID+1
		hotspot a+rgtmod,b+bckmod,ZZYZX,unID, bs_h, 1+128 : unID=unID+1
		hotspot a+rgtmod,b+bckmod,-1,unID, bs_h, 3 : unID=unID+1

		ADD 0,b-backSplashThk+bckmod, zzyzx
			PRISM_ 4,bs_h,
				-lftmod,0,13+2*(side_vis_m=2 OR side_vis_m=4),
				a+rgtmod,0, 8+7*(side_vis_m>2),
				a+rgtmod,backSplashThk,13+2*(side_vis_m>2),
				-lftmod,backSplashThk,8+7*(side_vis_m=2 OR side_vis_m=4)
		DEL 1

		ENDIF
	ENDIF

RETURN



1000:
roty 90
gosub 5000
del 1
return


5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

6000:

	rr=min (count_th*4/5, 0.02)
	maskk=8+(side_vis_m=2)*1+(side_vis_m=3)*4+(side_vis_m=4)*5

	rotz 90
	rotx 90
	IF dob_face = 0 Then
		prism_ 9, a,
			0, 0, 15,
			0, 0.0001, maskk,
			0, count_th-rr, 64+maskk,
			0, 10, 800,
			rr, count_th-rr, 900,
			rr, count_th, 3000+maskk,
			b, count_th, 15,
			b, 0, 15,
			0, 0, -1
	Else
		prism_ 14, a,
			0, 0, 15,
			0, 0.0001, maskk,
			0, count_th-rr, 64+maskk,
			0, 10, 800,

			rr, count_th-rr, 900,
			rr, count_th, 3000+maskk,
			b-rr, count_th, 64+maskk,
			b-rr+10, count_th, 800,
			b-rr, count_th-rr, 900,
			b, count_th-rr, 3000+maskk,
			b, count_th-rr, 64+maskk,

			b, 0.0001, maskk,
			b, 0, 15,
			0, 0, -1

	Endif
	del 2

return


