
EPS = 0.0001

_resol = max(3, gs_resol)

if gs_detlevel_3D_m = 0 then END

S = 90 / FilletResol
SS = 90 / ArcResol

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then
	! simple model

	BASE

	_mask			= 79
	_bFullyArched	= 0
	gosub "bufferPoints"
	CPRISM_ cush_mat,cush_mat,cush_mat,
			(NSP/3), cush_h,
			GET(NSP)

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4

	body -1
else
	! detailed model

	RESOL _resol

	ADDZ cush_h/2

	for i=1 to 2
		_mask			= 78
		_bFullyArched	= 1
		gosub "bufferPoints"
		HPRISM_ cush_mat,cush_mat,cush_mat,cush_mat,
				(NSP / 3), cush_h / 2, 0, r2, 1,
				GET(NSP)

		mulz -1
	next i
	del 2

	del 1

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	base
	body -1
endif

end

"bufferPoints":

	radius1	= r1
	radius2	= r3

	! Fillet Resolution
	S = 90 / FilletResol

	if GLOB_CONTEXT = 4 and _bFullyArched then put 0, radius1 + cush_b / 1000, 14
	if _bFullyArched then
		put	0, radius1, _mask
		for ii = 0 to FilletResol
			PUT radius1 + radius1 * sin(270 - ii * S), radius1 + radius1 * cos(270 - ii * S), _mask
		next ii
	endif

	put radius1 * _bFullyArched, 0, _mask - 64 * (GLOB_CONTEXT = 4 | NOT(_bFullyArched))
	if GLOB_CONTEXT = 4 and _bFullyArched then put cush_a - radius1 - cush_a / 1000, 0, 14
	if _bFullyArched then
		put cush_a - radius1, 0, _mask
		for ii = 0 to FilletResol
			PUT cush_a - radius1 + radius1 * sin(180 - ii * S), radius1 + radius1 * cos(180 - ii * S), _mask
		next ii
	endif

	put cush_a, radius1 * _bFullyArched, _mask - 64 * (GLOB_CONTEXT = 4 | NOT(_bFullyArched))
	if GLOB_CONTEXT = 4 and _bFullyArched then put cush_a, cush_b - radius2 - cush_b / 1000, 14
	put cush_a, cush_b - radius2, _mask
	for ii = 0 to FilletResol
		PUT cush_a - radius2 + radius2 * sin(90 - ii * S), cush_b - radius2 + radius2 * cos(90 - ii * S), _mask 
	next ii

	if _bFullyArched then put cush_a - radius2, cush_b, _mask - 64 * (GLOB_CONTEXT = 4)
	if GLOB_CONTEXT = 4 and _bFullyArched then put radius2 + cush_a / 1000, cush_b, 14
	put	radius2, cush_b, _mask
	for ii = 0 to FilletResol
		PUT radius2 + radius2 * sin(360 - ii * S), cush_b - radius2 + radius2 * cos(360 - ii * S), _mask
	next ii
	if _bFullyArched then put 0, cush_b - radius2, _mask - 64 * (GLOB_CONTEXT = 4)

return
