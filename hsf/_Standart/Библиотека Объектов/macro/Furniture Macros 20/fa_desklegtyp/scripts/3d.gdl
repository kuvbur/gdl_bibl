
if gs_leg_type_m = 0 then end

if gs_shadow = 0 then shadow off

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
	if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then
		iResol = 3
	else
		iResol = 8
	endif
else
	iResol = gs_resol
endif

resol iResol

pen gs_cont_pen
material gs_leg_mat

rotz 90

!!!!!!!!!!!!!!!!!!!!!! *** Type 1 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_leg_type_m = 1 then
	addz 0.01
	cylind leg_h, a
	del 1

	material legbase_mat
	cylind 0.01, a + 0.005
endif


!!!!!!!!!!!!!!!!!!!!!! *** Type 2 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_leg_type_m = 2 then
	addz 0.01
	addx -a / 2
	addy -b / 2
	brick a, b, leg_h
	del 3

	addx - (a + 0.01) / 2
	addy - (b + 0.01) / 2
	material legbase_mat
	block a + 0.01, b + 0.01, 0.01
	del 2
endif


!!!!!!!!!!!!!!!!!!!!!! *** Type 3 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_leg_type_m = 3 then
	add -a / 2, 0.07 / 2, 0

	rotx 90

	prism_ 9, 0.07,
		0, 0, 15,
		a, 0, 79,
		a - 0.04, 0, 900,
		0, 90, 4000,
		a - 0.04, 0.04, 79,
		0.04, 0.04, 79,
		0.04, 0, 900,
		0, 90, 4000,
		0, 0, 15
	del 1

	add a / 2 - 2 * 0.025, -0.07 / 2, 0.04
	cylind leg_h, 0.025
	addz leg_h
	cylind 0.05, 0.02
	del 1
	addx 4 * 0.025
	cylind leg_h, 0.025
	addz leg_h
	cylind 0.05, 0.02

	del 3

	del 1
endif


!!!!!!!!!!!!!!!!!!!!!! *** Type 4 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_leg_type_m = 4 then
	addx -a / 2
	addy -0.03 / 2
	brick a, 0.03, leg_h
	del 2
endif

!!!!!!!!!!!!!!!!!!!!!! *** Type 5 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_leg_type_m = 5 then
	legThk = 0.03
	infieldThk = 0.02
	discHeight = 0.015

	material legbase_mat

	addx -a / 3
	cylind discHeight, legThk / 2
	del 1

	addx a / 3
	cylind discHeight, legThk / 2
	del 1

	material gs_leg_mat
	addx -a / 2
	rotx 90
	addz -legThk / 2

	if bFullPanel then
		prism_ 10, legThk,
			0, discHeight, 15,
			a, discHeight, 15,
			a, leg_h, 15,
			0, leg_h, 15,
			0, discHeight, -1,

			legThk, discHeight + legThk, 15,
			a - legThk, discHeight + legThk, 15,
			a - legThk, leg_h - legThk, 15,
			legThk, leg_h - legThk, 15,
			legThk, discHeight + legThk, -1

		addz (legThk - infieldThk) / 2
		prism_ 5, infieldThk,
			legThk, discHeight + legThk, 15,
			a - legThk, discHeight + legThk, 15,
			a - legThk, leg_h - legThk, 15,
			legThk, leg_h - legThk, 15,
			legThk, discHeight + legThk, -1
		del 1
	else
		prism_ 10, legThk,
			0, discHeight, 15,
			a, discHeight, 15,
			0, 1, 815,
			a - legThk, discHeight + legThk, 1015 + 64,
			legThk, discHeight + legThk, 15,
			legThk, leg_h - legThk, 15,
			a - legThk, leg_h - legThk, 15 + 64,
			a, leg_h, 1015,
			0, leg_h, 15,
			0, 0, -1

		addz (legThk - infieldThk) / 2
		prism_ 5, infieldThk,
			legThk, discHeight + legThk, 15,
			a / 2, discHeight + legThk, 15,
			a / 2, leg_h - legThk, 15,
			legThk, leg_h - legThk, 15,
			legThk, discHeight + legThk, -1
		del 1
	endif

	del 3
endif


!!!!!!!!!!!!!!!!!!!!!! *** Type 6 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_leg_type_m = 6 then
	outRad = 0.03
	thk = 0.02
	legWidth = 0.05

	addx -a / 2
	rotx 90
	ADDZ -legWidth / 2

	prism_ 20, legWidth,
		outRad, 0, 15 + 64,
		a - outRad, 0, 15 + 64,
		a, outRad, 1015 + 64,
		a, leg_h - outRad, 15 + 64,
		a - outRad, leg_h, 1015 + 64,
		outRad, leg_h, 15 + 64,
		0, leg_h - outRad, 1015 + 64,
		0, outRad, 15 + 64,
		outRad, 0, 1015 + 64,
		outRad, 0, -1,

		outRad, thk, 15 + 64,
		a - outRad, thk, 15 + 64,
		a - thk, outRad, 1015 + 64,
		a - thk, leg_h - outRad, 15 + 64,
		a - outRad, leg_h - thk, 1015 + 64,
		outRad, leg_h - thk, 15 + 64,
		thk, leg_h - outRad, 1015 + 64,
		thk, outRad, 15 + 64,
		outRad, thk, 1015 + 64,
		outRad, thk, -1

	del 3
endif

del 1
