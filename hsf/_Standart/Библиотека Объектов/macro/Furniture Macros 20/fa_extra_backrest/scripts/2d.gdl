
pen gs_cont_pen
fill gs_fill_type

gosub "playField"


if abs (winner) < EPS then
	gosub "fieldHotspots"
endif


gosub "fieldLogic"


gosub "showTexts"


! ===============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ===============================================================================


! -------------------------------------------------------------------------------
"playField":
! -------------------------------------------------------------------------------

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, 0, 1,
		3, 0, 1,
		3, 3, 1,
		0, 3, 1

	line2 1, 0, 1, 3
	line2 2, 0, 2, 3
	line2 0, 1, 3, 1
	line2 0, 2, 3, 2

	unID = 1000000
	hotspot2 0, 0, unID : unID = unID + 1
	hotspot2 3, 0, unID : unID = unID + 1
	hotspot2 3, 3, unID : unID = unID + 1
	hotspot2 0, 3, unID : unID = unID + 1
return


! -------------------------------------------------------------------------------
"fieldHotspots":
! -------------------------------------------------------------------------------
	unID = 1000

	if abs (ownedFields[1][1]) < EPS then
		add2 0.5, 2.5
		hotspot2 0,						0, unID, editValuesForPlay11, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay11,	0, unID, editValuesForPlay11, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay11, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[1][2]) < EPS then
		add2 1.5, 2.5
		hotspot2 0,						0, unID, editValuesForPlay12, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay12,	0, unID, editValuesForPlay12, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay12, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[1][3]) < EPS then
		add2 2.5, 2.5
		hotspot2 0,						0, unID, editValuesForPlay13, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay13,	0, unID, editValuesForPlay13, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay13, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[2][1]) < EPS then
		add2 0.5, 1.5
		hotspot2 0,						0, unID, editValuesForPlay21, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay21,	0, unID, editValuesForPlay21, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay21, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[2][2]) < EPS then
		add2 1.5, 1.5
		hotspot2 0,						0, unID, editValuesForPlay22, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay22,	0, unID, editValuesForPlay22, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay22, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[2][3]) < EPS then
		add2 2.5, 1.5
		hotspot2 0,						0, unID, editValuesForPlay23, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay23,	0, unID, editValuesForPlay23, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay23, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[3][1]) < EPS then
		add2 0.5, 0.5
		hotspot2 0,						0, unID, editValuesForPlay31, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay31,	0, unID, editValuesForPlay31, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay31, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[3][2]) < EPS then
		add2 1.5, 0.5
		hotspot2 0,						0, unID, editValuesForPlay32, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay32,	0, unID, editValuesForPlay32, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay32, 3			: unID = unID + 1
		del 1
	endif
	if abs (ownedFields[3][3]) < EPS then
		add2 2.5, 0.5
		hotspot2 0,						0, unID, editValuesForPlay33, 1 + 128	: unID = unID + 1
		hotspot2 editValuesForPlay33,	0, unID, editValuesForPlay33, 2			: unID = unID + 1
		hotspot2 -1,					0, unID, editValuesForPlay33, 3			: unID = unID + 1
		del 1
	endif

return


! -------------------------------------------------------------------------------
"fieldLogic":
! -------------------------------------------------------------------------------
	add2 0.5, 2.5

	for i = 1 to vardim2(ownedFields)
		for j = 1 to vardim1(ownedFields)
!			text2 0, 0, ownedFields[i][j]
			if ownedFields[i][j] = 1 then gosub "drawCircle"
			if ownedFields[i][j] = 2 then gosub "drawX"
			add2 1, 0
		next j
		del vardim1(ownedFields)
		add2 0, -1
	next i
	del vardim2(ownedFields) + 1

return


! -------------------------------------------------------------------------------
"showTexts":
! -------------------------------------------------------------------------------
	define style "textstyle001" fontType, 250/GLOB_SCALE, 7, 2

	set style "textstyle001"

	text2 0, 3.8, `Tic-Tac-Toe Game`

	if abs(winner) < EPS & nTurns < 9 then
		if bFirstPlayersTurn then
			text2 0, 3.2, `Next turn: Player X`
		else
			text2 0, 3.2, `Next turn: Player O`
		endif
	endif

	if abs (winner) < EPS & nTurns = 9 then
		text2 0, 3.2, `Draw game.`
	endif
	if abs (winner - 1) < EPS then
		text2 0, 3.2, `The winner is Player O!`
	endif
	if abs (winner - 2) < EPS then
		text2 0, 3.2, `The winner is Player X!`
	endif

return


! -------------------------------------------------------------------------------
"drawCircle":
! -------------------------------------------------------------------------------
	circle2 0, 0, 0.25

return


! -------------------------------------------------------------------------------
"drawX":
! -------------------------------------------------------------------------------
	line2 -0.25, -0.25, 0.25,  0.25
	line2 -0.25,  0.25, 0.25, -0.25

return
