
IF gs_detlevel_3D_m=0 THEN END

If ABS(corncab_unID-1)<EPS Then
	unID=200
Else
	unID=corncab_unID
EndIf

PEN gs_cont_pen

if not(gs_shadow) then SHADOW OFF


MATERIAL toe_mat

alpha=ATN((b-rdepth_cab)/(a-ldepth_cab))

IF ABS(a-ldepth_cab)<EPS THEN a=ldepth_cab+0.001

IF not(dob_face) THEN
	ii=0
	msk=0
ELSE
	ii=toe_d
	msk=1
ENDIF

IF toe_h>0 THEN
	hotspot	ii+(fr_th1*dob_face), 0, 0, unID, toe_h, 1 : unID=unID+1
	hotspot	ii+(fr_th1*dob_face), 0, -1, unID, toe_h, 3 : unID=unID+1
	hotspot	ii+(fr_th1*dob_face), 0, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot	ldepth_cab-toe_d/SIN(alpha)-fr_th1, 0, 0, unID, toe_h, 1 : unID=unID+1
	hotspot	ldepth_cab-toe_d/SIN(alpha)-fr_th1, 0, -1, unID, toe_h, 3 : unID=unID+1
	hotspot	ldepth_cab-toe_d/SIN(alpha)-fr_th1, 0, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot	a, b-rdepth_cab+toe_d/SIN(90-alpha)+fr_th1, 0, unID, toe_h, 1 : unID=unID+1
	hotspot	a, b-rdepth_cab+toe_d/SIN(90-alpha)+fr_th1, -1, unID, toe_h, 3 : unID=unID+1
	hotspot	a, b-rdepth_cab+toe_d/SIN(90-alpha)+fr_th1, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot	a, b, 0, unID, toe_h, 1 : unID=unID+1
	hotspot	a, b, -1, unID, toe_h, 3 : unID=unID+1
	hotspot	a, b, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot	ii+(fr_th1*dob_face), b, 0, unID, toe_h, 1 : unID=unID+1
	hotspot	ii+(fr_th1*dob_face), b, -1, unID, toe_h, 3 : unID=unID+1
	hotspot	ii+(fr_th1*dob_face), b, toe_h, unID, toe_h, 2 : unID=unID+1

	If gs_toe_type_m = 1 Then
		PRISM_ 6, toe_h,
			ii+(fr_th1*dob_face), 0, 8+7*((side_vis_m=2) OR (side_vis_m=4) OR (left_cab>0)),
			ldepth_cab-toe_d/SIN(alpha)-fr_th1, 0, 15,
			a, b-rdepth_cab+toe_d/SIN(90-alpha)+fr_th1, 10+5*((side_vis_m=3) OR (side_vis_m=4) OR (right_cab>0)),
			a, b, 13+2*((side_vis_m=3) OR (side_vis_m=4) OR (right_cab>0)),
			ii+(fr_th1*dob_face), b, 15,
			ii+(fr_th1*dob_face), 0, -1
		gosub 1000
	Else
		r_leg=0.02

		add ii+r_leg+(fr_th1*dob_face), 0+r_leg, 0
		GOSUB 20

		add ldepth_cab-toe_d/SIN(alpha)-r_leg-fr_th1, 0+r_leg,0
		GOSUB 20

		add a-r_leg, b-rdepth_cab+toe_d/SIN(90-alpha)+r_leg+fr_th1,0
		GOSUB 20

		add a-r_leg, b-r_leg,0
		GOSUB 20

		add ii+r_leg+(fr_th1*dob_face), b-r_leg, 0
		GOSUB 20
	Endif
ENDIF
ADDZ toe_h

dd=sqr((a-ldepth_cab+0.05)^2+(b-rdepth_cab+0.05)^2)
alf=atn((b-rdepth_cab+0.05)/(a-ldepth_cab+0.05))
if count_th > 0 then
	if gs_sink_type_m <> 0 then
		ADD ldepth_cab+front_cab+(rx-lx)/2, ly+(ry-ly)/2, zzyzx-toe_h - 0.2
		ROTZ ATN((ry-ly)/(rx-lx))

		sink_z=0.2
	if gs_sink_type_m=1 then		!!! SINK 1
		sink_a=0.4
		sink_b=0.4
	else
		if gs_sink_type_m=2 then		!!! SINK 2
			sink_a=0.55
			sink_b=0.55
		else
			sink_a=0
			sink_b=0
		endif
	endif
	ADD -sink_a/2, front_cab+3", 0
	if sunder then addz -count_th

	call "fa_sinktyp" parameters all	a				= sink_a,
										b				= sink_b,
										zzyzx			= sink_z,
										bEnableTapEdit	= 1,
										gs_fill_type	= 0,
										gs_fill_pen		= 0,
										gs_back_pen		= 0

	if sunder then del 1
	DEL 1
	ADD 0, 3"+sink_b/2+front_cab, 0
		if gs_sink_type_m=1 then		!!! SINK 1 cut
			resol gs_resol
			CUTPOLYA 2,1,0,
				0,0,979,
				sink_a/2-0.02, 360, 4079
			DEL 2
		endif

		if sunder then
			radCorner = 0.05
			overhangLower = 0.045
			overhangUpper = 0.095
			overhangLeft = 0.045
			overhangRight = 0.045

		else
			overhangLower = 0.025
			overhangUpper = 0.04
			overhangLeft = 0.025
			overhangRight = 0.025
		endif
		if gs_detlevel_3D_m=2 and sunder then
			put	-sink_a/2+radCorner+overhangLeft, 	-sink_b/2+overhangLower, 			15,
				sink_a/2-radCorner-overhangRight,	-sink_b/2+overhangLower, 			15,
				sink_a/2-overhangRight, 			-sink_b/2+radCorner+overhangLower, 	1015,
				sink_a/2-overhangRight, 			sink_b/2-radCorner-overhangUpper, 	15,
				sink_a/2-radCorner-overhangRight,	sink_b/2-overhangUpper, 			1015,
				-sink_a/2+radCorner+overhangLeft,	sink_b/2-overhangUpper, 			15,
				-sink_a/2+overhangLeft, 			sink_b/2-radCorner-overhangUpper, 	1015,
				-sink_a/2+overhangLeft, 			-sink_b/2+radCorner+overhangLower, 	15,
				-sink_a/2+radCorner+overhangLeft,	-sink_b/2+overhangLower, 			1015,
				-sink_a/2+radCorner+overhangLeft,	-sink_b/2+overhangLower, 			-1
		else
			put	-sink_a/2+overhangLeft,	-sink_b/2+overhangLower,	15,
				sink_a/2-overhangRight,	-sink_b/2+overhangLower,	15,
				sink_a/2-overhangRight,	sink_b/2-overhangUpper,		15,
				-sink_a/2+overhangLeft,	sink_b/2-overhangUpper,		15,
				-sink_a/2+overhangLeft,	-sink_b/2+overhangLower,	-1
		endif

		if gs_sink_type_m=2 then		!!! SINK 2 cut
			CUTPOLYA nsp/3,1,0,
					use(nsp)

			DEL 2
		endif
		del 1
	endif
	ADDZ zzyzx-count_th-toe_h
	If gs_count_type_m = 1 or gs_detlevel_3D_m = 1 then
		CPRISM_ count_mat, count_mat, count_mat, 8, count_th,
				-lrear_cab, 			-left_cab, 			8+7*((side_vis_m=2) OR (side_vis_m>3)),
				ldepth_cab+front_cab, 	-left_cab, 			13+2*((side_vis_m=2) OR (side_vis_m>3)),
				lx, 					ly, 				15,
				rx, 					ry, 				15,
				a+right_cab, 			b-rdepth_cab-front_cab, 	8+7*(side_vis_m>2),
				a+right_cab, 			b+rrear_cab, 				13+2*(side_vis_m>2),
				-lrear_cab, 			b+rrear_cab, 				15,
				-lrear_cab, 			-left_cab, 					-1
	else					!If gs_count_type_m = 2 Then
		rr=min (count_th*4/5, 0.02)
		CPRISM_ count_mat, count_mat, count_mat, 7, count_th,
				-lrear_cab+rr, 				-left_cab, 	8+5*((side_vis_m=2) OR (side_vis_m>3)),
				ldepth_cab+front_cab-rr, 	-left_cab, 	8+2*((side_vis_m=2) OR (side_vis_m>3)),
				lx, 						ly, 		8,
				rx, 						ry, 		8,
				a+right_cab, 				b-rdepth_cab-(front_cab-rr), 	8+7*(side_vis_m>2),
				a+right_cab, 				b+rrear_cab, 					13+2*(side_vis_m>2),
				-lrear_cab+rr, 				b+rrear_cab, 					8
		Add -lrear_cab, -left_cab,count_th
		rotx -90
		If dob_face then
			CPRISM_ count_mat, count_mat, count_mat, 6, b+rrear_cab+left_cab,
					rr,	0,			8,
					rr,	count_th,	12+1*((side_vis_m=2) OR (side_vis_m>3)),
					0,	count_th,	14+1*((side_vis_m=2) OR (side_vis_m>3)),
					0,	rr,			14+1*((side_vis_m=2) OR (side_vis_m>3))+64,
					rr,	rr,			900,
					rr,	90,			4014+64
		Else
			CPRISM_ count_mat, count_mat, count_mat,4, b+rrear_cab+left_cab,
					rr,	0,			8,
					rr,	count_th,	12+1*((side_vis_m=2) OR (side_vis_m>3)),
					0,	count_th,	14+1*((side_vis_m=2) OR (side_vis_m>3)),
					0,	0,			14+1*((side_vis_m=2) OR (side_vis_m>3))+64
		Endif
		del 2
		if left_cab < EPS Then
			cutplane 1,0,1,1
		Endif
		if Right_cab < EPS Then
			add rx,ry,0
			rotz 90
			cutplane 1,0,1,1
			del 2
		Endif
		Addz count_th
    material count_mat
		tube 6,6, 3,
				0,							0,				1,
				0,							-count_th,		1,
				-rr,						-count_th,		1,
				-rr,						-rr,			1,
				0,							-rr,			900,
				rr,							-90,			4000,

				ldepth_cab+front_cab-rr, 	-left_cab*2,					0,	0,
				ldepth_cab+front_cab-rr, 	-left_cab,						0,	0,
				lx,							ly,								0,	0,
				rx,							ry,								0,	0,
				a+right_cab, 				b-rdepth_cab-(front_cab-rr),	0,	0,
				a+right_cab+2, 				b-rdepth_cab-(front_cab-rr),	0,	0

		if left_cab < EPS Then
			cutend
		Endif
		if Right_cab < EPS Then
			cutend
		Endif
		Del 1
	Endif
	del 1
endif
MATERIAL gs_cabinet_mat
if gs_detlevel_3D_m=1 or a<=0.04 OR b-0.05<=0.03 OR zzyzx-count_th-toe_h<=0.04 then
	EXTRUDE 6, 0, 0, zzyzx-count_th-toe_h, 55,        	!!! Simple cab
		(fr_th1*dob_face), 0, 0,
		ldepth_cab-fr_th1, 0, 0,
		a, b-rdepth_cab+fr_th1, 0,
		a, b, 0,
		(fr_th1*dob_face), b, 0,
		(fr_th1*dob_face), 0, -1
	IF gs_sink_type_m<>0 then CUTEND
else
	ADDZ zzyzx-count_th-toe_h-0.02
	PRISM_ 10,0.02,										! Top Panel
		   (fr_th1*dob_face), 0, 14,
		   ldepth_cab-fr_th1, 0, 14,
		   ldepth_cab+0.02/TAN(alpha)-fr_th1, 0.02, 13,
		   a-0.02, b-rdepth_cab-0.02/TAN(90-alpha)+fr_th1, 12,
		   a, b-rdepth_cab+fr_th1, 14,
		   a, b, 14,
		   (fr_th1*dob_face), b, 14,
		   (fr_th1*dob_face), b-0.02, 12+msk,
		   (fr_th1*dob_face), 0.02, 12,
		   (fr_th1*dob_face), 0, -1
	IF gs_sink_type_m<>0 then CUTEND
	del 1

	ADDZ 0.02
	IF not(dob_face) THEN
	PRISM_ 11, zzyzx-count_th-toe_h-2*0.02,
	   0, 0, 10,
	   ldepth_cab-fr_th1, 0, 10,
	   ldepth_cab+0.02/TAN(alpha)-fr_th1, 0.02, 15,
	   0.02, 0.02, 15,
	   0.02, b-0.02, 15,
	   a-0.02, b-0.02, 15,
	   a-0.02, b-rdepth_cab-0.02/TAN(90-alpha)+fr_th1, 10,
	   a, b-rdepth_cab+fr_th1, 10,
	   a, b, 10,
	   0, b, 10,
	   0, 0, -1
ELSE
	PRISM_ 5, zzyzx-count_th-toe_h-2*0.02,
	   (fr_th1*dob_face), 0, 10,
	   ldepth_cab-fr_th1, 0, 10,
	   ldepth_cab+0.02/TAN(alpha)-fr_th1, 0.02, 15,
	   (fr_th1*dob_face), 0.02, 10,
	   (fr_th1*dob_face), 0, -1

	PRISM_ 7, zzyzx-count_th-toe_h-2*0.02,
	   (fr_th1*dob_face), b-0.02, 15,
	   a-0.02, b-0.02, 15,
	   a-0.02, b-rdepth_cab-0.02/TAN(90-alpha)+fr_th1, 10,
	   a, b-rdepth_cab+fr_th1, 10,
	   a, b, 10,
	   (fr_th1*dob_face), b, 10,
	   (fr_th1*dob_face), b-0.02, -1
ENDIF
	DEL 1
	PRISM_ 10,0.02,										! Bottom Panel
		   (fr_th1*dob_face), 0, 11,
		   ldepth_cab-fr_th1, 0, 11,
   ldepth_cab+0.02/TAN(alpha)-fr_th1, 0.02, 13,
		   a-0.02, b-rdepth_cab-0.02/TAN(90-alpha)+fr_th1, 9,
		   a, b-rdepth_cab+fr_th1, 11,
		   a, b, 11,
		   (fr_th1*dob_face), b, 11,
		   (fr_th1*dob_face), b-0.02, 9+4*msk,
		   (fr_th1*dob_face), 0.02, 9,
   (fr_th1*dob_face), 0, -1
endif


!!! ****************************** BackSplash
if bs_h > 0 & gs_count_type_m # 0 then
	addz zzyzx-count_th-toe_h
	if not(dob_face) then
		addz count_th

		hotspot 0, -left_cab,	bs_h,	unID, bs_h, 2		: unID = unID + 1
		hotspot 0, -left_cab,	0,		unID, bs_h, 1+128	: unID = unID + 1
		hotspot 0, -left_cab,	-1,		unID, bs_h, 3		: unID = unID + 1

		hotspot a + right_cab, 	b,	bs_h,	unID, bs_h, 2		: unID = unID + 1
		hotspot a + right_cab, 	b,	0,		unID, bs_h, 1+128	: unID = unID + 1
		hotspot a + right_cab, 	b,	-1,		unID, bs_h, 3		: unID = unID + 1

		hotspot 0, b,	bs_h,	unID, bs_h, 2		: unID = unID + 1
		hotspot 0, b,	0,		unID, bs_h, 1+128	: unID = unID + 1
		hotspot 0, b,	-1,		unID, bs_h, 3		: unID = unID + 1

		cprism_  bs_mat, bs_mat, bs_mat, 7, bs_h,
			0 - lrear_cab, 				-left_cab, 				8 + 7 * ((side_vis_m = 2) or (side_vis_m > 3)),
			0.02 - lrear_cab, 			-left_cab, 				13 + 2 * ((side_vis_m = 2) or (side_vis_m > 3)),
			0.02 - lrear_cab, 			b - 0.02 + rrear_cab, 	15,
			a + right_cab, 				b - 0.02 + rrear_cab, 	8 + 7 * (side_vis_m > 2),
			a + right_cab, 				b + rrear_cab, 			13 + 2 * (side_vis_m > 2),
			0 - lrear_cab, 				b + rrear_cab, 			15,
			0 - lrear_cab, 				-left_cab, 	-1
	else
		add 0, b - 0.02, count_th

		hotspot -lrear_cab, 0.02,	bs_h,	unID, bs_h, 2		: unID = unID + 1
		hotspot -lrear_cab, 0.02,	0,		unID, bs_h, 1+128	: unID = unID + 1
		hotspot -lrear_cab, 0.02,	-1,		unID, bs_h, 3		: unID = unID + 1

		hotspot a + right_cab, 0.02,	bs_h,	unID, bs_h, 2		: unID = unID + 1
		hotspot a + right_cab, 0.02,	0,		unID, bs_h, 1+128	: unID = unID + 1
		hotspot a + right_cab, 0.02,	-1,		unID, bs_h, 3		: unID = unID + 1

		cprism_ bs_mat, bs_mat, bs_mat, 5, bs_h,
			-lrear_cab, 	0 + rrear_cab, 		15,
			a + right_cab, 	0 + rrear_cab, 		8 + 7 * (side_vis_m > 2),
			a + right_cab, 	0.02 + rrear_cab, 	13 + 2 * (side_vis_m > 2),
			-lrear_cab, 	0.02 + rrear_cab, 	15,
			-lrear_cab, 	0 + rrear_cab, 		-1
	endif
	del 1
endif
30:
del top


!! END OF MACRO
5555:

corncab_unID = unID
END corncab_unID

!! LEGS - START
20:

IF gs_toe_type_m=2 Then
	cylind toe_h, r_leg
Endif
IF gs_toe_type_m=3 Then
	prism_ 4,toe_h,
				-r_leg,	-r_leg,	15,
				r_leg,	-r_leg,	15,
				r_leg,	r_leg,	15,
				-r_leg,	r_leg,	15
Endif
gosub 1000
del 1

RETURN
!! LEGS - END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return
