

!front_cab=(front_cab+fr_th1)


If ABS(basecab_unID-1)<EPS Then
	unID=200
Else
	unID=basecab_unID
EndIf


if not(gs_shadow) then SHADOW OFF

if gs_toe_type_m=1 then
	r_leg = 0
else
	r_leg = 0.02
endif
IF cab_size=1 THEN			!cabinet
	sizeB = B- front_cab
	TempB= b-depth_cab+r_leg+fr_th1+toe_d
	sizeA = A- right_cab
ELSE						!counter
	sizeB = B
	TempB= b-depth_cab+r_leg+front_cab+fr_th1-YY2+toe_d
	sizeA = A
ENDIF


PEN gs_cont_pen
!!!!!!!!!!!!!!!!!!!!!!!!!!! *** TOE *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

IF toe_h>0 THEN
	If dob_face=1 then		!! DOBLE FACED
		dob_y=toe_d
	else
		dob_y=0
	endif

	if right_cab>0 then		!! MASKING of TOE
		toe_mask_1=15
		toe_mask_2=15
	else
		toe_mask_1=8+7*(side_vis_m>2)
		toe_mask_2=13+2*(side_vis_m>2)
	endif

	if left_cab>0 then
		toe_mask_3=15
		toe_mask_4=15
	else
		toe_mask_3=13+2*(side_vis_m=2 OR side_vis_m=4)
		toe_mask_4=8+7*(side_vis_m=2 OR side_vis_m=4)
	endif

	IF ToeHtps3D=1 THEN
		hotspot	left_cab, front_cab+fr_th1+toe_d, 0, unID, toe_h, 1+128 : unID=unID+1
		hotspot	left_cab, front_cab+fr_th1+toe_d, -1, unID, toe_h, 3+128 : unID=unID+1
		hotspot	left_cab, front_cab+fr_th1+toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

		hotspot	a-right_cab, TempB, 0, unID, toe_h, 1+128 : unID=unID+1
		hotspot	a-right_cab, TempB, -1, unID, toe_h, 3 : unID=unID+1
		hotspot	a-right_cab, TempB, toe_h, unID, toe_h, 2 : unID=unID+1

		hotspot	left_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, 0, unID, toe_h, 1+128 : unID=unID+1
		hotspot	left_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, -1, unID, toe_h, 3 : unID=unID+1
		hotspot	left_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, toe_h, unID, toe_h, 2 : unID=unID+1

		hotspot	a-right_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, 0, unID, toe_h, 1+128 : unID=unID+1
		hotspot	a-right_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, -1, unID, toe_h, 3 : unID=unID+1
		hotspot	a-right_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, toe_h, unID, toe_h, 2 : unID=unID+1

		hotspot left_cab, front_cab+fr_th1+toe_d, 0, unID : unID=unID+1
		hotspot a-right_cab, TempB, 0, unID : unID=unID+1
		hotspot left_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, 0, unID : unID=unID+1
		hotspot a-right_cab, b-rear_cab-DOB_Y-fr_th1*dob_face, 0, unID : unID=unID+1
	ENDIF

	MATERIAL toe_mat

	if gs_toe_type_m=1 then
		ADDY fr_th1+front_cab+toe_d
		ROTz alfa
		CUTPLANE 90
		del 2
		PRISM_ 4,toe_h,			!! TOE
				left_cab,front_cab+toe_d,toe_mask_3,
				a-right_cab,front_cab+toe_d, toe_mask_1,
				a-right_cab,b-rear_cab-DOB_Y-fr_th1*dob_face,toe_mask_2,
				left_cab,b-rear_cab-DOB_Y-fr_th1*dob_face,toe_mask_4
		gosub 1000
		CUTEND
	else
		for ii=1 to 2
			ADD	left_cab+r_leg, b-r_leg,0
			gosub "oneLeg"
			DEL 1
			addx a-(left_cab+r_leg)-(right_cab+r_leg)
		next ii
		del 2

		ADD	A-right_cab-r_leg, TempB,0
		gosub "oneLeg"
		del 1

		legOffset = ((sizeB-depth_cab)*2*r_leg)/sizeA + fr_th1 + toe_d

		ADD	left_cab+r_leg, front_cab+r_leg+legOffset,0
		gosub "oneLeg"
		del 1
	endif
ELSE
	IF ToeHtps3D=1 THEN
		HOTSPOT left_cab,front_cab-fr_th1,0, unID	: unID=unID+1
		HOTSPOT A-right_cab,TempB-toe_d,0, unID	: unID=unID+1
		HOTSPOT A-right_cab,B-rrear_cab-(front_cab-fr_th1)*dob_face,0, unID	: unID=unID+1
		HOTSPOT left_cab,B-rrear_cab-(front_cab-fr_th1)*dob_face,0, unID	: unID=unID+1
	ENDIF
ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!! *** CABINET *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!
ADDY fr_th1+front_cab
ROTz alfa
	CUTPLANE 90
del 2

MATERIAL gs_cabinet_mat

	IF sink=0 THEN
		cab_z=0.04
		ADDZ zzyzx-count_th-0.02
			PRISM_ 9,0.02,				!! Top Panel if there is no SINK
			left_cab,front_cab,14,
			left_cab,b-rear_cab-(fr_th1*dob_face),14,
			left_cab+0.02,b-rear_cab-(fr_th1*dob_face),12,
			a-right_cab-0.02,b-rear_cab-(fr_th1*dob_face),12,
			a-right_cab,b-rear_cab-(fr_th1*dob_face),14,
			a-right_cab,front_cab,14,
			a-right_cab-0.02,front_cab,13,
			left_cab+0.02,front_cab,12,
			left_cab,front_cab,-1

		gosub 1000
		DEL 1
	else
		cab_z=0.02
	ENDIF

	if dob_face=1 then
		mask_temp=13
	else
		mask_temp=9
	endif

	ADDZ toe_h
		PRISM_ 9,0.02,
			left_cab,front_cab,11,				!! Base Panel
			left_cab,b-rear_cab-(fr_th1*dob_face),11,
			left_cab+0.02,b-rear_cab-(fr_th1*dob_face),mask_temp,
			a-right_cab-0.02,b-rear_cab-(fr_th1*dob_face),9,
			a-right_cab,b-rear_cab-(fr_th1*dob_face),11,
			a-right_cab,front_cab,11,
			a-right_cab-0.02,front_cab,13,
			left_cab+0.02,front_cab,9,
			left_cab,front_cab,-1

	gosub 1000
	addz 0.02
		IF dob_face=0 then
			PRISM_ 9,ZZYZX-count_th-toe_h-CAB_Z,	!! Side Panels and Back
				left_cab,front_cab,10,
				left_cab+0.02,front_cab,15,
				left_cab+0.02,b-rear_cab-0.02,15,
				a-right_cab-0.02,b-rear_cab-0.02,15,
				a-right_cab-0.02,front_cab,10,
				a-right_cab,front_cab,10,
				a-right_cab,b-rear_cab,10,
				left_cab,b-rear_cab,10,
				left_cab,front_cab,-1
			gosub 1000
		ELSE
			PRISM_ 4,ZZYZX-count_th-toe_h-CAB_Z,	!! Left Side Panels
				left_cab,front_cab,10,
				left_cab+0.02,front_cab,15,
				left_cab+0.02,b-rear_cab-(fr_th1*dob_face),10,
				left_cab,b-rear_cab-(fr_th1*dob_face),10

			gosub 1000
			PRISM_ 4,ZZYZX-count_th-toe_h-CAB_Z,	!! Right Side Panels
				a-right_cab,front_cab,10,
				a-right_cab,b-rear_cab-(fr_th1*dob_face),10,
				a-right_cab-0.02,b-rear_cab-(fr_th1*dob_face),15,
				a-right_cab-0.02,front_cab,10
			gosub 1000
		ENDIF
	DEL 2


!!!!!!!!!!!!!!!!!!!!!!!!! *** SHELVES *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!
MATERIAL gs_cabinet_mat

shelf_z=(zzyzx-(count_th+(sink+shelf_num+1)*0.02+toe_h+drawer_h))/(shelf_num+1)

IF shelf_num > 0 then
	FOR ii=1 to shelf_num
		ADDZ toe_h+shelf_z
		MATERIAL gs_ShelfMat
		PRISM_ 4,0.02,
			left_cab+0.02,front_cab,15,				!! Shelf
			left_cab+0.02,b-0.02-rear_cab-((fr_th1-0.02)*dob_face),15,
			a-right_cab-0.02,b-0.02-rear_cab-((fr_th1-0.02)*dob_face),15,
			a-right_cab-0.02,front_cab,15
		gosub 1000
		DEL 1
	addz shelf_z+0.02
	NEXT ii
	DEL shelf_num
ENDIF

CUTEND

basecab_unID=unID

END basecab_unID

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

"oneLeg":
	IF gs_toe_type_m=2 Then
		cylind toe_h, r_leg
		gosub 1000
	Endif
	IF gs_toe_type_m=3 Then
		prism_ 4,toe_h,
			-r_leg,-r_leg,15,
			r_leg,-r_leg,15,
			r_leg,r_leg,15,
			-r_leg,r_leg,15
		gosub 1000
	Endif
return

