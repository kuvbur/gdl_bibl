
! Night Table 01 - 02 macro

IF gs_detlevel_3D_m=0 THEN END
IF not(gs_shadow) THEN SHADOW OFF

PEN gs_cont_pen

if not (bCalledAsMacro) then
	! 3D Hotspot
	unID=1

	! Total Height

	hotspot	0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot	0, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
	hotspot	0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot	A, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot	A, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
	hotspot	A, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot	0, B, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot	0, B, -1, unID, zzyzx, 3+128 : unID=unID+1
	hotspot	0, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot	A, B, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot	A, B, -1, unID, zzyzx, 3+128 : unID=unID+1
	hotspot	A, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

	! Leg Height

	hotspot	tboh, tboh+fr_th1, 0, unID, leg_h, 1 : unID=unID+1
	hotspot	tboh, tboh+fr_th1, -1, unID, leg_h, 3+128 : unID=unID+1
	hotspot	tboh, tboh+fr_th1, leg_h, unID, leg_h, 2 : unID=unID+1

	hotspot	A-tboh, tboh+fr_th1, 0, unID, leg_h, 1 : unID=unID+1
	hotspot	A-tboh, tboh+fr_th1, -1, unID, leg_h, 3+128 : unID=unID+1
	hotspot	A-tboh, tboh+fr_th1, leg_h, unID, leg_h, 2 : unID=unID+1

	hotspot	A-tboh, B, 0, unID, leg_h, 1 : unID=unID+1
	hotspot	A-tboh, B, -1, unID, leg_h, 3+128 : unID=unID+1
	hotspot	A-tboh, B, leg_h, unID, leg_h, 2 : unID=unID+1

	hotspot	tboh, B, 0, unID, leg_h, 1 : unID=unID+1
	hotspot	tboh, B, -1, unID, leg_h, 3+128 : unID=unID+1
	hotspot	tboh, B, leg_h, unID, leg_h, 2 : unID=unID+1
endif

ADD a/2, b,0

lffw=a-2*tboh-2*0.01                !!! Leafs full width
lffh=zzyzx-tbt-dwh * (iNightTableType = 2)-leg_h-2*0.01      !!! Leafs full height


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Detailed 3D !!!
	CALL "fa_cabinet_credenza" PARAMETERS a=a,b=b-fr_th1,zzyzx=zzyzx,cot=tbt,cooh=tboh, side_overhang=tboh,leg_h=leg_h,
	gs_cont_pen=gs_cont_pen,gs_cabinet_mat=gs_cabinet_mat


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Cornices !!!
if bCornice then
ADDZ zzyzx-tbt

CALL "fa_cornicetyp" PARAMETERS a=a,b=b,gs_detlevel_3D_m=gs_detlevel_3D_m,
	gs_cornice_type_m=gs_cornice_type_m,cot=tbt,cooh=tboh,side_overhang=tboh,gs_lining_m=1,gs_resol=gs_resol,
	gs_cornice_mat=gs_cornice_mat,gs_cont_pen=gs_cont_pen

DEL 1
endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Legs !!!

CALL "fa_cabinet_legtyp" PARAMETERS a=a,b=b-fr_th1,gs_detlevel_3D_m=gs_detlevel_3D_m,
	gs_leg_type_m=gs_leg_type_m,leg_h=leg_h,leg_w=leg_w,cooh=tboh, side_overhang=tboh,gs_resol=gs_resol,
	gs_leg_mat=gs_leg_mat,gs_cont_pen=gs_cont_pen,gs_lining_m=gs_lining_m


! --- iNightTableType = 1 - drawers --------------------------------------------

if iNightTableType = 1 then
	add -a/2+tboh+0.01,-b+fr_th1+tboh,leg_h+0.01

	if not(bDrawerNoneType) then
		if bdrawer then
			for t=1 to dnum
				call "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,door_ang=0,
				door_w=lffw,door_h=lffh/dnum,gs_door_type_m=gs_door_type_m,doorframe_w=doorframe_w,vert_gnum=vert_gnum,
				hor_gnum=hor_gnum,gs_cont_pen=gs_cont_pen,gs_shadow=gs_shadow,fr_th1=fr_th1,
				doorglas_mat=doorglas_mat,doorframe_mat=doorframe_mat,bKnob = 0, bKnobNoneType = 0, gs_bevel=gs_bevel


			!!! Knobs on vertical and horizontal drawers !!!
				if bKnob then
					add lffw/2,-0.02,lffh/dnum/2
					drawerHeight = lffh / dnum
					gosub "knobs"
				endif
				addz lffh/dnum
			next t
			del dnum + 1
		endif
	else
		for t=1 to dnum
			call "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,door_ang=0,
			door_w=lffw,door_h=lffh/dnum,gs_door_type_m=gs_door_type_m,doorframe_w=doorframe_w,vert_gnum=vert_gnum,
			hor_gnum=hor_gnum,gs_cont_pen=gs_cont_pen,gs_shadow=gs_shadow,fr_th1=fr_th1,
			doorglas_mat=doorglas_mat,doorframe_mat=doorframe_mat,bKnob = 0, bKnobNoneType = 0, gs_bevel=gs_bevel


		!!! Knobs on vertical and horizontal drawers !!!
			if bKnob then
				add lffw/2,-0.02,lffh/dnum/2
				drawerHeight = lffh / dnum
				gosub "knobs"
			endif
			addz lffh/dnum
		next t
		del dnum + 1
	endif

	del 1
endif


! --- iNightTableType = 2 ------------------------------------------------------

if iNightTableType = 2 then
	IF iNightTableDrawerPos = DRAWERPOS_DOWN OR iNightTableDrawerPos = DRAWERPOS_NONE THEN	! Down or None
		ADD 0,-0.02,zzyzx-tbt-dwh-0.02
		MATERIAL gs_cabinet_mat

		PRISM_ 4,0.02,
				a/2-tboh-0.02,0,15,
				a/2-tboh-0.02,-b+fr_th1+tboh+0.02,15,
				-a/2+tboh+0.02,-b+fr_th1+tboh+0.02,15,
				-a/2+tboh+0.02,0,15
		DEL 1
	ENDIF

	IF iNightTableDrawerPos = DRAWERPOS_UP THEN		! Up
		ADD 0,-0.02,zzyzx-tbt-dwh-0.02
		MATERIAL gs_cabinet_mat

		PRISM_ 4,0.02,
				a/2-tboh-0.02,0,15,
				a/2-tboh-0.02,-b+fr_th1+tboh+0.02,15,
				-a/2+tboh+0.02,-b+fr_th1+tboh+0.02,15,
				-a/2+tboh+0.02,0,15
		DEL 1
	ENDIF

	if iNightTableDrawerPos = DRAWERPOS_DOWN then				! Bottom drawer
		if bDrawerNoneType or (not(bDrawerNoneType) and bDrawer) then
			add -a/2+tboh+0.01,-b+fr_th1+tboh,leg_h+0.01
	
			call "fa_cabdoortyp" parameters gs_detlevel_3D_m	= gs_detlevel_3D_m,
											gs_full_edit		= 0,
											door_ang			= 0,
											door_w				= lffw,
											door_h				= lffh,
											gs_door_type_m		= gs_door_type_m,
											doorframe_w			= doorframe_w,
											vert_gnum			= vert_gnum,
											hor_gnum			= hor_gnum,
											gs_cont_pen			= gs_cont_pen,
											gs_shadow			= gs_shadow,
											fr_th1				= fr_th1,
											doorglas_mat		= doorglas_mat,
											doorframe_mat		= doorframe_mat,
											bKnob 				= 0, 
											bKnobNoneType 		= 0, 
											gs_bevel			= gs_bevel	
			!!! Knobs on top drawer !!!
			if bKnob then
				add lffw/2,-0.02,lffh/2
				drawerHeight = lffh
				gosub "knobs"
			endif
			del 1
		endif
	endif

	if iNightTableDrawerPos = DRAWERPOS_UP then		! Top drawer
		if bDrawerNoneType or (not(bDrawerNoneType) and bDrawer) then
			if ABS(dwh)>EPS then	!!dwh<>0
				add -a/2+tboh+0.01,-b+fr_th1+tboh,leg_h+0.01+lffh
	
				call "fa_cabdoortyp" parameters 	gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_full_edit		= 0,
													door_ang			= 0,
													door_w				= lffw,
													door_h				= dwh,
													gs_door_type_m		= gs_door_type_m,
													doorframe_w			= doorframe_w,
													vert_gnum			= vert_gnum,
													hor_gnum			= hor_gnum,
													gs_cont_pen			= gs_cont_pen,
													gs_shadow			= gs_shadow,
													fr_th1				= fr_th1,
													doorglas_mat		= doorglas_mat,
													doorframe_mat		= doorframe_mat,
													bKnob 				= 0,
													bKnobNoneType 		= 0,
													gs_bevel			= gs_bevel
	
				!!! Knobs on top drawer !!!
				if bKnob then
					add lffw/2,-0.02,dwh/2
					drawerHeight = dwh
					gosub "knobs"
				endif
				del 1
			endif
		endif
	endif
	del 1
endif
END

"knobs":
	IF gs_knob_type_m=4 or gs_knob_type_m=5 or gs_knob_type_m=6 or gs_knob_type_m=7 or gs_knob_type_m=8 then
		ROTY 90
		IF gs_door_type_m=3 THEN ADDx -drawerHeight/2+doorframe_w/2
	ENDIF

	IF gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 then
		IF gs_door_type_m=3 THEN ADDz drawerHeight/2-doorframe_w/2
	ENDIF

	if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
		addy -fr_th1+0.02
		CALL "fa_knobtyp" PARAMETERS all gs_knob_type_m = gs_knob_type_m
		del 1
	else
		if gs_knob_type_m=4 then
			ADDz knob_size/2 
			addy -fr_th1+0.02
			CALL "fa_knobtyp" PARAMETERS all gs_knob_type_m = gs_knob_type_m
			del 2
		else
			if gs_knob_type_m=8 then
				roty 180
				ADDz knob_size/2
				addy -fr_th1+0.02
				CALL "fa_knobtyp" PARAMETERS all
				del 3
			else
				ADDz knob_size/2
				addy -fr_th1+0.02
				CALL "fa_knobtyp" PARAMETERS all gs_knob_type_m = gs_knob_type_m
				del 2
			endif
		endif
	endif
	IF gs_knob_type_m=4 or gs_knob_type_m=5 or gs_knob_type_m=6 or gs_knob_type_m=7 or gs_knob_type_m=8 then DEL 1
	IF gs_door_type_m=3 THEN del 1
	DEL 1
return
