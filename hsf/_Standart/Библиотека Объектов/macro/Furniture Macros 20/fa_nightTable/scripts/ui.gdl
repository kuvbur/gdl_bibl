
ui_dialog stNightTableUI[1], 444, 266		! Night Table Settings

ui_current_page gs_ui_current_page

!iNightTableType
MULTITYPE	= 1
OPENTYPE	= 2

dim tabTitles[7]
	tabTitles[1] = stTabTitles[1]
	tabTitles[2] = stTabTitles[2]
	tabTitles[3] = stTabTitles[3]
	tabTitles[4] = stTabTitles[4]
	tabTitles[5] = stTabTitles[5]
	tabTitles[6] = stTabTitles[6]
	tabTitles[7] = stTabTitles[7]

dim tabIcons[7]
	tabIcons[1] = "uiTab_Style_flat(1)"
	tabIcons[2] = "uiTab_Drawer_flat(1)"
	tabIcons[3] = "uiTab_Knob_flat(1)"
	tabIcons[4] = "uiTab_Leg_flat(1)"
	tabIcons[5] = "uiTab_2DRepr_flat(1)"
	tabIcons[6] = "uiTab_MinSpace_flat(1)"
	tabIcons[7] = "uiTab_Descriptions_flat(1)"

! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 			=-1
_idxTab = 1
TABID_Style						= _idxTab  : _idxTab = _idxTab + 1
TABID_Leg						= _idxTab  : _idxTab = _idxTab + 1
TABID_Drawer					= _idxTab  : _idxTab = _idxTab + 1
TABID_Knob						= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

pageStartX = 0
pageStartY = 5

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog stNightTableUI[1], 444, 266		! Night Table Settings
ui_current_page gs_ui_current_page

UI_PAGE TABID_Style, TABID_Root, stTabTitles[1], tabIcons[1]
	gosub "stylePage"

UI_PAGE TABID_Drawer, TABID_Style, stTabTitles[2], tabIcons[2]
	gosub "drawerPage"

UI_PAGE TABID_Knob, TABID_Style, stTabTitles[3], tabIcons[3]
	gosub "knobPage"

UI_PAGE TABID_Leg, TABID_Style, stTabTitles[4], tabIcons[4]
	gosub "legPage"

UI_PAGE TABID_RepresentationAndSurfaces, TABID_Root, stTabTitles[5], tabIcons[5]
	gosub "representationsPage"

UI_PAGE TABID_MinimalSpace, TABID_Root, stTabTitles[6], tabIcons[6]
	gosub "minimalSpacePage"

UI_PAGE TABID_Descriptions, TABID_Root, stTabTitles[7], tabIcons[7]
	gosub "descriptionPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
FILE_DEPENDENCE "uiTab_NightTableStyle_flat(1)"
FILE_DEPENDENCE "uiTab_Drawer_flat(1)"
FILE_DEPENDENCE "uiTab_Knob_flat(1)"
FILE_DEPENDENCE "uiTab_Leg_flat(1)"
FILE_DEPENDENCE "uiTab_2DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_MinSpace_flat(1)"
FILE_DEPENDENCE "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"stylePage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	xPict = 297
	yPict = 45
	PictWidth = 23 
	PictHeight = 114
	_infHeight 	= 19
	_outfHeight = 15
	_offsetY = 4
	widthInf2	= 35

	x1 = 222
	x2 = 230
	x3 = 408
	dy1 = 24
	dy2 = 27

	ui_separator x1, pageStartY, x1, 130

	ui_style 0, 1
	_yCurr = pageStartY + 6
	ui_outfield `Тип`, 0, _yCurr, x1 - 2, _outfHeight
	ui_outfield `Стиль`, x2, _yCurr, 444 - x2 - 2, _outfHeight

	_yCurr = _yCurr + 17
	ui_infield{3} "iNightTableType", 0, _yCurr, 211,100,
		4, 1, 
		2, 1,
		100, 95, 90,90,
		1,  stNightTableTypes[1], 1,
		2,  stNightTableTypes[2], 2

	_yCurr = pageStartY + 6
	ui_outfield `Стиль`, x2, _yCurr, 444 - x2 - 2, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + dy1
	ui_infield{3} "bCornice", x2, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Карниз`, 0,
					"", `Карниз`, 1

	_yCurr = _yCurr + dy1
	ui_infield{3} "bBase", x2, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Основание`, 0,
					"", `Основание`, 1

	ui_separator x1 + 3, _yCurr + 22, 444, _yCurr + 22

	ui_pict_button ui_function, "", "uiFunctionLeg_flat(1)", x3, _yCurr, widthInf2, 19, -TABID_Leg ! Set GLOB_UI_BUTTON_ID

	_yCurr = _yCurr + dy2
	ui_infield{3} "bDrawer", x2, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Ящик`, 0,
					"", `Ящик`, 1

	ui_separator x1 + 3, _yCurr + 22, 444, _yCurr + 22

	ui_pict_button ui_function, "", "uiFunctionDrawer_flat(1)", x3, _yCurr, widthInf2, 19, -TABID_Drawer ! Set GLOB_UI_BUTTON_ID

	_yCurr = _yCurr + dy2
	ui_infield{3} "bKnob", x2, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Ручка`, 0,
					"", `Ручка`, 1

	ui_pict_button ui_function, "", "uiFunctionDrawerKnob_flat(1)", x3, _yCurr, widthInf2, 19, -TABID_Knob ! Set GLOB_UI_BUTTON_ID
return

! ------------------------------------------------------------------------------
"drawerPage":
! ------------------------------------------------------------------------------
	call "ui_furniture_parts" parameters all	bIsDrawerStyle	= 1,
												iNightTableType	= iNightTableType
return

! ------------------------------------------------------------------------------
"knobPage":
! ------------------------------------------------------------------------------

	call "ui_cabinetKnob" parameters all	iKnobClass = 3,	! KNOBCLASS_DRAWER
											iItemClass = 7	! ITEMCLASS_NIGHT_TABLE

return

! ------------------------------------------------------------------------------
"legPage":
! ------------------------------------------------------------------------------

 	call "ui_furniture_parts" parameters	all bIsCornice	= 1

	pageStartY	= 5
	pageEndY	= 266
	ui_separator 222, pageStartY - 5, 222, pageEndY

 	call "ui_furniture_parts" parameters	all bIsBase	= 1,
												pageStartX = 232

return

! ------------------------------------------------------------------------------
"representationsPage":
! ------------------------------------------------------------------------------

	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"

		if gs_detlevel_3D_m = 2 then	! Detailed
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
		endif

		if gs_detlevel_3D_m <> 0 then	! Not Off
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "bUniformSurfaces"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cabinet_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if bDrawer then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "doorframe_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

			if gs_door_type_m = 3 then
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "doorglas_mat"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
			endif
		endif

		if bCornice then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_cornice_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bBase then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_leg_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bKnob and bDrawer then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_knob_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray
return

! ------------------------------------------------------------------------------
"minimalSpacePage":
! ------------------------------------------------------------------------------

	call "fa_minimalspace" parameters MSType = 1 ! MINIMALSPACE_CLASS_NIGTH_TABLE

return

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return

