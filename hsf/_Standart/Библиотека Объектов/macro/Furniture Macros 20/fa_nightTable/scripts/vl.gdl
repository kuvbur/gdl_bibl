
if not (bCalledAsMacro) then
	call "FM_types" parameters all
	values{2} "gs_detlevel_3D_m" 	0, stDetLevel3d[3],
									1, stDetLevel3d[2],
									2, stDetLevel3d[1]
	values "gs_resol" range [3,)
	values "gs_lining_m" 1

	if gs_cont_pen = 0 then parameters gs_cont_pen = 4
	if gs_fill_pen = 0 then parameters gs_fill_pen = 19

	if gs_detlevel_3D_m = 1 then
		lock "gs_resol"
		hideparameter "gs_resol"
	endif

	if gs_detlevel_3D_m = 0 then
		lock "gs_resol", "gs_shadow"
		hideparameter "gs_resol", "gs_shadow"
	endif

	values "gs_resol" range [3,)

	! -----------------------------------------------------------------------------
	!  Uniform surfaces
	! -----------------------------------------------------------------------------

	if GLOB_MODPAR_NAME = "bUniformSurfaces" then
		if bUniformSurfaces then
			gs_cabinet_mat 	= doorframe_mat
			gs_cornice_mat	= doorframe_mat
			gs_leg_mat	 	= doorframe_mat
	
			parameters 	gs_cabinet_mat 	= gs_cabinet_mat,
						gs_cornice_mat	= gs_cornice_mat,
						gs_leg_mat		= gs_leg_mat
		endif
	else
		if bUniformSurfaces then
			if GLOB_MODPAR_NAME = "doorframe_mat" then
				gs_cabinet_mat 	= doorframe_mat
				gs_cornice_mat	= doorframe_mat
				gs_leg_mat	 	= doorframe_mat
		
				parameters 	gs_cabinet_mat 	= gs_cabinet_mat,
							gs_cornice_mat	= gs_cornice_mat,
							gs_leg_mat		= gs_leg_mat
	
			endif	
			if GLOB_MODPAR_NAME = "gs_cabinet_mat" then
				doorframe_mat 	= gs_cabinet_mat
				gs_cornice_mat	= gs_cabinet_mat
				gs_leg_mat	 	= gs_cabinet_mat
		
				parameters 	doorframe_mat 	= doorframe_mat,
							gs_cornice_mat	= gs_cornice_mat,
							gs_leg_mat		= gs_leg_mat
	
			endif
			if GLOB_MODPAR_NAME = "gs_cornice_mat" then
				doorframe_mat 	= gs_cornice_mat
				gs_cabinet_mat	= gs_cornice_mat
				gs_leg_mat	 	= gs_cornice_mat
		
				parameters 	doorframe_mat 	= doorframe_mat,
							gs_cabinet_mat	= gs_cabinet_mat,
							gs_leg_mat		= gs_leg_mat
	
			endif
			if GLOB_MODPAR_NAME = "gs_leg_mat" then
				doorframe_mat 	= gs_leg_mat
				gs_cabinet_mat	= gs_leg_mat
				gs_cornice_mat 	= gs_leg_mat
		
				parameters 	doorframe_mat 	= doorframe_mat,
							gs_cabinet_mat	= gs_cabinet_mat,
							gs_cornice_mat	= gs_cornice_mat
	
			endif
		endif
	endif
endif

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------
values "stNightTableType" stNightTableTypes
values "iNightTableType"  1, 2

if GLOB_MODPAR_NAME = "stNightTableType" then
	iNightTableType = 1
	if stNightTableType = stNightTableTypes[2] then iNightTableType = 2
	parameters iNightTableType = iNightTableType
else
	stNightTableType = stNightTableTypes[1]
	if iNightTableType = 2 then stNightTableType = stNightTableTypes[2]
	parameters stNightTableType = stNightTableType
endif


if iNightTableType = 1 then
	lock	"iNightTableDrawerPos",	"dwh"
	hideparameter	"iNightTableDrawerPos",	"dwh"
else
	lock "dnum"
	hideparameter "dnum"
endif

if bDrawerNoneType then
	if dnum = 0 or iNightTableDrawerPos = DRAWERPOS_NONE then
		lock 	"dwh", "fr_th1", "gs_bevel", "doorframe_w", "vert_gnum", "hor_gnum", "gs_door_type", "gs_door_type_m",
				"doorframe_mat", "doorglas_mat", "bKnob", "gs_knob_type_m", "knob_size", "gs_knob_mat"
		hideparameter 	"dwh", "fr_th1", "gs_bevel", "doorframe_w", "vert_gnum", "hor_gnum", "gs_door_type", "gs_door_type_m",
						"doorframe_mat", "doorglas_mat", "bKnob", "gs_knob_type_m", "knob_size", "gs_knob_mat"
	endif
else
	if not(bDrawer) then
		lock 	"dnum", "fr_th1", "gs_bevel", "doorframe_w", "vert_gnum", "hor_gnum", "gs_door_type", "gs_door_type_m",
				"doorframe_mat", "doorglas_mat", "bKnob", "gs_knob_type_m", "knob_size", "gs_knob_mat"
		hideparameter 	"dnum", "fr_th1", "gs_bevel", "doorframe_w", "vert_gnum", "hor_gnum", "gs_door_type", "gs_door_type_m",
						"doorframe_mat", "doorglas_mat", "bKnob", "gs_knob_type_m", "knob_size", "gs_knob_mat"
	endif
endif

if gs_detlevel_3D_m = 1 then
	lock "gs_knob_type_m", "knob_size", "gs_knob_mat", "bKnob"
	hideparameter "gs_knob_type_m", "knob_size", "gs_knob_mat", "bKnob"
endif

values "gs_door_type" stDoorTypes							! Style 1, Style 2, Style 3, Style 4
values "gs_door_type_m" 1,2,3,8

if GLOB_MODPAR_NAME = "gs_door_type" then
	gs_door_type_m = 8					! None
	for iDoor=1 to 3
		if gs_door_type = stDoorTypes[iDoor] then gs_door_type_m = iDoor
	next iDoor
	parameters gs_door_type_m = gs_door_type_m
else
	if gs_door_type_m = 8 then
		gs_door_type = stDoorTypes[4]
	else
		gs_door_type = stDoorTypes[gs_door_type_m]
	endif
	parameters gs_door_type = gs_door_type
endif

if gs_door_type_m = 1 then			! Style 1
	hideparameter "doorglas_mat","doorframe_w","vert_gnum","hor_gnum"
	lock "doorglas_mat","doorframe_w","vert_gnum","hor_gnum"
endif

if gs_door_type_m = 2 then			! Style 2
	hideparameter "doorglas_mat","vert_gnum","hor_gnum"
	lock "doorglas_mat","vert_gnum","hor_gnum"
endif

if gs_door_type_m = 8 then 			! Style 4
	bKnob = 0
	hideparameter "gs_knob_type_m", "bKnob","gs_knob_mat","doorglas_mat","doorframe_w","vert_gnum","hor_gnum"
	lock "gs_knob_type_m", "bKnob","gs_knob_mat","doorglas_mat","doorframe_w","vert_gnum","hor_gnum"
endif

values "gs_cornice_type" stCorniceTypes		! Style 1, Style 2, Style 3, Style 4
values "gs_cornice_type_m" 1,2,3,4

if bCornice then
	if GLOB_MODPAR_NAME = "gs_cornice_type"  then
		for iCorn=1 to 4
			if gs_cornice_type = stCorniceTypes[iCorn] then gs_cornice_type_m = iCorn
		next iCorn
		parameters gs_cornice_type_m = gs_cornice_type_m
	else
		 gs_cornice_type = stCorniceTypes[gs_cornice_type_m]
			parameters gs_cornice_type = gs_cornice_type
	endif
endif

if not(bCornice) then			! None
	hideparameter "tbt","tboh","gs_cornice_mat", "gs_cornice_type", "gs_cornice_type_m"
	lock "tbt","tboh","gs_cornice_mat", "gs_cornice_type", "gs_cornice_type_m"
endif


values "gs_leg_type" stLegTypes		! Style 1, Style 2, Style 3, Style 4, Style 5
values "gs_leg_type_m" 1,2,3,4,5

if GLOB_MODPAR_NAME = "gs_leg_type"  then
	for iLeg=1 to 5
		if gs_leg_type = stLegTypes[iLeg] then gs_leg_type_m = iLeg
	next iLeg
	parameters gs_leg_type_m = gs_leg_type_m
else
		gs_leg_type = stLegTypes[gs_leg_type_m]
	parameters gs_leg_type = gs_leg_type
endif

if not(bBase) then		! None
	hideparameter "leg_h","leg_w","gs_leg_mat", "gs_leg_type", "gs_leg_type_m"
	lock  "leg_h","leg_w","gs_leg_mat", "gs_leg_type", "gs_leg_type_m"
endif


values "gs_knob_type" stKnobTypes	! Style 1, Style 2, Style 3, Style 4, Style 5, Style 6, Style 7, Style 8, None
values "gs_knob_type_m" 1,2,3,4,5,6,7,8

if bKnob then
	if GLOB_MODPAR_NAME = "gs_knob_type" then
		iKnob = 1
		do
			if gs_knob_type = stKnobTypes[iKnob] then gs_knob_type_m = iKnob
			iKnob = iKnob+1
		while iKnob < 9
		parameters gs_knob_type_m = gs_knob_type_m
	else
		gs_knob_type = stKnobTypes[gs_knob_type_m]
		parameters gs_knob_type = gs_knob_type
	endif
endif

if not(bKnob) then		! None
	hideparameter "gs_knob_mat", "gs_knob_type_m", "knob_size"
	lock "gs_knob_mat", "gs_knob_type_m", "knob_size"
endif

values "leg_h" range [0,)
values "leg_w" range (0,)
values "tbt" range (0,)
values "vert_gnum" range [0,)
values "hor_gnum" range [0,)
values "doorframe_w" range [0.01,)
values "fr_th1" range [0.01,)

if iNightTableType = 1 then
	if not(bDrawerNoneType) then
		values "dnum" 1, 2
	else
		values "dnum" 0, 1, 2
	endif
else
	values "dwh" range [0,)

	if not(bDrawerNoneType) then
		values{2} "iNightTableDrawerPos" 	DRAWERPOS_UP, 	`Вверху`,
											DRAWERPOS_DOWN, `Внизу`
	else
		values{2} "iNightTableDrawerPos" 	DRAWERPOS_UP, 	`Вверху`,
											DRAWERPOS_DOWN, `Внизу`,
											DRAWERPOS_NONE, `Нет`! Up, Down, None
	endif

	if 	(iNightTableDrawerPos = DRAWERPOS_NONE and bDrawerNoneType) |\	! None
		(not(bDrawer) and not(bDrawerNoneType)) then
		hideparameter "dwh","gs_door_type","gs_knob_type_m", "bKnob", "knob_size", "doorframe_w","vert_gnum","hor_gnum",
			"doorframe_mat","doorglas_mat","gs_knob_mat","fr_th1"
		lock "dwh","gs_door_type","gs_knob_type_m", "bKnob", "knob_size","doorframe_w","vert_gnum","hor_gnum",
			"doorframe_mat","doorglas_mat","gs_knob_mat","fr_th1"
	endif
	if not(bDrawer) and not(bDrawerNoneType) then
		hideparameter "iNightTableDrawerPos"
		lock  "iNightTableDrawerPos"
	endif
endif

if gs_knob_type_m = 4 or gs_knob_type_m = 8 then
	values "knob_size" range [0.001,A-2*tboh-2*0.02)
endif
if gs_knob_type_m = 5 or gs_knob_type_m = 7 then
	values "knob_size" range [0.03,A-2*tboh-2*0.02)
endif
if gs_knob_type_m = 6 then
	values "knob_size" range [0.05,A-2*tboh-2*0.02)
endif

if bCornice then
	if gs_cornice_type_m = 1 then values "tboh" range [0,)		! Style 1
	if gs_cornice_type_m = 2 OR gs_cornice_type_m = 3 then values "tboh" range [1/2*tbt,)
	if gs_cornice_type_m = 4 then values "tboh" range [3/4*tbt,)
endif

if bCalledAsMacro then end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** parameters values for Macro

!values "hboard_type_m" 1,2,3,4

!!! ******************************** Minimal Space

if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif


