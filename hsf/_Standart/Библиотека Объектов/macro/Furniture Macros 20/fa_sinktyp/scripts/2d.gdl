
drainR = 0.03	! Radius of the drain

pen gs_cont_pen
fill gs_fill_type

drawindex 10

if gs_sink_type_m > 0 then gosub gs_sink_type_m * 10

if SinkSymbol_m = 3 then

	drawindex 10

	if bMovableDrain then

		circle2 ConPosX_3, ConPosY_3, drainR

		if	gs_sink_type_m = 3 |\
			gs_sink_type_m = 6 |\
			gs_sink_type_m = 8 |\
			gs_sink_type_m = 9 |\
			gs_sink_type_m = 12 |\
			gs_sink_type_m = 13 |\
			gs_sink_type_m = 14 then

			circle2	ConPosX_4, ConPosY_4, drainR
		endif

		if gs_sink_type_m = 8 then
			circle2 ConPosX_5, ConPosY_5, drainR
		endif
	else
		if gs_sink_type_m = 1 then
			circle2 0.2, 0.25, drainR
		endif
		if gs_sink_type_m = 2 then
			circle2 a/2, b/2-0.025, drainR
		endif
		if gs_sink_type_m = 3 then
			circle2   a/4+0.020, b/2+0.04, drainR
			circle2 3*a/4-0.020, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 4 then
			circle2 a/4+0.02, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 5 then
			circle2 a/2, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 6 then
			circle2 a*0.62/2+0.02, b/2+0.04, drainR
			circle2 a*0.62+a*0.38/2-0.02, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 7 then
			circle2 a*0.62/2+0.02, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 8 then
			circle2 a*0.4/2+0.02, b/2+0.04, drainR
			circle2 a*0.8-0.02, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 9 then
			circle2 a*0.4/2+0.02, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 10 then
			circle2 0.25, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 11 then
			circle2 0.75, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 12 then
			circle2 0.59, b/2+0.04, drainR
			circle2 0.97, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 13 then
			circle2 0.55, b/2+0.04, drainR
			circle2 0.95, b/2+0.04, drainR
		endif
		if gs_sink_type_m = 14 then
			circle2 0.23, b/2+0.04, drainR
			circle2 0.60, b/2+0.04, drainR
		endif
	endif
endif

END
10:		! --- Sink round/ TYPE_1 ---

	ADD2 a/2,b/2
	if sunder then
		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,	0,	901,
				a/2-0.02,360,4001
	else
		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,	0,	901,
				a/2,360,4001
	endif

	circle2 0,0,a/2-0.025

	if bShowHotspots then
		hotspot2  0,	 A/2
		hotspot2 -A/2,	 0
		hotspot2  0,	-A/2
		hotspot2  A/2,	 0
	endif

	drawindex 20

	add2 0, a/2+0.035			! TAP
	TapSymbol_a = 0
	TapSymbolSubType = 1
	gosub 200
	del 1

	DEL 1

RETURN



20:		! --- Sink 1 Bowl/ TYPE_2 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a-sSide
	bowlY2	= b-sTop
	gosub "bowl"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	ADD2 a/2, b-sTop*0.6            ! TAP
	TapSymbol_a = 0
	TapSymbolSubType = 2
	gosub 200
	del 1

return


30:		! --- Sink 2 Bowl/ TYPE_3 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a/2-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	bowlX1	= a/2+sMid
	bowlY1	= sBot
	bowlX2	= a-sSide
	bowlY2	= b-sTop
	gosub "bowl"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	ADD2 a*0.50, b-sTop*0.6         !! TAP
	TapSymbol_a = -30
	TapSymbolSubType = 2
	gosub 200
	del 1

return


40:		! --- Bar Sink/ TYPE_4 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a/2-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	trayDepth	= b - sTop - sBot
	trayX1		= a/2+sSide 	!+sMid
	trayX2		= a-sSide
	trayMod		= 0.03
	gosub "tray"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a/2, b-sTop*0.6       !! TAP
	TapSymbol_a = -30
	TapSymbolSubType = 2
	gosub 200
	del 1

return


50:		! --- Bar sink Center/ TYPE_5 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= a*sRate4
	bowlY1	= sBot
	bowlX2	= a*sRate3
	bowlY2	= b-sTop
	gosub "bowl"

	trayDepth	= b - sTop - sBot
	trayX1		= a*sRate3+sMid
	trayX2		= a-sSide
	trayMod		= 0.03
	gosub "tray"

	mul2 -1,1
	add2 -a, 0
	gosub "tray"
	del 2

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a/2, b-sTop*0.6       !! TAP
	TapSymbol_a			= 0
	TapSymbolSubType	= 2
	gosub 200
	del 1

return



60:		! --- Sink 2 Bowl Assymetric_1/ TYPE_6 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a*sRate-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	bowlX1	= a*sRate+sMid
	bowlY1	= sBot
	bowlX2	= a-sSide
	bowlY2	= b-sTop-sRed
	gosub "bowl"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a*(sRate - (TapSymbol_m=3) * 0.13), b-sTop*0.6      !! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 3
	gosub 200
	del 1

return


70:		! --- Sink 2 bowl Asymetric_2/ TYPE_7 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a*sRate-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	if SinkSymbol_m >= 2 then
		bowlX1	= a*sRate+sMid
		bowlY1	= sBot
		bowlX2	= a-sSide
		bowlY2	= b-sTop-sRed
		gosub "bowl"
	endif

	trayDepth	= b - sTop - sBot - sRed - 0.01
	trayX1		= a*sRate+sMid+sTray
	trayX2		= a-sSide-sTray
	trayMod		= 0
	gosub "tray"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a*sRate, b-sTop*0.6       !! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 2
	gosub 200
	del 1

return


80:		! --- Sink 3 Bowl_1/ TYPE_8 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a*sRate2-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	bowlX1	= a*sRate2+sMid
	bowlY1	= sBot
	bowlX2	= a*sRate1-sMid
	bowlY2	= b-sTop-sRed
	gosub "bowl"

	bowlX1	= a*sRate1+sMid
	bowlY1	= sBot
	bowlX2	= a-sSide
	bowlY2	= b-sTop
	gosub "bowl"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a*sRate2 - (TapSymbol_m=3) * a/8, b-sTop*0.6       !! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 3
	gosub 200
	del 1

return



90:		! --- Sink 3 Bowl_2/ TYPE_9 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a*sRate2-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	bowlX1	= a*sRate2+sMid
	bowlY1	= sBot
	bowlX2	= a*sRate1-sMid
	bowlY2	= b-sTop-sRed
	gosub "bowl"

	if SinkSymbol_m >= 2 then
		bowlX1	= a*sRate1+sMid
		bowlY1	= sBot
		bowlX2	= a-sSide
		bowlY2	= b-sTop
		gosub "bowl"
	endif

	trayDepth	= b - sTop - sBot - 0.01
	trayX1		= a*sRate1+sMid+sTray
	trayX2		= a-sSide-sTray
	trayMod		= 0
	gosub "tray"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a*sRate2 - (TapSymbol_m=3) * a/8, b-sTop*0.6       !! TAP
	TapSymbol_a = -30
	TapSymbolSubType = 3
	gosub 200
	del 1

return



100:		! --- Single with Tray/ TYPE_10 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a*sRate5-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	trayDepth	= b - sTop - sBot
	trayX1		= a*sRate5+sMid
	trayX2		= a-sSide
	trayMod		= 0.05
	gosub "tray"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a*sRate5, b-sTop*0.6       !! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 2
	gosub 200
	del 1

return


110:		! --- Single Centered/ TYPE_11 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= a*sRate7+sMid
	bowlY1	= sBot
	bowlX2	= a*sRate8-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	trayDepth	= b - sTop - sBot
	trayX1		= a*sRate8+sMid
	trayX2		= a-sSide
	trayMod		= 0.05
	gosub "tray"

	mul2 -1,1
	add2 -a, 0
	gosub "tray"
	del 2

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 sRate8*A, b-sTop*0.6       !! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 2
	gosub 200
	del 1

	return



120:		! --- Double Centered Asymmetric/ TYPE_12 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= a*sRate9+sMid
	bowlY1	= sBot
	bowlX2	= a*sRate10-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	bowlX1	= a*sRate11+sMid
	bowlY1	= sBot
	bowlX2	= a*sRate12-sMid
	bowlY2	= b-sTop-sRed
	gosub "bowl"

	trayDepth	= b - sTop - sBot
	trayX1		= a*sRate12+sMid
	trayX2		= a-sSide
	trayMod		= 0.05
	gosub "tray"

	mul2 -1,1
	add2 -a, 0
	gosub "tray"
	del 2

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a*(sRate10+(sRate11-sRate10)/2) - (TapSymbol_m=3) * 0.1, b-sTop*0.6       !! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 3
	gosub 200
	del 1

return



130:		! --- Double Centered/ TYPE_13 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= a/4+sMid
	bowlY1	= sBot
	bowlX2	= a/2-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	bowlX1	= a/2+sMid
	bowlY1	= sBot
	bowlX2	= a*0.75-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	trayDepth	= b - sTop - sBot
	trayX1		= a*0.75+sMid
	trayX2		= a-sSide
	trayMod		= 0.05
	gosub "tray"

	mul2 -1,1
	add2 -a, 0
	gosub "tray"
	del 2

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif
	drawindex 20

	add2 a/2, b-sTop*0.6      ! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 2
	gosub 200
	del 1

return



140:		! --- Double with tray/ TYPE_14 ---

	if sunder then
		gosub "contourUnderMounted"
	else
		gosub "contourNormal"
	endif

	bowlX1	= sSide
	bowlY1	= sBot
	bowlX2	= a*sRate13-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	bowlX1	= a*sRate14+sMid
	bowlY1	= sBot
	bowlX2	= a*sRate15-sMid
	bowlY2	= b-sTop
	gosub "bowl"

	trayDepth	= b - sTop - sBot
	trayX1		= a*sRate15+sMid
	trayX2		= a-sSide
	trayMod		= 0.05
	gosub "tray"

	if bShowHotspots then
		HOTSPOT2 0, b
		HOTSPOT2 0, 0
		HOTSPOT2 a, 0
		HOTSPOT2 a, b
	endif

	drawindex 20

	add2 a*sRate13 - (TapSymbol_m=3) * 0.1, b-sTop*0.6      !! TAP
	TapSymbol_a			= -30
	TapSymbolSubType	= 3
	gosub 200
	del 1

	return


! --- TAPS ---

200:
	if gs_tap_type_m <> 0 & SinkSymbol_m > 1 then

		if bEnableTapEdit then
			if gs_sink_type_m <> 1 then
				HOTSPOT2 0,			0, 5001, tapOffset, 1+128
				HOTSPOT2 -1,		0, 5002, tapOffset, 3
				HOTSPOT2 tapOffset, 0, 5003, tapOffset, 2
			endif

			add2 tapOffset * (gs_sink_type_m <> 1), 0
		endif

		call "tapType_m" parameters all	gs_tap_type_m		= tempTapType,
										bEnableTapEdit		= 0,
										bSinkTap			= 1,
										TapSymbol_a			= TapSymbol_a,
										TapSymbolSubType	= TapSymbolSubType

		if bEnableTapEdit then del 1
	endif
RETURN


"contourUnderMounted":
	if SinkSymbol_m >= 2 then
		POLY2_b 9, 1+2+4+64,gs_fill_pen,gs_back_pen,
			sSide-0.005+sRad,	sBot-0.005,			1,
			a-sSide+0.005-sRad,	sBot-0.005,			1,
			a-sSide+0.005,		sBot-0.005+sRad,	1001,
			a-sSide+0.005,		b-sTop+0.005-sRad,	1,
			a-sSide+0.005-sRad,	b-sTop+0.005,		1001,
			sSide-0.005+sRad, 	b-sTop+0.005,		1,
			sSide-0.005,		b-sTop+0.005-sRad,	1001,
			sSide-0.005,		sBot-0.005+sRad,	1,
			sSide-0.005+sRad, 	sBot-0.005,			1001
	else
		POLY2_b 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
			sSide-0.005,	sBot-0.005,		1,
			a-sSide+0.005,	sBot-0.005,		1,
			a-sSide+0.005,	b-sTop+0.005,	1,
			sSide-0.005,	b-sTop+0.005,	1,
			sSide-0.005,	sBot-0.005, 	-1
	endif
return


"contourNormal":
	if SinkSymbol_m >= 2 then
		POLY2_B 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
				sRad, 0, 1,
				a-sRad, 0, 1,
				a, sRad, 1001,
				a, b-sRad, 1,
				a-sRad, b, 1001,
				sRad, b, 1,
				0, b-sRad, 1001,
				0,  sRad, 1,
				sRad, 0, 1001
	else
		POLY2_b 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
			0, 0, 1,
			a, 0, 1,
			a, b, 1,
			0, b, 1,
			0, 0, -1
	endif
return


"bowl":
	if SinkSymbol_m >= 2 then
		POLY2_ 10, 1,
			bowlX1+sRad,	bowlY1,			1,
			bowlX2-sRad,	bowlY1,			1,
			bowlX2,			bowlY1+sRad,	1001,
			bowlX2,			bowlY2-sRad,	1,
			bowlX2-sRad,	bowlY2,			1001,
			bowlX1+sRad,	bowlY2,			1,
			bowlX1,			bowlY2-sRad,	1001,
			bowlX1,			bowlY1+sRad,	1,
			bowlX1+sRad,	bowlY1,			1001,
			bowlX1+sRad,	bowlY1,			-1
	else
		POLY2_ 5, 1,
			bowlX1, bowlY1, 1,
			bowlX2, bowlY1, 1,
			bowlX2, bowlY2, 1,
			bowlX1, bowlY2, 1,
			bowlX1, bowlY1, -1
	endif
return

"tray":
	add2 0, sBot
	if SinkSymbol_m >= 2 then
		nK = int(trayDepth/0.0325)+1

		for k1=1 to nK			! TOP 2
			if k1=1 or k1=nK then
				if abs(trayMod) > eps then
					modLength = trayMod * (sTray / trayMod)
				else
					modLength = 0
				endif
			else
				modLength = 0
			endif
			poly2_ 5,1,
					trayX1,				0.005,      1,
					trayX2-modLength,	0.005,      1,
					trayX2-modLength,	0.01,      1,
					trayX1,				0.01,      1,
					trayX1,				0.005,     -1
			add2 0, (trayDepth-0.01)/(nK-1)
		next k1
		del nK
	else
		nK = int(trayDepth/0.065)+1

		for k1=1 to nK
			LINE2 trayX1,     0.005, trayX2,     0.005
			add2 0, (trayDepth-0.01)/(nK-1)
		next k1

		del nK
	endif

	del 1
return
