
IF gs_detlevel_3D_m = 0 then END

if not(gs_shadow) then SHADOW OFF


MATERIAL gs_sink_mat
PEN gs_cont_pen

addStatus=0
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	gs_detlevel_3D_m=2
	addStatus=2
ENDIF

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 then 		!! for Simple Model and in 3D Windoow
	gs_resol = 3
else
	if gs_resol < 3 then gs_resol = 3
endif

RESOL gs_resol

if not(sunder) then
	if gs_detlevel_3D_m = 1 then
		addz 0.0001  ! small value to prevent Z-fighting
	else
		addz 0.005
	endif
endif

if gs_sink_type_m > 0 then gosub gs_sink_type_m * 10

if not(sunder) then
	del  1
endif


END

10:		! --- Sink round/ TYPE_1 ---

	if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m = 1 then
		RESOL 12     !Keep in syncron with the caller object
	else
		RESOL gs_resol
	endif

	ADD a/2, b/2, 0
	EXTRUDE 2,0,0,zzyzx,1+addStatus+16,
			0,0,901,
			a/2-0.025,360,4001

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 2,0,0,zzyzx+0.01,1+addStatus+16,
			0,0,901,
			a/2-0.02,360,4001
		del 1
	endif


	if gs_detlevel_3D_m = 1 then		! SIMPLE
		addz zzyzx
		POLY_ 	14,
				a/2, 	0, 		1,
				0,		0,		901,
				0, 		180, 	4001,
				-a/2, 	0, 		1,
				0,		0,		901,
				0, 		180, 	4001,
				a/2, 	0, 		-1,
				a/2-0.025, 0, 	1,
				0,		0,		901,
				0, 		180, 	4001,
				-(a/2-0.025), 0, 1,
				0,		0,		901,
				0, 		180, 	4001,
				(a/2-0.025), 0, -1
		del 1
	else
		addz zzyzx - topThk
		EXTRUDE 4,0,0,topThk,1+2+4+32,
				0,0,901,
				a/2,360,4001,
				0,0,901,
				a/2-0.025,360,4001
		del 1
	endif

	add 0, b/2+0.035, zzyzx            !!! TAP
	gosub 200
	del 1
	DEL 1

	return


20:		! --- Sink 1 Bowl/ TYPE_2 ----

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
				sSide+sRad,		sBot-thk,		1,
				a-sSide-sRad,	sBot-thk,		1,
				a-sSide+thk,	sBot+sRad,		1001,
				a-sSide+thk, 	b-sTop-sRad,	1,
				a-sSide-sRad,	b-sTop+thk,		1001,
				sSide+sRad,		b-sTop+thk,		1,
				sSide-thk,		b-sTop-sRad,	1001,
				sSide-thk,		sBot+sRad,		1,
				sSide+sRad,		sBot-thk,		1001,
				sSide+sRad,		sBot-thk,		-1
		del 1
	endif


	if gs_detlevel_3D_m=1 then		! SIMPLE
		EXTRUDE 5, 0, 0, zzyzx, 21,       ! SINK
				sSide,		sBot, 		0,
				a-sSide,	sBot,		0,
				a-sSide,	b-sTop,		0,
				sSide,		b-sTop,		0,
				sSide,		sBot, 		-1

		addz zzyzx

		POLY_ 	10,						! TOP
				0, 		0, 		1,
				a, 		0, 		1,
				a, 		b, 		1,
				0,		b,		1,
				0, 		0, 		-1,

				sSide, 		sBot, 	1,
				a-sSide,	sBot,	1,
				a-sSide,	b-sTop,	1,
				sSide, 		b-sTop, 1,
				sSide, 		sBot, 	-1
		DEL 1
	else							! DETAILED

		EXTRUDE 10, 0, 0, zzyzx -topThk, addStatus+21,       ! SINK
				sSide+sRad,		sBot,			1,
				a-sSide-sRad,	sBot,			1,
				a-sSide,		sBot+sRad,		1001,
				a-sSide, 		b-sTop-sRad,	1,
				a-sSide-sRad,	b-sTop,			1001,
				sSide+sRad,		b-sTop,			1,
				sSide,			b-sTop-sRad,	1001,
				sSide,			sBot+sRad,		1,
				sSide+sRad,		sBot,			1001,
				sSide+sRad,		sBot,			-1

		addz zzyzx - topThk

		PRISM_ 	20, topThk,           ! TOP
					sRad, 		0, 		13,
					a-sRad, 	0, 		13,
					a, 			sRad,	1013,
					a, 			b-sRad, 13,
					a-sRad, 	b, 		1013,
					sRad, 		b, 		13,
					0, 			b-sRad, 1013,
					0,  		sRad, 	13,
					sRad, 		0, 		1013,
					sRad, 		0, 		-1,

					sSide+sRad, 	sBot, 			12,
					a-sSide-sRad, 	sBot, 			12,
					a-sSide, 		sBot+sRad, 		1012,
					a-sSide, 		b-sTop-sRad,	12,
					a-sSide-sRad, 	b-sTop,			1012,
					sSide+sRad, 	b-sTop,			12,
					sSide, 			b-sTop-sRad,	1012,
					sSide, 			sBot+sRad,		12,
					sSide+sRad, 	sBot,			1012,
					sSide+sRad, 	sBot,			-1

		del 1
	endif

	add a/2, b-sTop*0.6, zzyzx          ! TAP
	gosub 200
	del 1

	RETURN

30:		! --- Sink 2 Bowl/ TYPE_3 ---

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
				sSide+sRad, 	sBot-thk, 		1,
				a-sSide-sRad, 	sBot-thk, 		1,
				a-sSide+thk, 	sBot+sRad, 		1001,
				a-sSide+thk, 	b-sTop-sRad, 	1,
				a-sSide-sRad, 	b-sTop+thk, 	1001,
				sSide+sRad, 	b-sTop+thk, 	1,
				sSide-thk, 		b-sTop-sRad, 	1001,
				sSide-thk, 		sBot+sRad, 		1,
				sSide+sRad, 	sBot-thk, 		1001,
				sSide+sRad, 	sBot-thk, 		-1
		del 1
	endif


	if gs_detlevel_3D_m=1 then				! SIMPLE
		EXTRUDE 5, 0, 0, zzyzx, 21,     	! SINK
				sSide, 		sBot, 	0,
				a/2-sMid,	sBot,	0,
				a/2-sMid,	b-sTop,	0,
				sSide, 		b-sTop,	0,
				sSide, 		sBot, 	-1

		EXTRUDE 5, 0, 0, zzyzx, 21,       	! SINK
				a/2+sMid, 	sBot, 	0,
				a-sSide,	sBot,	0,
				a-sSide,	b-sTop,	0,
				a/2+sMid, 	b-sTop, 0,
				a/2+sMid, 	sBot, 	-1
		addz zzyzx
		POLY_ 15,    				      	!!TOP
				0, 			0, 		1,
				a, 			0, 		1,
				a, 			b, 		1,
				0,			b,		1,
				0, 			0, 		-1,

				sSide, 		sBot, 	1,
				a/2-sMid,	sBot,	1,
				a/2-sMid,	b-sTop,	1,
				sSide, 		b-sTop, 1,
				sSide, 		sBot, 	-1,

				a/2+sMid, 	sBot, 	1,
				a-sSide,	sBot,	1,
				a-sSide,	b-sTop,	1,
				a/2+sMid, 	b-sTop, 1,
				a/2+sMid, 	sBot, 	-1
		del 1
	else
		EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,        ! SINK
				sSide+sRad, 	sBot, 			1,
				a/2-sMid-sRad, 	sBot, 			1,
				a/2-sMid, 		sBot+sRad, 		1001,
				a/2-sMid, 		b-sTop-sRad, 	1,
				a/2-sMid-sRad, 	b-sTop, 		1001,
				sSide+sRad, 	b-sTop, 		1,
				sSide, 			b-sTop-sRad, 	1001,
				sSide, 			sBot+sRad, 		1,
				sSide+sRad, 	sBot, 			1001,
				sSide+sRad, 	sBot, 			-1
		EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,
				a/2+sMid+sRad, 	sBot, 			1,
				a-sSide-sRad, 	sBot, 			1,
				a-sSide, 		sBot+sRad, 		1001,
				a-sSide, 		b-sTop-sRad, 	1,
				a-sSide-sRad, 	b-sTop, 		1001,
				a/2+sMid+sRad, 	b-sTop, 		1,
				a/2+sMid,		b-sTop-sRad, 	1001,
				a/2+sMid, 		sBot+sRad, 		1,
				a/2+sMid+sRad, 	sBot, 			1001,
				a/2+sMid+sRad, 	sBot, 			-1

		addz zzyzx-topThk                                 !! TOP
		PRISM_ 30, 			topThk,
				sRad, 		0, 		13,
				a-sRad, 	0, 		13,
				a, 			sRad, 	1013,
				a, 			b-sRad, 13,
				a-sRad,	 	b, 		1013,
				sRad, 		b, 		13,
				0, 			b-sRad, 1013,
				0,  		sRad,	13,
				sRad, 		0, 		1013,
				sRad, 		0, 		-1,

				sSide+sRad, 	sBot,			12,
				a/2-sMid-sRad, 	sBot,			12,
				a/2-sMid, 		sBot+sRad,		1012,
				a/2-sMid, 		b-sTop-sRad,	12,
				a/2-sMid-sRad, 	b-sTop,			1012,
				sSide+sRad, 	b-sTop,			12,
				sSide, 			b-sTop-sRad,	1012,
				sSide, 			sBot+sRad,		12,
				sSide+sRad, 	sBot,			1012,
				sSide+sRad, 	sBot,			-1,

				a/2+sMid+sRad, 	sBot, 			12,
				a-sSide-sRad, 	sBot, 			12,
				a-sSide, 		sBot+sRad, 		1012,
				a-sSide, 		b-sTop-sRad, 	12,
				a-sSide-sRad, 	b-sTop, 		1012,
				a/2+sMid+sRad, 	b-sTop,			12,
				a/2+sMid, 		b-sTop-sRad,	1012,
				a/2+sMid, 		sBot+sRad,		12,
				a/2+sMid+sRad, 	sBot,			1012,
				a/2+sMid+sRad, 	sBot,			-1
		del 1
	endif

	add a/2, b-sTop*0.6, zzyzx            !!! TAP
	gosub 200
	del 1

	return



40:		! --- Bar Sink/ TYPE_4 ---

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
				sSide+sRad, 	sBot-thk, 			1,
				a/2-sMid-sRad,	sBot-thk, 			1,
				a/2-sMid+thk, 	sBot+sRad, 		1001,
				a/2-sMid+thk, 	b-sTop-sRad, 	1,
				a/2-sMid-sRad, 	b-sTop+thk, 		1001,
				sSide+sRad, 	b-sTop+thk, 		1,
				sSide-thk, 		b-sTop-sRad, 	1001,
				sSide-thk, 		sBot+sRad, 		1,
				sSide+sRad, 	sBot-thk, 			1001,
				sSide+sRad, 	sBot-thk, 			-1
		del 1
	endif


	if gs_detlevel_3D_m=1 then				! SIMPLE
		EXTRUDE 5, 	0, 0, zzyzx, 21,		! SINK
				sSide, 		sBot, 	0,
				a/2-sMid,	sBot,	0,
				a/2-sMid,	b-sTop,	0,
				sSide, 		b-sTop, 	0,
				sSide, 		sBot, 	-1
		addz zzyzx
		POLY_ 	10,           				! TOP
				0, 			0, 		1,
				a, 			0, 		1,
				a, 			b, 		1,
				0,			b,		1,
				0, 			0, 		-1,

				sSide, 		sBot, 	1,
				a/2-sMid,	sBot,	1,
				a/2-sMid,	b-sTop,	1,
				sSide, 		b-sTop,	1,
				sSide, 		sBot, 	-1
		del 1
	else
		EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,        ! SINK
				sSide+sRad, 	sBot, 			1,
				a/2-sMid-sRad,	sBot, 			1,
				a/2-sMid, 		sBot+sRad, 		1001,
				a/2-sMid, 		b-sTop-sRad, 	1,
				a/2-sMid-sRad, 	b-sTop, 		1001,
				sSide+sRad, 	b-sTop, 		1,
				sSide, 			b-sTop-sRad, 	1001,
				sSide, 			sBot+sRad, 		1,
				sSide+sRad, 	sBot, 			1001,
				sSide+sRad, 	sBot, 			-1
		addz zzyzx - topThk
		PRISM_ 	20, topThk,
				sRad, 		0, 		13,
				a-sRad, 	0, 		13,
				a, 			sRad, 	1013,
				a, 			b-sRad, 13,
				a-sRad, 	b, 		1013,
				sRad, 		b, 		13,
				0, 			b-sRad, 1013,
				0,  		sRad, 	13,
				sRad, 		0, 		1013,
				sRad, 		0, 		-1,

				sSide+sRad, 	sBot,			12,
				a/2-sMid-sRad,	sBot,			12,
				a/2-sMid, 		sBot+sRad,		1012,
				a/2-sMid, 		b-sTop-sRad,	12,
				a/2-sMid-sRad, 	b-sTop,			1012,
				sSide+sRad, 	b-sTop,			12,
				sSide, 			b-sTop-sRad,	1012,
				sSide, 			sBot+sRad,		12,
				sSide+sRad, 	sBot,			1012,
				sSide+sRad, 	sBot,			-1
		addz topThk
		nK = int((b-sTop-sBot)/0.0325)+1
		addy sBot
		for k1=1 to nK                         ! TOP 2
			if k1=1 or k1=nK then
				modLength = 0.03 * (sTray / 0.04)
			else
				modLength = 0
			endif
			prism_ 	5,0.002,
					a/2+sMid,			0.005,	14,
					a-sSide-modLength,	0.005,	14,
					a-sSide-modLength,	0.01,	14,
					a/2+sMid,			0.01,	14,
					a/2+sMid,			0.005,	-1

			addy (b-sTop-sBot-0.01)/(nK-1)
		next k1
		del k1
		del 2
	endif

	add a/2, b-sTop*0.6, zzyzx            ! TAP
	gosub 200
	del 1

	return


50:		! --- Bar sink Center/ TYPE_5 ---

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
				sRate4*a+sRad, 	sBot-thk,		1,
				sRate3*a-sRad,	sBot-thk,		1,
				sRate3*a+thk, 	sBot+sRad,		1001,
				sRate3*a+thk, 	b-sTop-sRad,	1,
				sRate3*a-sRad,	b-sTop+thk, 	1001,
				sRate4*a+sRad,	b-sTop+thk, 	1,
				sRate4*a-thk, 	b-sTop-sRad, 	1001,
				sRate4*a-thk, 	sBot+sRad,		1,
				sRate4*a+sRad,	sBot-thk, 		1001,
				sRate4*a+sRad,	sBot-thk, 		-1
		del 1
	endif


	if gs_detlevel_3D_m=1 then				! SIMPLE
		EXTRUDE 5, 0, 0, zzyzx, 21,			! SINK
				sRate4*a, 	sBot, 		0,
				sRate3*a,	sBot,		0,
				sRate3*a,	b-sTop,		0,
				sRate4*a, 	b-sTop, 		0,
				sRate4*a, 	sBot, 		-1

		addz zzyzx
		POLY_ 	10,          				! TOP
				0, 		0, 	1,
				a, 		0, 	1,
				a, 		b, 	1,
				0,		b,	1,
				0, 		0, -1,

				sRate4*a, 	sBot, 	1,
				sRate3*a,	sBot,	1,
				sRate3*a,	b-sTop,	1,
				sRate4*a, 	b-sTop, 	1,
				sRate4*a, 	sBot, 	-1
		DEL 1
	else
		EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,        ! SINK
				sRate4*a+sRad, 	sBot,			1,
				sRate3*a-sRad,	sBot,			1,
				sRate3*a, 		sBot+sRad,		1001,
				sRate3*a, 		b-sTop-sRad,	1,
				sRate3*a-sRad,	b-sTop, 		1001,
				sRate4*a+sRad,	b-sTop, 		1,
				sRate4*a, 		b-sTop-sRad, 	1001,
				sRate4*a, 		sBot+sRad,		1,
				sRate4*a+sRad,	sBot, 			1001,
				sRate4*a+sRad,	sBot, 			-1
		addz zzyzx - topThk
		PRISM_ 	20, 	topThk,
				sRad, 		0, 		13,
				a-sRad, 	0, 		13,
				a, 			sRad, 	1013,
				a, 			b-sRad, 13,
				a-sRad, 	b, 		1013,
				sRad, 		b, 		13,
				0, 			b-sRad, 1013,
				0,  		sRad, 	13,
				sRad, 		0, 		1013,
				sRad, 		0, 		-1,

				sRate4*a+sRad,	sBot, 			12,
				sRate3*a-sRad,	sBot, 			12,
				sRate3*a, 		sBot+sRad,		1012,
				sRate3*a, 		b-sTop-sRad,	12,
				sRate3*a-sRad,	b-sTop, 		1012,
				sRate4*a+sRad,	b-sTop, 		12,
				sRate4*a, 		b-sTop-sRad,	1012,
				sRate4*a, 		sBot+sRad, 		12,
				sRate4*a+sRad,	sBot,	 		1012,
				sRate4*a+sRad,	sBot, 			-1
		addz topThk
		nK = int((b-sTop-sBot)/0.0325)+1
		addy sBot
		for k2=1 to 2
			for k1=1 to nK                !! TOP 2
				if k1=1 or k1=nK then
					modLength = 0.03 * (sTray / 0.04)
				else
					modLength = 0
				endif
				prism_ 5,0.002,
						sTray+modLength, 0.005,	14,
						sRate4*a-sTray,	0.005,	14,
						sRate4*a-sTray,	0.01,	14,
						sTray+modLength,	0.01,	14,
						sTray+modLength,	0.005,	-1
				addy (b-sTop-sBot-0.01)/(nK-1)
			next k1
			del  (k1-1)
			addx a
			mulx -1
		next k2
		del 2*(k2-1) + 1
		del 2
	endif

	add a/2, b - sTop*0.6, zzyzx            ! TAP
	gosub 200
	del 1

	return


60:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!! *** Sink 2 Bowl Assymetric_1/ TYPE_6  *** !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
			sSide+sRad, 		sBot-thk, 			1,
			a*sRate+sMid-sRad, 	sBot-thk, 			1,
			a*sRate+sMid, 	sBot+sRad, 			1001,
			a*sRate+sMid, 	b-sTop-sRad,		1,
			a*sRate+sMid-sRad, 	b-sTop+thk, 		1001,
			sSide+sRad, 		b-sTop+thk, 		1,
			sSide-thk, 			b-sTop-sRad,		1001,
			sSide-thk, 			sBot+sRad, 			1,
			sSide+sRad, 		sBot-thk, 			1001,
			sSide+sRad, 		sBot-thk, 			-1
		addz 0.05
		extrude 10, 0, 0, zzyzx-0.04, 1+addStatus+16,
			a*sRate+sMid+sRad, 	sBot-thk,			1,
			a-sSide-sRad, 		sBot-thk,			1,
			a-sSide+thk, 		sBot+sRad,			1001,
			a-sSide+thk, 		b-sTop-sRed-sRad, 	1,
			a-sSide-sRad, 		b-sTop-sRed+thk,	1001,
			a*sRate+sMid+sRad, 	b-sTop-sRed+thk,	1,
			a*sRate+sMid-thk, 	b-sTop-sRed-sRad,	1001,
			a*sRate+sMid-thk, 	sBot+sRad,			1,
			a*sRate+sMid+sRad, 	sBot-thk,			1001,
			a*sRate+sMid+sRad, 	sBot-thk,			-1
		del 2
	endif


IF gs_detlevel_3D_m = 1 THEN					!! SIMPLE
	EXTRUDE 5, 0, 0, zzyzx, 21,			!! SINK
			sSide,			sBot, 	0,
			a*sRate-sMid,	sBot,	0,
			a*sRate-sMid,	b-sTop,	0,
			sSide, 			b-sTop, 	0,
			sSide, 			sBot, 	-1
	ADDZ 0.05
	EXTRUDE 5, 0, 0, zzyzx-0.05, 21,		!! SINK
			a-sSide, 		sBot,			0,
			a*sRate+sMid,	sBot,			0,
			a*sRate+sMid,	b-sTop-sRed,	0,
			a-sSide, 		b-sTop-sRed,	0,
			a-sSide, 		sBot,			-1
	del 1
	addz zzyzx
	POLY_ 	15, 						!! TOP
			0, 			0, 		1,
			a, 			0, 		1,
			a, 			b, 		1,
			0,			b,		1,
			0, 			0, 		-1,

			sSide, 			sBot, 			1,
			a*sRate-sMid,	sBot,			1,
			a*sRate-sMid,	b-sTop,			1,
			sSide, 			b-sTop,			1,
			sSide, 			sBot, 			-1,

			a-sSide, 		sBot, 			1,
			a*sRate+sMid,	sBot,			1,
			a*sRate+sMid,	b-sTop-sRed,	1,
			a-sSide, 		b-sTop-sRed, 	1,
			a-sSide, 		sBot, 			-1
	DEL 1
ELSE
	EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,		!! SINK
			sSide+sRad, 		sBot, 		1,
			a*sRate-sMid-sRad, 	sBot, 		1,
			a*sRate-sMid, 		sBot+sRad, 	1001,
			a*sRate-sMid, 		b-sTop-sRad,1,
			a*sRate-sMid-sRad, 	b-sTop, 	1001,
			sSide+sRad, 		b-sTop, 	1,
			sSide, 				b-sTop-sRad,1001,
			sSide, 				sBot+sRad, 	1,
			sSide+sRad, 		sBot, 		1001,
			sSide+sRad, 		sBot, 		-1
	ADDZ 0.05
	EXTRUDE 10, 0, 0, zzyzx-0.055, addStatus+21,
			a*sRate+sMid+sRad, 	sBot,				1,
			a-sSide-sRad, 		sBot,				1,
			a-sSide, 			sBot+sRad,			1001,
			a-sSide, 			b-sTop-sRed-sRad, 	1,
			a-sSide-sRad, 		b-sTop-sRed,		1001,
			a*sRate+sMid+sRad, 	b-sTop-sRed,		1,
			a*sRate+sMid, 		b-sTop-sRed-sRad,	1001,
			a*sRate+sMid, 		sBot+sRad,			1,
			a*sRate+sMid+sRad, 	sBot,				1001,
			a*sRate+sMid+sRad, 	sBot,				-1
	del 1
	addz zzyzx - topThk
	PRISM_ 	30, topThk,							!! TOP
			sRad, 			0, 		13,
			a-sRad, 		0, 		13,
			a, 				sRad, 	1013,
			a, 				b-sRad, 13,
			a-sRad,			b, 		1013,
			sRad, 			b, 		13,
			0, 				b-sRad, 1013,
			0,  			sRad, 	13,
			sRad, 			0, 		1013,
			sRad, 			0, 		-1,

			sSide+sRad, 		sBot, 			12,
			a*sRate-sMid-sRad, 	sBot, 			12,
			a*sRate-sMid, 		sBot+sRad, 		1012,
			a*sRate-sMid, 		b-sTop-sRad, 	12,
			a*sRate-sMid-sRad,	b-sTop, 		1012,
			sSide+sRad, 		b-sTop, 		12,
			sSide, 				b-sTop-sRad, 	1012,
			sSide, 				sBot+sRad, 		12,
			sSide+sRad, 		sBot, 			1012,
			sSide+sRad, 		sBot, 			-1,

			a*sRate+sMid+sRad, 	sBot, 				12,
			a-sSide-sRad, 		sBot, 				12,
			a-sSide, 			sBot+sRad, 			1012,
			a-sSide, 			b-sTop-sRed-sRad, 	12,
			a-sSide-sRad, 		b-sTop-sRed,		1012,
			a*sRate+sMid+sRad, 	b-sTop-sRed,		12,
			a*sRate+sMid, 		b-sTop-sRed-sRad, 	1012,
			a*sRate+sMid, 		sBot+sRad, 			12,
			a*sRate+sMid+sRad, 	sBot, 				1012,
			a*sRate+sMid+sRad, 	sBot, 				-1
	del 1
ENDIF

add a*sRate, b-sTop*0.6, zzyzx					!!! TAP
gosub 200
del 1

return



70:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!! *** Sink 2 bowl Asymetric_2/ TYPE_7  *** !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
			sSide+sRad, 		sBot-thk, 		1,
			a*sRate-sRad-sMid, 	sBot-thk, 		1,
			a*sRate-sMid+thk, 	sBot+sRad, 		1001,
			a*sRate-sMid+thk, 	b-sTop-sRad, 	1,
			a*sRate-sRad-sMid, 	b-sTop+thk, 	1001,
			sSide+sRad, 		b-sTop+thk, 	1,
			sSide-thk, 			b-sTop-sRad,	1001,
			sSide-thk, 			sBot+sRad,		1,
			sSide+sRad, 		sBot-thk, 		1001,
			sSide+sRad, 		sBot-thk, 		-1

		del 1
	endif


IF gs_detlevel_3D_m = 1 THEN		!!! SIMPLE
	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
			sSide, 			sBot, 		0,
			a*sRate-sMid,	sBot,		0,
			a*sRate-sMid,	b-sTop,		0,
			sSide, 			b-sTop, 	0,
			sSide, 			sBot, 		-1
	addz zzyzx
	POLY_ 	10,					!! TOP
			0, 			0, 		1,
			a, 			0, 		1,
			a, 			b, 		1,
			0,			b,		1,
			0, 			0, 		-1,

			sSide, 			sBot, 		1,
			a*sRate-sMid,	sBot,		1,
			a*sRate-sMid,	b-sTop,		1,
			sSide, 			b-sTop, 	1,
			sSide, 			sBot, 		-1
	DEL 1
ELSE
	EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,		!! SINK
			sSide+sRad, 		sBot, 			1,
			a*sRate-sRad-sMid, 	sBot, 			1,
			a*sRate-sMid, 		sBot+sRad, 		1001,
			a*sRate-sMid, 		b-sTop-sRad, 	1,
			a*sRate-sRad-sMid, 	b-sTop, 		1001,
			sSide+sRad, 		b-sTop, 		1,
			sSide, 				b-sTop-sRad,	1001,
			sSide, 				sBot+sRad,		1,
			sSide+sRad, 		sBot, 			1001,
			sSide+sRad, 		sBot, 			-1
	addz zzyzx - topThk
	PRISM_ 	30, topThk,							!! TOP
			sRad, 			0, 		13,
			a-sRad, 		0, 		13,
			a, 				sRad, 	1013,
			a, 				b-sRad, 13,
			a-sRad, 		b, 		1013,
			sRad, 			b, 		13,
			0, 				b-sRad, 1013,
			0,  			sRad, 	13,
			sRad, 			0, 		1013,
			sRad, 			0, 		-1,

			sSide+sRad, 		sBot, 			12,
			a*sRate-sRad-sMid, 	sBot, 			12,
			a*sRate-sMid, 		sBot+sRad,		1012,
			a*sRate-sMid, 		b-sTop-sRad,	12,
			a*sRate-sRad-sMid, 	b-sTop, 		1012,
			sSide+sRad, 		b-sTop, 		12,
			sSide,				b-sTop-sRad,	1012,
			sSide, 				sBot+sRad, 		12,
			sSide+sRad, 		sBot, 			1012,
			sSide+sRad, 		sBot, 			-1,

			a*sRate+sRad+sMid, 	sBot, 				12,
			a-sSide-sRad, 		sBot, 				12,
			a-sSide, 			sBot+sRad,			1012,
			a-sSide, 			b-sTop-sRed-sRad, 	12,
			a-sSide-sRad,		b-sTop-sRed, 		1012,
			a*sRate+sRad+sMid, 	b-sTop-sRed, 		12,
			a*sRate+sMid, 		b-sTop-sRed-sRad, 	1012,
			a*sRate+sMid, 		sBot+sRad,			12,
			a*sRate+sRad+sMid, 	sBot, 				1012,
			a*sRate+sRad+sMid, 	sBot, 				-1
	nK = int((b-sTop-sBot-sRed)/0.0444)
	addy sBot
	for k1 = 1 to nK                         !!! TOP 2
		prism_ 	5,0.002,
				a*sRate+sSide,    0.01,   14,
				a-sSide-sTray,     0.01,   14,
				a-sSide-sTray,     0.02,   14,
				a*sRate+sSide,    0.02,   14,
				a*sRate+sSide,    0.01,   -1
		addy (b - sTop - sBot - sRed) / nk
	next k1
	del k1
	del 1
	ADDZ zzyzx - 0.01
	prism_ 	10, topThk,
			a*sRate+sRad+sMid, 	sBot, 				12,
			a-sSide-sRad, 		sBot, 				12,
			a-sSide, 			sBot+sRad,			1012,
			a-sSide, 			b-sTop-sRed-sRad,	12,
			a-sSide-sRad, 		b-sTop-sRed,		1012,
			a*sRate+sRad+sMid, 	b-sTop-sRed, 		12,
			a*sRate+sMid, 		b-sTop-sRed-sRad, 	1012,
			a*sRate+sMid, 		sBot+sRad, 			12,
			a*sRate+sRad+sMid, 	sBot, 				1012,
			a*sRate+sRad+sMid, 	sBot, 				-1
	del 1
ENDIF


add a*sRate, b-sTop*0.6, zzyzx            !!! TAP
gosub 200
del 1

return




80:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!! *** Sink 3 Bowl_1/ TYPE_8  *** !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
			sSide+sRad,		sBot-thk,		1,
			a-sSide-sRad,	sBot-thk,		1,
			a-sSide+thk,	sBot+sRad,		1001,
			a-sSide+thk, 	b-sTop-sRad,	1,
			a-sSide-sRad,	b-sTop+thk,		1001,
			sSide+sRad,		b-sTop+thk,		1,
			sSide-thk,		b-sTop-sRad,	1001,
			sSide-thk,		sBot+sRad,		1,
			sSide+sRad,		sBot-thk,		1001,
			sSide+sRad,		sBot-thk,		-1
		del 1
	endif


IF gs_detlevel_3D_m = 1 THEN		!!! SIMPLE
	for i= 1 to 2
		EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINKS on sides
				sSide, 			sBot, 		0,
				a*sRate2-sMid,	sBot,		0,
				a*sRate2-sMid,	b-sTop,		0,
				sSide, 			b-sTop, 	0,
				sSide, 			sBot, 		-1
		addx a*sRate1 + sMid - sSide
	next i
	del (i-1)
	ADDZ 0.05
	EXTRUDE 5, 0, 0, zzyzx-0.05, 21,   !! SINK middle
			a*sRate1-sMid, 	sBot, 			0,
			a*sRate2+sMid,	sBot,			0,
			a*sRate2+sMid,	b-sTop-sRed,	0,
			a*sRate1-sMid, 	b-sTop-sRed, 	0,
			a*sRate1-sMid, 	sBot, 			-1
	DEL 1
	addz zzyzx
	POLY_ 	20,							!! TOP
			0, 		0, 		1,
			a, 		0, 		1,
			a, 		b, 		1,
			0,		b,		1,
			0, 		0, 		-1,

			sSide, 			sBot, 	1,
			a*sRate2-sMid,	sBot,	1,
			a*sRate2-sMid,	b-sTop,	1,
			sSide,			b-sTop, 1,
			sSide, 			sBot, 	-1,

			a*sRate1-sMid,	sBot,			1,
			a*sRate2+sMid,	sBot,			1,
			a*sRate2+sMid,	b-sTop-sRed,	1,
			a*sRate1-sMid,	b-sTop-sRed,	1,
			a*sRate1-sMid,	sBot,			-1,

			a-sSide,		sBot,	1,
			a*sRate1+sMid,	sBot,	1,
			a*sRate1+sMid,	b-sTop,	1,
			a-sSide,		b-sTop,	1,
			a-sSide,		sBot,	-1
	DEL 1
ELSE
	for i= 1 to 2
		EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,        !! SINK
				sSide+sRad, 			sBot, 			1,
				a*sRate2-sMid-sRad, 	sBot, 			1,
				a*sRate2-sMid, 			sBot+sRad, 		1001,
				a*sRate2-sMid, 			b-sTop-sRad, 	1,
				a*sRate2-sMid-sRad, 	b-sTop, 		1001,
				sSide+sRad, 			b-sTop, 		1,
				sSide, 					b-sTop-sRad, 	1001,
				sSide, 					sBot+sRad, 		1,
				sSide+sRad, 			sBot,			1001,
				sSide+sRad, 			sBot, 			-1
		addx a*sRate1 + sMid - sSide
	next i
	del (i-1)
	ADDZ 0.05
	EXTRUDE 10, 0, 0, zzyzx-0.055, addStatus+21,
			a*sRate2+sMid+sRad, 	sBot, 				1,
			a*sRate1-sMid-sRad, 	sBot, 				1,
			a*sRate1-sMid, 			sBot+sRad, 			1001,
			a*sRate1-sMid, 			b-sTop-sRed-sRad, 	1,
			a*sRate1-sMid-sRad, 	b-sTop-sRed, 		1001,
			a*sRate2+sMid+sRad, 	b-sTop-sRed, 		1,
			a*sRate2+sMid, 			b-sTop-sRed-sRad, 	1001,
			a*sRate2+sMid, 			sBot+sRad,			1,
			a*sRate2+sMid+sRad, 	sBot, 				1001,
			a*sRate2+sMid+sRad, 	sBot,				-1
	DEL 1
	addz zzyzx - topThk
	PRISM_ 	40, 			topThk,			!! TOP
			sRad, 			0, 		13,
			a-sRad, 		0, 		13,
			a, 				sRad, 	1013,
			a, 				b-sRad, 13,
			a-sRad, 		b, 		1013,
			sRad, 			b, 		13,
			0, 				b-sRad, 1013,
			0,  			sRad, 	13,
			sRad, 			0, 		1013,
			sRad, 			0, 		-1,

			sSide+sRad, 			sBot,			12,
			a*sRate2-sMid-sRad, 	sBot,			12,
			a*sRate2-sMid, 			sBot+sRad,		1012,
			a*sRate2-sMid, 			b-sTop-sRad,	12,
			a*sRate2-sMid-sRad, 	b-sTop,			1012,
			sSide+sRad, 			b-sTop,			12,
			sSide, 					b-sTop-sRad,	1012,
			sSide, 					sBot+sRad,		12,
			sSide+sRad, 			sBot,			1012,
			sSide+sRad, 			sBot,			-1,

			a*sRate1+sMid+sRad, 	sBot, 			12,
			a-sSide-sRad, 			sBot, 			12,
			a-sSide, 				sBot+sRad,		1012,
			a-sSide, 				b-sTop-sRad,	12,
			a-sSide-sRad, 			b-sTop, 		1012,
			a*sRate1+sMid+sRad, 	b-sTop, 		12,
			a*sRate1+sMid, 			b-sTop-sRad,	1012,
			a*sRate1+sMid, 			sBot+sRad, 		12,
			a*sRate1+sMid+sRad, 	sBot, 			1012,
			a*sRate1+sMid+sRad, 	sBot, 			-1,

			a*sRate2+sMid+sRad, 	sBot, 				12,
			a*sRate1-sMid-sRad, 	sBot,				12,
			a*sRate1-sMid, 			sBot+sRad, 			1012,
			a*sRate1-sMid, 			b-sTop-sRed-sRad, 	12,
			a*sRate1-sMid-sRad, 	b-sTop-sRed, 		1012,
			a*sRate2+sMid+sRad, 	b-sTop-sRed, 		12,
			a*sRate2+sMid, 			b-sTop-sRed-sRad, 	1012,
			a*sRate2+sMid, 			sBot+sRad, 			12,
			a*sRate2+sMid+sRad, 	sBot, 				1012,
			a*sRate2+sMid+sRad, 	sBot, 				-1
	del 1
ENDIF

add a*sRate2, b-sTop*0.6, zzyzx            !!! TAP
gosub 200
del 1

return



90:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!! *** Sink 3 Bowl_2/ TYPE_9  *** !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
			sSide+sRad, 			sBot-thk,			1,
			a*sRate2+sMid-sRad, 	sBot-thk,			1,
			a*sRate2+sMid, 			sBot+sRad,			1001,
			a*sRate2+sMid, 			b-sTop-sRad,		1,
			a*sRate2+sMid-sRad, 	b-sTop+thk,			1001,
			sSide+sRad, 			b-sTop+thk,			1,
			sSide-thk, 				b-sTop-sRad,		1001,
			sSide-thk, 				sBot+sRad,			1,
			sSide+sRad, 			sBot-thk,			1001,
			sSide+sRad, 			sBot-thk,			-1
		addz 0.05
		extrude 10, 0, 0, zzyzx - 0.04, 1+addStatus+16,
			a*sRate2+sMid+sRad, 	sBot-thk,			1,
			a*sRate1-sMid-sRad, 	sBot-thk,			1,
			a*sRate1-sMid+thk, 		sBot+sRad,			1001,
			a*sRate1-sMid+thk, 		b-sTop-sRed-sRad, 	1,
			a*sRate1-sMid-sRad, 	b-sTop-sRed+thk,	1001,
			a*sRate2+sMid+sRad, 	b-sTop-sRed+thk,	1,
			a*sRate2+sMid-thk, 		b-sTop-sRed-sRad, 	1001,
			a*sRate2+sMid-thk, 		sBot+sRad,			1,
			a*sRate2+sMid+sRad, 	sBot-thk,			1001,
			a*sRate2+sMid+sRad, 	sBot-thk,			-1

		del 2
	endif



IF gs_detlevel_3D_m=1 THEN		!!! SIMPLE
	addz zzyzx
	POLY_ 	15,					!! TOP
			0, 			0, 			1,
			a, 			0, 			1,
			a, 			b, 			1,
			0,			b,			1,
			0, 			0, 			-1,

			sSide, 			sBot, 	1,
			a*sRate2-sMid,	sBot,	1,
			a*sRate2-sMid,	b-sTop,	1,
			sSide, 			b-sTop, 1,
			sSide, 			sBot, 	-1,

			a*sRate1-sMid, 	sBot, 		 1,
			a*sRate2+sMid,	sBot,		 1,
			a*sRate2+sMid,	b-sTop-sRed, 1,
			a*sRate1-sMid, 	b-sTop-sRed, 1,
			a*sRate1-sMid, 	sBot, 		-1
	DEL 1
	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
			sSide, 			sBot, 	0,
			a*sRate2-sMid,	sBot,	0,
			a*sRate2-sMid,	b-sTop,	0,
			sSide,			b-sTop, 0,
			sSide, 			sBot, 	-1
	ADDZ 0.05
	EXTRUDE 5, 0, 0, zzyzx-0.05, 21,       !! SINK
			a*sRate1-sMid, 	sBot, 		 0,
			a*sRate2+sMid,	sBot,		 0,
			a*sRate2+sMid,	b-sTop-sRed, 0,
			a*sRate1-sMid, 	b-sTop-sRed, 0,
			a*sRate1-sMid, 	sBot, 		-1
	DEL 1
ELSE
	addz zzyzx - topThk                             !! TOP
	PRISM_ 40, topThk,
			sRad, 			0, 		13,
			a-sRad, 		0, 		13,
			a, 				sRad, 	1013,
			a, 				b-sRad, 13,
			a-sRad, 		b, 		1013,
			sRad, 			b, 		13,
			0, 				b-sRad, 1013,
			0,  			sRad, 	13,
			sRad, 			0, 		1013,
			sRad, 			0, 		-1,

			sSide+sRad, 			sBot,			12,
			a*sRate2-sMid-sRad, 	sBot,			12,
			a*sRate2-sMid, 			sBot+sRad,		1012,
			a*sRate2-sMid, 			b-sTop-sRad,	12,
			a*sRate2-sMid-sRad, 	b-sTop,			1012,
			sSide+sRad, 			b-sTop,			12,
			sSide, 					b-sTop-sRad,	1012,
			sSide, 					sBot+sRad,		12,
			sSide+sRad, 			sBot,			1012,
			sSide+sRad, 			sBot,			-1,

			a*sRate1+sMid+sRad, 	sBot, 			12,
			a-sSide-sRad, 			sBot, 			12,
			a-sSide, 				sBot+sRad,		1012,
			a-sSide, 				b-sTop-sRad,	12,
			a-sSide-sRad, 			b-sTop, 		1012,
			a*sRate1+sMid+sRad, 	b-sTop, 		12,
			a*sRate1+sMid, 			b-sTop-sRad,	1012,
			a*sRate1+sMid, 			sBot+sRad, 		12,
			a*sRate1+sMid+sRad, 	sBot, 			1012,
			a*sRate1+sMid+sRad, 	sBot, 			-1,

			a*sRate2+sMid+sRad, 	sBot, 				12,
			a*sRate1-sMid-sRad, 	sBot,				12,
			a*sRate1-sMid, 			sBot+sRad, 			1012,
			a*sRate1-sMid, 			b-sTop-sRed-sRad, 	12,
			a*sRate1-sMid-sRad, 	b-sTop-sRed, 		1012,
			a*sRate2+sMid+sRad, 	b-sTop-sRed, 		12,
			a*sRate2+sMid, 			b-sTop-sRed-sRad, 	1012,
			a*sRate2+sMid, 			sBot+sRad, 			12,
			a*sRate2+sMid+sRad, 	sBot, 				1012,
			a*sRate2+sMid+sRad, 	sBot, 				-1
	addz -topThk
	prism_  10, topThk,
			a*sRate1+sMid+sRad, 	sBot,			12,
			a-sSide-sRad, 			sBot,			12,
			a-sSide, 				sBot+sRad,		1012,
			a-sSide, 				b-sTop-sRad,	12,
			a-sSide-sRad, 			b-sTop,			1012,
			a*sRate1+sMid+sRad, 	b-sTop,			12,
			a*sRate1+sMid, 			b-sTop-sRad,	1012,
			a*sRate1+sMid, 			sBot+sRad,		12,
			a*sRate1+sMid+sRad, 	sBot,			1012,
			a*sRate1+sMid+sRad, 	sBot,			-1
	del 1
	nK = int((b-sTop-sBot)/0.0444)
	addy sBot
	for k1=1 to nK                          !!! TOP 2
		prism_ 5,	0.002,
			   a*sRate1+sMid+sTray,  0.01,   14,
			   a*sRate1+sMid+sTray,  0.02,   14,
			   a-sSide-sTray,      	0.02,   14,
			   a-sSide-sTray,     	0.01,   14,
			   a*sRate1+sMid+sTray,  0.01,   -1

		addy (b - sTop - sBot) / nk
	next k1
	del k1
	del 1
	EXTRUDE 10, 0, 0, zzyzx - topThk, addStatus+21,        !! SINK
			sSide+sRad, 			sBot,			1,
			a*sRate2-sMid-sRad, 	sBot,			1,
			a*sRate2-sMid, 			sBot+sRad,		1001,
			a*sRate2-sMid, 			b-sTop-sRad,	1,
			a*sRate2-sMid-sRad, 	b-sTop,			1001,
			sSide+sRad, 			b-sTop,			1,
			sSide, 					b-sTop-sRad,	1001,
			sSide, 					sBot+sRad,		1,
			sSide+sRad, 			sBot,			1001,
			sSide+sRad, 			sBot,			-1
	ADDZ 0.05
	EXTRUDE 10, 0, 0, zzyzx - 0.055, addStatus+21,
			a*sRate2+sMid+sRad, 	sBot,				1,
			a*sRate1-sMid-sRad, 	sBot,				1,
			a*sRate1-sMid, 			sBot+sRad,			1001,
			a*sRate1-sMid, 			b-sTop-sRed-sRad, 	1,
			a*sRate1-sMid-sRad, 	b-sTop-sRed,		1001,
			a*sRate2+sMid+sRad, 	b-sTop-sRed,		1,
			a*sRate2+sMid, 			b-sTop-sRed-sRad, 	1001,
			a*sRate2+sMid, 			sBot+sRad,			1,
			a*sRate2+sMid+sRad, 	sBot,				1001,
			a*sRate2+sMid+sRad, 	sBot,				-1
	DEL 1
ENDIF

add a*sRate2, b-sTop*0.6, zzyzx            !!! TAP
gosub 200
del 1

return



100:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!! *** Single with Tray/ TYPE_10  *** !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
			sSide+sRad, 			sBot-thk,		1,
			a*sRate5-sMid-sRad, 	sBot-thk,		1,
			a*sRate5-sMid+thk, 		sBot+sRad,		1001,
			a*sRate5-sMid+thk, 		b-sTop-sRad,	1,
			a*sRate5-sMid-sRad, 	b-sTop+thk,		1001,
			sSide+sRad, 			b-sTop+thk,		1,
			sSide-thk, 				b-sTop-sRad,	1001,
			sSide-thk, 				sBot+sRad,		1,
			sSide+sRad, 			sBot-thk,		1001,
			sSide+sRad, 			sBot-thk,		-1

		del 1
	endif


IF gs_detlevel_3D_m = 1 THEN		!!! SIMPLE
	addz zzyzx
	POLY_ 	10,           !! TOP
			0, 		0, 		1,
			a, 		0, 		1,
			a, 		b, 		1,
			0,		b,		1,
			0, 		0, 		-1,

			sSide,			sBot, 	1,
			a*sRate5-sMid,	sBot,	1,
			a*sRate5-sMid,	b-sTop,	1,
			sSide,			b-sTop, 1,
			sSide,			sBot, 	-1
	DEL 1
	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
			sSide, 			sBot, 	0,
			a*sRate5-sMid,	sBot,	0,
			a*sRate5-sMid,	b-sTop,	0,
			sSide, 			b-sTop, 0,
			sSide, 			sBot, 	-1
ELSE
	addz zzyzx - topThk
	PRISM_ 20,		topThk,
			sRad, 		0, 		13,
			a-sRad, 	0, 		13,
			a, 			sRad, 	1013,
			a, 			b-sRad, 13,
			a-sRad, 	b, 		1013,
			sRad, 		b, 		13,
			0, 			b-sRad, 1013,
			0,  		sRad, 	13,
			sRad, 		0, 		1013,
			sRad, 		0, 		-1,

			sSide+sRad, 		sBot,			12,
			a*sRate5-sMid-sRad,	sBot,			12,
			a*sRate5-sMid, 		sBot+sRad,		1012,
			a*sRate5-sMid, 		b-sTop-sRad,	12,
			a*sRate5-sMid-sRad, b-sTop,			1012,
			sSide+sRad, 		b-sTop,			12,
			sSide, 				b-sTop-sRad,	1012,
			sSide, 				sBot+sRad,		12,
			sSide+sRad, 		sBot,			1012,
			sSide+sRad, 		sBot,			-1
	addz topThk
	nK = int((b-sTop-sBot)/0.0325)+1
	addy sBot
	for k=1 to nK                               !!! TOP 2
		if k=1 or k=nK then
			modLength = 0.05 * (sTray / 0.04)
		else
			modLength = 0
		endif

		prism_ 	5,0.002,
				a*sRate5+sMid,		0.005,	14,
				a-sSide-modLength,	0.005,	14,
				a-sSide-modLength,	0.01,	14,
				a*sRate5+sMid,		0.01,	14,
				a*sRate5+sMid,		0.005,	-1
		addy (b-sTop-sBot-0.01)/(nK-1)
	next k
	del k
	del 2
	Extrude	10, 0, 0, zzyzx, addStatus+1+4+16,        !!! SINK
		sSide+sRad, 			sBot,			1,
		a*sRate5-sMid-sRad, 	sBot,			1,
		a*sRate5-sMid, 			sBot+sRad,		1001,
		a*sRate5-sMid, 			b-sTop-sRad,	1,
		a*sRate5-sMid-sRad, 	b-sTop,			1001,
		sSide+sRad, 			b-sTop,			1,
		sSide, 					b-sTop-sRad,	1001,
		sSide, 					sBot+sRad,		1,
		sSide+sRad, 			sBot,			1001,
		sSide+sRad, 			sBot,			-1
ENDIF

add a*sRate5, b-sTop*0.6, zzyzx            !!! TAP
gosub 200
del 1

return



110:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!! *** Single Centered/ TYPE_11  *** !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
	addz -0.005
	extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
		a*sRate7+sMid+sRad, 	sBot-thk,		1,
		a*sRate8-sMid-sRad, 	sBot-thk,		1,
		a*sRate8-sMid+thk, 		sBot+sRad,		1001,
		a*sRate8-sMid+thk, 		b-sTop-sRad,	1,
		a*sRate8-sMid-sRad, 	b-sTop+thk,		1001,
		a*sRate7+sMid+sRad, 	b-sTop+thk,		1,
		a*sRate7+sMid-thk, 		b-sTop-sRad,	1001,
		a*sRate7+sMid-thk, 		sBot+sRad,		1,
		a*sRate7+sMid+sRad, 	sBot-thk,		1001,
		a*sRate7+sMid+sRad, 	sBot-thk,		-1

	del 1
endif


IF gs_detlevel_3D_m = 1 THEN		!!! SIMPLE
	addz zzyzx
	POLY_ 	10,           !! TOP
			0, 		0, 	1,
			a, 		0, 	1,
			a, 		b, 	1,
			0,		b,	1,
			0, 		0, -1,

			a*sRate7+sMid, 	sBot, 	1,
			a*sRate8-sMid,	sBot,	1,
			a*sRate8-sMid,	b-sTop,	1,
			a*sRate7+sMid, 	b-sTop, 1,
			a*sRate7+sMid, 	sBot, 	-1
	DEL 1
	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
			a*sRate7+sMid, 	sBot, 	0,
			a*sRate8-sMid,	sBot,	0,
			a*sRate8-sMid,	b-sTop,	0,
			a*sRate7+sMid, 	b-sTop, 0,
			a*sRate7+sMid, 	sBot, 	-1
ELSE
	addz zzyzx - topThk
	PRISM_  20,		topThk,
			sRad, 		0, 		13,
			a-sRad, 	0, 		13,
			a, 			sRad, 	1013,
			a, 			b-sRad, 13,
			a-sRad, 	b, 		1013,
			sRad, 		b, 		13,
			0, 			b-sRad, 1013,
			0,  		sRad, 	13,
			sRad, 		0, 		1013,
			sRad, 		0, 		-1,

			a*sRate7+sMid+sRad, sBot,			12,
			a*sRate8-sMid-sRad,	sBot,			12,
			a*sRate8-sMid, 		sBot+sRad,		1012,
			a*sRate8-sMid, 		b-sTop-sRad,	12,
			a*sRate8-sMid-sRad, b-sTop,			1012,
			a*sRate7+sMid+sRad, b-sTop,			12,
			a*sRate7+sMid, 		b-sTop-sRad,	1012,
			a*sRate7+sMid, 		sBot+sRad,		12,
			a*sRate7+sMid+sRad, sBot,			1012,
			a*sRate7+sMid+sRad, sBot,			-1
	addz topThk
	nK = int((b-sTop-sBot)/0.0325)+1
	addy sBot
	for k2=1 to 2
		for k1=1 to nK                                !!! TOP 2
			if k1=1 or k1=nK then
				modLength = 0.05 * (sTray / 0.04)
			else
				modLength = 0
			endif

			prism_ 	5,0.002,
				a*sRate8+sMid,		0.005,	14,
				a-sSide-modLength,	0.005,	14,
				a-sSide-modLength,	0.01,	14,
				a*sRate8+sMid,		0.01,	14,
				a*sRate8+sMid,		0.005,	-1
			addy (b-sTop-sBot-0.01)/(nK-1)
		next k1
		del (k1-1)
		addx a
		mulx -1
	next k2
	del 2*(k2-1)+1
	del 2
	Extrude	10, 0, 0, zzyzx - topThk, addStatus+1+4+16,        !!! SINK
		a*sRate7+sMid+sRad, 	sBot,			1,
		a*sRate8-sMid-sRad, 	sBot,			1,
		a*sRate8-sMid, 			sBot+sRad,		1001,
		a*sRate8-sMid, 			b-sTop-sRad,	1,
		a*sRate8-sMid-sRad, 	b-sTop,			1001,
		a*sRate7+sMid+sRad, 	b-sTop,			1,
		a*sRate7+sMid, 			b-sTop-sRad,	1001,
		a*sRate7+sMid, 			sBot+sRad,		1,
		a*sRate7+sMid+sRad, 	sBot,			1001,
		a*sRate7+sMid+sRad, 	sBot,			-1

ENDIF

add a*sRate8, b-sTop*0.6, zzyzx            !!! TAP
gosub 200
del 1

return



120:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!! *** Double Centered Asymmetric/ TYPE_12  *** !!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
	addz -0.005
	extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
		a*sRate9+sMid+sRad, 	sBot-thk,			1,
		a*sRate12-sMid-sRad, 	sBot-thk,			1,
		a*sRate12-sMid+thk, 	sBot+sRad,			1001,
		a*sRate12-sMid+thk, 	b-sTop-sRad,		1,
		a*sRate12-sMid-sRad, 	b-sTop+thk,			1001,
		a*sRate9+sMid+sRad, 	b-sTop+thk,			1,
		a*sRate9+sMid-thk, 		b-sTop-sRad,		1001,
		a*sRate9+sMid-thk, 		sBot+sRad,			1,
		a*sRate9+sMid+sRad, 	sBot-thk,			1001,
		a*sRate9+sMid+sRad, 	sBot-thk,			-1

	del 1
endif


IF gs_detlevel_3D_m = 1 THEN		!!! SIMPLE
	addz zzyzx    !! TOP
	POLY_ 	15,           !! TOP
			0, 		0, 	1,
			a, 		0, 	1,
			a, 		b, 	1,
			0,		b,	1,
			0, 		0, 	-1,

			a*sRate9+sMid, 	sBot, 	1,
			a*sRate10-sMid,	sBot,	1,
			a*sRate10-sMid,	b-sTop,	1,
			a*sRate9+sMid, 	b-sTop, 1,
			a*sRate9+sMid, 	sBot, 	-1,

			a*sRate12-sMid, 	sBot, 			1,
			a*sRate11+sMid,		sBot,			1,
			a*sRate11+sMid,		b-sTop-sRed,	1,
			a*sRate12-sMid, 	b-sTop-sRed, 	1,
			a*sRate12-sMid, 	sBot, 			-1
		DEL 1

	EXTRUDE 5, 		0, 0, zzyzx, 21,       !! SINK
			a*sRate9+sMid, 	sBot, 	0,
			a*sRate10-sMid,	sBot,	0,
			a*sRate10-sMid,	b-sTop,	0,
			a*sRate9+sMid, 	b-sTop, 	0,
			a*sRate9+sMid, 	sBot, 	-1

	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
			a*sRate12-sMid, 	sBot, 			0,
			a*sRate11+sMid,		sBot,			0,
			a*sRate11+sMid,		b-sTop-sRed,	0,
			a*sRate12-sMid, 	b-sTop-sRed, 	0,
			a*sRate12-sMid, 	sBot, 			-1

ELSE
	addz zzyzx-topThk
	PRISM_ 	30,      topThk,
			sRad, 		0, 		13,
			a-sRad, 	0, 		13,
			a, 			sRad, 	1013,
			a, 			b-sRad, 13,
			a-sRad, 	b, 		1013,
			sRad, 		b, 		13,
			0, 			b-sRad, 1013,
			0,  		sRad, 	13,
			sRad, 		0, 		1013,
			sRad, 		0, 		-1,

			a*sRate9+sMid+sRad, sBot,			12,
			a*sRate10-sMid-sRad,sBot,			12,
			a*sRate10-sMid, 	sBot+sRad,		1012,
			a*sRate10-sMid, 	b-sTop-sRad,	12,
			a*sRate10-sMid-sRad,b-sTop,			1012,
			a*sRate9+sMid+sRad, b-sTop,			12,
			a*sRate9+sMid, 		b-sTop-sRad,	1012,
			a*sRate9+sMid, 		sBot+sRad,		12,
			a*sRate9+sMid+sRad, sBot,			1012,
			a*sRate9+sMid+sRad, sBot,			-1,

			a*sRate11+sMid+sRad, sBot,				12,
			a*sRate12-sMid-sRad,sBot,				12,
			a*sRate12-sMid, 	 sBot+sRad,			1012,
			a*sRate12-sMid, 	 b-sTop-sRed-sRad,	12,
			a*sRate12-sMid-sRad, b-sTop-sRed,		1012,
			a*sRate11+sMid+sRad, b-sTop-sRed,		12,
			a*sRate11+sMid, 	 b-sTop-sRed-sRad,	1012,
			a*sRate11+sMid, 	 sBot+sRad,			12,
			a*sRate11+sMid+sRad, sBot,				1012,
			a*sRate11+sMid+sRad, sBot,				-1
	addz topThk
	nK = int((b-sTop-sBot)/0.0325)+1
	addy sBot
	for k2=1 to 2
		for k1=1 to nK                                !!! TOP 2
			if k1=1 or k1=nK then
				modLength = 0.05 * (sTray / 0.04)
			else
				modLength = 0
			endif

			prism_ 	5,0.002,
				a*sRate12+sMid,		0.005,	14,
				a-sSide-modLength,	0.005,	14,
				a-sSide-modLength,	0.01,	14,
				a*sRate12+sMid,		0.01,	14,
				a*sRate12+sMid,		0.005,	-1

			addy (b-sTop-sBot-0.01)/(nK-1)
		next k1
		del (k1-1)
		addx a
		mulx -1
	next k2
	del 2*(k2-1) + 1
	del 2

	Extrude	10, 0, 0, zzyzx - topThk, addStatus+1+4+16,        !!! SINK
		a*sRate9+sMid+sRad, 	sBot,			1,
		a*sRate10-sMid-sRad, 	sBot,			1,
		a*sRate10-sMid, 		sBot+sRad,		1001,
		a*sRate10-sMid, 		b-sTop-sRad,	1,
		a*sRate10-sMid-sRad, 	b-sTop,			1001,
		a*sRate9+sMid+sRad, 	b-sTop,			1,
		a*sRate9+sMid, 			b-sTop-sRad,	1001,
		a*sRate9+sMid, 			sBot+sRad,		1,
		a*sRate9+sMid+sRad, 	sBot,			1001,
		a*sRate9+sMid+sRad, 	sBot,			-1

	Extrude	10, 0, 0, zzyzx - topThk, addStatus+1+4+16,        !!! SINK
		a*sRate11+sMid+sRad, 	sBot,				1,
		a*sRate12-sMid-sRad, 	sBot,				1,
		a*sRate12-sMid, 		sBot+sRad,			1001,
		a*sRate12-sMid, 		b-sTop-sRed-sRad,	1,
		a*sRate12-sMid-sRad, 	b-sTop-sRed,		1001,
		a*sRate11+sMid+sRad, 	b-sTop-sRed,		1,
		a*sRate11+sMid, 		b-sTop-sRed-sRad,	1001,
		a*sRate11+sMid, 		sBot+sRad,			1,
		a*sRate11+sMid+sRad, 	sBot,				1001,
		a*sRate11+sMid+sRad, 	sBot,				-1
ENDIF

add a*(sRate10+(sRate11-sRate10)/2), b-0.06, zzyzx            !!! TAP
gosub 200
del 1

return


130:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!! *** Double Centered/ TYPE_13  *** !!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
	addz -0.005
		extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
			a/4+sMid+sRad, 		sBot-thk,		1,
			a*0.75-sMid-sRad,	sBot-thk,		1,
			a*0.75-sMid+thk, 	sBot+sRad,		1001,
			a*0.75-sMid+thk, 	b-sTop-sRad,	1,
			a*0.75-sMid-sRad,	b-sTop+thk,		1001,
			a/4+sMid+sRad, 		b-sTop+thk,		1,
			a/4+sMid-thk, 		b-sTop-sRad,	1001,
			a/4+sMid-thk, 		sBot+sRad,		1,
			a/4+sMid+sRad, 		sBot-thk,		1001,
			a/4+sMid+sRad, 		sBot-thk,		-1

	del 1
endif


IF gs_detlevel_3D_m = 1 THEN		!!! SIMPLE
	addz zzyzx    !! TOP
	POLY_ 15,           !! TOP
		0,	0, 1,
		a,	0, 1,
		a,	b, 1,
		0,	b, 1,
		0,	0, -1,

		a/4+sMid, sBot,		1,
		a/2-sMid, sBot,		1,
		a/2-sMid, b-sTop,	1,
		a/4+sMid, b-sTop,	1,
		a/4+sMid, sBot,		-1,

		a*0.75-sMid, sBot,		1,
		a/2+sMid, 	 sBot,		1,
		a/2+sMid, 	 b-sTop,	1,
		a*0.75-sMid, b-sTop,	1,
		a*0.75-sMid, sBot,		-1

	DEL 1

	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
		a/4+sMid, sBot,		0,
		a/2-sMid, sBot,		0,
		a/2-sMid, b-sTop,	0,
		a/4+sMid, b-sTop,	0,
		a/4+sMid, sBot,		-1

	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
		a*0.75-sMid, sBot,		0,
		a/2+sMid, 	 sBot,		0,
		a/2+sMid, 	 b-sTop,	0,
		a*0.75-sMid, b-sTop,	0,
		a*0.75-sMid, sBot,		-1
ELSE
	addz zzyzx - topThk
	PRISM_ 	30,      topThk,
			sRad, 		0, 		13,
			a-sRad, 	0, 		13,
			a, 			sRad, 	1013,
			a, 			b-sRad, 13,
			a-sRad, 	b, 		1013,
			sRad, 		b, 		13,
			0, 			b-sRad, 1013,
			0,  		sRad, 	13,
			sRad, 		0, 		1013,
			sRad, 		0, 		-1,

			a/4+sMid+sRad, sBot,			12,
			a/2-sMid-sRad,sBot,			12,
			a/2-sMid, 	sBot+sRad,		1012,
			a/2-sMid, 	b-sTop-sRad,	12,
			a/2-sMid-sRad,b-sTop,			1012,
			a/4+sMid+sRad, b-sTop,			12,
			a/4+sMid, 		b-sTop-sRad,	1012,
			a/4+sMid, 		sBot+sRad,		12,
			a/4+sMid+sRad, sBot,			1012,
			a/4+sMid+sRad, sBot,			-1,

			a/2+sMid+sRad, 		sBot,				12,
			a*0.75-sMid-sRad,	sBot,				12,
			a*0.75-sMid, 	 	sBot+sRad,			1012,
			a*0.75-sMid, 		b-sTop-sRad,	12,
			a*0.75-sMid-sRad,	b-sTop,		1012,
			a/2+sMid+sRad,		b-sTop,		12,
			a/2+sMid, 	 		b-sTop-sRad,	1012,
			a/2+sMid, 	 		sBot+sRad,			12,
			a/2+sMid+sRad, 		sBot,				1012,
			a/2+sMid+sRad, 		sBot,				-1

	addz topThk
		nK = int((b-sTop-sBot)/0.0325)+1
		addy sBot
		for k2=1 to 2
			for k1=1 to nK                                !!! TOP 2
				if k1=1 or k1=nK then
					modLength = 0.05 * (sTray / 0.04)
				else
					modLength = 0
				endif

				prism_ 5,0.002,
					a*0.75+sMid,		0.005,	14,
					a-sSide-modLength,	0.005,	14,
					a-sSide-modLength,	0.01,	14,
					a*0.75+sMid,		0.01,	14,
					a*0.75+sMid,		0.005,	-1

			addy (b-sTop-sBot-0.01)/(nK-1)
			next k1
			del (k1-1)
			addx a
			mulx -1
		next k2
		del 2*(k2-1)+1
	del 2

	for k3=1 to 2
		Extrude 10, 0, 0, zzyzx - topThk, addStatus+1+4+16,        !!! SINK
			a/4+sMid+sRad, 	sBot,			1,
			a/2-sMid-sRad, 	sBot,			1,
			a/2-sMid, 		sBot+sRad,		1001,
			a/2-sMid, 		b-sTop-sRad,	1,
			a/2-sMid-sRad, 	b-sTop,			1001,
			a/4+sMid+sRad, 	b-sTop,			1,
			a/4+sMid, 		b-sTop-sRad,	1001,
			a/4+sMid, 		sBot+sRad,		1,
			a/4+sMid+sRad, 	sBot,			1001,
			a/4+sMid+sRad, 	sBot,			-1
		addx a/4
	next k3
	del (k3-1)
ENDIF

add a/2, b-sTop*0.6, zzyzx            !!! TAP
gosub 200
del 1

return



140:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!! *** Double with tray/ TYPE_14  *** !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
	addz -0.005
	extrude 10, 0, 0, zzyzx +0.01, 1+addStatus+16,
		sSide+sRad, 			sBot-thk,		1,
		a*sRate15-sMid-sRad, 	sBot-thk,		1,
		a*sRate15-sMid+thk, 	sBot+sRad,		1001,
		a*sRate15-sMid+thk, 	b-sTop-sRad,	1,
		a*sRate15-sMid-sRad, 	b-sTop+thk,		1001,
		sSide+sRad, 			b-sTop+thk,		1,
		sSide-thk, 				b-sTop-sRad,	1001,
		sSide-thk, 				sBot+sRad,		1,
		sSide+sRad, 			sBot-thk,		1001,
		sSide+sRad, 			sBot-thk,		-1

	del 1
endif


IF gs_detlevel_3D_m=1 THEN		!!! SIMPLE
	addz zzyzx
	POLY_ 	15,           !! TOP
			0, 		0, 	1,
			a, 		0, 	1,
			a, 		b, 	1,
			0,		b,	1,
			0, 		0, 	-1,

			sSide, 			sBot, 	1,
			a*sRate13-sMid,	sBot,	1,
			a*sRate13-sMid,	b-sTop,	1,
			sSide, 			b-sTop, 1,
			sSide, 			sBot, 	-1,

			a*sRate15-sMid, 	sBot, 	1,
			a*sRate14+sMid,		sBot,	1,
			a*sRate14+sMid,		b-sTop,	1,
			a*sRate15-sMid, 	b-sTop, 1,
			a*sRate15-sMid, 	sBot, 	-1
	DEL 1
	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
		sSide, 			sBot, 	0,
		a*sRate13-sMid,	sBot,	0,
		a*sRate13-sMid,	b-sTop,	0,
		sSide, 			b-sTop, 0,
		sSide, 			sBot, 	-1
	EXTRUDE 5, 0, 0, zzyzx, 21,       !! SINK
		a*sRate15-sMid, 	sBot, 	0,
		a*sRate14+sMid,		sBot,	0,
		a*sRate14+sMid,		b-sTop,	0,
		a*sRate15-sMid, 	b-sTop, 0,
		a*sRate15-sMid, 	sBot, 	-1

ELSE
	addz zzyzx - topThk
	PRISM_     30,      topThk,
			sRad, 		0, 		13,
			a-sRad, 	0, 		13,
			a, 			sRad, 	1013,
			a, 			b-sRad, 13,
			a-sRad, 	b, 		1013,
			sRad, 		b, 		13,
			0, 			b-sRad, 1013,
			0,  		sRad, 	13,
			sRad, 		0, 		1013,
			sRad, 		0, 		-1,

			sSide+sRad, 		sBot,			12,
			a*sRate13-sMid-sRad,sBot,			12,
			a*sRate13-sMid, 	sBot+sRad,		1012,
			a*sRate13-sMid, 	b-sTop-sRad,	12,
			a*sRate13-sMid-sRad,b-sTop,			1012,
			sSide+sRad,			b-sTop,			12,
			sSide, 				b-sTop-sRad,	1012,
			sSide, 				sBot+sRad,		12,
			sSide+sRad, 		sBot,			1012,
			sSide+sRad, 		sBot,			-1,

			a*sRate14+sMid+sRad, sBot,			12,
			a*sRate15-sMid-sRad,sBot,			12,
			a*sRate15-sMid, 	 sBot+sRad,		1012,
			a*sRate15-sMid, 	 b-sTop-sRad,	12,
			a*sRate15-sMid-sRad, b-sTop,		1012,
			a*sRate14+sMid+sRad, b-sTop,		12,
			a*sRate14+sMid, 	 b-sTop-sRad,	1012,
			a*sRate14+sMid, 	 sBot+sRad,		12,
			a*sRate14+sMid+sRad, sBot,			1012,
			a*sRate14+sMid+sRad, sBot,			-1

	addz topThk
		nK = int((b-sTop-sBot)/0.0325)+1
		addy sBot
		 for k1=1 to nK                          !!! TOP 2
			if k1=1 or k1=nK then
				modLength = 0.05 * (sTray / 0.04)
			else
				modLength = 0
			endif

			prism_ 5,0.002,
				a*sRate15+sMid,		0.005,	14,
				a-sSide-modLength,	0.005,	14,
				a-sSide-modLength,	0.01,	14,
				a*sRate15+sMid,		0.01,	14,
				a*sRate15+sMid,		0.005,	-1

			addy (b-sTop-sBot-0.01)/(nK-1)
		next k1
		del k1
	del 2
	Extrude      10, 0, 0, zzyzx - topThk, addStatus+1+4+16,        !!! SINK
		sSide+sRad, 			sBot,			1,
		a*sRate13-sMid-sRad, 	sBot,			1,
		a*sRate13-sMid, 		sBot+sRad,		1001,
		a*sRate13-sMid, 		b-sTop-sRad,	1,
		a*sRate13-sMid-sRad, 	b-sTop,			1001,
		sSide+sRad, 			b-sTop,			1,
		sSide, 					b-sTop-sRad,	1001,
		sSide, 					sBot+sRad,		1,
		sSide+sRad, 			sBot,			1001,
		sSide+sRad, 			sBot,			-1

	Extrude      10, 0, 0, zzyzx - topThk, 1+4+16,        !!! SINK
		a*sRate14+sMid+sRad, 	sBot,			1,
		a*sRate15-sMid-sRad, 	sBot,			1,
		a*sRate15-sMid, 		sBot+sRad,		1001,
		a*sRate15-sMid, 		b-sTop-sRad,	1,
		a*sRate15-sMid-sRad, 	b-sTop,			1001,
		a*sRate14+sMid+sRad, 	b-sTop,			1,
		a*sRate14+sMid, 		b-sTop-sRad,	1001,
		a*sRate14+sMid, 		sBot+sRad,		1,
		a*sRate14+sMid+sRad, 	sBot,			1001,
		a*sRate14+sMid+sRad, 	sBot,			-1
ENDIF

add a*sRate13, b-sTop*0.6, zzyzx            !!! TAP
gosub 200
del 1

return



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!! *** TAPS *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

200:

if gs_tap_type_m<>0 then
	if sunder then
		addz count_th
	else
		if gs_sink_type_m = 1 then
			if gs_detlevel_3D_m = 2 then
				addz -topThk
			else
				addz -0.0001
			endif
		endif
	endif

	if bEnableTapEdit then
		if gs_sink_type_m <> 1 then
			HOTSPOT 0, 0, 0, 5001, tapOffset, 1+128
			HOTSPOT -1, 0, 0, 5002, tapOffset, 3
			HOTSPOT tapOffset, 0, 0, 5003, tapOffset, 2
		endif

		addx tapOffset * (gs_sink_type_m <> 1)
	endif

	call "tapType_m" parameters all	gs_tap_type_m		= tempTapType,
									bEnableTapEdit		= 0,
									bSinkTap			= 1,
									useOldSimpleTaps	= 0

	if bEnableTapEdit then del 1

	if sunder or gs_sink_type_m = 1 then del 1
endif

RETURN
