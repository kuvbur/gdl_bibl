
!! fa_sofalegtyp.ism !!!


if gs_shadow=0 then SHADOW OFF


PEN gs_cont_pen
MATERIAL gs_leg_mat

!!!!!!!!!!!!!!!!!!!!!! *** Type1 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
IF sofaleg_typ_m=1 THEN

	IF leg_pos_m=1  THEN		!! Leg Position: Standard
			GOSUB 110
		ADDX a-0.045
			GOSUB 110
		ADD 0.045, b, 0
		ROTZ 180
			GOSUB 110
		ADDX a-0.045
			GOSUB 110
		DEL 4
	ELSE					!! Leg Position: Curved
		ADDY (r+b)
		ROTZ angle
		ADDY -(r+b-dist)

		ADDX -0.0225
		GOSUB 110
		DEL 1

		ADDY b
		ROTZ 180
		ADDX -0.0225
		GOSUB 110
		DEL 6
	ENDIF

ENDIF
!!!!!!!!!!!!!!!!!!!!!! *** Type2 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
IF sofaleg_typ_m=2 THEN

	IF leg_h<=0 THEN END
	k=1
	IF leg_h<=0.05 THEN k=2

		IF leg_pos_m=1  THEN			!! Leg Position: Standard
					GOSUB 10+k
				ADDX a-0.025
					GOSUB 10+k
				DEL 1
		ELSE						!! Leg Position: Curved
				ADDY (r+b)
				ROTZ angle
				ADDY -(r+b-dist)

				ADDX -0.015
					GOSUB 10+k
				DEL 4
		ENDIF
ENDIF
!!!!!!!!!!!!!!!!!!!!!! *** Type3 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
IF sofaleg_typ_m=3 THEN

	IF leg_h<=0 THEN END
!!	IF leg_pos_m=2 THEN RESOL 6
	k=1
	IF leg_h<=0.02 THEN k=2

		IF leg_pos_m=1  THEN			!! Leg Position: Standard
				GOSUB 10+k
			ADDY b
				GOSUB 10+k
			ADDX a
				GOSUB 10+k
			ADDY -b
				GOSUB 10+k
			DEL 3
		ELSE						!! Leg Position: Curved
			ADDY (r+b)
			ROTZ angle
			ADDY -(r+b-dist)
				GOSUB 10+k
			ADDY b
				GOSUB 10+k
			DEL 4

		ENDIF
ENDIF

!!!!!!!!!!!!!!!!!!!!!! *** Type4 *** !!!!!!!!!!!!!!!!!!!!!!!!!!
IF sofaleg_typ_m=4 THEN

IF leg_h<=0 THEN END
!!IF leg_pos_m=2 THEN RESOL 6

alpha=ATN(0.055/leg_h)

	IF leg_pos_m=1  THEN		!! Leg Position: Standard
			GOSUB 140
		ADDY b
			GOSUB 140
		ADDX a
		ROTZ 180
			GOSUB 140
		ADDY b
			GOSUB 140
		DEL 4
	ELSE					!! Leg Position: Curved
		ADDY (r+b)
		ROTZ angle
		ADDY -(r+b-dist)

		ROTZ 90
			GOSUB 140
		DEL 1
		ADDY b
		ROTZ -90
			GOSUB 140
		DEL 5
	ENDIF
ENDIF

!!!!!!!!!!!!!!!!!!!!!! *** Type5 *** !!!!!!!!!!!!!!!!!!!!!!!!!!

IF sofaleg_typ_m=5 THEN

	IF leg_h<=0 THEN END
		IF leg_pos_m=1  THEN		!! Leg Position: Standard
				GOSUB 150
			ADDY b
				GOSUB 150
			ADDX a
				GOSUB 150
			ADDY -b
				GOSUB 150
			DEL 3
		ELSE					!! Leg Position: Curved
			ADDY (r+b)
			ROTZ angle
			ADDY -(r+b-dist)
				GOSUB 150
			ADDY b
				GOSUB 150
			DEL 4
		ENDIF
	ENDIF
END

11:
	IF sofaleg_typ_m=1 THEN GOSUB 110
	IF sofaleg_typ_m=2 THEN GOSUB 120
	IF sofaleg_typ_m=3 THEN GOSUB 130
	IF sofaleg_typ_m=4 THEN GOSUB 140
	IF sofaleg_typ_m=5 THEN GOSUB 150
RETURN

12:

	IF sofaleg_typ_m=2 THEN GOSUB 220
	IF sofaleg_typ_m=3 THEN GOSUB 230
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!! RETURN_1 !!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
110:

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 THEN
	BLOCK 0.045, 0.06, 0.08		!! Simple Modell

	ELSE						!! Detailed Modell
		ADD 0.035, 0.03, 0
	ROTZ 180
	ADD 0, 0.0075, 0.02
	ROTX 90
	ADDZ 0.015
	CUTPLANE 20
	DEL 1

		ADDX -0.01
			PRISM_ 9, 0.05,
				0, 0, 15,
				0, 0.06, 15,
				0.045, 0.06, 15,
				0.045, 0, 15,
				0.035, 0, 15,
				0.035, 0.05, 15,
				0.01, 0.05, 15,
				0.01, 0, 15,
				0, 0, -1
		DEL 3

	CUTEND

	ADDZ 0.03
	ROTY 90
	RESOL gs_resol
		CYLIND 0.025, 0.03
	DEl 4

	ENDIF
RETURN


120:
ROTY -90
	IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 THEN
		PRISM_ 9, -0.03,				!! Simple Modell
			0, 0, 15,
			leg_h, 0, 15,
			leg_h, b, 15,
			0, b, 15,
			0, b-0.015, 15,
			leg_h-0.015, b-0.015, 15,
			leg_h-0.015, 0.015, 15,
			0, 0.015, 15,
			0, 0, -1
	ELSE								!! Detailed Modell
		RESOL gs_resol
		PRISM_ 13, -0.03,
			0, 0, 15,
			leg_h-0.05, 0, 13,
			leg_h, 0.05, 1013,
			leg_h, b-0.05, 13,
			leg_h-0.05, b, 1013,
			0, b, 15,
			0, b-0.015, 15,
			leg_h-0.05, b-0.015, 13,
			leg_h-0.015, b-0.05, 1013,
			leg_h-0.015, 0.05, 13,
			leg_h-0.05, 0.015, 1013,
			0, 0.015, 15,
			0, 0, -1
	ENDIF
DEL 1
RETURN

130:
	IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 THEN gs_resol=8
	RESOL gs_resol
		CYLIND 0.02, 0.03
		ADDZ 0.02
		CYLIND leg_h-0.02, 0.015
		DEL 1
RETURN

140:
	IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 THEN gs_resol=8
	RESOL gs_resol
	ROTY alpha
	CONE leg_h/COS(alpha), 0.0115, 0.02, 90-alpha, 90-alpha
	DEL 1
RETURN

150:
!ur=0.08
!lr=0.07
RESOL 4
ROTZ 45
	CONE leg_h, (lr*2)/SQR(2), (ur*2)/SQR(2), 90, 90
DEL 1
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!! RETURN_2 !!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
220:
	IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 THEN

		BLOCK 0.03, b, leg_h

	ELSE
		RESOL gs_resol
		ROTY -90
		PRISM_ 7, -0.03,
			0, -0.00001, 15,
			0, 0, 13,
			0, leg_h, 913,
			leg_h, leg_h, 3013,
			leg_h, b-leg_h, 13,
			0, b, 1015,
			0, 0, -1
		DEL 1
	ENDIF

RETURN

230:
	CYLIND leg_h, 0.03
RETURN
