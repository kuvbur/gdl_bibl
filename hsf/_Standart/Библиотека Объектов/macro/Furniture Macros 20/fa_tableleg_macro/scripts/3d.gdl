
if gs_detlevel_3D_m = 0 then end
bDetailed = (gs_detlevel_3D_m = 2)	! Detailed


pen gs_cont_pen

resLeg	= max(int(gs_resol / 4), 4)
resLeg	= 4 * ceil(resLeg / 4)

topDist = 0.03

resol resLeg

! =========================================================================
! Middle hotspot
! =========================================================================
hotspot A/2, B/2, 0,	unID : unID = unID + 1

! =========================================================================

if bLegGroup then
	if gs_leg_type_m = 1 or gs_leg_type_m = 2 then	! Rectangular, Round
		add legOffsetLeftX, legOffsetBottomY, 0
		gosub 100
		del 1
		add A - legOffsetRightX, legOffsetBottomY, 0
		gosub 100
		del 1
		add A - legOffsetRightX, B - legOffsetTopY, 0
		gosub 100
		del 1
		add legOffsetLeftX, B - legOffsetTopY, 0
		gosub 100
		del 1
	else
		add legOffsetLeftX, B/2, 0
		gosub 100
		del 1
		add A - legOffsetRightX, B/2, 0
		gosub 100
		del 1

		if gs_leg_type_m = 4 then
			offset = max(B/5, -gs_leg_frame_ovhg)
			material gs_leg_mat
			add legOffsetLeftX - gs_leg_frame_width/2, offset, ZZYZX - topDist
			block A - legOffsetLeftX - legOffsetRightX + gs_leg_frame_width, topDist, topDist
			del 1
			add legOffsetLeftX - gs_leg_frame_width/2, B - offset - topDist, ZZYZX - topDist
			block A - legOffsetLeftX - legOffsetRightX + gs_leg_frame_width, topDist, topDist
			del 1
		endif
	endif
else
	gosub 100
endif

end unID

! =========================================================================

100:

if gs_leg_type_m = 1 then	! Rectangular
	material gs_leg_mat
	add -gs_leg_width/2, -gs_leg_depth/2, bDetailed * gs_leg_base_height
	block gs_leg_width, gs_leg_depth, ZZYZX - bDetailed * gs_leg_base_height
	del 1
	if bDetailed and gs_leg_base_height > EPS then
		material gs_legbase_mat
		add -gs_leg_width/2-0.005, -gs_leg_depth/2-0.005, 0
		block gs_leg_width+0.01, gs_leg_depth+0.01, gs_leg_base_height
		del 1
	endif
endif

if gs_leg_type_m = 2 then	! Round
	material gs_leg_mat
	addz bDetailed * gs_leg_base_height
	prism_ 2,ZZYZX - bDetailed * gs_leg_base_height,
		0,0,979,
		gs_leg_diameter/2, 360,4079
	del 1
	if bDetailed and gs_leg_base_height > EPS then
		material gs_legbase_mat
		prism_ 2,gs_leg_base_height,
			0,0,979,
			gs_leg_diameter/2+0.005, 360,4079
	endif
endif

if gs_leg_type_m = 3 then	! Solid
	material gs_leg_mat
	prism_ 4,ZZYZX,
		-gs_leg_width/2,-gs_leg_depth/2,15,
		-gs_leg_width/2, gs_leg_depth/2,15,
		 gs_leg_width/2, gs_leg_depth/2,15,
		 gs_leg_width/2,-gs_leg_depth/2,15
endif

if gs_leg_type_m = 4 then	! Frame

	rollerR = 0.025

	if gs_leg_frame_roller then
		bottomH = 2*rollerR + bDetailed * (gs_leg_base_height + 0.01)
	else
		bottomH = bDetailed * gs_leg_base_height
	endif

	material gs_leg_mat
	rotx 90
	roty 90
	addz -gs_leg_frame_width/2
	prism_ 4,gs_leg_frame_width,
		-B/2 - gs_leg_frame_ovhg, ZZYZX - topDist, 10,
		-B/2 - gs_leg_frame_ovhg + gs_leg_frame_depth, ZZYZX - topDist - gs_leg_frame_depth, 15,
		 B/2 + gs_leg_frame_ovhg - gs_leg_frame_depth, ZZYZX - topDist - gs_leg_frame_depth, 10,
		 B/2 + gs_leg_frame_ovhg, ZZYZX - topDist, 15
	prism_ 4,gs_leg_frame_width,
		-B/2 - gs_leg_frame_ovhg, ZZYZX - topDist, 10,
		-B/2 - gs_leg_frame_ovhg + gs_leg_frame_depth, ZZYZX - topDist - gs_leg_frame_depth, 15,
		-B/2 - gs_leg_frame_ovhg + gs_leg_frame_depth, bottomH, 15,
		-B/2 - gs_leg_frame_ovhg, bottomH, 15
	prism_ 4,gs_leg_frame_width,
		 B/2 + gs_leg_frame_ovhg, ZZYZX - topDist, 10,
		 B/2 + gs_leg_frame_ovhg - gs_leg_frame_depth, ZZYZX - topDist - gs_leg_frame_depth, 15,
		 B/2 + gs_leg_frame_ovhg - gs_leg_frame_depth, bottomH, 15,
		 B/2 + gs_leg_frame_ovhg, bottomH, 15
	del 3

	material gs_legbase_mat
	if gs_leg_frame_roller then
		resol max(resLeg, 6)
		for i=1 to 2
			addy -B/2 + gs_leg_frame_depth/2 - gs_leg_frame_ovhg
			rotz (i=2)*180
			if bDetailed then
				add 0.01,0.02, rollerR
				roty 90
				prism_ 2,0.02,
					0,0,979,
					rollerR,360,4079
				addz -0.04
				prism_ 2,0.02,
					0,0,979,
					rollerR,360,4079
				del 3
				addz bottomH
				if gs_leg_base_height > EPS then
					prism 4,-gs_leg_base_height,
						-gs_leg_frame_width/2, -gs_leg_frame_depth/2,
						-gs_leg_frame_width/2,  gs_leg_frame_depth/2,
						 gs_leg_frame_width/2,  gs_leg_frame_depth/2,
						 gs_leg_frame_width/2, -gs_leg_frame_depth/2
				endif
				roty 90
				addz -0.01
				prism 4,0.02,
					gs_leg_base_height, -gs_leg_frame_depth/2,
					gs_leg_base_height,  gs_leg_frame_depth/2,
					gs_leg_base_height + 0.01 + (3/2)*rollerR,  0.02+rollerR/2,
					gs_leg_base_height + 0.01 + (3/2)*rollerR, -gs_leg_frame_depth/2
				del 3
			else
				add -0.025,0.02, rollerR
				roty 90
				prism_ 2,0.05,
					0,0,979,
					rollerR,360,4079
				del 2
			endif
			del 2
			muly -1
		next i
		del 2
	else
		if bDetailed and gs_leg_base_height > EPS then
			for i=1 to 2
				addy -B/2 + gs_leg_frame_depth/2 - gs_leg_frame_ovhg
				prism 4, gs_leg_base_height,
					-gs_leg_frame_width/2-0.005,-gs_leg_frame_depth/2-0.005,
					-gs_leg_frame_width/2-0.005, gs_leg_frame_depth/2+0.005,
					 gs_leg_frame_width/2+0.005, gs_leg_frame_depth/2+0.005,
					 gs_leg_frame_width/2+0.005,-gs_leg_frame_depth/2-0.005
				del 1
				muly -1
			next i
			del 2
		endif
	endif
endif

if gs_leg_type_m = 5 or gs_leg_type_m = 6 then	! T-Leg, Double T-Leg
	material gs_leg_mat

	addx -0.07/2
	rotz 90
	rotx 90

	prism_ 9,0.07,
		-gs_leg_base_depth/2,0,15,
		 gs_leg_base_depth/2,0,79,
		 gs_leg_base_depth/2-0.04,0,900,
		 0,90,4000,
		 gs_leg_base_depth/2-0.04,0.04,79,
		-gs_leg_base_depth/2+0.04,0.04,79,
		-gs_leg_base_depth/2+0.04,0,900,
		 0,90,4000,
		 gs_leg_base_depth/2,0,15
	del 1

	if gs_leg_type_m = 5 then
		add 0,-0.07/2,0.04
		cylind ZZYZX-0.09,0.025
		addz ZZYZX-0.09
		cylind 0.05,0.02
		del 2
	else
		add -2*0.025,-0.07/2,0.04
		cylind ZZYZX-0.09,0.025
		addz ZZYZX-0.09
		cylind 0.05,0.02
		del 1
		addx 4*0.025
		cylind ZZYZX-0.09,0.025
		addz ZZYZX-0.09
		cylind 0.05,0.02
		del 3
	endif

	del 2
endif

return
