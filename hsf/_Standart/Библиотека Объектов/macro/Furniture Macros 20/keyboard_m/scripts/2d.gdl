
kY = A / 24										!button size X
kX = (B-0.01) / 8.25									!button size y

pen gs_cont_pen
fill gs_fill_type
poly2_B 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, B, 1,
		A, B, 1,
		A, 0, 1,
		0, 0, 1,
		0, B, -1

ADD2 0.5 * kY, 0.25 * kX


! ---------------------------------------------------------------------------------
! letter pad
! ---------------------------------------------------------------------------------

rect2 0, 0, 15 * kY, 5 * kX

if gs_detlevel_2D_m = 2 then
	ADD2 kY, 4 * kX								! 1st row
	nButtons = 13
	gosub "oneRow"
	DEL 1

	line2 0, 4 * kX, 15 * kY, 4 * kX			! 2nd row

	ADD2 1.5 * kY, 3 * kX
	nButtons = 13
	gosub "oneRow"
	DEL 1

	line2 0, 3 * kX, 13.75 * kY, 3 * kX			! 3rd row

	ADD2 1.75 * kY, 2 * kX
	nButtons = 13
	gosub "oneRow"
	DEL 1

	line2 0, 2 * kX, 15 * kY, 2 * kX			! 4th row

	ADD2 1.25 * kY, kX
	nButtons = 12
	gosub "oneRow"
	DEL 1

	line2 0, kX, 15 * kY, kX					! 5th row

	ADD2 1.75 * kY, 0
	for i = 1 to 3
		line2 0, 0, 0 * kY, kX
		ADD2 1.125 * kY, 0
	next i
	DEL 4
	ADD2 9.5 * kY, 0
	for i = 1 to 4
		line2 0, 0, 0, kX
		ADD2 1.25 * kY, 0
	next i
	DEL 5
endif


! ---------------------------------------------------------------------------------
! positioning pad
! ---------------------------------------------------------------------------------

ADD2  15.5 * kY, 3 * kX
rect2 0, 0, 3 * kY, 2 * kX
if gs_detlevel_2D_m = 2 then
	for i = 1 to 2
		line2 kY, 0, kY, 2 * kX
		ADD2 kY, 0
	next i
	DEL 2
	line2 0, kX, 3 * kY, kX
endif


! ---------------------------------------------------------------------------------
! numpad
! ---------------------------------------------------------------------------------

ADD2 3.5 * kY, -3 * kx
rect2 0, 0, 4 * kY, 5 * kX
if gs_detlevel_2D_m = 2 then
	for i = 1 to 2
		line2 0, kX, 3 * kY, kX
		ADD2 0, 2 * kX
	next i
	DEL 2
	for i = 1 to 2
		line2 0, 2 * kX, 4 * kY, 2 *kX
		ADD2 0, 2 * kX
	next i
	DEL 2
	ADD2 kY, 0
	for i = 1 to 2
		line2 kY, 0, kY, 5 * kX
		ADD2 kY, 0
	next i
	DEL 3
	line2 kY, kX, kY, 5 * kX
endif


! ---------------------------------------------------------------------------------
! arrows
! ---------------------------------------------------------------------------------

ADD2 -3.5 * kY, 0
poly2_B 9, 1, gs_fill_pen, gs_back_pen,
		0, 0, 1,
		0, kX, 1,
		kY, kX, 1,
		kY, 2 * kX, 1,
		2 * kY, 2 * kX, 1,
		2 * kY, kX, 1,
		3 * kY, kX, 1,
		3 * kY, 0, 1,
		0, 0, -1

if gs_detlevel_2D_m = 2 then
	line2 kY, 0, kY, kX
	line2 2 * kY, 0, 2 * kY, kX
	line2 kY, kX, 2 * kY, kX
endif

DEL 4


! ---------------------------------------------------------------------------------
! upper row buttons
! ---------------------------------------------------------------------------------

ADD2 0.5 * kY, 5.75 * kX
rect2 0, 0, kY, kX
ADD2 2 * kY, 0
for i = 1 to 3
	rect2 0, 0, 4* kY, kX
	if gs_detlevel_2D_m = 2 then
		for j = 1 to 3
			line2 kY, 0, kY, kX
			ADD2 kY, 0
		next j
		DEL 3
	endif
	ADD2 4.5 * kY, 0
next i

rect2 0, 0, 3* kY, kX
if gs_detlevel_2D_m = 2 then
	for i = 1 to 2
		line2 kY, 0, kY, kX
		ADD2 kY, 0
	next i
	del 2
endif

del 5

end

"oneRow":
	for i = 1 to nButtons
		line2 0, 0, 0, kX
		ADD2 kY, 0
	next i
	del nButtons
return
