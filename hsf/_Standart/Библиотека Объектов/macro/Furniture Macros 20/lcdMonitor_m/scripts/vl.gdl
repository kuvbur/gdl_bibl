
call "FM_types" parameters all

values "monitorType" stMonitorType
values "iMonitorType" 1, 2
if GLOB_MODPAR_NAME = "monitorType" then
	if monitorType = stMonitorType[1]	then iMonitorType = 1
	if monitorType = stMonitorType[2]	then iMonitorType = 2
	parameters iMonitorType = iMonitorType
else
	if iMonitorType = 1 then	monitorType = stMonitorType[1]
	if iMonitorType = 2 then	monitorType = stMonitorType[2]
	parameters monitorType = monitorType
endif

values{2} "gs_detlevel_3D_m" 	2, `Детальный`,
								1, `Простой`,
								0, `Откл.`

if gs_detlevel_3D_m = 1 then hideparameter "gs_resol"
if gs_detlevel_3D_m = 0 then hideparameter "gs_resol", "gs_shadow"

values{2} "iScreenRatio"	SCREEN_43,		"4:3",
							SCREEN_169,		"16:9",
							SCREEN_239,		"2.39:1",
							SCREEN_CUST,	`Специальный`

values{2} "iScreenSize"		1, stScreenSize[1],
							2, stScreenSize[2],
							3, stScreenSize[3],
							4, stScreenSize[4],
							5, stScreenSize[5],
							6, stScreenSize[6],
							7, stScreenSize[7],
							8, stScreenSize[8]

values "screenHeight" 	range (0,)
values "screenWidth" 	range (0,)

if iScreenRatio <> SCREEN_CUST then

	screenWidth 	= screenSizeX
	screenHeight 	= screenSizeY

	parameters 	screenWidth 	= screenWidth,
				screenHeight 	= screenHeight

	lock 			"screenHeight", "screenWidth"
	hideparameter 	"screenHeight", "screenWidth"

	if iScreenSize <> 8 then
		screenSize = _screenSizeDiagonal
		parameters screenSize = screenSize

		lock 			"screenSize"
		hideparameter 	"screenSize"
	endif 
else
	iScreenSize = 8		! Custom
	screenSize 	= sqr(screenWidth ^ 2 + screenHeight ^ 2)

	parameters 	iScreenSize	= iScreenSize,
				screenSize 	= screenSize

	lock 			"iScreenSize", "screenSize"
	hideparameter 	"iScreenSize", "screenSize"
endif

if iMonitorType = 1 then
	lock 			"displayAng", "screenMat", "frameMat", "standMat"
	hideparameter 	"displayAng", "screenMat", "frameMat", "standMat"

	values "gs_resol" RANGE [3,)

	IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=10
	IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=91

	frameThickness = 0.015
	bottomFrameThickness = 0.015
	tp = zzyzx/7

	values "screenSize"	range (0.13 / cos(_alpha), )
	values "B" 			range (0.09, )

	A 		= screenSizeX + 2 * frameThickness
	ZZYZX	= screenSizeY + frameThickness + bottomFrameThickness + tp

	parameters 	A = A,
				ZZYZX = ZZYZX

	lock "A", "ZZYZX"

endif

if iMonitorType = 2 then
	lock 			"box_mat", "feetMat", "dispmat"
	hideparameter	"box_mat", "feetMat", "dispmat"

	A 		= displayWidth
	B 		= standRadBottom * 2
	ZZYZX	= displayHeight-standCutRad+backPanelThk+0.01+standHeight

	parameters 	A = A,
				B = B,
				ZZYZX = ZZYZX

	lock "A", "B", "ZZYZX"

	values "gs_resol" 		range [3,)
	values "screenSize" 	range (0,)
endif

if pic then
	lock "screenMat", "dispmat"
	hideparameter "screenMat", "dispmat"
else
	lock "gs_picture_custom", "gs_picture_name"
	hideparameter "gs_picture_custom", "gs_picture_name"
endif
if not(gs_picture_custom) then
	lock "gs_picture_name"
	hideparameter "gs_picture_name"
endif
