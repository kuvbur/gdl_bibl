
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 					=-1
_idxTab = 1
TABID_StyleAndDimensions	= _idxTab  : _idxTab = _idxTab + 1
TABID_Knob					= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace			= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions			= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Офисного Шкафа`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_Knob, TABID_StyleAndDimensions, `Ручка`, "uiTab_Knob_flat(1)"
	gosub "Knobpage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalSpacepage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Drawer_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37
	xPict = 163
	yPict = 114
	PictWidth = 65 
	PictHeight = 133
	x1 = 0
	x2 = 124
	x3 = 179
	x4 = 231
	x5 = 390
	x6 = xPict + PictWidth + 3
	dy = 24
	_iFlag = 0

	_yCurr = pageStartY
	ui_infield{3} "gs_bevel", x4, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Фаска Торца`, 0,
					"", `Фаска Торца`, 1

	ui_infield{3} "cabinetScheme", x2, _yCurr, 90, 68,
		2, 1, 5, 1,
		77, 64, 77, 62,
		1, stCabinetScheme[1],	stCabinetScheme[1],
		2, stCabinetScheme[2],	stCabinetScheme[2],
		3, stCabinetScheme[3],	stCabinetScheme[3],
		4, stCabinetScheme[4],	stCabinetScheme[4],
		5, stCabinetScheme[5],	stCabinetScheme[5]

	_yCurr = _yCurr + _yOffset
	ui_outfield `Стиль`, x1, _yCurr, 120, _outfHeight

	_yCurr = _yCurr - _yOffset + dy
	ui_infield{3} "bCabinetBase", x4, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Основание`, 0,
					"", `Основание`, 1

	if iCabinetScheme = 1 or iCabinetScheme = 4 then
		_yCurr = _yCurr + dy + _yOffset
		ui_outfield `Кол-во Дверей`, x4, _yCurr, 150, _outfHeight

		_yCurr = _yCurr - _yOffset
		ui_infield{3} "iCupboardDoors", x5, _yCurr, 53, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", `1`, 1,
				"", `2`, 2 ui_tooltip `Двери`
	else
		if iCabinetScheme = 2 or iCabinetScheme = 5 then
			_yCurr = _yCurr + dy
			ui_infield{3} "bShelves", x4, _yCurr, 150, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Полки`, 0,
					"", `Полки`, 1
		ui_infield "shelfNum", x5, _yCurr, 53, _infHeight ui_tooltip `Кол-во Полок`

		endif
	endif

	if iCabinetScheme <> 4 and iCabinetScheme <> 5 then
		_yCurr = _yCurr + dy + _yOffset
		ui_outfield `Количество Ящиков`, x4, _yCurr, 150, _outfHeight

		_yCurr = _yCurr - _yOffset
		ui_infield "drawerNum", x5, _yCurr, 53, _infHeight ui_tooltip `Количество Ящиков`
		
	endif

	if iCabinetScheme <> 1 and iCabinetScheme <> 2 then _yCurr = _yCurr + dy

	ui_pict_button ui_function, "", "uiFunctionDrawerKnob_flat(1)", x3, _yCurr-1, widthInf2, 22, -TABID_Knob ! Set GLOB_UI_BUTTON_ID

	if iCabinetScheme = 5 then _iFlag = 4

	_yCurr = _yCurr + _yOffset
	ui_outfield `Ручка`, x1, _yCurr, 210 - widthInf2, _outfHeight, _iFlag

	_yCurr = _yCurr - _yOffset + dy
	ui_separator x1, _yCurr, 444, _yCurr

	if iCabinetScheme = 1 or iCabinetScheme = 2 then
		_yCurr = yPict + 11
		ui_infield "gs_upperCabHeight", x6, _yCurr, 90, _infHeight ui_tooltip `Высота Верхнего Шкафа`
	
		_yCurr = yPict + 86
		ui_infield "gs_drawerHeight", x6, _yCurr, 90, _infHeight ui_tooltip `Высота Ящиков`
	endif

	_yCurr = yPict + 119
	ui_infield "cabinetBaseHeight", x6, _yCurr, 90, _infHeight ui_tooltip `Высота Основания`

	if iCabinetScheme = 1 then
		ui_pict 3, xPict, yPict, PictWidth, PictHeight, 1
	endif

	if iCabinetScheme = 2 then
		ui_pict 4, xPict, yPict, PictWidth, PictHeight, 1
	endif

	if iCabinetScheme = 3 then
		ui_pict 5, xPict, yPict, PictWidth, PictHeight, 1
	endif

	if iCabinetScheme = 4 then
		ui_pict 6, xPict, yPict, PictWidth, PictHeight, 1
	endif

	if iCabinetScheme = 5 then
		ui_pict 7, xPict, yPict, PictWidth, PictHeight, 1
	endif
return

! ------------------------------------------------------------------------------
"Knobpage":
! ------------------------------------------------------------------------------
	call "ui_cabinetKnob" parameters all	iKnobClass = 1,	! KNOBCLASS_DOOR_DRAWER
											iItemClass = 1	! ITEMCLASS_OFFICE_CABINET
return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	if iCabinetScheme = 5 then 								! Shelves
		dim stSymbolTypes[3]
			stSymbolTypes[1] = `Стиль 1`
			stSymbolTypes[2] = `Стиль 2`
			stSymbolTypes[3] = `Стиль 3`
	else
		if iCabinetScheme = 2 or iCabinetScheme = 3 then 	! Drawers + Shelves / Drawers
			dim stSymbolTypes[6]
				stSymbolTypes[1] = `Стиль 1`
				stSymbolTypes[2] = `Стиль 2`
				stSymbolTypes[3] = `Стиль 3`
				stSymbolTypes[4] = ``
				stSymbolTypes[5] = ``
				stSymbolTypes[6] = `Стиль 4`
		else 												! Drawers + Cupboard / Cupboard
			dim stSymbolTypes[9]
			stSymbolTypes[1] = `Стиль 1`
			stSymbolTypes[2] = `Стиль 2`
			stSymbolTypes[3] = `Стиль 3`
			stSymbolTypes[4] = `Стиль 4`
			stSymbolTypes[5] = `Стиль 5`
			stSymbolTypes[6] = `Стиль 6`
			stSymbolTypes[7] = `Стиль 7`
			stSymbolTypes[8] = `Стиль 8`
			stSymbolTypes[9] = `Стиль 9`
		endif
	endif
	
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 13,
													stSymbolTypes 		= stSymbolTypes,
													iSymbolStyle 		= iSymbolStyle,
													bOpeningAngle		= 0,
													iFillListID			= TABID_2DRepresentation
return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
 	SurfItemID = 1   
		if bCabinetBase then
			surfacesArray[SurfItemID]	= "toe_mat"
			SurfItemID = SurfItemID + 1
		endif
		surfacesArray[SurfItemID]	= "gs_cabinet_mat"
		SurfItemID = SurfItemID + 1
		if iCabinetScheme = DRAWERS_CUPBOARD or iCabinetScheme = CUPBOARD then
			surfacesArray[SurfItemID]	= "gs_upper_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bKnob and (iCabinetScheme = DRAWERS_CUPBOARD or iCabinetScheme = CUPBOARD) then
			surfacesArray[SurfItemID]	= "gs_knob_mat"
			SurfItemID = SurfItemID + 1
		endif
		if iCabinetScheme <> CUPBOARD and iCabinetScheme <> SHELVES then
			surfacesArray[SurfItemID]	= "gs_drawer_mat"
			SurfItemID = SurfItemID + 1
		endif

		if iCabinetScheme = DRAWERS_SHELVES or iCabinetScheme = SHELVES then
			surfacesArray[SurfItemID]	= "matShelf"
			SurfItemID = SurfItemID + 1
		endif

		if bKnob and (iCabinetScheme = DRAWERS_SHELVES or iCabinetScheme = DRAWERS_CUPBOARD) then
			surfacesArray[SurfItemID]	= "matDrawerKnob"
			SurfItemID = SurfItemID + 1
		endif
	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3DRepresentation
return

! ------------------------------------------------------------------------------
"MinimalSpacepage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	xPict = 142
	yPict = 61
	PictWidth = 141 
	PictHeight = 144
	x1 = xPict + 29
	x2 = xPict + PictWidth + 3

	ui_pict 8, xPict, yPict, PictWidth, PictHeight, 1
	
	_yCurr = yPict - _infHeight - 3
	ui_infield "A", x1, _yCurr, 90, _infHeight ui_tooltip `Размер 1`

	_yCurr = yPict + 23
	ui_infield "B", x2, _yCurr, 90, _infHeight ui_tooltip `Размер 2`

	_yCurr = _yCurr + 65
	ui_infield "MSFront", x2, _yCurr, 90, _infHeight ui_tooltip `Минимальное Пространство Спереди`
return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return
