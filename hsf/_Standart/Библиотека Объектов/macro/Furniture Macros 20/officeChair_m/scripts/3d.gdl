
if ABS(officeChair_unID - 1) < EPS then
	unID = 1000
else
	unID = officeChair_unID
endif

if gs_detlevel_3D_m = 0 then end

if NOT(gs_shadow) then shadow off

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
	iResol = 6
else
	iResol = gs_resol
endif

resol iResol

pen gs_cont_pen

! ======================================================================
! MUL Definition
! ======================================================================

if NOT(bPartOfLayout) or (bPartOfLayout and bEnableSeatHeightHsEdit) then
	HOTSPOT A/2,B*0.2,0,UnID,"gs_seat_height",1+128 :UnID = UnID+1
	HOTSPOT A/2,B*0.2,-0.1,UnID,"gs_seat_height",3 :UnID = UnID+1
	HOTSPOT A/2,B*0.2,gs_seat_height,UnID,"gs_seat_height",2 :UnID = UnID+1
endif

if NOT(bPartOfLayout) then
	HOTSPOT A/2,B*0.85,0,UnID,"zzyzx",1 :UnID = UnID+1
	HOTSPOT A/2,B*0.85,-0.1,UnID,"zzyzx",3 :UnID = UnID+1
	HOTSPOT A/2,B*0.85,zzyzx,UnID,"zzyzx",2 :UnID = UnID+1
endif

mul a/0.6160925782939,b/0.5916612803969, 1
add 0.3080462891469, 0.3616612803969, 0

if bPartOfLayout and (GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40) then

	! ======================================================================
	! Model for 3D Feedback
	! ======================================================================

	addy -0.0897
		gosub "modelFor3DFeedback"
	del 1

	del 2

else

	IF gs_headrest THEN
		IF gs_back_type_m = 1 THEN
			backHeight = 0.50
		ELSE
			backHeight = 0.60
		ENDIF

		IF gs_back_type_m = 1 THEN ADD 0, 0.165, gs_seat_height + backHeight
		IF gs_back_type_m = 2 THEN ADD 0, 0.165, gs_seat_height + backHeight
		IF gs_back_type_m = 3 THEN ADD 0, 0.18, gs_seat_height + backHeight
		gosub 30
		DEL 1
	ENDIF

	ADD 0,0, gs_seat_height-0.055
	ROTx -2

	ADD 0,-0.35,-0.03
	CALL "fa_OfficechairSeat" PARAMETERS 	A = 0.46, B = 0.44,
											mat = gs_seat_mat,
											mat2 = gs_frame_mat,
											bDetailed = bDetailed,
											gs_cont_pen = gs_cont_pen
	DEL 2

	IF gs_back_type_m = 1 THEN
		ADD 0, 0.105, 0.08
		CALL "fa_officechair_back_02" PARAMETERS 	A = 0.44,
												B = 0.09,
												zzyzx = 0.475,
												back_mat = gs_back_mat2,
												front_mat = gs_back_mat,
												gs_detlevel_3D_m = gs_detlevel_3D_m,
												gs_cont_pen = gs_cont_pen
		DEL 1
	ENDIF

	IF gs_back_type_m = 2 THEN
		ADD 0, 0.165, 0.06
		CALL "fa_officechair_back_03" PARAMETERS 	A = 0.45,
												B = 0.14,
												zzyzx = 0.595,
												back_mat = gs_back_mat2,
												front_mat = gs_back_mat,
												gs_detlevel_3D_m = gs_detlevel_3D_m,
												gs_cont_pen = gs_cont_pen
		DEL 1
	ENDIF

	IF gs_back_type_m = 3 THEN
		ADD 0, 0.07, -0.01
			backwidth = 0.42
			backheight = 0.665
			backdepth = 0.11

			GOSUB 100
		DEL 1
	ENDIF

	resol gs_resol

	! ======================================================================
	! OfficeRoll LEG
	! ======================================================================
	GOSUB 10:

	! ======================================================================
	! OfficeRoll ARM
	! ======================================================================
	IF gs_Armrest THEN
		GOSUB 20:
		MULx -1
		GOSUB 20:
		DEL 1
	ENDIF

	DEL 3

endif

! ======================================================================
officeChair_unID = unID
end officeChair_unID
! ======================================================================


! ======================================================================
! OfficeRoll LEG
! ======================================================================
10:
	MATERIAL gs_frame_mat
	ADD 0,-0.09,-0.02

	PRISM_ 8,0.03,
		-0.1,-0.17,15,
		0.1,-0.17,15,
		0.1,0.1,15,
		0.05,0.1,15,
		0.05,0.2,15,
		-0.05,0.2,15,
		-0.05,0.1,15,
		-0.1,0.1,15

	IF gs_back_type_m < 3 THEN
		ADD -0.05, 0.20, 0
		BLOCK 0.10, 0.03, 0.12
		DEL 1
	ENDIF

	ADDz -0.05
	RULED 4,1+2+16+32,
		-0.06,0.1,1,
		-0.06,-0.1,1,
		 0.06,-0.1,1,
		 0.06,0.1,1,

		-0.1, 0.1,0.05,
		-0.1,-0.17,0.05,
		 0.1,-0.17,0.05,
		 0.1, 0.1,0.05
	DEL 1

	if gs_detlevel_3D_m = 2 then
		ADD -0.06,-0.04,-0.04
		ROTz 180
		ROTy 3
		MATERIAL gs_frame_mat
		PRISM_ 4, 0.005,
			0,0,15,
			0.15,0,15,
			0.15,0.01,15,
			0,0.01,15

		MATERIAL gs_frame_mat
		PRISM_ 4, 0.005,
			0.15,-0.005,15,
			0.15,0.015,15,
			0.20,0.03,15,
			0.20,-0.02,15

		DEL 3
	endif

	ADDz -0.05
	rollheight=(gs_seat_height-0.305)/2
	ROTy 90
	MATERIAL gs_legCylind_mat
	REVOLVE 6,360,1+2+16+32,
		0,0.05,1,
		0.08,0.05,1,
		0.08,0.04,1,
		0.08+rollheight,0.04,1,
		0.08+rollheight,0.03,1,
		0.08+rollheight*2+0.02,0.03,1
	DEL 2

	ADDz -0.05
	ROTz 90
	ADDz -(gs_seat_height-0.15-0.158)
	ROTy 90

	ADDx 0.08
	ROTz 90
	Rollrot = rnd(360)
	FOR t=1 to 5
		MATERIAL gs_legStar_mat
		ROTy 72*(t-1)
		RULED 4,4+16,
			-0.025,0,1,
			-0.025,0.04,1,
			0.025,0.04,1,
			0.025,0,1,

			-0.015,-0.05,0.28,
			-0.015,-0.01,0.28,
			0.015,-0.01,0.28,
			0.015,-0.05,0.28

		ADD 0,-0.05,0.28
		ROTz 90
		REVOLVE 2,180,1+2,
			0,0.015,1,
			0.04,0.015,1

		REVOLVE 2,360,1+2,
			0,0.008,1,
			-0.04,0.008,1
		DEL 2

		ADD 0,-0.078,0.28
		ROTy Rollrot-(72*(t-1))
		ADDz -0.015
		MATERIAL rollmat
		REVOLVE 4,360,1+2,
			-0.015,0.022,1,
			-0.012,0.025,1,
			0.012,0.025,1,
			0.015,0.022,1
		DEL 3
		DEL 1
	NEXT t
	DEL 7
RETURN

! ======================================================================
! OfficeRoll ARM
! ======================================================================
20:
	armstart = 0.1
	armwidth = 0.245
	armradius = 0.03
	armheight = 0.3
	armdepth = 0.015

	armdx = max(0.02,armdepth+0.01)
	armdy = 0.045
	armdx1 = 0.04
	armdy1 = 0.15
	armdz1 = 0.03

	if gs_detlevel_3D_m = 2 then
		armresol = 16
	else
		armresol = 8
	endif

	ADD 0,-0.075,-0.02
	ROTx 90
	MATERIAL gs_frame_mat
	PRISM_ 9,0.06,
		armstart,0,15,
		armwidth-armradius,0,79,
		armwidth-armradius,armradius,979,
		armwidth,armradius,1079,
		armwidth,max(armheight-0.2,armradius+0.001),15,
		armwidth-armdepth,max(armheight-0.2,armradius+0.001),15,
		armwidth-armdepth,armradius,79,
		armwidth-armradius,armdepth,1079,
		armstart,armdepth,15

	DEL 1
	ADD armwidth-armdepth/2,-0.03,armheight-0.2
	FOR i=1 to armresol
		alfa=i*(360/armresol)
		PUT cos(alfa)*armdx,sin(alfa)*armdy,1
	NEXT i
	FOR t=1 to armresol
		alfa=t*(360/armresol)
		PUT cos(alfa)*armdx1/2,sin(alfa)*armdy1/2,0.15
	NEXT t

	MATERIAL gs_arm_mat
	RULED armresol,1+4+16,
		GET (nsp)

	ADDz 0.15
	ROTx 5
	MUL armdx1/1,armdy1/1,armdz1/1
		SPHERE 1
	DEL 2
	DEL 1

	DEL 2
RETURN

! ======================================================================
! Headrest
! ======================================================================
30:
	material gs_headrest_mat
	arcDepth = 0.012
	rrF = 0.02
	rrB = 0.01

	num = 10
	ang = 180-2*atn((headrestWidth/2)/arcDepth)
	rr = (headrestWidth/2)/sin(ang)
	alpha_i = (2*ang)/num

	alpha = -ang-alpha_i
	for i = 1 to num+3
		put rr*sin(alpha), -rr+rr*cos(alpha), 0, 0
		alpha = alpha + alpha_i
	next i

	if gs_detlevel_3D_m = 2 then
		tube 12, NSP/4, 0,
			-rrB, 0, 1,
			-headrestDepth+rrF, 0, 1,
			-headrestDepth+rrF-rrF*sin(45), rrF-rrF*sin(45), 1,
			-headrestDepth, rrF, 1,
			-headrestDepth, headrestHeight-rrF, 1,
			-headrestDepth+rrF-rrF*sin(45), headrestHeight-rrF+rrF*sin(45), 1,
			-headrestDepth+rrF, headrestHeight, 1,
			-rrB, headrestHeight, 1,
			-rrB+rrB*sin(45), headrestHeight-rrB+rrB*sin(45), 1,
			0, headrestHeight-rrB, 1,
			0, rrB, 1,
			-rrB+rrB*sin(45), rrB-rrB*sin(45), 1,

			get(NSP)
	else
		tube 4, NSP/4, 16+32,
			0, 0, 1,
			-headrestDepth, 0, 1,
			-headrestDepth, headrestHeight, 1,
			0, headrestHeight, 1,

			get(NSP)
	endif


	resol 32
	headrestDepth = headrestDepth/cos(alpha_i/2)

	for i = -1 to 1 step 2
		mulx i
		add -headrestWidth/2, -arcDepth, headrestHeight/2
		rotz -90+ang
		rotx 90
		mulz 0.5
		if gs_detlevel_3D_m = 2 then
			put 0, 0, 1
			put 0, headrestHeight/2-rrB, 1
			put (rrB-rrB*sin(45))/cos(alpha_i/2), headrestHeight/2-rrB+rrB*sin(45), 1
			put rrB/cos(alpha_i/2), headrestHeight/2, 1
			put headrestDepth-rrF/cos(alpha_i/2), headrestHeight/2, 1
			put headrestDepth-(rrF-rrF*sin(45))/cos(alpha_i/2), headrestHeight/2-rrF+rrF*sin(45), 1
			put headrestDepth, headrestHeight/2-rrF, 1
			put headrestDepth, 0, 1
		else
			resol 8
			put 0, 0, 1
			put 0, headrestHeight/2, 1
			put headrestDepth, headrestHeight/2, 1
			put headrestDepth, 0, 1
		endif

		revolve NSP/3, 180, 1+2,
			get(NSP)

		del 5
	next i
RETURN

! ======================================================================
! Backrest 03
! ======================================================================
100:
	mul 1, backdepth/0.1102012964569, backheight/0.6445534812206
	ADDz 0.1103579410231
	ROTz 90

	backdepth = 0.04
	if gs_detlevel_3D_m = 2 then
		resol min(gs_resol,32)
	else
		resol 12
	endif

	FOR i=1 to 2
		MULy -1
		material gs_back_mat

		if gs_detlevel_3D_m = 2 then
			put 0.0245593906768, 0, -0.1437961649344, 0
			put 0.01510989343449, 0, -0.09383466516744, 0
			put 0.007653135991759, 0, -0.05440920414196, 0
			put 0.001787825202898, 0, -0.0148407880208, 0
			put -0.002225070712195, 0, 0.02182895640646, 0
			put -0.004560305032575, 0, 0.05279013540052, 0
			put -0.005089775774214, 0, 0.0798911173429, 0
			put -0.003538637382029, 0, 0.1089770048216, 0
			put 0.0001283610840427, 0, 0.1406309372751, 0
			put 0.01121282898356, 0, 0.1943304912331, 0
			put 0.02408423328828, 0, 0.2411567715138, 0
			put 0.03563833359022, 0, 0.2963188347289, 0
			put 0.04337523021511, 0, 0.3529064573565, 0
			put 0.04729011679692, 0, 0.4125306140087, 0
			put 0.0523441870116, 0, 0.4636983617117, 0
			put 0.05663369878502, 0, 0.4921310683932, 0
			put 0.06140675873532, 0, 0.5176722643419, 0
			put 0.0651421787899, 0, 0.5376609291728, 0
		else
			put 0.0245593906768, 0, -0.1437961649344, 0
			put 0.01510989343449, 0, -0.09383466516744, 0
			put 0.007653135991759, 0, -0.05440920414196, 0
			put -0.004560305032575, 0, 0.05279013540052, 0
			put 0.0001283610840427, 0, 0.1406309372751, 0
			put 0.01121282898356, 0, 0.1943304912331, 0
			put 0.04337523021511, 0, 0.3529064573565, 0
			put 0.05663369878502, 0, 0.4921310683932, 0
			put 0.06140675873532, 0, 0.5176722643419, 0
			put 0.0651421787899, 0, 0.5376609291728, 0
		endif

		TUBE 7,NSP/4,1+2,
			-0.02,0.001,1,
			0,0,1,
			backwidth/2-backdepth/2,0,1,
			backwidth/2-backdepth/2,backdepth/2,901,
			backdepth/2,180,4001,
			0,backdepth,1,
			-0.02,backdepth-0.001,1,

			use(NSP)

		material gs_back_mat2
		TUBE 5,NSP/4,1+2+16+32,
			0,backdepth-0.005,1,
			backwidth/2,backdepth-0.005,1,
			backwidth/2,backdepth+0.005,1,
			0,backdepth+0.005,1,
			-0.02,backdepth+0.004,1,

			get(NSP)

	NEXT i
	DEL 2

	ADD 0.06140675873532, 0, 0.5176722643419
	ROTz 90
	ROTx 10
	ADDy -backdepth/2
	ROTx -10
	material gs_back_mat
	REVOLVE 5,200, 1+2+16+32,
		-backwidth/2,0,1,
		-backwidth/2+backdepth/2,0,901,
		0, -90,4001,
		backwidth/2-backdepth/2,backdepth/2,1,
		backwidth/2,0,1001
	DEL 5

	ADD 0.01510989343449, 0, -0.09383466516744
	ROTz -90
	ROTx 10
	ADDy backdepth/2
	ROTx -190

	REVOLVE 5,200, 1+2+16+32,
		-backwidth/2,0,1,
		-backwidth/2+backdepth/2,0,901,
		0, -90,4001,
		 backwidth/2-backdepth/2,backdepth/2,1,
		 backwidth/2,0,1001

	DEL 8
RETURN

! ======================================================================
! Model for 3D Feedback
! ======================================================================
"modelFor3DFeedback":

	material gs_frame_mat

	for i = 1 to 5
		prism_ 4, 0.04,
			-0.025,       0.2565,      15,
			0.025,       0.2565,      15,
			0.025,       0.3065,      15,
			-0.025,       0.3065,      15

		addz 0.04
		prism_ 5, 0.04,
			0, 0, 8,
			0.02, 0.02 * TAN(90 - 360 / 10), 15,
			0.02, 0.3015, 15,
			-0.02, 0.3015, 15,
			-0.02, 0.02 * TAN(90 - 360 / 10), 10
		del 1

		rotz 360 / 5
	next i
	del 5

	material gs_frame_mat

	addz 0.04 * 2
	prism_ 4, gs_seat_height - 0.05 - 0.04 * 2,
		-0.03, -0.03, 15,
		0.03, -0.03, 15,
		0.03, 0.03, 15,
		-0.03, 0.03, 15
	del 1

	material gs_seat_mat

	addz gs_seat_height - 0.05
	prism_ 4, 0.05,
		-0.2315,      -0.2429,      15,
		0.2315,      -0.2429,      15,
		0.2315,       0.1921,      15,
		-0.2315,       0.1921,      15
	del 1

	material gs_frame_mat
	addz gs_seat_height - 0.05 - 0.02
	prism_ 4, 0.02,
		-0.2315 + 0.04,      -0.2429 + 0.04,      15,
		0.2315 - 0.04,      -0.2429 + 0.04,      15,
		0.2315 - 0.04,       0.1921 - 0.04,      15,
		-0.2315 + 0.04,       0.1921 - 0.04,      15
	del 1

	add -0.05 / 2, 0.03, gs_seat_height - 0.05 - 0.025 - 0.02
	roty 90
	prism_ 6, 0.05,
		-0.025, 0, 15,
		0, 0, 15,
		0, 0.2, 15,
		-0.145, 0.2, 15,
		-0.145, 0.175, 15,
		-0.025, 0.175, 15
	del 2

	if gs_armrest then
		material gs_arm_mat

		for i = 1 to 2
			add -0.2315 + 0.04, 0.03, gs_seat_height - 0.05 - 0.02
			rotx 90
			prism_ 6, 0.06,
				-0.055, 0, 15,
				0, 0, 15,
				0, 0.02, 15,
				-0.04, 0.02, 15,
				-0.04, 0.12, 15,
				-0.055, 0.12, 15
			del 2

			addz gs_seat_height	- 0.05 + 0.1
			prism_ 4, 0.1,
				0.214, -0.045, 15,
				0.214, 0.045, 15,
				0.264, 0.045, 15,
				0.264, -0.045, 15
			del 1

			addz gs_seat_height	- 0.05 + 0.1 + 0.1
			prism_ 4, 0.0576,
				0.199, 0.129, 15,
				0.199, -0.159, 15,
				0.279, -0.159, 15,
				0.279, 0.129, 15
			del 1

			mulx -1
		next i
		del 2
	endif

	add -0.44 / 2, 0.03, gs_seat_height - 0.05 - 0.025 - 0.02
	roty 90

	if gs_back_type_m = 1 then
		material gs_back_mat
		prism_ 4, 0.44,
			-0.125, 0.1375, 15,
			-0.125, 0.1675, 15,
			-0.595, 0.1675, 15,
			-0.595, 0.1375, 15

		addz 0.02
		material gs_back_mat2
		prism_ 4, 0.44 - 0.02 * 2,
			-0.145, 0.1675, 15,
			-0.145, 0.2075, 15,
			-0.575, 0.2075, 15,
			-0.575, 0.1675, 15
		del 1

		if gs_headrest then
			material gs_seat_mat
			addz 0.44 / 2 - headrestWidth / 2
			prism_ 4, headrestWidth,
				-0.595, 0.1675, 15,
				-0.595, 0.1375, 15,
				-0.595 - headrestHeight, 0.1375, 15,
				-0.595 - headrestHeight, 0.1675, 15
			del 1
		endif
	endif

	if gs_back_type_m = 2 then
		material gs_back_mat
		prism_ 6, 0.44,
			-0.315, 0.1105746451448, 13,
			-0.695, 0.1707607324682, 15,
			-0.695, 0.2007607324682, 15,
			-0.315, 0.1405746451448, 13,
			-0.135, 0.1706676888065, 15,
			-0.135, 0.1406676888065, 15

		addz 0.02
		material gs_back_mat2
		prism_ 6, 0.44 - 0.02 * 2,
			-0.315, 0.1405746451448, 13,
			-0.145, 0.1675, 15,
			-0.145, 0.2075, 15,
			-0.315, 0.1805746451448, 13,
			-0.675, 0.2375930436617, 15,
			-0.675, 0.1975930436617, 15
		del 1

		if gs_headrest then
			material gs_seat_mat
			addz 0.44 / 2 - headrestWidth / 2
			prism_ 4, headrestWidth,
				-0.695, 0.2007607324682, 15,
				-0.695, 0.1707607324682, 15,
				-0.695 - headrestHeight, 0.1707607324682, 15,
				-0.695 - headrestHeight, 0.2007607324682, 15
			del 1
		endif
	endif

	if gs_back_type_m = 3 then
		material gs_back_mat
		prism_ 6, 0.44,
			-0.125, 0.1375, 15,
			-0.125, 0.1675, 15,
			-0.315, 0.1675, 13,
			-0.695, 0.2141581331431, 15,
			-0.695, 0.1841581331431, 15,
			-0.315, 0.1375, 13

		addz 0.02
		material gs_back_mat2
		prism_ 6, 0.44 - 0.02 * 2,
			-0.145, 0.1675, 15,
			-0.145, 0.2075, 15,
			-0.315, 0.2075, 13,
			-0.675, 0.251702441925, 15,
			-0.675, 0.211702441925, 15,
			-0.315, 0.1675, 13
		del 1

		if gs_headrest then
			material gs_seat_mat
			addz 0.44 / 2 - headrestWidth / 2
			prism_ 4, headrestWidth,
				-0.695, 0.2141581331431, 15,
				-0.695, 0.1841581331431, 15,
				-0.695 - headrestHeight, 0.1841581331431, 15,
				-0.695 - headrestHeight, 0.2141581331431, 15
			del 1
		endif
	endif

	del 2

return
