
call "FM_types" parameters all

values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
values "gs_detlevel_3D_m" 2, 1, 0

if NOT(bPartOfLayout) then
	values "gs_resol" range [3, )

	call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
								gs_detlevel_2D_m	= gs_detlevel_2D_m,
								enable2dDetLevels	= 2 + 4 + 8 + 16 + 32
endif

if gs_detlevel_2D_m <> 4 then
	HIDEPARAMETER "gs_SymbolType"
	lock "gs_SymbolType", "gs_SymbolType_m"
endif

call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

if bEnableFullSurfaceEdit then
	if NOT(gs_armrest) then lock "gs_arm_mat"

	if gs_headrest then
		headrestHeight = 0.14
	else
		headrestHeight = 0
		lock"gs_headrest_mat"
	endif
endif

if gs_back_type_m = 1 then
	backHeight = 0.50 + headrestHeight
else
	backHeight = 0.60 + headrestHeight
endif

if NOT(bPartOfLayout) then

	HIDEPARAMETER ALL "A", "B", "zzyzx"

	values "a" range [0.3, )
	values "b" range [0.3, )

	if gs_cont_pen=0 then parameters gs_cont_pen=4
	if gs_fill_pen=0 then parameters gs_fill_pen=19

	if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
		gs_detlevel_3D_m = 0
		if gs_detlevel_3D = `Простой` then parameters gs_detlevel_3D_m = 1
		if gs_detlevel_3D = `Детальный` then parameters gs_detlevel_3D_m = 2
	else
		gs_detlevel_3D = `Откл.`
		if gs_detlevel_3D_m = 1 then parameters gs_detlevel_3D = `Простой`
		if gs_detlevel_3D_m = 2 then parameters gs_detlevel_3D = `Детальный`
	endif

	if gs_detlevel_3D_m = 1 then
		lock"gs_resol"
		hideparameter "gs_resol"
	endif
	if gs_detlevel_3D_m = 0 then
		lock"gs_resol", "gs_shadow"
		hideparameter "gs_resol", "gs_shadow"
	endif

	values "ZZYZX" range[0.40 + backHeight,]

	if GLOB_MODPAR_NAME = "gs_seat_height" then
		if gs_seat_height < 0.40 then
			gs_seat_height = 0.40
			parameters gs_seat_height = gs_seat_height
		endif
		ZZYZX = gs_seat_height + backHeight
		parameters ZZYZX = ZZYZX
	else
		if GLOB_MODPAR_NAME = "gs_back_type" OR GLOB_MODPAR_NAME = "gs_back_type_m" OR GLOB_MODPAR_NAME = "gs_headrest" then
			ZZYZX = gs_seat_height + backHeight
			parameters ZZYZX = ZZYZX
		else
			gs_seat_height = ZZYZX - backHeight
			parameters gs_seat_height = gs_seat_height
		endif
	endif


	! ======================================================================
	! Minimal Space
	! ======================================================================

	values "MSFront" range [0,)
	values "MSSide" range [0,)
	values "MSRear" range [0,)
else
	values "chairHeight" range[0.40 + backHeight,]

	if GLOB_MODPAR_NAME = "gs_seat_height" then
		if gs_seat_height < 0.40 then
			gs_seat_height = 0.40
			parameters gs_seat_height = gs_seat_height
		endif
		chairHeight = gs_seat_height + backHeight
		parameters chairHeight = chairHeight
	else
		if GLOB_MODPAR_NAME = "gs_back_type" OR GLOB_MODPAR_NAME = "gs_back_type_m" OR GLOB_MODPAR_NAME = "gs_headrest" then
			chairHeight = gs_seat_height + backHeight
			parameters chairHeight = chairHeight
		else
			gs_seat_height = chairHeight - backHeight
			parameters gs_seat_height = gs_seat_height
		endif
	endif

	parameters AC_show2DHotspotsIn3D = 0
endif

values "gs_back_type" stBackType
values "gs_back_type_m" 1, 2, 3
if GLOB_MODPAR_NAME = "gs_back_type" then
	for i = 1 to vardim1(stBackType)
		if gs_back_type = stBackType[i] then
			gs_back_type_m = i
			parameters gs_back_type_m = gs_back_type_m
			i = vardim1(stBackType)
		endif
	next i
else
	gs_back_type = stBackType[min(max(gs_back_type_m, 1),vardim1(stBackType))]
	parameters gs_back_type = gs_back_type
endif

values "gs_seat_height" range[0.40,)
