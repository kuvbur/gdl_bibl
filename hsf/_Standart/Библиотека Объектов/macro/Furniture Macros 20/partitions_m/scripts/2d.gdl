
unID = partitions_unID

pen gs_cont_pen
line_type 1
fill gs_fill_type

drawindex 50

! ======================================================================
! SCALE
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D



if (NOT(bPartOfLayout) and NOT(bPartOfWsSolo)) or ((det2D <> 3) and (iPartitionPos = PARPOS_ABOVE_D)) then
	! ======================================================================
	partitions_unID = unID
	end partitions_unID
	! ======================================================================
endif

! ======================================================================
! Partition
! ======================================================================

if bOnlyPartitionColumn then
	if iDeskAngle = 90 then
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-partitionFrameThk / 2, -partitionFrameThk / 2, bShowPartEdgeBottom,
			partitionFrameThk / 2, -partitionFrameThk / 2, bShowPartEdgeRight,
			partitionFrameThk / 2, partitionFrameThk / 2, bShowPartEdgeTop,
			-partitionFrameThk / 2, partitionFrameThk / 2, bShowPartEdgeLeft
	endif
	if iDeskAngle = 120 then
		if bPartOfWsSolo then
			add2 -partitionFrameThk / TAN(iDeskAngle / 2) / 2, -partitionFrameThk / 2
			poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, bShowPartEdgeBottom,
				partitionFrameThk / TAN(iDeskAngle / 2), 0, bShowPartEdgeRight,
				partitionFrameThk / TAN(iDeskAngle / 2), partitionFrameThk, bShowPartEdgeTop,
				-partitionFrameThk / TAN(iDeskAngle / 2) * COS(60), partitionFrameThk / TAN(iDeskAngle / 2) * SIN(60), bShowPartEdgeLeft
			del 1
		else
			add2 0, partitionFrameThk / 2 * TAN(30)
			poly2_b 3, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-partitionFrameThk / 2, 0, bShowPartEdgeTop,
				partitionFrameThk / 2, 0, bShowPartEdgeRight,
				0, -partitionFrameThk / 2 * TAN(60), bShowPartEdgeLeft
			del 1
		endif
	endif
else
	add2 xPartOrigPos, yPartOrigPos
	rot2 anglePartititon

	if NOT(bCalledAsExtension) and NOT(bPartOfLayout) and NOT(bPartOfWsSolo) then
		add2	gs_partitionPos_front[1][1] * (iPartitionLoc = 1) + gs_partitionPos_side1[1][1] * (iPartitionLoc = 2) +\
				gs_partitionPos_side2[1][1] * (iPartitionLoc = 3) + gs_partitionPos_end[1][1] * (iPartitionLoc = 4),\
				gs_partitionPos_front[1][2] * (iPartitionLoc = 1) + gs_partitionPos_side1[1][2] * (iPartitionLoc = 2) +\
				gs_partitionPos_side2[1][2] * (iPartitionLoc = 3) + gs_partitionPos_end[1][2] * (iPartitionLoc = 4)
	endif

	if NOT(bCalledAsExtension) then			!!! Not Called as Extension
		if NOT(bOnlyPartitionCover) then
			pLength	= realPartitionLength
			poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-pLength / 2 - partCutModLeft, -partitionFrameThk / 2, bShowPartEdgeLeft,
				-pLength / 2 + partCutModLeft, partitionFrameThk / 2, 1,
				pLength / 2 - partCutModRight, partitionFrameThk / 2, bShowPartEdgeRight,
				pLength / 2 + partCutModRight, -partitionFrameThk / 2, 1
		else
			pLength	= realPartitionLength
			poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-pLength / 2, -partitionFrameThk / 2, bShowPartEdgeLeft,
				-pLength / 2, partitionFrameThk / 2, 1,
				pLength / 2, partitionFrameThk / 2, bShowPartEdgeRight,
				pLength / 2, -partitionFrameThk / 2, 1
		endif
	else									!!! Called as Extension
		poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-extPartLengthStart, -partitionFrameThk / 2, 1,
			-extPartLengthStart, partitionFrameThk / 2, 1,

			extPartLengthEnd, partitionFrameThk / 2, 1,
			extPartLengthEnd, -partitionFrameThk / 2, 1,

			partitionFrameThk / 2, -partitionFrameThk / 2, bShowExtPartConnEdge,
			-partitionFrameThk / 2, -partitionFrameThk / 2, 1
	endif

	if NOT(bCalledAsExtension) and NOT(bPartOfLayout) and NOT(bPartOfWsSolo) then del 1



	! ======================================================================
	! 2D Hostspot Editing
	! ======================================================================

	if NOT(bCalledAsExtension) and NOT(bPartOfLayout) and NOT(bPartOfWsSolo) then gosub "2DHotspots"

	del 2
endif

! ======================================================================
partitions_unID = unID
end partitions_unID
! ======================================================================



"partition":
	! ======================================================================
	! Partition Frame
	! ======================================================================

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-pLength / 2, -partitionFrameThk / 2, 1,
		-pLength / 2, partitionFrameThk / 2, 1,
		-pLength / 2 + partitionFrameWidth, partitionFrameThk / 2, 1,
		-pLength / 2 + partitionFrameWidth, -partitionFrameThk / 2, 1

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		pLength / 2, -partitionFrameThk / 2, 1,
		pLength / 2, partitionFrameThk / 2, 1,
		pLength / 2 - partitionFrameWidth, partitionFrameThk / 2, 1,
		pLength / 2 - partitionFrameWidth, -partitionFrameThk / 2, 1

	! ======================================================================
	! Partition Fill
	! ======================================================================

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-pLength / 2 + partitionFrameWidth, -partitionFillThk / 2, 1,
		-pLength / 2 + partitionFrameWidth, partitionFillThk / 2, 1,
		pLength / 2 - partitionFrameWidth, partitionFillThk / 2, 1,
		pLength / 2 - partitionFrameWidth, -partitionFillThk / 2, 1
return



"2DHotspots":
	if iPartitionLoc = 1 then
		if bEnableHotspotEditingX then
			hotspot2 0, gs_partitionPos_front[1][2], unID, gs_partitionPos_front[1][1], 1 + 128:						unID = unID + 1
			hotspot2 -1, gs_partitionPos_front[1][2], unID, gs_partitionPos_front[1][1], 3 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_front[1][1], gs_partitionPos_front[1][2], unID, gs_partitionPos_front[1][1], 2:	unID = unID + 1
		endif
		if bEnableHotspotEditingY then
			hotspot2 gs_partitionPos_front[1][1], 0, unID, gs_partitionPos_front[1][2], 1 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_front[1][1], -1, unID, gs_partitionPos_front[1][2], 3 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_front[1][1], gs_partitionPos_front[1][2], unID, gs_partitionPos_front[1][2], 2:	unID = unID + 1
		endif
	endif
	if iPartitionLoc = 2 then
		if bEnableHotspotEditingX then
			hotspot2 0, gs_partitionPos_side1[1][2], unID, gs_partitionPos_side1[1][1], 1 + 128:						unID = unID + 1
			hotspot2 -1, gs_partitionPos_side1[1][2], unID, gs_partitionPos_side1[1][1], 3 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_side1[1][1], gs_partitionPos_side1[1][2], unID, gs_partitionPos_side1[1][1], 2:	unID = unID + 1
		endif
		if bEnableHotspotEditingY then
			hotspot2 gs_partitionPos_side1[1][1], 0, unID, gs_partitionPos_side1[1][2], 1 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_side1[1][1], -1, unID, gs_partitionPos_side1[1][2], 3 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_side1[1][1], gs_partitionPos_side1[1][2], unID, gs_partitionPos_side1[1][2], 2:	unID = unID + 1
		endif
	endif
	if iPartitionLoc = 3 then
		if bEnableHotspotEditingX then
			hotspot2 0, gs_partitionPos_side2[1][2], unID, gs_partitionPos_side2[1][1], 1 + 128:						unID = unID + 1
			hotspot2 -1, gs_partitionPos_side2[1][2], unID, gs_partitionPos_side2[1][1], 3 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_side2[1][1], gs_partitionPos_side2[1][2], unID, gs_partitionPos_side2[1][1], 2:	unID = unID + 1
		endif
		if bEnableHotspotEditingY then
			hotspot2 gs_partitionPos_side2[1][1], 0, unID, gs_partitionPos_side2[1][2], 1 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_side2[1][1], -1, unID, gs_partitionPos_side2[1][2], 3 + 128:						unID = unID + 1
			hotspot2 gs_partitionPos_side2[1][1], gs_partitionPos_side2[1][2], unID, gs_partitionPos_side2[1][2], 2:	unID = unID + 1
		endif
	endif
	if iPartitionLoc = 4 then
		if bEnableHotspotEditingX then
			hotspot2 0, gs_partitionPos_end[1][2], unID, gs_partitionPos_end[1][1], 1 + 128:							unID = unID + 1
			hotspot2 -1, gs_partitionPos_end[1][2], unID, gs_partitionPos_end[1][1], 3 + 128:							unID = unID + 1
			hotspot2 gs_partitionPos_end[1][1], gs_partitionPos_end[1][2], unID, gs_partitionPos_end[1][1], 2:			unID = unID + 1
		endif
		if bEnableHotspotEditingY then
			hotspot2 gs_partitionPos_end[1][1], 0, unID, gs_partitionPos_end[1][2], 1 + 128:							unID = unID + 1
			hotspot2 gs_partitionPos_end[1][1], -1, unID, gs_partitionPos_end[1][2], 3 + 128:							unID = unID + 1
			hotspot2 gs_partitionPos_end[1][1], gs_partitionPos_end[1][2], unID, gs_partitionPos_end[1][2], 2:			unID = unID + 1
		endif
	endif
return
