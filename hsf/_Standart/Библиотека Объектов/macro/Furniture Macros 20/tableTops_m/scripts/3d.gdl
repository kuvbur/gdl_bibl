
if gs_detlevel_3D_m = 0 then end

if not(gs_shadow) then shadow off

unID = tableTops_unID

if archedTableTop then
	iResol = gs_resol
else
	if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
		if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then
			iResol = 3
		else
			iResol = 360 / (180 - tableTopAngle) * 3
		endif
	else
		iResol = gs_resol
	endif
endif

resol iResol

pen gs_cont_pen

! ======================================================================
! TableTop
! ======================================================================

material gs_top_mat

addz zzyzx - topThk

gosub "3DHotspots"

if iTableTopType = TTOP_L_SHAPED then	gosub "deskLShaped"
if iTableTopType = TTOP_STRAIGHT then	gosub "deskStraight"
if archedTableTop then					gosub "deskArched"
if iTableTopType = TTOP_RECT then		gosub "deskRectangle"

del 1

! ======================================================================
tableTops_unID = unID
end tableTops_unID
! ======================================================================

"deskLShaped":
	put	0, 0, 15,
		tableLength1, 0, 15,
		tableLength1, tableDepth1, 15

	if iInnerEdge = STRAIGHT then
		put	innerCornerPos, tableDepth1,	15
	else
		put	innerArcStartPos, tableDepth1, 15 + 64,
			innerEdgeRad, -(90 - tableTopAngle / 2) * 2, 2015
	endif

	put	xPosTopCorner, yPosTopCorner, 15,
		-xPosSideCorner, yPosSideCorner, 15

	prism_ NSP / 3, topThk,
			get(NSP)

return



"deskStraight":
	addx -extraDeskWidth / 2 * bCalledAsExtension
	prism_ 4, topThk,
		0, 0, 15,
		tableLength1, 0, 15,
		tableLength1, tableDepth1, 15,
		0, tableDepth1, 15
	del 1

return



"deskArched":
	rotz 60 * (iTableTopType = TTOP_ARCHED_360)

	if bPartColumnCut then
		put	-partColumnCutWidth, 0, 15,
			-partColumnCutWidth, -partColumnCutWidth, 15,
			0, -partColumnCutWidth, 15
	endif
	put	0, -b, 79,
		0, 0, 900,
		0, -tableTopAngle, 4015,
		b * (iTableTopType = TTOP_ARCHED_P_TOP) - partColumnCutWidth * bPartColumnCut, -b * (iTableTopType = TTOP_ARCHED_360 or iTableTopType = TTOP_ARCHED_P_TOP), 15

	prism_ NSP / 3, topThk,
			get(NSP)
	del 1

return



"deskRectangle":
	prism_ 4, topThk,
		-a / 2, -b / 2, 15,
		a / 2, -b / 2, 15,
		a / 2, b / 2, 15,
		-a / 2, b / 2, 15

return



"3DHotspots":
	addz topThk - zzyzx
		if iTableTopType = TTOP_L_SHAPED then
			if bEnableDeskHsCorner then
				hotspot 0, 0, zzyzx, unID:																unID = unID + 1
			endif
			if bEnableDeskHsHalfEdge then
				hotspot tableLength1 / 2, 0, zzyzx, unID:												unID = unID + 1
				hotspot -xPosSideCorner / 2, yPosSideCorner / 2, zzyzx, unID:							unID = unID + 1
			endif
			if bEnableDeskHsEditingX then
				hotspot 0, tableDepth1, zzyzx, unID, tableLength1, 1 + 128:								unID = unID + 1
				hotspot -tableLength1, tableDepth1, zzyzx, unID, tableLength1, 3 + 128:					unID = unID + 1
				hotspot tableLength1, tableDepth1, zzyzx, unID, tableLength1, 2:						unID = unID + 1

				hotspot 0, 0, zzyzx, unID, tableLength1, 1 + 128:										unID = unID + 1
				hotspot -tableLength1, 0, zzyzx, unID, tableLength1, 3 + 128:							unID = unID + 1
				hotspot tableLength1, 0, zzyzx, unID, tableLength1, 2:									unID = unID + 1
			endif
			if bEnableDeskHsEditingC then
				if iInnerEdge = STRAIGHT then
					hotspot 0, tableDepth1, zzyzx, unID, innerCornerPos, 1 + 128, tableDepth2:			unID = unID + 1
					hotspot -1, tableDepth1, zzyzx, unID, innerCornerPos, 3 + 128, tableDepth2:			unID = unID + 1
					hotspot innerCornerPos, tableDepth1, zzyzx, unID, innerCornerPos, 2, tableDepth2:	unID = unID + 1
					hotspot innerCornerPos, 0, zzyzx, unID, tableDepth1, 1 + 128:						unID = unID + 1
					hotspot innerCornerPos, -tableDepth1, zzyzx, unID, tableDepth1, 3 + 128:			unID = unID + 1
					hotspot innerCornerPos, tableDepth1, zzyzx, unID, tableDepth1, 2:					unID = unID + 1
				else
					hotspot innerArcStartPos, 0, zzyzx, unID, tableDepth1, 1 + 128:							unID = unID + 1
					hotspot innerArcStartPos, -1, zzyzx, unID, tableDepth1, 3 + 128:						unID = unID + 1
					hotspot innerArcStartPos, tableDepth1, zzyzx, unID, tableDepth1, 2:						unID = unID + 1
					hotspot 0, tableDepth1, zzyzx, unID, innerArcStartPos, 1 + 128, innerEdgeRad:			unID = unID + 1
					hotspot -1, tableDepth1, zzyzx, unID, innerArcStartPos, 3 + 128, innerEdgeRad:			unID = unID + 1
					hotspot innerArcStartPos, tableDepth1, zzyzx, unID, innerArcStartPos, 2, innerEdgeRad:	unID = unID + 1

					rotz tableTopAngle - 180
						hotspot -innerArcEndPos, 0, zzyzx, unID, tableDepth2, 1 + 128:						unID = unID + 1
						hotspot -innerArcEndPos, -1, zzyzx, unID, tableDepth2, 3 + 128:						unID = unID + 1
						hotspot -innerArcEndPos, tableDepth2, zzyzx, unID, tableDepth2, 2:					unID = unID + 1

						hotspot 0, tableDepth2, zzyzx, unID, innerArcEndPos, 1 + 128, innerEdgeRad: 		unID = unID + 1
						hotspot 1, tableDepth2, zzyzx, unID, innerArcEndPos, 3 + 128, innerEdgeRad: 		unID = unID + 1
						hotspot -innerArcEndPos, zzyzx, tableDepth2, unID, innerArcEndPos, 2, innerEdgeRad:	unID = unID + 1
					del 1
				endif
			endif
			if bEnableDeskHsEditingY then
				rotz tableTopAngle - 180
					hotspot -tableLength2, 0, zzyzx, unID, tableDepth2, 1 + 128:						unID = unID + 1
					hotspot -tableLength2, -tableDepth2, zzyzx, unID, tableDepth2, 3 + 128:				unID = unID + 1
					hotspot -tableLength2, tableDepth2, zzyzx, unID, tableDepth2, 2:					unID = unID + 1

					hotspot 0, tableDepth2, zzyzx, unID, tableLength2, 1 + 128:							unID = unID + 1
					hotspot tableLength2, tableDepth2, zzyzx, unID, tableLength2, 3 + 128:				unID = unID + 1
					hotspot -tableLength2, tableDepth2, zzyzx, unID, tableLength2, 2:					unID = unID + 1
				del 1

				hotspot tableLength1, 0, zzyzx, unID, tableDepth1, 1 + 128:								unID = unID + 1
				hotspot tableLength1, -tableDepth1, zzyzx, unID, tableDepth1, 3 + 128:					unID = unID + 1
				hotspot tableLength1, tableDepth1, zzyzx, unID, tableDepth1, 2:							unID = unID + 1

				hotspot 0, 0, zzyzx, unID, tableLength2, 1 + 128:										unID = unID + 1
				hotspot 1, -1, zzyzx, unID, tableLength2, 3 + 128: 										unID = unID + 1
				hotspot -xPosSideCorner, yPosSideCorner, zzyzx, unID, tableLength2, 2: 					unID = unID + 1
			endif
		endif

		if iTableTopType = TTOP_STRAIGHT then
			if NOT(bCalledAsExtension) then
				if NOT(bPartOfLayout) and NOT(bPartOfWsSolo) then
					if bEnableDeskHsEditingX then
						hotspot 0, 0, zzyzx, unID, a, 1 + 256:		unID = unID + 1
						hotspot -1, 0, zzyzx, unID, a, 3 + 128:		unID = unID + 1
						hotspot a, 0, zzyzx, unID, a, 2:			unID = unID + 1

						hotspot 0, b, zzyzx, unID, a, 1 + 256:		unID = unID + 1
						hotspot -1, b, zzyzx, unID, a, 3 + 128:		unID = unID + 1
						hotspot a, b, zzyzx, unID, a, 2:			unID = unID + 1
					endif
					if bEnableDeskHsEditingY then
						hotspot 0, 0, zzyzx, unID, b, 1 + 256:		unID = unID + 1
						hotspot 0, -1, zzyzx, unID, b, 3 + 128:		unID = unID + 1
						hotspot 0, b, zzyzx, unID, b, 2:			unID = unID + 1

						hotspot a, 0, zzyzx, unID, b, 1 + 256:		unID = unID + 1
						hotspot a, -1, zzyzx, unID, b, 3 + 128:		unID = unID + 1
						hotspot a, b, zzyzx, unID, b, 2:			unID = unID + 1
					endif

					hotspot a / 2, b / 2, zzyzx, unID:				unID = unID + 1

					if bEnableDeskHsHalfEdge then
						hotspot 0, b / 2, zzyzx, unID:				unID = unID + 1
						hotspot a, b / 2, zzyzx, unID:				unID = unID + 1
						hotspot a / 2, 0, zzyzx, unID:				unID = unID + 1
						hotspot a / 2, b, zzyzx, unID:				unID = unID + 1
					endif
				else
					if bPartOfWsSolo then
						if bEnableDeskHsEditingX then
							hotspot 0, 0, zzyzx, unID, tableLength1, 1 + 256:					unID = unID + 1
							hotspot -1, 0, zzyzx, unID, tableLength1, 3 + 128:					unID = unID + 1
							hotspot tableLength1, 0, zzyzx, unID, tableLength1, 2:				unID = unID + 1

							hotspot 0, tableDepth1, zzyzx, unID, tableLength1, 1 + 256:			unID = unID + 1
							hotspot -1, tableDepth1, zzyzx, unID, tableLength1, 3 + 128:		unID = unID + 1
							hotspot tableLength1, tableDepth1, zzyzx, unID, tableLength1, 2:	unID = unID + 1
						endif
						if bEnableDeskHsEditingY then
							hotspot 0, 0, zzyzx, unID, tableDepth1, 1 + 256:					unID = unID + 1
							hotspot 0, -1, zzyzx, unID, tableDepth1, 3 + 128:					unID = unID + 1
							hotspot 0, tableDepth1, zzyzx, unID, tableDepth1, 2:				unID = unID + 1

							hotspot tableLength1, 0, zzyzx, unID, tableDepth1, 1 + 256:			unID = unID + 1
							hotspot tableLength1, -1, zzyzx, unID, tableDepth1, 3 + 128:		unID = unID + 1
							hotspot tableLength1, tableDepth1, zzyzx, unID, tableDepth1, 2:		unID = unID + 1
						endif
					else
						if bEnableDeskHsEditingX then
							hotspot 0, 0, zzyzx, unID, tableLength1, 1 + 128:					unID = unID + 1
							hotspot -1, 0, zzyzx, unID, tableLength1, 3 + 128:					unID = unID + 1
							hotspot tableLength1, 0, zzyzx, unID, tableLength1, 2:				unID = unID + 1

							hotspot 0, tableDepth1, zzyzx, unID, tableLength1, 1 + 128:			unID = unID + 1
							hotspot -1, tableDepth1, zzyzx, unID, tableLength1, 3 + 128:		unID = unID + 1
							hotspot tableLength1, tableDepth1, zzyzx, unID, tableLength1, 2:	unID = unID + 1
						endif
						if bEnableDeskHsEditingY then
							hotspot 0, 0, zzyzx, unID, tableDepth1, 1 + 128:					unID = unID + 1
							hotspot 0, -1, zzyzx, unID, tableDepth1, 3 + 128:					unID = unID + 1
							hotspot 0, tableDepth1, zzyzx, unID, tableDepth1, 2:				unID = unID + 1
						endif
						if bEnableDeskHsEditingY then
							hotspot tableLength1, 0, zzyzx, unID, tableDepth1, 1 + 128:			unID = unID + 1
							hotspot tableLength1, -1, zzyzx, unID, tableDepth1, 3 + 128:		unID = unID + 1
							hotspot tableLength1, tableDepth1, zzyzx, unID, tableDepth1, 2:		unID = unID + 1
						endif
					endif
				endif
			else
				hotspot 0, extraDeskDepth, zzyzx, unID, halfExtraDeskWidth, 1 + 128, extraDeskWidth:				unID = unID + 1
				hotspot -1, extraDeskDepth, zzyzx, unID, halfExtraDeskWidth, 3, extraDeskWidth:						unID = unID + 1
				hotspot halfExtraDeskWidth, extraDeskDepth, zzyzx, unID, halfExtraDeskWidth, 2, extraDeskWidth:		unID = unID + 1
				hotspot halfExtraDeskWidth, 0, zzyzx, unID, extraDeskDepth, 1 + 128:								unID = unID + 1
				hotspot halfExtraDeskWidth, -extraDeskDepth, zzyzx, unID, extraDeskDepth, 3:						unID = unID + 1
				hotspot halfExtraDeskWidth, extraDeskDepth, zzyzx, unID, extraDeskDepth, 2:							unID = unID + 1

				hotspot 0, extraDeskDepth, zzyzx, unID, halfExtraDeskWidth, 1 + 128, extraDeskWidth:				unID = unID + 1
				hotspot 1, extraDeskDepth, zzyzx, unID, halfExtraDeskWidth, 3, extraDeskWidth:						unID = unID + 1
				hotspot -halfExtraDeskWidth, extraDeskDepth, zzyzx, unID, halfExtraDeskWidth, 2, extraDeskWidth:	unID = unID + 1
				hotspot -halfExtraDeskWidth, 0, zzyzx, unID, extraDeskDepth, 1 + 128:								unID = unID + 1
				hotspot -halfExtraDeskWidth, -extraDeskDepth, zzyzx, unID, extraDeskDepth, 3:						unID = unID + 1
				hotspot -halfExtraDeskWidth, extraDeskDepth, zzyzx, unID, extraDeskDepth, 2:						unID = unID + 1
			endif
		endif

		if archedTableTop and NOT(bPartOfLayout) and NOT(bPartOfWsSolo) then
			if iTableTopType <> TTOP_ARCHED_360 then
				hotspot 0, 0, zzyzx, unID:													unID = unID + 1
			else
				hotspot -b * SIN(tableTopAngle), -b * COS(tableTopAngle), zzyzx, unID:				unID = unID + 1
				hotspot -b * SIN(tableTopAngle - 60), -b * COS(tableTopAngle - 60), zzyzx, unID:	unID = unID + 1
			endif

			if iTableTopType = TTOP_ARCHED_P_TOP then
				hotspot b, -b, zzyzx, unID:													unID = unID + 1
			endif

			hotspot 0, 0, zzyzx, unID, b, 1 + 128:											unID = unID + 1
			hotspot 1, 0, zzyzx, unID, b, 3 + 128:											unID = unID + 1
			hotspot 0, -b, zzyzx, unID, b, 2:												unID = unID + 1

			if tableTopAngle > 135 then
				hotspot 0, 0, zzyzx, unID, b, 1 + 128:										unID = unID + 1
				hotspot 1, 0, zzyzx, unID, b, 3 + 128:										unID = unID + 1
				hotspot -b * SIN(90), -b * COS(90), zzyzx, unID, b, 2:						unID = unID + 1
			endif
			if tableTopAngle > 180 then
				hotspot 0, 0, zzyzx, unID, b, 1 + 128:										unID = unID + 1
				hotspot 1 - 2 * (tableTopAngle > 180), 0, zzyzx, unID, b, 3 + 128:			unID = unID + 1
				hotspot -b * SIN(180), -b * COS(180), zzyzx, unID, b, 2:					unID = unID + 1
			endif
			if iTableTopType <> TTOP_ARCHED_360 then
				hotspot 0, 0, zzyzx, unID, b, 1 + 128:										unID = unID + 1
				hotspot 1 - 2 * (tableTopAngle > 180), 0, zzyzx, unID, b, 3 + 128:			unID = unID + 1
				hotspot -b * SIN(tableTopAngle), -b * COS(tableTopAngle), zzyzx, unID, b, 2:unID = unID + 1
			endif
		endif

		if iTableTopType = TTOP_RECT and NOT(bPartOfLayout) and NOT(bPartOfWsSolo) then
			hotspot 0, 0, zzyzx, unID:								unID = unID + 1

			hotspot 0, halfB, zzyzx, unID, halfA, 1 + 128, a:		unID = unID + 1
			hotspot -halfA, halfB, zzyzx, unID, halfA, 3, a:		unID = unID + 1
			hotspot halfA, halfB, zzyzx, unID, halfA, 2, a:			unID = unID + 1

			hotspot halfA, 0, zzyzx, unID, halfB, 1 + 128, b:		unID = unID + 1
			hotspot halfA, -halfB, zzyzx, unID, halfB, 3, b:		unID = unID + 1
			hotspot halfA, halfB, zzyzx, unID, halfB, 2, b:			unID = unID + 1

			hotspot 0, -halfB, zzyzx, unID, halfA, 1 + 128, a:		unID = unID + 1
			hotspot -halfA, -halfB, zzyzx, unID, halfA, 3, a:		unID = unID + 1
			hotspot halfA, -halfB, zzyzx, unID, halfA, 2, a:		unID = unID + 1

			hotspot halfA, 0, zzyzx, unID, halfB, 1 + 128, b:		unID = unID + 1
			hotspot halfA, halfB, zzyzx, unID, halfB, 3, b:			unID = unID + 1
			hotspot halfA, -halfB, zzyzx, unID, halfB, 2, b:		unID = unID + 1

			hotspot 0, halfB, zzyzx, unID, halfA, 1 + 128, a:		unID = unID + 1
			hotspot halfA, halfB, zzyzx, unID, halfA, 3, a:			unID = unID + 1
			hotspot -halfA, halfB, zzyzx, unID, halfA, 2, a:		unID = unID + 1

			hotspot -halfA, 0, zzyzx, unID, halfB, 1 + 128, b:		unID = unID + 1
			hotspot -halfA, -halfB, zzyzx, unID, halfB, 3, b:		unID = unID + 1
			hotspot -halfA, halfB, zzyzx, unID, halfB, 2, b:		unID = unID + 1

			hotspot 0, -halfB, zzyzx, unID, halfA, 1 + 128, a:		unID = unID + 1
			hotspot halfA, -halfB, zzyzx, unID, halfA, 3, a:		unID = unID + 1
			hotspot -halfA, -halfB, zzyzx, unID, halfA, 2, a:		unID = unID + 1

			hotspot -halfA, 0, zzyzx, unID, halfB, 1 + 128, b:		unID = unID + 1
			hotspot -halfA, halfB, zzyzx, unID, halfB, 3, b:		unID = unID + 1
			hotspot -halfA, -halfB, zzyzx, unID, halfB, 2, b:		unID = unID + 1
		endif

		if archedTableTop and NOT(bPartOfLayout) and NOT(bPartOfWsSolo) and iTableTopType = TTOP_ARCHED_360 then
			hotspot 0, 0, zzyzx, unID:								unID = unID + 1
		endif

	del 1

return

