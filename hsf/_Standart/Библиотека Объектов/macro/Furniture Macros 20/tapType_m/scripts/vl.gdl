
! ==============================================================================
! Tap Type
! ==============================================================================

dim stTapGroups[7]
	stTapGroups[1] = `Одноуровневый Смеситель`
	stTapGroups[2] = `Смеситель с Двумя Вентилями`
	stTapGroups[3] = `Традиционный Смеситель с Опорой`
	stTapGroups[4] = `Сенсорный Кран`
	stTapGroups[5] = `Крепление к Стене`
	stTapGroups[6] = `Нет`
	stTapGroups[7] = `Специальные Смесители`

! enableTapGroups - possible values
!	2	- enable Single Level Mixer
!	4	- enable Two Handle Mixer
!	8	- enable Traditional Basin Pillar Tap
!	16	- enable Sensor Faucet
!	32	- enable Wall Mounted
!	64	- enable None
!	128	- enable No tapgroup / Only Custom taps

if enableTapGroups then
	dim tapGroupValueList[]
	j = 1
	for i = 1 to vardim1(stTapGroups)
		if bittest(enableTapGroups, i) then
			if i = 1 then put 1			! Single Level Mixer
			if i = 2 then put 2			! Two Handle Mixer
			if i = 3 then put 3			! Traditional Basin Pillar Tap
			if i = 4 then put 4			! Sensor Faucet
			if i = 5 then put 5			! Wall Mounted
			if i = 6 then put 6			! None
			if i = 7 then put 7			! Custom Taps

			tapGroupValueList[j] = stTapGroups[i]
			j = j + 1
		endif
	next i

	values "tapGroup" tapGroupValueList
	values "iTapGroup" get(NSP)

	if GLOB_MODPAR_NAME = "tapGroup" then
		for iTap = 1 to vardim1(stTapGroups)
			if tapGroup = stTapGroups[iTap] then
				if tapGroup = stTapGroups[vardim1(stTapGroups)] then
					iTapGroup = 0
				else
					iTapGroup = iTap
				endif
			endif
		next iTap
		parameters iTapGroup = iTapGroup
	else
		if iTapGroup = 0 then
			tapGroup = stTapGroups[7]
		else
			tapGroup = stTapGroups[max(iTapGroup, 1)]
		endif
		parameters tapGroup = tapGroup
	endif
else		! No tapgroup / Only Custom taps
	iTapGroup = TAPGROUP_CUSTOMTAPS
endif



! ==============================================================================
! Tap Style
! ==============================================================================

! enableSingleLevelTaps - possible values
!	2			- enable Simple
!	4			- enable None
!	8			- enable Style 1
!	16			- enable Style 2
!	32			- enable Style 3
!	64			- enable Style 4
!	128			- enable Style 5
!	256			- enable Style 6
!	512			- enable Style 7
!	1024		- enable Style 8
!	2048		- enable Style 9
!	4096		- enable Style 10
!	8192		- enable Style 11
!	16384		- enable Style 12
!	32768		- enable Style 13
!	65536		- enable Style 14
!	131072		- enable Style 15
!	262144		- enable Style 16
!	524288		- enable Style 17
!	1048576		- enable Style 18
!	2097152		- enable Style 19
!	4194304		- enable Style 20
!	8388608		- enable Style 21
!	16777216	- enable Style 22
!	33554432	- enable Style 23
!	67108864	- enable Style 24
!	134217728	- enable Style 25
!	268435456	- enable Style 26
!	536870912	- enable Style 27

dim stTapStyle[29]
	stTapStyle[1] = `Простой`
	stTapStyle[2] = `Нет`
	if bShowerCabin then
		if bShowerCabinFreeStand then
			stTapStyle[3] = `Верхняя Часть`
		else
			stTapStyle[3] = `Настенный Термостат`
		endif
		stTapStyle[4] = `Труба`
		stTapStyle[5] = `Панель`
		stTapStyle[6] = `Верхняя Часть`
	else
		stTapStyle[3] = `Стиль 1`
		stTapStyle[4] = `Стиль 2`
		stTapStyle[5] = `Стиль 3`
		stTapStyle[6] = `Стиль 4`
	endif
	stTapStyle[7] = `Стиль 5`
	stTapStyle[8] = `Стиль 6`
	stTapStyle[9] = `Стиль 7`
	stTapStyle[10] = `Стиль 8`
	stTapStyle[11] = `Стиль 9`
	stTapStyle[12] = `Стиль 10`
	stTapStyle[13] = `Стиль 11`
	stTapStyle[14] = `Стиль 12`
	stTapStyle[15] = `Стиль 13`
	stTapStyle[16] = `Стиль 14`
	stTapStyle[17] = `Стиль 15`
	stTapStyle[18] = `Стиль 16`
	stTapStyle[19] = `Стиль 17`
	stTapStyle[20] = `Стиль 18`
	stTapStyle[21] = `Стиль 19`
	stTapStyle[22] = `Стиль 20`
	stTapStyle[23] = `Стиль 21`
	stTapStyle[24] = `Стиль 22`
	stTapStyle[25] = `Стиль 23`
	stTapStyle[26] = `Стиль 24`
	stTapStyle[27] = `Стиль 25`
	stTapStyle[28] = `Стиль 26`
	stTapStyle[29] = `Стиль 27`

if iTapGroup = TAPGROUP_NONE then			enableTaps = 4
if iTapGroup = TAPGROUP_SINGLE then			enableTaps = enableSingleLevelTaps
if iTapGroup = TAPGROUP_TWOHANDLE then		enableTaps = enableTwoHandleTaps
if iTapGroup = TAPGROUP_TRADITIONAL then	enableTaps = enableTraditionalTaps
if iTapGroup = TAPGROUP_SENSOR then			enableTaps = enableSensorFaucets
if iTapGroup = TAPGROUP_WALLMOUNTED then	enableTaps = enableWallMountedTaps
if iTapGroup = TAPGROUP_CUSTOMTAPS then		enableTaps = enableCustomTaps

if enableTaps then
	dim tapValueList[]
	dim iTapValueList[]
	j = 1
	k = 1
	for i = 1 to vardim1(stTapStyle)
		if bittest(enableTaps, i) then
			if i < vardim1(stTapStyle) - 1 then
				tapValueList[j] = stTapStyle[j + 2]
			else
				if i = vardim1(stTapStyle) - 1 then
					if bittest(enableTaps, 1) and bittest(enableTaps, 2) then
						tapValueList[j] = stTapStyle[j]
					else
						tapValueList[j] = stTapStyle[j + 2]
					endif
				else
					tapValueList[j] = stTapStyle[j + 2 - bittest(enableTaps, 1) - bittest(enableTaps, 2)]
				endif
			endif

			j = j + 1
		endif

		if bittest(enableTaps, i + 2) then
			iTapValueList[k] = i

			k = k + 1
		endif
	next i

	if bittest(enableTaps, 1) then
		if bittest(enableTaps, 2) then
			tapValueList[j - 2]	= stTapStyle[1]
			tapValueList[j - 1]	= stTapStyle[2]
		else
			tapValueList[j - 1]	= stTapStyle[1]
		endif
	else
		if bittest(enableTaps, 2) then
			tapValueList[j - 1] = stTapStyle[2]
		endif
	endif

	if bittest(enableTaps, 1) then
		if bittest(enableTaps, 2) then
			iTapValueList[j - 2]	= -1
			iTapValueList[j - 1]	= 0
		else
			iTapValueList[j - 1]	= -1
		endif
	else
		if bittest(enableTaps, 2) then
			iTapValueList[j - 1] = 0
		endif
	endif

	values "gs_tap_type" tapValueList
	values "gs_tap_type_m" iTapValueList

	if GLOB_MODPAR_NAME = "gs_tap_type" then
		gs_tap_type_m = 1

		for it = 1 to vardim1(tapValueList)
			if gs_tap_type = tapValueList[it] then gs_tap_type_m = iTapValueList[it]
		next it

		parameters gs_tap_type_m = gs_tap_type_m
	else
		gs_tap_type = stTapStyle[1]

		for it = 1 to vardim1(tapValueList)
			if gs_tap_type_m = iTapValueList[it] then gs_tap_type = tapValueList[it]
		next it

		parameters gs_tap_type = gs_tap_type
	endif

	if iTapGroup <> TAPGROUP_CUSTOMTAPS then
		if GLOB_MODPAR_NAME = "tapGroup" or GLOB_MODPAR_NAME = "iTapGroup" then

			gs_tap_type = tapValueList[1]
			gs_tap_type_m = iTapValueList[1]

			parameters gs_tap_type = gs_tap_type
			parameters gs_tap_type_m = gs_tap_type_m
		endif
	endif
endif



! ==============================================================================
! 2D Tap Symbol
! ==============================================================================

	dim st_tapsymbol[5]
		st_tapsymbol[1]=`Детальный`
		st_tapsymbol[2]=`Простой`
		if bShowerTapSymbol then
			st_tapsymbol[3]=`Символический 1`
		else
			st_tapsymbol[3]=`Символический`
		endif
		st_tapsymbol[4]=`Символический 2`
		st_tapsymbol[5]=`Откл.`

	! enableTapSymbols - possible values
	!	2	- enable Detailed
	!	4	- enable Simple
	!	8	- enable Symbolic / Symbolic 1
	!	16	- enable Symbolic 2
	!	32	- enable None

	if enableTapSymbols then
		dim tapSymbolValueList[]
		j = 1
		for i = 1 to vardim1(st_tapsymbol)
			if bittest(enableTapSymbols, i) then
				if i = 1 then put 2			! Detailed
				if i = 2 then put 1			! Simple
				if i = 3 then put 3			! Symbolic / Symbolic 1
				if i = 4 then put 4			! Symbolic 2
				if i = 5 then put 0			! None

				tapSymbolValueList[j] = st_tapsymbol[i]
				j = j + 1
			endif
		next i

		values "TapSymbol" tapSymbolValueList
		values "TapSymbol_m" get(NSP)

		if GLOB_MODPAR_NAME = "TapSymbol" or TapSymbol_m = -1 then
			TapSymbol_m = 2
			if TapSymbol = st_tapsymbol[2] then TapSymbol_m = 1
			if TapSymbol = st_tapsymbol[3] then TapSymbol_m = 3
			if TapSymbol = st_tapsymbol[4] then TapSymbol_m = 4
			if TapSymbol = st_tapsymbol[5] then TapSymbol_m = 0
			parameters TapSymbol_m = TapSymbol_m
		else
			TapSymbol = st_tapsymbol[1]
			if TapSymbol_m = 1 then TapSymbol = st_tapsymbol[2]
			if TapSymbol_m = 3 then TapSymbol = st_tapsymbol[3]
			if TapSymbol_m = 4 then TapSymbol = st_tapsymbol[4]
			if TapSymbol_m = 0 then TapSymbol = st_tapsymbol[5]
			parameters TapSymbol = TapSymbol
		endif
	endif

if gs_tap_type_m = 0 then
	lock			"TapSymbol", "gs_tap_mat"
	hideparameter	"TapSymbol", "gs_tap_mat"
endif



! ==============================================================================
! Tap Dimensions
! ==============================================================================

yTapPosMin	= -0.05
yTapPosMax	= 0.05
xTap		= 0
yTap		= 0
zTap		= 0
xShower		= 0

if gs_tap_type_m = 1 then
	xTap = 0.025
	yTap = 0.025
endif
if gs_tap_type_m = 2 then
	xTap = 0.071
	yTap = 0.03
endif
if gs_tap_type_m = 3 then
	xTap = 0.026
	yTap = 0.024
endif
if gs_tap_type_m = 4 then
	xTap = 0.13
	yTap = 0.03
endif
if gs_tap_type_m = 5 then
	xTap = 0.158
	yTap = 0.028
endif
if gs_tap_type_m = 6 then
	xTap = 0.13
	yTap = 0.033
endif
if gs_tap_type_m = 7 then
	xTap = 0.02
	yTap = 0.044
endif
if gs_tap_type_m = 8 then
	xTap = 0.075
	yTap = 0.01
endif
if gs_tap_type_m = 9 then
	xTap = 0.168
	yTap = 0.028
endif
if gs_tap_type_m = 10 then
	xTap = 0.079
	yTap = 0.02
endif
if gs_tap_type_m = 11 then
	xTap = 0.132
	yTap = 0.034
endif
if gs_tap_type_m = 12 then
	xTap = 0.14
	yTap = 0.03
endif
if gs_tap_type_m = 13 then
	xTap = 0.017
	yTap = 0.039
endif
if gs_tap_type_m = 14 then
	xTap = 0.115
	yTap = 0.026
endif
if gs_tap_type_m = 15 then
	xTap = 0.112
	yTap = 0.022
endif
if gs_tap_type_m = 16 then
	xTap = 0.032
	yTap = 0.032
	xShower = 0.18 * bShower
endif
if gs_tap_type_m = 17 then
	xTap = 0.033
	yTap = 0.033
endif
if gs_tap_type_m = 18 then
	xTap = 0.178
	yTap = 0.024
	xShower = 0.105 * bShower
endif
if gs_tap_type_m = 19 then
	xTap = 0.122
	yTap = 0.033
	xShower = 0.19 * bShower
endif
if gs_tap_type_m = 20 then
	xTap = 0.02
	yTap = 0.02
endif
if gs_tap_type_m = 21 then
	xTap = 0.109
	zTap = 0.035
	xShower = 0.174 * bShower
endif
if gs_tap_type_m = 22 then
	xTap = 0.137
	zTap = 0.05
endif
if gs_tap_type_m = 23 then
	xTap = 0.152
	zTap = 0.03
endif
if gs_tap_type_m = -1 then
	if iTapGroup = TAPGROUP_SINGLE then
		xTap = 0.025
		yTap = 0.025
	endif
	if iTapGroup = TAPGROUP_TWOHANDLE then
		xTap = 0.025
		yTap = 0.025
	endif
	if iTapGroup = TAPGROUP_TRADITIONAL then
		xTap = 0.025 + 0.09
		yTap = 0.025
	endif
	if iTapGroup = TAPGROUP_SENSOR then
		xTap = 0.025
		yTap = 0.025
	endif
	if iTapGroup = TAPGROUP_WALLMOUNTED then
		xTap = 0.1
		zTap = 0
	endif
endif

yTapPosMin = yTapRangeMin + yTap
yTapPosMax = yTapRangeMax - yTap
xTapPosMin = xTapRangeMin + xTap
xTapPosMax = xTapRangeMax - xTap - xShower
zTapPosMin = zTapRangeMin + zTap

if bEnableTapEdit then
	if xTapPosMin > xTapPosMax - EPS then
		values "xTapPos" (xTapPosMin + xTapPosMax) / 2
	else
		values "xTapPos" range [xTapPosMin, xTapPosMax]
	endif

	if bWallMountedTap then
		values "zTapPos" range [zTapPosMin,]
	else
		if yTapPosMin > yTapPosMax - EPS then
			values "yTapPos" (yTapPosMin + yTapPosMax) / 2
		else
			values "yTapPos" range [yTapPosMin, yTapPosMax]
		endif
	endif
endif

end xTap, xTap + xShower, yTap
