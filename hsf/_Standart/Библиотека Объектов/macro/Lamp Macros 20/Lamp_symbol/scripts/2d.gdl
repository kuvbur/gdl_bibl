
if Symbol_unID < 1 then
	unID = 100
else
	unID = Symbol_unID
endif
r = ss/2
pen gs_cont_pen
nTrans = 0

if iEnableLampSymbolTypes > 0 then
	bWallLamp			= 0
	bSpotSingle			= 0
	bCeilingLamp		= 0
	bDirect				= 0
	bRectSymbol			= 0
	if gs_symbtype_2D_m = SYMBOL_ELECTRIC then
		if	iSymbolType = 2 then	bWallLamp		= 1
		if	iSymbolType = 3 then	bRectSymbol		= 1
	endif
	if gs_symbtype_2D_m = SYMBOL_RCP then
		if	iSymbolType = 2 |\
			iSymbolType = 5 |\
			iSymbolType = 6 then	bWallLamp		= 1
		if	iSymbolType = 3 then	bCeilingLamp	= 1
		if	iSymbolType = 6 then	bSpotSingle		= 1
		if	iSymbolType = 4 |\
			iSymbolType = 5 |\
			iSymbolType = 6 then	bDirect			= 1
		if	iSymbolType = 7 |\
			iSymbolType = 8 then	bRectSymbol		= 1
	endif
endif

symbolLength	= ss / 2
if bRectSymbol then symbolLength	= a / 2

if LibraryLangCode <> "JPN" then

	if show_txt then
		rot2 angleSymbolRotation * bRotateIndependently

		pen gs_cont_pen

		hotspot2 txt_pos_x, 0,         unID, txt_pos_y, 1+128	: unID=unID+1
		hotspot2 txt_pos_x, -1,        unID, txt_pos_y, 3		: unID=unID+1
		hotspot2 txt_pos_x, txt_pos_y, unID, txt_pos_y, 2		: unID=unID+1

		hotspot2 0,        txt_pos_y, unID, txt_pos_x, 1+128	: unID=unID+1
		hotspot2 -1,       txt_pos_y, unID, txt_pos_x, 3		: unID=unID+1
		hotspot2 txt_pos_x,txt_pos_y, unID, txt_pos_x, 2		: unID=unID+1

		define style textStyle fontType, 10/GLOB_SCALE*fsz, 5,
								gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline + 128 * gs_text_style_strike
		style textStyle

		add2 txt_pos_x,txt_pos_y
		nTrans = nTrans + 1

		! --- Oriented View ----------------------------------------------------

		angleViewRot = 0
		rrr = REQUEST ("View_Rotangle", "", angleViewRot)
		totalRotate = (SYMB_ROTANGLE + angleViewRot + angleSymbolRotation * bRotateIndependently) MOD 360

		! Text Rotation - Readable / Always Horizontal / Align with Symbol
		! ----------------------------------------------------------------------
		if typeTextRotation_m = 2 then								! Readable
			if (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps)) then
				rot2 180
				nTrans = nTrans + 1
			endif
		else
			if typeTextRotation_m = 1 then							! Horizontal
				rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED)) ! 1/-1
				nTrans = nTrans + 1
			endif
		endif
		! ----------------------------------------------------------------------

		text2 0,0,txt
		del nTrans

		del 1
	endif

	if gs_symbtype_2D_m <> SYMBOL_REALISTIC then				! Not Realistic Symbol
		rot2 angleSymbolRotation * bRotateIndependently

		if bMigrationRotZ then
			rot2 90
		endif

		if bWallLamp then
			if bSpotSingle then
				gosub "wallspotold"
			else
				gosub "wall"
			endif
		endif

		if NOT(bRectSymbol) then
			hotspot2 0, 0
			hotspot2 0, r
			hotspot2 0, -r
			hotspot2 r, 0
			hotspot2 -r, 0
		else
			hotspot2 0, 0,				unID	: unID = unID + 1
			hotspot2 -a / 2, -b / 2,	unID	: unID = unID + 1
			hotspot2 a / 2, -b / 2,		unID	: unID = unID + 1
			hotspot2 a / 2, b / 2,		unID	: unID = unID + 1
			hotspot2 -a / 2, b / 2,		unID	: unID = unID + 1
		endif

		fill gs_fill_type
		if NOT(bRectSymbol) then
			poly2_b 2, 1 + 2 + 4, gs_fill_pen, gs_back_pen,
					0, 0, 901,
					r, 360, 4001
		else
			poly2_b 4, 1 + 2 + 4, gs_fill_pen, gs_back_pen,
					-a / 2, -b / 2, 1,
					a / 2, -b / 2, 1,
					a / 2, b / 2, 1,
					-a / 2, b / 2, 1
		endif

		if gs_symbtype_2D_m = SYMBOL_ELECTRIC then				! Electrical Symbol
			if NOT(bRectSymbol) then
				line2    -(r/2)*sqr(2), (r/2)*sqr(2), (r/2)*sqr(2), -(r/2)*sqr(2)
				line2    -(r/2)*sqr(2), -(r/2)*sqr(2), (r/2)*sqr(2), (r/2)*sqr(2)
			else
				line2 -a / 2, 0, a / 2, 0
			endif
		endif

		if gs_symbtype_2D_m = SYMBOL_RCP then				! RCP Symbol
			if bDirect and rx > -89.99 then
				gosub "arrow"
			else
				if bCeilingLamp then
					line2     0, r*sqr(2), 0, -(r)*sqr(2)
					line2    -(r)*sqr(2), 0, r*sqr(2), 0
				else
					if NOT(bRectSymbol) then
						gosub "sun"
					else
						if iSymbolType = 8 then	line2 -a / 2, 0, a / 2, 0
					endif
				endif
			endif
		endif

		if bWallLamp then del 1
		if bMigrationRotZ then del 1

		del 1

		if bRotateIndependently then
			if bSpotSingle then
				hotspot2 0, 0,				unID,	angleSymbolRotation, 6: unID = unID + 1
				hotspot2 0, symbolLength,	unID,	angleSymbolRotation, 4 + 128 : unID = unID + 1
				hotspot2 -symbolLength * SIN(angleSymbolRotation), symbolLength * COS(angleSymbolRotation), unID, angleSymbolRotation, 5 : unID = unID + 1
			else
				hotspot2 0, 0,				unID,	angleSymbolRotation, 6: unID = unID + 1
				hotspot2 symbolLength, 0,	unID,	angleSymbolRotation, 4 + 128 : unID = unID + 1
				hotspot2 symbolLength * COS(angleSymbolRotation), symbolLength * SIN(angleSymbolRotation), unID, angleSymbolRotation, 5 : unID = unID + 1
			endif
		endif
	endif

else			!!! Japanese Symbol
		rot2 -specSymbolRotation

		define style textStyleJPN fontType, 10/GLOB_SCALE*fsz, 5,
								gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline + 128 * gs_text_style_strike
		style textStyleJPN

	if gs_symbtype_2D_m = SYMBOL_REALISTIC then
		if _bEnableJPNText & show_txt then
			rot2 -SYMB_ROTANGLE + specSymbolRotation
			text2 0,0,txt
			del 1
		endif
	else
		SymbSizeHalf = ss/2
		alf = acs(1/2)

		if	iSpecSymbolType = SPEC_INCADESCENT_WALL  | \
			iSpecSymbolType = SPEC_FLUORESCENT_1_WALL | \
			iSpecSymbolType = SPEC_FLUORESCENT_2_WALL | \
			iSpecSymbolType = SPEC_FLUORESCENT_VERT_WALL | \
			iSpecSymbolType = SPEC_WALL_WASHER | \
			iSpecSymbolType = SPEC_EMERGENCY_FLOURESCENT_WALL | \
			iSpecSymbolType = SPEC_EMERGENCY_VERT_WALL | \
			iSpecSymbolType = SPEC_EMERGENCY_VERT_WALL_CIRC then
				add2 0, ss/2
		endif

		CircleFill = 1
		if	iSpecSymbolType = SPEC_INCADESCENT then
			gosub 2200
		endif

		if	_bEnableJPNText then
			gosub	2200

			if show_txt then
				rot2 -SYMB_ROTANGLE + specSymbolRotation
				text2 0,0,txt
				del 1
			endif
		endif

		if iSpecSymbolType = SPEC_PENDANT_CORD then
			gosub	2200
			line2	-ss/2, 0,	ss/2, 0

		endif

		if iSpecSymbolType = SPEC_INCADESCENT_WALL then
			CircleFill = 2
			gosub	2200
			gosub	2300
		endif

		if iSpecSymbolType = SPEC_INCADESCENT_LONGLAST then
			gosub	2200

			line2	(ss/2)*SIN(45),   (ss/2)*COS(45),		(ss/2)*SIN(225),   (ss/2)*COS(225)
			line2	(ss/2)*SIN(315),   (ss/2)*COS(315),		(ss/2)*SIN(135),   (ss/2)*COS(135)
		endif

		if iSpecSymbolType = SPEC_INCADESCENT_EMERGENCY then
			gosub	2200
			gosub	2400
		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_1_CEILING then

			gosub	2200

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   SymbSizeHalf/2

			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf/2, 1,
				-specSymbolWidth/2,    SymbSizeHalf/2, 1,
				-(ss/2)*SIN(alf),     SymbSizeHalf/2, 1,
				0, 0, 900,
				-(ss/2)*SIN(alf),     -SymbSizeHalf/2, 3001
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				specSymbolWidth/2,   -SymbSizeHalf/2, 1,
				specSymbolWidth/2,    SymbSizeHalf/2, 1,
				(ss/2)*SIN(alf),     SymbSizeHalf/2, 1,
				0, 0, 900,
				(ss/2)*SIN(alf),     -SymbSizeHalf/2, 3001
		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_2_CEILING then
			CircleFill = 0
			gosub	2200

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf
			hotspot2     specSymbolWidth/2,   SymbSizeHalf

			fill gs_fill_type
			poly2_b 4,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf, 1,
				specSymbolWidth/2,   -SymbSizeHalf, 1,
				specSymbolWidth/2,   SymbSizeHalf, 1,
				-specSymbolWidth/2,   SymbSizeHalf, 1

			line2    -specSymbolWidth/2,   0,    -ss/2,	0
			line2    specSymbolWidth/2,   0,    ss/2,	0
		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_3_CEILING then
			CircleFill = 0
			gosub	2200

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf*1.5
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf*1.5
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf*1.5
			hotspot2     specSymbolWidth/2,   SymbSizeHalf*1.5

			fill gs_fill_type
			poly2_b 4,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf*1.5, 1,
				specSymbolWidth/2,   -SymbSizeHalf*1.5, 1,
				specSymbolWidth/2,   SymbSizeHalf*1.5, 1,
				-specSymbolWidth/2,   SymbSizeHalf*1.5, 1

			line2    -specSymbolWidth/2,   -SymbSizeHalf/2,    -(ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2    -specSymbolWidth/2,    SymbSizeHalf/2,    -(ss/2)*SIN(alf),     SymbSizeHalf/2
			line2     specSymbolWidth/2,   -SymbSizeHalf/2,     (ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2     specSymbolWidth/2,    SymbSizeHalf/2,     (ss/2)*SIN(alf),     SymbSizeHalf/2

		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_4_CEILING then
			CircleFill = 0
			gosub	2200

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf*2
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf*2
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf*2
			hotspot2     specSymbolWidth/2,   SymbSizeHalf*2

			fill gs_fill_type
			poly2_b 4,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf*2, 1,
				specSymbolWidth/2,   -SymbSizeHalf*2, 1,
				specSymbolWidth/2,   SymbSizeHalf*2, 1,
				-specSymbolWidth/2,   SymbSizeHalf*2, 1

			line2    -specSymbolWidth/2,   0,    -ss/2,	0
			line2    specSymbolWidth/2,   0,    ss/2,	0
			line2    -specSymbolWidth/2,    -SymbSizeHalf,    specSymbolWidth/2,	 -SymbSizeHalf
			line2    -specSymbolWidth/2,    SymbSizeHalf,    specSymbolWidth/2,	 SymbSizeHalf
		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_1_WALL then
			CircleFill = 2
			gosub	2200
			gosub	2300

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   SymbSizeHalf/2

			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf/2, 1,
				-specSymbolWidth/2,    SymbSizeHalf/2, 1,
				-(ss/2)*SIN(alf),     SymbSizeHalf/2, 1,
				0, 0, 900,
				-(ss/2)*SIN(alf),     -SymbSizeHalf/2, 3001
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				specSymbolWidth/2,   -SymbSizeHalf/2, 1,
				specSymbolWidth/2,    SymbSizeHalf/2, 1,
				(ss/2)*SIN(alf),     SymbSizeHalf/2, 1,
				0, 0, 900,
				(ss/2)*SIN(alf),     -SymbSizeHalf/2, 3001

			line2    -specSymbolWidth/2,   -SymbSizeHalf/2,    -(ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2    -specSymbolWidth/2,    SymbSizeHalf/2,    -(ss/2)*SIN(alf),     SymbSizeHalf/2
			line2     specSymbolWidth/2,   -SymbSizeHalf/2,     (ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2     specSymbolWidth/2,    SymbSizeHalf/2,     (ss/2)*SIN(alf),     SymbSizeHalf/2
			line2   -specSymbolWidth/2,   -SymbSizeHalf/2,   -specSymbolWidth/2,   SymbSizeHalf/2
			line2    specSymbolWidth/2,   -SymbSizeHalf/2,    specSymbolWidth/2,   SymbSizeHalf/2
		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_2_WALL then
			CircleFill = 0
			gosub	2200
			gosub	2300

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf
			hotspot2     specSymbolWidth/2,   SymbSizeHalf

			fill gs_fill_type
			poly2_b 9,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf, 1,
				specSymbolWidth/2,   -SymbSizeHalf, 1,
				specSymbolWidth/2,   SymbSizeHalf, 1,
				-specSymbolWidth/2,   SymbSizeHalf, 1,
				-specSymbolWidth/2,   -SymbSizeHalf, -1,

				(ss/2)*SIN(120),   (ss/2)*COS(120), 1,
				0, 0, 901,
				0, -120, 4001,
				(ss/2)*SIN(120),   (ss/2)*COS(120), -1

			line2    -specSymbolWidth/2,   0,    -ss/2,	0
			line2    specSymbolWidth/2,   0,    ss/2,	0

		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_VERT_WALL then
			CircleFill = 2
			gosub	2200
			gosub	2300

			hotspot2     -SymbSizeHalf/2,    specSymbolDepth-ss/2
			hotspot2     SymbSizeHalf/2,    specSymbolDepth-ss/2


			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-SymbSizeHalf/2,	specSymbolDepth-ss/2,	1,
				-SymbSizeHalf/2,	(ss/2)*sin(alf),	1,
				0, 0, 901,
				SymbSizeHalf/2,	(ss/2)*sin(alf), 3001,
				SymbSizeHalf/2,	specSymbolDepth-ss/2, 1

		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_EMERGENCY_CEIL then
			gosub	2200

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   SymbSizeHalf/2

			line2    -specSymbolWidth/2,   -SymbSizeHalf/2,    -(ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2    -specSymbolWidth/2,    SymbSizeHalf/2,    -(ss/2)*SIN(alf),     SymbSizeHalf/2
			line2     specSymbolWidth/2,   -SymbSizeHalf/2,     (ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2     specSymbolWidth/2,    SymbSizeHalf/2,     (ss/2)*SIN(alf),     SymbSizeHalf/2
			line2   -specSymbolWidth/2,   -SymbSizeHalf/2,   -specSymbolWidth/2,   SymbSizeHalf/2
			line2    specSymbolWidth/2,   -SymbSizeHalf/2,    specSymbolWidth/2,   SymbSizeHalf/2

			fill    specSymbolSolidFill
			poly2_b    5,1+2+4, gs_cont_pen, gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf/2,	1,
				-(ss/2)*SIN(alf),    -SymbSizeHalf/2,	1,
				0, 0, 901,
				-(ss/2)*SIN(alf),    SymbSizeHalf/2,	3001,
				-specSymbolWidth/2,   SymbSizeHalf/2,	1

			poly2_b    5,1+2+4, gs_cont_pen, gs_cont_pen,
				specSymbolWidth/2,   -SymbSizeHalf/2,	1,
				(ss/2)*SIN(alf),    -SymbSizeHalf/2,	1,
				0, 0, 901,
				(ss/2)*SIN(alf),    SymbSizeHalf/2,	3001,
				specSymbolWidth/2,   SymbSizeHalf/2,	1
		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_LONGLAST_1_CEIL then
			gosub	2200

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   SymbSizeHalf/2

			line2    -specSymbolWidth/2,   -SymbSizeHalf/2,    -(ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2    -specSymbolWidth/2,    SymbSizeHalf/2,    -(ss/2)*SIN(alf),     SymbSizeHalf/2
			line2     specSymbolWidth/2,   -SymbSizeHalf/2,     (ss/2)*SIN(alf),    -SymbSizeHalf/2
			line2     specSymbolWidth/2,    SymbSizeHalf/2,     (ss/2)*SIN(alf),     SymbSizeHalf/2
			line2   -specSymbolWidth/2,   -SymbSizeHalf/2,   -specSymbolWidth/2,   SymbSizeHalf/2
			line2    specSymbolWidth/2,   -SymbSizeHalf/2,    specSymbolWidth/2,   SymbSizeHalf/2

			line2	(ss/2)*SIN(45),   (ss/2)*COS(45),		(ss/2)*SIN(225),   (ss/2)*COS(225)
			line2	(ss/2)*SIN(315),   (ss/2)*COS(315),		(ss/2)*SIN(135),   (ss/2)*COS(135)

			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,   -SymbSizeHalf/2,	1,
				-(ss/2)*SIN(alf),    -SymbSizeHalf/2,	1,
				0, 0, 901,
				-(ss/2)*SIN(alf),    SymbSizeHalf/2,	3001,
				-specSymbolWidth/2,   SymbSizeHalf/2,	1

			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				specSymbolWidth/2,   -SymbSizeHalf/2,	1,
				(ss/2)*SIN(alf),    -SymbSizeHalf/2,	1,
				0, 0, 901,
				(ss/2)*SIN(alf),    SymbSizeHalf/2,	3001,
				specSymbolWidth/2,   SymbSizeHalf/2,	1
		endif

		if iSpecSymbolType = SPEC_FLUORESCENT_LONGLAST_2_CEIL then
			CircleFill = 0
			gosub	2200

			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf
			hotspot2     specSymbolWidth/2,   SymbSizeHalf

			fill gs_fill_type
			poly2_b 4,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,	-SymbSizeHalf,	1,
				-specSymbolWidth/2,	SymbSizeHalf,	1,
				specSymbolWidth/2,	SymbSizeHalf,	1,
				specSymbolWidth/2,	-SymbSizeHalf,	1

			line2    -specSymbolWidth/2,   0,    -ss/2,	0
			line2    specSymbolWidth/2,   0,    ss/2,	0

			line2	(ss/2)*SIN(45),   (ss/2)*COS(45),		(ss/2)*SIN(225),   (ss/2)*COS(225)
			line2	(ss/2)*SIN(315),   (ss/2)*COS(315),		(ss/2)*SIN(135),   (ss/2)*COS(135)
		endif

		if iSpecSymbolType = SPEC_WALL_WASHER then
			gosub	2200

			add2	0,	-ss/2+(ss/3)/2
			hotspot2     0, (ss/3)/2
			hotspot2    -(ss/3)/2, 0
			hotspot2     (ss/3)/2, 0
			circle2	0,	0,	(ss/3)/2
			del	1

		endif

		if iSpecSymbolType = SPEC_SPOT then


			ss1= specSymbolDepth/3
			add2	0,	-specSymbolDepth/2+ss1/2
			hotspot2     0, -ss1/2
			hotspot2     0, ss1/2
			hotspot2    -ss1/2, 0
			hotspot2     ss1/2, 0

			hotspot2     -ss/2, 	specSymbolDepth-ss1/2
			hotspot2     ss/2, 	specSymbolDepth-ss1/2

			fill gs_fill_type
			poly2_b 2,1+2+4,gs_fill_pen,gs_back_pen,
				0,0, 901,
				ss1/2, 360, 4001

			poly2_b 3,1+2+4,gs_fill_pen,gs_back_pen,
				0,	ss1/2, 1,
				ss/2, 	specSymbolDepth-ss1/2, 1,
				-ss/2, 	specSymbolDepth-ss1/2, 1
			del	1

		endif

		if iSpecSymbolType = SPEC_OUTDOOR_HID then
			gosub	2200

			ss1= ss*2/3

			hotspot2     ss/2,	ss/2
			hotspot2     ss/2,	-ss/2
			hotspot2     -ss/2,	ss/2
			hotspot2     -ss/2,	-ss/2

			circle2	0,	0,	ss1/2
			line2	ss/2,		ss/2,		(ss1/2)*SIN(45),   (ss1/2)*COS(45)
			line2	ss/2,		-ss/2,	(ss1/2)*SIN(135),   (ss1/2)*COS(135)
			line2	-ss/2,	-ss/2,	(ss1/2)*SIN(225),   (ss1/2)*COS(225)
			line2	-ss/2,	ss/2,		(ss1/2)*SIN(315),   (ss1/2)*COS(315)
		endif

		if	iSpecSymbolType = SPEC_EMERGENCY_FLOURESCENT_CEIL then
			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   SymbSizeHalf/2


			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,				-SymbSizeHalf/2, 1,
				-specSymbolWidth/2,    			SymbSizeHalf/2, 1,
				-(ss/2)*SIN(alf),		SymbSizeHalf/2, 1,
				0,0, 901,
				-(ss/2)*SIN(alf),		-SymbSizeHalf/2, 3001
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				specSymbolWidth/2,				-SymbSizeHalf/2, 1,
				specSymbolWidth/2,    			SymbSizeHalf/2, 1,
				(ss/2)*SIN(alf),		SymbSizeHalf/2, 1,
				0,0, 901,
				(ss/2)*SIN(alf),		-SymbSizeHalf/2, 3001
			gosub	2200
			add2	-ss,	0
			gosub	2200
			gosub	2400
			del	1
		endif


		if iSpecSymbolType = SPEC_EMERGENCY_FLOURESCENT_WALL then
			hotspot2     -specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     -specSymbolWidth/2,   SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   -SymbSizeHalf/2
			hotspot2     specSymbolWidth/2,   SymbSizeHalf/2

			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-specSymbolWidth/2,				-SymbSizeHalf/2, 1,
				-specSymbolWidth/2,    			SymbSizeHalf/2, 1,
				-(ss/2)*SIN(alf),		SymbSizeHalf/2, 1,
				0,0, 901,
				-(ss/2)*SIN(alf),		-SymbSizeHalf/2, 3001
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				specSymbolWidth/2,				-SymbSizeHalf/2, 1,
				specSymbolWidth/2,    			SymbSizeHalf/2, 1,
				(ss/2)*SIN(alf),		SymbSizeHalf/2, 1,
				0,0, 901,
				(ss/2)*SIN(alf),		-SymbSizeHalf/2, 3001

			CircleFill = 2
			gosub	2200
			gosub	2300
			add2	-ss,	0
			gosub	2200
			gosub	2400
			del	1
		endif

		if iSpecSymbolType = SPEC_EMERGENCY_VERT_WALL then
			add2	0,	-ss
			gosub	2200
			gosub	2300

			hotspot2     -SymbSizeHalf/2,    specSymbolDepth-ss/2
			hotspot2     SymbSizeHalf/2,    specSymbolDepth-ss/2

			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-SymbSizeHalf/2,	(ss/2)*sin(alf),	1,
				0,		0,					901,
				SymbSizeHalf/2,	(ss/2)*sin(alf),	3001,
				SymbSizeHalf/2,	specSymbolDepth-ss/2,			1,
				-SymbSizeHalf/2,	specSymbolDepth-ss/2,			1
			del	1

			gosub	2200
			gosub	2400
		endif

		if iSpecSymbolType = SPEC_EMERGENCY_VERT_WALL_CIRC then
			add2	0,	-ss
			gosub	2200
			gosub	2300

			hotspot2     -SymbSizeHalf/2,    specSymbolDepth-ss/2
			hotspot2     SymbSizeHalf/2,    specSymbolDepth-ss/2

			fill gs_fill_type
			poly2_b 5,1+2+4,gs_fill_pen,gs_back_pen,
				-SymbSizeHalf/2,	(ss/2)*sin(alf),	1,
				0,		0,					901,
				SymbSizeHalf/2,	(ss/2)*sin(alf),	3001,
				SymbSizeHalf/2,	specSymbolDepth-ss/2,			1,
				-SymbSizeHalf/2,	specSymbolDepth-ss/2,			1
			del	1

			gosub	2200
			line2	(ss/2)*SIN(45),   (ss/2)*COS(45),		(ss/2)*SIN(225),   (ss/2)*COS(225)
			line2	(ss/2)*SIN(315),   (ss/2)*COS(315),		(ss/2)*SIN(135),   (ss/2)*COS(135)
		endif
	endif
endif


end

! ==============================================================================

"wall":
	add2 0, -r

	line2 -r, r, r, r
	hotspot2 -r, r
	hotspot2 r, r
	hotspot2 0, r
return

"wallspotold":
	add2 r, 0

	line2 -r, -r, -r, r
	hotspot2 -r, -r
	hotspot2 -r, r
	hotspot2 -r, r
return

"arrow":
	rot2 rz
	line2     0, -r,  0,    -4*r
	line2     0, -4*r,  0.8*r, -2.8*r
	line2     0, -4*r, -0.8*r, -2.8*r
	del 1
return

"sun":
	rot2 45
	for ii = 1 to 4
		line2 r, 0, r*1.4, 0
		rot2 90
	next ii
	del 4+1
return



! ==============================================================================
! Japanese subroutines
! ==============================================================================

2200:
hotspot2     0,-ss/2
hotspot2     0, ss/2
hotspot2    -ss/2, 0
hotspot2     ss/2, 0

fill gs_fill_type
if CircleFill then
	if CircleFill = 1 then	! full circle fill
		poly2_b 2, 1 + 2 + 4,gs_fill_pen,gs_back_pen,
			0,0, 901,
			ss/2, 360, 4001
	else
		poly2_b 3, 1 + 2 + 4,gs_fill_pen,gs_back_pen,
			0, 0, 901,
			(ss/2)*SIN(120),   (ss/2)*COS(120), 1,
			0, 240, 4001
	endif
else
	circle2 0,0, ss/2
endif

return


2300:
fill    specSymbolSolidFill
poly2_b    3,1+2+4, gs_cont_pen, gs_back_pen,
		0, 0, 901,
		(ss/2)*SIN(120),   (ss/2)*COS(120), 1,
		0, -120, 4001
return


2400:
fill    specSymbolSolidFill
poly2    24,1+2+4,
	(ss/2)*SIN(0),   (ss/2)*COS(0),
	(ss/2)*SIN(15),   (ss/2)*COS(15),
	(ss/2)*SIN(30),   (ss/2)*COS(30),
	(ss/2)*SIN(45),   (ss/2)*COS(45),
	(ss/2)*SIN(60),   (ss/2)*COS(60),
	(ss/2)*SIN(75),   (ss/2)*COS(75),
	(ss/2)*SIN(90),   (ss/2)*COS(90),
	(ss/2)*SIN(105),   (ss/2)*COS(105),
	(ss/2)*SIN(120),   (ss/2)*COS(120),
	(ss/2)*SIN(135),   (ss/2)*COS(135),
	(ss/2)*SIN(150),   (ss/2)*COS(150),
	(ss/2)*SIN(165),   (ss/2)*COS(165),
	(ss/2)*SIN(180),   (ss/2)*COS(180),
	(ss/2)*SIN(195),   (ss/2)*COS(195),
	(ss/2)*SIN(210),   (ss/2)*COS(210),
	(ss/2)*SIN(225),   (ss/2)*COS(225),
	(ss/2)*SIN(240),   (ss/2)*COS(240),
	(ss/2)*SIN(255),   (ss/2)*COS(255),
	(ss/2)*SIN(270),   (ss/2)*COS(270),
	(ss/2)*SIN(285),   (ss/2)*COS(285),
	(ss/2)*SIN(300),   (ss/2)*COS(300),
	(ss/2)*SIN(315),   (ss/2)*COS(315),
	(ss/2)*SIN(330),   (ss/2)*COS(330),
	(ss/2)*SIN(345),   (ss/2)*COS(345)
return


