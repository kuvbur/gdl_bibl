
! Outlet_symbol macro

if gs_cont_pen<=0 then gs_cont_pen=1

pen gs_cont_pen

hotspot2  0,   0
hotspot2  A/2, 0
hotspot2 -A/2, 0
hotspot2  0,   A/2

arc2  0, 0, A/2, 0, 180

if bGang then
	if lGang<EPS then lGang=0.25*A
	hotspot2 0, A/2+lGang
	line2    0, A/2,   0, A/2+lGang
endif

if bInterlock then
	fill gs_fill_type2
	poly2_b 6, 7, gs_fill_pen2, gs_back_pen2,
			0,           0.6754848*A,  1,
			0.3377424*A, 0.6754848*A,  1,
			0.2236068*A, 0.4472136*A,  1,
			0,           0,            900,
			0,           A/2,          3001,
			0,           0.6754848*A,  1
endif

if nSwitches > 0 then
	rot2 -26.57
	hotspot2 0.247678*A, 1.368*A
	hotspot2 0, 1.368*A

	line2    0,         0.5*A,    0, 1.368*A
	for i = 1 to nSwitches
		line2 0, 1.368*A, 0.247678*A, 1.368*A
		add2 0, -0.125*A
	next i
	del nSwitches + 1
endif

if bPilotLight then
	if bDoubleArc then
		mul2 5/6, 5/6
	endif
	fill gs_fill_type
	POLY2_B 5, 1+2, gs_fill_pen, gs_back_pen,
			 0,    0.2886751346*A,      1,
			-A/2,				0,      1,
			 0,   				0,	  900,
			 0,				 -180,	 4001,
			 0,    0.2886751346*A,      1

	line2    0, 0.2886751346*A, -A/4, 0.4330127*A
	line2    0, 0.2886751346*A,  A/4, 0.4330127*A
	if bDoubleArc then
		del 1
	endif
endif

if bDoubleArc then
	arc2 0, 0, 0.417*A, 0, 180
endif

if bText then
	! Oriented View
	angleViewRot = 0
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
	totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

	nTrans = 0

	! style
	pen tpen
	define style "textStyle"	fontType, 10/GLOB_SCALE*fsz, 5,
							gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline

	style "textStyle"
	textHeight = 10
	rrr = request("Height_of_style", "textStyle", textHeight)
	add2 0, (textHeight / 1000 * GLOB_SCALE) /2
	if bGang then
		add2 0, A*0.752
	else
		add2 0, A/2
	endif
	nTrans = nTrans + 2

	! Text Rotation
	if typeTextRotation_m = 2 then								! Readable
		if (totalRotate > (gs_readable_angle + EPS) & totalRotate < (gs_readable_angle + 180 + eps)) then
			rot2 180
			nTrans = nTrans + 1
		endif
	else
		if typeTextRotation_m = 1 then							! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED))	! 1/-1
			nTrans = nTrans + 1
		endif
	endif

	text2 0, 0, txt
	del nTrans
endif
