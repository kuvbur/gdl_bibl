
if gs_detlevel_3D_m=0 then end
if gs_detlevel_3D_m=1 then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 6

if not(gs_shadow) then shadow off

if not(bWall) then
	rotx rotAngleX
endif

if bWall then
	roty -90
endif

!!---- Geometry Hotspot Editing
unID = 1
hotspot 0,0,0,unID : unID=unID+1

if bGround then
	rotz 180
	addz 0.05
else
	addz -0.07
endif
hotspot 0,0,0,unID,rz,6 : unID=unID+1
hotspot 1,0,0,unID,rz,4+128 : unID=unID+1
hotspot 0,0,1,unID,rz,7 : unID=unID+1
hotspot 0.045*cos(rz),0.045*sin(rz),0,unID,rz,5 : unID=unID+1
if bGround then
	del 1
endif
del 1

if bGround then
	addz 0.135
else
	addz -0.155
endif
rotz rz
hotspot 0,0,0,unID,real_rx,6,rx : unID=unID+1
hotspot 0,-1,0,unID,real_rx,4+128,rx : unID=unID+1
hotspot -1,0,0,unID,real_rx,7,rx : unID=unID+1
hotspot 0,-0.083*cos(real_rx),0.083*sin(real_rx),unID,real_rx,5,rx : unID=unID+1
del 2

! --- Geometry

pen gs_cont_pen
resol gs_resol
material matFrame

if bGround then
	roty 180
	addz -0.01
	cylind 0.01,0.06
else
	addz -0.03
	prism_ 9,0.03,
			-0.005,-0.015,79,
			0.045,-0.015,79,
			0.055,-0.005,1079,
			0.055,0.005,79,
			0.045,0.015,1079,
			-0.005,0.015,79,
			-0.015,0.005,1079,
			-0.015,-0.005,79,
			-0.005,-0.015,1079
endif

addz -0.015
cylind 0.015,0.01

addz -0.01
cylind 0.01,0.035

addz -0.015
cylind 0.015,0.01

! --- UM ACHSE Z GEDREHTER TEIL

if bGround then
	rotz -rz
else
	rotz rz
endif

prism_ 6,0.01,
		0.045,-0.01,15,
		0.045,0.01,15,
		0,0.01,15,
		1,0,800,
		0,-0.01,1079,
		0,-0.01,15

addx 0.035

roty 90
prism_ 4,0.01,
		0,-0.01,15,
		0.085,-0.01,79,
		0.085,0.01,1079,
		0,0.01,15
del 1

addz 0.01
rotx 90
addz -0.0025

prism_ 5,0.005,
		0,0,15,
		0.01,0,15,
		0.01,0.0058,79,
		0.0061,0.0107,1079,
		0,0.0121,15
del 1

addy -0.095
roty -90

prism_ 13,0.005,
		0.01,0.06,15,
		0.01,0.07,15,
		0,0.07,79,
		-0.0606,-0.0350,1079,
		-0.0581,-0.0393,79,
		-0.0513,-0.0412,1079,
		0.0050,-0.0087,15,
		-0.0173,-0.01,800,
		-0.0087,-0.0050,1015,
		-0.0520,-0.0300,15,
		-0.01,0.0173,800,
		0,0.06,1079,
		0.01,0.06,79

cylind 0.015,0.01


! ---UM ACHSE X/Z GEDREHTER TEIL

resol 1.5*gs_resol

if bGround then
	rotz -rx
else
	rotz rx
endif
addz 0.035

addx -0.055
roty 90
cylind 0.065,0.025

addz -0.0025
roty 90
addz -0.005

cylind 0.04,0.0025

del 5

addx 0.01

revolve 6, 360, 0,
		0,		0.025,	2,
		0.045,	0.05,	2,
		0.07,	0.048,	2,
		0.07,	0.043,	2,
		0.045,	0.045,	2,
		0,		0.02,	2

addx 0.07

if not(gs_light_switch) then
	material matGlass

	revolve 2, 360, 2,
			0,		0.043,	0,
			0.003,	0.0355,	1
endif


!!---- Light Cone ----!!

roty -90

If gs_light_switch Then

	addz 0.067

	! ======================================================================
	! Non Visible Omni Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
											gs_color_red						= gs_color_red,
											gs_color_green						= gs_color_green,
											gs_color_blue						= gs_color_blue,
											gs_light_intensity						= gs_light_intensity,
											iColorPreset			= iColorPreset,
											iLightGroupType			= LIGHTGROUP_BUILTIN_SPOT,
											c4dDetUseInner			= 1,
											c4dDetInnerAngle		= c4dDetInnerAngle,
											innerConeAngleHalf		= innerConeAngleHalf,
											innerConeRadius			= innerConeRadius,
											c4dDetOuterAngle		= c4dDetOuterAngle,
											outerConeAngleHalf		= outerConeAngleHalf,
											outerConeRadius			= outerConeRadius,
											iC4dDetAreaShape		= AREASHAPE_SPHERE,
											c4dDetAreaX				= 0.03,
											c4dDetAreaY				= 0.03,
											c4dDetAreaZ				= 0.03,
											bGenShadow				= bGenShadow,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 3,
											bDetShowFalloff 		= 1,
											iFalloffType			= FALLOFFTYPE_SPOT,
											c4dDetRadius			= c4dDetRadius,
											c4dVisBrightness		= c4dVisBrightness,
											c4dPhoPhotometric		= c4dPhoPhotometric,
											iC4dPhoUnit				= iC4dPhoUnit,
											photoIntensityLumen		= photoIntensityLumen,
											photoIntensityCandela	= photoIntensityCandela,
											bShowIllumination3D		= bShowIllumination3D,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											penIllumination			= penIllumination,
											iModelType				= iModelType,
											lightUnID				= unID

	addz -0.064
	rotz 90

	! ======================================================================
	! Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch			= gs_light_switch,
											gs_color_red			= gs_color_red,
											gs_color_green			= gs_color_green,
											gs_color_blue			= gs_color_blue,
											gs_light_intensity		= 400,
											iLightGroupType			= LIGHTGROUP_BUILTIN_VIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_DISC,
											c4dDetAreaX				= 0.086,
											c4dDetAreaY				= 0.086,
											bGenShadow				= 1,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											c4dDetRadius			= 0,
											c4dDetFalloffAngle		= 0,
											gs_resol				= 1.5 * gs_resol,
											gs_cont_pen				= gs_cont_pen,
											bShowIllumination3D		= 0

	del 3
EndIf

del top
