
hideparameter all

call "FM_types" parameters all

ac_bottomlevel = 0
ac_toplevel = ZZYZX
parameters	ac_bottomlevel	= ac_bottomlevel,
			ac_toplevel		= ac_toplevel

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_SPOT,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes	= 1,
									bEnableSymbolic2D	= 1



values "gs_resol" range [3, )

if gs_cont_pen<=0 then parameters gs_cont_pen=10
if gs_fill_pen<=0 then parameters gs_fill_pen=91

if gs_detlevel_3D_m = DET3D_SIMPLE then
	lock "gs_resol"
	hideparameter "gs_resol"
endif

if gs_detlevel_3D_m = DET3D_OFF then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif


if bGround then
	parameters a=0.12, b=0.15, ZZYZX=0.185
else
	if bWall then
		parameters a=0.205, b=0.15, ZZYZX=0.105
	else
		parameters a=0.105, b=0.15, ZZYZX=0.205
	endif
endif
lock "A","B","ZZYZX"

values "rz" range [0, 360]

if bGround then
	values "rx" range [-30,90]
	values "real_rx" range[330,360], range [0,90]
else
	values "rx" range [-90,30]
	values "real_rx" range[270,360], range [0,30]
endif

! --- Font end

If GLOB_MODPAR_NAME="rx" Then
	If rx>=0 Then
		parameters real_rx=rx
	Else
		parameters real_rx=360+rx
	EndIf
EndIf

If GLOB_MODPAR_NAME="real_rx" Then
	if bGround then
		If real_rx>90 And real_rx<180 Then parameters real_rx=90, rx=90
		If real_rx>=180 And real_rx<=330 Then parameters real_rx=330, rx=-30
	else
		If real_rx>30 And real_rx<180 Then parameters real_rx=30, rx=30
		If real_rx>=180 And real_rx<=270 Then parameters real_rx=270, rx=-90
	endif
	If real_rx<180 Then
		parameters rx=real_rx
		rx=real_rx
	Else
		parameters rx=real_rx-360
		rx=real_rx-360
	EndIf
EndIf

if not(bWall) then
	values "rotAngleX" range (-90, 90)
endif


if (gs_light_switch) then lock "matGlass"


Dot_in_mm = 25.4 / 72


! --- Synhronized mm and point in marker width
if GLOB_MODPAR_NAME = "fsz" then
	parameters fsz_point = fsz / Dot_in_mm
	fsz_point = fsz / Dot_in_mm
endif
if GLOB_MODPAR_NAME = "fsz_point" then
	parameters fsz = fsz_point * Dot_in_mm
	fsz = fsz_point * Dot_in_mm
endif

values "gs_ui_current_page" pageValues

