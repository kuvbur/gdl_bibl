
values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3D_m = DET3D_SIMPLE then
	lock "gs_resol"
endif

if gs_detlevel_3D_m = DET3D_OFF then
	lock "gs_resol", "gs_shadow"
endif

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_OMNI,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes = 0,
									bEnableSymbolic2D	= 0

dim	iType[]
for i = 1 to vardim1(stType)
	iType[i] = i
next i

values "gs_symbolType" stType
values "gs_symbolType_m" iType

if GLOB_MODPAR_NAME = "gs_symbolType" then
	gs_symbolType_m = 1
	for i = 2 to vardim1(stType)
		if gs_symbolType = stType[i] then
			gs_symbolType_m	= i
			i				= vardim1(stType)	! break
		endif
	next i
	parameters gs_symbolType_m = gs_symbolType_m
else
	gs_symbolType_m	= max(1, min(gs_symbolType_m, vardim1(stType)))
	gs_symbolType	= stType[gs_symbolType_m]
	parameters gs_symbolType = gs_symbolType
endif


if not (bCalledAsMacro) then
	values "A" range (0,)
	values "B" range (0,)
	values "A2" range (0,)
	values "ZZYZX" range (0,)
	values "lsh" range (0, ZZYZX - 0.1)
	values "lud" range (0,)
	values "lud2" range (0,)


	If GLOB_MODPAR_NAME="A" Then
		parameters A2=A/2, B=A
	EndIf

	If GLOB_MODPAR_NAME="B" Then
		parameters A=B, A2=B/2
	EndIf

	If GLOB_MODPAR_NAME="A2" Then
		parameters A=A2*2, B=A2*2
	EndIf

	if gs_cont_pen=0 then parameters gs_cont_pen=10
	if gs_fill_pen=0 then parameters gs_fill_pen=91

	values "gs_resol" range [3, ]
endif

If GLOB_MODPAR_NAME="lud2" Then
	lud = lud2 * 2
	parameters lud = lud
else
	lud2 = lud / 2
	parameters lud2 = lud2
endif

values "gs_ui_current_page" pageValues

