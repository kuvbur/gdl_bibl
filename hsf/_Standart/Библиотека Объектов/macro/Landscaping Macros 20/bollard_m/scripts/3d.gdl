
! ==============================================================================
! bollard_m
! ==============================================================================


if gs_detlevel_3D_m = 0 then end
if not (gs_shadow) then shadow off

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then
	gs_resol = 6
	gs_detlevel_3D_m = 1
endif

if gs_detlevel_3D_m = 1 then resol 6
if gs_detlevel_3D_m = 2 then resol gs_resol

pen gs_cont_pen


! ==============================================================================
! 3D Hotspots
! ==============================================================================

Unid = bollardUnID
if not(bPartOfLayout) then
	hotspot 0,0,0,unID, ZZYZX,1+256 : unID=unID+1
	hotspot 0,0,-1,unID, ZZYZX,3 : unID=unID+1
	hotspot 0,0,ZZYZX,unID, ZZYZX,2 : unID=unID+1
endif

if bPartOfLayout then
	if bRotableLayout then
		if gs_terra_m = LENGTHWISE_SLOPE then
			ADDz max(bollardDepth/2,bollardDia/2)*tan(gs_terrx)
			ROTx gs_terrx
		endif
		if gs_terra_m = SIDEWISE_SLOPE then
			ADDz max(bollardWidth/2,bollardDia/2)*tan(gs_terry)
			ROTy gs_terry
		endif
	endif

	hotspot 0, yBollardPos, 0, unID, xBollardPos, 1+128 		: unID=unID+1
	hotspot -1, yBollardPos, 0, unID, xBollardPos, 3 			: unID=unID+1
	hotspot xBollardPos, yBollardPos, 0, unID, xBollardPos, 2	: unID=unID+1

	hotspot xBollardPos, 0, 0, unID, yBollardPos, 1+128 		: unID=unID+1
	hotspot xBollardPos, -1, 0, unID, yBollardPos, 3 			: unID=unID+1
	hotspot xBollardPos, yBollardPos, 0, unID, yBollardPos, 2 	: unID=unID+1

	if bRotableLayout then
		if gs_terra_m = LENGTHWISE_SLOPE or gs_terra_m = SIDEWISE_SLOPE then del 2
	endif
endif


! ==============================================================================
! Bollard Types 3D
! ==============================================================================

if bPartOfLayout then
	bollardOffsetRatX = 1
	bollardOffsetRatY = 1
	bollardOffsetZ = 0
	if bRotableLayout then
		if gs_terra_m = LENGTHWISE_SLOPE then
			bollardOffsetRatX = 1
			bollardOffsetRatY = cos(gs_terrx)
			bollardOffsetZ = yBollardPos*sin(gs_terrx)
		else
			if gs_terra_m = SIDEWISE_SLOPE then
				bollardOffsetRatX = cos(gs_terry)
				bollardOffsetRatY = 1
				bollardOffsetZ = -xBollardPos*sin(gs_terry)
			endif
		endif
	endif
	add xBollardPos*bollardOffsetRatX, yBollardPos*bollardOffsetRatY, bollardOffsetZ
endif

if iBollardType = BOLLARD_SPHERE then
	material BollardMat
	baseHeight = A/6
	baseDiameter = A/2
	baseAngle = asn(baseDiameter/A)

	roty -90
	revolve 2, 360, 1+2+4,
		0, baseDiameter/2, 0,
		baseHeight, baseDiameter/2, 0

	revolve 3, 360, 1+2+4,
		baseHeight, baseDiameter/2, 1,
		baseHeight + A/2*cos(baseAngle), 0, 900,
		baseHeight + A/2*cos(baseAngle) + A/2, 0, 3001
	del 1
endif

if iBollardType = BOLLARD_PRISM then
	material BollardMat
	add -A/2, -B/2, 0
	block A, B, ZZYZX
	del 1
endif

if iBollardType = BOLLARD_CYLINDER then
	material BollardMat
	recessWidth = A/6
	roty -90
	revolve 6, 360, 1+2+4,
		0, A/2, 0,
		ZZYZX*0.85, A/2, 0,
		ZZYZX*0.85, A/2 - recessWidth/2, 0,
		ZZYZX*0.85 + recessWidth, A/2 - recessWidth/2, 0,
		ZZYZX*0.85 + recessWidth, A/2, 0,
		ZZYZX, A/2, 0
	del 1
endif

if iBollardType = BOLLARD_TRUNCATEDCONE then
	material BollardMat
	recessWidth = A/6
	roty -90
	revolve 6, 360, 1+2+4,
		0, bollardBaseDia/2, 0,
		ZZYZX*0.85, bollardTopDia/2 + (bollardBaseDia - bollardTopDia)/2 * 0.15, 0,
		ZZYZX*0.85, bollardTopDia/2 + (bollardBaseDia - bollardTopDia)/2 * 0.15 - recessWidth/2, 0,
		ZZYZX*0.85 + recessWidth, bollardTopDia/2 + (bollardBaseDia - bollardTopDia)/2 * 0.15 - recessWidth/2, 0,
		ZZYZX*0.85 + recessWidth, bollardTopDia/2 + (bollardBaseDia - bollardTopDia)/2 * (0.15 - recessWidth/ZZYZX), 0,
		ZZYZX, bollardTopDia/2, 0
	del 1
endif

material BollardMat1
if iBollardType > BOLLARD_TRUNCATEDCONE then
	if gs_detlevel_3D_m = 0 then

		CYLIND zzyzx,A*0.5

	else

		if bBollardBase = 1 then
			CYLIND zzyzx*0.01,A*0.7
			tempHgt=0.01
		else
			tempHgt=0
		endif

		! --- Type 1 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE1 then

			ADDz zzyzx*tempHgt
			CYLIND zzyzx-0.145-zzyzx*tempHgt,A*0.25
			DEL 1

			ADDz zzyzx
			MUL A/0.1,A/0.1, 1
			ROTy 90

			REVOLVE 18, 360, 1+2+4,
				   0.001 ,   0.0001,                 1,
				   0.145, 0.0000000001,         1,
				   0.145,0.02949594285609,      1,
				   0.141,0.02949594285609,        1,
				   0.141,0.02636781867606,      1,
				   0.135,0.02636781867606,       1,
				   0.133,0.04999999999999,      1,
				   0.1277043968093,0.05,                  1,
				   0.1277043968093,0.04688230007212,      1,
				   0.09898875908975,0.02796603793029,      1,
				   0.04856068701758,0.01348711227696,      1,
				   0.02415895317025,0.01267898454638,      1,
				   0.02415895317025,0.02231266503429,      1,
				   0.01503087782967,0.02231266503429,      1,
				   0.01503087782967,0.01341089212763,      1,
				   0.01136079606199,0.01079271410895,      1,
				   0.0000000000001,0.003531173253517,      1,
				   0.00000000001,  0.000000000001,      1

			DEL 3

		endif

		! --- Type 2 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE2 then

			addz zzyzx*tempHgt
			CYLIND  zzyzx-zzyzx*tempHgt-0.04, A*0.35


			addz zzyzx-zzyzx*tempHgt-0.04
			ELLIPS 0.04, A*0.5
			ADDZ -0.075
			CYLIND  0.075, A*0.5
			ADDZ -0.075-0.025
			CYLIND  0.075, A*0.45
			ADDZ -0.075-0.025
			CYLIND  0.075, A*0.45
			ADDZ -0.075-0.025
			CYLIND  0.075, A*0.45

			DEL 6

		endif

		! --- Type 3 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE3 then

			ADDz zzyzx*tempHgt
			CYLIND zzyzx-zzyzx*tempHgt-A*0.8-0.14+0.06896245097668,a*0.5
			DEL 1

			ADDz zzyzx-A*0.8
			MUL a/0.2, a/0.2, 1
			ROTy 90

			ADDx -0.06896245097668
			REVOLVE 6, 360, 1+2+4,
					0.14,            0.01,      1 ,
					0.14, 0.1,      1,
					0.1301432187996, 0.1,      1 ,
					0.09539887997368, 0.1,    900,
							   0.01, -139.0414119842,   4001,
					0.06896245097668, 0.07455668617625,      1
			DEL 1

			DEL 3
			ADDz zzyzx-A/2
			SPHERE A/2
			DEL 1

		ENDIf

		! --- Type 4 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE4 then

			ADDz zzyzx*tempHgt
			CYLIND zzyzx-zzyzx*tempHgt-A*0.89-0.14,a*0.5
			DEL 1

			ADDz zzyzx-A*0.89
			MUL	a/0.2,a/0.2,1
			ROTy 90

			REVOLVE 20, 360, 1+2+4,
					 0.03632480476879, 0.0738421979779,      1,
					0.03282599448956, 0.06963492522465,    900,
							   0.01, 99.94210149313,   4001,
					0.02807782369099, 0.07235479497483,      1,
					0.02376250601043, 0.05968814172672,      1,
					0.01163340452885, 0.06385584721941,    900,
							   0.01, -135.9362136203,   4001,
					1.937721942044E-005, 0.05841532872571,      1,
					-1.942890293094E-016,            0.01,      1,
					0.14,            0.01,      1,
					0.14, 0.1,      1,
					0.1301432187996, 0.1,      1,
					0.09539887997368, 0.1,    900,
							   0.01, -139.0414119842,   4001,
					0.06896245097668, 0.07455668617625,      1,
					0.06897242213698, 0.08537452940194,      1,
					0.05264861342848, 0.08537452940194,    900,
							   0.01, 174.8627863352,   4001,
					0.03639037552697, 0.08683618294823,      1,
					0.03632480476879, 0.0738421979779,      1
			DEL 3
			ADDz zzyzx-A/2
			SPHERE A/2
			DEL 1

		endif

		! --- Type 5 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE5 then

			MATERIAL BollardMat1

			CYLIND zzyzx-a*0.4, a*0.4

			ADDz zzyzx*tempHgt
			CYLIND zzyzx-a*0.4-zzyzx*tempHgt,a*0.4

			DEL 1

			addz zzyzx-a*0.4
			CONE a*0.4, a*0.4, 0, 90, 90

			DEL 1

			MATERIAL BollardMat2
			addz zzyzx-a*0.4-0.05
			CYLIND 0.02, a*0.5
			addz -0.075
			CYLIND 0.02, a*0.5
			DEL 2
		endif

		! --- Type 6 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE6 then

			add 0,a*0.5, zzyzx
			cutplane 45
			del 1
			ADDz zzyzx*tempHgt
			cylind zzyzx-zzyzx*tempHgt,a*0.5
			DEL 1
			cutend

			MATERIAL BollardMat2
			ADDz zzyzx*tempHgt
			cylind zzyzx-zzyzx*tempHgt, a*0.45
			DEL 1

		endif


		! --- Type 7 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE7 then

			MATERIAL BollardMat1

			ADDz zzyzx*tempHgt
			CYLIND zzyzx-zzyzx*tempHgt-0.15,a*0.5
			DEL 1

			ADDz zzyzx-0.15
			MATERIAL BollardMat2
			CYLIND 0.05,a*0.4
			ADDz 0.05
			MATERIAL BollardMat1
			CYLIND 0.1,a*0.5
			DEL 2

		endif

		! --- Type 8 ---------------------------------------------------------------

		if iBollardType = BOLLARD_TYPE8 then

			MATERIAL BollardMat1
			addz zzyzx*tempHgt
			put a*0.485, 0, 79
			put a*0.25*cos(45/2), a*0.25*sin(45/2), 900
			put 0, 10, 4079
			for alfa=45/2 to 360 step 360/8/2
				put a*0.485*cos(alfa), a*0.485*sin(alfa), 1079
			next alfa
			resol gs_resol*2
			PRISM_ nsp/3, zzyzx-zzyzx*tempHgt-a*0.5,
				use(nsp)

			del 1
			body -1

			addz zzyzx-a*0.5
			FPRISM_ BollardMat1, BollardMat1, BollardMat1, BollardMat1,
					nsp/3, a*0.5, 0,a*0.5,
					use(nsp)
		endif

	endif	! --- End of Detailed Model
endif

if bPartOfLayout then del 1
