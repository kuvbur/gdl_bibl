
! ==============================================================================
! bollard_m
! ==============================================================================


dim stBollardTypes[12]
	stBollardTypes[1]	= `Сфера`
	stBollardTypes[2]	= `Призма`
	stBollardTypes[3]	= `Цилиндр`
	stBollardTypes[4]	= `Усеченный Конус`
if bPartOfLayout then
	stBollardTypes[5]	= `Дизайнерский 1`
	stBollardTypes[6]	= `Дизайнерский 2`
	stBollardTypes[7]	= `Дизайнерский 3`
	stBollardTypes[8]	= `Дизайнерский 4`
	stBollardTypes[9]	= `Дизайнерский 5`
	stBollardTypes[10]	= `Дизайнерский 6`
	stBollardTypes[11]	= `Дизайнерский 7`
	stBollardTypes[12]	= `Дизайнерский 8`
else
	stBollardTypes[5]	= `Тип 1`
	stBollardTypes[6]	= `Тип 2`
	stBollardTypes[7]	= `Тип 3`
	stBollardTypes[8]	= `Тип 4`
	stBollardTypes[9]	= `Тип 5`
	stBollardTypes[10]	= `Тип 6`
	stBollardTypes[11]	= `Тип 7`
	stBollardTypes[12]	= `Тип 8`
endif

! enableBollardTypes - possible values
!	2		- enable Sphere
!	4		- enable Prism
!	8		- enable Cylinder
!	16		- enable Truncated Cone
!	32		- enable Type 1
!	64		- enable Type 2
!	128		- enable Type 3
!	256		- enable Type 4
!	512		- enable Type 5
!	1024	- enable Type 6
!	2048	- enable Type 7
!	4096	- enable Type 8

if enableBollardTypes < EPS then enableBollardTypes = 1 + 2 + 4 + 8 + 16 + 32 + 64 + 128 + 256 + 512 + 1024 + 2048 + 4096

dim bollardTypeValueList[]
j = 1
for i = 1 to vardim1(stBollardTypes)
	if bittest(enableBollardTypes, i) then
		if i = 1 then put 1			! Sphere
		if i = 2 then put 2			! Prism
		if i = 3 then put 3			! Cylinder
		if i = 4 then put 4			! Truncated Cone
		if i = 5 then put 5			! Type 1
		if i = 6 then put 6			! Type 2
		if i = 7 then put 7			! Type 3
		if i = 8 then put 8			! Type 4
		if i = 9 then put 9			! Type 5
		if i = 10 then put 10		! Type 6
		if i = 11 then put 11		! Type 7
		if i = 12 then put 12		! Type 8

		bollardTypeValueList[j] = stBollardTypes[i]
		j = j + 1
	endif
next i

values "bollardType" bollardTypeValueList
values "iBollardType" get(NSP)

if GLOB_MODPAR_NAME = "BollardType" then
	for hi = 1 to vardim1(stBollardTypes)
		if bollardType = stBollardTypes[hi] then
			iBollardType = hi
		endif
	next hi
	parameters iBollardType = iBollardType
else
	bollardType = stBollardTypes[max(1, min(iBollardType, vardim1(stBollardTypes)))]
	parameters bollardType = bollardType
endif


! ==============================================================================
! Macro for Bollard object
! ==============================================================================

if not(bPartOfLayout) then

	call "FM_types" parameters all

	dim stDetail_3D[3]
		stDetail_3D[1] = `Откл.`
		stDetail_3D[2] = `Простой`
		stDetail_3D[3] = `Детальный`

	values "gs_detlevel_3D" stDetail_3D[3], stDetail_3D[2], stDetail_3D[1]
	values "gs_detlevel_3D_m" 2, 1, 0

	if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
		gs_detlevel_3D_m = 2
		if gs_detlevel_3D = stDetail_3D[1] then gs_detlevel_3D_m = 0
		if gs_detlevel_3D = stDetail_3D[2] then gs_detlevel_3D_m = 1
		parameters gs_detlevel_3D_m = gs_detlevel_3D_m
	else
		gs_detlevel_3D = stDetail_3D[3]
		if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetail_3D[1]
		if gs_detlevel_3D_m = 1 then gs_detlevel_3D = stDetail_3D[2]
		parameters gs_detlevel_3D = gs_detlevel_3D
	endif

	if gs_detlevel_3D_m = 1 then lock "bBollardBase", "BollardMat2"
	if iBollardType = BOLLARD_TYPE1 | iBollardType = BOLLARD_TYPE2 | iBollardType = BOLLARD_TYPE3 |\
		iBollardType = BOLLARD_TYPE4 | iBollardType = BOLLARD_TYPE8 then
			lock "BollardMat2"
	endif

	if gs_cont_pen = 0 then parameters gs_cont_pen=2
	if gs_fill_pen = 0 then parameters gs_fill_pen=91

	values "A" range (0,)
	values "B" range (0,)
	values "temp_a" range (0,)
	values "gs_resol" range [3,)

	if iBollardType = BOLLARD_TYPE1 then values "ZZYZX" range [0.01*bBollardBase+0.145+0.01,)
	if iBollardType = BOLLARD_TYPE2 then values "ZZYZX" range [0.01*bBollardBase+0.4+0.04+0.01,)
	if iBollardType = BOLLARD_TYPE3 then values "ZZYZX" range [0.01*bBollardBase+A*0.89+0.14+0.01,)
	if iBollardType = BOLLARD_TYPE4 then values "ZZYZX" range [0.01*bBollardBase+A*0.89+0.15+0.01,)
	if iBollardType = BOLLARD_TYPE5 then values "ZZYZX" range [0.01*bBollardBase+A+0.1+0.01,)
	if iBollardType = BOLLARD_TYPE6 then values "ZZYZX" range [0.01*bBollardBase+0.15+0.01,)
	if iBollardType = BOLLARD_TYPE7 then values "ZZYZX" range [0.01*bBollardBase+0.15+0.01,)
	if iBollardType = BOLLARD_TYPE8 then values "ZZYZX" range (0,)

	if GLOB_MODPAR_NAME = "A" then
		B = A
		parameters B = B
	else
		A = B
		parameters A = A
	endif
	if GLOB_MODPAR_NAME = "temp_a" then
		A = temp_a * 2
		parameters A = A
	else
		temp_a = A/2
		parameters temp_a = temp_a
	endif
	if GLOB_MODPAR_NAME = "B" then
		temp_a = B/2
		parameters temp_a = temp_a
	else
		B = temp_a * 2
		parameters B = B
	endif

	values "gs_ui_current_page" 1
endif


! ==============================================================================
! Macro for Parking object
! ==============================================================================

if bPartOfLayout then

	values "bollardDia" range (0, )
	values "bollardWidth" range (0, )
	values "bollardDepth" range (0, )
	values "bollardBaseDia" range (0, )
	values "bollardTopDia" range (0, )

	if iBollardType < BOLLARD_TYPE1 then values "bollardHeight" range [0.1,)
	if iBollardType = BOLLARD_TYPE1 then values "bollardHeight" range [0.01*bBollardBase+0.145+0.01,)
	if iBollardType = BOLLARD_TYPE2 then values "bollardHeight" range [0.01*bBollardBase+0.4+0.04+0.01,)
	if iBollardType = BOLLARD_TYPE3 then values "bollardHeight" range [0.01*bBollardBase+A*0.89+0.14+0.01,)
	if iBollardType = BOLLARD_TYPE4 then values "bollardHeight" range [0.01*bBollardBase+A*0.89+0.15+0.01,)
	if iBollardType = BOLLARD_TYPE5 then values "bollardHeight" range [0.01*bBollardBase+A+0.1+0.01,)
	if iBollardType = BOLLARD_TYPE6 then values "bollardHeight" range [0.01*bBollardBase+0.15+0.01,)
	if iBollardType = BOLLARD_TYPE7 then values "bollardHeight" range [0.01*bBollardBase+0.15+0.01,)
	if iBollardType = BOLLARD_TYPE8 then values "bollardHeight" range (0,)

	if	iBollardType = BOLLARD_SPHERE | iBollardType = BOLLARD_PRISM |\
		iBollardType = BOLLARD_CYLINDER | iBollardType = BOLLARD_TRUNCATEDCONE then
		lock "BollardMat1", "BollardMat2"
		hideparameter "BollardMat1", "BollardMat2"
	else
		if iBollardType <> BOLLARD_TYPE5 & iBollardType <> BOLLARD_TYPE6 & iBollardType <> BOLLARD_TYPE7 then
			lock "BollardMat2"
			hideparameter "BollardMat2"
		endif
		lock "BollardMat"
		hideparameter "BollardMat"
	endif

	if iBollardType = BOLLARD_PRISM | iBollardType = BOLLARD_TRUNCATEDCONE then
		lock "bollardDia"
		hideparameter "bollardDia"
	endif

	if iBollardType <> BOLLARD_PRISM then
		lock "bollardWidth", "bollardDepth"
		hideparameter "bollardWidth", "bollardDepth"
	endif

	if iBollardType <> BOLLARD_TRUNCATEDCONE then
		lock "bollardBaseDia", "bollardTopDia"
		hideparameter "bollardBaseDia", "bollardTopDia"
	endif

	if iBollardType = BOLLARD_SPHERE then
		lock "bollardHeight"
		hideparameter "bollardHeight"
	endif

	if iBollardType < BOLLARD_TYPE1 then
		lock "bBollardBase"
		hideparameter "bBollardBase"
	endif
endif
