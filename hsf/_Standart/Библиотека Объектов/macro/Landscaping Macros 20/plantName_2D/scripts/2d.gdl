
! ==============================================================================
! TEXT
! ======================================================================
nTrans = 0
if txt_show then

	unID = 100
	hotspot2 txt_pos_x,	0,			UnID, txt_pos_y, 1+128	: unID=unID+1
	hotspot2 txt_pos_x,	-1,			UnID, txt_pos_y, 3		: unID=unID+1
	hotspot2 txt_pos_x,	txt_pos_y,	UnID, txt_pos_y, 2		: unID=unID+1

	hotspot2 0,			txt_pos_y,	UnID, txt_pos_x, 1+128	: unID=unID+1
	hotspot2 -1,		txt_pos_y,	UnID, txt_pos_x, 3		: unID=unID+1
	hotspot2 txt_pos_x,	txt_pos_y,	UnID, txt_pos_x, 2		: unID=unID+1

	pen txt_pen

	define style fe1 fontType, fontSizeFixed_mm, 5, bBold + bItalic * 2 + bUnderlined * 4
	style fe1
	add2 txt_pos_x, txt_pos_y
	nTrans = nTrans + 1

	! --- Oriented View ------------------------------ [

	angleViewRot = 0
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
	totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

	! Text Rotation - Readable / Always Horizontal / Align with Symbol
	! ----------------------------------------------------------------------
	if typeTextRotation_m = 2 then								! Readable
		if (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps)) then
			rot2 180
			nTrans = nTrans + 1
		endif
	else
		if typeTextRotation_m = 1 then							! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED)) ! 1/-1
			nTrans = nTrans + 1
		endif
	endif
	! ----------------------------------------------------------------------

	text2 0,0,txt
	del nTrans
endif

! ==============================================================================
end
! ==============================================================================
