
if bWindow then
	ui_dialog `Параметры Паспорта Окна`
else
	ui_dialog `Параметры Паспорта Двери`
endif


! ==============================================================================
! Tabpage IDs, Titles and Icons
! ==============================================================================

PAGE_ID_CONTENT_SC		= 1
PAGE_ID_CONTENT_1		= 2
PAGE_ID_CONTENT_2		= 3
PAGE_ID_CONTENT_3		= 4
PAGE_ID_ADD_TAGS		= 5
PAGE_ID_TEXT_SETT		= 6
PAGE_ID_FRAME_DIM		= 7
PAGE_ID_SIZE_POS		= 8

dim tabTitles[8]
	tabTitles[1] = `Данные согласно Масштабу`
	tabTitles[2] = `Раздел Данных 1`
	tabTitles[3] = `Раздел Данных 2`
	tabTitles[4] = `Раздел Данных 3`
	tabTitles[5] = `Дополнительные Метки`
	tabTitles[6] = `Текст`
	tabTitles[7] = `Рамка и Единицы Измерения`
	tabTitles[8] = `Размеры и Расположение Паспорта`

dim tabIcons[8]
	tabIcons[1] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[2] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[3] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[4] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[5] = "uiTab_additionalTags_flat(1)"
	tabIcons[6] = "uiTab_TextMarkerLabelZone_flat(1)"
	tabIcons[7] = "uiTab_Attributes_flat(1)"
	tabIcons[8] = "uiTab_MarkerGeom_flat(1)"

file_dependence "uiTab_MarkerLabelContent_flat(1)"
file_dependence "uiTab_additionalTags_flat(1)"
file_dependence "uiTab_TextMarkerLabelZone_flat(1)"
file_dependence "uiTab_Attributes_flat(1)"
file_dependence "uiTab_MarkerGeom_flat(1)"

ui_current_page gs_ui_current_page
ui_page gs_ui_current_page

! ==============================================================================
! Call tabpage selector
! ==============================================================================
call "ui_tab_popup" parameters 	nTabs 				= vardim1(tabTitles),
								gs_ui_current_page 	= gs_ui_current_page,
								tabTitles			= tabTitles,
								tabIcons			= tabIcons


! ==============================================================================
! Tabpages
! ==============================================================================

px		= 30
py		= 20
cx		= px
cy		= py
itemID	= 1
UIGroup	= 1

y0 = 35


ui_page PAGE_ID_CONTENT_SC
	gosub "ContentByScalePage"

ui_page PAGE_ID_CONTENT_1
	gosub "Content1Page"

ui_page PAGE_ID_CONTENT_2
	gosub "Content2Page"

ui_page PAGE_ID_CONTENT_3
	gosub "Content3Page"

ui_page PAGE_ID_ADD_TAGS
	gosub "AdditionalTagsPage"

ui_page PAGE_ID_TEXT_SETT
	gosub "TextSettingsPage"

ui_page PAGE_ID_FRAME_DIM
	gosub "FrameAndDimensionsPage"

ui_page PAGE_ID_SIZE_POS
	gosub "SizeAndPositionPage"


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"ContentByScalePage":
! ------------------------------------------------------------------------------

	y = y0
	y2 = y0 + 130
	dy = 20
	x0 = 1
	x1 = 215
	xsep = 222
	x2 = 230
	x3 = 443
	xwidth = 136

	ui_style 0, 1
	ui_outfield `Детализация`, x0, y, 168, 16
	ui_infield{3}  "iDetLevel", x0+169, y, 150, 16,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stDetLevels[1],	DETLEV_SCALESENS,
		"",	stDetLevels[2],	DETLEV_DETAILED,
		"",	stDetLevels[3],	DETLEV_SIMPLE

	y = y + dy + 1

	ui_separator 0, y, 444, y
	ui_separator 0, y2 - 4, 444, y2 - 4

	y = y + 4

	if bMetricDimensionNeeded then
		ui_outfield `Показ до 1:50`, x0, y, x1-x0-1, 16
		ui_outfield `Показ от 1:50`, x0, y2, x3-x2-1, 16
	else
		ui_outfield `Показ в Детализированном`, x0, y, x1-x0-1, 16
		ui_outfield `Показ в Среднем и Простом`, x0, y2, x3-x2-1, 16
	endif

	ui_style 1, 0

	dim paramNames[13]
		paramNames[1] = "bShowID"				! "bShowID_detailed" "bShowID_simple"
		paramNames[2] = "bShowWallholeDim"		! "bShowWallholeDim_detailed" "bShowWallholeDim_simple"
		paramNames[3] = "bShowUnitDim"			! "bShowUnitDim_detailed" "bShowUnitDim_simple"
		if bWindow then
			paramNames[4] = "bShowRevealDim"	! "bShowRevealDim_detailed" "bShowRevealDim_simple"
			paramNames[5] = "bShowSill"			! "bShowSill_detailed" "bShowSill_simple"
		else
			paramNames[4] = "bShowEgressDim"	! "bShowEgressDim_detailed" "bShowEgressDim_simple"
			paramNames[5] = "bShowThreshold"	! "bShowThreshold_detailed" "bShowThreshold_simple"
		endif

		paramNames[6] = "bShowFire"				! "bShowFire_detailed" "bShowFire_simple"
		paramNames[7] = "bShowAcoustic"			! "bShowAcoustic_detailed" "bShowAcoustic_simple"
		paramNames[8] = "bShowUValue"			! "bShowUValue_detailed" "bShowUValue_simple"

		paramNames[9] = "bShowMaterial"			! "bShowMaterial_detailed" "bShowMaterial_simple"
		paramNames[10] = "bShowHardware"		! "bShowHardware_detailed" "bShowHardware_simple"
		if bWindow then
			paramNames[11] = "bShowCust1"		! "bShowCust1_detailed" "bShowCust1_simple"
			paramNames[12] = "bShowCust2"		! "bShowCust2_detailed" "bShowCust2_simple"
		else
			paramNames[11] = "bShowHandle"		! "bShowHandle_detailed" "bShowHandle_simple"
			paramNames[12] = "bShowCust"		! "bShowCust_detailed" "bShowCust_simple"
		endif
		paramNames[13] = "bShowAdditionalTags"		! "bShowAdditionalTags_detailed" "bShowAdditionalTags_simple"

	dim paramDesc[13]
		paramDesc[1] = `ID`
		paramDesc[2] = `Размеры Проема`
		paramDesc[3] = `Размеры Блока`
		if bWindow then
			paramDesc[4] = `Размеры Четверти`
			paramDesc[5] = `Высота Отлива`
		else
			paramDesc[4] = `Размеры Прохода`
			paramDesc[5] = `Высота Порога`
		endif

		paramDesc[6] = `Огнестойкость`
		paramDesc[7] = `Шумозащита`
		paramDesc[8] = `U-значение`

		paramDesc[9] = `Материал`
		paramDesc[10] = `Фурнитура`
		if bWindow then
			paramDesc[11] = `Специальный Текст 1`
			paramDesc[12] = `Специальный Текст 2`
		else
			paramDesc[11] = `Ручка`
			paramDesc[12] = `Специальный Текст`
		endif
		paramDesc[13] = `Дополнительные Метки`

	y = y + dy - 1
	y2 = y2 + dy - 1

	ui_listfield UIGroup, 0, y, 444, 84, 0 : UIGroup = UIGroup + 1
	ui_listfield UIGroup, 0, y2, 444, 84, 0 : UIGroup = UIGroup + 1

	for i = 1 to 13
		NameSuffix	= "_detailed"
		UIGroup		= 1
		for k =1 to 2
			ui_listitem itemID, UIGroup, paramNames[i]+NameSuffix, 0, "", paramDesc[i]
			itemID		= itemID + 1
			UIGroup		= 2
			NameSuffix	= "_simple"
		next k
	next i

return


! =============================================================================
"Content1Page":
! ------------------------------------------------------------------------------

	y = 40
	dy = 27

	x1 = 1
	x2 = 15
	x3 = 180
	x4 = 310
	x5 = 325
	x6 = 440

	ui_style 0, 1
	ui_outfield `ID`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iIDText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	sIDTextValues[1], IDVALUE_IDNUMBER,
		"",	sIDTextValues[3], IDVALUE_FULLIDNUM,
		"",	sIDTextValues[2], IDVALUE_CUSTOMTEXT

	if iIDText = IDVALUE_CUSTOMTEXT then
		ui_infield "strIDValue", x5, y-3, x6-x5, 20
	endif

	y = y + dy
	ui_separator 0, y, 444, y
	y = y + dy/2

	ui_style 0, 1
	ui_outfield `Размеры Проема`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iWHDimText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stMeasuredCustom[1], DIM_MEASURED,
		"",	stMeasuredCustom[2], DIM_CUSTOM

	if iWHDimText = DIM_CUSTOM then
		ui_infield "strWHDimValue", x5, y-3, x6-x5, 20
	endif

	y = y + dy

	ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
	ui_infield "strWHPrefix", x3, y-3, x4-x3, 18
	y = y + dy

	ui_style 0, 1
	ui_outfield `Размеры Блока`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iUnitDimText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stMeasuredCustom[1], DIM_MEASURED,
		"",	stMeasuredCustom[2], DIM_CUSTOM

	if iUnitDimText = DIM_CUSTOM then
		ui_infield "strUnitDimValue", x5, y-3, x6-x5, 20
	endif

	y = y + dy

	ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
	ui_infield "strUnitPrefix", x3, y-3, x4-x3, 18
	y = y + dy

	if bWindow then
		ui_style 0, 1
		ui_outfield `Размеры Четверти`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_infield{3} "iRevealDimText", x3, y-3, x4-x3, 20,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stMeasuredCustom[1], DIM_MEASURED,
			"",	stMeasuredCustom[2], DIM_CUSTOM

		if iRevealDimText = DIM_CUSTOM then
			ui_infield "strRevealDimValue", x5, y-3, x6-x5, 20
		endif
		y = y + dy

		ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
		ui_infield "strRevealPrefix", x3, y-3, x4-x3, 18
		y = y + dy
	else
		ui_style 0, 1
		ui_outfield `Размеры Прохода`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_infield{3} "iEgressDimText", x3, y-3, x4-x3, 20,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stMeasuredCustom[1], DIM_MEASURED,
			"",	stMeasuredCustom[2], DIM_CUSTOM

		if iEgressDimText = DIM_CUSTOM then
			ui_infield "strEgressDimValue", x5, y-3, x6-x5, 20
		endif
		y = y + dy

		ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
		ui_infield "strEgressPrefix", x3, y-3, x4-x3, 18
		y = y + dy
	endif


	ui_style 0, 1
	ui_outfield `Разделитель Значений`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield "GS_Separator", x3, y-3, x4-x3, 18

return


! =============================================================================
"Content2Page":
! ------------------------------------------------------------------------------

	y = 40
	dy = 22

	x1 = 1
	x2 = 15
	x3 = 180
	x4 = 310
	x5 = 325
	x6 = 440

	if bWindow then
		ui_style 0, 1
		ui_outfield `Высота Отлива`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_infield{3} "iSillText", x3, y-3, x4-x3, 20,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stMeasuredCustom[1], DIM_MEASURED,
			"",	stMeasuredCustom[2], DIM_CUSTOM

		if iSillText = DIM_CUSTOM then
			ui_infield "strSillValue", x5, y-3, x6-x5, 20
		endif
		y = y + dy

		ui_outfield `Высота Измеряется от`, x2, y, x3-x2-1, 18
		ui_infield{3} "iShowSillTo", x3, y-3, x4-x3, 20,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stSillToOptions[1], SILL_TO_ANCHOR,
			"",	stSillToOptions[2], SILL_TO_WALLBASE,
			"",	stSillToOptions[3], SILL_TO_CURRSTORY

		y = y + dy

		ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
		ui_infield "strSillPrefix", x3, y-3, x4-x3, 20

		y = y + dy

		ui_infield{3} "bSillNominal", x2, y, x4-x2, 18,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",	`Номинальная Высота Отлива`, 0,
			"",	`Номинальная Высота Отлива`, 1

		y = y + dy
	else
		ui_style 0, 1
		ui_outfield `Высота Порога`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_infield{3} "iThresholdText", x3, y-3, x4-x3, 20,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stMeasuredCustom[1], DIM_MEASURED,
			"",	stMeasuredCustom[2], DIM_CUSTOM

		if iThresholdText = DIM_CUSTOM then
			ui_infield "strThresholdValue", x5, y-3, x6-x5, 20
		endif
		y = y + dy

		ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
		ui_infield "strThresholdPrefix", x3, y-3, x4-x3, 20

		y = y + dy
		y = y + dy
		y = y + dy
	endif

	ui_separator 0, y, 444, y

	y = y + 9
	ui_style 0, 1
	ui_outfield `Огнестойкость`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iFireText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stAutoCustom[1], VALUE_AUTOMATIC,
		"",	stAutoCustom[2], VALUE_CUSTOM

	if iFireText = VALUE_CUSTOM then
		ui_infield "strFireValue", x5, y-3, x6-x5, 20
	endif
	y = y + dy

	ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
	ui_infield "strFirePrefix", x3, y-3, x4-x3, 20

	y = y + dy
	ui_style 0, 1
	ui_outfield `Шумозащита`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iAcousticText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stAutoCustom[1], VALUE_AUTOMATIC,
		"",	stAutoCustom[2], VALUE_CUSTOM

	if iAcousticText = VALUE_CUSTOM then
		ui_infield "strAcousticValue", x5, y-3, x6-x5, 20
	endif
	y = y + dy

	ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
	ui_infield "strAcousticPrefix", x3, y-3, x4-x3, 20

	y = y + dy
	ui_style 0, 1
	ui_outfield `U-значение`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iUValueText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stAutoCustom[1], VALUE_AUTOMATIC,
		"",	stAutoCustom[2], VALUE_CUSTOM

	if iUValueText = VALUE_CUSTOM then
		ui_infield "strUValueValue", x5, y-3, x6-x5, 20
	endif
	y = y + dy

	ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
	ui_infield "strUValuePrefix", x3, y-3, x4-x3, 20

return


! =============================================================================
"Content3Page":
! ------------------------------------------------------------------------------

	y = 40
	dy = 27

	x1 = 1
	x2 = 15
	x3 = 180
	x4 = 310
	x5 = 325
	x6 = 440

	ui_style 0, 1
	ui_outfield `Материал`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iMaterialText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stMaterials[1], 1,
		"",	stMaterials[2], 2,
		"",	stMaterials[3], 3,
		"",	stMaterials[4], 4

	if iMaterialText = 4 then
		ui_infield "strMaterialValue", x5, y-3, x6-x5, 20
	endif
	y = y + dy

	ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
	ui_infield "strMaterialPrefix", x3, y-3, x4-x3, 20

	y = y + dy
	ui_style 0, 1
	ui_outfield `Фурнитура`, x1, y, x3-x1-1, 18
	ui_style 0, 0

	ui_infield{3} "iHardwareText", x3, y-3, x4-x3, 20,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"",	stAutoCustom[1], VALUE_AUTOMATIC,
		"",	stAutoCustom[2], VALUE_CUSTOM

	if iHardwareText = VALUE_CUSTOM then
		ui_infield "strHardwareValue", x5, y-3, x6-x5, 20
	endif
	y = y + dy

	ui_outfield `Отображаемое Имя`, x2, y, x3-x2-1, 18
	ui_infield "strHardwarePrefix", x3, y-3, x4-x3, 20

	y = y + dy

	if bWindow then
		ui_style 0, 1
		ui_outfield `Специальный Текст 1`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_outfield `Отображаемое Имя 1`, x3, y, x4-x3, 20
		ui_infield "strCust1Prefix", x5, y-3, x6-x5, 20
		y = y + dy

		ui_outfield `Текст 1`, x3, y, x4-x3, 20
		ui_infield "strCust1Value", x5, y-3, x6-x5, 20
		y = y + dy

		ui_style 0, 1
		ui_outfield `Специальный Текст 2`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_outfield `Отображаемое Имя 2`, x3, y, x4-x3, 20
		ui_infield "strCust2Prefix", x5, y-3, x6-x5, 20
		y = y + dy

		ui_outfield `Текст 2`, x3, y, x4-x3, 20
		ui_infield "strCust2Value", x5, y-3, x6-x5, 20
		y = y + dy
	else
		ui_style 0, 1
		ui_outfield `Ручка`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_outfield `Отображаемое Имя`, x3, y, x4-x3, 20
		ui_infield "strHandlePrefix", x5, y-3, x6-x5, 20
		y = y + dy

		ui_outfield `Текст`, x3, y, x4-x3, 20
		ui_infield "strHandleValue", x5, y-3, x6-x5, 20
		y = y + dy

		ui_style 0, 1
		ui_outfield `Специальный Текст`, x1, y, x3-x1-1, 18
		ui_style 0, 0

		ui_outfield `Отображаемое Имя`, x3, y, x4-x3, 20
		ui_infield "strCustPrefix", x5, y-3, x6-x5, 20
		y = y + dy

		ui_outfield `Текст`, x3, y, x4-x3, 20
		ui_infield "strCustValue", x5, y-3, x6-x5, 20
		y = y + dy
	endif

return


! =============================================================================
"AdditionalTagsPage":
! ------------------------------------------------------------------------------
	call "TnCParamRequest" parameters all	bShowAdditionalTags	= 1,
											bDrawUiPage			= 1,
											UISubPageTop		= 42,
											UISubPageBot		= 266,
											UIGroup				= 3,
											iUIElemType			= 3 + bWindow,	! 3: Door, 4: Window
											itemID				= itemID

return


! =============================================================================
"TextSettingsPage":
! ------------------------------------------------------------------------------

	y = 40
	dy = 28

	ui_style 0, 1
	ui_infield{3} "GS_UniTextFormat", 5, y, 250, 18,
		7, "",
		0, 0,
		0, 0, 0, 0,
		"",	`Единые Параметры Текста`, 0,
		"",	`Единые Параметры Текста`, 1
	ui_style 0, 0

	y = y + dy

	ui_separator 0, y, 444, y

	y = y + dy /2 - 2


	if GS_UniTextFormat then

		ui_outfield `Тексты`, 5, y, 250, 18

		y = y + dy

		ui_infield{3} "iAlignment", 64, y, 74, 52,
			2, 1,		! ui_IDAlign
			3, 1,
			59, 38, 59, 38,
			1, stIDAligns[1], IDALIGN_LEFT,
			2, stIDAligns[2], IDALIGN_CENTER,
			3, stIDAligns[3], IDALIGN_RIGHT

		x1 = 205
		x2 = 320

		x3 = 340
		x4 = 380
		x5 = 420

		ui_infield "fontType2", x1, y, x2-x1-1, 18

		ui_pict "ui_pen_flat(1)", x4+8, y, 13, 19, 1
		ui_infield "TextPen_U", x5+24-35, y, 35, 22

		y = y + 52-24

		ui_pict "ui_FontSize_flat(1)", x1, y+3, 27, 14, 1
		if bMetricDimensionNeeded then
			ui_infield "AC_TextSize_1", x1+30, y+4, x2-x1-31, 18
		else
			ui_infield "TextSize_U_pt", x1+30, y+4, x2-x1-31, 18
		endif

		ui_infield{3} "bBoldText", x3, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_bold_flat(1)", "", 0,
			"ui_bold_flat(1)", "", 1\
			ui_tooltip `Жирный`

		ui_infield{3} "bItalicText", x4, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_italic_flat(1)", "", 0,
			"ui_italic_flat(1)", "", 1\
			ui_tooltip `Курсив`

		ui_infield{3} "bUnderText", x5, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_underline_flat(1)", "", 0,
			"ui_underline_flat(1)", "", 1\
			ui_tooltip `Подчеркнутый`

	else

		ui_outfield `Текст ID`, 5, y, 250, 18

		y = y + dy

		ui_infield{3} "iIDAlignment", 64, y, 74, 52,
			2, 1,		! ui_IDAlign
			3, 1,
			59, 38, 59, 38,
			1, stIDAligns[1], IDALIGN_LEFT,
			2, stIDAligns[2], IDALIGN_CENTER,
			3, stIDAligns[3], IDALIGN_RIGHT

		x1 = 205
		x2 = 320

		x3 = 340
		x4 = 380
		x5 = 420

		ui_infield "AC_TextFont_1", x1, y, x2-x1-1, 18

		ui_pict "ui_pen_flat(1)", x4+8, y, 13, 19, 1
		ui_infield "AC_TextPen_1", x5+24-35, y, 35, 22

		y = y + 52-24

		ui_pict "ui_FontSize_flat(1)", x1, y+3, 27, 14, 1
		if bMetricDimensionNeeded then
			ui_infield "AC_TextSize_1", x1+30, y+4, x2-x1-31, 18
		else
			ui_infield "TextSize_1_pt", x1+30, y+4, x2-x1-31, 18
		endif

		ui_infield{3} "bBoldIDText", x3, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_bold_flat(1)", "", 0,
			"ui_bold_flat(1)", "", 1\
			ui_tooltip `Жирный`

		ui_infield{3} "bItalicIDText", x4, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_italic_flat(1)", "", 0,
			"ui_italic_flat(1)", "", 1\
			ui_tooltip `Курсив`

		ui_infield{3} "bUnderIDText", x5, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_underline_flat(1)", "", 0,
			"ui_underline_flat(1)", "", 1\
			ui_tooltip `Подчеркнутый`

		y = y + 35

		!ui_separator 0, y, 444, y

		y = y + dy /2 - 2

		ui_outfield `Формат Текста Данных`, 5, y, 250, 18, 4-4*not(GS_UniTextFormat)

		y = y + dy

		ui_infield{3} "iBoxAlignment", 64, y, 74, 52,
			2, 2,		! ui_boxAlign
			4, 1,
			59, 38, 59, 38,
			1, stBoxAligns[1], BOXALIGN_LEFT,
			2, stBoxAligns[2], BOXALIGN_CENTER,
			3, stBoxAligns[3], BOXALIGN_SIDE,
			4, stBoxAligns[4], BOXALIGN_RIGHT


		x1 = 205
		x2 = 320

		x3 = 340
		x4 = 380
		x5 = 420

		ui_infield "AC_TextFont_2", x1, y, x2-x1-1, 18

		if GS_UniTextFormat then
			ui_pict "ui_pen_lock_flat(1)", x4+8, y, 13, 19, 1
		else
			ui_pict "ui_pen_flat(1)", x4+8, y, 13, 19, 1
		endif
		ui_infield "AC_TextPen_2", x5+24-35, y, 35, 22

		y = y + 52-24

		if GS_UniTextFormat then
			ui_pict "ui_FontSize_hide_flat(1)", x1, y+3, 27, 14, 1
		else
			ui_pict "ui_FontSize_flat(1)", x1, y+3, 27, 14, 1
		endif

		if bMetricDimensionNeeded then
			ui_infield "AC_TextSize_2", x1+30, y+4, x2-x1-31, 18
		else
			ui_infield "TextSize_2_pt", x1+30, y+4, x2-x1-31, 18
		endif

		ui_infield{3} "bBoldDetailText", x3, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_bold_flat(1)", "", 0,
			"ui_bold_flat(1)", "", 1\
			ui_tooltip `Жирный`

		ui_infield{3} "bItalicDetailText", x4, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_italic_flat(1)", "", 0,
			"ui_italic_flat(1)", "", 1\
			ui_tooltip `Курсив`

		ui_infield{3} "bUnderDetailText", x5, y, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_underline_flat(1)", "", 0,
			"ui_underline_flat(1)", "", 1\
			ui_tooltip `Подчеркнутый`

	endif

return


! =============================================================================
"FrameAndDimensionsPage":
! ------------------------------------------------------------------------------

	y = 40
	dy = 28

	x1 = 1
	x2 = 93
	x3 = x2 + 98
	x4 = 310
	x5 = 360
	x6 = 440

	ui_outfield `Стиль Рамки`, x1, y, x2-x1-1, 18

	ui_infield{3} "iFrameStyle", x2, y, 78, 94,
		2, 3,			! ui_frameTypes
		7, 1,
		64, 78, 64, 78,
		1, stFrameStyles[1], FRAME_NONE,
		2, stFrameStyles[2], FRAME_STYLE1,
		3, stFrameStyles[3], FRAME_STYLE2,
		4, stFrameStyles[4], FRAME_STYLE3,
		5, stFrameStyles[5], FRAME_STYLE4,
		6, stFrameStyles[6], FRAME_STYLE5,
		7, stFrameStyles[7], FRAME_STYLE6

	ui_outfield `Тип Линии`, x3, y, x4-x3-1, 18
	ui_infield "gs_line_type", x4, y-2, x6-x4, 26

	y = y + dy

	ui_outfield `Тип Штриховки`, x3, y, x4-x3-1, 18
	ui_infield "AC_MarkerFill", x4, y-1, x6-x4, 24

	y = y + dy

	ui_outfield `Перо Штриховки`, x3, y, x4-x3-1, 18
	ui_infield "gs_fill_pen", x6-35, y-1, 35, 22

	y = y + dy

	ui_outfield `Перо Фона Штриховки`, x3, y, x4-x3-1, 18
	ui_infield "gs_back_pen", x6-35, y-3, 35, 22

	y = y + dy

	ui_separator 0, y, 444, y

	y = y + 19

	ui_outfield `Единицы Размеров`,  x1, y,	x4-x1-142, 15
	ui_infield{3} "iWorkUnitLinear", x4-141, y-4, 140, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", stUnitFormats[1],	DIMUNIT_MM,
			"", stUnitFormats[2],	DIMUNIT_CM,
			"", stUnitFormats[3],	DIMUNIT_DM,
			"", stUnitFormats[4],	DIMUNIT_METER,
			"", stUnitFormats[5],	DIMUNIT_INCH,
			"", stUnitFormats[6],	DIMUNIT_PROJECT

	ui_outfield `Десятичные`,  x4, y,	x6-x4-41, 15, 4 * _bLockLinearUnitDecimal
	ui_infield{3} "iWorkUnitLinearDecimal", x6-40, y-4,	40, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4

	y = y + dy

	if bWindow then
		ui_outfield `Единицы Высоты Отлива`,  x1, y,	x4-x1-142, 15
	else
		ui_outfield `Единицы Высоты Порога`,  x1, y,	x4-x1-142, 15
	endif

	ui_infield{3} "iWorkUnitSillHgt", x4-141, y-4, 140, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", stUnitFormats[1],	DIMUNIT_MM,
			"", stUnitFormats[2],	DIMUNIT_CM,
			"", stUnitFormats[3],	DIMUNIT_DM,
			"", stUnitFormats[4],	DIMUNIT_METER,
			"", stUnitFormats[5],	DIMUNIT_INCH,
			"", stUnitFormats[6],	DIMUNIT_PROJECT

	ui_outfield `Десятичные`,  x4, y,	x6-x4-41, 15, 4 * _bLockSillHgtUnitDecimal
	ui_infield{3} "iWorkUnitSillHgtDecimal", x6-40 , y-4,	40, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4

return


! =============================================================================
"SizeAndPositionPage":
! ------------------------------------------------------------------------------

	y = 40
	dy = 28

	x1 = 1
	x2 = 93
	x3 = x2 + 98
	x4 = 310
	x5 = 360
	x6 = 440


	if bWindow then
		ui_outfield `Ширина Паспорта Окна`, x1, y, x3-x1-1, 15
	else
		ui_outfield `Ширина Паспорта Двери`, x1, y, x3-x1-1, 15
	endif

	ui_infield{3} "iStampWidth", x3, y-4, x5-x3-10, 19,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"", stAutoFixed[1], VALUE_AUTOMATIC,
		"", stAutoFixed[2], VALUE_CUSTOM

	if bMetricDimensionNeeded then
		ui_infield "stampWidth_mm", x5, y-4, x6-x5, 19
	else
		ui_infield "stampWidth_pt", x5, y-4, x6-x5, 19
	endif

	y = y + dy

	ui_outfield `Межстрочное Расстояние ID`, x1, y, x3-x1-1, 15

	ui_infield{3} "iIDLineSpacing", x3, y-4, x5-x3-10, 19,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"", stAutoFixed[1], VALUE_AUTOMATIC,
		"", stAutoFixed[2], VALUE_CUSTOM

	if bMetricDimensionNeeded then
		ui_infield "IDLineSpacing_mm", x5, y-4, x6-x5, 19
	else
		ui_infield "IDLineSpacing_pt", x5, y-4, x6-x5, 19
	endif

	y = y + dy

	ui_outfield `Межстрочное Расстояние Данных`, x1, y, x3-x1-1, 15

	ui_infield{3} "iDetailLineSpacing", x3, y-4, x5-x3-10, 19,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"", stAutoFixed[1], VALUE_AUTOMATIC,
		"", stAutoFixed[2], VALUE_CUSTOM

	if bMetricDimensionNeeded then
		ui_infield "detailLineSpacing_mm", x5, y-4, x6-x5, 19
	else
		ui_infield "detailLineSpacing_pt", x5, y-4, x6-x5, 19
	endif

	y = y + dy

	ui_separator 0, y, 444, y

	y = y + 19

	ui_outfield	`По Горизонтали`,	x1, y, x3-x1-1, 15
	ui_infield	"markerPositionHor",	x5-90, y-4, x6-x5, 19

	y = y + dy


	ui_outfield	`По Вертикали`,	x1, y, x3-x1-1, 15
	ui_infield	"markerPositionVert",	x5-90, y-4, x6-x5, 19

return

