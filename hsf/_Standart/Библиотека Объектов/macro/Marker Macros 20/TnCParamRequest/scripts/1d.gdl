
DIM _stSelectParams[2]
	_stSelectParams[1]	= `Отсортировать`
	_stSelectParams[2]	= `Только Категории`

PARAMSELECTION_SORTING	= 1
PARAMSELECTION_CATONLY	= 2

DIM _stDisplayDataBy[5]
	_stDisplayDataBy[1]	= `Имя Параметра и Значение`
	_stDisplayDataBy[2]	= `Название Параметра`
	_stDisplayDataBy[3]	= `Значение Параметра`
	_stDisplayDataBy[4]	= `Группа / Набор Свойств IFC и Имя`
	_stDisplayDataBy[5]	= `Группа / Набор Свойств IFC, Имя, Значение`

DISPLAYDATABY_NAMEVALUE			= 1
DISPLAYDATABY_NAME				= 2
DISPLAYDATABY_VALUE				= 3
DISPLAYDATABY_GROUPNAME			= 4
DISPLAYDATABY_GROUPNAMEVALUE	= 5

DIM _stdisplayNameAs[2]
	_stdisplayNameAs[1]	= `Автоматически`
	_stdisplayNameAs[2]	= `Специальный`

DISPLAYNAMEAS_AUTOMATIC	= 1
DISPLAYNAMEAS_CUSTOM	= 2

! ----- in case of display none type at sorting -------
! hiding parameter name display without clearing the custom name values
dim _isEmpty[20]
for iEmpty = 1 to 20
	_isEmpty[iEmpty] = 0
next iEmpty

if iTnCSelectParams = PARAMSELECTION_SORTING then
	if sIDRow1 = "" then _isEmpty[1] = 1
	if sIDRow2 = "" then _isEmpty[2] = 1
	if sIDRow3 = "" then _isEmpty[3] = 1
	if sIDRow4 = "" then _isEmpty[4] = 1
	if sIDRow5 = "" then _isEmpty[5] = 1
	if sIDRow6 = "" then _isEmpty[6] = 1
	if sIDRow7 = "" then _isEmpty[7] = 1
	if sIDRow8 = "" then _isEmpty[8] = 1
	if sIDRow9 = "" then _isEmpty[9] = 1
	if sIDRow10 = "" then _isEmpty[10] = 1
	if sIDRow11 = "" then _isEmpty[11] = 1
	if sIDRow12 = "" then _isEmpty[12] = 1
	if sIDRow13 = "" then _isEmpty[13] = 1
	if sIDRow14 = "" then _isEmpty[14] = 1
	if sIDRow15 = "" then _isEmpty[15] = 1
	if sIDRow16 = "" then _isEmpty[16] = 1
	if sIDRow17 = "" then _isEmpty[17] = 1
	if sIDRow18 = "" then _isEmpty[18] = 1
	if sIDRow19 = "" then _isEmpty[19] = 1
	if sIDRow20 = "" then _isEmpty[20] = 1
endif


!================================================
! T&C parameters Reguest
!================================================

DIM _sTnCParamData[][5]
	_sTnCParamData[1][1] = ""
	_sTnCParamData[1][2] = ""
	_sTnCParamData[1][3] = ""
	_sTnCParamData[1][4] = ""
	_sTnCParamData[1][5] = ""
!	_sTnCParamData[1..n][GroupString][idString][NameString][ParamValueString][TypeString]

DIM _sTnCShortData[][5]
	_sTnCShortData[1][1] = ""
	_sTnCShortData[1][2] = ""
	_sTnCShortData[1][3] = ""
	_sTnCShortData[1][4] = ""
	_sTnCShortData[1][5] = ""
!	_sTnCShortData[1..n][GroupString][idString][NameString][ParamValueString][TypeString]

DIM __DisplayDataByWorkData[][6]


PROPERTY_FOLDER		= 1			! NEW: GROUP
PROPERTY_ID			= 2			! NEW: ID
PROPERTY_NAME		= 3			! NEW: NAME
PROPERTY_VALUE		= 4			! NEW: VALUE
PROPERTY_TYPE		= 5			! BRAND NEW TYPE



! --------------------------------------------------
! Request of Sub parameters
! --------------------------------------------------

dim _parentProperties[]
if GLOB_SCRIPT_TYPE <> 5 then
	n = REQUEST ("Properties_Of_Parent", "", _parentProperties)
endif
! _parentProperties == [Id1, TypeName1, GroupName1, PropertyName1, Id2, TypeName2, GroupName2, PropertyName2, ...]
_numProperties = vardim1(_parentProperties) / 4


_sumParamNum = 1

for i = 1 to _numProperties
	_sTnCParamData[_sumParamNum][PROPERTY_FOLDER]		= _parentProperties[4 * (i - 1) + 3]
	_sTnCParamData[_sumParamNum][PROPERTY_ID]			= _parentProperties[4 * (i - 1) + 1]
	_sTnCParamData[_sumParamNum][PROPERTY_NAME]			= _parentProperties[4 * (i - 1) + 4]
	_sTnCParamData[_sumParamNum][PROPERTY_VALUE]		= ""
	_sTnCParamData[_sumParamNum][PROPERTY_TYPE]			= _parentProperties[4 * (i - 1) + 2]
	_sumParamNum = _sumParamNum + 1
next i


! --------------------------------------------------
! Custom Ordering
! --------------------------------------------------
_sumParamNum = vardim1(_sTnCParamData)

NONE_VALUE = 1
sInvalidString = `Нет`
sInvalidID = ""

if iTnCSelectParams = PARAMSELECTION_SORTING then

	dim _custSortParamID[]
	dim	_custSortParamLongName[]
		for iRow = 1 to _sumParamNum
			_custSortParamID[iRow]			= _sTnCParamData[iRow][PROPERTY_ID]
			_custSortParamLongName[iRow]	= _sTnCParamData[iRow][PROPERTY_NAME]
		next iRow


	dim _tempIDCustomOrdering[20]
		_tempIDCustomOrdering[1] = sIDRow1
		_tempIDCustomOrdering[2] = sIDRow2
		_tempIDCustomOrdering[3] = sIDRow3
		_tempIDCustomOrdering[4] = sIDRow4
		_tempIDCustomOrdering[5] = sIDRow5
		_tempIDCustomOrdering[6] = sIDRow6
		_tempIDCustomOrdering[7] = sIDRow7
		_tempIDCustomOrdering[8] = sIDRow8
		_tempIDCustomOrdering[9] = sIDRow9
		_tempIDCustomOrdering[10] = sIDRow10
		_tempIDCustomOrdering[11] = sIDRow11
		_tempIDCustomOrdering[12] = sIDRow12
		_tempIDCustomOrdering[13] = sIDRow13
		_tempIDCustomOrdering[14] = sIDRow14
		_tempIDCustomOrdering[15] = sIDRow15
		_tempIDCustomOrdering[16] = sIDRow16
		_tempIDCustomOrdering[17] = sIDRow17
		_tempIDCustomOrdering[18] = sIDRow18
		_tempIDCustomOrdering[19] = sIDRow19
		_tempIDCustomOrdering[20] = sIDRow20


	for iCustomRow = 1 to 20
		! initialization custom array
		_sTnCShortData[iCustomRow][PROPERTY_FOLDER]		= ""
		_sTnCShortData[iCustomRow][PROPERTY_ID]			= sInvalidID
		_sTnCShortData[iCustomRow][PROPERTY_NAME]		= sInvalidString
		_sTnCShortData[iCustomRow][PROPERTY_VALUE]		= ""
		_sTnCShortData[iCustomRow][PROPERTY_TYPE]		= ""

		for iTnCRow = 1 to vardim1(_sTnCParamData)
			if _tempIDCustomOrdering[iCustomRow]	= _sTnCParamData[iTnCRow][PROPERTY_ID] then

				_sTnCShortData[iCustomRow][PROPERTY_FOLDER]		= _sTnCParamData[iTnCRow][PROPERTY_FOLDER]
				_sTnCShortData[iCustomRow][PROPERTY_ID]			= _sTnCParamData[iTnCRow][PROPERTY_ID]
				_sTnCShortData[iCustomRow][PROPERTY_NAME]		= _sTnCParamData[iTnCRow][PROPERTY_NAME]
				_sTnCShortData[iCustomRow][PROPERTY_VALUE]		= _sTnCParamData[iTnCRow][PROPERTY_VALUE]
				_sTnCShortData[iCustomRow][PROPERTY_TYPE]		= _sTnCParamData[iTnCRow][PROPERTY_TYPE]
			endif
		next iTnCRow

	next iCustomRow
endif

! ==================================================
! Fill _sPropertytoDisplay[][]
! - description types depend on a boolean:	
!	long name (Property.name (Property.Set)) PROPERTY_NAME colum
! ==================================================

dim _sPropertytoDisplay[][]

if iTnCSelectParams = PARAMSELECTION_SORTING then

	dim _tempiTypeRowName[20]
		_tempiTypeRowName[1] = iRow1CustomName
		_tempiTypeRowName[2] = iRow2CustomName
		_tempiTypeRowName[3] = iRow3CustomName
		_tempiTypeRowName[4] = iRow4CustomName
		_tempiTypeRowName[5] = iRow5CustomName
		_tempiTypeRowName[6] = iRow6CustomName
		_tempiTypeRowName[7] = iRow7CustomName
		_tempiTypeRowName[8] = iRow8CustomName
		_tempiTypeRowName[9] = iRow9CustomName
		_tempiTypeRowName[10] = iRow10CustomName
		_tempiTypeRowName[11] = iRow11CustomName
		_tempiTypeRowName[12] = iRow12CustomName
		_tempiTypeRowName[13] = iRow13CustomName
		_tempiTypeRowName[14] = iRow14CustomName
		_tempiTypeRowName[15] = iRow15CustomName
		_tempiTypeRowName[16] = iRow16CustomName
		_tempiTypeRowName[17] = iRow17CustomName
		_tempiTypeRowName[18] = iRow18CustomName
		_tempiTypeRowName[19] = iRow19CustomName
		_tempiTypeRowName[20] = iRow20CustomName

	dim _tempRowCustomName[20]
		_tempRowCustomName[1] = sRow1CustomNameValue
		_tempRowCustomName[2] = sRow2CustomNameValue
		_tempRowCustomName[3] = sRow3CustomNameValue
		_tempRowCustomName[4] = sRow4CustomNameValue
		_tempRowCustomName[5] = sRow5CustomNameValue
		_tempRowCustomName[6] = sRow6CustomNameValue
		_tempRowCustomName[7] = sRow7CustomNameValue
		_tempRowCustomName[8] = sRow8CustomNameValue
		_tempRowCustomName[9] = sRow9CustomNameValue
		_tempRowCustomName[10] = sRow10CustomNameValue
		_tempRowCustomName[11] = sRow11CustomNameValue
		_tempRowCustomName[12] = sRow12CustomNameValue
		_tempRowCustomName[13] = sRow13CustomNameValue
		_tempRowCustomName[14] = sRow14CustomNameValue
		_tempRowCustomName[15] = sRow15CustomNameValue
		_tempRowCustomName[16] = sRow16CustomNameValue
		_tempRowCustomName[17] = sRow17CustomNameValue
		_tempRowCustomName[18] = sRow18CustomNameValue
		_tempRowCustomName[19] = sRow19CustomNameValue
		_tempRowCustomName[20] = sRow20CustomNameValue


	for iCustomRow = 1 to 20
		if _tempiTypeRowName[iCustomRow] = DISPLAYNAMEAS_CUSTOM then
			_sTnCShortData[iCustomRow][PROPERTY_NAME]	= _tempRowCustomName[iCustomRow]
		endif
	next iCustomRow

	! None displaying by design ------------------------------------------------------------------
	! "None" would be displayed only between two non-empty rows as empty string on 2D


	for iCustomRow = 1 to 20
		if _sTnCShortData[iCustomRow][PROPERTY_ID]	= "" then	! None Type
			_sTnCShortData[iCustomRow][PROPERTY_NAME]	= ""
		endif
	next iCustomRow

	! None displaying by design ------------------------------------------------------------------
	! at the begin and at the end the empty rows are not displyed

    nFrontNoneRows = 0
    bOnlyNone = 1
    for jFront = 1 to 20
	    if _sTnCShortData[jFront][PROPERTY_ID] = "" & bOnlyNone = 1 then
		    nFrontNoneRows = nFrontNoneRows + 1
	    else
		    bOnlyNone = 0
	    endif
    next jFront
    
    nBackNoneRows = 0
    bOnlyNone = 1
    for jBack = 20 to 1 step -1
	    if _sTnCShortData[jBack][PROPERTY_ID] = "" & bOnlyNone = 1 then
		    nBackNoneRows = nBackNoneRows + 1
	    else
		    bOnlyNone = 0
	    endif
    next jBack

	iRow = 1
	for j = (nFrontNoneRows+1) to (20 - nBackNoneRows)
		_sPropertytoDisplay[iRow][PROPERTY_FOLDER] 		=	_sTnCShortData[j][PROPERTY_FOLDER]
		_sPropertytoDisplay[iRow][PROPERTY_ID] 			=	_sTnCShortData[j][PROPERTY_ID]
		_sPropertytoDisplay[iRow][PROPERTY_NAME] 		=	_sTnCShortData[j][PROPERTY_NAME]
		_sPropertytoDisplay[iRow][PROPERTY_VALUE] 		=	_sTnCShortData[j][PROPERTY_VALUE]
		_sPropertytoDisplay[iRow][PROPERTY_TYPE] 		=	_sTnCShortData[j][PROPERTY_TYPE]
		iRow = iRow + 1
	next j

else
	_displayRow = 1
	for iRow = 1 to vardim1(_sTnCParamData)
		if _sTnCParamData[iRow][PROPERTY_FOLDER] = "" & _sTnCParamData[iRow][PROPERTY_TYPE] = "" then
			for iCol = 1 to vardim2(_sTnCParamData) 
				_sPropertytoDisplay[_displayRow][iCol] = _sTnCParamData[iRow][iCol]
			next iCol
			_displayRow = _displayRow + 1
		endif
	next iRow
endif

! ==================================================
! Fill _sUsedProperty[][]
! - collect neccessery properties
! - the ordering is fixed
! ==================================================

nColumn = 1
dim _sUsedProperty[][]
	if bUsePropertyFolder then
		for i = 1 to vardim1(_sPropertytoDisplay)
			_sUsedProperty[i][nColumn] = _sPropertytoDisplay[i][PROPERTY_FOLDER]
		next i
		nColumn = nColumn + 1
	endif
	if bUsePropertyID then
		for i = 1 to vardim1(_sPropertytoDisplay)
			_sUsedProperty[i][nColumn] = _sPropertytoDisplay[i][PROPERTY_ID]
		next i
		nColumn = nColumn + 1
	endif
	if bUsePropertyName then
		for i = 1 to vardim1(_sPropertytoDisplay)
			_sUsedProperty[i][nColumn] = _sPropertytoDisplay[i][PROPERTY_NAME]
		next i
		nColumn = nColumn + 1
	endif
	if bUsePropertyValue then
		for i = 1 to vardim1(_sPropertytoDisplay)
			type = 0
			dim _propValues[]
			if GLOB_SCRIPT_TYPE = 2 then
				n = REQUEST ("Property_Value_Of_Parent", _sPropertytoDisplay[i][PROPERTY_ID], _typeValue, dim1, dim2, _propValues)
			endif

			propValStr = ""
			for j = 1 to vardim1 (_propValues)	
				if j > 1 then
					propValStr = propValStr + "; "
				endif

				if _typeValue = 4 then
					propValStr = propValStr + _propValues[j]
				else
					if _typeValue = 3 then
						propValStr = propValStr + str ("%.2", _propValues[j])
					else
						if _typeValue = 2 then
							propValStr = propValStr + str ("%.0", _propValues[j])
						else
							if _propValues[j] > 0 then
								propValStr = propValStr + `ИСТИНА`
							else
								propValStr = propValStr + `ЛОЖЬ`
							endif
						endif
					endif
				endif
			next j

			_sUsedProperty[i][nColumn] = propValStr
		next i
		nColumn = nColumn + 1
	endif
	if bUsePropertyType then
		for i = 1 to vardim1(_sPropertytoDisplay)
			_sUsedProperty[i][nColumn] = _sPropertytoDisplay[i][PROPERTY_TYPE]
		next i
		nColumn = nColumn + 1
	endif

goto "MasterEnd"

"MasterEnd":


