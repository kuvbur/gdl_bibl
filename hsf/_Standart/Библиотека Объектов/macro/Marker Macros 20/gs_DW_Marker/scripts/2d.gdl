
if gs_hide_marker then end

! ==============================================================================
! Initializations
! ==============================================================================

PAPER_TO_MODEL = GLOB_SCALE / 1000	! Convert paper size to model size

AC_MarkerSize		= AC_MarkerSize * PAPER_TO_MODEL

headDistModel			= headDistM			* GLOB_SCALE
footLengthModel			= footLengthM		* GLOB_SCALE
markerOffsetModel		= markerOffsetM		* GLOB_SCALE
whDistModel				= whDistM			* GLOB_SCALE
sillDistModel			= sillDistM			* GLOB_SCALE
sillOffsetModel			= sillOffsetM		* GLOB_SCALE
fireDistModel			= fireDistM			* GLOB_SCALE
fireOffsetModel			= fireOffsetM		* GLOB_SCALE
acousticDistModel		= acousticDistM		* GLOB_SCALE
acousticOffsetModel		= acousticOffsetM	* GLOB_SCALE
uvalueDistModel			= uvalueDistM		* GLOB_SCALE
uvalueOffsetModel		= uvalueOffsetM		* GLOB_SCALE

unID = 1

note1	= ""
note2	= ""
note3	= ""
note4	= ""

lengthOfNote1	= 0
lengthOfNote2	= 0
lengthOfNote3	= 0
lengthOfNote4	= 0

_iUnitTypeByContent	= 0

if AC_WIDO_TYPE = 3 then
	Skylight_B = 0
	succ1 = REQUEST ("ASSOCLP_PARVALUE", "B", Dummy, Dummy, Dummy, Dummy, Dummy, temp_Skylight_B)
	if succ1 then Skylight_B = temp_Skylight_B

	Skylight_roofang = 0
	succ2 = REQUEST ("ASSOCLP_PARVALUE", "ac_roofang", Dummy, Dummy, Dummy, Dummy, Dummy, temp_Skylight_roofang)
	if succ2 then Skylight_roofang = temp_Skylight_roofang

	gs_marker_text_dist = 0
	succ3 = REQUEST ("ASSOCLP_PARVALUE", "gs_marker_text_dist", Dummy, Dummy, Dummy, Dummy, Dummy, temp_gs_marker_text_dist)
	if succ3 then gs_marker_text_dist = temp_gs_marker_text_dist

	if (succ1 & succ2) | succ3 then
		if succ3 then
			upbott_text_dist = gs_marker_text_dist
		else
			upbott_text_dist = Skylight_B * abs(cos(Skylight_roofang))
		endif
	else
		upbott_text_dist = AC_WALL_THICKNESS
	endif
else
	upbott_text_dist = AC_WALL_THICKNESS
endif

! --- Oriented View ------------------------------------------------------------

angleViewRot = 0
if AC_Readable then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

textAngle = totalRotate

! ==============================================================================
! 2D Degradation
! ==============================================================================

if GLOB_CONTEXT < 20 then
	bShowSillHeight		= 1
	bShowWidthHeight	= 1
	bShowID				= 1
	bShowDrawing		= 1
	bShowFire			= 1
	bShowAcoustic		= 1
	bShowUvalue			= 1
else
	! feedback mode
	bShowSillHeight		= (gs_GLOB_MODPAR_NAME = "sillDistM" | gs_GLOB_MODPAR_NAME = "sillOffsetM")
	bShowWidthHeight	= (gs_GLOB_MODPAR_NAME = "whDistM")
	bShowID				= 0
	bShowDrawing		= (gs_GLOB_MODPAR_NAME = "headDistM" | \
							gs_GLOB_MODPAR_NAME = "footLengthM" | \
							gs_GLOB_MODPAR_NAME = "markerOffsetM")
	bShowFire			= (gs_GLOB_MODPAR_NAME = "fireDistM" | gs_GLOB_MODPAR_NAME = "fireOffsetM")
	bShowAcoustic		= (gs_GLOB_MODPAR_NAME = "acousticDistM" | gs_GLOB_MODPAR_NAME = "acousticOffsetM")
	bShowUvalue			= (gs_GLOB_MODPAR_NAME = "uvalueDistM" | gs_GLOB_MODPAR_NAME = "uvalueOffsetM")
endif


! ==============================================================================
! Positioning on left or right side
! ==============================================================================

bDefaultElem = (GLOB_INTGUID = "" | GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")

if AC_WIDO_TYPE = 3 then
	if ( not( abs(AC_SKYL_ROTANGLE)>EPS exor AC_SameSide)) & not(bDefaultElem & GLOB_CONTEXT = 5) then
		rot2 180
		textAngle = textAngle + 180
		if textAngle >= 360 then
			textAngle = textAngle - 360
		endif
	endif
	if Skylight_roofang > 90 + EPS then add2 0, upbott_text_dist
	if AC_SameSide then
		if Skylight_roofang > 90 then
			add2 0, -upbott_text_dist
		else
			add2 0, upbott_text_dist
		endif
	endif
else
	if AC_WIDO_REVEAL_SIDE = AC_SameSide & not(bDefaultElem & GLOB_CONTEXT = 5) then
		rot2 180
		add2 0, upbott_text_dist
		textAngle = textAngle + 180
		if textAngle >= 360 then
			textAngle = textAngle - 360
		endif
	endif
endif


! ==============================================================================
! Drawing Lines
! ==============================================================================

pen SYMB_VIEW_PEN

if AC_ID_On | AC_WidHei_On | GS_Center_Line then
	if iMarkerSizeUnit = 1 then
		hotspot2  0,					headDistModel,unID,		markerOffsetM,	1+1024+128,	markerOffsetMm
		hotspot2 -1,					headDistModel,unID+1,	markerOffsetM,	3+1024,		markerOffsetMm
		hotspot2  markerOffsetModel,	headDistModel,unID+2,	markerOffsetM,	2+1024,		markerOffsetMm
		unID = unID + 3

	hotspot2  0,				-upbott_text_dist - footLengthModel,unID,		markerOffsetM,1+1024+128,	markerOffsetMm
		hotspot2 -0.1,				-upbott_text_dist - footLengthModel,unID+1,	markerOffsetM,3+1024,		markerOffsetMm
		hotspot2 markerOffsetModel,	-upbott_text_dist - footLengthModel,unID+2,	markerOffsetM,2+1024,		markerOffsetMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2  0,					headDistModel,unID,		markerOffsetM,	1+1024+128,	markerOffsetCm
		hotspot2 -1,					headDistModel,unID+1,	markerOffsetM,	3+1024,		markerOffsetCm
		hotspot2  markerOffsetModel,	headDistModel,unID+2,	markerOffsetM,	2+1024,		markerOffsetCm
		unID = unID + 3

		hotspot2  0,				-upbott_text_dist - footLengthModel,unID,	markerOffsetM,1+1024+128,	markerOffsetCm
		hotspot2 -0.1,				-upbott_text_dist - footLengthModel,unID+1,	markerOffsetM,3+1024,		markerOffsetCm
		hotspot2 markerOffsetModel,	-upbott_text_dist - footLengthModel,unID+2,	markerOffsetM,2+1024,		markerOffsetCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2  0,					headDistModel,unID,		markerOffsetM,	1+1024+128,	markerOffsetPoint
		hotspot2 -1,					headDistModel,unID+1,	markerOffsetM,	3+1024,		markerOffsetPoint
		hotspot2  markerOffsetModel,	headDistModel,unID+2,	markerOffsetM,	2+1024,		markerOffsetPoint
		unID = unID + 3

		hotspot2  0,				-upbott_text_dist - footLengthModel,unID,	markerOffsetM,1+1024+128,	markerOffsetPoint
		hotspot2 -0.1,				-upbott_text_dist - footLengthModel,unID+1,	markerOffsetM,3+1024,		markerOffsetPoint
		hotspot2 markerOffsetModel,	-upbott_text_dist - footLengthModel,unID+2,	markerOffsetM,2+1024,		markerOffsetPoint
		unID = unID + 3
	endif

	if iMarkerSizeUnit = 1 then
		hotspot2 markerOffsetModel,  0,				unID,	headDistM, 1+1024+128,	headDistMm
		hotspot2 markerOffsetModel, -0.1,			unID+1,	headDistM, 3+1024,		headDistMm
		hotspot2 markerOffsetModel,  headDistModel,	unID+2,	headDistM, 2+1024,		headDistMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2 markerOffsetModel,  0,				unID,	headDistM, 1+1024+128,	headDistCm
		hotspot2 markerOffsetModel, -0.1,			unID+1,	headDistM, 3+1024,		headDistCm
		hotspot2 markerOffsetModel,  headDistModel,	unID+2,	headDistM, 2+1024,		headDistCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2 markerOffsetModel,  0,				unID,	headDistM, 1+1024+128,	headDistPoint
		hotspot2 markerOffsetModel, -0.1,			unID+1,	headDistM, 3+1024,		headDistPoint
		hotspot2 markerOffsetModel,  headDistModel,	unID+2,	headDistM, 2+1024,		headDistPoint
		unID = unID + 3
	endif

	if iMarkerSizeUnit = 1 then
		hotspot2 markerOffsetModel, -upbott_text_dist,						unID,	footLengthM, 1+1024+128,	footLengthMm
		hotspot2 markerOffsetModel, -upbott_text_dist + 0.1,				unID+1,	footLengthM, 3+1024,		footLengthMm
		hotspot2 markerOffsetModel, -upbott_text_dist - footLengthModel,	unID+2,	footLengthM, 2+1024,		footLengthMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2 markerOffsetModel, -upbott_text_dist,						unID,	footLengthM, 1+1024+128,	footLengthCm
		hotspot2 markerOffsetModel, -upbott_text_dist + 0.1,				unID+1,	footLengthM, 3+1024,		footLengthCm
		hotspot2 markerOffsetModel, -upbott_text_dist - footLengthModel,	unID+2,	footLengthM, 2+1024,		footLengthCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2 markerOffsetModel, -upbott_text_dist,						unID,	footLengthM, 1+1024+128,	footLengthPoint
		hotspot2 markerOffsetModel, -upbott_text_dist + 0.1,				unID+1,	footLengthM, 3+1024,		footLengthPoint
		hotspot2 markerOffsetModel, -upbott_text_dist - footLengthModel,	unID+2,	footLengthM, 2+1024,		footLengthPoint
		unID = unID + 3
	endif
endIf

define style  "mtext1style"  AC_TextFont_1, AC_TextSize_1, 7, AC_TextStyle_1
define style  "mtext2style"  AC_TextFont_2, AC_TextSize_2, 7, AC_TextStyle_2
define style  "mtext3style"  AC_TextFont_3, AC_TextSize_3, 7, AC_TextStyle_3
define style  "mtext4style"  AC_TextFont_4, AC_TextSize_4, 7, AC_TextStyle_4
define style  "mtext5style"  AC_TextFont_5, AC_TextSize_5, 7, AC_TextStyle_5
define style  "mtext6style"  AC_TextFont_6, AC_TextSize_6, 7, AC_TextStyle_6
define style  "mtext7style"  AC_TextFont_7, AC_TextSize_7, 7, AC_TextStyle_7

! ==============================================================================
! Show ID
! ==============================================================================

pen AC_TextPen_1

style  "mtext1style"
frame_width = stw("  " + AC_MarkerText_1 + "  ") * PAPER_TO_MODEL

if bShowDrawing & iMarkerShape <> SHAPE_NONE then
	drawindex 20
	mask = 7
	pen gs_cont_pen

	if GS_MarkerFill then fill AC_MarkerFill

! Circle - Marker Shape
! ----------------------------------------------------------------------
	if iMarkerShape = SHAPE_CIRCLE then

		if gs_markertext_fit = 0 then
			dx1 = AC_MarkerSize/2
		else
			dx1 = frame_width/2
		endif

		poly2_b	2, mask, gs_fill_pen, gs_back_pen,
				markerOffsetModel, headDistModel,901,
				dx1,360,4001

		if GS_Center_Line then
			pen SYMB_VIEW_PEN
			line2 markerOffsetModel, headDistModel-dx1, markerOffsetModel, -upbott_text_dist - footLengthModel
		endif

	else

! Oval - Marker Shape
! ----------------------------------------------------------------------

		if iMarkerShape = SHAPE_OVAL then

			dx1 = frame_width/2

			poly2_b	7, mask, gs_fill_pen, gs_back_pen,
				markerOffsetModel-dx1+AC_MarkerSize/2, headDistModel+AC_MarkerSize/2, 1,
				markerOffsetModel+dx1-AC_MarkerSize/2, headDistModel+AC_MarkerSize/2, 1,
				markerOffsetModel+dx1-AC_MarkerSize/2, headDistModel, 901,
				markerOffsetModel+dx1-AC_MarkerSize/2, headDistModel-AC_MarkerSize/2, 3001,
				markerOffsetModel-dx1+AC_MarkerSize/2, headDistModel-AC_MarkerSize/2, 1,
				markerOffsetModel-1000, headDistModel-AC_MarkerSize/2, 801,
				markerOffsetModel-dx1+AC_MarkerSize/2, headDistModel+AC_MarkerSize/2, 1001

			if GS_Center_Line then
				pen SYMB_VIEW_PEN
				line2 markerOffsetModel, headDistModel-AC_MarkerSize/2, markerOffsetModel, -upbott_text_dist - footLengthModel
			endif

			goto 100

		endif

! Fragment - Marker Shape
! ----------------------------------------------------------------------

		if iMarkerShape = SHAPE_FRAGMENT then

			poly2_b 4, mask, gs_fill_pen, gs_back_pen,
				markerOffsetModel-frame_width/2, headDistModel-AC_MarkerSize/2, 1,
				markerOffsetModel-frame_width/2, headDistModel+AC_MarkerSize/2, 1,
				markerOffsetModel+frame_width/2, headDistModel+AC_MarkerSize/2, 1,
				markerOffsetModel+frame_width/2, headDistModel-AC_MarkerSize/2, 1

			if GS_Center_Line then
				pen SYMB_VIEW_PEN
				line2 markerOffsetModel, headDistModel-AC_MarkerSize/2, markerOffsetModel, -upbott_text_dist - footLengthModel
			endif

		else

! Triangle - Marker Shape
! ----------------------------------------------------------------------

			if iMarkerShape = SHAPE_TRIANGLE then
				num = 3
				if not(gs_marker_rotation) then
					alfa		= 30
					text_offset	= 0
				else
					alfa		= 90
					text_offset	= 1
				endif
			endif

! Rectangle - Marker Shape
! ----------------------------------------------------------------------

			if iMarkerShape = SHAPE_RECT then
				num = 4
				if not(gs_marker_rotation) then
					alfa		= 0
					text_offset	= 0
				else
					alfa		= 45
					text_offset	= 1
				endif
			endif

! Pentagon - Marker Shape
! ----------------------------------------------------------------------

			if iMarkerShape = SHAPE_PENTAGON then
				num = 5
				if not(gs_marker_rotation) then
					alfa		= 54
					text_offset	= 0
				else
					alfa		= 90
					text_offset	= 1
				endif
			endif

! Hexagon - Marker Shape
! ----------------------------------------------------------------------

			if iMarkerShape = SHAPE_HEXAGON then
				num = 6
				if not(gs_marker_rotation) then
					alfa		= 90
					text_offset	= 0
				else
					alfa		= 60
					text_offset	= 1
				endif
			endif

! Octagon - Marker Shape
! ----------------------------------------------------------------------

			if iMarkerShape = SHAPE_OCTAGON then
				num = 8
				if not(gs_marker_rotation) then
					alfa		= 0
					text_offset	= 0
				else
					alfa		= 67.5
					text_offset	= 1
				endif
			endif

			if GS_Center_Line then
				pen SYMB_VIEW_PEN
				if not(gs_marker_rotation) then
					line2 markerOffsetModel, headDistModel-AC_MarkerSize/2, markerOffsetModel, -upbott_text_dist - footLengthModel
				else
					line2 markerOffsetModel, headDistModel-AC_MarkerSize/2*sin(90-360/(2*num)), markerOffsetModel, -upbott_text_dist - footLengthModel
				endif
			endif

			for i=1 to num
				put markerOffsetModel+AC_MarkerSize/2*cos(alfa), headDistModel+AC_MarkerSize/2*sin(alfa), 1
				alfa = alfa + 360 / num
			next i

			pen gs_cont_pen
			poly2_b	num, mask, gs_fill_pen, gs_back_pen,
					get(nsp)
		endif
	endif
else
	if bShowDrawing & GS_Center_Line then
		drawindex 10
		pen SYMB_VIEW_PEN

		rrr= REQUEST("Height_of_style",mtext1style,style_hght)

		if iMarkerDir = 2 then
			line2	markerOffsetModel,  headDistModel - AC_ID_On * frame_width/2,
					markerOffsetModel, -upbott_text_dist - footLengthModel
		else
			line2	markerOffsetModel,  headDistModel - AC_ID_On * (style_hght * PAPER_TO_MODEL)/2,
					markerOffsetModel, -upbott_text_dist - footLengthModel
		endif
	endif
endif

100:


! ==============================================================================
! Show ID
! ==============================================================================


if bShowID & AC_ID_On then
	add2 markerOffsetModel,0
	pen AC_TextPen_1

	posX	 = 0
	posY	 = headDistModel / PAPER_TO_MODEL - AC_TextSize_1 / 2
	_horAlign= 2
	note1	 = AC_MarkerText_1
	note2	 = ""
	note3	 = ""
	note4	 = ""
	style1	 = "mtext1style"
	style2	 = ""
	style3	 = ""
	style4	 = ""
	notesize = AC_TextSize_1
	angle	 = textAngle
	enabled	 = AC_Readable

	gosub "Print_text_considering_readability"

	del 1
endif


! ==============================================================================
! Show Parapet
! ==============================================================================

define style  "extraStyle4" AC_TextFont_4, 2/3*AC_TextSize_4, 7, AC_TextStyle_4

if AC_Sill_On then
	add2 markerOffsetModel,0

	if iMarkerSizeUnit = 1 then
		hotspot2 sillOffsetModel, -upbott_text_dist,					unID,	sillDistM, 1+1024+128,	sillDistMm
		hotspot2 sillOffsetModel, -upbott_text_dist + 1,				unID+1,	sillDistM, 3+1024,		sillDistMm
		hotspot2 sillOffsetModel, -upbott_text_dist - sillDistModel,	unID+2,	sillDistM, 2+1024,		sillDistMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2 sillOffsetModel, -upbott_text_dist,					unID,	sillDistM, 1+1024+128,	sillDistCm
		hotspot2 sillOffsetModel, -upbott_text_dist + 1,				unID+1,	sillDistM, 3+1024,		sillDistCm
		hotspot2 sillOffsetModel, -upbott_text_dist - sillDistModel,	unID+2,	sillDistM, 2+1024,		sillDistCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2 sillOffsetModel, -upbott_text_dist,					unID,	sillDistM, 1+1024+128,	sillDistPoint
		hotspot2 sillOffsetModel, -upbott_text_dist + 1,				unID+1,	sillDistM, 3+1024,		sillDistPoint
		hotspot2 sillOffsetModel, -upbott_text_dist - sillDistModel,	unID+2,	sillDistM, 2+1024,		sillDistPoint
		unID = unID + 3
	endif

	if iMarkerSizeUnit = 1 then
		hotspot2  0,				-upbott_text_dist - sillDistModel, unID,  sillOffsetM, 1+1024+128,	sillOffsetMm
		hotspot2 -1,				-upbott_text_dist - sillDistModel, unID+1,sillOffsetM, 3+1024,		sillOffsetMm
		hotspot2  sillOffsetModel,	-upbott_text_dist - sillDistModel, unID+2,sillOffsetM, 2+1024,		sillOffsetMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2  0,				-upbott_text_dist - sillDistModel, unID,  sillOffsetM, 1+1024+128,	sillOffsetCm
		hotspot2 -1,				-upbott_text_dist - sillDistModel, unID+1,sillOffsetM, 3+1024,		sillOffsetCm
		hotspot2  sillOffsetModel,	-upbott_text_dist - sillDistModel, unID+2,sillOffsetM, 2+1024,		sillOffsetCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2  0,				-upbott_text_dist - sillDistModel, unID,  sillOffsetM, 1+1024+128,	sillOffsetPoint
		hotspot2 -1,				-upbott_text_dist - sillDistModel, unID+1,sillOffsetM, 3+1024,		sillOffsetPoint
		hotspot2  sillOffsetModel,	-upbott_text_dist - sillDistModel, unID+2,sillOffsetM, 2+1024,		sillOffsetPoint
		unID = unID + 3
	endif


	add2 sillOffsetModel, 0

	if bShowSillHeight then
		pen AC_TextPen_4

		_iUnitTypeByContent	= 8		! "Sill_height_dimension"

		if GS_iCustomText_4 = CTXT_CUSTOMTEXT then
			_horAlign	= 2
			note1		= AC_MarkerText_4
			note2		= ""
			note3		= ""
			note4		= ""
		else
			_lengthValue	= _lengthSillValue
			_horAlign		= 2
			_TextFont		= AC_TextFont_4
			_TextSize		= AC_TextSize_4
			_TextStyle		= AC_TextStyle_4
			_szPrefix		= AC_SillPrefix + " "

			gosub "call_quantity_formatter"
		endif

		posX	 = 0
		posY	 = (-sillDistModel - upbott_text_dist) / PAPER_TO_MODEL
		style1	 = "mtext4style"
		style2	 = "extraStyle4"
		style3	 = ""
		style4	 = ""
		notesize = AC_TextSize_4
		angle	 = textAngle
		enabled	 = AC_Readable
		iMarkerDir = 1

		gosub "Print_text_considering_readability"
	endif

	del 2
endif


! ==============================================================================
! Show Fire Rating
! ==============================================================================

if AC_show_firerating then
	add2 markerOffsetModel, 0

	if iMarkerSizeUnit = 1 then
		hotspot2 fireOffsetModel,	 0,				unID,	fireDistM, 1+1024+128,	fireDistMm
		hotspot2 fireOffsetModel,	-1,				unID+1,	fireDistM, 3+1024,		fireDistMm
		hotspot2 fireOffsetModel,	 fireDistModel,	unID+2,	fireDistM, 2+1024,		fireDistMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2 fireOffsetModel,	 0,				unID,	fireDistM, 1+1024+128,	fireDistCm
		hotspot2 fireOffsetModel,	-1,				unID+1,	fireDistM, 3+1024,		fireDistCm
		hotspot2 fireOffsetModel,	 fireDistModel,	unID+2,	fireDistM, 2+1024,		fireDistCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2 fireOffsetModel,	 0,				unID,	fireDistM, 1+1024+128,	fireDistPoint
		hotspot2 fireOffsetModel,	-1,				unID+1,	fireDistM, 3+1024,		fireDistPoint
		hotspot2 fireOffsetModel,	 fireDistModel,	unID+2,	fireDistM, 2+1024,		fireDistPoint
		unID = unID + 3
	endif

	if iMarkerSizeUnit = 1 then
		hotspot2  0,				fireDistModel,	unID,	fireOffsetM, 1+1024+128,	fireOffsetMm
		hotspot2 -1,				fireDistModel,	unID+1,	fireOffsetM, 3+1024,		fireOffsetMm
		hotspot2  fireOffsetModel,	fireDistModel,	unID+2,	fireOffsetM, 2+1024,		fireOffsetMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2  0,				fireDistModel,	unID,	fireOffsetM, 1+1024+128,	fireOffsetCm
		hotspot2 -1,				fireDistModel,	unID+1,	fireOffsetM, 3+1024,		fireOffsetCm
		hotspot2  fireOffsetModel,	fireDistModel,	unID+2,	fireOffsetM, 2+1024,		fireOffsetCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2  0,				fireDistModel,	unID,	fireOffsetM, 1+1024+128,	fireOffsetPoint
		hotspot2 -1,				fireDistModel,	unID+1,	fireOffsetM, 3+1024,		fireOffsetPoint
		hotspot2  fireOffsetModel,	fireDistModel,	unID+2,	fireOffsetM, 2+1024,		fireOffsetPoint
		unID = unID + 3
	endif


	add2 fireOffsetModel, 0

	pen AC_TextPen_5
	define style  "lowerIndex"  AC_TextFont_5, AC_TextSize_5 * 2/3, 7, AC_TextStyle_5

	if bShowFire then
		firerating_textMain = ""
		firerating_textLowerIndex = ""
		firerating_textMain2 = ""

		if (LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER") and\
		gs_dw_marker_type <> 3 and\
		GS_iCustomText_5 = CTXT_MEASUREDVALUE then	
	
			if gs_dw_marker_type = 2 then																! D Marker
				if _contbInsulation and _contindexInsul <> "" then	! For the index style as fraction
					posIndex = strstr(_contExpressionFR, _contindexInsul)
					posList = posIndex + strlen( _contindexInsul)
					posEnd = strlen( _contExpressionFR)
					firerating_textMain = AC_FirePrefix + strsub(_contExpressionFR, 0, posIndex-1)
					firerating_textLowerIndex = _contindexInsul
					firerating_textMain2 = strsub(_contExpressionFR, posList, posEnd)
				else
					firerating_textMain = AC_FirePrefix + _contExpressionFR
				endif
			else																						! W Marker
				if _contbSelfC and _contindexSelfC <> "" then	! For the index style as fraction
					posIndex = strstr(_contExpressionFR, _contindexSelfC)
					posList = posIndex + strlen( _contindexSelfC)
					posEnd = strlen( _contExpressionFR)
					firerating_textMain = AC_FirePrefix + strsub(_contExpressionFR, 0, posIndex-1)
					firerating_textLowerIndex = _contindexSelfC
					firerating_textMain2 = strsub(_contExpressionFR, posList, posEnd)
				else
					firerating_textMain = AC_FirePrefix + _contExpressionFR
				endif
			endif
		else
			firerating_textMain = AC_MarkerText_5
		endif

		posX	 = 0
		posY	 = fireDistModel / PAPER_TO_MODEL
		_horAlign= 1
		note1	 = firerating_textMain
		note2	 = ""
		note3	 = firerating_textLowerIndex
		note4	 = firerating_textMain2
		style1	 = "mtext5style"
		style2	 = ""
		style3	 = "lowerIndex"
		style4	 = "mtext5style"
		notesize = AC_TextSize_5
		angle	 = textAngle
		enabled	 = AC_Readable
		iMarkerDir = 1

		gosub "Print_text_considering_readability"

	endif
	del 2


	! --------------------------------------------------------------------------
	! Show Fire Rating LINEs for NED openings
	! --------------------------------------------------------------------------

	PEN AC_Pen_FireRate
	RadFPLine = 0.072
	Dx  = 0.08
	Dy1 = 0.5
	Dy2 = 0.3
	Dy3 = 0.16

	! BRAND parameter check - It is for NED Doors/Windows
	rrBrand = request ("ASSOCLP_PARVALUE", "iNedFireRating",
		index_firerating2, type_firerating2, flags_firerating2,
		dim1_firerating2, dim2_firerating2, firerating2)

	IF rrBrand > EPS THEN
		ADD2 AC_MarkerOffset,0
		IF firerating2=1 or firerating2=4 or firerating2=5 THEN      ! "zelfsluitend" - Smoke Protection
			POLY2_ 9,1,
				0,4*RadFPLine,1,
				0,3*RadFPLine,901,
				RadFPLine,-180,4001,
				0,1*RadFPLine,901,
				RadFPLine,180,4001,
				0,-RadFPLine,901,
				RadFPLine,-180,4001,
				0,-3*RadFPLine,901,
				RadFPLine,180,4001
		ENDIF

		IF firerating2 = 2 or  firerating2 = 4 THEN      ! "30 min. brandwerend" - 30 Min. Fire Protection
			LINE2 -Dx,Dy1,Dx,Dy1+Dy3
			LINE2 -Dx,-Dy1,Dx,-Dy1-Dy3
			LINE2 Dx,Dy1,-Dx,Dy1+Dy3
			LINE2 Dx,-Dy1,-Dx,-Dy1-Dy3
		ENDIF

		IF firerating2 = 3 or firerating2 = 5 THEN      ! "60 min. brandwerend" - 60 Min. Fire Protection
			LINE2 -Dx,Dy2,Dx,Dy2+Dy3
			LINE2 -Dx,-Dy2,Dx,-Dy2-Dy3
			LINE2 Dx,Dy2,-Dx,Dy2+Dy3
			LINE2 Dx,-Dy2,-Dx,-Dy2-Dy3

			LINE2 -Dx,Dy1,Dx,Dy1+Dy3
			LINE2 -Dx,-Dy1,Dx,-Dy1-Dy3
			LINE2 Dx,Dy1,-Dx,Dy1+Dy3
			LINE2 Dx,-Dy1,-Dx,-Dy1-Dy3
		ENDIF
		DEL 1
	ENDIF
ENDIF

! ==============================================================================
! Show Acoustic Rating
! ==============================================================================

if AC_show_acousticrating then
	add2 markerOffsetModel, 0

	if iMarkerSizeUnit = 1 then
		hotspot2 acousticOffsetModel,	 0,					unID,	acousticDistM, 1+1024+128,	acousticDistMm
		hotspot2 acousticOffsetModel,	-1,					unID+1,	acousticDistM, 3+1024,		acousticDistMm
		hotspot2 acousticOffsetModel,	 acousticDistModel,	unID+2,	acousticDistM, 2+1024,		acousticDistMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2 acousticOffsetModel,	 0,					unID,	acousticDistM, 1+1024+128,	acousticDistCm
		hotspot2 acousticOffsetModel,	-1,					unID+1,	acousticDistM, 3+1024,		acousticDistCm
		hotspot2 acousticOffsetModel,	 acousticDistModel,	unID+2,	acousticDistM, 2+1024,		acousticDistCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2 acousticOffsetModel,	 0,					unID,	acousticDistM, 1+1024+128,	acousticDistPoint
		hotspot2 acousticOffsetModel,	-1,					unID+1,	acousticDistM, 3+1024,		acousticDistPoint
		hotspot2 acousticOffsetModel,	 acousticDistModel,	unID+2,	acousticDistM, 2+1024,		acousticDistPoint
		unID = unID + 3
	endif


	if iMarkerSizeUnit = 1 then
		hotspot2  0,					acousticDistModel,	unID,	acousticOffsetM, 1+1024+128,	acousticOffsetMm
		hotspot2 -1,					acousticDistModel,	unID+1,	acousticOffsetM, 3+1024,		acousticOffsetMm
		hotspot2  acousticOffsetModel,	acousticDistModel,	unID+2,	acousticOffsetM, 2+1024,		acousticOffsetMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2  0,					acousticDistModel,	unID,	acousticOffsetM, 1+1024+128,	acousticOffsetCm
		hotspot2 -1,					acousticDistModel,	unID+1,	acousticOffsetM, 3+1024,		acousticOffsetCm
		hotspot2  acousticOffsetModel,	acousticDistModel,	unID+2,	acousticOffsetM, 2+1024,		acousticOffsetCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2  0,					acousticDistModel,	unID,	acousticOffsetM, 1+1024+128,	acousticOffsetPoint
		hotspot2 -1,					acousticDistModel,	unID+1,	acousticOffsetM, 3+1024,		acousticOffsetPoint
		hotspot2  acousticOffsetModel,	acousticDistModel,	unID+2,	acousticOffsetM, 2+1024,		acousticOffsetPoint
		unID = unID + 3
	endif


	add2 acousticOffsetModel, 0

	if bShowAcoustic then
		pen AC_TextPen_6

		posX	 = 0
		posY	 = acousticDistModel / PAPER_TO_MODEL
		_horAlign= 1
		note1	 = AC_MarkerText_6
		note2	 = ""
		note3	 = ""
		note4	 = ""
		style1	 = "mtext6style"
		style2	 = ""
		style3	 = ""
		style4	 = ""
		notesize = AC_TextSize_6
		angle	 = textAngle
		enabled	 = AC_Readable
		iMarkerDir = 1

		gosub "Print_text_considering_readability"
	endif

	del 2
endif


! ==============================================================================
! Show U-value
! ==============================================================================

if AC_show_Uvalue then
	add2 markerOffsetModel, 0

	if iMarkerSizeUnit = 1 then
		hotspot2 uvalueOffsetModel,	 0,					unID,	uvalueDistM, 1+1024+128,	uvalueDistMm
		hotspot2 uvalueOffsetModel,	-1,					unID+1,	uvalueDistM, 3+1024,		uvalueDistMm
		hotspot2 uvalueOffsetModel,	 uvalueDistModel,	unID+2,	uvalueDistM, 2+1024,		uvalueDistMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2 uvalueOffsetModel,	 0,					unID,	uvalueDistM, 1+1024+128,	uvalueDistCm
		hotspot2 uvalueOffsetModel,	-1,					unID+1,	uvalueDistM, 3+1024,		uvalueDistCm
		hotspot2 uvalueOffsetModel,	 uvalueDistModel,	unID+2,	uvalueDistM, 2+1024,		uvalueDistCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2 uvalueOffsetModel,	 0,					unID,	uvalueDistM, 1+1024+128,	uvalueDistPoint
		hotspot2 uvalueOffsetModel,	-1,					unID+1,	uvalueDistM, 3+1024,		uvalueDistPoint
		hotspot2 uvalueOffsetModel,	 uvalueDistModel,	unID+2,	uvalueDistM, 2+1024,		uvalueDistPoint
		unID = unID + 3
	endif


	if iMarkerSizeUnit = 1 then
		hotspot2  0,					uvalueDistModel,	unID,	uvalueOffsetM, 1+1024+128,	uvalueOffsetMm
		hotspot2 -1,					uvalueDistModel,	unID+1,	uvalueOffsetM, 3+1024,		uvalueOffsetMm
		hotspot2  uvalueOffsetModel,	uvalueDistModel,	unID+2,	uvalueOffsetM, 2+1024,		uvalueOffsetMm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 2 then
		hotspot2  0,					uvalueDistModel,	unID,	uvalueOffsetM, 1+1024+128,	uvalueOffsetCm
		hotspot2 -1,					uvalueDistModel,	unID+1,	uvalueOffsetM, 3+1024,		uvalueOffsetCm
		hotspot2  uvalueOffsetModel,	uvalueDistModel,	unID+2,	uvalueOffsetM, 2+1024,		uvalueOffsetCm
		unID = unID + 3
	endif
	if iMarkerSizeUnit = 3 then
		hotspot2  0,					uvalueDistModel,	unID,	uvalueOffsetM, 1+1024+128,	uvalueOffsetPoint
		hotspot2 -1,					uvalueDistModel,	unID+1,	uvalueOffsetM, 3+1024,		uvalueOffsetPoint
		hotspot2  uvalueOffsetModel,	uvalueDistModel,	unID+2,	uvalueOffsetM, 2+1024,		uvalueOffsetPoint
		unID = unID + 3
	endif


	add2 uvalueOffsetModel, 0

	if bShowUvalue then
		pen AC_TextPen_7

		posX	 = 0
		posY	 = uvalueDistModel / PAPER_TO_MODEL
		_horAlign= 1
		note1	 = AC_MarkerText_7
		note2	 = ""
		note3	 = ""
		note4	 = ""
		style1	 = "mtext7style"
		style2	 = ""
		style3	 = ""
		style4	 = ""
		notesize = AC_TextSize_7
		angle	 = textAngle
		enabled	 = AC_Readable
		GS_MarkerDir=`Стандарт`

		gosub "Print_text_considering_readability"
	endif

	del 2
endif


! ==============================================================================
! Show Width/Height
! ==============================================================================

if not(RotWidthText) then
	rot2 90

	textAngle = textAngle + 90
	if textAngle >= 360 then
		textAngle = textAngle - 360
	endif
endif


if AC_WidHei_On then

	IF not(RotWidthText) THEN
		add2 0,-markerOffsetModel

		if iMarkerSizeUnit = 1 then
			hotspot2  0,			0, unID,	whDistM, 1+1024+128,	whDistMm
			hotspot2 -1,			0, unID+1,	whDistM, 3+1024,		whDistMm
			hotspot2  whDistModel,0, unID+2,	whDistM, 2+1024,		whDistMm
			unID = unID + 3
		endif
		if iMarkerSizeUnit = 2 then
			hotspot2  0,			0, unID,	whDistM, 1+1024+128,	whDistCm
			hotspot2 -1,			0, unID+1,	whDistM, 3+1024,		whDistCm
			hotspot2  whDistModel,0, unID+2,	whDistM, 2+1024,		whDistCm
			unID = unID + 3
		endif
		if iMarkerSizeUnit = 3 then
			hotspot2  0,			0, unID,	whDistM, 1+1024+128,	whDistPoint
			hotspot2 -1,			0, unID+1,	whDistM, 3+1024,		whDistPoint
			hotspot2  whDistModel,0, unID+2,	whDistM, 2+1024,		whDistPoint
			unID = unID + 3
		endif


		if bShowWidthHeight then
			if textAngle > (gs_readable_angle + eps) & textAngle < (gs_readable_angle + 180 + eps) & AC_Readable then
				bSwitchTexts = 1
			else
				bSwitchTexts = 0
			endif

			define style  "extraStyle2" AC_TextFont_2, 2/3*AC_TextSize_2, 7, AC_TextStyle_2
			define style  "extraStyle3" AC_TextFont_3, 2/3*AC_TextSize_3, 7, AC_TextStyle_3

			pen AC_TextPen_3

			_iUnitTypeByContent	= 7		! "Windor_Door_Dimension"

			if GS_iCustomText_3 = CTXT_CUSTOMTEXT then
				_horAlign	= 3
				note1		= AC_MarkerText_3
				note2		= ""
				note3		= ""
				note4		= ""
			else
				_lengthValue	= _lengthHeightValue
				_horAlign		= 3
				_TextFont		= AC_TextFont_3
				_TextSize		= AC_TextSize_3
				_TextStyle		= AC_TextStyle_3
				_szPrefix		= ""

				gosub "call_quantity_formatter"
			endif

			_tmpNote1 = note1

			posX	 = whDistModel / PAPER_TO_MODEL
			if bSwitchTexts then
				posY	 = AC_TextSize_1 / 2
			else
				posY	 = -1.5 * AC_TextSize_1
			endif
			style1   = "mtext3style"
			style2   = "extraStyle2"
			style3   = ""
			style4   = ""
			notesize = AC_TextSize_3
			angle	 = textAngle
			enabled	 = AC_Readable
			iMarkerDir = 1

			if not(bIsDutchDimType) then
				gosub "Print_text_considering_readability"
			endif

			pen AC_TextPen_2

			if GS_iCustomText_2 = CTXT_CUSTOMTEXT then
				_horAlign	= 3
				note1		= AC_MarkerText_2
				note2		= ""
				note3		= ""
				note4		= ""
			else
				_lengthValue	= _lengthWidthValue
				_horAlign		= 3
				_TextFont		= AC_TextFont_2
				_TextSize		= AC_TextSize_2
				_TextStyle		= AC_TextStyle_2
				_szPrefix		= ""

				gosub "call_quantity_formatter"
			endif

			if bIsDutchDimType then
				note1		= NedSizePref + note1 + NedSizeSep + _tmpNote1
				note2		= ""
				note3		= ""
				note4		= ""
			endif

			posX	 = whDistModel / PAPER_TO_MODEL
			if bSwitchTexts then
				posY	 = -1.5 * AC_TextSize_1
			else
				posY	 = AC_TextSize_1 / 2
			endif
			style1   = "mtext2style"
			style2   = "extraStyle3"
			style3   = ""
			style4   = ""
			notesize = AC_TextSize_2
			angle	 = textAngle
			enabled	 = AC_Readable
			iMarkerDir = 1

			gosub "Print_text_considering_readability"
		endif

		del 1

	else

		! --- NED MARKER - when the size is under the Sill text ---

		add2 AC_MarkerOffset,0

		hotspot2 0, -upbott_text_dist,					unID,	AC_WidHeiDist, 1+128,	GS_WidHeiDist_edit
		hotspot2 0, -upbott_text_dist + 1,				unID+1,	AC_WidHeiDist, 3,		GS_WidHeiDist_edit
		hotspot2 0, -upbott_text_dist - AC_WidHeiDist,	unID+2,	AC_WidHeiDist, 2,		GS_WidHeiDist_edit
		unID = unID + 3

		if AC_WidHei_On then
			posX	 = 0
			posY	 = (-AC_WidHeiDist - upbott_text_dist) / PAPER_TO_MODEL
			_horAlign= 2
			note1	 = AC_MarkerText_2
			note2	 = extra
			note3	 = ""
			note4	 = ""
			style1	 = "mtext4style"
			style2	 = "extraStyle4"
			style3   = ""
			style4   = ""
			notesize = AC_TextSize_4
			angle	 = textAngle
			enabled	 = AC_Readable

			gosub "Print_text_considering_readability"
		endif

		del 1
	ENDIF
endif


end


! ==============================================================================
"call_quantity_formatter":
! ------------------------------------------------------------------------------

	if _iUnitTypeByContent = 8 then				! "Sill_height_dimension"
		_iWorkUnit			= iWorkUnitSillHgt
		_iWorkUnitDecimal	= iWorkUnitSillHgtDecimal
	else
		_iWorkUnit			= iWorkUnitLinear
		_iWorkUnitDecimal	= iWorkUnitLinearDecimal
	endif

	_custom_form = ""

	if _iWorkUnit = DIMUNIT_MM 	then _custom_form = "%~0." + str(_iWorkUnitDecimal,1,0) + "mm"		!mm
	if _iWorkUnit = DIMUNIT_CM 	then _custom_form = "%~0." + str(_iWorkUnitDecimal,1,0) + "cm"		!cm
	if _iWorkUnit = DIMUNIT_DM	then
		_custom_form = "%~0." + str(_iWorkUnitDecimal,1,0) + "m"		!dm
		_lengthValue = _lengthValue * 10
	endif
	if _iWorkUnit = DIMUNIT_METER	then _custom_form = "%~0." + str(_iWorkUnitDecimal,1,0) + "m"		!m
	if _iWorkUnit = DIMUNIT_INCH	then _custom_form = "%0.64fi"

	if _iWorkUnit = DIMUNIT_PROJECT then
		_iUnitType  	= _iUnitTypeByContent
	else
		_iUnitType  	= 0		! Custom
	endif

	call "quantity_text_formatter" parameters 	lengthValue				= _lengthValue,
												custom_form				= _custom_form,
												iUnitType				= _iUnitType,
												horizontalAlignment 	= _horAlign,
												verticalAlignment		= 1,
												AC_TextFont				= _TextFont,
												AC_TextSize				= _TextSize,
												AC_TextStyle			= _TextStyle,
												szPrefix				= _szPrefix,
												bShowText				= 0,
												bShowUnit				= 0,
												bShowSuperScript		= 1,
							returned_parameters lengthOfNote1,
												lengthOfNote2,
												lengthOfNote3,
												lengthOfNote4,
												dummy_totalLength,
												dummy_totalHeight,
												dummy_lengthOfUnit,
												dummy_lengthOfIntegerPart,
												note1,				!szPrefix + main
												note2,				!frac,
												note3,				!szUnit,
												note4,				!szUnitIndex,
												dummy_szExtraPrecision,
												dummy_iAnchorString
return


! ==============================================================================
! Print text considering readability
! ==============================================================================
! works for anchor point 1 only! (top left corner)
! optional extra text in 2/3 size, elevated to 1/3 hight
!
! posX:		text position X
! posY:		text position Y
! note1:	text
! note2:	extra text
! style1:	style of text
! style2:	style of extra
! _horAlign:1-left, 2-center, 3-right
! angle:	text angle
! enabled:	may rotate
! ==============================================================================

"Print_text_considering_readability":

	mul2 PAPER_TO_MODEL, PAPER_TO_MODEL
	numTrans = 1

	add2 0, posY + notesize / 2
	numTrans  = numTrans + 1

	rotdir_angle = 0 	! iMarkerDir = 1
	if iMarkerDir = 2 then	rotdir_angle = 90
	if iMarkerDir = 3 then	rotdir_angle = -textAngle
	if iMarkerDir = 4 then rotdir_angle = -textAngle + 90

	rot2 rotdir_angle
	numTrans  = numTrans + 1

	style style1
	if	abs(lengthOfNote1 < EPS)	| \
		abs(lengthOfNote2 < EPS)	| \
		abs(lengthOfNote3 < EPS)	| \
		abs(lengthOfNote4 < EPS)	then

		gosub "compute_note_lengths"
	endif

	if _horAlign = 2 then
		add2 -(lengthOfNote1+lengthOfNote2+lengthOfNote3+lengthOfNote4)/2, 0
		numTrans  = numTrans + 1
	endif
	if _horAlign = 3 then
		add2 -(lengthOfNote1+lengthOfNote2+lengthOfNote3+lengthOfNote4), 0
		numTrans  = numTrans + 1
	endif

	if (angle > (gs_readable_angle + eps) & angle < (gs_readable_angle + 180 + eps) & enabled & iMarkerDir = 1)  | \
		(angle > (gs_readable_angle - 90 + eps) & angle < (gs_readable_angle + 180 - 90 + eps) & enabled & iMarkerDir = 2) then

		add2 posX + lengthOfNote1 + lengthOfNote2 + lengthOfNote3 + lengthOfNote4, notesize/2			! top right corner
		rot2 180
		numTrans = numTrans + 2
	else
		add2 posX, - notesize/2							! bottom left corner
		numTrans  = numTrans + 1
	endif


	rrr = request("Height_of_style", style1, sy, descent, leading)
	dl	= (descent + leading)

	if GLOB_CONTEXT < 20 then
		text2 0, -dl, note1

		if note2 <> "" then
			style style2
			text2 lengthOfNote1, notesize/3 - dl*2/3, note2
		endif

		if note3 <> "" then
			style style3
			text2 lengthOfNote1 + lengthOfNote2, - dl, note3
		endif

		if note4 <> "" then
			style style4
			text2 lengthOfNote1 + lengthOfNote2 + lengthOfNote3, -dl, note4
		endif
	else
		rect2 0,-dl, lengthOfNote1 + lengthOfNote2 + lengthOfNote3 + lengthOfNote4, notesize
	endif

	del numTrans
return

! ==============================================================================
! Compute note lengths if necessary
! ==============================================================================
! The matching style has to be active.
!
! Input:
!  note1:
!  note2:
! Output:
!  lengthOfNote1:
!  lengthOfNote2:
! ==============================================================================
"compute_note_lengths":
! ------------------------------------------------------------------------------
	lengthOfNote1	= stw (note1)
	lengthOfNote2	= stw (note2) * 2/3
	lengthOfNote3	= stw (note3) * 2/3
	lengthOfNote4	= stw (note4)

return
