

ui_dialog `Параметры Маркера`,444,266

! ==============================================================================
! Tabpage IDs, Titles and Icons
! ==============================================================================

PAGE_ID_GEOMETRY	= 1
PAGE_ID_CONTENT_1	= 2
PAGE_ID_CONTENT_2	= 3
PAGE_ID_TEXT_1		= 4
PAGE_ID_TEXT_2		= 5

dim tabTitles[5]
	tabTitles[1] = `Геометрия Маркера`
	tabTitles[2] = `Данные Маркера 1 - ID, Отлив, Размеры`
	tabTitles[3] = `Данные Маркера 2 - Огнестойкость, Шумозащита`
	tabTitles[4] = `Формат Текста 1 - Текст Маркера и Размеры`
	tabTitles[5] = `Формат Текста 2 - Отлив, Огнестойкость и Шумозащита`

dim tabIcons[5]
	tabIcons[1] = "uiTab_MarkerGeom_flat(1)"
	tabIcons[2] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[3] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[4] = "uiTab_TextMarkerLabelZone_flat(1)"
	tabIcons[5] = "uiTab_TextMarkerLabelZone_flat(1)"

file_dependence "uiTab_MarkerGeom_flat(1)"
file_dependence "uiTab_MarkerLabelContent_flat(1)"
file_dependence "uiTab_TextMarkerLabelZone_flat(1)"

ui_current_page gs_ui_current_page
ui_page gs_ui_current_page

! ==============================================================================
! Call tabpage selector
! ==============================================================================
call "ui_tab_popup" parameters 	nTabs 				= vardim1(tabTitles),
								gs_ui_current_page 	= gs_ui_current_page,
								tabTitles			= tabTitles,
								tabIcons			= tabIcons


! ==============================================================================
! Tabpages
! ==============================================================================

uiPaperSize = `Размер Бумаги`

prev_pos_x=278
prev_pos_y=90

px = 30
py = 20
cx = px
cy = py


ui_page PAGE_ID_GEOMETRY
	gosub "GeometryPage"

ui_page PAGE_ID_CONTENT_1
	gosub "Content1Page"

ui_page PAGE_ID_CONTENT_2
	gosub "Content2Page"

ui_page PAGE_ID_TEXT_1
	gosub "Text1Page"

ui_page PAGE_ID_TEXT_2
	gosub "Text2Page"


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"GeometryPage":
! ------------------------------------------------------------------------------

	x1		=	10
	x2		=	165
	x3 		= 	210
	yblock	=	35
	xCheckbox	= 15
	outFldHgt	= 15
	inFldHgt	= 19

	if iMarkerShape = SHAPE_CIRCLE or iMarkerShape = SHAPE_FRAGMENT then
		rot_flag = 4
	else
		rot_flag = 0
	endif
! ------------------------------------------------------------------------------

	ui_separator prev_pos_x-5, yblock, prev_pos_x-5, 266

	ui_infield{3} "AC_SameSide",	x1,		yblock-4,		prev_pos_x - x1 -10,	inFldHgt,
		7,"",2,1,
		0,0,0,0,
		1,`Зеркальное Расположение Маркера`, 0,
		2,`Зеркальное Расположение Маркера`, 1

	yblock = yblock + 21

	ui_outfield `Форма Маркера`,		x1 + xCheckbox,	yblock,		x2-(x1 + xCheckbox)-1,		outFldHgt
	ui_infield{4} "iMarkerShape", x2, yblock-4, prev_pos_x - x2 -10, inFldHgt,
		8, "", 9, 1,
		0, 0, 0, 0,
		1, stShapes[1], SHAPE_CIRCLE,
		2, stShapes[2], SHAPE_RECT,
		3, stShapes[3], SHAPE_FRAGMENT,
		4, stShapes[4], SHAPE_OVAL,
		5, stShapes[5], SHAPE_TRIANGLE,
		6, stShapes[6], SHAPE_PENTAGON,
		7, stShapes[7], SHAPE_HEXAGON,
		8, stShapes[8], SHAPE_OCTAGON,
		9, stShapes[9], SHAPE_NONE

	yblock	=	yblock + 21

	if iMarkerShape = SHAPE_CIRCLE then
		ui_infield{3} "gs_markertext_fit",	x1,		yblock-4,		prev_pos_x - x1 -10,	inFldHgt,
			7,"",2,1,
			0,0,0,0,
			1,`Маркер по Размеру Текста`, 0,
			2,`Маркер по Размеру Текста`, 1
	else
		ui_infield{3} "gs_marker_rotation",	x1,		yblock-4,		prev_pos_x - x1 -10,	inFldHgt,
			7,"",2,1,
			0,0,0,0,
			1,`Поворот Маркера`, 0,
			2,`Поворот Маркера`, 1
	endif

	yblock = yblock + 21

	ui_outfield `Ориентации Текста ID`,	x1 + xCheckbox,		yblock,		x2-(x1 + xCheckbox)-1,	outFldHgt
	ui_infield{4} "iMarkerDir", x2, yblock-4, prev_pos_x - x2 -10, inFldHgt,
		8, "", 4, 1,
		0, 0, 0, 0,
		1, stMarkerDirValues[1], 1,
		2, stMarkerDirValues[2], 2,
		3, stMarkerDirValues[3], 3,
		4, stMarkerDirValues[4], 4

	yblock = yblock + 19

! ------------------------------------------------------------------------------
	ui_separator 	x1,		yblock, 	prev_pos_x-10,	yblock

	yblock = yblock + 7

	ui_outfield `Единицы Размеров Маркера`,		x1 + xCheckbox, yblock,		x3-(x1 + xCheckbox)-1,	outFldHgt
	ui_infield{4} "iMarkerSizeUnit", x3, yblock-4, prev_pos_x - x3 -10, inFldHgt,
		8, "", 3, 1,
		0, 0, 0, 0,
		1, `мм`, 1,
		2, `см`, 2,
		3, `точки`, 3
	yblock = yblock + 21

	ui_outfield `(1)`,					0,				yblock,	x1 + xCheckbox-1,		outFldHgt
	ui_outfield `Расположение Маркера`,		x1 + xCheckbox, yblock,	x3-(x1 + xCheckbox)-1, 	outFldHgt
	if iMarkerSizeUnit = 1 then	ui_infield "headDistMm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 2 then	ui_infield "headDistCm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 3 then	ui_infield "headDistPoint",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize

	yblock = yblock + 21

	ui_outfield `(2)`,					0,				yblock,	x1 + xCheckbox-1,		outFldHgt
	ui_outfield `Смещение Маркера`,		x1 + xCheckbox, yblock,	x3-(x1 + xCheckbox)-1,	outFldHgt
	if iMarkerSizeUnit = 1 then	ui_infield "markerOffsetMm",	x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 2 then	ui_infield "markerOffsetCm",	x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 3 then	ui_infield "markerOffsetPoint", x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize

	yblock = yblock + 21

	ui_outfield `(3)`,					0,				yblock,	x1 + xCheckbox-1,		outFldHgt, 4 * not(AC_WidHei_On)
	ui_outfield `Расположение Размеров`,		x1 + xCheckbox, yblock,	x3-(x1 + xCheckbox)-1,	outFldHgt, 4 * not(AC_WidHei_On)
	if iMarkerSizeUnit = 1 then	ui_infield "whDistMm",			x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 2 then	ui_infield "whDistCm",			x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 3 then	ui_infield "whDistPoint",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize

	yblock = yblock + 21

	ui_outfield `(4)`,					0,				yblock,		x1 + xCheckbox-1,		outFldHgt
	ui_infield{3} "GS_Center_Line",		x1 + xCheckbox,	yblock-4,	x3-(x1 + xCheckbox)-1,	inFldHgt,
		7,"",2,1,
		0,0,0,0,
		1,`Продление Линии`, 0,
		2,`Продление Линии`, 1
	if iMarkerSizeUnit = 1 then	ui_infield "footLengthMm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 2 then	ui_infield "footLengthCm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 3 then	ui_infield "footLengthPoint",	x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize

	yblock = yblock + 21

	ui_outfield `(5)`,					0,				yblock, x1 + xCheckbox-1,		outFldHgt, 4 * not(AC_Sill_On)
	ui_outfield `Расстояние до Высоты Отлива`, x1 + xCheckbox, yblock, x3-(x1 + xCheckbox)-1,	outFldHgt, 4 * not(AC_Sill_On)
	if iMarkerSizeUnit = 1 then	ui_infield "sillDistMm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 2 then	ui_infield "sillDistCm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 3 then	ui_infield "sillDistPoint",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize

	yblock = yblock + 21

	ui_outfield `(6)`,					0,				yblock, x1 + xCheckbox-1,		outFldHgt, 4 * not(AC_Sill_On)
	ui_outfield `Смещение Высоты Отлива `,	x1 + xCheckbox, yblock, x3-(x1 + xCheckbox)-1,	outFldHgt, 4 * not(AC_Sill_On)
	if iMarkerSizeUnit = 1 then	ui_infield "sillOffsetMm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 2 then	ui_infield "sillOffsetCm",		x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize
	if iMarkerSizeUnit = 3 then	ui_infield "sillOffsetPoint",	x3, yblock-4, prev_pos_x - x3 -10, inFldHgt ui_tooltip uiPaperSize

	yblock = yblock + 21

	bIsFirstPage = 1
	gosub "getPictures"

return


! ==============================================================================
"Content1Page":
! ------------------------------------------------------------------------------

	prev_pos_x = 278

	x1			=	1
	x2			=	165
	x3 			= 	210

	yblock		=	28
	xCheckbox	=	15
	heightRow	= 	18

! ------------------------------------------------------------------------------
	ui_separator prev_pos_x-5, yblock, prev_pos_x-5, 266
	yblock = yblock + 6

! --- Marker Text --------------------------------------------------------------

	ui_style 0, 1
	ui_infield{3} "AC_ID_On",		x1,		yblock,		x2 -x1-1,	heightRow*1.5,
		7,"",2,1,
		0,0,0,0,
		1,`Показ ID`, 0,
		2,`Показ ID`, 1
	ui_style 0, 0

	ui_infield{4} "GS_iCustomText_1",	x2,		yblock-2,	prev_pos_x - x2 -10, heightRow,
		8,"",3,1,
		0,0,0,0,
		1,stCustomText[1], CTXT_IDNUMBER,
		2,stCustomText[4], CTXT_FULLIDNUM,
		3,stCustomText[3], CTXT_CUSTOMTEXT

	yblock = yblock + 21

	ui_infield "AC_MarkerText_1", 	x2,		yblock-2,	prev_pos_x - x2 -10, heightRow

	yblock = yblock + 21

! ------------------------------------------------------------------------------
	ui_separator 	x1,		yblock, 	prev_pos_x-10,	yblock
	yblock = yblock + 6

! --- Sill Values --------------------------------------------------------------

	ui_sill_flag = 0
	if AC_Sill_On = 0 then ui_sill_flag = 4
	if GS_iCustomText_4 = CTXT_CUSTOMTEXT then ui_sill_flag = 4

	ui_style 0, 1
	ui_infield{3} "AC_Sill_On",		x1,		yblock,		x2 -x1-1,			heightRow,
		7,"",2,1,
		0,0,0,0,
		1,`Показ Высоты Отлива`, 0,
		2,`Показ Высоты Отлива`, 1
	ui_style 0, 0

	ui_infield{4} "GS_iCustomText_4", 	x2,		yblock,	prev_pos_x - x2 -10, heightRow,
		8,"",2,1,
		0,0,0,0,
		1, stCustomText[2], CTXT_MEASUREDVALUE,
		2, stCustomText[3], CTXT_CUSTOMTEXT

	yblock = yblock + 21

	if GS_iCustomText_4 = CTXT_CUSTOMTEXT then
		ui_infield "AC_MarkerText_4", 			x2,			yblock,	prev_pos_x - x2 -10, heightRow
	else
		ui_outfield `Высота Отлива от` , x1+xCheckbox, 	yblock,	x2 -x1-1 -xCheckbox, heightRow, ui_sill_flag
		ui_infield{4} "iShowSillTo",		x2,				yblock,	prev_pos_x - x2 -10, heightRow,
			8, "", 3, 1,
			0, 0, 0, 0,
			1, stShowSillToValues[1], 1,
			2, stShowSillToValues[2], 2,
			3, stShowSillToValues[3], 3
	endif


	yblock = yblock + 21

	if gs_dw_marker_type = 1 | gs_dw_marker_type = 2 then
		ui_infield{3} "AC_SillNominal",		x1,		yblock-2,		x2 -x1-1-30, heightRow*1.5,
			7,"",2,1,
			0,0,0,0,
			1,`Номинальная Высота Отлива`, 0,
			2,`Номинальная Высота Отлива`, 1
	endif

	ui_outfield `Префикс`,			x2 - 30,	yblock,		50, heightRow, ui_sill_flag
	ui_infield "AC_SillPrefix", 	x2 + 21,	yblock,	prev_pos_x - x2 -10 -21, heightRow

	yblock = yblock + 30

	ui_style 0, 1

! ------------------------------------------------------------------------------
	ui_separator 	x1,		yblock, 	prev_pos_x-10,	yblock
	yblock = yblock + 6


! --- Marker Dimensions --------------------------------------------------------
	! NOT NED version Marker UI page
	if not(gsIsNedMarker) then
		if AC_WidHei_On=0 then ui_WH_flag=4 else ui_WH_flag=0

		ui_style 0, 1
		ui_infield{3} "AC_WidHei_On",	x1,		yblock,		x2 -x1-1,			heightRow,
			7,"",2,1,
			0,0,0,0,
			1,`Показ Размеров`, 0,
			2,`Показ Размеров`, 1
		ui_style 0, 0

		if gs_dw_marker_type <> 3 then
			!ui_infield "gs_dimension_select",	x2,		yblock,	prev_pos_x - x2 -10, heightRow
			ui_infield{4} "gs_dimension_select_int", x2, yblock, prev_pos_x - x2 -10, heightRow,
				8, "", 8, 1,
				0, 0, 0, 0,
				1, stDimensionTypes[1], 1,
				2, stDimensionTypes[2], 2,
				3, stDimensionTypes[3], 3,
				4, stDimensionTypes[4], 4,
				5, stDimensionTypes[5], 5,
				6, stDimensionTypes[6], 6,
				7, stDimensionTypes[7], 7,
				8, stDimensionTypes[8], 8
		endif

		yblock = yblock + 21

		ui_outfield `Ширина Проема`, x1 + xCheckBox, yblock, 148, 20, ui_WH_flag
		ui_infield{4} "GS_iCustomText_2", x2,		yblock,	prev_pos_x - x2 -10, heightRow,
			8,"",2,1,
			0,0,0,0,
			1, stCustomText[2], CTXT_MEASUREDVALUE,
			2, stCustomText[3], CTXT_CUSTOMTEXT

		yblock = yblock + 21

		ui_infield "AC_MarkerText_2", x2,		yblock,	prev_pos_x - x2 -10, heightRow

		yblock = yblock + 21

		ui_outfield `Высота Проема`, x1 + xCheckBox, yblock, 148, 20, ui_WH_flag
		ui_infield{4} "GS_iCustomText_3", x2,		yblock,	prev_pos_x - x2 -10, heightRow,
			8,"",2,1,
			0,0,0,0,
			1, stCustomText[2], CTXT_MEASUREDVALUE,
			2, stCustomText[3], CTXT_CUSTOMTEXT

		yblock = yblock + 21

		ui_infield "AC_MarkerText_3", x2,		yblock,	prev_pos_x - x2 -10, heightRow
	endif

	if gsIsNedMarker then
		if AC_WidHei_On=0 then ui_WH_flag=4 else ui_WH_flag=0

		ui_style 0, 1
		ui_infield{3} "AC_WidHei_On",	x1,		yblock,		x2 -x1-1,	heightRow,
			7,"",2,1,
			0,0,0,0,
			1,`Показ Размеров`, 0,
			2,`Показ Размеров`, 1
		ui_style 0, 0

		ui_infield{4} "gs_dimension_select_int", 90, yblock+23-3, 180, heightRow,
			8, "", 8, 1,
			0, 0, 0, 0,
			1, stDimensionTypes[1], 1,
			2, stDimensionTypes[2], 2,
			3, stDimensionTypes[3], 3,
			4, stDimensionTypes[4], 4,
			5, stDimensionTypes[5], 5,
			6, stDimensionTypes[6], 6,
			7, stDimensionTypes[7], 7,
			8, stDimensionTypes[8], 8
		yblock=yblock+21
		ui_style 0, 0

		IF  bIsDutchDimType THEN

			ui_outfield `Префикс`, 140, yblock+21, 50, heightRow, ui_WH_flag
			ui_infield "NedSizePref", 191, yblock+21-3, 80, heightRow

			ui_outfield `Разделитель`, 140, yblock+42, 50, heightRow, ui_WH_flag
			ui_infield "NedSizeSep", 191, yblock+42-3, 80, heightRow

			ui_outfield `Показ текста под Отливом`, 80, yblock+63, 110, heightRow, ui_WH_flag
			ui_infield "RotWidthText",  191, yblock+63-3, 80, heightRow

		endif

	endif

	ui_style 0, 0

	bIsFirstPage = 0
	gosub "getPictures"

return


! ==============================================================================
"Content2Page":
! ------------------------------------------------------------------------------

	prev_pos_x = 278

	x1			=	1
	x2			=	165
	x3 			= 	210

	yblock		=	32
	xCheckbox	=	15
	heightRow	= 	18


! ------------------------------------------------------------------------------
	ui_separator prev_pos_x-5, yblock, prev_pos_x-5, 266

! --- Fire Rating --------------------------------------------------------------

	ui_fire_flag = 1	! Aligns to the right edge
	if GS_iCustomText_5 = CTXT_CUSTOMTEXT and AC_show_firerating then
		ui_fire_flag = ui_fire_flag + 4
	endif

	ui_style 0, 1
	ui_infield{3} "AC_show_firerating",		x1,		yblock,		x2 -x1-3,	heightRow*2,
		7,"",2,1,
		0,0,0,0,
		1,`Показ Огнестойкости`, 0,
		2,`Показ Огнестойкости`, 1
	ui_style 0, 0

	ui_infield{4} "GS_iCustomText_5",	x2,		yblock+8,	prev_pos_x - x2 -10, heightRow,
		8,"",2,1,
		0,0,0,0,
		1, stCustomText[2], CTXT_MEASUREDVALUE,
		2, stCustomText[3], CTXT_CUSTOMTEXT

	yblock = yblock + 21 + 10

	ui_infield "AC_MarkerText_5", 	x2,		yblock-2,	prev_pos_x - x2 -10, heightRow

	yblock = yblock + 21

	ui_outfield `Префикс`,			x1,		yblock,		x2 -x1-3, heightRow, ui_fire_flag
	ui_infield "AC_FirePrefix", 	x2,		yblock-2,	prev_pos_x - x2 -10, heightRow

	yblock=yblock+21

! ------------------------------------------------------------------------------
	ui_separator 	x1,		yblock, 	prev_pos_x-10,	yblock
	yblock = yblock + 6

! --- Acoustic Rating ----------------------------------------------------------

	ui_acoustic_flag = 1		! Aligns to the right edge
	if GS_iCustomText_6 = CTXT_CUSTOMTEXT and AC_show_acousticrating then
		ui_acoustic_flag = ui_acoustic_flag + 4
	endif

	ui_style 0, 1
	ui_infield{3} "AC_show_acousticrating",		x1,		yblock,		x2 -x1-3,	heightRow*2 ,
		7,"",2,1,
		0,0,0,0,
		1,`Показ Шумозащиты`, 0,
		2,`Показ Шумозащиты`, 1
	ui_style 0, 0

	ui_infield{4} "GS_iCustomText_6",	x2,		yblock+8,	prev_pos_x - x2 -10, heightRow,
		8,"",2,1,
		0,0,0,0,
		1, stCustomText[2], CTXT_MEASUREDVALUE,
		2, stCustomText[3], CTXT_CUSTOMTEXT

	yblock = yblock + 21 + 10

	ui_infield "AC_MarkerText_6", 	x2,		yblock-2,	prev_pos_x - x2 -10, heightRow

	yblock = yblock + 21

	ui_outfield `Префикс`,			x1,		yblock,		x2 -x1-3, heightRow, ui_acoustic_flag
	ui_infield "AC_AcousticPrefix", x2,		yblock-2,	prev_pos_x - x2 -10, heightRow

	yblock = yblock + 21

! ------------------------------------------------------------------------------
	ui_separator 	x1,		yblock, 	prev_pos_x-10,	yblock
	yblock = yblock + 6

! --- U-value ----------------------------------------------------------

	ui_uvalue_flag = 1		! Aligns to the right edge
	if GS_iCustomText_7 = CTXT_CUSTOMTEXT and AC_show_Uvalue then
		ui_uvalue_flag = ui_uvalue_flag + 4
	endif

	ui_style 0, 1
	ui_infield{3} "AC_show_Uvalue",		x1,		yblock,		x2 -x1-3,	heightRow*2 ,
		7,"",2,1,
		0,0,0,0,
		1,`Показ U-значения`, 0,
		2,`Показ U-значения`, 1
	ui_style 0, 0

	ui_infield{4} "GS_iCustomText_7",	x2,		yblock+8,	prev_pos_x - x2 -10, heightRow,
		8,"",2,1,
		0,0,0,0,
		1, stCustomText[2], CTXT_MEASUREDVALUE,
		2, stCustomText[3], CTXT_CUSTOMTEXT

	yblock = yblock + 21 + 10

	ui_infield "AC_MarkerText_7", 	x2,		yblock-2,	prev_pos_x - x2 -10, heightRow

	yblock = yblock + 21

	ui_outfield `Префикс`,			x1,		yblock,		x2 -x1-3, heightRow, ui_acoustic_flag
	ui_infield "AC_UvaluePrefix", x2,		yblock-2,	prev_pos_x - x2 -10, heightRow

	yblock = yblock + 21

	bIsFirstPage = 0
	gosub "getPictures"

return


! ==============================================================================
"Text1Page":
! ------------------------------------------------------------------------------

	yblock	= 35
	x1 		= 1

	ui_style 0, 1
	ui_infield{3} "GS_UniTextFormat",		x1,		yblock,		350,	heightRow,
		7,"",2,1,
		0,0,0,0,
		1,`Единые Параметры Текста`, 0,
		2,`Единые Параметры Текста`, 1

	yblock=yblock+23

	ui_style 0, 0


!  --- Marker Text -------------------------------------------------------------

	if Gs_UniTextFormat then
		ui_outfield `Текст Маркера`, 0, yblock, 150, 15
		iFlag = 4
	else
		ui_outfield `Текст ID`, 0, yblock, 150, 15
		iFlag = 0
	endif

	yblock=yblock+21

	ui_pict "Pen Colour_flat(1)", 20, yblock-4, 13, 19, 1
	ui_infield "AC_TextPen_1", 40, yblock-4, 30, 19

	ui_pict "ui_FontSize_flat(1)", 75, yblock-1, 27, 14, 1
	ui_infield "AC_TextSize_1", 105, yblock-4, 35, 19

	ui_infield "AC_TextFont_1", 150, yblock-4, 100, 19

	if not (Gs_UniTextFormat) then
		ui_infield{3} "GS_Text_Style_1B", 270, yblock-7, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_bold_flat(1)", "", 0,
			"ui_bold_flat(1)", "", 1\
			ui_tooltip `Жирный`

		ui_infield{3} "GS_Text_Style_1I", 305, yblock-7, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_italic_flat(1)", "", 0,
			"ui_italic_flat(1)", "", 1\
			ui_tooltip `Курсив`

		ui_infield{3} "GS_Text_Style_1U", 340, yblock-7, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_underline_flat(1)", "", 0,
			"ui_underline_flat(1)", "", 1\
			ui_tooltip `Подчеркнутый`
	endif

	yblock=yblock+23


! --- Opening Width ------------------------------------------------------------

	ui_outfield `Ширина Проема`, 0, yblock, 150, 15, iFlag

	yblock=yblock+21

	ui_pict "Pen Colour_flat(1)", 20, yblock-4, 13, 19, 1
	ui_infield "AC_TextPen_2", 40, yblock-4, 30, 19

	ui_pict "ui_FontSize_flat(1)", 75, yblock-1, 27, 14, 1
	ui_infield "AC_TextSize_2", 105, yblock-4, 35, 19

	ui_infield "AC_TextFont_2", 150, yblock-4, 100, 19

	ui_infield{3} "GS_Text_Style_2B", 270, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_bold_flat(1)", "", 0,
		"ui_bold_flat(1)", "", 1\
		ui_tooltip `Жирный`

	ui_infield{3} "GS_Text_Style_2I", 305, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_italic_flat(1)", "", 0,
		"ui_italic_flat(1)", "", 1\
		ui_tooltip `Курсив`

	ui_infield{3} "GS_Text_Style_2U", 340, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_underline_flat(1)", "", 0,
		"ui_underline_flat(1)", "", 1\
		ui_tooltip `Подчеркнутый`

	yblock=yblock+23


! --- Opening Height -----------------------------------------------------------

	ui_outfield `Высота Проема`, 0, yblock, 150, 15, iFlag

	yblock=yblock+23

	ui_pict "Pen Colour_flat(1)", 20, yblock-4, 13, 19, 1
	ui_infield "AC_TextPen_3", 40, yblock-4, 30, 19

	ui_pict "ui_FontSize_flat(1)", 75, yblock-1, 27, 14, 1
	ui_infield "AC_TextSize_3", 105, yblock-4, 35, 19

	ui_infield "AC_TextFont_3", 150, yblock-4, 100, 19

	ui_infield{3} "GS_Text_Style_3B", 270, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_bold_flat(1)", "", 0,
		"ui_bold_flat(1)", "", 1\
		ui_tooltip `Жирный`

	ui_infield{3} "GS_Text_Style_3I", 305, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_italic_flat(1)", "", 0,
		"ui_italic_flat(1)", "", 1\
		ui_tooltip `Курсив`

	ui_infield{3} "GS_Text_Style_3U", 340, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_underline_flat(1)", "", 0,
		"ui_underline_flat(1)", "", 1\
		ui_tooltip `Подчеркнутый`

	yblock=yblock+26

	ui_outfield `Единицы Размеров`,  0, yblock,	148, 15, 4 * not(AC_WidHei_On)
	ui_infield{3} "iWorkUnitLinear", 150, yblock-4, 144, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", stUnitFormats[1],	DIMUNIT_MM,
			"", stUnitFormats[2],	DIMUNIT_CM,
			"", stUnitFormats[3],	DIMUNIT_DM,
			"", stUnitFormats[4],	DIMUNIT_METER,
			"", stUnitFormats[5],	DIMUNIT_INCH,
			"", stUnitFormats[6],	DIMUNIT_PROJECT

	ui_outfield `Десятичные`,  300, yblock,	100, 15, 4 * _bLockLinearUnitDecimal
	ui_infield{3} "iWorkUnitLinearDecimal", 402 , yblock-4,	40, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4

return


! ==============================================================================
"Text2Page":
! ------------------------------------------------------------------------------

	yblock	= 35
	x1 		= 1

	ui_style 0, 1
	ui_infield{3} "GS_UniTextFormat",		x1,		yblock,		350,	heightRow,
		7,"",2,1,
		0,0,0,0,
		1,`Единые Параметры Текста`, 0,
		2,`Единые Параметры Текста`, 1

	yblock=yblock+23

	ui_style 0, 0


! --- Sill ---------------------------------------------------------------------

	ui_outfield `Параметры Отлива`, 0, yblock, 150, 15, iFlag

	yblock=yblock+21

	ui_pict "Pen Colour_flat(1)", 20, yblock-4, 13, 19, 1
	ui_infield "AC_TextPen_4", 40, yblock-4, 30, 19

	ui_pict "ui_FontSize_flat(1)", 75, yblock-1, 27, 14, 1
	ui_infield "AC_TextSize_4", 105, yblock-4, 35, 19

	ui_infield "AC_TextFont_4", 150, yblock-4, 100, 19

	ui_infield{3} "GS_Text_Style_4B", 270, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_bold_flat(1)", "", 0,
		"ui_bold_flat(1)", "", 1\
		ui_tooltip `Жирный`

	ui_infield{3} "GS_Text_Style_4I", 305, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_italic_flat(1)", "", 0,
		"ui_italic_flat(1)", "", 1\
		ui_tooltip `Курсив`

	ui_infield{3} "GS_Text_Style_4U", 340, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_underline_flat(1)", "", 0,
		"ui_underline_flat(1)", "", 1\
		ui_tooltip `Подчеркнутый`

	yblock=yblock+26

	ui_outfield `Единицы Высоты Отлива`,  0, yblock,	148, 15, 4 * not(AC_Sill_On)
	ui_infield{3} "iWorkUnitSillHgt", 150, yblock-4, 144, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", stUnitFormats[1],	DIMUNIT_MM,
			"", stUnitFormats[2],	DIMUNIT_CM,
			"", stUnitFormats[3],	DIMUNIT_DM,
			"", stUnitFormats[4],	DIMUNIT_METER,
			"", stUnitFormats[5],	DIMUNIT_INCH,
			"", stUnitFormats[6],	DIMUNIT_PROJECT

	ui_outfield `Десятичные`,  300, yblock,	100, 15, 4 * _bLockSillHgtUnitDecimal
	ui_infield{3} "iWorkUnitSillHgtDecimal", 402 , yblock-4,	40, 19,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4

	yblock=yblock+23


! --- Fire Rating --------------------------------------------------------------

	ui_outfield `Текст Огнестойкости`, 0, yblock, 150, 15, iFlag

	yblock=yblock+21

	ui_pict "Pen Colour_flat(1)", 20, yblock-4, 13, 19, 1
	ui_infield "AC_TextPen_5", 40, yblock-4, 30, 19

	ui_pict "ui_FontSize_flat(1)", 75, yblock-1, 27, 14, 1
	ui_infield "AC_TextSize_5", 105, yblock-4, 35, 19

	ui_infield "AC_TextFont_5", 150, yblock-4, 100, 19

	ui_infield{3} "GS_Text_Style_5B", 270, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_bold_flat(1)", "", 0,
		"ui_bold_flat(1)", "", 1\
		ui_tooltip `Жирный`

	ui_infield{3} "GS_Text_Style_5I", 305, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_italic_flat(1)", "", 0,
		"ui_italic_flat(1)", "", 1\
		ui_tooltip `Курсив`

	ui_infield{3} "GS_Text_Style_5U", 340, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_underline_flat(1)", "", 0,
		"ui_underline_flat(1)", "", 1\
		ui_tooltip `Подчеркнутый`

	yblock=yblock+23


! --- Acoustic Rating ----------------------------------------------------------

	ui_outfield `Текст Шумозащиты`, 0, yblock, 150, 15, iFlag

	yblock=yblock+21

	ui_pict "Pen Colour_flat(1)", 20, yblock-4, 13, 19, 1
	ui_infield "AC_TextPen_6", 40, yblock-4, 30, 19

	ui_pict "ui_FontSize_flat(1)", 75, yblock-1, 27, 14, 1
	ui_infield "AC_TextSize_6", 105, yblock-4, 35, 19

	ui_infield "AC_TextFont_6", 150, yblock-4, 100, 19

	ui_infield{3} "GS_Text_Style_6B", 270, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_bold_flat(1)", "", 0,
		"ui_bold_flat(1)", "", 1\
		ui_tooltip `Жирный`

	ui_infield{3} "GS_Text_Style_6I", 305, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_italic_flat(1)", "", 0,
		"ui_italic_flat(1)", "", 1\
		ui_tooltip `Курсив`

	ui_infield{3} "GS_Text_Style_6U", 340, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_underline_flat(1)", "", 0,
		"ui_underline_flat(1)", "", 1\
		ui_tooltip `Подчеркнутый`

	yblock=yblock+23


! --- U-value ----------------------------------------------------------

	ui_outfield `Текст U-значения`, 0, yblock, 150, 15, iFlag

	yblock=yblock+21

	ui_pict "Pen Colour_flat(1)", 20, yblock-4, 13, 19, 1
	ui_infield "AC_TextPen_7", 40, yblock-4, 30, 19

	ui_pict "ui_FontSize_flat(1)", 75, yblock-1, 27, 14, 1
	ui_infield "AC_TextSize_7", 105, yblock-4, 35, 19

	ui_infield "AC_TextFont_7", 150, yblock-4, 100, 19

	ui_infield{3} "GS_Text_Style_7B", 270, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_bold_flat(1)", "", 0,
		"ui_bold_flat(1)", "", 1\
		ui_tooltip `Жирный`

	ui_infield{3} "GS_Text_Style_7I", 305, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_italic_flat(1)", "", 0,
		"ui_italic_flat(1)", "", 1\
		ui_tooltip `Курсив`

	ui_infield{3} "GS_Text_Style_7U", 340, yblock-7, 24, 24,
		6, "",
		2,
		1, 16, 16, 16, 16,
		"ui_underline_flat(1)", "", 0,
		"ui_underline_flat(1)", "", 1\
		ui_tooltip `Подчеркнутый`

return


! ==============================================================================
"getPictures":
! ------------------------------------------------------------------------------

	arrow_bot_mod = 0
	arrow_top_mod = 0
	if gs_dw_marker_type = 1 then
		ui_pict "Marker Window Preview(13)", prev_pos_x, prev_pos_y, 150, 140, 1
	endif
	if gs_dw_marker_type = 2 then
		ui_pict "Marker Door Preview(13)", prev_pos_x, prev_pos_y, 150, 140, 1
	endif
	if gs_dw_marker_type = 3 then
		ui_pict "Marker Skylight Preview(15)", prev_pos_x+(150-68)/2+7, prev_pos_y+40, 68, 96, 1
		arrow_bot_mod = 8
		arrow_top_mod = 61
	endif

	if abs(markerOffsetMm) < EPS then
		Moffset = 0
	else
		if markerOffsetMm > 0 then
			Moffset = 25
		else
			Moffset = -25
		endif
	endif

	if Moffset <> 0 then
		ui_pict "Marker Center Line(13)", prev_pos_x+82, prev_pos_y+20
	endif

	if GS_Center_Line then
		ui_pict "Marker Line(13)",			prev_pos_x+Moffset+82, prev_pos_y-36
	else
		ui_pict "Marker Dashed Line(13)",	prev_pos_x+Moffset+82, prev_pos_y-36, 1, 180, 1
	endif

	if AC_Sill_On then
		if abs(sillOffsetMm) < EPS then
			Soffset=0
		else
			if sillOffsetMm > 0 then
				Soffset = 15
			else
				Soffset = -15
			endif
		endif

		ui_style 0, 1
		ui_outfield `Отлив`, prev_pos_x+Moffset+Soffset+82-35, prev_pos_y+155, 70, 20,2
	endif


	if AC_show_firerating then
		ui_outfield `ОС`, prev_pos_x+145, prev_pos_y+85, 20, 20
	endif

	if AC_show_acousticrating then
		ui_outfield `ШЗ`, prev_pos_x+145, prev_pos_y+65, 20, 20
	endif

	if AC_show_Uvalue then
		ui_outfield `U`, prev_pos_x+145, prev_pos_y+45, 20, 20
	endif

	if bIsFirstPage then
		call "ui_arrow" parameters	dir=0, pixel=136-arrow_top_mod, x=prev_pos_x+18, y=prev_pos_y-35, type=1,
									auto_type=1, num=1, num_pos=4, sec_pos=0

		if GS_Center_Line then
			call "ui_arrow" parameters	dir=0, pixel=16-arrow_bot_mod, x=prev_pos_x+18, y=prev_pos_y+128+arrow_bot_mod, type=1,
										auto_type=1, num=4, num_pos=4, sec_pos=0
		endif

		if AC_WidHei_On then
			call "ui_arrow" parameters	dir=0, pixel=101-arrow_top_mod, x=prev_pos_x+142, y=prev_pos_y, type=1,
										auto_type=1, num=3, num_pos=4, sec_pos=0
		endif

		if AC_Sill_On then
			call "ui_arrow" parameters	dir=0, pixel=42-arrow_bot_mod, x=prev_pos_x+142, y=prev_pos_y+128+arrow_bot_mod, type=1,
										auto_type=1, num=5, num_pos=4, sec_pos=0

			if Soffset >= 0 then
				call "ui_arrow" parameters	dir=1, pixel=Soffset, x=prev_pos_x+Moffset+83,
											 y=prev_pos_y+150, type=1,
											auto_type=1, num=6, num_pos=4, sec_pos=4
			else
				call "ui_arrow" parameters	dir=1, pixel=-1*Soffset, x=prev_pos_x+Moffset+Soffset+82,
											 y=prev_pos_y+150, type=1,
											auto_type=1, num=6, num_pos=6, sec_pos=6
			endif
		endif

		if Moffset >= 0 then
			call "ui_arrow" parameters	dir=1, pixel=Moffset, x=prev_pos_x+82,
										y=prev_pos_y+70, type=1,
										auto_type=1, num=2, num_pos=1, sec_pos=1
		else
			call "ui_arrow" parameters	dir=1, pixel=-1*Moffset, x=prev_pos_x+Moffset+83,
										y=prev_pos_y+70, type=1,
										auto_type=1, num=2, num_pos=3, sec_pos=0
		endif
	endif

	if iMarkerShape <> SHAPE_NONE then
		if iMarkerShape = SHAPE_CIRCLE or iMarkerShape = SHAPE_FRAGMENT or iMarkerShape = SHAPE_OVAL then

			if iMarkerShape = SHAPE_FRAGMENT then
				ui_pict "MH Fragment(13)", prev_pos_x+Moffset+56, prev_pos_y-56, 53, 39, 1
			else
				if iMarkerShape = SHAPE_CIRCLE then
					ui_pict "MH Circle(13)", prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				else
					ui_pict "MH Oval(13)", prev_pos_x+Moffset+56, prev_pos_y-56, 53, 41, 1
				endif
			endif
		else
			if gs_marker_rotation then
				if iMarkerShape = SHAPE_TRIANGLE Then ui_pict "MH Triangle Rotated(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_RECT Then ui_pict "MH Rectangle Rotated(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_PENTAGON Then ui_pict "MH Pentagon Rotated(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_HEXAGON Then ui_pict "MH Hexagon Rotated(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_OCTAGON Then ui_pict "MH Octagon Rotated(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
			else
				if iMarkerShape = SHAPE_TRIANGLE Then ui_pict "MH Triangle Normal(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_RECT Then ui_pict "MH Rectangle Normal(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_PENTAGON Then ui_pict "MH Pentagon Normal(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_HEXAGON Then ui_pict "MH Hexagon Normal(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
				if iMarkerShape = SHAPE_OCTAGON Then ui_pict "MH Octagon Normal(13)",
					prev_pos_x+Moffset+61, prev_pos_y-56, 43, 43, 1
			endif
		endif
	endif

	if AC_ID_On then
		if GS_iCustomText_1 = CTXT_CUSTOMTEXT then
			ui_pict "C Letter(13)", prev_pos_x+Moffset+77, prev_pos_y-40, 11, 18, 1
		else
			ui_pict "N Letter(13)", prev_pos_x+Moffset+77, prev_pos_y-40, 11, 18, 1
		endif
	endif

	if AC_WidHei_On then
		if GS_iCustomText_2 = CTXT_CUSTOMTEXT then
			ui_pict "Marker Custom(14)", prev_pos_x+Moffset+70, prev_pos_y+6, 9, 20, 1
		else
			if bIsDutchDimType THEN
				if RotWidthText then
					ui_pict "Marker HeightWidth(14)", prev_pos_x+Moffset+50, prev_pos_y+140, 75, 11, 1
				else
					ui_pict "Marker HeightWidth Vert(14)", prev_pos_x+Moffset+70, prev_pos_y, 11, 75, 1
				endif
			else
				ui_pict "Marker Width(14)", prev_pos_x+Moffset+70, prev_pos_y, 11, 33, 1
			endif
		endif
		if GS_iCustomText_3 = CTXT_CUSTOMTEXT then
			ui_pict "Marker Custom(14)", prev_pos_x+Moffset+86, prev_pos_y+6, 9, 20, 1
		else
			if not(bIsDutchDimType) THEN
				ui_pict "Marker Height(14)", prev_pos_x+Moffset+86, prev_pos_y, 11, 33, 1
			endif
		endif
	endif

	if gs_show_hotspot_ui and bIsFirstPage then
		! Marker Head Position Editing
		ui_pict "Hotspot HorDir(13)", prev_pos_x+Moffset+70, prev_pos_y-34, 25, 5, 1
		ui_pict "Hotspot VerDir(13)", prev_pos_x+Moffset+80, prev_pos_y-46, 5, 25, 1

		! Center Line Length Editing
		ui_pict "Hotspot HorDir(13)", prev_pos_x+Moffset+70, prev_pos_y+140, 25, 5, 1
		ui_pict "Hotspot VerDir(13)", prev_pos_x+Moffset+80, prev_pos_y+130, 5, 25, 1

		if AC_WidHei_On then
			! Width/Height Position Editing
			ui_pict "Hotspot VerDir(13)", prev_pos_x+Moffset+80, prev_pos_y, 5, 25, 1
		endif

		If AC_Sill_On then
			ui_pict "Hotspot HorDir(13)", prev_pos_x+Moffset+Soffset+70, prev_pos_y+169, 25, 5, 1
			ui_pict "Hotspot VerDir(13)", prev_pos_x+Moffset+Soffset+80, prev_pos_y+159, 5, 25, 1
		endif

		if AC_show_firerating then
			ui_pict "Hotspot HorDir(13)", prev_pos_x+135, prev_pos_y+95, 25, 5, 1
			ui_pict "Hotspot VerDir(13)", prev_pos_x+145, prev_pos_y+85, 5, 25, 1
		endif

		if AC_show_acousticrating then
			ui_pict "Hotspot HorDir(13)", prev_pos_x+135, prev_pos_y+75, 25, 5, 1
			ui_pict "Hotspot VerDir(13)", prev_pos_x+145, prev_pos_y+65, 5, 25, 1
		endif

		if AC_show_Uvalue then
			ui_pict "Hotspot HorDir(13)", prev_pos_x+135, prev_pos_y+55, 25, 5, 1
			ui_pict "Hotspot VerDir(13)", prev_pos_x+145, prev_pos_y+45, 5, 25, 1
		endif
	endif

return


