
! --------------------------------------------------------------------------
!  CONTENT_SKIN
! --------------------------------------------------------------------------
if _bEnableContentSkinData then
! ==============================================================================
! SKIN CONTENT: in case the custom content array is empty, it is filled up with the automatic content 
! ==============================================================================

	! calculate custom rows
	 _bEmptyCustomArray = 1
	for iSearch = 1 to vardim1(dataCustomSkinRow)
		if dataCustomSkinRow[iSearch] # "" then
			 _bEmptyCustomArray = 0
		endif
	next iSearch

	iCustom = 1
	if _bEmptyCustomArray and not(bDefaultElem) then
		for iRow = 1 to _nRowLabel
			if iContentOfRow[iRow] = CONTENT_SKIN then
				dataCustomSkinRow[iCustom] = _actualSkins[iCustom]
				iCustom = iCustom + 1
			endif
		next iRow 

		parameters dataCustomSkinRow = dataCustomSkinRow
	endif
endif

! --------------------------------------------------------------------------
!  CONTENT_MATERIAL
! --------------------------------------------------------------------------
if _bEnableContentMaterial then

	dim iAvailableMaterial[]
	iAvailableMaterial = _iAvailableMaterial
	parameters iAvailableMaterial = iAvailableMaterial 

	dim sAvailableMaterial[]
	sAvailableMaterial = _sAvailableMaterial
	parameters sAvailableMaterial = sAvailableMaterial 

	dim sAvailableMaterialPict[]
	sAvailableMaterialPict = _sAvailableMaterialPict
	parameters sAvailableMaterialPict = sAvailableMaterialPict

	! if by default elem set displayed Surface is not available in the parent tool after placing, 
	! content is switched to custom instead of another surface type
	_isOutofRange = 0	! for changing parent tool
	for i = 1 to vardim1(iAvailableMaterial)
		if iSurfaceToDisplay <> iAvailableMaterial[i] then
			_isOutofRange = 1
		else
			_isOutofRange = 0
			i = vardim1(iAvailableMaterial) ! force loop end
		endif
	next i
	if _isOutofRange then
		iSurfaceToDisplay = 99	! Custom Text
		parameters iSurfaceToDisplay = iSurfaceToDisplay
	endif

	values{2} "iSurfaceToDisplay" iAvailableMaterial, sAvailableMaterial
	if iSurfaceToDisplay <> 99 then
		lock "sCustomSurface"
		hideparameter "sCustomSurface"
	endif

endif
! ==========================================================================
!END 	!END	!END 	!END 	!END 	!END 	!END 	!END 	!END 	!END
! ==========================================================================




