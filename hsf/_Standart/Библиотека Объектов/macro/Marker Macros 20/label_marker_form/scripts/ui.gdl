
w_units_form = ""
rrr = request("Working_length_unit", "", w_units_form)
_bMetricDimension = strstr(w_units_form, "m")		! m, cm, mm

inFieldHeight = 19
outFieldHeight = 15
pictboxHeight = 40
penBoxHeight = 19
buttonHeight = 22

inFieldWidth = 115
inFieldWidthText = 135
inFieldWidthShort = 55
linkPictWidth = 18
penBoxWidth = 33
buttonWidth = 23

sepx = 285

x1 = 5
x2 = 165
x3 = sepx - 3
x5 = 354
x6 = 412
w6 = 444 - (x6 + 2)
	
pageStartY = 40

dy = 23

file_dependence "ui_InfoIcon_flat(1)"

_BoxWidthTooltip = ""
_BoxWidthTooltipLeft = ""
if bWrapText then
	_BoxWidthTooltip = `Ширина Текстового Блока`
	if bEnablePrefixColumn then
		_BoxWidthTooltipLeft = `Ширина Текста Столбца Названий Параметров`
		_BoxWidthTooltip = `Ширина Текста Столбца Значений`
	endif
else
	_BoxWidthTooltip = `Ширина Текстового Блока. Активируется при включении Переноса Текста в панели Стиля Текста.`
	if bEnablePrefixColumn then
		_BoxWidthTooltipLeft = `Ширина Текста Столбца Названий Параметров. Активируется при включении Переноса Текста в панели Стиля Текста.`
		_BoxWidthTooltip = `Ширина Текста Столбца Значений. Активируется при включении Переноса Текста в панели Стиля Текста.`
	endif
endif


! ==============================================================================
! Tabpages
! ==============================================================================

if iMyPage_Geometry > 0 then
	ui_page iMyPage_Geometry
		gosub "GeometrySettingsPage"
endif

if iMyPage_HeadStyle > 0 then		! Only Skin List Label has this page
	ui_page iMyPage_HeadStyle
		gosub "HeadStyleSettingsPage"
endif

if iMyPage_CustomSkins > 0 then		! Only Skin List Label has this page
	ui_page iMyPage_CustomSkins
		gosub "CustomSkinsContentPage"
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"GeometrySettingsPage":
! ------------------------------------------------------------------------------

	yCurr = pageStartY

	if bEnableLeaderLine then
		ui_separator sepx+3, 32, sepx+3, 266
	endif

	if bEnableHeadSwitch then
		ui_style 0,1
		ui_infield{3} "bShowHead", sepx+7, yCurr-4, 442-sepx-7, inFieldHeight,
				7, "", 2, 1, 0, 0, 0, 0,
				1, `Маркер над Флажком`, 0,
				2, `Маркер над Флажком`, 1
		ui_style 0,0
	endif

	_flagHiddenFrame = 4 * _bHideHeadFrameSettings

	ui_style 0,1
	ui_outfield `Форма Рамки`,			x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenFrame
	ui_style 0,0
	ui_infield{3} "iShapeHead",			x2, yCurr-4, x3-x2, pictboxHeight,
		2, 1, 20, 5, ! ui_head_shapes.png
		33, 33, 32, 32,
		1, _stShapeHead[2], MARKER_HEAD_CIRCLE,
		3, _stShapeHead[17], MARKER_HEAD_SQUARE_ROTATED,
		4, _stShapeHead[18], MARKER_HEAD_ROUNDED_RECT,
		2, _stShapeHead[8], MARKER_HEAD_RECTANGLE,
		5, _stShapeHead[12], MARKER_HEAD_POINTED_RECT_90,
		6, _stShapeHead[19], MARKER_HEAD_POINTED_RECT_120,
		8, _stShapeHead[9], MARKER_HEAD_DIAMOND,
		7, _stShapeHead[3], MARKER_HEAD_OVAL,
		9, _stShapeHead[4], MARKER_HEAD_TRIANGLE_UP,
		12, _stShapeHead[5], MARKER_HEAD_TRIANGLE_DOWN,
		11, _stShapeHead[6], MARKER_HEAD_TRIANGLE_LEFT,
		10, _stShapeHead[7], MARKER_HEAD_TRIANGLE_RIGHT,
		13, _stShapeHead[10], MARKER_HEAD_PENTAGON_UP,
		14, _stShapeHead[11], MARKER_HEAD_PENTAGON_DOWN,
		15, _stShapeHead[13], MARKER_HEAD_HEXAGON,
		16, _stShapeHead[14], MARKER_HEAD_HEXAGON_ROTATED,
		17, _stShapeHead[15], MARKER_HEAD_OCTOGON,
		18, _stShapeHead[16], MARKER_HEAD_OCTOGON_ROTATED

	yCurr = yCurr + dy + (pictboxHeight - outFieldHeight-4)

	ui_infield{3} "bFitFrameToText", x1, yCurr,	x3-x1-1, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Рамка по Размеру Текста`, 0,
			2, `Рамка по Размеру Текста`, 1

	yCurr = yCurr + dy

	_bShowInfo = (bUseBuiltinPointer | not(_isHeadShape)) & not(bEnableHeadWithPointer)
	_bShowFrameSizes = not(bFitFrameToText) & (not(bUseBuiltinPointer) | bEnableHeadWithPointer)

	if _bShowFrameSizes then
		uI_infield{3} "bLinkSizes", x3-linkPictWidth-2, yCurr-4, linkPictWidth+2, 42,
						6, 2, 1, 1, !ui_LockFrameSizes.png
						14, 28, 14, 28,
						1, "", 0,
						1, "", 1	ui_tooltip `Связать Размеры Рамки (сохранять пропорции)`

		ui_outfield `Ширина Рамки`,		x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenFrame
		if _bMetricDimension then
			ui_infield "widthHead_mm",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight
			ui_outfield `мм`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenFrame
		else
			ui_infield "widthHead_pt",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight
			ui_outfield `пт`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenFrame
		endif

		yCurr = yCurr + dy

		ui_outfield `Высота Рамки`,		x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenFrame
		if _bMetricDimension then
			ui_infield "heightHead_mm",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight
			ui_outfield `мм`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenFrame
		else
			ui_infield "heightHead_pt",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight
			ui_outfield `пт`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenFrame
		endif

		yCurr = yCurr + dy
	endif

	if _bShowInfo then
		ui_pict "ui_InfoIcon_flat(1)", x1, yCurr, 14, 14, 1
		if bUseBuiltinPointer & not(bEnableHeadWithPointer) then
			ui_outfield `Применены Параметры Выноски (панель 'Линия-Выноска').`, 20, yCurr, sepx-26, outFieldHeight*1.5
		else
			ui_outfield `Включить рамку можно в панели Стиля Текста.`, 20, yCurr, sepx-26, outFieldHeight*2
		endif
	endif

	_flagHiddenPrefix	= 4 * (_bHideTextBoxContent | not(bUsePrefixColumn))
	_flagHiddenTextBox	= 4 * _bHideTextBoxContent

	yCurr = yCurr + 2*dy * not(_bShowFrameSizes)

	if bEnablePrefixColumn then
		ui_outfield `Ширина Текста Названий`,	x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenPrefix
		if _bMetricDimension then
			ui_infield "widthTextBoxPrefix_mm",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight ui_tooltip _BoxWidthTooltipLeft
			ui_outfield `мм`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenPrefix
		else
			ui_infield "widthTextBoxPrefix_pt",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight ui_tooltip _BoxWidthTooltipLeft
			ui_outfield `пт`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenPrefix
		endif
		yCurr = yCurr + dy

		ui_outfield `Ширина Текста Значений`,	x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenTextBox
	else
		ui_outfield `Ширина Текстового Блока`,	x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenTextBox
	endif
	if _bMetricDimension then
		ui_infield "widthTextBoxContent_mm",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight ui_tooltip _BoxWidthTooltip
		ui_outfield `мм`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenTextBox
	else
		ui_infield "widthTextBoxContent_pt",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight ui_tooltip _BoxWidthTooltip
		ui_outfield `пт`,				x3-33-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenTextBox
	endif

	yCurr = 170

	if bEnableSeparatorLine then
		ui_infield{3} "bShowSeparatorLines", x1, yCurr,	x3-x1-1, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Разделитель`, 0,
			2, `Разделитель`, 1
	endif

	yCurr = 187


	if bEnableLeaderLine then
		ui_separator 0, yCurr, sepx, yCurr
		yCurr = yCurr + dy/2
	endif

	if bEnableCustomHeaderStyle then		! Only Skin List Label has this option
		_flagHiddenStyle = 4 * (_bHideHeadEverySettings | not(bUseCustomHeaderStyle))

		ui_style 0,1
		ui_infield{3} "bUseCustomHeaderStyle", x1, yCurr-4,	x3-x1-1, outFieldHeight,
				7, "", 2, 1, 0, 0, 0, 0,
				1, `Замена Стиля Текста Маркера`, 0,
				2, `Замена Стиля Текста Маркера`, 1
		ui_style 0,0

		yCurr = yCurr + dy

		ui_infield{3} "fontTypeHeader", x1, yCurr-4,	inFieldWidthText, inFieldHeight

		if _flagHiddenStyle > 0 then
			ui_pict "Pen ColourLock_flat(1)", sepx - penBoxWidth - 1 - 20, yCurr-4, 13, 19, 1
		else
			ui_pict "Pen Colour_flat(1)", sepx - penBoxWidth - 1 - 20, yCurr-4, 13, 19, 1
		endif
		ui_infield{3} "penTextHeader", x3 - penBoxWidth, yCurr-4, penBoxWidth, penBoxHeight
		yCurr = yCurr + dy

		pictx = 27
		picty = 14
		if _flagHiddenStyle > 0 then
			ui_pict "ui_FontSize_hide_flat(1)", x1, yCurr, pictx, picty, 1
		else
			ui_pict "ui_FontSize_flat(1)", x1, yCurr, pictx, picty, 1
		endif

		if _bMetricDimension then
			ui_infield{3} "sizeFontHeader_mm", x1 + pictx + 5, yCurr-4,	inFieldWidthText - pictx - 5 - 18, inFieldHeight
			ui_outfield `мм`, x1 + pictx + 5 + inFieldWidthText - pictx - 5 - 18 + 3, yCurr, 30, outFieldHeight, _flagHiddenStyle
		else
			ui_infield{3} "sizeFontHeader_pt", x1 + pictx + 5, yCurr-4,	inFieldWidthText - pictx - 5 - 18, inFieldHeight
			ui_outfield `пт`, x1 + pictx + 5 + inFieldWidthText - pictx - 5 - 18 + 3, yCurr, 30, outFieldHeight, _flagHiddenStyle
		endif

		xdiff = 5
		ui_infield{3} "bBoldStyleHeader", x3-4*(buttonWidth+xdiff)+xdiff, yCurr-2, buttonWidth, buttonHeight,
				6, "",
				2,
				1, 16, 16, 16, 16,
				"ui_bold_flat(1)", "", 0,
				"ui_bold_flat(1)", "", 1\
				ui_tooltip `Жирный`

	
		ui_infield{3} "bItalicStyleHeader", x3-3*(buttonWidth+xdiff)+xdiff, yCurr-2, buttonWidth, buttonHeight,
				6, "",
				2,
				1, 16, 16, 16, 16,
				"ui_italic_flat(1)", "", 0,
				"ui_italic_flat(1)", "", 1\
				ui_tooltip `Курсив`

		ui_infield{3} "bUnderlineStyleHeader", x3-2*(buttonWidth+xdiff)+xdiff, yCurr-2, buttonWidth, buttonHeight,
				6, "",
				2,
				1, 16, 16, 16, 16,
				"ui_underline_flat(1)", "", 0,
				"ui_underline_flat(1)", "", 1\
				ui_tooltip `Подчеркнутый`

		ui_infield{3} "bStrikethroughStyleHeader", x3-1*(buttonWidth+xdiff)+xdiff, yCurr-2, buttonWidth, buttonHeight,
				6, "",
				2,
				1, 16, 16, 16, 16,
				"ui_text_strikethrough_flat(1)", "", 0,
				"ui_text_strikethrough_flat(1)", "", 1\
				ui_tooltip `Зачеркнутый`
	else
		if bEnableLeaderLine then
			gosub "Marker_line_details"
		endif
	endif

	if bEnableLeaderLine then
		_bPicturesForHeadStyle = 1
		gosub "Marker_geometry_picts"
	endif

return

! ==============================================================================
"HeadStyleSettingsPage":
! ------------------------------------------------------------------------------

	yCurr = pageStartY

	ui_separator sepx+3, 32, sepx+3, 266

	ui_style 0,1
	ui_infield{3} "bShowSeparatorLines", x1, yCurr-4,	x3-x1-1, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Линии Разделителей`, 0,
			2, `Линии Разделителей`, 1
	yCurr = yCurr + dy

	ui_infield{3} "bHideTextBoxContour", x1, yCurr-4,	x3-x1-1, outFieldHeight+15,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Отключение Рамки и Фона для Списка Слоев`, 0,
			2, `Отключение Рамки и Фона для Списка Слоев`, 1
	ui_style 0,0

	yCurr = yCurr + dy + 15
	ui_separator 0, yCurr, sepx, yCurr
	yCurr = yCurr + dy/2

	if bEnableLeaderLine then
		gosub "Marker_line_details"
	endif


	_bPicturesForHeadStyle = 0
	gosub "Marker_geometry_picts"

return

! ==============================================================================
"CustomSkinsContentPage":
! ------------------------------------------------------------------------------

	yCurr = pageStartY

	ui_style 0,1
	ui_infield{3} "bCustomListContent", x1, yCurr-1, x2-x1-1, inFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Специальные Данные`, 0,
			2, `Специальные Данные`, 1
	ui_style 0,0

	if bCustomListContent then
		if gs_ui_sub_tab > 1 then
			ui_button UI_FUNCTION, "<", 272, yCurr, 20,20, 10001	! Previous
		endif
		if gs_ui_sub_tab < 4 then
			ui_button UI_FUNCTION, ">", 384, yCurr, 20,20, 10002	! Next
		endif
	endif

	dim stLineNums[4]
		stLineNums[1] = `Строки 1-8`
		stLineNums[2] = `Строки 9-16`
		stLineNums[3] = `Строки 17-24`
		stLineNums[4] = `Строки 25-32`

	for iSubTab = 1 to 4
		if gs_ui_sub_tab = iSubTab then
			ui_outfield stLineNums[iSubTab], 293, yCurr+4, 90, 15
			cicleStart = 1+(iSubTab-1)*8
			cicleEnd = 8+(iSubTab-1)*8
		endif
	next iSubTab

	yCurr = yCurr + 32

	y0 = yCurr

	for iNr = cicleStart to cicleEnd
		strNrOutfield = str(iNr, 1, 0)+"."
		ui_outfield strNrOutfield, 40, yCurr, 20, outFieldHeight, 4-4*(bCustomListContent)
		ui_infield{3} dataCustomSkinRow[iNr], 62, yCurr-4, 342, 19

		yCurr = yCurr + 25
	next iNr

return

! ==============================================================================
"Marker_line_details":
! ------------------------------------------------------------------------------

	ui_style 0,1
	ui_infield{3} "bShowLeaderLine", x1, yCurr,	x2-x1-1, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Выносная Линия`, 0,
			2, `Выносная Линия`, 1
	ui_style 0,0

	if bEnableExtensionPolygon then
		ui_infield{3} "iLeaderLineStyle",	x2, yCurr-4, x3-x2, inFieldHeight,
			8, "", 0, 0, 0, 0, 0, 0,
			"", _stLeaderLine[1], LINE_SINGLE,
			"", _stLeaderLine[2], LINE_DOUBLE

		yCurr = yCurr + dy

		_flagHiddenDoubleLine	= 4 * (_bHideLeaderLineParams | iLeaderLineStyle <> LINE_DOUBLE)
		_flagHiddenExtension	= 4 * _bHideLeaderLineParams

		ui_outfield `Расстояние между Линиями`, x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenDoubleLine
		if _bMetricDimension then
			ui_infield "thkLeaderLine_mm",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight
			ui_outfield `мм`,					x3-30-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenDoubleLine
		else
			ui_infield "thkLeaderLine_pt",		x2, yCurr-4, x3-x2-35-linkPictWidth, inFieldHeight
			ui_outfield `пт`,					x3-30-linkPictWidth, yCurr, 30, outFieldHeight, _flagHiddenDoubleLine
		endif

		yCurr = yCurr + dy

		ui_outfield `Стиль Окончания Линии`, 	x1, yCurr, x2-x1-1, outFieldHeight, _flagHiddenExtension
		ui_infield{3} "iExtensionLineStyle",	x2, yCurr-4, x3-x2, inFieldHeight,
				8, "", 0, 0, 0, 0, 0, 0,
				"", stExtensionLine[1], EXTLINE_SAMEASMARKER,
				"", stExtensionLine[2], EXTLINE_RECT
	endif

	if bEnableDivisionLine then
		yCurr = yCurr + dy
		ui_style 0,1
		ui_infield{3} "bDivisionLine", x1, yCurr,	x2-x1-1, outFieldHeight,
				7, "", 2, 1, 0, 0, 0, 0,
				1, `Линия Разделения`, 0,
				2, `Линия Разделения`, 1
		ui_style 0,0
	endif
return

! ==============================================================================
"Marker_geometry_picts":
! ------------------------------------------------------------------------------

	_picid = 0
	if bUseTextBoxOnLine then		! Only Skin List Label has this option
		if _bPicturesForHeadStyle then
			if _bHideHeadEverySettings then
				_picid = 15 + not(_bHideLeaderLineParams)
				! 15 ui_marker_form_headdistance_Headoff_Lineoff.png
				! 16 ui_marker_form_headdistance_Headoff_Lineon.png
			else
				_picid = 5 + bShowLeaderLine
				! 5  ui_marker_form_headdistance_Headon_Lineoff.png
				! 6  ui_marker_form_headdistance_Headon_Lineon.png
			endif
		else
			if bUseBuiltinPointer then
				_picid =  15
				! 15 ui_marker_form_headdistance_Headoff_Lineoff.png
			else
				_picid = 7 + bShowHead * 2 + bShowLeaderLine
				! 7  ui_marker_form_skinlist_Headoff_Lineoff.png
				! 8  ui_marker_form_skinlist_Headoff_Lineon.png
				! 9  ui_marker_form_skinlist_Headon_Lineoff.png
				! 10 ui_marker_form_skinlist_Headon_Lineon.png
			endif
		endif
	else	! not(bUseTextBoxOnLine)	-> not(Skin List Label)
		if bEnableExtensionPolygon then
			_picid = 3 + not(bShowLeaderLine)
			! 3  ui_marker_form_macro1.png
			! 4  ui_marker_form_macro2.png
		else
			_picid = 11 + 2 * bShowLeaderLine + bDivisionLine
			! 11 ui_marker_form_dw_Headon_Lineoff_Divoff.png
			! 12 ui_marker_form_dw_Headon_Lineoff_Divon.png
			! 13 ui_marker_form_dw_Headon_Lineon_Divoff.png
			! 14 ui_marker_form_dw_Headon_Lineon_Divon.png
		endif
	endif
	ui_pict _picid, 293, 71, 58, 134, 1
	
	_flagExtLineLength		= 4 * (_bHideLeaderLineParams)
	_flagExtLineThickness	= 4 * (_bHideLeaderLineParams | (iExtensionLineStyle <> EXTLINE_RECT))
	_flagHiddenHead			= 4 * _bHideHeadEverySettings

	_showDistanceLabelHead	= not(bUseTextBoxOnLine) | (_bPicturesForHeadStyle & not(_bHideHeadEverySettings))
	_showListTextTopDistance = bUseTextBoxOnLine & not(_bPicturesForHeadStyle) & not(bUseBuiltinPointer)
	_showLengthExtensionLine =	(bUseTextBoxOnLine & not(_bPicturesForHeadStyle) & not(bUseBuiltinPointer) & bShowLeaderLine) |\
								(not(bUseTextBoxOnLine) & bShowLeaderLine)
	_showThkExtensionLine	=	(bUseTextBoxOnLine & not(_bPicturesForHeadStyle) & not(bUseBuiltinPointer) & bShowLeaderLine) |\
								(not(bUseTextBoxOnLine) & bShowLeaderLine & bEnableExtensionPolygon)

	_y1 = 131
	_y2 = 172
	_y3 = 201

	if _showDistanceLabelHead then
		if _bMetricDimension then
			ui_infield "distanceLabelHead_mm", x5, _y1 , x6-x5, inFieldHeight ui_tooltip `Расстояние до Рамки (мм)`
			ui_outfield `мм`, x6+2, _y1 + 2, w6, outFieldHeight, _flagHiddenHead
		else
			ui_infield "distanceLabelHead_pt", x5, _y1, x6-x5, inFieldHeight ui_tooltip `Расстояние до Рамки (пт)`
			ui_outfield `пт`, x6+2, _y1 + 2, w6, outFieldHeight, _flagHiddenHead
		endif
	endif

	if _showListTextTopDistance then
		if _bMetricDimension then
			ui_infield "listTextTopDistance_mm", x5, _y1, x6-x5, inFieldHeight ui_tooltip `Расстояние до Верха Флажка (мм)`
			ui_outfield `мм`, x6+2, _y1 + 2, w6, outFieldHeight
		else
			ui_infield "listTextTopDistance_pt", x5, _y1, x6-x5, inFieldHeight ui_tooltip `Расстояние до Верха Флажка (пт)`
			ui_outfield `пт`, x6+2, _y1 + 2, w6, outFieldHeight
		endif
	endif

	if _showLengthExtensionLine then
		if _bMetricDimension then
			ui_infield "lengthExtensionLine_mm", x5, _y2, x6-x5, inFieldHeight ui_tooltip `Длина Окончания Линии (мм)`
			ui_outfield `мм`, x6+2, _y2 + 2, w6, outFieldHeight, _flagExtLineLength
		else
			ui_infield "lengthExtensionLine_pt", x5, _y2, x6-x5, inFieldHeight ui_tooltip `Длина Окончания Линии (пт)`
			ui_outfield `пт`, x6+2, _y2 + 2, w6, outFieldHeight, _flagExtLineLength
		endif
	endif

	if _showThkExtensionLine then
		if _bMetricDimension then
			ui_infield "thkExtensionLine_mm", x5, _y3, x6-x5, inFieldHeight ui_tooltip `Ширина Окончания Линии (мм)`
			ui_outfield `мм`, x6+2, _y3 + 2, w6, outFieldHeight, _flagExtLineThickness
		else
			ui_infield "thkExtensionLine_pt", x5, _y3, x6-x5, inFieldHeight ui_tooltip `Ширина Окончания Линии (пт)`
			ui_outfield `пт`, x6+2, _y3 + 2, w6, outFieldHeight, _flagExtLineThickness
		endif
	endif
		
return

