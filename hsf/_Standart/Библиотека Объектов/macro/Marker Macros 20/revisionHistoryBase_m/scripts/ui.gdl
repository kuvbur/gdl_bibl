


!------------------------------------------------------------------------------------------------
! Variables
!------------------------------------------------------------------------------------------------

_titleSuffix1 = ""
_titleSuffix2 = " " + str((_nColMaxPage+1), 1, 0) + "-" + str(_nColumnsMax, 1, 0)
if nColumns > _nColMaxPage then
	_titleSuffix1 = " " + "1-" + str(_nColMaxPage, 1, 0)
endif

_widthButton = 13
_heightButton = 13
_radButtonHeight = 30
_stCustomOutfield = `Специальный`

_bLockPlusButton  = 0
_bLockMinusButton = 0

if nColumns = _nColumnsMax then _bLockPlusButton = 1
if nColumns = 1 then _bLockMinusButton = 1

!------------------------------------------------------------------------------------------------
! Tabpages
!------------------------------------------------------------------------------------------------

ui_dialog `Параметры Истории Редакций`
ui_current_page gs_ui_current_page

ui_page idTab_Style, idTab_Root, `Стиль`, "uiTab_Dimension_flat(1)"
		gosub "pageStyle"

ui_page idTab_Content, idTab_Root, `Данные` + _titleSuffix1, "uiTab_RevContent_flat(1)"
gosub "pageContent1"

if nColumns > _nColMaxPage then
	ui_page idTab_Content2, idTab_Root, `Данные` + _titleSuffix2, "uiTab_RevContent_flat(1)"
		gosub "pageContent2"
endif

if bDisplayHeader then
	ui_page idTab_CustTitle, idTab_Root, `Специальные Заголовки` + _titleSuffix1, "uiTab_RevCustomHeader_flat(1)"
		gosub "pageCustomTitle1"
	
	if nColumns > _nColMaxPage then
		ui_page idTab_CustTitle2, idTab_Root, `Специальные Заголовки` + _titleSuffix2, "uiTab_RevCustomHeader_flat(1)"
			gosub "pageCustomTitle2"
	endif
endif

ui_page idTab_DisplayOptions, idTab_Root, `Дополнительные Параметры Показа`, "uiTab_RevWatermark_flat(1)"
	gosub "pageDisplayOptions"

ui_page idTab_2DRepText, idTab_Root, `2D-отображение Текст`, "uiTab_2DRepr_flat(1)"
	gosub "page2DRepText"

if nRows = 4 then
	ui_page idTab_AdditionalWork, idTab_Root, "2048", "uiTab_GameRevHistory_flat(1)"
	if gs_ui_current_page = idTab_AdditionalWork then
		gosub "pageAdditionalWork"
	endif
endif

file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_RevContent_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_RevWatermark_flat(1)"
file_dependence "uiTab_RevCustomHeader_flat(1)"
file_dependence "uiTab_GameRevHistory_flat(1)"

end



!------------------------------------------------------------------------------------------------
! Layout Settings
!------------------------------------------------------------------------------------------------
"pageStyle":
	pageStartY = 8

	ui_style 0, 0
	dy = 22
	x1 = 2
	x2 = 106
	x3 = 206
	x4 = 238
	_infieldHeight = 19

	yCurr = pageStartY

	x1 = 2
	x2 = 106
	x3 = 206
	x4 = 238
	dy = 27

	_ySave = yCurr
	ui_outfield `Строки`, x1, yCurr, x2-x1-1, 15
	ui_infield "nRows", x2, yCurr-4, x3-x2, _infieldHeight
	yCurr = yCurr + dy

	ui_outfield `Единицы`, x1, yCurr, x2-x1-1, 15
	ui_infield "nBlocks", x2, yCurr-4, x3-x2, _infieldHeight
	yCurr = yCurr + dy

	yCurr = _ySave

	__picButtonHeight = 24
	__picButtonWidth = 32
	ui_outfield `Границы`, x4, yCurr, 309-x4-1, 15
	ui_infield{3} "bShowFrame", 309, yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",					! borderOut.png
			2,
			1, 16, 16, 25, 13,
			"ui_borderOut_flat(1)", "", 0,
			"ui_borderOut_flat(1)", "", 1\
			ui_tooltip `Наружные Границы`
	
	ui_infield{3} "bShowHorizLines", 309+(__picButtonWidth+2), yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",
			2,
			1, 16, 16, 25, 13,
			"ui_horizontalSeparator_flat(1)", "", 0,
			"ui_horizontalSeparator_flat(1)", "", 1\
			ui_tooltip `Горизонтальные Линии`
	
	ui_infield{3} "bShowVertLines", 309+2*(__picButtonWidth+2), yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",
			2,
			1, 16, 16, 25, 13,
			"ui_verticalSeparator_flat(1)", "", 0,
			"ui_verticalSeparator_flat(1)", "", 1\
			ui_tooltip `Вертикальные Линии`

	ui_infield{3} "bShowLinesEmptyRows", 309+3*(__picButtonWidth+2), yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",
			2,
			1, 16, 16, 25, 13,
			"ui_showEmptyRows_off_flat(1)", "", 0,
			"ui_showEmptyRows_on_flat(1)", "", 1\
			ui_tooltip `Показать Линии Пустых Строк`
	yCurr = yCurr + dy

	ui_outfield `Сортировка`, x4, yCurr, 309-x4-1, 15
	ui_infield{3} "iDataSortingOrder", 309, yCurr-6, 2*33, 24,
			4, 1, 2, 1,					! sorting.png
			0, 0, 29, 20,
			1, "",	FIRST_TO_LATEST, 
			2, "",	LATEST_TO_FIRST ui_tooltip `Сортировка от старых редакций к новым или от новым к старым.`

	ui_infield{3} "iDataAlign", 309+66+2, yCurr-6, 2*33, 24,
			4, 2, 2, 1,					! align.png
			0, 0, 29, 20,
			1, "",	ALIGN_TO_TOP, 
			2, "",	ALIGN_TO_BOTTOM ui_tooltip `Выравнивание списка по верху (слева) или по низу (справа).`
	yCurr = yCurr + dy + 3

	ui_separator 0, yCurr-8, 444, yCurr-8
	x1 = 2
	x2 = 106
	x3 = 206
	x4 = 238
	dy = 24
	ui_infield{3}	"bShowLatestRevisonOnly", x1, yCurr-4,	x4-x1-1, _infieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показ Только Последней Редакции`, 0,
			2, `Показ Только Последней Редакции`, 1

	ui_infield{3}	"bDisplayFirstRevision", x4, yCurr-4,	440-x4, _infieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Включить Первую Редакцию`, 0,
			2, `Включить Первую Редакцию`, 1
	yCurr = yCurr + dy

	ui_infield{3}	"bUseCustomRowFilter", x1, yCurr-4,	440-x1, _infieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Фильтрация Редакций по критериям`, 0,
			2, `Фильтрация Редакций по критериям`, 1
	yCurr = yCurr + dy

	_conditionInfieldParID = "stCustFilterColumnID"
	_xWidthControl = x3-x1-16
	_xStartControl = x1+16
	gosub "conditionInfield"

	x3 = 210
	x4 = 224
	x5 = 309
	ui_infield{3}	"iCustFilterConditionType",	x3, yCurr-4, x5-x3-3, 19,
			8, "", 5, 1,
			0, 0, 0, 0,
			"", _stConditionValues[1],	COND_STARTSWITH,
			"", _stConditionValues[2],	COND_STARTSWITHNOT,
			"", _stConditionValues[3],	COND_ENDSWITH,
			"", _stConditionValues[4],	COND_ENDSWITHNOT,
			"", _stConditionValues[5],	COND_CONTAINS,
			"", _stConditionValues[6],	COND_CONTAINSNOT,
			"", _stConditionValues[7],	COND_IS,
			"", _stConditionValues[8],	COND_ISNOT

	ui_infield "stCustFilterConditionText", x5, yCurr-4, 440 - x5, 19
	yCurr = yCurr + dy

	ui_infield{3}	"bCustFilterCaseSensitive", x1+16, yCurr-4,	x5-x1-16-1, _infieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Учет Регистра`, 0,
			2, `Учет Регистра`, 1

	yCurr = yCurr + dy

	dy = 24
	x1 = 2
	x2 = 106
	x3 = 206
	x4 = 238
	ui_separator 0, yCurr-4, 444, yCurr-4

	ui_outfield `Стиль Текста`, x1, yCurr, x3-x1, 15
	yCurr = yCurr + dy
	
	ui_infield "fontTypeText", x1, yCurr-4, x3-x1, _infieldHeight
	
	ui_pict "ui_FontSize_flat(1)", x4-27-3, yCurr, 27, 14, 1
	
	if _bMetricDimensionNeeded then
		ui_infield "fontSizeTextMm", x4, yCurr-4, 68, _infieldHeight ui_tooltip `Размер Шрифта Заголовка (мм)`
	else
		ui_infield "fontSizeTextPt", x4, yCurr-4, 68, _infieldHeight ui_tooltip `Размер Шрифта Заголовка (пт)`
	endif

	__boldParName		= "bTextBold"
	__italicParName		= "bTextItalic"
	__underlineParName	= "bTextUnderline"
	__strikethroughParName ="bTextStrikethrough"
	gosub "textAttributeButtons"

	yCurr = yCurr + dy
	
	ui_infield{3}	"bDisplayHeader", x1, yCurr-4,	x2-x1-2, _infieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Заголовок`, 0,
			2, `Заголовок`, 1

	ui_infield{3} "iHeaderPosition", x2, yCurr-4, x3-x2, _infieldHeight,
			8, "", 2, 1,
			0, 0, 0, 0,
			"", _stHeaderPosDesc[1], HEADER_TOP,
			"", _stHeaderPosDesc[2], HEADER_BOTTOM


	yCurr = yCurr + dy

	ui_infield "fontTypeHeader", x1, yCurr-4, x3-x1, _infieldHeight
	
	if bDisplayHeader then
		ui_pict "ui_FontSize_flat(1)", x4-27-3, yCurr, 27, 14, 1
	else
		ui_pict "ui_FontSize_hide_flat(1)", x4-27-3, yCurr, 27, 14, 1
	endif
	
	if _bMetricDimensionNeeded then
		ui_infield "fontSizeHeaderMm", x4, yCurr-4, 68, _infieldHeight ui_tooltip `Размер Шрифта Заголовка (мм)`
	else
		ui_infield "fontSizeHeaderPt", x4, yCurr-4, 68, _infieldHeight ui_tooltip `Размер Шрифта Заголовка (пт)`
	endif

	__boldParName		= "bHeaderTextBold"
	__italicParName		= "bHeaderTextItalic"
	__underlineParName	= "bHeaderTextUnderline"
	__strikethroughParName ="bHeaderTextStrikethrough"

	gosub "textAttributeButtons"
return


!------------------------------------------------------------------------------------------------
! Content Order 1
!------------------------------------------------------------------------------------------------
"pageContent1":
	ui_style 0, 0
	pageStartY = 5
	
	x0 = 1
	x1 = 25

	_widthIOfield = 225
	_heightIOfield = 19
	dy = 32

	! --------------------------------------------------------------------------
	! displaying infields 
	! --------------------------------------------------------------------------
	y = pageStartY
	_idxBlockOutfield = 1
	_rowIndexOnPage = _nColMaxPage
	x0 = 1
	x1 = 25
	gosub "displayInfields"

	! --------------------------------------------------------------------------
	! displaying, greying out / activating ordering buttons  
	! --------------------------------------------------------------------------

	buttonOffsetY = ( _heightIOfield - _heightButton)/4
	_idxBlockOutfield = 1
	
	y = pageStartY
	xButton = 258

	gosub "displayPlusMinusButtons"
	
	if _bLockPlusButton then
		ui_pict_button ui_function, "", "ui_button_plus_lock_flat(1)", xButton,	y - 16 - buttonOffsetY,	_widthButton,	_heightButton 	! greyed-out button with no effect
	else
		ui_pict_button ui_function, "", "ui_button_plus_flat(1)", xButton,	y - 16 - buttonOffsetY,	_widthButton,	_heightButton, CONTENTORDER_PLUSBUTTON_ID + i	 ui_tooltip `Добавить Строку`
	endif
return

!------------------------------------------------------------------------------------------------
! Content Order 2
!------------------------------------------------------------------------------------------------
"pageContent2":
	ui_style 0, 0
	pageStartY = 15
	
	x0 = 1
	x1 = 25
	
	_widthIOfield = 225
	_heightIOfield = 19
	dy = 32
	
	! --------------------------------------------------------------------------
	! displaying infields 
	! --------------------------------------------------------------------------
	y = pageStartY
	_idxBlockOutfield = _nColMaxPage + 1
	_rowIndexOnPage = _nColumnsMax
	x0 = 1
	x1 = 25
	gosub "displayInfields"
	
	! --------------------------------------------------------------------------
	! displaying, greying out / activating ordering buttons  
	! --------------------------------------------------------------------------
	
	buttonOffsetY = ( _heightIOfield - _heightButton)/4
	_idxBlockOutfield = _nColMaxPage + 1
	
	y = pageStartY
	xButton = 258

	gosub "displayPlusMinusButtons"

	if nColumns < _rowIndexOnPage then
		ui_pict_button ui_function, "", "ui_button_plus_flat(1)", xButton,	y - 16 - buttonOffsetY,	_widthButton,	_heightButton , CONTENTORDER_PLUSBUTTON_ID + i	 ui_tooltip `Добавить Строку`
	endif
return



!------------------------------------------------------------------------------------------------
! 2D Representation and Text
!------------------------------------------------------------------------------------------------
"page2DRepText":
	listfieldID = idTab_2DRepText
	itemID = 1

	ui_listfield listfieldID, 0, 0, 444, 265, 1

	if bShowFrame | bShowVertLines | bShowHorizLines then
		ui_listitem  itemID, listfieldID, "", 0, "", `Рамка`		:itemID = itemID + 1

		if bShowFrame | bShowVertLines | bShowHorizLines then
			ui_listitem  itemID, listfieldID, "penFrame", 1, "ui_list_contourPen_flat(1)", `Перо Границы`		:itemID = itemID + 1
			if bShowFrame then
				ui_listitem  itemID, listfieldID, "lineTypeFrame", 1, "ui_list_contLineType_flat(1)", `Тип Линии Границы`
			endif
			itemID = itemID + 1
			if bShowVertLines | bShowHorizLines then
				ui_listitem  itemID, listfieldID, "lineTypeSeparators", 1, "ui_list_contLineType_flat(1)", `Тип Линий Разделителей`
			endif
			itemID = itemID + 1
		else
			itemID = itemID + 3
		endif
	else
		itemID = itemID + 4
	endif

	ui_listitem  itemID, listfieldID, "", 0, "", `Штриховка`		:itemID = itemID + 1
	ui_listitem  itemID, listfieldID, "fillStamp", 1, 		"ui_list_fillType_flat(1)", `Тип Штриховки`		:itemID = itemID + 1
	ui_listitem  itemID, listfieldID, "fillFgPenStamp", 1,	"ui_list_fillPen_flat(1)", `Перо Штриховки`		:itemID = itemID + 1
	ui_listitem  itemID, listfieldID, "fillBgPenStamp", 1,	"ui_list_fillBgPen_flat(1)", `Перо Фона Штриховки`		:itemID = itemID + 1

	ui_listitem  itemID, listfieldID, "", 0, "", `Текст`		:itemID = itemID + 1

	ui_listitem  itemID, listfieldID, "textIndent", 1, "", `Отступ Текста`		:itemID = itemID + 1
	if iLayoutType = LAYOUT_SIMPLE then
		ui_listitem  itemID, listfieldID, "separatorString", 1, "", `Строка Разделителя`
	endif
	itemID = itemID + 1
	if bDisplayHeader then
		ui_listitem  itemID, listfieldID, "iHeaderAlignment", 1, "", `Вертикальное Выравнивание Текста Заголовка`		:itemID = itemID + 1
		ui_listitem  itemID, listfieldID, "penHeaderText", 1, "ui_list_pencolor_flat(1)", `Цвет Заголовка`		:itemID = itemID + 1
	else
		itemID = itemID + 2
	endif
	ui_listitem  itemID, listfieldID, "penText", 1, "ui_list_pencolor_flat(1)", `Цвет Текста`		:itemID = itemID + 1
	if bWatermark then
		ui_listitem  itemID, listfieldID, "penWatermarkText", 1, "ui_list_pencolor_flat(1)", `Цвет Водяного Знака`
	endif
	itemID = itemID + 1
return



!------------------------------------------------------------------------------------------------
! Display Options
!------------------------------------------------------------------------------------------------
"pageDisplayOptions":
	ui_style 0, 1
	yCurr = 5
	dy = 23
	x1 = 2
	x2 = 110
	x3 = 210
	x4 = 224
	x5 = 309

	ui_infield{3}	"bWatermark", x1, yCurr-4,	440-x1, 19,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показ Водяного Знака на Макетах в Процессе Изменений`, 0,
			2, `Показ Водяного Знака на Макетах в Процессе Изменений`, 1
	
	yCurr = yCurr + dy
	ui_style 0, 0
	ui_outfield `Ориентация`, x1, yCurr, x3-x1-1, 17, 4-4*bWatermark
	ui_infield{3}	"iWMarkOrientation",	x3, yCurr-4, 440-x3, 19,
			8, "", 2, 1,
			0, 0, 0, 0,
			1, `Сверху Вниз`,	LEFTTOP_RIGHTBOTTOM,
			2, `Снизу Вверх`,	LEFTBOTTOM_RIGHTTOP

	yCurr = yCurr + dy

	ui_outfield `Текст`, x1, yCurr, x3-x1-1, 15, 4-4*bWatermark
	ui_infield "stWatermark", x3, yCurr-4, 440-x3, 19
	yCurr = yCurr + dy

	ui_outfield `Стиль Текста`, x1, yCurr, x3-x1, 15, 4-4*bWatermark
	yCurr = yCurr + dy
	
	ui_infield "fontTypeWatermark", x1, yCurr-4, x3-x1, 19
	
	if bWatermark then
		ui_pict "ui_FontSize_flat(1)", 214, yCurr, 27, 14, 1
	else
		ui_pict "ui_FontSize_hide_flat(1)", 214, yCurr, 27, 14, 1
	endif

	if _bMetricDimensionNeeded then
		ui_infield "fontSizeWatermarkMm", 246, yCurr-4, 60, 19 ui_tooltip `Размер Шрифта Водяного Знака (мм)`
	else
		ui_infield "fontSizeWatermarkPt", 246, yCurr-4, 60, 19 ui_tooltip `Размер Шрифта Водяного Знака (пт)`
	endif

	__boldParName		= "bWmTextBold"
	__italicParName		= "bWmTextItalic"
	__underlineParName	= "bWmTextUnderline"
	__strikethroughParName ="bWmTextStrikethrough"
	gosub "textAttributeButtons"
	
	yCurr = yCurr + dy
	ui_outfield `Перо Текста`, x1, yCurr, x3-x1-1, 15, 4-4*bWatermark
	ui_infield "penWatermarkText", x3, yCurr-4, 33, 19
	
	yCurr = yCurr + 18

	ui_separator 0, yCurr, 444, yCurr
	yCurr = yCurr + 8
	ui_style 0, 1
	ui_infield{3}	"bShowNCSRevisionMarker", x1, yCurr-4,	440-x1, 19,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показ Маркера вокруг ID Редакции, если:`, 0,
			2, `Показ Маркера вокруг ID Редакции, если:`, 1

	ui_style 0, 0

	yCurr = yCurr + dy
	_conditionInfieldParID = "stNCSConditionColumnID"
	_xWidthControl = x3-x1-5
	_xStartControl = x1
	gosub "conditionInfield"

	x1 = 50
	ui_pict 3, 10, yCurr+dy+3, 33, 30, 1			! marker.png

	ui_infield{3}	"iNCSConditionType",	x3, yCurr-4, x5-x3-3, 19,
			8, "", 5, 1,
			0, 0, 0, 0,
			"", _stConditionValues[1],	COND_STARTSWITH,
			"", _stConditionValues[2],	COND_STARTSWITHNOT,
			"", _stConditionValues[3],	COND_ENDSWITH,
			"", _stConditionValues[4],	COND_ENDSWITHNOT,
			"", _stConditionValues[5],	COND_CONTAINS,
			"", _stConditionValues[6],	COND_CONTAINSNOT,
			"", _stConditionValues[7],	COND_IS,
			"", _stConditionValues[8],	COND_ISNOT

	ui_infield "stNCSConditionText", x5, yCurr-4, 440 - x5, 19

	yCurr = yCurr + dy

	ui_infield{3}	"bNCSCaseSensitive", x1, yCurr-4,	x5-x1-16-1, _infieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Учет Регистра`, 0,
			2, `Учет Регистра`, 1

	yCurr = yCurr + dy
	
	ui_outfield `Размер Маркера`, x1, yCurr, x3-x1-1, 15, 4-4*bShowNCSRevisionMarker
	ui_infield "sizeNCSMarker", x3, yCurr-5, x5-x3-3, 19
	yCurr = yCurr + dy

	ui_outfield `Перо Маркера`, x1, yCurr, x3-x1-1, 15, 4-4*bShowNCSRevisionMarker
	ui_infield "penNCSMarker", x3, yCurr-4, 33, 19
return



!------------------------------------------------------------------------------------------------
! Custom Title 1
!------------------------------------------------------------------------------------------------
"pageCustomTitle1":
	ui_style 0, 0
	pageStartY = 5
	
	x0 = 1
	x1 = 25
	
	_widthIOfield = 144
	_heightIOfield = 19
	dy = 32
	
	! --------------------------------------------------------------------------
	! displaying infields 
	! --------------------------------------------------------------------------
	y = pageStartY
	_idxBlockOutfield = 1
	_rowIndexOnPage = _nColMaxPage
	x0 = 1
	x1 = 25
	x2 = 55
	x3 = 200
	x4 = 300

	for hh = _idxBlockOutfield to min(nColumns, _rowIndexOnPage)

		ui_outfield  str("%1.0", _idxBlockOutfield) + ". ", x0, y+4, 23, 15

		if hh = 1 then
			ui_radiobutton "bUseCustHeader_1", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 2 then
			ui_radiobutton "bUseCustHeader_2", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 3 then
			ui_radiobutton "bUseCustHeader_3", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 4 then
			ui_radiobutton "bUseCustHeader_4", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 5 then
			ui_radiobutton "bUseCustHeader_5", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 6 then
			ui_radiobutton "bUseCustHeader_6", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 7 then
			ui_radiobutton "bUseCustHeader_7", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 8 then
			ui_radiobutton "bUseCustHeader_8", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		
		if hh = 1 then
			ui_radiobutton "bUseCustHeader_1", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 2 then
			ui_radiobutton "bUseCustHeader_2", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 3 then
			ui_radiobutton "bUseCustHeader_3", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 4 then
			ui_radiobutton "bUseCustHeader_4", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 5 then
			ui_radiobutton "bUseCustHeader_5", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 6 then
			ui_radiobutton "bUseCustHeader_6", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 7 then
			ui_radiobutton "bUseCustHeader_7", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 8 then
			ui_radiobutton "bUseCustHeader_8", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif

		ui_infield{4} _stCustHeaderParNames[hh], x4, y, _widthIOfield, 19
		y = y + dy
		_idxBlockOutfield = _idxBlockOutfield + 1
	next hh
return



!------------------------------------------------------------------------------------------------
! Custom Title 1
!------------------------------------------------------------------------------------------------
"pageCustomTitle2":
	ui_style 0, 0
	pageStartY = 5	
	x0 = 1
	x1 = 25
	
	_widthIOfield = 144
	_heightIOfield = 19
	dy = 32
	
	! --------------------------------------------------------------------------
	! displaying infields 
	! --------------------------------------------------------------------------
	y = pageStartY
	_idxBlockOutfield = _nColMaxPage + 1
	_rowIndexOnPage = _nColumnsMax
	x0 = 1
	x1 = 25
	x2 = 55
	x3 = 200
	x4 = 300

	for hh = _idxBlockOutfield to min(nColumns, _rowIndexOnPage)

		ui_outfield  str("%1.0", _idxBlockOutfield) + ". ", x0, y+4, 23, 15

		if hh = 9 then
			ui_radiobutton "bUseCustHeader_9", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 10 then
			ui_radiobutton "bUseCustHeader_10", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 11 then
			ui_radiobutton "bUseCustHeader_11", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 12 then
			ui_radiobutton "bUseCustHeader_12", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 13 then
			ui_radiobutton "bUseCustHeader_13", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 14 then
			ui_radiobutton "bUseCustHeader_14", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 15 then
			ui_radiobutton "bUseCustHeader_15", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif
		if hh = 16 then
			ui_radiobutton "bUseCustHeader_16", 0, selectedColumnNames[hh], x1, y-4, 150, _radButtonHeight
		endif


		if hh = 9 then
			ui_radiobutton "bUseCustHeader_9", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 10 then
			ui_radiobutton "bUseCustHeader_10", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 11 then
			ui_radiobutton "bUseCustHeader_11", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 12 then
			ui_radiobutton "bUseCustHeader_12", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 13 then
			ui_radiobutton "bUseCustHeader_13", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 14 then
			ui_radiobutton "bUseCustHeader_14", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 15 then
			ui_radiobutton "bUseCustHeader_15", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif
		if hh = 16 then
			ui_radiobutton "bUseCustHeader_16", 1, _stCustomOutfield, x3, y-4, x4-x3-2, _radButtonHeight
		endif

		ui_infield{4} _stCustHeaderParNames[hh], x4, y, _widthIOfield, 19
		y = y + dy
		_idxBlockOutfield = _idxBlockOutfield + 1
	next hh
return



! ==============================================================================
"displayInfields":
! fill data into fixed arrays for ui_infield{4}
! ------------------------------------------------------------------------------
!	Input Parameters:
!		nColumns:						parameter (Integer) for number of displayed columns set on UI
!		_stSelectedColParNames[14]:		array variable (String) to store parameter name for row data selection in UI infield iterations
!		_allSchemeTitleID[]:			array variable (String), all header column IDs corresponding _fullRevisionData array
!		_allSchemeTitleName[]:			array variable (String), all header column names corresponding _fullRevisionData array
!		_stTextAlignParNames[14]:		array variable (String) to store parameter name for row data alignment in UI infield iterations
!		_uiDataID[]:					array variable (String), containing list of _allSchemeTitleID[] entries
!		_uiDataName[]:					array variable (String), containing list of _allSchemeTitleName[] entries
!	Output Variables:
!		-
! ==============================================================================

	dim _uiPicName[]
	__idxCol = 1

	for __idxCol = 1 to vardim1(_allSchemeTitleID)
		_uiPicName[__idxCol] = ""
	next __idxCol

	for i = _idxBlockOutfield to min(nColumns, _rowIndexOnPage)
		x2 = x1 + _widthIOfield + 20

		ui_outfield  str("%1.0", _idxBlockOutfield) + ". ", x0, y, 23, 15

		if vardim1(_allSchemeTitleID) > 0 then
			ui_infield{4} _stSelectedColParNames[i], x1, y-4, _widthIOfield, 19,
					8, "", 1, 1,
					0, 0, 0, 0,
					_uiPicName, _allSchemeTitleName, _allSchemeTitleID

		endif

		ui_infield{4} _stTextAlignParNames[i], 444-4*30, y-5, 4*30, 24,
					4, "all_alignment_flat(1)", 4, 1,
					0, 0, 26, 18,
					1, "",	ANCHOR_LEFT,
					2, "",	ANCHOR_CENTER,
					3, "",	ANCHOR_RIGHT,
					4, "",	ANCHOR_JUSTIFIED


		_idxBlockOutfield = _idxBlockOutfield +1
		y = y + dy
	next i
return


! ==============================================================================
"displayPlusMinusButtons":
! ------------------------------------------------------------------------------
!	Input Parameters:
!		_bLockMinusButton:			variable (Boolean) to control "-" button function
!		_bLockPlusButton:			variable (Boolean) to control "+" button function
!	Output Variables:
!		-
! ==============================================================================

	for i = _idxBlockOutfield to min(nColumns, _rowIndexOnPage)
		if _bLockMinusButton then
			ui_pict_button ui_function, "", "ui_button_minus_lock_flat(1)", xButton,	y - buttonOffsetY ,	_widthButton, _heightButton 	! greyed-out button with no effect
		else
			ui_pict_button ui_function, "", "ui_button_minus_flat(1)", xButton,	y - buttonOffsetY,	_widthButton, _heightButton, CONTENTORDER_MINUSBUTTON_ID - i	 ui_tooltip `Удалить Строку`
		endif
		if i > 1 then	
			if _bLockPlusButton then
				ui_pict_button ui_function, "", "ui_button_plus_lock_flat(1)", xButton,	y - 16 - buttonOffsetY,	_widthButton,	_heightButton 	! greyed-out button with no effect
			else
				ui_pict_button ui_function, "", "ui_button_plus_flat(1)", xButton,	y - 16 - buttonOffsetY,	_widthButton,	_heightButton, CONTENTORDER_PLUSBUTTON_ID + i	 ui_tooltip `Добавить Строку`
			endif
		endif
		y = y + dy
	next i
return


! ==============================================================================
"textAttributeButtons":
! ------------------------------------------------------------------------------
	__picButtonHeight = 24
	__picButtonWidth = 32
	
	ui_infield{4} __boldParName, 309, yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_bold_flat(1)", "", 0,
			"ui_bold_flat(1)", "", 1\
			ui_tooltip `Жирный`
	
	ui_infield{4} __italicParName, 309+__picButtonWidth+2, yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_italic_flat(1)", "", 0,
			"ui_italic_flat(1)", "", 1\
			ui_tooltip `Курсив`
	
	ui_infield{4} __underlineParName, 309+2*(__picButtonWidth+2), yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_underline_flat(1)", "", 0,
			"ui_underline_flat(1)", "", 1\
			ui_tooltip `Подчеркнутый`
	
	ui_infield{4} __strikethroughParName, 309+3*(__picButtonWidth+2), yCurr-6, __picButtonWidth, __picButtonHeight,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_text_strikethrough_flat(1)", "", 0,
			"ui_text_strikethrough_flat(1)", "", 1\
			ui_tooltip `Зачеркнутый`

return



! ==============================================================================
"conditionInfield":
! fill data into fixed arrays for ui_infield{4}
! ------------------------------------------------------------------------------
!	Input Parameters:
!		_conditionInfieldParID:		name of actual displayed parameter in variable
!		_issueSchemeTitleID[]:		array variable (String), header IDs collected from GlobIssueScheme for NCS marker head UI selection 
!		_issueSchemeName[]:			array variable (String), header Names collected from GlobIssueScheme for NCS marker head UI selection values
!	Output Variables:
!		-
! ==============================================================================
	__idxCol = 1
	dim _uiPicNames[]

	if vardim1(_issueSchemeTitleID) > 0 then
		for __idxCol = 1 to vardim1(_issueSchemeTitleID)
			_uiPicNames[__idxCol] = ""
		next __idxCol

		ui_infield{4} _conditionInfieldParID, _xStartControl, yCurr-4, _xWidthControl, 19,
				8, "", 1, 1,
				0, 0, 0, 0,
				_uiPicNames, _issueSchemeName, _issueSchemeTitleID
	endif
return


! ==============================================================================
"pageAdditionalWork":
! ==============================================================================
	
	ui_button UI_FUNCTION, "Up", 	180, 	  2,	84,		84,	1
	ui_button UI_FUNCTION, "Down", 	180, 	178,	84,		84,	2
	ui_button UI_FUNCTION, "Left",	 96, 	 90,	84,		84,	3
	ui_button UI_FUNCTION, "Right", 264, 	 90,	84,		84,	4
	
	ui_button UI_FUNCTION, "Reset",	  5,	  5,	90,		30,	5
	ui_button UI_FUNCTION, "Back to Work",	  5,	  40,	90,		30,	6

	ui_outfield "Score:",						290, 5, 80, 30, 1
	ui_outfield str(currentScore,8,0),			380, 5, 60, 30, 1
	
	ui_outfield "High Score:",					290, 35, 80, 30, 1
	ui_outfield str(highScore,8,0),				380, 35, 60, 30, 1
return
