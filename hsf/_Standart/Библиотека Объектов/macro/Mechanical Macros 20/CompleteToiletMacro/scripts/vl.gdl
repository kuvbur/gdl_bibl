
!================================================================================
! FM
!================================================================================

call "FM_types" parameters all


!================================================================================
! String parameters
!================================================================================


! --- Bowl ----------------------------------------------------------------------

! --- Disabled Grab Bars --------------------------------------------------------
if bDisabledWC then
	values{2} "iPositionGrabBars"	GRAB_FLOOR,		st_positionGrabBars[GRAB_FLOOR],
									GRAB_SIDESIDE,	st_positionGrabBars[GRAB_SIDESIDE],
									GRAB_REARREAR,	st_positionGrabBars[GRAB_REARREAR],
									GRAB_TOILET,	st_positionGrabBars[GRAB_TOILET],
									GRAB_SIDEREAR,	st_positionGrabBars[GRAB_SIDEREAR]
endif

! --- Mounting Type ---

if iWCType = WC_NORMAL then
	if (bDisabledWC & gs_iDisplayStandards = LANGUAGE_USA) then		! Only Floor Mounted
		values "strToiletMounting" strToiletMountingValues[1]
		values "iToiletMounting" 1

		strToiletMounting = strToiletMountingValues[1]
		iToiletMounting =  1
		parameters strToiletMounting = strToiletMounting,
					iToiletMounting = iToiletMounting
		lock "strToiletMounting", "iToiletMounting"
		hideparameter "strToiletMounting", "iToiletMounting"
	else
		values "strToiletMounting" strToiletMountingValues
		values "iToiletMounting" 1,2

		if GLOB_MODPAR_NAME = "strToiletMounting" then
			iToiletMounting = 1
			if strToiletMounting = strToiletMountingValues[2] then iToiletMounting = 2
			parameters iToiletMounting = iToiletMounting
		else
			strToiletMounting = strToiletMountingValues[1]
			if iToiletMounting = 2 then strToiletMounting = strToiletMountingValues[2]
			parameters strToiletMounting = strToiletMounting
		endif
	endif
endif

! --- Floor Mounted Bowl ---

if iWCType = WC_NORMAL then
	if (bDisabledWC & gs_iDisplayStandards = LANGUAGE_USA) then
		values "strFloorMountedBowlType"	strFloorMountedBowlTypeValues[1],strFloorMountedBowlTypeValues[2],\
											strFloorMountedBowlTypeValues[3],strFloorMountedBowlTypeValues[4]
		values "iFloorMountedBowlType" 1,2,3,4
	else
		values "strFloorMountedBowlType" strFloorMountedBowlTypeValues
		values "iFloorMountedBowlType" 1,2,3,4,5,6
	endif

	if GLOB_MODPAR_NAME = "strFloorMountedBowlType" then
		iFloorMountedBowlType = 1
		if strFloorMountedBowlType = strFloorMountedBowlTypeValues[2] then iFloorMountedBowlType = 2
		if strFloorMountedBowlType = strFloorMountedBowlTypeValues[3] then iFloorMountedBowlType = 3
		if strFloorMountedBowlType = strFloorMountedBowlTypeValues[4] then iFloorMountedBowlType = 4
		if strFloorMountedBowlType = strFloorMountedBowlTypeValues[5] then iFloorMountedBowlType = 5
		if strFloorMountedBowlType = strFloorMountedBowlTypeValues[6] then iFloorMountedBowlType = 6
		parameters iFloorMountedBowlType = iFloorMountedBowlType
	else
		strFloorMountedBowlType = strFloorMountedBowlTypeValues[1]
		if iFloorMountedBowlType = 2 then strFloorMountedBowlType = strFloorMountedBowlTypeValues[2]
		if iFloorMountedBowlType = 3 then strFloorMountedBowlType = strFloorMountedBowlTypeValues[3]
		if iFloorMountedBowlType = 4 then strFloorMountedBowlType = strFloorMountedBowlTypeValues[4]
		if iFloorMountedBowlType = 5 then strFloorMountedBowlType = strFloorMountedBowlTypeValues[5]
		if iFloorMountedBowlType = 6 then strFloorMountedBowlType = strFloorMountedBowlTypeValues[6]
		parameters strFloorMountedBowlType = strFloorMountedBowlType
	endif
endif

! --- Wall Hung Bowl ---

if iWCType = WC_NORMAL then
	values "strWallHungBowlType" strWallHungBowlTypeValues
	values "iWallHungBowlType" 1,2,3,4

	if GLOB_MODPAR_NAME = "strWallHungBowlType" then
		iWallHungBowlType = 1
		if strWallHungBowlType = strWallHungBowlTypeValues[2] then iWallHungBowlType = 2
		if strWallHungBowlType = strWallHungBowlTypeValues[3] then iWallHungBowlType = 3
		if strWallHungBowlType = strWallHungBowlTypeValues[4] then iWallHungBowlType = 4
		parameters iWallHungBowlType = iWallHungBowlType
	else
		strWallHungBowlType = strWallHungBowlTypeValues[1]
		if iWallHungBowlType = 2 then strWallHungBowlType = strWallHungBowlTypeValues[2]
		if iWallHungBowlType = 3 then strWallHungBowlType = strWallHungBowlTypeValues[3]
		if iWallHungBowlType = 4 then strWallHungBowlType = strWallHungBowlTypeValues[4]
		parameters strWallHungBowlType = strWallHungBowlType
	endif
endif

! --- Corner Bowl ---

if iWCType = WC_CORNER then
	values "strCornerBowlType" strCornerBowlTypeValues
	values "iCornerBowlType" 1,2,3

	if GLOB_MODPAR_NAME = "strCornerBowlType" then
		iCornerBowlType = 1
		if strCornerBowlType = strCornerBowlTypeValues[2] then iCornerBowlType = 2
		if strCornerBowlType = strCornerBowlTypeValues[3] then iCornerBowlType = 3
		parameters iCornerBowlType = iCornerBowlType
	else
		strCornerBowlType = strCornerBowlTypeValues[1]
		if iCornerBowlType = 2 then strCornerBowlType = strCornerBowlTypeValues[2]
		if iCornerBowlType = 3 then strCornerBowlType = strCornerBowlTypeValues[3]
		parameters strCornerBowlType = strCornerBowlType
	endif
endif

! --- Squatting Bowl ---

if iWCType = WC_SQUATTING then
	values "strSquattingBowlType" strSquattingBowlTypeValues
	values "iSquattingBowlType" 1

	strSquattingBowlType = strSquattingBowlTypeValues[1]
	parameters strSquattingBowlType = strSquattingBowlType

	iSquattingBowlType = 1
	parameters iSquattingBowlType = iSquattingBowlType
endif



! --- Seat ----------------------------------------------------------------------

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	values "gs_seat" gs_seatValues
	values "gs_seat_m" 1,2

	if GLOB_MODPAR_NAME = "gs_seat" then
		gs_seat_m = 1
		if gs_seat = gs_seatValues[2] then gs_seat_m = 2
		parameters gs_seat_m = gs_seat_m
	else
		gs_seat = gs_seatValues[1]
		if gs_seat_m = 2 then gs_seat = gs_seatValues[2]
		parameters gs_seat = gs_seat
	endif
endif



! --- Flush ---------------------------------------------------------------------

! --- Set default when changing from floor mounted with cistern to wall hung ----
if iWCType = WC_NORMAL then
	if GLOB_MODPAR_NAME = "strToiletMounting" or GLOB_MODPAR_NAME = "iToiletMounting" then
		if iToiletMounting = WALL_HUNG and iFlushType = CISTERN then
			strFlushType = strFlushTypeValues[3]
			iFlushType   = IN_WALL_CARRIER

			parameters 	strFlushType = strFlushType,
						iFlushType   = iFlushType
		endif
	endif
endif


! --- Flush Method ---

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	if iWCType = WC_NORMAL then
			if bDisabledWC then
				if gs_iDisplayStandards = LANGUAGE_USA then		! Only Cistern
					values "strFlushType" 	strFlushTypeValues[1]
					values "iFlushType" 1
					strFlushType = strFlushTypeValues[1]
					iFlushType =  CISTERN
					parameters	strFlushType = strFlushType,
								iFlushType = iFlushType
					lock "strFlushType", "iFlushType", "bFlush"
					hideparameter "strFlushType", "iFlushType", "bFlush"
				else
					! INT
					values "strFlushType" 	strFlushTypeValues[1],
											strFlushTypeValues[3],
											strFlushTypeValues[5]
					values "iFlushType" 1,3,5
				endif

				if not(bFlush) then 
					lock "iFlushType", "strFlushType", "gs_flushHandlePlace_m", "gs_flushHandlePlace"
				endif
				if bFlush & iFlushType = CISTERN then
					lock "bShowInWallCarrier2D", "bShowInWallCarrier3D", "strInWallCarrierButtonType", "iInWallCarrierButtonType"
				endif
				if bFlush & iFlushType = IN_WALL_CARRIER then
					lock "iCisternType", "strCisternType", "gs_flushHandlePlace_m", "gs_flushHandlePlace"
				endif
			else
				values "strFlushType" 	strFlushTypeValues[1],
										strFlushTypeValues[2],
										strFlushTypeValues[3],
										strFlushTypeValues[4],
										strFlushTypeValues[5]
				values "iFlushType" 1,2,3,4,5
			endif

		endif

	if iWCType = WC_CORNER then
		values "strFlushType" 	strFlushTypeValues[1],
								strFlushTypeValues[3],
								strFlushTypeValues[4],
								strFlushTypeValues[5]
		values "iFlushType" 1,3,4,5
	endif

	if GLOB_MODPAR_NAME = "strFlushType" then
		iFlushType = 1
		if bDisabledWC then bFlush = 1
		if strFlushType = strFlushTypeValues[2] then iFlushType = 2
		if strFlushType = strFlushTypeValues[3] then iFlushType = 3
		if strFlushType = strFlushTypeValues[4] then iFlushType = 4
		if strFlushType = strFlushTypeValues[5] then
			iFlushType = 5
			if bDisabledWC then bFlush = 0
		endif
		parameters iFlushType = iFlushType
		if bDisabledWC then parameters bFlush = bFlush
	else
		strFlushType = strFlushTypeValues[1]
		if iFlushType = 2 then strFlushType = strFlushTypeValues[2]
		if iFlushType = 3 then strFlushType = strFlushTypeValues[3]
		if iFlushType = 4 then strFlushType = strFlushTypeValues[4]
		if iFlushType = 5 then strFlushType = strFlushTypeValues[5]
		parameters strFlushType = strFlushType
	endif

	if bDisabledWC then
		if GLOB_MODPAR_NAME = "bFlush" then
			if not(bFlush) then iFlushType = 5
			if bFlush and iFlushType = 5 then iFlushType = 1
			parameters iFlushType = iFlushType
		endif
	endif
endif

! --- Cistern ---

if iWCType = WC_NORMAL then
	values "strCisternType" strCisternTypeValues
	values "iCisternType" 1,2,3,4,5

	if GLOB_MODPAR_NAME = "strCisternType" then
		iCisternType = 1
		if strCisternType = strCisternTypeValues[2] then iCisternType = 2
		if strCisternType = strCisternTypeValues[3] then iCisternType = 3
		if strCisternType = strCisternTypeValues[4] then iCisternType = 4
		if strCisternType = strCisternTypeValues[5] then iCisternType = 5
		parameters iCisternType = iCisternType
	else
		strCisternType = strCisternTypeValues[1]
		if iCisternType = 2 then strCisternType = strCisternTypeValues[2]
		if iCisternType = 3 then strCisternType = strCisternTypeValues[3]
		if iCisternType = 4 then strCisternType = strCisternTypeValues[4]
		if iCisternType = 5 then strCisternType = strCisternTypeValues[5]
		parameters strCisternType = strCisternType
	endif
endif

! --- Corner Cistern ---

if iWCType = WC_CORNER then
	values "strCornerCisternType" strCornerCisternTypeValues
	values "iCornerCisternType" 1,2,3,4,5

	if GLOB_MODPAR_NAME = "strCornerCisternType" then
		iCornerCisternType = 1
		if strCornerCisternType = strCornerCisternTypeValues[2] then iCornerCisternType = 2
		if strCornerCisternType = strCornerCisternTypeValues[3] then iCornerCisternType = 3
		parameters iCornerCisternType = iCornerCisternType
	else
		strCornerCisternType = strCornerCisternTypeValues[1]
		if iCornerCisternType = 2 then strCornerCisternType = strCornerCisternTypeValues[2]
		if iCornerCisternType = 3 then strCornerCisternType = strCornerCisternTypeValues[3]
		parameters strCornerCisternType = strCornerCisternType
	endif
endif

! --- Elevated Cistern ---

if iWCType = WC_NORMAL then
	values "strElevatedCisternType" strElevatedCisternTypeValues
	values "iElevatedCisternType" 1,2,3

	if GLOB_MODPAR_NAME = "strElevatedCisternType" then
		iElevatedCisternType = 1
		if strElevatedCisternType = strElevatedCisternTypeValues[2] then iElevatedCisternType = 2
		if strElevatedCisternType = strElevatedCisternTypeValues[3] then iElevatedCisternType = 3
		parameters iElevatedCisternType = iElevatedCisternType
	else
		strElevatedCisternType = strElevatedCisternTypeValues[1]
		if iElevatedCisternType = 2 then strElevatedCisternType = strElevatedCisternTypeValues[2]
		if iElevatedCisternType = 3 then strElevatedCisternType = strElevatedCisternTypeValues[3]
		parameters strElevatedCisternType = strElevatedCisternType
	endif
endif

! --- In-Wall Carrier Button ---

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	values "strInWallCarrierButtonType" strInWallCarrierButtonTypeValues
	values "iInWallCarrierButtonType" 1,2,3,4,5

	if GLOB_MODPAR_NAME = "strInWallCarrierButtonType" then
		iInWallCarrierButtonType = 1
		if strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[2] then iInWallCarrierButtonType = 2
		if strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[3] then iInWallCarrierButtonType = 3
		if strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[4] then iInWallCarrierButtonType = 4
		if strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[5] then iInWallCarrierButtonType = 5
		parameters iInWallCarrierButtonType = iInWallCarrierButtonType
	else
		strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[1]
		if iInWallCarrierButtonType = 2 then strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[2]
		if iInWallCarrierButtonType = 3 then strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[3]
		if iInWallCarrierButtonType = 4 then strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[4]
		if iInWallCarrierButtonType = 5 then strInWallCarrierButtonType = strInWallCarrierButtonTypeValues[5]
		parameters strInWallCarrierButtonType = strInWallCarrierButtonType
	endif
endif

! --- Flushometer ---

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	call "flushometer_m" parameters all	returned_parameters xTapPos
endif

! --- Button Position ----

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	if bDisabledWC then
		strButtonPosition = strButtonPositionValues[2]
		iButtonPosition = 2
		parameters 	strButtonPosition = strButtonPosition,
					iButtonPosition = iButtonPosition
		lock "strButtonPosition", "iButtonPosition"
		hideparameter "strButtonPosition", "iButtonPosition"
	else
		values "strButtonPosition" strButtonPositionValues
		values "iButtonPosition" 1,2

		if GLOB_MODPAR_NAME = "strButtonPosition" then
			iButtonPosition = 1
			if strButtonPosition = strButtonPositionValues[2] then iButtonPosition = 2
			parameters iButtonPosition = iButtonPosition
		else
			strButtonPosition = strButtonPositionValues[1]
			if iButtonPosition = 2 then strButtonPosition = strButtonPositionValues[2]
			parameters strButtonPosition = strButtonPosition
		endif
	endif
endif



! --- Effluent ------------------------------------------------------------------

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	if iWCType = WC_NORMAL then
		if iToiletMounting = FLOOR_MOUNTED then
			values "DirEffluent" 	DirEffluentValues[1],
									DirEffluentValues[2]
			values "DirEffluent_ID" 1,2
		endif
		if iToiletMounting = WALL_HUNG then
!!!			if iFlushType = IN_WALL_CARRIER then
!!!				values "DirEffluent" 	DirEffluentValues[1],
!!!										DirEffluentValues[2]
!!!				values "DirEffluent_ID" 1,2
!!!			else
				values "DirEffluent" 	DirEffluentValues[1]
				values "DirEffluent_ID" 1
				lock "DirEffluent", "DirEffluent_ID"
				parameters DirEffluent_ID = 1, DirEffluent = DirEffluentValues[1]
!!!			endif
		endif
	endif

	if iWCType = WC_CORNER then
		values "DirEffluent" 	DirEffluentValues[1],
								DirEffluentValues[2]
		values "DirEffluent_ID" 1,2
	endif

	if GLOB_MODPAR_NAME = "DirEffluent" then
		DirEffluent_ID = 1
		if DirEffluent = DirEffluentValues[2] then DirEffluent_ID = 2
		parameters DirEffluent_ID = DirEffluent_ID
	else
		DirEffluent = DirEffluentValues[DirEffluent_ID]
		parameters DirEffluent = DirEffluent
	endif
endif


! --- 3D ------------------------------------------------------------------------

values "gs_detlevel_3D" gs_detlevel_3DValues
values "gs_detlevel_3D_m" DETLEV_3D_OFF,DETLEV_3D_SIMPLE,2,DETLEV_3D_DRAFT

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DETLEV_3D_DETAILED													! Detailed
	if gs_detlevel_3D = gs_detlevel_3DValues[2] then gs_detlevel_3D_m = DETLEV_3D_SIMPLE	! Simple
	if gs_detlevel_3D = gs_detlevel_3DValues[3] then gs_detlevel_3D_m = DETLEV_3D_DRAFT	! Draft
	if gs_detlevel_3D = gs_detlevel_3DValues[4] then gs_detlevel_3D_m = DETLEV_3D_OFF	! Off
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = gs_detlevel_3DValues[1]								! Detailed
	if gs_detlevel_3D_m = DETLEV_3D_SIMPLE then gs_detlevel_3D = gs_detlevel_3DValues[2]	! Simple
	if gs_detlevel_3D_m = DETLEV_3D_DRAFT then gs_detlevel_3D = gs_detlevel_3DValues[3]	! Draft
	if gs_detlevel_3D_m = DETLEV_3D_OFF then gs_detlevel_3D = gs_detlevel_3DValues[4]	! Off
	parameters gs_detlevel_3D = gs_detlevel_3D
endif


! --- 2D ------------------------------------------------------------------------

call "2dDetailLevel" parameters gs_detlevel_2D 		= gs_detlevel_2D,
								gs_detlevel_2D_m 	= gs_detlevel_2D_m,
								enable2dDetLevels	= 30


!================================================================================
! Sizes
!================================================================================

! A is width of the bowl
! B is depth of the bowl
! ZZYZX is height of the bowl

values "A" range[0.200,)
values "B" range[0.200,)

if iWCType = WC_SQUATTING then
	ZZYZX = 0.150
	parameters ZZYZX = 0.150
	lock "ZZYZX"
else
	if bDisabledWC then
		values "gs_seat_height"	range[0.430,0.485]

		if (iWCType = WC_NORMAL and iToiletMounting = WALL_HUNG) then
			values "bowlHeight" 	range[0.200,)
			values "ZZYZX" 			range[0.200,)
		else
			values "bowlHeight" 	range[0.430,0.485]
			values "ZZYZX" 			range[0.430,0.485]
		endif
	else
		values "ZZYZX" 			range[0.200,)
		values "bowlHeight" 	range[0.200,)

		if (iWCType = WC_NORMAL and iToiletMounting = WALL_HUNG) then
			values "gs_seat_height" range[bowlHeight,)
		else
			values "gs_seat_height" range[0.200,)
		endif
	endif
endif

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	values "A2" range[0.200/2,)
	values "B2" range(,-0.200]

	values "bowlWidth" range[0.200,)
	values "bowlDepth" range[0.200,)

	values "cisternWidth" range(0,)
	values "cisternDepth" range(0,)
	values "cisternHeight" range(0,)
	values "cisternWidth2" range(0,)
	values "cisternDepth2" range(,0)

	values "carrierWidth" range(0,)
	values "carrierDepth" range(0,)
	values "carrierHeight" range(0,)
endif


! --- Width ---

if GLOB_MODPAR_NAME = "A2" then
	A 			= A2 * 2
	bowlWidth 	= A2 * 2
	parameters 	A 			= A,
				bowlWidth 	= bowlWidth
else
	if GLOB_MODPAR_NAME = "A" then
		A2 			= A/2
		bowlWidth 	= A
		parameters 	A2 			= A2,
					bowlWidth 	= bowlWidth
	else
		A 	= bowlWidth
		A2 	= bowlWidth/2
		parameters 	A 	= A,
					A2	= A2
	endif
endif

! --- Depth ---

if GLOB_MODPAR_NAME = "B2" then
	B 			= ABS(B2)
	bowlDepth 	= ABS(B2)
	parameters 	B 			= B,
				bowlDepth 	= bowlDepth
else
	if GLOB_MODPAR_NAME = "B" then
		B2 			= -ABS(B)
		bowlDepth 	=  B
		parameters 	B2 			= B2,
					bowlDepth 	= bowlDepth
	else
		B 	= bowlDepth
		B2 	= -ABS(bowlDepth)
		parameters 	B 	= B,
					B2 	= B2
	endif
endif

! --- Height ---

if GLOB_MODPAR_NAME = "ZZYZX" then
	bowlHeight = ZZYZX
	parameters bowlHeight = bowlHeight
else
	ZZYZX = bowlHeight
	parameters ZZYZX = ZZYZX
endif

if GLOB_MODPAR_NAME = "gs_seat_height" then
	if (iWCType = WC_NORMAL and iToiletMounting = FLOOR_MOUNTED) or iWCType = WC_CORNER then
		ZZYZX      = gs_seat_height
		bowlHeight = gs_seat_height
		parameters ZZYZX = ZZYZX, bowlHeight = bowlHeight
	endif
else
	if (iWCType = WC_NORMAL and iToiletMounting = FLOOR_MOUNTED) or iWCType = WC_CORNER then
		gs_seat_height = bowlHeight
		parameters gs_seat_height = gs_seat_height
	endif
endif


! --- Cistern Sizes ---

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	if GLOB_MODPAR_NAME = "cisternWidth2" then
		cisternWidth = cisternWidth2 * 2
		parameters cisternWidth = cisternWidth
	else
		cisternWidth2 = cisternWidth / 2
		parameters cisternWidth2 = cisternWidth2
	endif

	if GLOB_MODPAR_NAME = "cisternDepth2" then
		cisternDepth = ABS(cisternDepth2)
		parameters cisternDepth = cisternDepth
	else
		cisternDepth2 = -ABS(cisternDepth)
		parameters cisternDepth2 = cisternDepth2
	endif
endif



!================================================================================
! Connection Heights
!================================================================================

waterXPosition = 0

if GLOB_MODPAR_NAME = "strFlushType" or GLOB_MODPAR_NAME = "iFlushType" then
	if iFlushType = FLUSHOMETER then
		waterHeightConnection = 0.755
		ConPosZ_1 = waterHeightConnection

		parameters 	waterHeightConnection 	= waterHeightConnection,
					ConPosZ_1 				= ConPosZ_1
	endif
endif

! --- Water Limits (depends on flush settings) ---

if iWCType = WC_NORMAL then
	if iFlushType = CISTERN then
		waterLowerLimit = gs_seat_height + ConWidth_1/2
		waterUpperLimit = gs_seat_height + cisternHeight - ConWidth_1/2

		waterXPosition = 0
	endif

	if iFlushType = ELEVATED_CISTERN then
		waterLowerLimit = elevatedCisternElevation + ConWidth_1/2
		waterUpperLimit = elevatedCisternElevation + cisternHeight - ConWidth_1/2

		waterXPosition = 0
	endif

	if iFlushType = IN_WALL_CARRIER then
		if iToiletMounting = FLOOR_MOUNTED then
			waterLowerLimit = ConWidth_1/2
			waterUpperLimit = carrierHeight - ConWidth_1/2
		endif

		if iToiletMounting = WALL_HUNG then
			waterLowerLimit = ConWidth_1/2
			waterUpperLimit = carrierHeight - ConWidth_1/2
		endif

		waterXPosition = 0
	endif

	if iFlushType = FLUSHOMETER then
		waterLowerLimit = gs_seat_height+0.14
		waterUpperLimit = gs_seat_height+3

		if GLOB_MODPAR_NAME = "waterConnectionHeight" then
			ConPosZ_1 = waterConnectionHeight
			parameters ConPosZ_1 = ConPosZ_1
		else
			waterConnectionHeight = ConPosZ_1
			parameters waterConnectionHeight = waterConnectionHeight
		endif

		values "waterConnectionHeight" range[waterLowerLimit,waterUpperLimit)

		if iFlushometerType = FLUSHOMETER_SIMPLE 	then waterXPosition = xTapPos
		if iFlushometerType = FLUSHOMETER_AUTOMATIC then waterXPosition = xTapPos
		if iFlushometerType = FLUSHOMETER_MANUAL 	then waterXPosition = 0
	endif

	if iFlushType = NONEFLUSH then
		waterUpperLimit = gs_seat_height - ConWidth_1/2

		if iToiletMounting = FLOOR_MOUNTED then
			waterLowerLimit = ConWidth_1/2
			if iFloorMountedBowlType = FLOORMOUNTED_TYPE_1 then waterLowerLimit = waterUpperLimit
			if iFloorMountedBowlType = FLOORMOUNTED_TYPE_2 then waterLowerLimit = waterUpperLimit
			if iFloorMountedBowlType = FLOORMOUNTED_TYPE_3 then waterLowerLimit = bowlHeight*0.033/0.389+ConWidth_1/2
			if iFloorMountedBowlType = FLOORMOUNTED_TYPE_5 then waterLowerLimit = bowlHeight*0.353/0.406+ConWidth_1/2
		else
			if iWallHungBowlType = WALLHUNG_TYPE_4 then
				waterLowerLimit = gs_seat_height-0.270/0.330*bowlHeight + ConWidth_1/2
			else
				waterLowerLimit = gs_seat_height-bowlHeight + ConWidth_1/2
			endif
		endif
	endif
endif


if iWCType = WC_CORNER then
	if iFlushType = CISTERN then
		waterLowerLimit = gs_seat_height + ConWidth_1/2
		waterUpperLimit = gs_seat_height + cisternHeight - ConWidth_1/2

		waterXPosition = 0
	endif

	if iFlushType = IN_WALL_CARRIER then
		waterLowerLimit = ConWidth_1/2
		waterUpperLimit = carrierHeight - ConWidth_1/2

		waterXPosition = 0
	endif

	if iFlushType = FLUSHOMETER then
		waterLowerLimit = gs_seat_height+0.14
		waterUpperLimit = gs_seat_height+3

		if GLOB_MODPAR_NAME = "waterConnectionHeight" then
			ConPosZ_1 = waterConnectionHeight
			parameters ConPosZ_1 = ConPosZ_1
		else
			waterConnectionHeight = ConPosZ_1
			parameters waterConnectionHeight = waterConnectionHeight
		endif

		values "waterConnectionHeight" range[waterLowerLimit,waterUpperLimit)

		if iFlushometerType = FLUSHOMETER_SIMPLE 	then waterXPosition = xTapPos
		if iFlushometerType = FLUSHOMETER_AUTOMATIC then waterXPosition = xTapPos
		if iFlushometerType = FLUSHOMETER_MANUAL 	then waterXPosition = 0
	endif

	if iFlushType = NONEFLUSH then
		waterLowerLimit = bowlHeight - 0.05/0.400*bowlHeight - ConWidth_1/2
		waterUpperLimit = bowlHeight - 0.05/0.400*bowlHeight - ConWidth_1/2
	endif
endif

if iWCType = WC_SQUATTING then
	if iSquattingBowlType = SQUATTING_TYPE_1 then
		waterLowerLimit = -0.032
		waterUpperLimit = waterLowerLimit
		waterXPosition  = 0
	endif
endif




! --- Waste Water Limits (depends on effluent method) ---

if iWCType = WC_NORMAL then
	if iToiletMounting = FLOOR_MOUNTED then
		if DirEffluent_ID = EFFLUENT_BACK then
			if iFlushType = IN_WALL_CARRIER then
				wasteLowerLimit = ConWidth_2/2
				wasteUpperLimit = carrierHeight - ConWidth_2/2
			else
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_1 then
					wasteLowerLimit = 0.200/0.390*bowlHeight
					wasteUpperLimit = wasteLowerLimit
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_2 then
					wasteLowerLimit = 0.200/0.400*bowlHeight
					wasteUpperLimit = wasteLowerLimit
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_3 then
					wasteLowerLimit = 0.033/0.389*bowlHeight + ConWidth_2/2
					wasteUpperLimit = gs_seat_height - ConWidth_2/2
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_4 then
					wasteLowerLimit = ConWidth_2/2
					wasteUpperLimit = gs_seat_height - ConWidth_2/2
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_5 then
					wasteLowerLimit = 0.036/0.406*bowlHeight + ConWidth_2/2
					wasteUpperLimit = 0.353/0.406*bowlHeight - ConWidth_2/2
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_6 then
					wasteLowerLimit = ConWidth_2/2
					wasteUpperLimit = gs_seat_height - ConWidth_2/2
				endif
			endif
		endif

		if DirEffluent_ID = EFFLUENT_BOTTOM then
			if iFlushType = IN_WALL_CARRIER then
				wasteLowerLimit = ConWidth_2/2
				wasteUpperLimit = carrierDepth - ConWidth_2/2
			else
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_1 then
					wasteLowerLimit = 0.069/0.560*bowlDepth + ConWidth_2/2
					wasteUpperLimit = 0.511/0.560*bowlDepth - ConWidth_2/2
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_2 then
					wasteLowerLimit = 0.121/0.650*bowlDepth
					wasteUpperLimit = wasteLowerLimit
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_3 then
					wasteLowerLimit = ConWidth_2/2
					wasteUpperLimit = 0.649/0.685*bowlDepth - ConWidth_2/2
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_4 then
					wasteLowerLimit = ConWidth_2/2
					wasteUpperLimit = 0.451/0.550*bowlDepth - ConWidth_2/2
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_5 then
					wasteLowerLimit = 0.035/0.700*bowlDepth + ConWidth_2/2
					wasteUpperLimit = 0.665/0.700*bowlDepth - ConWidth_2/2
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_6 then
					if builtInCistern then
						wasteLowerLimit = ConWidth_2/2
						wasteUpperLimit = 0.560/0.680*bowlDepth - ConWidth_2/2
					else
						wasteLowerLimit = ConWidth_2/2
						wasteUpperLimit = 0.420/0.540*bowlDepth - ConWidth_2/2
					endif
				endif
			endif
		endif
	endif

	if iToiletMounting = WALL_HUNG then
		if iFlushType = IN_WALL_CARRIER then
			if DirEffluent_ID = EFFLUENT_BACK then
				wasteLowerLimit = ConWidth_2/2
				wasteUpperLimit = carrierHeight - ConWidth_2/2
			else
				wasteLowerLimit = ConWidth_2/2
				wasteUpperLimit = carrierDepth - ConWidth_2/2
			endif
		else
			if iWallHungBowlType = WALLHUNG_TYPE_4 then
				wasteLowerLimit = gs_seat_height-0.270/0.330*bowlHeight + ConWidth_2/2
				wasteUpperLimit = gs_seat_height - ConWidth_2/2
			else
				wasteLowerLimit = gs_seat_height-bowlHeight + ConWidth_2/2
				wasteUpperLimit = gs_seat_height - ConWidth_2/2
			endif
		endif
	endif
endif


if iWCType = WC_CORNER then
	if DirEffluent_ID = EFFLUENT_BACK then
		if iFlushType = IN_WALL_CARRIER then
			wasteLowerLimit = ConWidth_2/2
			wasteUpperLimit = carrierHeight - ConWidth_2/2
		else
			if iCornerBowlType = CORNER_TYPE_1 then
				wasteLowerLimit = 0.200/0.390*bowlHeight
				wasteUpperLimit = wasteLowerLimit
			endif
			if iCornerBowlType = CORNER_TYPE_2 then
				wasteLowerLimit = 0.200/0.400*bowlHeight
				wasteUpperLimit = wasteLowerLimit
			endif
			if iCornerBowlType = CORNER_TYPE_3 then
				wasteLowerLimit = 0.199/0.389*bowlHeight
				wasteUpperLimit = wasteLowerLimit
			endif
		endif
	endif

	if DirEffluent_ID = EFFLUENT_BOTTOM then
		if iFlushType = IN_WALL_CARRIER then
			wasteLowerLimit = ConWidth_2/2
			wasteUpperLimit = carrierDepth - ConWidth_2/2
		else
			if iCornerBowlType = CORNER_TYPE_1 then
				wasteLowerLimit = 0.282/0.350*bowlWidth/2 + 0.073/0.590*bowlDepth + ConWidth_2/2
				wasteUpperLimit = 0.282/0.350*bowlWidth/2 + 0.539/0.590*bowlDepth - ConWidth_2/2
			endif
			if iCornerBowlType = CORNER_TYPE_2 then
				wasteLowerLimit = 0.322/0.346*bowlWidth/2 + 0.121/0.650*bowlDepth
				wasteUpperLimit = wasteLowerLimit
			endif
			if iCornerBowlType = CORNER_TYPE_3 then
				wasteLowerLimit = 0.105/0.350*bowlWidth/2 + 0.032/0.705*bowlDepth + ConWidth_2/2
				wasteUpperLimit = 0.105/0.350*bowlWidth/2 + 0.667/0.705*bowlDepth - ConWidth_2/2
			endif
		endif
	endif

	if GLOB_MODPAR_NAME = "DirEffluent" then
		if DirEffluent_ID = EFFLUENT_BACK then
			ConPosZ_2 = 0.20
			parameters ConPosZ_2 = ConPosZ_2
		else
			ConPosY_2 = - (wasteUpperLimit+wasteLowerLimit)/2
			parameters ConPosY_2 = ConPosY_2
		endif
	endif
endif


if iWCType = WC_SQUATTING then
	if iSquattingBowlType = SQUATTING_TYPE_1 then
		wasteLowerLimit = 0.180/0.520*bowlDepth
		wasteUpperLimit = wasteLowerLimit

		wasteZPosition = 0.13
	endif
endif



!================================================================================
! Avaiblable parameters
!================================================================================

! --- WC Type -------------------------------------------------------------------

if iWCType = WC_NORMAL then
	if iToiletMounting = FLOOR_MOUNTED 	then
		lock 			"strWallHungBowlType", 		"iWallHungBowlType"
		hideparameter 	"strWallHungBowlType", 		"iWallHungBowlType"
	endif

	if iToiletMounting = WALL_HUNG 		then
		lock 	"strFloorMountedBowlType", 	"iFloorMountedBowlType"
		hideparameter 	"strFloorMountedBowlType", 	"iFloorMountedBowlType"

!!!		if iFlushType <>IN_WALL_CARRIER then
!!!			lock "DirEffluent", "DirEffluent_ID"
!!!			hideparameter "DirEffluent", "DirEffluent_ID"
!!!		endif
	endif
endif


! --- Flush ---------------------------------------------------------------------

if iWCType = WC_CORNER then
	if GLOB_MODPAR_NAME = "strFlushType" or GLOB_MODPAR_NAME = "iFlushType" then
		carrierDepth = 0.300
		parameters carrierDepth = carrierDepth
	endif

	if GLOB_MODPAR_NAME = "carrierWidth" then
		carrierDepth = carrierWidth/2
		parameters carrierDepth = carrierDepth
	else
		carrierWidth = carrierDepth*2
		parameters carrierWidth = carrierWidth
	endif
endif

if iWCType = WC_NORMAL then
	if iFlushType <> CISTERN then
		lock 			"strCisternType", "iCisternType"
		hideparameter 	"strCisternType", "iCisternType"
	endif

	if iFlushType <> ELEVATED_CISTERN then
		lock 	"strElevatedCisternType", 	"iElevatedCisternType",
				"elevatedCisternElevation", "matCisternPipe"

		hideparameter 	"strElevatedCisternType", 	"iElevatedCisternType",
						"elevatedCisternElevation", "matCisternPipe"
	endif

	if builtInCistern then
		lock "strCisternType", "iCisternType"
		lock "cisternWidth", "cisternDepth", "cisternHeight"

		lock "gs_tank_mat"
		hideparameter "gs_tank_mat"
	endif
endif

if iWCType = WC_CORNER then
	lock 	"matCisternPipe"

	hideparameter 	"matCisternPipe"

	if iFlushType <> CISTERN then
		lock 			"strCornerCisternType", "iCornerCisternType"
		hideparameter 	"strCornerCisternType", "iCornerCisternType"
	endif
endif

if iWCType <> WC_SQUATTING then
	if iFlushType <> IN_WALL_CARRIER then
		lock	"strInWallCarrierButtonType",
				"iInWallCarrierButtonType",
				"carrierHeight",
				"carrierDepth",
				"strButtonPosition",
				"iButtonPosition",
				"bShowInWallCarrier2D",
				"bShowInWallCarrier3D",
				"matFlushButton"

		hideparameter	"strInWallCarrierButtonType",
						"iInWallCarrierButtonType",
						"carrierHeight",
						"carrierDepth",
						"strButtonPosition",
						"iButtonPosition",
						"bShowInWallCarrier2D",
						"bShowInWallCarrier3D",
						"matFlushButton"
	endif

	if iFlushType <> FLUSHOMETER then
		lock	"strFlushometerType",
				"iFlushometerType",
				"waterConnectionHeight",
				"matSensor",
				"gs_pipe_mat"

		hideparameter	"strFlushometerType",
						"iFlushometerType",
						"waterConnectionHeight",
						"matSensor",
						"gs_pipe_mat"
	endif

	if iFlushType <> CISTERN and iFlushType <> ELEVATED_CISTERN then
		lock 			"cisternWidth", "cisternDepth", "cisternHeight"
		hideparameter 	"cisternWidth", "cisternDepth", "cisternHeight"

		lock 			"gs_tank_mat"
		hideparameter 	"gs_tank_mat"
	endif

	if iFlushType = NONEFLUSH then
			lock 	"strCisternType",
					"iCisternType",
					"strElevatedCisternType",
					"iElevatedCisternType",
					"elevatedCisternElevation",
					"matCisternPipe",
					"strInWallCarrierButtonType",
					"iInWallCarrierButtonType",
					"carrierHeight",
					"carrierDepth",
					"strButtonPosition",
					"iButtonPosition",
					"bShowInWallCarrier2D",
					"bShowInWallCarrier3D",
					"matFlushButton",
					"strCornerCisternType",
					"iCornerCisternType",
					"strFlushometerType",
					"iFlushometerType",
					"waterConnectionHeight",
					"matSensor",
					"gs_pipe_mat"

			hideparameter 	"strCisternType",
							"iCisternType",
							"strElevatedCisternType",
							"iElevatedCisternType",
							"elevatedCisternElevation",
							"matCisternPipe",
							"strInWallCarrierButtonType",
							"iInWallCarrierButtonType",
							"carrierHeight",
							"carrierDepth",
							"strButtonPosition",
							"iButtonPosition",
							"bShowInWallCarrier2D",
							"bShowInWallCarrier3D",
							"matFlushButton",
							"strCornerCisternType",
							"iCornerCisternType",
							"strFlushometerType",
							"iFlushometerType",
							"waterConnectionHeight",
							"matSensor",
							"gs_pipe_mat"
	endif
endif


!================================================================================
! Default Bowl Sizes
!================================================================================

if 	GLOB_MODPAR_NAME = "strToiletMounting"			| GLOB_MODPAR_NAME = "iToiletMounting" | \
	GLOB_MODPAR_NAME = "strFloorMountedBowlType"	| GLOB_MODPAR_NAME = "iFloorMountedBowlType" | \
	GLOB_MODPAR_NAME = "strWallHungBowlType"		| GLOB_MODPAR_NAME = "iWallHungBowlType" | \
	GLOB_MODPAR_NAME = "strCornerBowlType"			| GLOB_MODPAR_NAME = "iCornerBowlType" | \
	GLOB_MODPAR_NAME = "strSquattingBowlType"		| GLOB_MODPAR_NAME = "iSquattingBowlType" then

		! --- Normal WC Bowls ---------------------------------------------------

		if iWCType = WC_NORMAL then

			if iToiletMounting = FLOOR_MOUNTED then
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_1 then
					bowlWidth 	= 0.350
					bowlDepth 	= 0.640
					bowlHeight 	= 0.400
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_2 then
					bowlWidth 	= 0.346
					bowlDepth 	= 0.651
					bowlHeight 	= 0.400
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_3 then
					bowlWidth 	= 0.381
					bowlDepth 	= 0.685
					bowlHeight 	= 0.389
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_4 then
					bowlWidth 	= 0.370
					bowlDepth 	= 0.550
					bowlHeight 	= 0.390
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_5 then
					bowlWidth 	= 0.580
					bowlDepth 	= 0.700
					bowlHeight 	= 0.406
				endif
				if iFloorMountedBowlType = FLOORMOUNTED_TYPE_6 then
					bowlWidth 	= 0.360
					bowlDepth 	= 0.540 + 0.140 * builtInCistern
					bowlHeight 	= 0.400
				endif

				A 				= bowlWidth
				B 				= bowlDepth
				ZZYZX 			= bowlHeight
				gs_seat_height 	= bowlHeight
			endif

			if iToiletMounting = WALL_HUNG then
				if iWallHungBowlType = WALLHUNG_TYPE_1 then
					bowlWidth 	= 0.350
					bowlDepth 	= 0.560
					bowlHeight 	= 0.330
				endif
				if iWallHungBowlType = WALLHUNG_TYPE_2 then
					bowlWidth 	= 0.356
					bowlDepth 	= 0.546
					bowlHeight 	= 0.298
				endif
				if iWallHungBowlType = WALLHUNG_TYPE_3 then
					bowlWidth 	= 0.370
					bowlDepth	= 0.550
					bowlHeight	= 0.300
				endif
				if iWallHungBowlType = WALLHUNG_TYPE_4 then
					bowlWidth 	= 0.360
					bowlDepth 	= 0.540
					bowlHeight 	= 0.330
				endif

				A 				= bowlWidth
				B 				= bowlDepth
				ZZYZX 			= bowlHeight
			endif

		endif

		! --- Corner WC Bowls ---------------------------------------------------

		if iWCType = WC_CORNER then
			if iCornerBowlType = CORNER_TYPE_1 then
				bowlWidth 	  	= 0.350
				bowlDepth 	  	= 0.590
				bowlHeight 		= 0.400
			endif
			if iCornerBowlType = CORNER_TYPE_2 then
				bowlWidth 	  	= 0.346
				bowlDepth 	  	= 0.650
				bowlHeight 		= 0.400
			endif
			if iCornerBowlType = CORNER_TYPE_3 then
				bowlWidth 	  	= 0.381
				bowlDepth 	  	= 0.705
				bowlHeight 		= 0.389
			endif

			A 				= bowlWidth
			B 				= bowlDepth
			ZZYZX 			= bowlHeight
		endif

		! --- Squatting WC Bowls ------------------------------------------------

		if iWCType = WC_SQUATTING then
			if iSquattingBowlType = SQUATTING_TYPE_1 then
				bowlWidth 	  	= 0.375
				bowlDepth 	  	= 0.520
				bowlHeight 		= 0.150
			endif
		endif

		! --- Parameter Setting -------------------------------------------------

		parameters 	bowlWidth 		= bowlWidth,
					bowlDepth 		= bowlDepth,
					bowlHeight 		= bowlHeight,
					A 				= A,
					B 				= B,
					ZZYZX 			= ZZYZX,
					gs_seat_height 	= gs_seat_height
endif


!================================================================================
! Default Flush (Cistern) Sizes
!================================================================================

if 	GLOB_MODPAR_NAME = "strToiletMounting"			| GLOB_MODPAR_NAME = "iToiletMounting" | \
	GLOB_MODPAR_NAME = "strFloorMountedBowlType"	| GLOB_MODPAR_NAME = "iFloorMountedBowlType" | \
	GLOB_MODPAR_NAME = "strWallHungBowlType"		| GLOB_MODPAR_NAME = "iWallHungBowlType" | \
	GLOB_MODPAR_NAME = "strCornerBowlType"			| GLOB_MODPAR_NAME = "iCornerBowlType" | \
	GLOB_MODPAR_NAME = "strSquattingBowlType"		| GLOB_MODPAR_NAME = "iSquattingBowlType" | \

	GLOB_MODPAR_NAME = "strFlushType"				| GLOB_MODPAR_NAME = "iFlushType" | \
	GLOB_MODPAR_NAME = "strCisternType" 			| GLOB_MODPAR_NAME = "iCisternType" | \
	GLOB_MODPAR_NAME = "strCornerCisternType"		| GLOB_MODPAR_NAME = "iCornerCisternType" | \
	GLOB_MODPAR_NAME = "strElevatedCisternType"		| GLOB_MODPAR_NAME = "iElevatedCisternType" then

		! --- Normal ---------------------------------------------------

		if iWCType = WC_NORMAL then

			if iFlushType = CISTERN then
				if iCisternType = CISTERN_TYPE_1 then
					cisternWidth 	= 0.435
					cisternDepth 	= 0.095
					cisternHeight 	= 0.300
				endif

				if iCisternType = CISTERN_TYPE_2 then
					cisternWidth 	= 0.271
					cisternDepth 	= 0.08
					cisternHeight 	= 0.282
				endif

				if iCisternType = CISTERN_TYPE_3 then
					cisternWidth 	= 0.374
					cisternDepth 	= 0.110
					cisternHeight 	= 0.373
				endif

				if iCisternType = CISTERN_TYPE_4 then
					cisternWidth 	= 0.400
					cisternDepth 	= 0.08
					cisternHeight 	= 0.440
				endif

				if iCisternType = CISTERN_TYPE_5 then
					cisternWidth 	= 0.400
					cisternDepth 	= 0.085
					cisternHeight 	= 0.405
				endif


				! --- Built In Cisterns

				if builtInCistern then
					if iFloorMountedBowlType = FLOORMOUNTED_TYPE_5 then cisternHeight = 0.194
					if iFloorMountedBowlType = FLOORMOUNTED_TYPE_6 then cisternHeight = 0.525
				endif
			endif


			if iFlushType = ELEVATED_CISTERN then
				if iElevatedCisternType = ELEV_CISTERN_TYPE_1 then
					cisternWidth 	= 0.435
					cisternDepth 	= 0.181
					cisternHeight 	= 0.300
				endif

				if iElevatedCisternType = ELEV_CISTERN_TYPE_2 then
					cisternWidth 	= 0.340
					cisternDepth 	= 0.135
					cisternHeight 	= 0.425
				endif

				if iElevatedCisternType = ELEV_CISTERN_TYPE_3 then
					cisternWidth 	= 0.410
					cisternDepth 	= 0.128
					cisternHeight 	= 0.380
				endif
			endif

		endif

		! --- Corner WC ---------------------------------------------------

		if iWCType = WC_CORNER then
			if iCornerCisternType = CORNER_CISTERN_TYPE_1 then
				cisternWidth 	= 0.360
				cisternDepth 	= 0.360
				cisternHeight 	= 0.382
			endif

			if iCornerCisternType = CORNER_CISTERN_TYPE_2 then
				cisternWidth 	= 0.350
				cisternDepth 	= 0.350
				cisternHeight 	= 0.380
			endif

			if iCornerCisternType = CORNER_CISTERN_TYPE_3 then
				cisternWidth 	= 0.350
				cisternDepth 	= 0.350
				cisternHeight 	= 0.380
			endif
		endif

		! --- Squatting WC ------------------------------------------------

		! Built-In Flush System


		! --- Parameter Setting -------------------------------------------------

		parameters 	cisternWidth 	= cisternWidth,
					cisternDepth 	= cisternDepth,
					cisternHeight 	= cisternHeight
endif

if iWCType = WC_NORMAL then
	values "elevatedCisternElevation" range[gs_seat_height+diameterPipe*2,)
endif


!================================================================================
! Minimal Space
!================================================================================

if not(bDisabledWC) then 
	values "MSFront" range[0,)
	values "MSSide" range[0,)
	values "MSSide2" range[0,)
endif

!================================================================================
! Distance from Wall
!================================================================================

if (iWCType = WC_NORMAL) & (iToiletMounting = FLOOR_MOUNTED) & (DirEffluent_ID = EFFLUENT_BOTTOM) then

	if iFlushType = CISTERN | iFlushType = NONEFLUSH | iFlushType = IN_WALL_CARRIER then
		values "distanceFromWall" range [0, ]
	endif
	
	if iFlushType = ELEVATED_CISTERN | iFlushType = FLUSHOMETER then
		values "distanceFromWall" range [0, 0.045]
	endif
	
else
	if iWCType <> WC_SQUATTING then
		lock "distanceFromWall"
		parameters distanceFromWall = 0
	endif
endif

if GLOB_MODPAR_NAME = "DirEffluent_ID" | GLOB_MODPAR_NAME = "strToiletMounting" then

	if (iWCType = WC_NORMAL) & (iToiletMounting = FLOOR_MOUNTED) & (DirEffluent_ID = EFFLUENT_BOTTOM) then
		distanceFromWall = 0.025
		parameters distanceFromWall = distanceFromWall
	endif

endif

!================================================================================
! MEP Connections
!================================================================================

values "ConPosZ_1" range [waterLowerLimit, waterUpperLimit]

if iWCType = WC_NORMAL or iWCType = WC_CORNER then
	if DirEffluent_ID = EFFLUENT_BACK then
		values "ConPosZ_2" range [wasteLowerLimit, wasteUpperLimit]
	else
		values "ConPosY_2" range [-wasteUpperLimit - distanceFromWall * bBowlMove, -wasteLowerLimit - distanceFromWall * bBowlMove]
	endif
endif

if iWCType = WC_SQUATTING then
	values "ConPosY_2" range [-wasteUpperLimit, -wasteLowerLimit]
endif

if isMEPEnabled then

	if not(ConStatus_1) and not(ConStatus_2) then
		lock "gs_con_mat"
		hideparameter"gs_con_mat"
	endif

	dim shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

	! --- Connections -----------------------------------------------------------

	parameters MEP_NumberConnections = 2


	! --- 1st Connection --------------------------------------------------------

	ConID = 1
	ConName = `Подключение Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if ConWidth_1 < 0.001 then
		ConWidth_1 = 0.001
		parameters ConWidth_1 = ConWidth_1
	endif
	if ConWidth_1 > min(A/2, 0.30/2) then
		ConWidth_1 = min(A/2, 0.30/2)
		parameters ConWidth_1 = ConWidth_1
	endif
	values "ConWidth_1" range (0, min(A/2, 0.30/2)]
	parameters ConDepth_1 = ConWidth_1
	values "ConLength_1" range [0, ]


	parameters ConPosX_1 = waterXPosition
	parameters ConPosY_1 = -distanceFromWall * bBowlMove


	call "MEP_m_ConnectionsACL_4" parameters 	SetProgram 					= SetProgram,
												gs_Connections 				= gs_Connections,
												MEP_NumConnectionData 		= MEP_NumConnectionData,
												MEP_StrConnectionData 		= MEP_StrConnectionData,
												MEP_NumberConnections 		= MEP_NumberConnections,
												gs_AddConnections 			= gs_AddConnections,
												ConName 					= ConName,
												ConID 						= ConID,
												ConPosX 					= ConPosX_1,
												ConPosY 					= ConPosY_1,
												ConPosZ 					= ConPosZ_1,
												ConDVecX 					= ConDVecX,
												ConDVecY 					= ConDVecY,
												ConDVecZ 					= ConDVecZ,
												ConWVecX 					= ConWVecX,
												ConWVecY 					= ConWVecY,
												ConWVecZ 					= ConWVecZ,

												ConNominalWidth 			= ConNominalWidth_1,
												ConNominalDepth 			= ConNominalDepth_1,
												ConWidth 					= ConWidth_1,
												ConDepth 					= ConDepth_1,
												ConLength 					= ConLength_1,
												ConWallThickness 			= ConWallThickness_1,
												ConConnectorWidth 			= ConConnectorWidth_1,
												ConConnectorDepth 			= ConConnectorDepth_1,
												ConConnectorWidth2 			= ConConnectorWidth2_1,
												ConToolType 				= ConToolType_1,
												ConStatus 					= ConStatus_1,
												ConType_1 					= ConType_1,
												ConConnectorType_1 			= ConConnectorType_1,
												ConSystem_1 				= ConSystem_1,
												ui_current_con 				= ui_current_con,
												shape_typ_to_validation 	= shape_typ_to_validation,
												MEP_enabled_geometry_mod 	= 1,
												gs_ui 						= 1


	parameters MEP_ConInfo[ConID][1] = str(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


	! --- 2nd Connection --------------------------------------------------------

	ConID = 2
	ConName = `Подключение Канализации`

	if ConWidth_2 < 0.001 then
		ConWidth_2 = 0.001
		parameters ConWidth_2 = ConWidth_2
	endif
	values "ConWidth_2" range (0, ]
	parameters ConDepth_2 = ConWidth_2
	values "ConLength_2" range [0, ]


	if iWCType = WC_NORMAL or iWCType = WC_CORNER then
		if DirEffluent_ID = EFFLUENT_BACK then
			ConDVecX = 0
			ConDVecY = 1
			ConDVecZ = 0

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			parameters ConPosX_2 = 0
			parameters ConPosY_2 = 0
		else
			ConDVecX = 0
			ConDVecY = 0
			ConDVecZ = -1

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			parameters ConPosX_2 = 0
			parameters ConPosZ_2 = 0
		endif
	endif

	if iWCType = WC_SQUATTING then
		ConDVecX = 0
		ConDVecY = 0
		ConDVecZ = -1

		ConWVecX = -1
		ConWVecY = 0
		ConWVecZ = 0

		parameters ConPosX_2 = 0
		parameters ConPosZ_2 = -wasteZPosition
	endif


	call "MEP_m_ConnectionsACL_4" parameters 	SetProgram 					= SetProgram,
												gs_Connections 				= gs_Connections,
												MEP_NumConnectionData 		= MEP_NumConnectionData,
												MEP_StrConnectionData 		= MEP_StrConnectionData,
												MEP_NumberConnections 		= MEP_NumberConnections,
												gs_AddConnections 			= gs_AddConnections,
												ConName 					= ConName,
												ConID 						= ConID,
												ConPosX 					= ConPosX_2,
												ConPosY 					= ConPosY_2 - (ConWidth_2/2+0.030)*(iWCType = WC_CORNER and DirEffluent_ID = EFFLUENT_BACK and iFlushType <> IN_WALL_CARRIER),
												ConPosZ 					= ConPosZ_2,
												ConDVecX 					= ConDVecX,
												ConDVecY 					= ConDVecY,
												ConDVecZ 					= ConDVecZ,
												ConWVecX 					= ConWVecX,
												ConWVecY 					= ConWVecY,
												ConWVecZ 					= ConWVecZ,

												ConNominalWidth 			= ConNominalWidth_2,
												ConNominalDepth 			= ConNominalDepth_2,
												ConWidth 					= ConWidth_2,
												ConDepth 					= ConDepth_2,
												ConLength 					= ConLength_2,
												ConWallThickness 			= ConWallThickness_2,
												ConConnectorWidth 			= ConConnectorWidth_2,
												ConConnectorDepth 			= ConConnectorDepth_2,
												ConConnectorWidth2 			= ConConnectorWidth2_2,
												ConToolType 				= ConToolType_2,
												ConStatus 					= ConStatus_2,
												ConType_2 					= ConType_2,
												ConConnectorType_2 			= ConConnectorType_2,
												ConSystem_2 				= ConSystem_2,
												shape_typ_to_validation 	= shape_typ_to_validation,
												MEP_enabled_geometry_mod 	= 1,
												gs_ui 						= 0


	parameters MEP_ConInfo[ConID][1] = str(ConToolType_2, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	if gs_ui_current_page = 3 then
		ac_mep_connectionpage_active = 1
	else
		ac_mep_connectionpage_active = 0
	endif
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
else
	hideparameter"gs_connection", "gs_con_mat"

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active
endif


!================================================================================
! User Interface
!================================================================================

call "ui_tabcontrol" parameters controlType 		= 1,
								gs_ui_current_page 	= gs_ui_current_page

call "ui_plumbingfixtures" parameters 	iObjectType		  	= 40 + bDisabledWC,
										iWCType 			= iWCType,
										iFlushType			= iFlushType,
										iFlushometerType	= iFlushometerType,
										gs_ui_current_page  = gs_ui_current_page,
										bDisabledWC			= bDisabledWC

