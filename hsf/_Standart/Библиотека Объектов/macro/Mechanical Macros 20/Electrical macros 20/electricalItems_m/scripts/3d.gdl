
! ==============================================================================
! electricalItems_m
! ==============================================================================


if gs_detlevel_3D_m = 0 then end

bCenterPlaced = (iPlaceBy = 1)

if gs_detlevel_3D_m = 2 then
	resol gs_resol
else
	resol 6
endif
pen gs_cont_pen


! ==============================================================================
! Height editing hotspot
! ==============================================================================

unID = 1

if iGroupType = GROUP_OUTLET & iObjectType = OBJECT_FLOOR_OUTLET then
else
	hotspot 0, 0, 0, unID, gs_placementHeight, 1 : unID = unID + 1
	hotspot 0, 0, gs_placementHeight, unID, gs_placementHeight, 2 : unID = unID + 1
	hotspot 0, 0, -1, unID, gs_placementHeight, 3 : unID = unID + 1
endif


! ==============================================================================
! Placing object
! ==============================================================================

if iGroupType = GROUP_OUTLET & iObjectType = OBJECT_FLOOR_OUTLET then
	addz 0
	rotx 0
else
	if iOrientation = 1 then
		placeHeightMod = (min(gs_plateHeight, gs_boxHeight))/2
	else
		placeHeightMod = (max(gs_plateHeight, gs_boxHeight))/2
	endif
	addz gs_placementHeight + not (bCenterPlaced) * placeHeightMod + bottomPanelOverhang
	rotx 90
endif
if iOrientation = 2 then rotz 90
addx -fullLength/2 + max (leftPanelOverhang, leftBoxOverhang) + elementLength/2

for ijk = 1 to nModItem
	if ijk = 1 then
		lPanelOverhang = leftPanelOverhang
		lBoxOverhang = leftBoxOverhang
		bLeftEdge = not (bLeftCon)
		bShowLeftHotspot = 1
	else
		lPanelOverhang = 0
		lBoxOverhang = 0
		bLeftEdge = 0
		bShowLeftHotspot = 0
	endif

	if ijk = nModItem then
		rPanelOverhang = rightPanelOverhang
		rBoxOverhang = rightBoxOverhang
		bRightEdge = not (bRightCon)
		bShowRightHotspot = 1
	else
		rPanelOverhang = 0
		rBoxOverhang = 0
		bRightEdge = 0
		bShowRightHotspot = 0
	endif

	bSwitch = 1
	bOutlet = 1
	if iGroupType = GROUP_SWITCH_OUTLET then
		if ijk = nModItem then
			bOutlet = 1
			bSwitch = 0
		else
			bOutlet = 0
			bSwitch = 1
		endif
	endif
	gosub "Item"

	addx elementLength
next ijk

del nModItem + 1
if iOrientation = 2 then del 1
del 2


end




! ==============================================================================
"Item":
! ==============================================================================

	if bShowLeftHotspot then
		hotspot -elementLength/2 - lPanelOverhang, -elementHeight/2 - bottomPanelOverhang, 0, unID : unID = unID + 1
		hotspot -elementLength/2 - lPanelOverhang,  elementHeight/2 + topPanelOverhang, 0, unID : unID = unID + 1
	endif
	if bShowRightHotspot then
		hotspot elementLength/2 + rPanelOverhang, -elementHeight/2 - bottomPanelOverhang, 0, unID : unID = unID + 1
		hotspot elementLength/2 + rPanelOverhang,  elementHeight/2 + topPanelOverhang, 0, unID : unID = unID + 1
	endif

	if (iGroupType = GROUP_OUTLET | iGroupType = GROUP_SWITCH_OUTLET | (iGroupType = GROUP_SIGNAL & (iObjectType = OBJECT_PHONE_SIGNAL | iObjectType = OBJECT_COMMUNICATION_SIGNAL))) & bOutlet then
		if iOutletType = RECEPTACLE then
			material gs_matOutlet

			addz gs_plateThk/4
			if iItemType = SINGLE & (iObjectType = OBJECT_OUTLET_1 | iObjectType = OBJECT_OUTLET_2\
			 | iObjectType = OBJECT_SWITCH_OUTLET | iObjectType = OBJECT_FLOOR_OUTLET) then
				addy 0
				nOutlet = 1
			else
				if iObjectType = OBJECT_OUTLET_1 & iItemType = TRIPLE then
					addy 0
					nOutlet = 1
				else
					addy -0.018
					nOutlet = 2
				endif
			endif
			for i = 1 to nOutlet
				put	 0.01,  -0.012, 15,
					-0.007,  0, 900,
					 0.01,  0.012, 3015,
					-0.01,  0.012, 15,
					 0.007,  0, 900,
					-0.01,  -0.012, 3015,
					 0.01,  -0.012, -1

				if gs_detlevel_3D_m = 2 then
					put -0.005, 0, 15,
						-0.007, 0, 15,
						-0.007, 0.006, 15,
						-0.005, 0.006, 15,
						-0.005, 0, -1,
						 0.005, 0, 15,
						 0.007, 0, 15,
						 0.007, 0.006, 15,
						 0.005, 0.006, 15,
						 0.005, 0, -1,
						-0.002, -0.01, 15,
						 0.002, -0.01, 15,
						 0.002, -0.008, 15 + 64,
						-0.002, -0.008, 1015 + 64,
						-0.002, -0.01, -1

				endif

				prism_ nsp/3, gs_plateThk*3/4 + 0.002,
					get (nsp)

				add 0, -0.018, -gs_plateThk/4
				cutpolya 7, 1, gs_plateThk/4,
					-0.01,  0.006, 15,
					 0.01,  0.006, 15,
					-0.007,  0.018, 900,
					 0.01,  0.030, 3015,
					-0.01,  0.030, 15,
					 0.007,  0.018, 900,
					-0.01,  0.006, 3015
				del 1

				addy 0.036
			next i

			del 2 + nOutlet
		endif

		if iOutletType = CABLE_OUTLET then
			addz gs_plateThk

			rrr = 0.004
			rrr2 = 0.006
			material gs_matOutlet

			if gs_detlevel_3D_m = 2 then
				prism_ 7, 0.003,
					 rrr2, 0, 15,
					 rrr2 * cos(60),  rrr2 * sin(60), 15,
					-rrr2 * cos(60),  rrr2 * sin(60), 15,
					-rrr2, 0, 15,
					-rrr2 * cos(60), -rrr2 * sin(60), 15,
					 rrr2 * cos(60), -rrr2 * sin(60), 15,
					 rrr2, 0, -1

				addz 0.003
				cylind 0.009, rrr
				del 1
			else
				cylind 0.012, rrr
			endif
			del 1
		endif

		if iOutletType = PHONE_OUTLET then
			addz gs_plateThk/4

			material gs_matOutlet

			prism_ 14, gs_plateThk*3/4 + 0.002,
				-0.0075, -0.0075, 15,
				-0.0075,  0.0075, 15,
				 0.0075,  0.0075, 15,
				 0.0075, -0.0075, 15,
				-0.0075, -0.0075, -1,

				-0.0055, -0.0045, 15,
				-0.0035, -0.0045, 15,
				-0.0035, -0.0065, 15,
				 0.0035, -0.0065, 15,
				 0.0035, -0.0045, 15,
				 0.0055, -0.0045, 15,
				 0.0055,  0.0045, 15,
				-0.0055,  0.0045, 15,
				-0.0055, -0.0045, -1

			del 1

			cutpolya 4, 1, gs_plateThk/4,
				-0.0075, -0.0075, 15,
				-0.0075,  0.0075, 15,
				 0.0075,  0.0075, 15,
				 0.0075, -0.0075, 15
		endif
	endif

	if (iGroupType = GROUP_SWITCH | iGroupType = GROUP_SWITCH_OUTLET) & bSwitch then
		if 	iSwitchType = TOGGLE then
			material gs_matSwitch
			roty 90
			rotz 90
			addz -0.005

			prism_ 9, 0.01,
				-0.015, 0, 15,
				 0.015, 0, 15,
				 0.015, gs_plateThk*3/4, 15,
				 0.012, gs_plateThk*3/4, 15,
				 0.012, gs_plateThk*3/4+0.01, 15,
				 0.004, gs_plateThk*3/4+0.01, 15,
				 0.004, gs_plateThk*3/4, 15,
				-0.015, gs_plateThk*3/4, 15,
				-0.015, 0, -1

			del 3

			if gs_detlevel_3D_m = 2 then
				prism_ 10, gs_plateThk + 0.001,
					-0.007, -0.017, 15,
					 0.007, -0.017, 15,
					 0.007,  0.017, 15,
					-0.007,  0.017, 15,
					-0.007, -0.017, -1,
					-0.005, -0.015, 15,
					 0.005, -0.015, 15,
					 0.005,  0.015, 15,
					-0.005,  0.015, 15,
					-0.005, -0.015, -1

				cutpolya 4, 1, 0,
					-0.007, -0.017, 15,
					 0.007, -0.017, 15,
					 0.007,  0.017, 15,
					-0.007,  0.017, 15
			else
				cutpolya 4, 1, 0,
					-0.005, -0.015, 15,
					 0.005, -0.015, 15,
					 0.005,  0.015, 15,
					-0.005,  0.015, 15
			endif
		endif

		if iSwitchType = ROCKER then
			material gs_matSwitch
			roty 90
			rotz 90
			addz -0.016

			prism_ 7, 0.032,
				-0.035, 0, 15,
				 0.035, 0, 15,
				 0.035, gs_plateThk + 0.002, 15,
				 0, gs_plateThk + 0.002, 15,
				-0.031, gs_plateThk*3, 15,
				-0.035, gs_plateThk + 0.002, 15,
				-0.035, 0, -1

			del 3

			if gs_detlevel_3D_m = 2 then
				prism_ 10, gs_plateThk + 0.001,
					-0.018, -0.037, 15,
					 0.018, -0.037, 15,
					 0.018,  0.037, 15,
					-0.018,  0.037, 15,
					-0.018, -0.037, -1,
					-0.016, -0.035, 15,
					 0.016, -0.035, 15,
					 0.016,  0.035, 15,
					-0.016,  0.035, 15,
					-0.016, -0.035, -1

				cutpolya 4, 1, 0,
					-0.018, -0.037, 15,
					 0.018, -0.037, 15,
					 0.018,  0.037, 15,
					-0.018,  0.037, 15
			else
				cutpolya 4, 1, 0,
					-0.016, -0.035, 15,
					 0.016, -0.035, 15,
					 0.016,  0.035, 15,
					-0.016,  0.035, 15
			endif
		endif

		if iSwitchType = BUTTON then
			addz gs_plateThk
			material gs_matSwitch
			if gs_detlevel_3D_m = 2 then
				cylind 0.006, 0.0075
				addz 0.006
				cylind 0.01, 0.006
				del 1
			else
				cylind 0.016, 0.006
			endif
			del 1
		endif
	endif

	material gs_matPlate

	if gs_detlevel_3D_m = 2 then
		if not(bRightEdge) then
			cutpolya 4, 1, 0,
				elementLength/2 + rPanelOverhang, -1, 8,
				elementLength/2 + rPanelOverhang+1, -1, 8,
				elementLength/2 + rPanelOverhang+1, 1, 8,
				elementLength/2 + rPanelOverhang, 1, 8
		endif
		if not(bLeftEdge) then
			cutpolya 4, 1, 0,
				-elementLength/2 - lPanelOverhang, -1, 8,
				-elementLength/2 - lPanelOverhang-1, -1, 8,
				-elementLength/2 - lPanelOverhang-1, 1, 8,
				-elementLength/2 - lPanelOverhang, 1, 8
		endif
		if bTopCon then
			cutpolya 4, 1, 0,
				-elementLength/2 - lPanelOverhang - 1, elementHeight/2 + topPanelOverhang, 8,
				 elementLength/2 + rPanelOverhang + 1, elementHeight/2 + topPanelOverhang, 8,
				 elementLength/2 + rPanelOverhang + 1, elementHeight/2 + topPanelOverhang + 1, 8,
				-elementLength/2 - lPanelOverhang - 1, elementHeight/2 + topPanelOverhang + 1, 8
		endif
		if bBottomCon then
			cutpolya 4, 1, 0,
				-elementLength/2 - lPanelOverhang - 1, -elementHeight/2 - bottomPanelOverhang, 8,
				 elementLength/2 + rPanelOverhang + 1, -elementHeight/2 - bottomPanelOverhang, 8,
				 elementLength/2 + rPanelOverhang + 1, -elementHeight/2 - bottomPanelOverhang - 1, 8,
				-elementLength/2 - lPanelOverhang - 1, -elementHeight/2 - bottomPanelOverhang - 1, 8
		endif

		put	-elementLength/2 - lPanelOverhang-(0.1 + gs_plateThk)*not(bLeftEdge),	-elementHeight/2 - bottomPanelOverhang - bBottomCon * (0.1 + gs_plateThk), 8 + 2 * bLeftEdge * not (bBottomCon) + 5 * not(bBottomCon),
			 elementLength/2 + rPanelOverhang+(0.1 + gs_plateThk)*not(bRightEdge),	-elementHeight/2 - bottomPanelOverhang - bBottomCon * (0.1 + gs_plateThk), 8 + 2 * bRightEdge * not (bBottomCon) + 5 * bRightEdge,
			 elementLength/2 + rPanelOverhang+(0.1 + gs_plateThk)*not(bRightEdge),	 elementHeight/2 + topPanelOverhang + bTopCon * (0.1 + gs_plateThk), 8 + 2 * bRightEdge * not (bTopCon) + 5 * not(bTopCon),
			-elementLength/2 - lPanelOverhang-(0.1 + gs_plateThk)*not(bLeftEdge),	 elementHeight/2 + topPanelOverhang + bTopCon * (0.1 + gs_plateThk), 8 + 2 * bLeftEdge * not (bTopCon) + 5 * bLeftEdge,
			-elementLength/2 - lPanelOverhang-(0.1 + gs_plateThk)*not(bLeftEdge),	-elementHeight/2 - bottomPanelOverhang - bBottomCon * (0.1 + gs_plateThk), -1

	else
		put	-elementLength/2 - lPanelOverhang,	-elementHeight/2 - bottomPanelOverhang, 8 + 2 * bLeftEdge * not (bBottomCon) + 5 * not(bBottomCon),
			 elementLength/2 + rPanelOverhang,	-elementHeight/2 - bottomPanelOverhang, 8 + 2 * bRightEdge * not (bBottomCon) + 5 * bRightEdge,
			 elementLength/2 + rPanelOverhang,	 elementHeight/2 + topPanelOverhang, 8 + 2 * bRightEdge * not (bTopCon) + 5 * not(bTopCon),
			-elementLength/2 - lPanelOverhang,	 elementHeight/2  +topPanelOverhang, 8 + 2 * bLeftEdge * not (bTopCon) + 5 * bLeftEdge,
			-elementLength/2 - lPanelOverhang,	-elementHeight/2 - bottomPanelOverhang, -1
	endif

	if gs_detlevel_3D_m = 2 then
		hprism_ gs_matPlate, gs_matPlate, gs_matPlate, gs_matPlate,
			nsp/3, gs_plateThk, 0, gs_plateThk/4, 1,
				get (nsp)

		if not(bRightEdge) then cutend
		if not(bLeftEdge) then cutend
		if bTopCon then cutend
		if bBottomCon then cutend
	else
		prism_ nsp/3, gs_plateThk,
				get (nsp)
	endif

	if (iGroupType = GROUP_OUTLET | iGroupType = GROUP_SWITCH_OUTLET | (iGroupType = GROUP_SIGNAL & (iObjectType = OBJECT_PHONE_SIGNAL | iObjectType = OBJECT_COMMUNICATION_SIGNAL))) & bOutlet then
		if iOutletType = PHONE_OUTLET then cutend
		if iOutletType = RECEPTACLE then
			for ji = 1 to nOutlet
				cutend
			next ji
		endif
	endif
	if (iGroupType = GROUP_SWITCH | iGroupType = GROUP_SWITCH_OUTLET) & bSwitch then
		if 	iSwitchType = TOGGLE then cutend
		if iSwitchType = ROCKER then cutend
	endif


	if gs_bShowBoxIn3D then
		addz -gs_boxThk

		material gs_matBox

		prism_ 5, gs_boxThk,
			-elementLength/2 - lBoxOverhang,	-elementHeight/2 - bottomBoxOverhang, 8 + 2 * bLeftEdge * not (bBottomCon) + 5 * not(bBottomCon),
			 elementLength/2 + rBoxOverhang,	-elementHeight/2 - bottomBoxOverhang, 8 + 2 * bRightEdge * not (bBottomCon) + 5 * bRightEdge,
			 elementLength/2 + rBoxOverhang,	 elementHeight/2 + topBoxOverhang, 8 + 2 * bRightEdge * not (bTopCon) + 5 * not(bTopCon),
			-elementLength/2 - lBoxOverhang,	 elementHeight/2 + topBoxOverhang, 8 + 2 * bLeftEdge * not (bTopCon) + 5 * bLeftEdge,
			-elementLength/2 - lBoxOverhang,	-elementHeight/2 - bottomBoxOverhang, -1

		if bShowLeftHotspot then
			hotspot -elementLength/2 - lBoxOverhang, -elementHeight/2 - bottomBoxOverhang, 0, unID : unID = unID + 1
			hotspot -elementLength/2 - lBoxOverhang,  elementHeight/2 + topBoxOverhang, 0, unID : unID = unID + 1
			hotspot -elementLength/2 - lBoxOverhang, -elementHeight/2 - bottomBoxOverhang, gs_boxThk, unID : unID = unID + 1
			hotspot -elementLength/2 - lBoxOverhang,  elementHeight/2 + topBoxOverhang, gs_boxThk, unID : unID = unID + 1
		endif
		if bShowRightHotspot then
			hotspot elementLength/2 + rBoxOverhang, -elementHeight/2 - bottomBoxOverhang, 0, unID : unID = unID + 1
			hotspot elementLength/2 + rBoxOverhang,  elementHeight/2 + topBoxOverhang, 0, unID : unID = unID + 1
			hotspot elementLength/2 + rBoxOverhang, -elementHeight/2 - bottomBoxOverhang, gs_boxThk, unID : unID = unID + 1
			hotspot elementLength/2 + rBoxOverhang,  elementHeight/2 + topBoxOverhang, gs_boxThk, unID : unID = unID + 1
		endif
		del 1
	endif

return
