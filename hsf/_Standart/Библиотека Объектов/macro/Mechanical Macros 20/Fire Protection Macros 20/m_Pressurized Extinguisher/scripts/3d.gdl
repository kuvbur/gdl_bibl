
IF gs_detlevel_3D_m=0 THEN END

if gs_shadow then
	SHADOW ON
else
	SHADOW OFF
endif

PEN gs_cont_pen

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

! --- Hotspots -----------------------------------------------------------------
unID = startUnID
HOTSPOT b/2+0.04, -b/2, 0, unID
unID=unID+1
HOTSPOT b/2+0.04,  b/2, 0, unID
unID=unID+1
IF ra/2<=SQR(0.015*0.015+0.14*0.14) THEN
	HOTSPOT -0.14, -ra/2, 0, unID
	unID=unID+1
	HOTSPOT -0.14,  ra/2, 0, unID
	unID=unID+1
else
	unID=unID+2
ENDIF

IF ra/2>SQR(0.015*0.015+0.14*0.14) THEN
	HOTSPOT -ra/2, -ra/2, 0, unID
	unID=unID+1
	HOTSPOT -ra/2,  ra/2, 0, unID
	unID=unID+1
else
	unID=unID+2
ENDIF

hotspot 0, 0, 0, unID, zzyzx, 1+128
unID=unID+1
hotspot 0, 0, -1, unID,  zzyzx, 3
unID=unID+1
hotspot 0, 0, zzyzx, unID,  zzyzx, 2
unID=unID+1

! --- Simple 3D ----------------------------------------------------------------

IF gs_detlevel_3D_m=1 THEN

	RESOL 12
	MATERIAL cyl_mat

	ROTY -90
	REVOLVE 4, 360, 1+2+4+8+16+32,
		0, ra/2, 0,
		zzyzx-0.160, ra/2, 1,
		zzyzx-0.085, 0.025, 1000,
		zzyzx, 0.025,  0
	DEL 1

	END
ENDIF

! --- Detailed 3D --------------------------------------------------------------

RESOL gs_resol
MATERIAL cyl_mat

ADDZ zzyzx-0.04
ROTX 90
CUTPOLYA 4, 1, 0,
	0.025, 0, 79,
	0, 1, 800,
   -0.025, 0, 1079,
	0.025, 0, 1079
DEL 2

ROTY -90
REVOLVE 16, 360, 1+2+4+8+16+32,
	0, ra/2, 0,
	0.030, ra/2, 0,
	0.030, ra/2+0.002, 0,
	0.045, ra/2+0.002, 0,
	0.045, ra/2, 0,
	zzyzx-0.175, ra/2, 0,
	zzyzx-0.175, ra/2+0.002, 0,
	zzyzx-0.160, ra/2+0.002, 0,
	zzyzx-0.160, ra/2, 0,
	zzyzx-0.160+0.00001, ra/2, 1,
	1, 0, 800,
	zzyzx-0.085, 0.025, 1001,
	zzyzx-0.085, 0.025, 0,
	zzyzx-0.07, 0.025, 0,
	zzyzx-0.07, 0.015, 0,
	zzyzx-0.04, 0.015, 0
DEL 1

CUTEND



ADD 0, 0.025, zzyzx-0.04
ROTX 90
CYLIND 0.05, 0.025
DEL 2


MATERIAL pipe_mat

ADDZ zzyzx-0.04
ROTX 90
CUTPOLYA 4, 1, 0,
	0.025, 0, 79,
	0, 1, 800,
   -0.025, 0, 1079,
	0.025, 0, 1079
DEL 2

ADD -0.013, 0.015, zzyzx-0.04
FOR HI=1 TO 2
ROTY -90
ROTX 90

RESOL gs_resol*4
PRISM_ 6, 0.03,
	0, 0, 79,
	0, 1, 800,
	0.3, 25, 2015,
	0.013, 205, 279,
   -0.3, 0, 900,
	0, -25, 4079
DEL 2

MULZ -1
NEXT HI
DEL 2

DEL 1



_even_gs_resol = 2 * ceil ( gs_resol / 2 )

ROTX 90
RESOL _even_gs_resol

beta=360/_even_gs_resol/2
r=0.052
eta=-90+beta
for i=1 to _even_gs_resol/4*2 step 1
	put ra/2+0.04-0.064+r*cos(eta), zzyzx-0.04-r-r*sin(eta),0,0
	eta=eta+360/_even_gs_resol/2
next i

TUBE  4,   5+_even_gs_resol/4*2,   1+2+16+32,
		0.012,    0,     1,
		0,    1,   800,
		-0.012,    0,  1001,
		0.012,    0,  1001,

		-1, zzyzx-0.04, 0,  0,
		0,  zzyzx-0.04,    0,  0,
		ra/2+0.04-0.064, zzyzx-0.04,  0,  0,
		get ((_even_gs_resol/4)*4*2),
		ra/2+0.028,  0.03,  0, 0,
		ra/2+0.028,  0,  0, 0

DEL 1


CUTEND


END
