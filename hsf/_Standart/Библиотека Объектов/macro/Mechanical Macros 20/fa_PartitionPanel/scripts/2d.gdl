
unID = GS_HotspotUnIDBegin

! === Panel ====================================================================

pen penPartition
line_type lineType2dPanel
fill fillPartition
drawindex 30


if iPartitionType = PARTITION_TYPE_STALL or iPartitionType = PARTITION_TYPE_URINAL_SCREEN then

	poly2_b 4, 1+2+4+32, fillPenPartition, fillPenBgPartition,
			0,				0,					1,
			0,				thicknessPanel,		1,
			lengthPanel,	thicknessPanel,		1,
			lengthPanel,	0,					1

	hotline2 0, 0, lengthPanel, 0
	hotline2 0, thicknessPanel, lengthPanel, thicknessPanel
	if abs (maxThickness - thicknessPanel) > eps then
		add2 0, thicknessPanel / 2
		hotline2 0, -maxThickness / 2,	lengthPanel, -maxThickness / 2
		hotline2 0,  maxThickness / 2,	lengthPanel,  maxThickness / 2
		del 1
	endif


	! === Bottom Bracing ===========================================================


	if bBottomBracing and bShowB then

		pen penBottomBracing
		line_type lineTypeBottomBracing
		fill fillTypeBottomBracing
		drawindex 20

		add2 -leftOverhangBottomBracing, thicknessPanel / 2
		if abs(thicknessBottomBracing - thicknessPanel) > eps then
			line2 leftDiffBottomBracing,		 thicknessBottomBracing / 2,		lengthBracing,		 thicknessBottomBracing / 2
			line2 leftDiffBottomBracing,		-thicknessBottomBracing / 2,		lengthBracing,		-thicknessBottomBracing / 2
			poly2_b 5, 2+64, fillPenBottomBracing, fillBackPenBottomBracing,
					leftDiffBottomBracing,	-thicknessBottomBracing / 2,		1,
					leftDiffBottomBracing,	-thicknessPanel / 2,				1,
					lengthBracing,			-thicknessPanel / 2,				1,
					lengthBracing,			-thicknessBottomBracing / 2,		1,
					leftDiffBottomBracing,	-thicknessBottomBracing / 2,		-1
			poly2_b 5, 2+64, fillPenBottomBracing, fillBackPenBottomBracing,
					leftDiffBottomBracing,	thicknessBottomBracing / 2,			1,
					leftDiffBottomBracing,	thicknessPanel / 2,					1,
					lengthBracing,			thicknessPanel / 2,					1,
					lengthBracing,			thicknessBottomBracing / 2,			1,
					leftDiffBottomBracing,	thicknessBottomBracing / 2,			-1
		endif
		if leftOverhangBottomBracing > eps then
			if abs(thicknessBottomBracing - thicknessPanel) < eps then
				line2 leftDiffBottomBracing,	-thicknessPanel / 2,	leftOverhangBottomBracing,	-thicknessPanel / 2
				line2 leftDiffBottomBracing,	thicknessPanel / 2,		leftOverhangBottomBracing,	thicknessPanel / 2
			endif
			poly2_b 5, 2+64, fillPenBottomBracing, fillBackPenBottomBracing,
					leftDiffBottomBracing,				-thicknessPanel / 2,	1,
					leftDiffBottomBracing,				 thicknessPanel / 2,	1,
					leftOverhangBottomBracing,		 thicknessPanel / 2,	1,
					leftOverhangBottomBracing,		-thicknessPanel / 2,	1,
					leftDiffBottomBracing,				-thicknessPanel / 2,	-1
			if bStandAlone then line2 0, -thicknessBottomBracing / 2, 0, thicknessBottomBracing / 2
		else
			if bStandAlone then
				line2 0,  thicknessPanel / 2, 0,  thicknessBottomBracing / 2
				line2 0, -thicknessPanel / 2, 0, -thicknessBottomBracing / 2
			endif
		endif
		if rightOverhangBottomBracing > eps then
			if abs(thicknessBottomBracing - thicknessPanel) < eps then
				line2 leftOverhangBottomBracing + lengthPanel, -thicknessPanel / 2, lengthBracing, -thicknessPanel / 2
				line2 leftOverhangBottomBracing + lengthPanel,  thicknessPanel / 2, lengthBracing,  thicknessPanel / 2
			endif
			poly2_b 5, 2+64, fillPenBottomBracing, fillBackPenBottomBracing,
					leftOverhangBottomBracing + lengthPanel,	-thicknessPanel / 2,	1,
					leftOverhangBottomBracing + lengthPanel,	 thicknessPanel / 2,	1,
					lengthBracing,								 thicknessPanel / 2,	1,
					lengthBracing,								-thicknessPanel / 2,	1,
					leftOverhangBottomBracing + lengthPanel,	-thicknessPanel / 2,	-1
			line2 lengthBracing, -thicknessBottomBracing / 2, lengthBracing, thicknessBottomBracing / 2
		else
			line2 lengthBracing,	 thicknessBottomBracing / 2,	lengthBracing,		 thicknessPanel / 2
			line2 lengthBracing,	-thicknessBottomBracing / 2,	lengthBracing,		-thicknessPanel / 2
		endif

		del 1
	endif


	! === Top Bracing ==============================================================

	pen penTopBracing
	line_type lineTypeTopBracing

	if bTopBracing and bShowB and abs(thicknessTopBracing - thicknessPanel) > eps and \
			(not(bBottomBracing) or abs( thicknessBottomBracing - thicknessTopBracing ) > eps) then
		add2 -leftOverhangTopBracing, thicknessPanel / 2
		line2 leftDiffTopBracing,		-thicknessTopBracing / 2,		lengthBracing,		-thicknessTopBracing / 2
		line2 leftDiffTopBracing,		 thicknessTopBracing / 2,		lengthBracing,		 thicknessTopBracing / 2

		if 	thicknessTopBracing - (thicknessBottomBracing * bBottomBracing) > eps then
			if bBottomBracing then
				line2 lengthBracing,	-thicknessTopBracing / 2,	lengthBracing,	-thicknessBottomBracing / 2
				line2 lengthBracing,	 thicknessTopBracing / 2,	lengthBracing,	 thicknessBottomBracing / 2
			else
				if rightOverhangTopBracing > eps then
					line2 lengthBracing,	 thicknessTopBracing / 2,	lengthBracing,	 -thicknessTopBracing / 2
				else
					line2 lengthBracing,	-thicknessTopBracing / 2,	lengthBracing,	-thicknessPanel / 2
					line2 lengthBracing,	 thicknessTopBracing / 2,	lengthBracing,	 thicknessPanel / 2
				endif
			endif
		endif

		if 	bStandAlone and thicknessTopBracing - (thicknessBottomBracing * bBottomBracing) > eps \
								and thicknessTopBracing - (thicknessPost * bFrontSidePost) > eps then
			if bFrontSidePost and bBottomBracing then
				line2 0,	 thicknessTopBracing / 2,	0,	 (max(thicknessBottomBracing, thicknessPost)) / 2
				line2 0,	-thicknessTopBracing / 2,	0,	-(max(thicknessBottomBracing, thicknessPost)) / 2
			endif
			if bFrontSidePost and not (bBottomBracing) then
				line2 0,	-thicknessTopBracing / 2,	0,	-thicknessPost / 2
				line2 0,	 thicknessTopBracing / 2,	0,	 thicknessPost / 2
			endif
			if not (bFrontSidePost) and bBottomBracing then
				line2 0,	-thicknessTopBracing / 2,	0,	-thicknessBottomBracing / 2
				line2 0,	 thicknessTopBracing / 2,	0,	 thicknessBottomBracing / 2
			endif
			if not (bFrontSidePost) and not (bBottomBracing) then
				if leftOverhangTopBracing < eps then
					line2 0,	-thicknessTopBracing / 2,	0,	-thicknessPanel / 2
					line2 0,	 thicknessTopBracing / 2,	0,	 thicknessPanel / 2
				else
					line2 0,	-thicknessTopBracing / 2,	0,	thicknessTopBracing / 2
				endif
			endif
		endif
		del 1
	endif

	if bFrontSidepost then
		pen penPost
		line_type lineTypePost
		fill fillTypePost
		drawindex 30
		add2 -widthPost - leftOverhangTopBracing, - (thicknessPost - thicknessPanel) /2
		poly2_b 4, 1+2+4+32, fillPenPost, fillBackPenPost,
			0,			0,					3,
			widthPost,	0,					3,
			widthPost,	thicknessPost,		3,
			0,			thicknessPost,		3
		hotspot2 widthPost, 0,				unID : unID = unID + 1
		hotspot2 widthPost, thicknessPost,	unID : unID = unID + 1
		del 1
	else
		unID = unID + 2
	endif

endif

if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	add2 lengthPanel, dividerWidth/2
	rot2 90
	poly2_b 5, 1+2+4+32, fillPenPartition, fillPenBgPartition,
		dividerWidth / 2, 0, 1,
		dividerWidth / 2 - x1, y1, 1,
		0, dividerDepth, 1001,
		-dividerWidth / 2 + x1, y1, 1001,
		-dividerWidth / 2, 0, 1
	del 2
endif

end
