
pen penPartition

if iPartitionType = PARTITION_TYPE_STALL or iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
	addz bottomHeightPanel
	cprism_ matPanel, matPanel, matPanel,
		4, 		heightPanel - bottomHeightPanel,
		0,				0,					15,
		lengthPanel,	0,					15,
		lengthPanel,	thicknessPanel,		15,
		0,				thicknessPanel,		15
	del 1

	if bFooter then
		cprism_ matFooter, matFooter, matFooter,
			4,		bottomHeightPanel,
			0,				0,					15,
			lengthPanel,	0,					15,
			lengthPanel,	thicknessPanel,		15,
			0,				thicknessPanel,		15
	endif

	if bTopBracing then
		addx -leftOverhangTopBracing
		addy - ( (thicknessTopBracing - thicknessPanel) / 2 )
		addz bottomHeightTopBracing
		cprism_ matTopBracing, matTopBracing, matTopBracing,
				4,		heightTopBracing,
				leftDiffTopBracing,	0,							15,
				lengthBracing,		0,							15,
				lengthBracing,		thicknessTopBracing,		15,
				leftDiffTopBracing,	thicknessTopBracing,		15
		del 3
	endif

	if bBottomBracing then
		addx -leftOverhangBottomBracing
		addy - ( (thicknessBottomBracing - thicknessPanel) / 2 )
		addz topHeightBottomBracing - heightBottomBracing
		cprism_ matBottomBracing, matBottomBracing, matBottomBracing,
				4,		heightBottomBracing,
				leftDiffBottomBracing,	0,							15,
				lengthBracing,		0,							15,
				lengthBracing,		thicknessBottomBracing,		15,
				leftDiffBottomBracing,	thicknessBottomBracing,		15
		del 3
	endif

	if bFrontSidepost then
		addx -widthPost - leftOverhangTopBracing
		addy - (thicknessPost - thicknessPanel) /2
		addz bottomHeightPost
		cprism_		matPost, matPost, matPost,
					4, topHeightPost - bottomHeightPost,
					0,				0,					15,
					widthPost,		0,					15,
					widthPost,		thicknessPost,		15,
					0,				thicknessPost,		15
		del 3
	endif
endif

if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	material matDivision
	add lengthPanel, dividerWidth/2,0

	addz dividerHeight / 2 + dividerElev
	mulz dividerHeight / (dividerDepth * 2)
	rotx 90
	roty -90
	revolve 5, 180, 1 + 2 + 4 + 8 + 16 + 32,
		dividerWidth / 2, 0, 1,
		dividerWidth / 2 - x1, y1, 1,
		0, dividerDepth, 1001,
		-dividerWidth / 2 + x1, y1, 1001,
		-dividerWidth / 2, 0, 1
	del 5
endif

