

if gs_detlevel_3d_m=0 then END
if gs_detlevel_3d_m=1 then res=4

unID = GS_HotspotUnIDBegin

resol res

if gs_shadow = 0 then shadow off


! =============================================================================
! front panels
! =============================================================================

addz bottomHeightPanel
cprism_ matPanel, matPanel, matPanel,
	4, 		heightPanel - bottomHeightPanel,
	0,						0,					13 + (isEndLeft) * 2,
	widthHingeSidePanel,	0,					15,
	widthHingeSidePanel,	thicknessPanel,		15,
	0,						thicknessPanel,		0 + (isEndLeft) * 15

addx widthHingeSidePanel + widthDoor
cprism_ matPanel, matPanel, matPanel,
	4, 		heightPanel - bottomHeightPanel,
	0,					0,					15,
	widthLockSidePanel,	0,					0 +  (isEndRight) * 15,
	widthLockSidePanel,	thicknessPanel,		13 + (isEndRight) * 2,
	0,					thicknessPanel,		15

del 2

if bFooter  then
!	addy thicknessPanel/2
!	CYLIND bottomHeightPanel, thicknessPanel/2
!!	cprism_ matFooter, matFooter, matFooter,
!!			4,		bottomHeightPanel,
!!			0,						0,					13 + (isEndLeft) * 2,
!!			widthHingeSidePanel,	0,					15,
!!			widthHingeSidePanel,	thicknessPanel,		15,
!!			0,						thicknessPanel,		0 + (isEndLeft) * 15
!	addx widthHingeSidePanel + widthDoor
!	CYLIND bottomHeightPanel, thicknessPanel/2
!!	cprism_ matFooter, matFooter, matFooter,
!!			4,		bottomHeightPanel,
!!			0,						0,					15,
!!			widthLockSidePanel,		0,					0 + (isEndRight) * 15,
!!			widthLockSidePanel,		thicknessPanel,		13 + (isEndRight) * 2,
!!			0,						thicknessPanel,		15
!	del 2
endif

if bCeilingMountPanel then
	addz heightPanel
	cprism_ matTopPanel, matTopPanel, matTopPanel,
			4,		zzyzx - heightPanel,
			0,						0,					13 + (isEndLeft) * 2,
			widthHingeSidePanel,	0,					15,
			widthHingeSidePanel,	thicknessPanel,		15,
			0,						thicknessPanel,		0 + (isEndLeft) * 15
	addx widthHingeSidePanel + widthDoor
	cprism_ matTopPanel, matTopPanel, matTopPanel,
			4,		zzyzx - heightPanel,
			0,						0,					15,
			widthLockSidePanel,		0,					0 + (isEndRight) * 15,
			widthLockSidePanel,		thicknessPanel,		13 + (isEndRight) * 2,
			0,						thicknessPanel,		15
	del 2
endif


if bTopBracing then
	addx -leftOverhangTopBracing * isEndLeft - (leftOverhangTopBracing < eps) * isEndLeft * (thicknessTopBracing - thicknessPanel)/2
	addy - ( (thicknessTopBracing - thicknessPanel) / 2 )
	addz heightPanel
	cprism_ matTopBracing, matTopBracing, matTopBracing,
			4,		heightTopBracing,
			0,														0,						0 + (isEndLeft) * 15,
			0,														thicknessTopBracing,	13 + (isEndLeft) * 2,
			leftOverhangTopBracing * isEndLeft + rightOverhangTopBracing * isEndRight + a\
				+ (leftOverhangTopBracing < eps) * isEndLeft * 	(thicknessTopBracing - thicknessPanel)/2 \
				+ (rightOverhangTopBracing < eps) * isEndRight * 	(thicknessTopBracing - thicknessPanel)/2,
																	thicknessTopBracing,	0 + (isEndRight) * 15,
			leftOverhangTopBracing * isEndLeft + rightOverhangTopBracing * isEndRight + a\
				+ (leftOverhangTopBracing < eps) * isEndLeft * 	(thicknessTopBracing - thicknessPanel)/2 \
				+ (rightOverhangTopBracing < eps) * isEndRight * 	(thicknessTopBracing - thicknessPanel)/2,
																	0,						13 + (isEndRight) * 2
	del 3
endif

!if bBottomBracing then
!	addx -leftOverhangBottomBracing - (leftOverhangBottomBracing < eps) * bLeftPanel * (thicknessBottomBracing - thicknessPanel)/2
!	addy - ( (thicknessBottomBracing - thicknessPanel) / 2 )
!	addz bottomHeightPanel - heightBottomBracing
!	cprism_ matBottomBracing, matBottomBracing, matBottomBracing,
!			4,		heightBottomBracing,
!			0,														0,						15,
!			0,														thicknessBottomBracing,	15,
!			leftOverhangBottomBracing + widthHingeSidePanel\
!				+ (leftOverhangBottomBracing < eps) * bLeftPanel * 	(thicknessBottomBracing - thicknessPanel)/2 ,	thicknessBottomBracing,	15,
!			leftOverhangBottomBracing + widthHingeSidePanel\
!				+ (leftOverhangBottomBracing < eps) * bLeftPanel * 	(thicknessBottomBracing - thicknessPanel)/2 ,	0,						15
!	del 1
!	if bLeftPanel then
!		addx leftOverhangBottomBracing + (leftOverhangBottomBracing < eps) * bLeftPanel * (thicknessBottomBracing - thicknessPanel)/2
!		cprism_ matBottomBracing, matBottomBracing, matBottomBracing,
!				4, bottomHeightPanel - heightBottomBracing,
!				0,							0,							15,
!				0,							thicknessBottomBracing,		15,
!				thicknessBottomBracing,		thicknessBottomBracing,		15,
!				thicknessBottomBracing,		0,							15
!		del 1
!	endif
!	addx widthHingeSidePanel + widthDoor + leftOverhangBottomBracing + (leftOverhangBottomBracing < eps) * bLeftPanel * (thicknessBottomBracing - thicknessPanel)/2
!	addz bottomHeightPanel - heightBottomBracing
!	cprism_ matBottomBracing, matBottomBracing, matBottomBracing,
!			4,		heightBottomBracing,
!			0,		0,							15,
!			0,		thicknessBottomBracing,		15,
!			rightOverhangBottomBracing + widthLockSidePanel\
!				+ (rightOverhangBottomBracing < eps) * bRightPanel * 	(thicknessBottomBracing - thicknessPanel)/2,	thicknessBottomBracing,	15,
!			rightOverhangBottomBracing + widthLockSidePanel\
!				+ (rightOverhangBottomBracing < eps) * bRightPanel * 	(thicknessBottomBracing - thicknessPanel)/2,	0,						15
!	del 1
!	addx widthLockSidePanel - thicknessBottomBracing
!	if bRightPanel then
!		cprism_ matBottomBracing, matBottomBracing, matBottomBracing,
!				4, bottomHeightPanel - heightBottomBracing,
!				0,							0,							15,
!				0,							thicknessBottomBracing,		15,
!				thicknessBottomBracing,		thicknessBottomBracing,		15,
!				thicknessBottomBracing,		0,							15
!	endif
!	del 4
!endif


! =============================================================================
! Door-panel
! =============================================================================

if bOutsideOpening then
	rr = -openingAngle3d
	yy = 0
	dthck=thicknessDoor
	anglemodify = 512
	vv = -1
else	! inside
	rr = openingAngle3d
	yy = thicknessPanel
	dthck=-thicknessDoor
	anglemodify = 0
	vv = 1
endif

addx widthHingeSidePanel
addz bottomHeightDoor
addy yy

hotspot 0,									0,										heightDoor / 2,			unID, openingAngle3d, 6 + anglemodify	: unID = unID + 1
hotspot widthDoor,							0,										heightDoor / 2,			unID, openingAngle3d, 4 + 128			: unID = unID + 1
hotspot widthDoor * cos (openingAngle3d),	vv * widthDoor * sin (openingAngle3d),	heightDoor / 2,			unID, openingAngle3d, 5			: unID = unID + 1
hotspot 0, 									0,										heightDoor / 2 + vv,	unID, openingAngle3d, 7			: unID = unID + 1

material matFooter
add -thicknessPanel,thicknessPanel/2,-bottomHeightDoor
CYLIND bottomHeightPanel, thicknessPanel/2

addx widthDoor+thicknessPanel*2
CYLIND bottomHeightPanel, thicknessPanel/2
del 2

rotz rr
cprism_ matDoor, matDoor, matDoor,
		4,		heightDoor,
		0,			0,		15,
		widthDoor,	0,		15,
		widthDoor,	dthck,	15,
		0,			dthck,	15

pen 1
lin_ 0, 0, 0, widthDoor, 0, heightDoor/2
lin_ 0, dthck, 0, widthDoor, dthck, heightDoor/2

lin_ 0, 0, heightDoor, widthDoor, 0, heightDoor/2
lin_ 0, dthck, heightDoor, widthDoor, dthck, heightDoor/2
pen penSwing
hotspot  0,			0,	0, unID, widthDoor, 1 + 128	: unID = unID + 1
hotspot -1,			0,	0, unID, widthDoor, 3		: unID = unID + 1
hotspot widthDoor,	0,	0, unID, widthDoor, 2		: unID = unID + 1

hotspot widthDoor, 0,  0,			unID, heightDoor, 1 + 128	: unID = unID + 1
hotspot widthDoor, 0, -1,			unID, heightDoor, 3			: unID = unID + 1
hotspot widthDoor, 0, heightDoor,	unID, heightDoor, 2			: unID = unID + 1

addz -bottomHeightDoor

hotspot widthDoor, 0,  0,				unID, bottomHeightDoor, 1 + 128	: unID = unID + 1
hotspot widthDoor, 0, -1,				unID, bottomHeightDoor, 3		: unID = unID + 1
hotspot widthDoor, 0, bottomHeightDoor, unID, bottomHeightDoor, 2		: unID = unID + 1

del 1


! --- knob --------------------------------------------------------------------
if bGraffiti then
	define material "pictmat" 2, 1, 1, 1 ! RGB
	material "pictmat"
	mulx -1
	add -widthDoor * 0.9, thicknessDoor + eps*5, heightDoor / 2
	rotx 90
	picture 1, widthDoor * 0.8, heightDoor * 0.4, 5
	del 3
endif

if latchType_m > 0 then
	hotspot widthDoor,				0,	heightLatch - bottomHeightDoor, unID, xLatchPos, 1 + 128	: unID = unID + 1
	hotspot widthDoor + 1,			0,	heightLatch - bottomHeightDoor, unID, xLatchPos, 3			: unID = unID + 1
	hotspot widthDoor- xLatchPos,	0,	heightLatch - bottomHeightDoor, unID, xLatchPos, 2			: unID = unID + 1

	hotspot widthDoor - xLatchPos, 0, -bottomHeightDoor,				unID, heightLatch, 1 + 128	: unID = unID + 1
	hotspot widthDoor - xLatchPos, 0, -bottomHeightDoor - 1,			unID, heightLatch, 3		: unID = unID + 1
	hotspot widthDoor - xLatchPos, 0, heightLatch - bottomHeightDoor,	unID, heightLatch, 2		: unID = unID + 1
endif

if doorpullType_m > 0 then
	hotspot widthDoor,					0,	heightDoorpull - bottomHeightDoor, unID, xDoorpullPos, 1 + 128	: unID = unID + 1
	hotspot widthDoor + 1,				0,	heightDoorpull - bottomHeightDoor, unID, xDoorpullPos, 3		: unID = unID + 1
	hotspot widthDoor- xDoorpullPos,	0,	heightDoorpull - bottomHeightDoor, unID, xDoorpullPos, 2		: unID = unID + 1

	hotspot widthDoor - xDoorpullPos, 0, -bottomHeightDoor,					unID, heightDoorpull, 1 + 128	: unID = unID + 1
	hotspot widthDoor - xDoorpullPos, 0, -bottomHeightDoor - 1,				unID, heightDoorpull, 3			: unID = unID + 1
	hotspot widthDoor - xDoorpullPos, 0, heightDoorpull - bottomHeightDoor,	unID, heightDoorpull, 2			: unID = unID + 1
endif

muly -1

if bOutsideOpening then
	tempThk = thicknessDoor
else
	tempThk = thicknessPanel
endif

add widthDoor - xLatchPos, yy-tempThk, heightLatch - bottomHeightDoor
call "da_latchtyp" parameters	gs_latch_typ_m	= latchType_m,
							gs_latch_mat		= matHandle,
							gs_detlevel_3d_m	= gs_detlevel_3d_m,
							gs_cont_pen 		= gs_cont_pen,
							gs_door_thck		= thicknessDoor

del 1

add widthDoor - xDoorpullPos,	yy-tempThk,	heightDoorpull - bottomHeightDoor
call "fa_knobtyp" parameters	gs_knob_type_m	= doorpullType_m,
							gs_knob_mat			= matHandle,
							gs_detlevel_3d_m	= gs_detlevel_3d_m,
							gs_cont_pen 		= gs_cont_pen

addy thicknessDoor
muly -1
call "fa_knobtyp" parameters	gs_knob_type_m	= doorpullType_m,
							gs_knob_mat			= matHandle,
							gs_detlevel_3d_m	= gs_detlevel_3d_m,
							gs_cont_pen 		= gs_cont_pen
del 2 + 1

del 1

del 4

end

