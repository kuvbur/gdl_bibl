
! ==============================================================================
! showerCabin_m
! ==============================================================================


values "gs_detlevel_3D_m" 0,1,2

if GLOB_MODPAR_NAME = "halfDoorWidth" then
	doorWidth = 2*halfDoorWidth
	parameters doorWidth = 2*halfDoorWidth
else
	halfDoorWidth = doorWidth/2
	parameters halfDoorWidth = doorWidth/2
endif

if iCabinForm = CABINFORM_SQUAREDALCOVE | iCabinForm = CABINFORM_SQUAREDCORNER | iCabinForm = CABINFORM_SQUAREDPENINSULAR | iCabinForm = CABINFORM_SQUARE then			! Square
	if iDoorType <> DOORTYPE_CORNERSLIDING then
		sidePanelRatio = sidePanelWidthL / (sidePanelWidthL + sidePanelWidthR)
		if bSidePanelL & bSidePanelR & (GLOB_MODPAR_NAME = "A" | GLOB_MODPAR_NAME = "cabinSize" | GLOB_MODPAR_NAME = "doorType" | GLOB_MODPAR_NAME = "iDoorType") then
			sidePanelWidthL = (A - doorWidth) * sidePanelRatio
			sidePanelWidthR = A - doorWidth - sidePanelWidthL
			parameters sidePanelWidthL = sidePanelWidthL
			parameters sidePanelWidthR = sidePanelWidthR
		endif
		if bSidePanelL & not(bSidePanelR) then
			if GLOB_MODPAR_NAME = "sidePanelWidthL" then
				doorWidth = A - sidePanelWidthL
				parameters doorWidth = doorWidth
			else
				sidePanelWidthL = A - doorWidth
				parameters sidePanelWidthL = sidePanelWidthL
			endif
		endif
		if bSidePanelL & bSidePanelR then
			if (GLOB_MODPAR_NAME = "doorWidth" | GLOB_MODPAR_NAME = "halfDoorWidth") then
				sidePanelWidthL = sidePanelWidthL + (A - sidePanelWidthL - sidePanelWidthR) / 2 - doorWidth/2
				sidePanelWidthR = A - sidePanelWidthL - 2 * halfDoorWidth
				parameters sidePanelWidthL = sidePanelWidthL
				parameters sidePanelWidthR = sidePanelWidthR
			endif
			if GLOB_MODPAR_NAME = "sidePanelWidthL" | GLOB_MODPAR_NAME = "sidePanelWidthR" then
				doorWidth = A - sidePanelWidthL - sidePanelWidthR
				parameters doorWidth = doorWidth
			endif
		endif
	endif
endif
if iCabinForm = CABINFORM_ROUNDEDCORNER | iCabinForm = CABINFORM_CHAMFERCORNER | (iCabinForm = CABINFORM_SQUAREDCORNER & iDoorType = DOORTYPE_CORNERSLIDING) then			! Rounded & Chamfer Corner
	if GLOB_MODPAR_NAME = "A" | GLOB_MODPAR_NAME = "B" | GLOB_MODPAR_NAME = "cabinForm" | GLOB_MODPAR_NAME = "iCabinForm" | GLOB_MODPAR_NAME = "cabinSize"\
	 | GLOB_MODPAR_NAME = "doorWidth" | GLOB_MODPAR_NAME = "halfDoorWidth" | GLOB_MODPAR_NAME = "doorType" | GLOB_MODPAR_NAME = "iDoorType" then
		sidePanelWidthL = A - doorWidth*cos(45)
		sidePanelWidthR = sidePanelWidthL - A + B
		parameters sidePanelWidthL = sidePanelWidthL
		parameters sidePanelWidthR = sidePanelWidthR
	endif
	if GLOB_MODPAR_NAME = "sidePanelWidthL" then
		doorWidth = (A - sidePanelWidthL)/cos(45)
		sidePanelWidthR = sidePanelWidthL - A + B
		parameters doorWidth = doorWidth
		parameters sidePanelWidthR = sidePanelWidthR
	endif
	if iCabinForm = CABINFORM_ROUNDEDCORNER & (iDoorType = DOORTYPE_SLIDING | iDoorType = DOORTYPE_DOUBLESLIDING) then			! Rounded Corner
		if GLOB_MODPAR_NAME = "gs_open3D" then
			gs_open_3D = gs_open3D/((doorWidth*cos(45) - frameThk) * PI) * 180
			parameters gs_open_3D = gs_open_3D
		else
			gs_open3D = (doorWidth*cos(45) - frameThk) * PI * (gs_open_3D) / 180
			parameters gs_open3D = gs_open3D
		endif
	endif
endif
if iCabinForm = CABINFORM_ROUND then			! Round
	if iDoorType = DOORTYPE_SLIDING | iDoorType = DOORTYPE_DOUBLESLIDING then			! Rounded Corner
		if GLOB_MODPAR_NAME = "gs_open3D" then
			gs_open_3D = gs_open3D/((A/2 - frameThk) * PI) * 180
			parameters gs_open_3D = gs_open_3D
		else
			gs_open3D = (A/2 - frameThk) * PI * (gs_open_3D) / 180
			parameters gs_open3D = gs_open3D
		endif
	endif
endif

