
EPS						= 0.0001

verFrameHeight			= 0.024
verFrameWidth			= 0.024

maniHeadHeight 			= 0
inOutPipeOv				= 0

tankBoundingBoxWidth	= 0
tankBoundingBoxHeight	= 0

verFrameEnable			= iSolarObjectType = 2 or (bSupport and abs(ac_tilt_angle) > eps)

if iSolarObjectType = 1 then

	verFrameFrontOffset	= 0
	verFrameDist		= A - verFrameWidth
	verFrameNum			= 2 + int(verFrameDist)

	horFrameHeight		= 0.048
	horFrameWidth		= 0.0325
	horFrameLength		= verFrameDist + verFrameWidth

	panelWidth			= A
	panelHeight			= panelLength
	glassFrameHeight	= 0.074
	glassFrameProfilThk	= glassFrameHeight - horFrameHeight
	glassFrameWidth		= 0.07
	glassThk			= 0.005

	inOutPipeDia		= 0.027
	inOutPipeOv			= horFrameWidth

	mepPosY				= panelLength - 0.1
	mepPosZ				= verFrameHeight * verFrameEnable + horFrameHeight  / 2

	boxHeight_temp		= glassFrameHeight

	horLine1Start		= glassFrameWidth + 0.1
	horLineSpace1		= 0.12
	horLine2Start		= glassFrameWidth + 0.12
	horLineSpace2		= 0.12

	horLine1Start_2D		= (glassFrameWidth + 0.1)	* cos(ac_tilt_angle)
	horLineSpace1_2D		= 0.12	* cos(ac_tilt_angle)
	horLine2Start_2D		= (glassFrameWidth + 0.12)	* cos(ac_tilt_angle)
	horLineSpace2_2D		= 0.12	* cos(ac_tilt_angle)

endif

if iSolarObjectType = 2 then

	verFrameFrontOffset	= 0.05

	tubeDia				= 0.058
	tubeCapDia			= tubeDia + 0.005 * 2
	tubeCapLength		= 0.075
	sideTubeGap			= tubeGap

	maniHeadHeight		= 0.106

	maniHeadSideOffset	= (tubeDia + tubeGap) / 2

	verFrameDist		= A - maniHeadSideOffset * 2
	maniHeadLength		= (verFrameDist) + maniHeadSideOffset * 2
	verFrameNum			= max(2, 2 + int(verFrameDist))

	horFrameHeight		= 0.024
	horFrameWidth		= 0.024
	horFrameLength		= verFrameDist + verFrameWidth

	if bWaterTank then
		panelLength		= tubeLength + waterTankDia / 2 * ( 1 + tan ( ( 90 - ( ac_tilt_angle - baseSurfaceAngle ) ) / 2 ) ) + horFrameWidth + verFrameFrontOffset
	else
		panelLength		= tubeLength + maniFoldWidth + horFrameWidth + verFrameFrontOffset
	endif
	parameters panelLength = panelLength

	_waterTankDist		= panelLength - waterTankDia / 2 * tan ( ( 90 - ( ac_tilt_angle - baseSurfaceAngle ) ) / 2 )

	mepPosY				= panelLength - maniFoldWidth / 2 * not ( bWaterTank ) - waterTankDia / 2 * bWaterTank * tan ( ( 90 - ( ac_tilt_angle - baseSurfaceAngle ) ) / 2 )
	mepPosZ				= verFrameHeight * verFrameEnable + maniHeadHeight / 2 *not(bWaterTank) + waterTankDia / 2 * bWaterTank

	boxHeight_temp		= maniHeadHeight

endif

if iSolarObjectType = 3 then

	panelLength			= panelLength

	verFrameFrontOffset	= 0
	verFrameDist		= A - verFrameWidth
	verFrameNum			= 2 + int(verFrameDist)

	horFrameHeight		= 0.025*0
	horFrameWidth		= 0.025
	horFrameLength		= verFrameDist + verFrameWidth

	glassFrameHeight	= 0.074
	glassFrameProfilThk	= glassFrameHeight - horFrameHeight
	glassFrameWidth		= 0.024
	glassThk			= 0.005

	mepPosY				= panelLength - 0.1
	mepPosZ				= verFrameHeight * verFrameEnable + horFrameHeight  / 2

	boxHeight_temp		= panel_depth

	verLine1Start		= - A / 2 + glassFrameWidth + 0.0175
	verLineSpace1		= 0.2
	verLine2Start		= - A / 2 + glassFrameWidth + 0.0175 + 0.05
	verLineSpace2		= 0.2
	verLine3Start		= - A / 2 + glassFrameWidth + 0.0175 + 0.05 + 0.05
	verLineSpace3		= 0.2
	horLineStart		= glassFrameWidth + 0.0275
	horLineSpace1		= 0.2

endif

mountingThk			= 0.005
mountingWidth		= 0.1
legHeightFront		= 0.1
legProfilThk		= 0.005
legDist				= cos(ac_tilt_angle - baseSurfaceAngle) * (panelLength - verFrameWidth)
legHeightBack		= legHeightFront + sin(ac_tilt_angle - baseSurfaceAngle) * (panelLength - verFrameWidth)
crossFrameAngle		= atn((legHeightBack - legHeightFront * 2)  / (verFrameDist / (verFrameNum - 1)))
crossFrameLength	= (legHeightBack - legHeightFront * 2) / sin(crossFrameAngle)
sideFrameAngle		= ac_tilt_angle - baseSurfaceAngle
sideFrameLength		= panelLength

mepConnPosY_temp	= verFrameWidth / 2 + cos(ac_tilt_angle - baseSurfaceAngle) * (mepPosY - verFrameWidth / 2)\
					- sin(ac_tilt_angle - baseSurfaceAngle) * (mepPosZ - verFrameHeight / 2 * verFrameEnable)
mepConnPosZ_temp	= legHeightFront - verFrameHeight / 2 * verFrameEnable + cos(ac_tilt_angle - baseSurfaceAngle) * verFrameHeight / 2 * verFrameEnable\
					+ sin(ac_tilt_angle - baseSurfaceAngle) * (mepPosY - verFrameWidth / 2) + cos(ac_tilt_angle - baseSurfaceAngle) * (horFrameHeight / 2 * (iSolarObjectType = 1)\
					+ maniHeadHeight / 2 * (iSolarObjectType = 2)*not(bWaterTank)+ waterTankDia / 2 * (iSolarObjectType = 2)*bWaterTank)
mepConnAngle_temp	= (atn(mepConnPosZ_temp / mepConnPosY_temp) + 180) MOD 180
mepDist_temp		= sqr(mepConnPosZ_temp ** 2 + mepConnPosY_temp ** 2)
mepConnAngle		= mepConnAngle_temp + baseSurfaceAngle

if bSupport and abs(ac_tilt_angle) > eps then
	B				= cos(baseSurfaceAngle) * (legDist + verFrameWidth)

	topHotspotPosY_temp	= verFrameWidth / 2 + cos(ac_tilt_angle - baseSurfaceAngle) * (panelLength - verFrameWidth / 2)\
						- sin(ac_tilt_angle - baseSurfaceAngle) * (boxHeight_temp + verFrameHeight / 2 * verFrameEnable)
	topHotspotPosZ_temp	= legHeightFront - verFrameHeight / 2 * verFrameEnable + cos(ac_tilt_angle - baseSurfaceAngle) * verFrameHeight / 2 * verFrameEnable\
						+ sin(ac_tilt_angle - baseSurfaceAngle) * (panelLength - verFrameWidth / 2) + cos(ac_tilt_angle - baseSurfaceAngle) * boxHeight_temp

	topAngle_temp	= (atn(topHotspotPosZ_temp / topHotspotPosY_temp) + 180) MOD 180
	topDist_temp	= sqr(topHotspotPosZ_temp ** 2 + topHotspotPosY_temp ** 2)
	topAngle		= topAngle_temp + baseSurfaceAngle

	zzyzx			= sin(topAngle) * topDist_temp
	topHotspotPosY	= cos(topAngle) * topDist_temp

	hEditRad			= panelLength - verFrameHeight / 2 * verFrameEnable
	bottomHotspotPosY	= topHotspotPosY + sin(ac_tilt_angle) * (boxHeight_temp + verFrameHeight / 2 * verFrameEnable)
	bottomHotspotPosZ	= zzyzx - cos(ac_tilt_angle) * (boxHeight_temp + verFrameHeight / 2 * verFrameEnable)

	mepRePosY		= cos(mepConnAngle) * mepDist_temp
else
	B				= cos(ac_tilt_angle) * panelLength
	zzyzx			= sin(ac_tilt_angle) * panelLength + cos(ac_tilt_angle) * (boxHeight_temp + verFrameHeight * verFrameEnable)
	topHotspotPosY	= cos(ac_tilt_angle) * panelLength - sin(ac_tilt_angle) * (boxHeight_temp + verFrameHeight * verFrameEnable)

	hEditRad			= panelLength
	bottomHotspotPosY	= topHotspotPosY + sin(ac_tilt_angle) * (boxHeight_temp + verFrameHeight * verFrameEnable)
	bottomHotspotPosZ	= zzyzx - cos(ac_tilt_angle) * (boxHeight_temp + verFrameHeight * verFrameEnable)

	mepRePosY	= cos(ac_tilt_angle) * (mepPosY) - sin(ac_tilt_angle) * (mepPosZ)
endif

if iSolarObjectType = 2 then

	!--- Center of the watertank / manifold header
		if bSupport and abs(ac_tilt_angle)>eps then
			tankCenterY = mepRePosY
			tankCenterZ = sin(mepConnAngle) * mepDist_temp
		else
			tankCenterY = mepRePosY
			tankCenterZ = sin(ac_tilt_angle) * (mepPosY) + cos(ac_tilt_angle) * (mepPosZ)
		endif
	
		tankBoundingBoxWidth = not(bWaterTank) * maniFoldWidth + bWaterTank * waterTankDia
		tankBoundingBoxHeight = not(bWaterTank) * maniHeadHeight + bWaterTank * waterTankDia

endif

! --- zzyzx correction if watertank is on
if bWaterTank then
zzyzx = zzyzx + cos ( ac_tilt_angle ) * ( - boxHeight_temp + waterTankDia / 2) - waterTankDia / 2 * sin ( ac_tilt_angle ) * tan ( ( 90 - ac_tilt_angle ) / 2 ) + waterTankDia / 2
endif
! --- End of zzyzx correction if watertank is on

! Show MEP Connections -------------------------------------------------------------[

isavailable = 0
isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)
isMEPEnabled = (isavailable OR isInArchiCAD = 0)
if isMEPEnabled then
	bShowMEP = 1
	reqBMep = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showMepConnect", reqBMep)
	if success > 0 then
		bShowMEP = reqBMep
	endif
else
	bShowMEP = 0
endif

! Show MEP Connections -------------------------------------------------------------]
