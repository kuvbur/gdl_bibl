
! ==============================================================================
! prefabChimney_m
! ==============================================================================

pen gs_cont_pen
fill gs_fill_type

bChimney2D = 0
if ZZYZX > GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1] then
	bChimney2D = 1
endif
bTopCover2D = 0
if GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1] > max(zCornerPointsPos[1][1],zCornerPointsPos[1][2],zCornerPointsPos[2][1],zCornerPointsPos[2][2])\
	& ZZYZX > GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1] then
	bTopCover2D = 1
endif
bTopview2D = 0
if GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1] > ZZYZX then
	bTopview2D = 1
endif

unID = 200
if bChimney2D then
	hotspot2 flueRadius + diff - A/2, 0, unID : unID = unID + 1

	hotspot2	-A/2,   -b/2, unID	: unID = unID + 1
	hotspot2	-A/2,	 b/2, unID	: unID = unID + 1
	hotspot2	 A/2,	 b/2, unID	: unID = unID + 1
	hotspot2	 A/2,	-b/2, unID	: unID = unID + 1

	hotline2 	-A/2,   -b/2,  A/2,	-b/2, 11111
	hotline2	 A/2,	-b/2,  A/2,	 b/2, 22222
	hotline2	 A/2,	 b/2, -A/2,	 b/2, 33333
	hotline2	-A/2,	 b/2, -A/2,	-b/2, 44444

	add2 flueRadius + diff - A/2, 0

	if iFlueShape = FLUESHAPE_CIRCLE then
		poly2_b 4, 1 + 2 + 4 + 64, gs_fill_pen, gs_back_pen,
			0, 0, 900,
			flueRadius+_tubeThk, 360, 4000+1,
			0, 0, 900,
			flueRadius, 360, 4000+1
	endif

	put -flueRadius-diff,   -b/2 , 1,
		-flueRadius-diff+a, -b/2,  1,
		-flueRadius-diff+a,  b/2,  1,
		-flueRadius-diff,    b/2,  1,
		-flueRadius-diff,   -b/2, -1

	_centerPosX = 0

	gosub "getFluePoints1"

	if bDoubleFlue then
		if ventilation then
			put flueRadius+diff,       (b-0.1)/2,  1,
				flueRadius+diff+hole,  (b-0.1)/2,  1,
				flueRadius+diff+hole, -(b-0.1)/2,  1,
				flueRadius+diff,      -(b-0.1)/2,  1,
				flueRadius+diff,       (b-0.1)/2, -1

			_centerPosX = flueRadius+2*diff+hole+flueRadius2

			gosub "getFluePoints2"

			add2 _centerPosX, 0
		else
			_centerPosX = flueRadius+diff+flueRadius2+insulFullThk

			gosub "getFluePoints2"

			add2 _centerPosX, 0
		endif
		hotspot2 0, 0, unID	: unID = unID + 1

		if iFlueShape = FLUESHAPE_CIRCLE then
			poly2_b 4, 1 + 2 + 4 + 64, gs_fill_pen, gs_back_pen,
				0, 0, 900,
				flueRadius2+_tubeThk, 360, 4000+1,
				0, 0, 900,
				flueRadius2, 360, 4000+1
		endif

		del 1
	else
		if ventilation then
			put	 flueRadius+diff,      -(b-0.1)/2, 1,
				 flueRadius+diff+hole, -(b-0.1)/2, 1,
				 flueRadius+diff+hole,  (b-0.1)/2, 1,
				 flueRadius+diff,       (b-0.1)/2, 1,
				 flueRadius+diff,      -(b-0.1)/2, -1
		endif

		unID = unID + 1
	endif

	fill gs_fill_type
	poly2_b (NSP)/3, 1 + 2 + 4 + 64, gs_fill_pen, gs_back_pen,
		get(NSP)

	del 1
else
	unID = unID + 6
endif

if bTopCover & bTopCover2D then
	fill gs_top_cover_fill_type
	poly2_b 5 + 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, 1,
		 A/2 + bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, 1,
		 A/2 + bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, 1,
		-A/2 - bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, 1,
		-A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, -1,

		-A/2, -B/2, 1,
		 A/2, -B/2, 1,
		 A/2,  B/2, 1,
		-A/2,  B/2, 1,
		-A/2, -B/2, -1

	hotspot2 -A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 A/2 + bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 A/2 + bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 -A/2 - bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 -A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, unID	: unID = unID + 1
else
	unID = unID + 5
endif

if bTopview2D then
	poly2_b 5, 1, gs_fill_pen, gs_back_pen,
		-A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, 1,
		 A/2 + bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, 1,
		 A/2 + bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, 1,
		-A/2 - bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, 1,
		-A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, -1

	hotspot2 -A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 A/2 + bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 A/2 + bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 -A/2 - bTopCover * topCoverThk,  B/2 + bTopCover * topCoverThk, unID	: unID = unID + 1
	hotspot2 -A/2 - bTopCover * topCoverThk, -B/2 - bTopCover * topCoverThk, unID	: unID = unID + 1

	_centerPosX = flueRadius + diff - A/2

	if iFlueShape = FLUESHAPE_CIRCLE then
		circle2 _centerPosX, 0, flueRadius
	else
		rect2	_centerPosX - flueWidth/2,	-flueDepth/2, \
				_centerPosX + flueWidth/2,	 flueDepth/2
	endif

	hotspot2 _centerPosX, 0, unID	: unID = unID + 1

	if bShowCap then
		circle2 flueRadius + diff - A/2, 0, flueRadius+_tubeThk
		circle2 flueRadius + diff - A/2, 0, flueRadius+insulFullThk
	else
		if bTopCover then
			poly2_b 5, 1, gs_fill_pen, gs_back_pen,
				-A/2, -B/2, 1,
				 A/2, -B/2, 1,
				 A/2,  B/2, 1,
				-A/2,  B/2, 1,
				-A/2, -B/2, -1
		endif
	endif

	if bDoubleFlue then
		if ventilation then
			poly2_b 5, 1, gs_fill_pen, gs_back_pen,
				flueRadius + diff - A/2 + flueRadius+diff,       (b-0.1)/2,  1,
				flueRadius + diff - A/2 + flueRadius+diff+hole,  (b-0.1)/2,  1,
				flueRadius + diff - A/2 + flueRadius+diff+hole, -(b-0.1)/2,  1,
				flueRadius + diff - A/2 + flueRadius+diff,      -(b-0.1)/2,  1,
				flueRadius + diff - A/2 + flueRadius+diff,       (b-0.1)/2, -1
			add2 flueRadius + diff - A/2 + flueRadius+2*diff+hole+flueRadius2, 0
		else
			add2 flueRadius + diff - A/2 + flueRadius+diff+flueRadius2+insulFullThk, 0
		endif

		_centerPosX = 0

		if iFlueShape = FLUESHAPE_CIRCLE then
			circle2 _centerPosX, 0, flueRadius2
		else
			rect2	_centerPosX - flueWidth2/2,	-flueDepth2/2, \
					_centerPosX + flueWidth2/2,	 flueDepth2/2
		endif

		hotspot2 _centerPosX, 0, unID	: unID = unID + 1

		if bShowCap then
			circle2 0, 0, flueRadius2+_tubeThk
			circle2 0, 0, flueRadius2+insulFullThk
		endif
		del 1
	else
		if ventilation then
			poly2_b 5, 1, gs_fill_pen, gs_back_pen,
				flueRadius + diff - A/2 + flueRadius+diff+minWallThk,      -(b-0.1)/2, 1,
				flueRadius + diff - A/2 + flueRadius+diff+minWallThk+hole, -(b-0.1)/2, 1,
				flueRadius + diff - A/2 + flueRadius+diff+minWallThk+hole,  (b-0.1)/2, 1,
				flueRadius + diff - A/2 + flueRadius+diff+minWallThk,       (b-0.1)/2, 1,
				flueRadius + diff - A/2 + flueRadius+diff+minWallThk,      -(b-0.1)/2, -1
		endif
	endif
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"getFluePoints1":
! ------------------------------------------------------------------------------
! input:
! _centerPosX: Center point of the current Flue
! ------------------------------------------------------------------------------

	if iFlueShape = FLUESHAPE_CIRCLE then
		put	_centerPosX, 0, 900,
			flueRadius+insulFullThk, 360, 4001

		if iSymbolType = SYMBOL_STYLE2 then
			line2	_centerPosX - flueRadius * cos(45),	-flueRadius * sin(45), \
					_centerPosX + flueRadius * cos(45),	 flueRadius * sin(45)
			line2	_centerPosX - flueRadius * cos(45),	 flueRadius * sin(45), \
					_centerPosX + flueRadius * cos(45),	-flueRadius * sin(45)
		endif
	else
		put	_centerPosX - flueWidth/2,	-flueDepth/2,	1,
			_centerPosX + flueWidth/2,	-flueDepth/2,	1,
			_centerPosX + flueWidth/2,	 flueDepth/2,	1,
			_centerPosX - flueWidth/2,	 flueDepth/2,	1,
			_centerPosX - flueWidth/2,	-flueDepth/2,	-1

		if iSymbolType = SYMBOL_STYLE2 then
			line2	_centerPosX - flueWidth/2,	-flueDepth/2, \
					_centerPosX + flueWidth/2,	 flueDepth/2
			line2	_centerPosX - flueWidth/2,	 flueDepth/2, \
					_centerPosX + flueWidth/2,	-flueDepth/2
		endif
	endif

return


! ==============================================================================
"getFluePoints2":
! ------------------------------------------------------------------------------
! input:
! _centerPosX: Center point of the current Flue
! ------------------------------------------------------------------------------

	if iFlueShape = FLUESHAPE_CIRCLE then
		put	_centerPosX, 0, 900,
			flueRadius2+insulFullThk, 360, 4001

		if iSymbolType = SYMBOL_STYLE2 then
			line2	_centerPosX - flueRadius2 * cos(45),	-flueRadius2 * sin(45), \
					_centerPosX + flueRadius2 * cos(45),	 flueRadius2 * sin(45)
			line2	_centerPosX - flueRadius2 * cos(45),	 flueRadius2 * sin(45), \
					_centerPosX + flueRadius2 * cos(45),	-flueRadius2 * sin(45)
		endif
	else
		put	_centerPosX - flueWidth2/2,	-flueDepth2/2,	1,
			_centerPosX + flueWidth2/2,	-flueDepth2/2,	1,
			_centerPosX + flueWidth2/2,	 flueDepth2/2,	1,
			_centerPosX - flueWidth2/2,	 flueDepth2/2,	1,
			_centerPosX - flueWidth2/2,	-flueDepth2/2,	-1

		if iSymbolType = SYMBOL_STYLE2 then
			line2	_centerPosX - flueWidth2/2,	-flueDepth2/2, \
					_centerPosX + flueWidth2/2,	 flueDepth2/2
			line2	_centerPosX - flueWidth2/2,	 flueDepth2/2, \
					_centerPosX + flueWidth2/2,	-flueDepth2/2
		endif
	endif

return

