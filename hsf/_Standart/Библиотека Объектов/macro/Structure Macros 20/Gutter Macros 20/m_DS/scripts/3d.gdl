success = LIBRARYGLOBAL ("Libraryflobals-as", "gs_detlevel_3D_m", gs_detlevel_3D_m)

! *****************************************************************************
! DOWNSPOUT 3D
! *****************************************************************************

if gs_detlevel_3D_m=1 or (GLOB_CONTEXT>20 and GLOB_CONTEXT<40) then gs_resol=6	! Simple
if gs_detlevel_3D_m=0 then END	!!Off

resol gs_resol
pen gs_cont_pen
material gs_gutter_ds_mat

if gs_shadow=0 then SHADOW OFF
if gs_shadow=1 then SHADOW ON

rotx gs_gutter_ds_ang

if gs_gutter_ds_profile_num = 1 then 	!! round profile
	sizeA = gs_gutter_dd
	sizeB = gs_gutter_dd
else
	sizeA = gs_gutter_aa
	sizeB = gs_gutter_bb
endif

addy - (dis_wall - sizeB)

! -----------------------------------------------------------------------------
! Downspout 3D body
! -----------------------------------------------------------------------------
call "m_DSProfiles" parameters 	gs_gutter_ds_profile_num	= gs_gutter_ds_profile_num,
								gs_mask						= 1,
								gs_gutter_aa				= gs_gutter_aa,
								gs_gutter_bb				= gs_gutter_bb,
								gs_gutter_dd				= gs_gutter_dd


! ---  Cutting at toP ---
addz gs_gutter_ds_lgth
if abs(topCut) <= EPS then
	cutplane gs_gutter_topangle
	lengthAddBottom = ABS(sizeB * TAN(gs_gutter_bottomangle))
	LengthAddTop = ABS(sizeB * TAN(gs_gutter_topangle))
	temp_Length = lengthAddTop + gs_gutter_ds_lgth
else
	rotz -90
	cutplane topCut
	del 1
	lengthAddBottom = 0
	lengthAddTop = ABS(sizeA * TAN(topCut))
	temp_Length = lengthAddTop + gs_gutter_ds_lgth
endif

if abs(lengthAddBottom)<EPS then lengthAddBottom = 0.0001
if abs(temp_Length)<EPS then temp_Length = 0.0001

del 1

! ---  Cutting at bottom ---
cutplane (180 + gs_gutter_bottomangle) % 360
if NSP>EPS and nsp/3>1 then

	tube nsp/3, 4, 16+32,
		get(nsp),
		0,	0,		temp_Length + 1,		0,
		0,	0,		temp_Length,			0,
		0,	0,		-lengthAddBottom,		0,
		0,	0,		-lengthAddBottom - 1,	0
endif

cutend
cutend

! -----------------------------------------------------------------------------
! 3D hotspots
! -----------------------------------------------------------------------------

unID=gs_unID

if gs_HtpsMacro=0 then 													! not used in Downspout Complex
	if gs_gutter_ds_profile_num=1 then									! round profile
		AA = 0			 	: BB = 0				: gosub "length_settings"
		AA = 0			 	: BB = gs_gutter_dd		: tempdir = -1 :gosub "width_length"
		AA = 0			 	: BB =  -gs_gutter_dd	: tempdir = 1 :	gosub "width_length"
		AA = gs_gutter_dd 	: BB = 0				: tempdir = 0 :	gosub "width_length"
		AA = - gs_gutter_dd : BB = 0				: tempdir = 0 :	gosub "width_length"
		AA = - gs_gutter_dd	: BB = gs_gutter_dd		: gosub "hanger_offset"
		AA = gs_gutter_dd	: BB = gs_gutter_dd		: gosub "hanger_offset"
		AA = gs_gutter_dd	: BB = -dis_wall		: gosub "wall_distance"
	else																! not round profile
		AA = 0			 	: BB = 0				: gosub "length_settings"
		AA = gs_gutter_aa 	: BB = gs_gutter_bb 	: tempdir = -1 :gosub "width_length"
		AA = gs_gutter_aa 	: BB = - gs_gutter_bb 	: tempdir = 1 :	gosub "width_length"
		AA = - gs_gutter_aa : BB = gs_gutter_bb 	: tempdir = -1 :gosub "width_length"
		AA = - gs_gutter_aa : BB = - gs_gutter_bb 	: tempdir = 1 :	gosub "width_length"
		AA = - gs_gutter_aa : BB = gs_gutter_bb		: gosub "hanger_offset"
		AA = gs_gutter_aa 	: BB = gs_gutter_bb		: gosub "hanger_offset"
		AA = gs_gutter_aa	: BB = -dis_wall		: gosub "wall_distance"
	endif
endif
del 1

! -----------------------------------------------------------------------------
! DS hangers 3D / hotspots
! -----------------------------------------------------------------------------
if gs_gutter_ds_h = 1 then
	temp_tanang = tan(gs_gutter_ds_ang)
	temp_cosang = cos(gs_gutter_ds_ang)
	temp_sinang = sin(gs_gutter_ds_ang)
	
	IF gs_gutter_ds_h_positioning = HANGER_UNIFORM THEN
		for i=1 to INT((gs_gutter_ds_lgth - gs_gutter_ds_h_off - sthw)/gs_gutter_ds_h_dist)+1
	
			! --- Hanger position ---
!			if gs_HtpsMacro=0 then 											! not used in Downspout Complex
!				add 0,-gs_gutter_bb-sth - (dis_wall - gs_gutter_bb),0
!	
!				HOTSPOT 0,	0,	0,								unID,	gs_gutter_ds_h_pos[i],	1+128	:unID=unID+1
!				HOTSPOT 0,	0,	-1,								unID,	gs_gutter_ds_h_pos[i],	3		:unID=unID+1
!				HOTSPOT 0,	0,	gs_gutter_ds_h_pos[i]+sthw/2,	unID,	gs_gutter_ds_h_pos[i],	2		:unID=unID+1
!	
!				del 1
!			endif
	
			temp_diswall = gs_gutter_ds_h_pos[i] * temp_tanang + dis_wall / temp_cosang
			add 0, temp_diswall - dis_wall, gs_gutter_ds_h_pos[i]
			
			call "m_DSHanger" parameters	gs_gutter_ds_profile_num	= gs_gutter_ds_profile_num,
											gs_gutter_ds_h_mat			= gs_gutter_ds_h_mat,
											gs_detlevel_3D_m			= gs_detlevel_3D_m,
											gs_resol					= gs_resol,
											gs_shadow					= gs_shadow,
											gs_cont_pen					= gs_cont_pen,
											gs_gutter_aa				= gs_gutter_aa,
											gs_gutter_bb				= gs_gutter_bb,
											gs_gutter_dd				= gs_gutter_dd,
											dis_wall					= temp_diswall, gs_gutter_type=gs_gutter_type
			del 1
		next i
	else
		for i=1 to hangeri
	
			! --- Hanger position ---
!			if gs_HtpsMacro=0 then 											! not used in Downspout Complex
!				add 0,-gs_gutter_bb-sth - (dis_wall - gs_gutter_bb),0
!	
!				HOTSPOT 0,	0,	0,								unID,	gs_gutter_ds_h_pos[i],	1+128	:unID=unID+1
!				HOTSPOT 0,	0,	-1,								unID,	gs_gutter_ds_h_pos[i],	3		:unID=unID+1
!				HOTSPOT 0,	0,	gs_gutter_ds_h_pos[i]+sthw/2,	unID,	gs_gutter_ds_h_pos[i],	2		:unID=unID+1
!	
!				del 1
!			endif
	
			temp_diswall = gs_gutter_ds_h_pos[i] * temp_tanang + dis_wall / temp_cosang
			add 0, temp_diswall - dis_wall, gs_gutter_ds_h_pos[i]

			call "m_DSHanger" parameters 	gs_gutter_ds_profile_num	= gs_gutter_ds_profile_num,
											gs_gutter_ds_h_mat			= gs_gutter_ds_h_mat,
											gs_detlevel_3D_m			= gs_detlevel_3D_m,
											gs_resol					= gs_resol,
											gs_shadow					= gs_shadow,
											gs_cont_pen					= gs_cont_pen,
											gs_gutter_aa				= gs_gutter_aa,
											gs_gutter_bb				= gs_gutter_bb,
											gs_gutter_dd				= gs_gutter_dd,
											dis_wall					= temp_diswall, gs_gutter_type=gs_gutter_type

			del 1
		next i
	endif
endif

del 1

end
! =============================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
! =============================================================================

! -----------------------------------------------------------------------------
"length_settings":
! -----------------------------------------------------------------------------
HOTSPOT AA,		BB,		0,						unID,	gs_gutter_ds_lgth,	1+256	:unID=unID+1
HOTSPOT AA,		BB,		gs_gutter_ds_lgth-100,	unID,	gs_gutter_ds_lgth,	3		:unID=unID+1
HOTSPOT AA,		BB,		gs_gutter_ds_lgth,		unID,	gs_gutter_ds_lgth,	2		:unID=unID+1
return

! -----------------------------------------------------------------------------
"width_length":
! -----------------------------------------------------------------------------
HOTSPOT AA,		BB,		tempdir * lengthAddBottom,					unID	:unID=unID+1
HOTSPOT AA,		BB,		gs_gutter_ds_lgth + tempdir * lengthAddTop,	unID	:unID=unID+1
return

! -----------------------------------------------------------------------------
"hanger_offset":
! -----------------------------------------------------------------------------
if gs_gutter_ds_h = 1 then
	HOTSPOT AA,		BB,	0,						unID,	gs_gutter_ds_h_off, 1+128	:unID=unID+1
	HOTSPOT AA,		BB,	-1,						unID,	gs_gutter_ds_h_off, 3		:unID=unID+1
	HOTSPOT AA,		BB,	gs_gutter_ds_h_pos[1],	unID,	gs_gutter_ds_h_off, 2		:unID=unID+1
endif
return

! -----------------------------------------------------------------------------
"wall_distance":		!3D Hotsports for Dis. Between Downspout Axes and Wall
! -----------------------------------------------------------------------------
	!new editable hotspots, using fix unID because of migration
	hotspot AA,		-BB,	0,	20001, dis_wall, 1+128
	hotspot AA,		0,		0,	20002, dis_wall, 2
	hotspot AA,		BB + 1,	0,	20003, dis_wall, 3

	hotspot -AA,	-BB,	0,	20004, dis_wall, 1+128
	hotspot -AA,	0,		0,	20005, dis_wall, 2
	hotspot -AA,	BB + 1,	0,	20006, dis_wall, 3

	hotspot AA,		-BB,	gs_gutter_ds_lgth,	20007, dis_wall, 1+128
	hotspot AA,		0,		gs_gutter_ds_lgth,	20008, dis_wall, 2
	hotspot AA,		BB + 1,	gs_gutter_ds_lgth,	20009, dis_wall, 3

	hotspot -AA,	-BB,	gs_gutter_ds_lgth,	20010, dis_wall, 1+128
	hotspot -AA,	0,		gs_gutter_ds_lgth,	20011, dis_wall, 2
	hotspot -AA,	BB + 1,	gs_gutter_ds_lgth,	20012, dis_wall, 3
return

