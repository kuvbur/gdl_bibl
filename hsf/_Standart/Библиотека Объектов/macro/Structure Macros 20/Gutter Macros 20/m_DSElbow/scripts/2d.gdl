
pen gs_cont_pen
line_type gs_line_type
fill gs_fill_type

! -----------------------------------------------------------------------------
! ELBOW 2D
! -----------------------------------------------------------------------------

if gs_gutter_ds_profile_num = 1 then
	add2 0, - (dis_wall - gs_gutter_dd)
else
	add2 0, - (dis_wall - gs_gutter_bb)
endif

if gs_gutter_ds_profile_num=1 then													! round profile
	temp_aa=gs_gutter_dd
else
	temp_aa=gs_gutter_aa
endif

rot2 180
call "m_DSProfiles" parameters 	gs_gutter_ds_profile_num	= gs_gutter_ds_profile_num,
								gs_mask						= 0,
								gs_gutter_aa				= gs_gutter_aa,
								gs_gutter_bb				= gs_gutter_bb,
								gs_gutter_dd				= gs_gutter_dd

add2 0,	gs_gutter_elb_l_lgth*sin(gs_gutter_elb_l_ang)								! ELBOW 2D - Lower profile
	if ABS( ABS(gs_gutter_elb_l_ang)-90) < EPS then
		line2 -gs_gutter_aa, 0, gs_gutter_aa, 0
	else
		mul2 1,cos(gs_gutter_elb_l_ang)
			poly2_b nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen,
				use (nsp)
		del 1
	endif
del 1

if not(ABS(gs_gutter_elb_l_ang) < EPS) then											! ELBOW 2D - Lower body
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		 temp_aa,		 gs_gutter_elb_l_lgth*sin(gs_gutter_elb_l_ang),	 1,
		 temp_aa,		 0,												 0,
		-temp_aa,		 0,												 1,
		-temp_aa,		 gs_gutter_elb_l_lgth*sin(gs_gutter_elb_l_ang),	 0
endif

if ABS(gs_gutter_elb_u_ang + gs_gutter_elb_l_ang) - 180 < EPS then					! ELBOW 2D - Middle profile
	mul2 1,cos((gs_gutter_elb_l_ang-gs_gutter_elb_u_ang)/2)/(cos((gs_gutter_elb_l_ang+gs_gutter_elb_u_ang)/2))
		poly2_b nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen,	use (nsp)
	del 1
endif

if not(ABS(gs_gutter_elb_u_ang) < EPS) then											! ELBOW 2D - Lower body
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		 temp_aa,		 gs_gutter_elb_u_lgth*sin(gs_gutter_elb_u_ang),	 1,
		 temp_aa,		 0,												 0,
		-temp_aa,		 0,												 1,
		-temp_aa,		 gs_gutter_elb_u_lgth*sin(gs_gutter_elb_u_ang),	 0
endif

add2 0,	gs_gutter_elb_u_lgth*sin(gs_gutter_elb_u_ang)								! ELBOW 2D - Upper profile
	if ABS( ABS(gs_gutter_elb_u_ang)-90) < EPS then
		line2 -gs_gutter_aa, 0, gs_gutter_aa, 0
	else
		mul2 1,cos(gs_gutter_elb_u_ang)
			poly2_b nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen, use (nsp)
		del 1
	endif
del 1

gosub "flush_buffer"


! -----------------------------------------------------------------------------
! HOTSPOTS
! -----------------------------------------------------------------------------

unID = gs_unID

if gs_gutter_ds_profile_num=1 then													! ELBOW 2D HOTSPOTs - round profile

	add2 0,	gs_gutter_elb_u_lgth*sin(gs_gutter_elb_u_ang)							! ELBOW 2D HOTSPOTs - Upper, round profile
		if ABS(gs_gutter_elb_u_ang - 90) < EPS then
				gosub "htsp_round_prof4"
		else
			mul2 1,cos(gs_gutter_elb_u_ang)
				gosub "htsp_round_prof1"
			del 1
		endif
	del 1

	add2 0,	gs_gutter_elb_l_lgth*sin(gs_gutter_elb_l_ang)							! ELBOW 2D HOTSPOTs - Lower, round profile
		if ABS(gs_gutter_elb_l_ang - 90) < EPS then
			gosub "htsp_round_prof4"
		else
			mul2 1,cos(gs_gutter_elb_l_ang)
				if gs_gutter_elb_u_ang > 0 then
					gosub "htsp_round_prof2"
				else
					gosub "htsp_round_prof3"
				endif
			del 1
		endif
	del 1

	gosub "htsp_dis_wall"

else																				! ELBOW 2D HOTSPOTs - not_round profile

	add2 0,	gs_gutter_elb_u_lgth*sin(gs_gutter_elb_u_ang)							! ELBOW 2D HOTSPOTs - Upper, not_round profile
		if ABS(gs_gutter_elb_u_ang - 90) < EPS then
			mul2 1,cos(gs_gutter_elb_u_ang)
				gosub "htsp_round_prof4"
			del 1
		else
			gosub "htsp_ntround_prof1"
		endif
	del 1

	add2 0,	gs_gutter_elb_l_lgth*sin(gs_gutter_elb_l_ang)							! ELBOW 2D HOTSPOTs - Lower, not_round profile
		if ABS(gs_gutter_elb_l_ang - 90) < EPS then
			mul2 1,cos(gs_gutter_elb_l_ang)
				gosub "htsp_round_prof4"
			del 1
		else
			mul2 1,cos(gs_gutter_elb_l_ang)
				if gs_gutter_elb_u_ang > 0 then 
					gosub "htsp_ntround_prof2"
				else
					gosub "htsp_ntround_prof3"
				endif
			del 1
		endif
	del 1

	gosub "htsp_dis_wall"

endif
																					! Edit the lower part hor. overhang parameter
hotspot2 0,		0, 								unID,	gs_gutter_elb_hor_overhang, 1, gs_gutter_elb_l_lgth :unID = unID + 1
hotspot2 0, 	gs_gutter_elb_hor_overhang, 	unID,	gs_gutter_elb_hor_overhang, 2, gs_gutter_elb_l_lgth :unID = unID + 1
hotspot2 0, 	-1, 							unID,	gs_gutter_elb_hor_overhang, 3, gs_gutter_elb_l_lgth :unID = unID + 1

del 1
END
! =============================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
! =============================================================================

! -----------------------------------------------------------------------------
"flush_buffer":													!flush buffer
! -----------------------------------------------------------------------------
	for i=1 to nsp
		temp = get(1)
	next i
return

! -----------------------------------------------------------------------------
"htsp_round_prof1":								! round profile 2D hotspots 1
! -----------------------------------------------------------------------------
	HOTSPOT2  0,	 0,		unID:	unID=unID+1
	HOTSPOT2  0,	 gs_gutter_dd,	unID:	unID=unID+1
	HOTSPOT2  0,	-gs_gutter_dd,	unID:	unID=unID+1
	HOTSPOT2  gs_gutter_dd,	 0,		unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_dd,	 0,		unID:	unID=unID+1

	unID_ = unID
	unID = 20000

	FOR kk=1 to 4
		!!! *** RADIUS
		HOTSPOT2 0, 0, unID, gs_gutter_dd, 1+128, gs_gutter_dd2 : unID=unID+1
		HOTSPOT2 -1, 0, unID, gs_gutter_dd, 3, gs_gutter_dd2 : unID=unID+1
		HOTSPOT2 gs_gutter_dd, 0, unID, gs_gutter_dd, 2, gs_gutter_dd2 : unID=unID+1
		ROT2 90
		NEXT kk
	DEL 4

	unID = unID_
return

! -----------------------------------------------------------------------------
"htsp_round_prof2":								! round profile 2D hotspots 2
! -----------------------------------------------------------------------------
	HOTSPOT2  0,	-gs_gutter_dd,	unID:	unID=unID+1
	HOTSPOT2  gs_gutter_dd,	 0,		unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_dd,	 0,		unID:	unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_round_prof3":								! round profile 2D hotspots 3
! -----------------------------------------------------------------------------
	HOTSPOT2  0,	 gs_gutter_dd,	unID:	unID=unID+1
	HOTSPOT2  gs_gutter_dd,	 0,		unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_dd,	 0,		unID:	unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_ntround_prof1":						!not-round profile 2D hotspots 1
! -----------------------------------------------------------------------------
	HOTSPOT2  0,	 0,		unID:	unID=unID+1
	HOTSPOT2  gs_gutter_aa,	 gs_gutter_bb,	unID:	unID=unID+1
	HOTSPOT2  gs_gutter_aa,	-gs_gutter_bb,	unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_aa,	 gs_gutter_bb,	unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_aa,	-gs_gutter_bb,	unID:	unID=unID+1

	unID_ = unID
	unID = 40000

	!!! *** Width
	ADD2 0,gs_gutter_bb
		HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 -1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1

		HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 -gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1
	DEL 1
	ADD2 0,-gs_gutter_bb
		HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 -1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1

		HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
		HOTSPOT2 -gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1
	DEL 1

	!!! *** Length
	ADD2 gs_gutter_aa,0
		HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, -1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1

		HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, 1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, -gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1
	DEL 1
	ADD2 -gs_gutter_aa,0
		HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, -1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1

		HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, 1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
		HOTSPOT2 0, -gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1
	DEL 1

	unID = unID_
return

! -----------------------------------------------------------------------------
"htsp_ntround_prof2":						!not-round profile 2D hotspots 2
! -----------------------------------------------------------------------------
	HOTSPOT2  0,	 0,		unID:	unID=unID+1
	HOTSPOT2  gs_gutter_aa,	-gs_gutter_bb,	unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_aa,	-gs_gutter_bb,	unID:	unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_ntround_prof3":						!not-round profile 2D hotspots 3
! -----------------------------------------------------------------------------
	HOTSPOT2  0,	 0,		unID:	unID=unID+1
	HOTSPOT2  gs_gutter_aa,	 gs_gutter_bb,	unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_aa,	 gs_gutter_bb,	unID:	unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_round_prof4":								! round profile 2D hotspots 4
! -----------------------------------------------------------------------------
	HOTSPOT2  0,	 0,		unID:	unID=unID+1
	HOTSPOT2  gs_gutter_aa,	 0,		unID:	unID=unID+1
	HOTSPOT2 -gs_gutter_aa,	 0,		unID:	unID=unID+1

return

! -----------------------------------------------------------------------------
"htsp_dis_wall":								! Distance Between Downspout Axes and Wall
! -----------------------------------------------------------------------------
	add2 0, - dis_wall
	HOTSPOT2  0,	 0,	 		unID,	dis_wall, 1+128, 	dis_wall	:	unID=unID+1
	HOTSPOT2  0,	 dis_wall,	unID,	dis_wall, 2, 		dis_wall	:	unID=unID+1
	HOTSPOT2  0,	 -1,		unID,	dis_wall, 3, 		dis_wall	:	unID=unID+1

	del 1
return

