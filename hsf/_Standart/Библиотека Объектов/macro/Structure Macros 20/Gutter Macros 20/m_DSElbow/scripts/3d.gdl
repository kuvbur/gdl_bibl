
if gs_detlevel_3D_m=1 or (GLOB_CONTEXT>20 and GLOB_CONTEXT<40) then gs_resol=6		! Simple
if gs_detlevel_3D_m=0 then END														! Off

resol gs_resol
pen gs_cont_pen
material gs_gutter_ds_mat

if gs_shadow=0 then SHADOW OFF
if gs_shadow=1 then SHADOW ON

if gs_gutter_ds_profile_num = 1 then
	addy - (dis_wall - gs_gutter_dd)
else
	addy - (dis_wall - gs_gutter_bb)
endif

rotz 180
call "m_DSProfiles" parameters 	gs_gutter_ds_profile_num	= gs_gutter_ds_profile_num,
								gs_mask						= 1,
								gs_gutter_aa				= gs_gutter_aa,
								gs_gutter_bb				= gs_gutter_bb,
								gs_gutter_dd				= gs_gutter_dd

if gs_gutter_ds_profile_num=1 then
	sizeB = gs_gutter_dd
else
	sizeB = gs_gutter_bb
endif

ulength = gs_gutter_elb_u_lgth * COS(gs_gutter_elb_u_ang)
llength = gs_gutter_elb_l_lgth * COS(gs_gutter_elb_l_ang)
lengthAddTop = ABS(sizeB * TAN(gs_gutter_topangle))

addz llength + sizeB * sin (gs_gutter_elb_l_ang)
addz ulength

cutplane gs_gutter_topangle
del 1

tube nsp/3,5,4+8+16+32,
	get(nsp),
	0,	 2*(gs_gutter_elb_u_lgth * SIN(gs_gutter_elb_u_ang)),	 ulength + lengthAddTop + 1,	0,
	0,	 gs_gutter_elb_u_lgth * SIN(gs_gutter_elb_u_ang)	,	 ulength + lengthAddTop,		0,
	0,	 0													,	 0,				0,
	0,	 gs_gutter_elb_l_lgth * SIN(gs_gutter_elb_l_ang)	,	-llength,		0,
	0,	 2*(gs_gutter_elb_l_lgth * SIN(gs_gutter_elb_l_ang)),	 -2 * llength,	0

cutend


! -----------------------------------------------------------------------------
! 3D HOTSPOTs
! -----------------------------------------------------------------------------
unID=gs_unID

if gs_gutter_ds_profile_num=1 then												! round profile
	rotX -gs_gutter_elb_u_ang
													! HOTSPOTs for Upper_Part_Length setting
	XX= 0			 : YY= 0			 									: gosub "htsp_upper_length"
	
	if gs_HtpsMacro=0 then												! not used in Downspout Complex
		XX= 0			 : YY= gs_gutter_dd	 	: gosub "htsp_upper_round"
		XX= 0			 : YY= -gs_gutter_dd	: gosub "htsp_upper_round"
		XX= gs_gutter_dd : YY= 0			 	: gosub "htsp_upper_round"
		XX=-gs_gutter_dd : YY= 0			 	: gosub "htsp_upper_round"
		XX=-gs_gutter_dd : YY= -dis_wall  		: gosub "htsp_wall_dist"
	endif
	del 1

	rotX (gs_gutter_elb_l_ang-gs_gutter_elb_u_ang)/2							! HOTSPOTs at middle prt of the object
	mulY 1/(COS((gs_gutter_elb_l_ang+gs_gutter_elb_u_ang)/2))
	HOTSPOT  0,				 0,				0,	unID : unID=unID+1
	del 2
																				! HOTSPOTs for Upper_Part_Length setting
	XX= 0			 : YY= 0			 : gosub "htsp_lower_length"

	rotX 180+gs_gutter_elb_l_ang
	XX= 0			 : YY= gs_gutter_dd	 : gosub "htsp_lower_round"
	XX= 0			 : YY=-gs_gutter_dd	 : gosub "htsp_lower_round"
	XX= gs_gutter_dd : YY= 0			 : gosub "htsp_lower_round"
	XX=-gs_gutter_dd : YY= 0			 : gosub "htsp_lower_round"
	del 1
else																			! not round profile
	rotX -gs_gutter_elb_u_ang													! HOTSPOTs for Upper_Part_Length setting
	
	XX= 0 : YY= 0 															: gosub "htsp_upper_length"

	if gs_HtpsMacro=0 then													! not used in Downspout Complex
		XX= gs_gutter_aa : YY= gs_gutter_bb	: gosub "htsp_upper_rect"
		XX= gs_gutter_aa : YY=-gs_gutter_bb : gosub "htsp_upper_rect"
		XX=-gs_gutter_aa : YY= gs_gutter_bb : gosub "htsp_upper_rect"
		XX=-gs_gutter_aa : YY=-gs_gutter_bb : gosub "htsp_upper_rect"
		XX=-gs_gutter_aa : YY= -dis_wall 	: gosub "htsp_wall_dist"
	endif
	del 1

	rotX (gs_gutter_elb_l_ang-gs_gutter_elb_u_ang)/2							! HOTSPOTs at middle prt of the object
	mulY 1/(COS((3*gs_gutter_elb_u_ang-gs_gutter_elb_l_ang)/2))
	HOTSPOT  0,	 0,	0,	unID : unID=unID+1
	del 2

	XX= 0 : YY= 0 		: gosub "htsp_lower_length"

	rotX 180+gs_gutter_elb_l_ang												! HOTSPOTs for Upper_Part_Length setting
	XX= gs_gutter_aa 	: YY= gs_gutter_bb : gosub "htsp_lower_rect"
	XX= gs_gutter_aa 	: YY=-gs_gutter_bb : gosub "htsp_lower_rect"
	XX=-gs_gutter_aa 	: YY= gs_gutter_bb : gosub "htsp_lower_rect"
	XX=-gs_gutter_aa 	: YY=-gs_gutter_bb : gosub "htsp_lower_rect"
	del 1
endif

if gs_HtpsMacro=0 then															! not used in Downspout Complex
! --- HOTSPOTs for Upper_Ang set ---
		HOTSPOT  0,	0,													 0,													unID,	gs_gutter_elb_u_ang,	6	:unID=unID+1 !!center
		HOTSPOT  0,	0,													 gs_gutter_elb_u_lgth/2,							unID,	gs_gutter_elb_u_ang,	4+128	:unID=unID+1 !!base
		HOTSPOT  0,	gs_gutter_elb_u_lgth/2*SIN(gs_gutter_elb_u_ang),	 gs_gutter_elb_u_lgth/2*COS(gs_gutter_elb_u_ang),	unID,	gs_gutter_elb_u_ang,	5	:unID=unID+1 !!moving
		HOTSPOT -1,	0,													 0,													unID,	gs_gutter_elb_u_ang,	7	:unID=unID+1 !!ref
endif

! --- HOTSPOTs for Lower_Ang set ---
HOTSPOT  0,	0,													 0,													unID,	gs_gutter_elb_l_ang,	6	:unID=unID+1 !!center
HOTSPOT  0,	0,													-gs_gutter_elb_l_lgth/2,							unID,	gs_gutter_elb_l_ang,	4+128	:unID=unID+1 !!base
HOTSPOT  0,	gs_gutter_elb_l_lgth/2*SIN(gs_gutter_elb_l_ang),	-gs_gutter_elb_l_lgth/2*COS(gs_gutter_elb_l_ang),	unID,	gs_gutter_elb_l_ang,	5	:unID=unID+1 !!moving
HOTSPOT  1,	0,													 0,													unID,	gs_gutter_elb_l_ang,	7	:unID=unID+1 !!ref

del 2
del 1
end
! =============================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
! =============================================================================

! -----------------------------------------------------------------------------
"htsp_upper_length":						! 3D HOTSPOTs for Upper Part Length
! -----------------------------------------------------------------------------
	HOTSPOT XX,		YY,		0,						unID,	gs_gutter_elb_u_lgth,	1+128	:unID=unID+1
	HOTSPOT XX,		YY,		-1,						unID,	gs_gutter_elb_u_lgth,	3		:unID=unID+1
	HOTSPOT XX,		YY,		gs_gutter_elb_u_lgth,	unID,	gs_gutter_elb_u_lgth,	2		:unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_lower_length":						! 3D HOTSPOTs for Lower Part Length
! -----------------------------------------------------------------------------
if gs_gutter_ds_profile_num=1 then
		lower_part_z = gs_gutter_elb_l_lgth * cos (gs_gutter_elb_l_ang) + gs_gutter_dd * sin (gs_gutter_elb_l_ang)
else
		lower_part_z = gs_gutter_elb_l_lgth * cos (gs_gutter_elb_l_ang) + gs_gutter_bb * sin (gs_gutter_elb_l_ang)
endif

	HOTSPOT 0, 0, 							-lower_part_z,	unID, gs_gutter_elb_hor_overhang, 1+128,	gs_gutter_elb_l_lgth	:unID=unID+1
	HOTSPOT 0, gs_gutter_elb_hor_overhang, 	-lower_part_z, 	unID, gs_gutter_elb_hor_overhang, 2,		gs_gutter_elb_l_lgth	:unID=unID+1
	HOTSPOT 0, -1,							-lower_part_z,	unID, gs_gutter_elb_hor_overhang, 3,		gs_gutter_elb_l_lgth	:unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_upper_round":							! 3D HOTSPOTs for Upper Part Round
! -----------------------------------------------------------------------------
	HOTSPOT XX,		YY,		gs_gutter_elb_u_lgth,	unID	:unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_lower_round":							! 3D HOTSPOTs for Lower Part Round
! -----------------------------------------------------------------------------
	HOTSPOT XX,		YY,		gs_gutter_elb_l_lgth,	unID	:unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_upper_rect":						! 3D HOTSPOTs for Upper Part Rectangular
! -----------------------------------------------------------------------------
	HOTSPOT XX,		YY,		gs_gutter_elb_u_lgth,	unID	:unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_lower_rect":						! 3D HOTSPOTs for Lower Part Rectangular
! -----------------------------------------------------------------------------
	HOTSPOT XX,		YY,		gs_gutter_elb_l_lgth,	unID	:unID=unID+1
return

! -----------------------------------------------------------------------------
"htsp_wall_dist":		! 3D Hotsports for Dis. Between Downspout Axes and Wall
! -----------------------------------------------------------------------------
	!new editable hotspots, using fix unID because of migration
	hotspot  XX,	YY,			gs_gutter_elb_u_lgth,	30001, dis_wall, 	1+128
	hotspot  XX,	0,			gs_gutter_elb_u_lgth,	30002, dis_wall, 	2
	hotspot  XX,	YY - 1,		gs_gutter_elb_u_lgth,	30003, dis_wall, 	3	

	hotspot  -XX,	YY,			gs_gutter_elb_u_lgth,	30004, dis_wall, 	1+128
	hotspot  -XX,	0,			gs_gutter_elb_u_lgth,	30005, dis_wall, 	2
	hotspot  -XX,	YY - 1,		gs_gutter_elb_u_lgth,	30006, dis_wall, 	3
return
