
VALUES "gs_gutter_ds_profile_num" 1, 2, 3										! 1-Round, 2-Rectangular, 3-Corrugated
VALUES "gs_detlevel_3D_m" 2, 1, 0												! 2-Detailed, 1-Simple, 0-Off

if gs_cont_pen= 0 then parameters gs_cont_pen=2
if gs_fill_pen=0 then parameters gs_fill_pen=91

if gs_resol<3 then parameters gs_resol=3
if gs_detlevel_3D_m=1 OR gs_gutter_ds_profile_num=2 then lock "gs_resol"		! 1-Simple, 2-Rectangular


parameters ac_bottomlevel = 0
parameters ac_toplevel = ZZYZX

if gs_gutter_ds_profile_num = 1 then
	values "dis_wall" range [gs_gutter_dd, )
else
	values "dis_wall" range [gs_gutter_bb, )
endif

! -----------------------------------------------------------------------------
! Downspout profile parameters
! -----------------------------------------------------------------------------
min_gamma=30
if GLOB_MODPAR_NAME= "gs_gutter_elb_u_ang" and ABS(gs_gutter_elb_u_ang + gs_gutter_elb_l_ang) > (180-min_gamma) then
	PARAMETERS gs_gutter_elb_u_ang = gs_gutter_elb_l_ang - min_gamma
endif

if GLOB_MODPAR_NAME= "gs_gutter_elb_l_ang" and ABS(gs_gutter_elb_u_ang + gs_gutter_elb_l_ang) > (180-min_gamma) then
	PARAMETERS gs_gutter_elb_l_ang = gs_gutter_elb_u_ang - min_gamma
endif

if GLOB_MODPAR_NAME = "gs_gutter_aa" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	parameters 	gs_gutter_aa2 = gs_gutter_aa*2
				gs_gutter_aa2 = gs_gutter_aa*2
endif

if GLOB_MODPAR_NAME = "gs_gutter_aa2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	parameters 	gs_gutter_aa = gs_gutter_aa2/2
				gs_gutter_aa = gs_gutter_aa2/2
endif

if GLOB_MODPAR_NAME = "gs_gutter_bb" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	parameters 	gs_gutter_bb2 = gs_gutter_bb*2
				gs_gutter_bb2 = gs_gutter_bb*2
endif

if GLOB_MODPAR_NAME = "gs_gutter_bb2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	parameters 	gs_gutter_bb = gs_gutter_bb2/2
				gs_gutter_bb = gs_gutter_bb2/2
endif

if GLOB_MODPAR_NAME = "gs_gutter_dd" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	parameters 	gs_gutter_dd2 = gs_gutter_dd*2
				gs_gutter_dd2 = gs_gutter_dd*2
endif

if GLOB_MODPAR_NAME = "gs_gutter_dd2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	parameters 	gs_gutter_dd = gs_gutter_dd2/2
				gs_gutter_dd = gs_gutter_dd2/2
endif


if gs_gutter_ds_profile_num=1 then
	if GLOB_MODPAR_NAME = "gs_gutter_elb_hor_overhang" then
		gs_gutter_elb_l_lgth = (gs_gutter_elb_hor_overhang + gs_gutter_dd * cos (gs_gutter_elb_l_ang)) / sin (gs_gutter_elb_l_ang)
		parameters gs_gutter_elb_l_lgth = gs_gutter_elb_l_lgth
	else
		gs_gutter_elb_hor_overhang = gs_gutter_elb_l_lgth * sin (gs_gutter_elb_l_ang) - gs_gutter_dd * cos (gs_gutter_elb_l_ang)
		parameters gs_gutter_elb_hor_overhang = gs_gutter_elb_hor_overhang
	endif
else
	if GLOB_MODPAR_NAME = "gs_gutter_elb_hor_overhang" then
		gs_gutter_elb_l_lgth = (gs_gutter_elb_hor_overhang + gs_gutter_bb * cos (gs_gutter_elb_l_ang)) / sin (gs_gutter_elb_l_ang)
		parameters gs_gutter_elb_l_lgth = gs_gutter_elb_l_lgth
	else
		gs_gutter_elb_hor_overhang = gs_gutter_elb_l_lgth * sin (gs_gutter_elb_l_ang) - gs_gutter_bb * cos (gs_gutter_elb_l_ang)
		parameters gs_gutter_elb_hor_overhang = gs_gutter_elb_hor_overhang
	endif
endif

if gs_gutter_ds_profile_num = 1 then
	HIDEPARAMETER "gs_gutter_aa2"
	HIDEPARAMETER  "gs_gutter_bb2"
else
	HIDEPARAMETER  "gs_gutter_dd2"
endif

VALUES "gs_gutter_aa" RANGE (0,)
VALUES "gs_gutter_aa2" RANGE (0,)
VALUES "gs_gutter_bb" RANGE (0,)
VALUES "gs_gutter_bb2" RANGE (0,)
VALUES "gs_gutter_dd" RANGE (0,)
VALUES "gs_gutter_dd2" RANGE (0,)
VALUES "gs_gutter_elb_u_lgth" RANGE (0,)
VALUES "gs_gutter_elb_l_lgth" RANGE (0,)
VALUES "gs_gutter_elb_u_ang" RANGE [0,90]
VALUES "gs_gutter_elb_l_ang" RANGE [0,90]

! -----------------------------------------------------------------------------
! A, B, ZZYZX
! -----------------------------------------------------------------------------
if gs_gutter_ds_profile_num=1 then												! round profile
	A=gs_gutter_dd2
	PARAMETERS A = A
	B=gs_gutter_dd2
	PARAMETERS B = B
	ZZYZX = gs_gutter_elb_u_lgth + gs_gutter_elb_l_lgth * cos (gs_gutter_elb_l_ang) + gs_gutter_dd * sin (gs_gutter_elb_l_ang)
	PARAMETERS ZZYZX = ZZYZX
else								 											! rectangular and corrugated profiles
	A=gs_gutter_aa2
	PARAMETERS A = A
	B=gs_gutter_bb2
	PARAMETERS B = B
	ZZYZX = gs_gutter_elb_u_lgth + gs_gutter_elb_l_lgth * cos (gs_gutter_elb_l_ang) + gs_gutter_bb * sin (gs_gutter_elb_l_ang)
	PARAMETERS ZZYZX = ZZYZX
endif
LOCK "A","B","ZZYZX"

