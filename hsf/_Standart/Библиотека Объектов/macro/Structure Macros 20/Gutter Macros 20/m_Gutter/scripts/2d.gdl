

LINE_TYPE gs_line_type
PEN gs_cont_pen
FILL gs_fill_type

!!!*****************************************************************************************
!!!*** 2D HOTSPOTS *************************************************************************
unID=1
!!!*** Gutter Length
!HOTSPOT2  0,				 gs_gutter_p_w/2,	unID,	gs_gutter_lgth,	1+256	:unID=unID+1 !!base											!!length of  Gutter!!
!HOTSPOT2 -1,				 gs_gutter_p_w/2,	unID,	gs_gutter_lgth,	3		:unID=unID+1 !!lgth ref
!HOTSPOT2  gs_gutter_lgth,	 gs_gutter_p_w/2,	unID,	gs_gutter_lgth,	2		:unID=unID+1 !!lgth mov

!!!*** End Cap Left Angle ***
ADD2 0, gs_gutter_p_w/2
ROT2 180
	HOTSPOT2 0,0,unID,gs_gutter_endcap_l_ang2,6,gs_gutter_endcap_l_ang : unID=unID+1
	HOTSPOT2 gs_gutter_p_w,0,unID,gs_gutter_endcap_l_ang2,4+128,gs_gutter_endcap_l_ang : unID=unID+1
	HOTSPOT2 (gs_gutter_p_w)/TAN(gs_gutter_endcap_l_ang2),(gs_gutter_p_w),unID,gs_gutter_endcap_l_ang2,5,gs_gutter_endcap_l_ang : unID=unID+1
DEL 2

!!!*** End Cap Right Angle ***
ADD2 gs_gutter_lgth, gs_gutter_p_w/2
ROT2 180
	HOTSPOT2 0,0,unID,gs_gutter_endcap_r_ang2,6,gs_gutter_endcap_r_ang : unID=unID+1
	HOTSPOT2 gs_gutter_p_w,0,unID,gs_gutter_endcap_r_ang2,4+128,gs_gutter_endcap_r_ang : unID=unID+1
	HOTSPOT2 (gs_gutter_p_w)/TAN(gs_gutter_endcap_r_ang2),(gs_gutter_p_w),unID,gs_gutter_endcap_r_ang2,5,gs_gutter_endcap_r_ang : unID=unID+1
DEL 2

!!!*****************************************************************************************
!!!*** Gutter ******************************************************************************

FILL gs_fill_type
POLY2_B{2} 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
	0,0,0,
	0,															  gs_gutter_p_w/2,	 1,
	gs_gutter_lgth,												  gs_gutter_p_w/2,	 1,
	gs_gutter_lgth + gs_gutter_p_w * TAN(gs_gutter_endcap_r_ang),-gs_gutter_p_w/2,	 1,
	gs_gutter_p_w * TAN(gs_gutter_endcap_l_ang),				 -gs_gutter_p_w/2,	 1

HOTLINE2  0,	gs_gutter_p_w/2,	gs_gutter_lgth,	 gs_gutter_p_w/2

IF gs_gutter_endcap_l_ang < 0 THEN LGTH = LGTH + gs_gutter_p_w * TAN(gs_gutter_endcap_l_ang)
IF gs_gutter_endcap_r_ang > 0 THEN LGTH = LGTH - gs_gutter_p_w * TAN(gs_gutter_endcap_r_ang)


!!!*****************************************************************************************
!!!*** Downspout (Hole) ********************************************************************

IF gs_gutter_downspout THEN

	CALL "m_DSProfiles" PARAMETERS gs_gutter_ds_profile_num=gs_gutter_ds_profile_num,
										 gs_mask=0,
										 gs_gutter_aa=gs_gutter_aa,
										 gs_gutter_bb=gs_gutter_bb,
										 gs_gutter_dd=gs_gutter_dd
	!!2D Body DownSpout_Hole!!

	ADD2 gs_gutter_lgth-gs_gutter_downspout_x_pos*COS(gs_gutter_slope_in_degree)-gs_gutter_p_h*SIN(gs_gutter_slope_in_degree), gs_gutter_downspout_y_pos

		POLY2_B nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen, use(nsp)

	DEL 1

	!!!*** 2D HOTSPOTS POSITION ****************************************************************

	IF gs_gutter_ds_profile_num = 1 THEN
		AAA= gs_gutter_dd	: BBB= 0			: GOSUB 1	!!2D HOTSPOTs DownSpout_HOle - Round Profile!!
		IF ABS(gs_gutter_slope_in_degree)<EPS  THEN
			AAA= gs_gutter_dd	: BBB= 0			: GOSUB 2
			AAA= 0	: BBB= gs_gutter_dd				: GOSUB 2
			AAA= -gs_gutter_dd	: BBB= 0			: GOSUB 2
			AAA= 0	: BBB= -gs_gutter_dd			: GOSUB 2
		ENDIF
	ELSE													!!2D HOTSPOTs DownSpout_HOle - NOT_Round Profile!!
		AAA= gs_gutter_aa	: BBB= 0			: GOSUB 1
		IF ABS(gs_gutter_slope_in_degree)<EPS  THEN
			AAA= gs_gutter_aa	: BBB= gs_gutter_bb		: GOSUB 2
			AAA= -gs_gutter_aa	: BBB= gs_gutter_bb		: GOSUB 2
			AAA= -gs_gutter_aa	: BBB= -gs_gutter_bb	: GOSUB 2
			AAA= gs_gutter_aa	: BBB= -gs_gutter_bb	: GOSUB 2
		ENDIF
	ENDIF
ENDIF

!!!*****************************************************************************************
!!!*** Gutter ******************************************************************************

	IF gs_gutter_hanger AND gs_detlevel_2D_m=2 THEN		!!*** Detailed Symbol
		IF gs_gutter_ds_h_positioning = HANGER_UNIFORM THEN
			FOR i=1 TO INT((gs_gutter_lgth-gs_gutter_hanger_off-sthw)/gs_gutter_hanger_dist)+1
		
				!!!*** 2D HOTSPOTs HANGER POSITION
				HOTSPOT2  0,						0, unID, gs_gutter_hanger_pos[i], 1+128	:unID=unID+1 !!base
				HOTSPOT2 -1,						0, unID, gs_gutter_hanger_pos[i], 3		:unID=unID+1 !!lgth ref
				HOTSPOT2  gs_gutter_hanger_pos[i],	0, unID, gs_gutter_hanger_pos[i], 2		:unID=unID+1 !!lgth mov
		
				ADD2 (gs_gutter_hanger_pos[i]-sthw/2),0
							CALL "m_GutterHanger" PARAMETERS gs_gutter_p_num=gs_gutter_p_num,
															gs_gutter_p_w=gs_gutter_p_w,
															gs_gutter_p_h=gs_gutter_p_h,
															gs_gutter_p_r=gs_gutter_p_r,
															gs_detlevel_3D_m=gs_detlevel_3D_m,
															gs_resol=gs_resol,
															gs_shadow=gs_shadow,
															gs_line_type=gs_line_type,
															gs_cont_pen=gs_cont_pen,
															gs_fill_type=gs_fill_type,
															gs_fill_pen=gs_fill_pen,
															gs_back_pen=gs_back_pen,
															gs_gutter_hanger_mat=gs_gutter_hanger_mat,
															gs_gutter_h_profile_num=gs_gutter_h_profile_num,
															sthw=0.03
																											!!GUTTER HANGERs 2D Body!!
				DEL 1
		
			NEXT i
		ELSE
			i = 0
			FOR i=1 TO hangeri 

				!!!*** 2D HOTSPOTs HANGER POSITION
				HOTSPOT2  0,						0, unID, gs_gutter_hanger_pos[i], 1+128	:unID=unID+1 !!base
				HOTSPOT2 -1,						0, unID, gs_gutter_hanger_pos[i], 3		:unID=unID+1 !!lgth ref
				HOTSPOT2  gs_gutter_hanger_pos[i],	0, unID, gs_gutter_hanger_pos[i], 2		:unID=unID+1 !!lgth mov
		
				ADD2 (gs_gutter_hanger_pos[i]-sthw/2),0
							CALL "m_GutterHanger" PARAMETERS gs_gutter_p_num=gs_gutter_p_num,
															gs_gutter_p_w=gs_gutter_p_w,
															gs_gutter_p_h=gs_gutter_p_h,
															gs_gutter_p_r=gs_gutter_p_r,
															gs_detlevel_3D_m=gs_detlevel_3D_m,
															gs_resol=gs_resol,
															gs_shadow=gs_shadow,
															gs_line_type=gs_line_type,
															gs_cont_pen=gs_cont_pen,
															gs_fill_type=gs_fill_type,
															gs_fill_pen=gs_fill_pen,
															gs_back_pen=gs_back_pen,
															gs_gutter_hanger_mat=gs_gutter_hanger_mat,
															gs_gutter_h_profile_num=gs_gutter_h_profile_num,
															sthw=0.03
																											!!GUTTER HANGERs 2D Body!!
				DEL 1
		
			NEXT i
		ENDIF
	ENDIF
	

END

1: !!2D HOTSPOTs DOWNSPOUT_HOLE Position!!
IF ABS(gs_gutter_slope_in_degree)<EPS THEN		!!!**>gs_gutter_slope_in_degree=0
	ADD2 gs_gutter_lgth,0
		HOTSPOT2 0,							 BBB,		unID,	gs_gutter_downspout_x_pos,	1+128:	unID=unID+1 !!base
		HOTSPOT2 1,							 BBB,		unID,	gs_gutter_downspout_x_pos,	3:		unID=unID+1 !!lgth ref
		HOTSPOT2 -gs_gutter_downspout_x_pos,	 BBB,		unID,	gs_gutter_downspout_x_pos,	2:		unID=unID+1 !!lgth mov
	DEL 1

	ELSE
	ADD2 gs_gutter_lgth-gs_gutter_downspout_x_pos*COS(gs_gutter_slope_in_degree)-gs_gutter_p_h*SIN(gs_gutter_slope_in_degree), gs_gutter_downspout_y_pos
		HOTSPOT2 0,0,unID: unID=unID+1
	DEL 1
ENDIF
RETURN

2:	!!2D HOTSPOTs DOWNSPOUT PROFILE!!
	ADD2 gs_gutter_lgth-gs_gutter_downspout_x_pos,0
		HOTSPOT2 AAA,BBB,unID: unID=unID+1
	DEL 1
RETURN
