success = LIBRARYGLOBAL ("Libraryflobals-as", "gs_detlevel_3D_m", gs_detlevel_3D_m)

IF gs_detlevel_3D_m = 1 or (GLOB_CONTEXT>20 and GLOB_CONTEXT<40) THEN 	!!Simple
	!!!*** Simple Model
	gs_gutter_guard=0
	gs_gutter_hanger=0
	gs_gutter_strainer=0
	gs_resol=6
	gs_shadow = 0
ENDIF

IF gs_detlevel_3D_m = 0 THEN END 	!!Off

RESOL gs_resol
PEN gs_cont_pen
MATERIAL gs_gutter_mat

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

! --- 3D Hotspots --------------------------------------------------------------

unID = 1

ROTY gs_gutter_slope_in_degree
! Gutter Length
HOTSPOT 0,				gs_gutter_p_w/2,		0,	unID,	gs_gutter_lgth2,	1+256		:unID=unID+1 ! base
HOTSPOT -1,				gs_gutter_p_w/2,		0,	unID,	gs_gutter_lgth2,	3			:unID=unID+1 ! lgth ref
HOTSPOT gs_gutter_lgth2,	gs_gutter_p_w/2,	0,	unID,	gs_gutter_lgth2,	2			:unID=unID+1 ! lgth mov

! Ends
HOTSPOT  gs_gutter_p_w * TAN(gs_gutter_endcap_l_ang),					-gs_gutter_p_w/2,	0,	unID	:unID=unID+1
HOTSPOT  gs_gutter_lgth2 + gs_gutter_p_w * TAN(gs_gutter_endcap_r_ang),	-gs_gutter_p_w/2,	0,	unID	:unID=unID+1
DEL 1

	! Gutter Slope
	IF gs_slope_mode_m = 1 THEN 	!!(Â°)
		ADDy -gs_gutter_p_w/2
			HOTSPOT 0,				0,0,unID,gs_gutter_slope_in_degree,6 : unID=unID+1
			HOTSPOT gs_gutter_lgth/2,	0,0,unID,gs_gutter_slope_in_degree,4+128 : unID=unID+1
			HOTSPOT 0,				1,0,unID,gs_gutter_slope_in_degree,7 : unID=unID+1
			HOTSPOT gs_gutter_lgth/2,	0, -(gs_gutter_lgth/2)*tan(gs_gutter_slope_in_degree),unID,gs_gutter_slope_in_degree,5 : unID=unID+1
		DEL 1
	ELSE
		HOTSPOT gs_gutter_lgth/2,		-gs_gutter_p_w/2,		0,	unID,	gs_gutter_lgthZ,	1+128, gs_gutter_slope_in_percent		:unID=unID+1 !!base
		HOTSPOT gs_gutter_lgth/2,		-gs_gutter_p_w/2,		1,	unID,	gs_gutter_lgthZ,	3, gs_gutter_slope_in_percent		:unID=unID+1 !!lgth ref
		HOTSPOT gs_gutter_lgth/2,		-gs_gutter_p_w/2,	-gs_gutter_lgthZ,	unID,	gs_gutter_lgthZ,	2, gs_gutter_slope_in_percent			:unID=unID+1 !!lgth mov
	ENDIF


	!!!*****************************************************************************************
	!!!*** 3D Modell ***************************************************************************


	IF ABS(gs_gutter_endcap_l_ang)>EPS THEN				!!!*** gs_gutter_endcap_l_ang <>0
		ADDY gs_gutter_p_w/2							!!!*** CUT left END!!
		ROTZ gs_gutter_endcap_l_ang
			CUTPLANE 0,1,1,1
		DEL 2
	ENDIF

	IF ABS(gs_gutter_endcap_r_ang)>EPS THEN				!!!*** gs_gutter_endcap_r_ang <>0
		ADD gs_gutter_lgth, gs_gutter_p_w/2, 0			!!!*** CUT rigth END!!
		ROTZ gs_gutter_endcap_r_ang
			CUTPLANE 0,1,1,0
		DEL 2
	ENDIF

!	ROTY(slope_ang)										!!!*** Sloping!!
	ROTY gs_gutter_slope_in_degree
	!!!*****************************************************************************************
	!!!*** Gutter with Downspout (Hole) ********************************************************

	IF gs_gutter_downspout THEN

	   CALL "m_DSProfiles" PARAMETERS gs_gutter_ds_profile_num=gs_gutter_ds_profile_num,
											gs_mask=15, gs_gutter_aa=gs_gutter_aa,
											gs_gutter_bb=gs_gutter_bb, gs_gutter_dd=gs_gutter_dd

		!!!*** Dist. from end to DS axis ***

		ADD  gs_gutter_lgth-gs_gutter_downspout_x_pos, -gs_gutter_downspout_y_pos, 0

			CUTPOLYA nsp/3, 1, 0, get(nsp)

		DEL 1

	ENDIF


	!!!*****************************************************************************************
	!!!*** Gutter without Downspout ************************************************************

	CALL "m_GutterProfiles" PARAMETERS gs_gutter_p_num=gs_gutter_p_num,
									 gs_mask=0,
									 gs_gutter_p_w=gs_gutter_p_w,
									 gs_gutter_p_h=gs_gutter_p_h,
									 gs_gutter_p_r=gs_gutter_p_r


	ROTZ -90 : ROTX 90


!!		LGTH = -gs_gutter_lgth/COS(slope_ang)
		LGTH = -gs_gutter_lgth/COS(gs_gutter_slope_in_degree)
		IF gs_gutter_endcap_l_ang < 0 THEN LGTH = LGTH + gs_gutter_p_w * TAN(gs_gutter_endcap_l_ang)
		IF gs_gutter_endcap_l_ang < 0 THEN ADDZ - gs_gutter_p_w * TAN(gs_gutter_endcap_l_ang)
		IF gs_gutter_endcap_r_ang > 0 THEN LGTH = LGTH - gs_gutter_p_w * TAN(gs_gutter_endcap_r_ang)

		EXTRUDE   nsp/3, 0, 0, LGTH, 0, get(nsp)						!!GUTTER 3D MODELL

		IF gs_gutter_endcap_l_ang < 0 THEN DEL 1

	DEL 2

	IF gs_gutter_downspout THEN
	  CUTEND
	ENDIF



	!!!*****************************************************************************************
	!!!*** Gutter Guard ************************************************************************

	IF gs_gutter_guard = 1 THEN			!!3D GUTTER GUARD!!
		ww=0.002						!!guard wire width!!
		hx1=0.023						!!hole width!!
		hy1=0.023						!!hole depth!!

		nx=INT(-LGTH/0.025)				!!guard x number of holes!!
		ny=INT(gs_gutter_p_w/0.025)		!!guard y number of holes!!

		IF gs_gutter_endcap_l_ang < 0 THEN
			ADDX gs_gutter_p_w * TAN(gs_gutter_endcap_l_ang)
			hossz=gs_gutter_lgth - (gs_gutter_p_w * TAN(gs_gutter_endcap_l_ang))
		Else
			hossz = gs_gutter_lgth
		ENDIF

		IF gs_gutter_endcap_r_ang > 0 THEN 	hossz=hossz + (gs_gutter_p_w * TAN(gs_gutter_endcap_r_ang))


		ADD 0, -gs_gutter_p_w/2, -sth/10

		MATERIAL gs_gutter_guard_mat
		FOR j=1 TO nx
			IF j > 1 THEN
				Plane 4,
					0,0,0,
					0.002,0,0,
					0.002,gs_gutter_p_w,0,
					0,gs_gutter_p_w,0
			ENDIF
			addx 0.025
		NEXT j
		DEL nx

		FOR j=1 TO ny
			IF j > 1 THEN
				Plane 4,
					0,0,0,
					hossz,0,0,
					hossz,0.002,0,
					0,0.002,0
			ENDIF
			addy 0.025
		NEXT j
		DEL ny

		DEL 1 : IF gs_gutter_endcap_l_ang < 0 THEN DEL 1
	MATERIAL gs_gutter_mat
	ENDIF


	!!!*****************************************************************************************
	!!!*** Downspout ***************************************************************************

	!!Downspout Part beginnig!!
	IF gs_gutter_downspout THEN


		 ADD gs_gutter_lgth-gs_gutter_downspout_x_pos, -gs_gutter_downspout_y_pos, 0


		!!STRAINER!!
		IF gs_gutter_strainer=1 THEN

			IF gs_gutter_p_num =3 THEN		!!(U Style)
				ADDz -gs_gutter_p_h+gs_gutter_dd2 / 5
				ELSE
				ADDz -gs_gutter_p_r/2
			ENDIF

			CALL "m_GutterStrainer" PARAMETERS gs_gutter_ds_profile_num = gs_gutter_ds_profile_num,
											gs_gutter_p_num=gs_gutter_p_num,
											gs_gutter_aa = gs_gutter_aa2-0.002,
											gs_gutter_bb = gs_gutter_bb2-0.002,
											gs_gutter_dd2 = gs_gutter_dd2-0.002,
											gs_detlevel_3D_m=gs_detlevel_3D_m,
											gs_resol=gs_resol,
											gs_shadow=gs_shadow,
											gs_cont_pen=gs_cont_pen,
											gs_gutter_strainer_mat=gs_gutter_strainer_mat,
											drotszam=gs_resol

			DEL 1

		ENDIF

		ROTX 90 : ROTY -90

		CALL "m_GutterProfiles" PARAMETERS  gs_gutter_p_num=gs_gutter_p_num,
										  gs_mask=0,					     !!STACK UPLOAD!!
										  gs_gutter_p_w=gs_gutter_p_w,
										  gs_gutter_p_h=gs_gutter_p_h,
										  gs_gutter_p_r=gs_gutter_p_r

		CUTPOLYA nsp/3, 1, 0, get(nsp)										!!CUTTING POLYGON!!

		ROTX 90
		ROTZ 90

		CALL "m_DSProfiles" PARAMETERS gs_gutter_ds_profile_num=gs_gutter_ds_profile_num,
											gs_mask=1, gs_gutter_aa=gs_gutter_aa,
											gs_gutter_bb=gs_gutter_bb,
											gs_gutter_dd=gs_gutter_dd

		IF gs_gutter_p_num=1 THEN	!!(Half Round)
			temp_ConnectHeight=gs_gutter_p_r
			ELSE
			temp_ConnectHeight=gs_gutter_p_h
		ENDIF

		EXTRUDE   nsp/3, 0, 0, temp_ConnectHeight, 32, get(nsp)					!!GUTTER 3D MODELL


		DEL 2

		CUTEND

		DEL 3

		IF gs_gutter_ds_profile_num = 1 THEN
			!!!**>3D HOTSPOTs DownSpout_Hole - Round Profile
			AAA= gs_gutter_dd	: BBB= 0			: GOSUB 1
			AAA= gs_gutter_dd	: BBB= 0			: GOSUB 2
			AAA= -gs_gutter_dd	: BBB=0				: GOSUB 2
			AAA= 0				: BBB= gs_gutter_dd	: GOSUB 2
			AAA= 0				: BBB=-gs_gutter_dd	: GOSUB 2
		ELSE
			!!!*** 3D HOTSPOTs DownSpout_Hole - NOT_Round Profile
			AAA= gs_gutter_aa	: BBB= 0			: GOSUB 1
			AAA= gs_gutter_aa	: BBB= gs_gutter_bb	: GOSUB 2
			AAA= gs_gutter_aa	: BBB=-gs_gutter_bb	: GOSUB 2
			AAA= -gs_gutter_aa	: BBB= gs_gutter_bb	: GOSUB 2
			AAA= -gs_gutter_aa	: BBB=-gs_gutter_bb	: GOSUB 2
		ENDIF

	ENDIF


DEL 1			!!!**>Sloping!!
IF ABS(gs_gutter_endcap_r_ang)>EPS THEN CUTEND			!!!*** gs_gutter_endcap_r_ang <>0	CUTEND rigth END!!
IF ABS(gs_gutter_endcap_l_ang)>EPS THEN CUTEND			!!!*** gs_gutter_endcap_l_ang <>0	CUTEND left END!!


	!!!*****************************************************************************************
	!!!*** GUTTER_HANGER ***********************************************************************

	IF gs_gutter_hanger THEN

		SHADOW OFF
		IF gs_gutter_ds_h_positioning = HANGER_UNIFORM THEN
			FOR i=1 TO INT((gs_gutter_lgth-gs_gutter_hanger_off-sthw)/gs_gutter_hanger_dist)+1
	
				IF (gs_gutter_lgth*TAN(gs_gutter_slope_in_degree)) > 0 THEN
					zz  = (gs_gutter_lgth*TAN(gs_gutter_slope_in_degree)) * ( (gs_gutter_hanger_pos[i] / gs_gutter_lgth))
				ELSE
					zz  = 0
				ENDIF
				zzz = zz + gs_gutter_hanger_v
	
	
				!!!**>3D HOTSPOTs for HANGER Position setting!!
				HOTSPOT  0,							gs_gutter_p_w/2, -zz, unID, gs_gutter_hanger_pos[i], 1+128	:unID=unID+1 !!base
				HOTSPOT -1,							gs_gutter_p_w/2, -zz, unID, gs_gutter_hanger_pos[i], 3		:unID=unID+1 !!lgth ref
				HOTSPOT  gs_gutter_hanger_pos[i],	gs_gutter_p_w/2, -zz, unID, gs_gutter_hanger_pos[i], 2		:unID=unID+1 !!lgth mov
	
				M1=gs_gutter_p_r+sth+sth
				M2=(sthw*TAN(gs_gutter_slope_in_degree))*COS(gs_gutter_slope_in_degree)
				M12=M1/COS(gs_gutter_slope_in_degree)
				M22=M2/COS(gs_gutter_slope_in_degree)
	
					ADDz -gs_gutter_hanger_pos[i]*TAN(gs_gutter_slope_in_degree)+sthw/2*TAN(gs_gutter_slope_in_degree)+sth
					ADDX gs_gutter_hanger_pos[i]-sthw/2
	
						IF gs_gutter_p_num=1	THEN MULz (M12+M22)/(M1)	!!(Half Round)														!!GUTTER HANGERs 3D!!
						!!!**>GUTTER HANGERs 3D!!
						CALL "m_GutterHanger" PARAMETERS gs_gutter_p_num=gs_gutter_p_num,
														gs_gutter_p_w=gs_gutter_p_w+2*sth,
														gs_gutter_p_h=sth+gs_gutter_p_h/COS(gs_gutter_slope_in_degree)+sthw*TAN(gs_gutter_slope_in_degree),
														gs_gutter_p_r=gs_gutter_p_r+sth,
														gs_detlevel_3D_m=gs_detlevel_3D_m,
														gs_resol=gs_resol,
														gs_shadow=gs_shadow,
														gs_line_type=gs_line_type,
														gs_cont_pen=gs_cont_pen,
														gs_fill_type=gs_fill_type,
														gs_fill_pen=gs_fill_pen,
														gs_back_pen=gs_back_pen,
														gs_gutter_hanger_mat=gs_gutter_hanger_mat,
														gs_gutter_h_profile_num=gs_gutter_h_profile_num
	
						IF gs_gutter_p_num=1	THEN DEL 1		!!(Half Round)
	
	
						!!!*** Part of Hanger at Roof
	
	
						MATERIAL gs_gutter_hanger_mat
	
						IF gs_gutter_h_profile_num = 4 OR gs_gutter_h_profile_num = 5 OR gs_gutter_h_profile_num = 6 THEN GOSUB 3	!!(Bar 1-2-3)
	
					DEL 2
	
			NEXT i
		ELSE
			i = 0
			FOR i=1 TO hangeri 

				IF (gs_gutter_lgth*TAN(gs_gutter_slope_in_degree)) > 0 THEN
					zz  = (gs_gutter_lgth*TAN(gs_gutter_slope_in_degree)) * ( (gs_gutter_hanger_pos[i] / gs_gutter_lgth))
				ELSE
					zz  = 0
				ENDIF
				zzz = zz + gs_gutter_hanger_v
	
	
				!!!**>3D HOTSPOTs for HANGER Position setting!!
				HOTSPOT  0,							gs_gutter_p_w/2, -zz, unID, gs_gutter_hanger_pos[i], 1+128	:unID=unID+1 !!base
				HOTSPOT -1,							gs_gutter_p_w/2, -zz, unID, gs_gutter_hanger_pos[i], 3		:unID=unID+1 !!lgth ref
				HOTSPOT  gs_gutter_hanger_pos[i],	gs_gutter_p_w/2, -zz, unID, gs_gutter_hanger_pos[i], 2		:unID=unID+1 !!lgth mov
	
				M1=gs_gutter_p_r+sth+sth
				M2=(sthw*TAN(gs_gutter_slope_in_degree))*COS(gs_gutter_slope_in_degree)
				M12=M1/COS(gs_gutter_slope_in_degree)
				M22=M2/COS(gs_gutter_slope_in_degree)
	
					ADDz -gs_gutter_hanger_pos[i]*TAN(gs_gutter_slope_in_degree)+sthw/2*TAN(gs_gutter_slope_in_degree)+sth
					ADDX gs_gutter_hanger_pos[i]-sthw/2
	
						IF gs_gutter_p_num=1	THEN MULz (M12+M22)/(M1)	!!(Half Round)														!!GUTTER HANGERs 3D!!
						!!!**>GUTTER HANGERs 3D!!
						CALL "m_GutterHanger" PARAMETERS gs_gutter_p_num=gs_gutter_p_num,
														gs_gutter_p_w=gs_gutter_p_w+2*sth,
														gs_gutter_p_h=sth+gs_gutter_p_h/COS(gs_gutter_slope_in_degree)+sthw*TAN(gs_gutter_slope_in_degree),
														gs_gutter_p_r=gs_gutter_p_r+sth,
														gs_detlevel_3D_m=gs_detlevel_3D_m,
														gs_resol=gs_resol,
														gs_shadow=gs_shadow,
														gs_line_type=gs_line_type,
														gs_cont_pen=gs_cont_pen,
														gs_fill_type=gs_fill_type,
														gs_fill_pen=gs_fill_pen,
														gs_back_pen=gs_back_pen,
														gs_gutter_hanger_mat=gs_gutter_hanger_mat,
														gs_gutter_h_profile_num=gs_gutter_h_profile_num
	
						IF gs_gutter_p_num=1	THEN DEL 1		!!(Half Round)
	
	
						!!!*** Part of Hanger at Roof
	
	
						MATERIAL gs_gutter_hanger_mat
	
						IF gs_gutter_h_profile_num = 4 OR gs_gutter_h_profile_num = 5 OR gs_gutter_h_profile_num = 6 THEN GOSUB 3	!!(Bar 1-2-3)
	
					DEL 2
	
			NEXT i

		ENDIF
	ENDIF

	!!!*****************************************************************************************
	!!!*** Gutter END CAPS *********************************************************************

	!!*** Left End Cap ***

	MATERIAL gs_gutter_mat
	IF gs_gutter_endcap_l THEN


		IF ABS(gs_gutter_endcap_l_ang)>EPS THEN			!!!*** gs_gutter_endcap_l_ang <>0

			ADDX gs_gutter_p_w/2 * TAN(gs_gutter_endcap_l_ang)
			ROTz 90+gs_gutter_endcap_l_ang
			ROTx 90
			MUL 1/COS(gs_gutter_endcap_l_ang), 1,  1

				MULy (gs_gutter_p_r/cos(gs_gutter_slope_in_degree))/gs_gutter_p_r
				IF gs_gutter_p_num=1 THEN ARC gs_gutter_p_r,180,360	!!(Half Round)
				DEL 1

				XX2L=gs_gutter_p_w*TAN(gs_gutter_endcap_l_ang)
				ZZ2L=XX2L*TAN(gs_gutter_slope_in_degree)
				XX3L=gs_gutter_p_w/2*TAN(gs_gutter_endcap_l_ang)
				ZZ3L=XX3L*TAN(gs_gutter_slope_in_degree)

				IF gs_gutter_p_num=2 THEN		!!(K Style)
					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree),1,
					0, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree)-ZZ3L,1,
					-gs_gutter_p_w/2,-ZZ2L,1,
					gs_gutter_p_w/2,0,1
				ENDIF

				IF gs_gutter_p_num=3 THEN		!!(U Style)

					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree),1,
					-gs_gutter_p_w/2, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree)-ZZ2L,1,
					-gs_gutter_p_w/2,-ZZ2L,1,
					gs_gutter_p_w/2,0,1

				ENDIF

			DEL 4
		ELSE

			ROTY(gs_gutter_slope_in_degree)
			ROTz 90
			ROTx 90

				IF gs_gutter_p_num=1 THEN ARC gs_gutter_p_r,180,360		!!(Half Round)

				IF gs_gutter_p_num=2 THEN		!!(K Style)
					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h,1,
					0, -gs_gutter_p_h,1,
					-gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2,0,1
				ENDIF

				IF gs_gutter_p_num=3 THEN		!!(U Style)
					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h,1,
					-gs_gutter_p_w/2, -gs_gutter_p_h,1,
					-gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2,0,1
				ENDIF

			DEL 3
		ENDIF

	ENDIF

	!!*** Right End Cap ***

	IF gs_gutter_endcap_r THEN

		IF ABS(gs_gutter_endcap_r_ang)>EPS THEN			!!!*** gs_gutter_endcap_r_ang <>0

			ADDz -gs_gutter_lgth*TAN(gs_gutter_slope_in_degree)
			ADD gs_gutter_lgth+gs_gutter_p_w/2 * TAN(gs_gutter_endcap_r_ang),0,0
			ROTz 90+gs_gutter_endcap_r_ang
			ROTx 90
			MUL 1/COS(gs_gutter_endcap_r_ang), 1,  1

				MULy (gs_gutter_p_r/cos(gs_gutter_slope_in_degree))/gs_gutter_p_r
				IF gs_gutter_p_num=1 THEN ARC gs_gutter_p_r,180,360		!!(Half Round)
				DEL 1

				XX2R=gs_gutter_p_w*TAN(gs_gutter_endcap_r_ang)
				ZZ2R=XX2R*TAN(gs_gutter_slope_in_degree)
				XX3R=gs_gutter_p_w/2*TAN(gs_gutter_endcap_r_ang)
				ZZ3R=XX3R*TAN(gs_gutter_slope_in_degree)


				IF gs_gutter_p_num=2 THEN		!!(K Style)

					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree),1,
					0, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree)-ZZ3R,1,
					-gs_gutter_p_w/2,-ZZ2R,1,
					gs_gutter_p_w/2,0,1

				ENDIF

				IF gs_gutter_p_num=3 THEN		!!(U Style)

					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree),1,
					-gs_gutter_p_w/2, -gs_gutter_p_h/COS(gs_gutter_slope_in_degree)-ZZ2R,1,
					-gs_gutter_p_w/2,-ZZ2R,1,
					gs_gutter_p_w/2,0,1

				ENDIF

			DEL 5

		ELSE
			ADD gs_gutter_lgth+gs_gutter_p_w/2 * TAN(gs_gutter_endcap_r_ang),0,-gs_gutter_lgth*TAN(gs_gutter_slope_in_degree)
			ROTY(gs_gutter_slope_in_degree)
			ROTz 90
			ROTx 90
				IF gs_gutter_p_num=1 THEN ARC gs_gutter_p_r,180,360	!!(Half Round)

				IF gs_gutter_p_num=2 THEN		!!(K Style)
					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h,1,
					0, -gs_gutter_p_h,1,
					-gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2,0,1
				ENDIF

				IF gs_gutter_p_num=3 THEN		!!(U Style)
					POLY_ 5,
					gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2, -gs_gutter_p_h,1,
					-gs_gutter_p_w/2, -gs_gutter_p_h,1,
					-gs_gutter_p_w/2,0,1,
					gs_gutter_p_w/2,0,1

				ENDIF

			DEL 4
		ENDIF

	ENDIF

END


1: !!3D HOTSPOTs DOWNSPOUT_HOLE Position!!

ADDx gs_gutter_lgth
	HOTSPOT 0,							 BBB,	-temp_ConnectHeight,	unID,	gs_gutter_downspout_x_pos,	1+128:	unID=unID+1 !!base
	HOTSPOT 1,							 BBB,	-temp_ConnectHeight,	unID,	gs_gutter_downspout_x_pos,	3:		unID=unID+1 !!lgth ref
	HOTSPOT -gs_gutter_downspout_x_pos,	 BBB,	-temp_ConnectHeight,	unID,	gs_gutter_downspout_x_pos,	2:		unID=unID+1 !!lgth mov
DEL 1

RETURN

2: !!3D HOTSPOTs DOWNSPOUT

ADDx gs_gutter_lgth-gs_gutter_downspout_x_pos
	HOTSPOT AAA,	BBB,	-temp_ConnectHeight,	unID:	unID=unID+1
DEL 1


RETURN

!!!*****************************************************************************************
!!!*** HANGER ******************************************************************************

3:		!!hanger made in Gutter.gsm, because of sloping!!

IF gs_gutter_h_profile_num=6 THEN ADDy -gs_gutter_p_w/2-sth	!!(Bar 3)
ADDy sth
	IF zzz>0 THEN
		ADD	0, gs_gutter_p_w/2+sth,0
		ROTX 90
			POLY_ 5,
			0,0,1,
			sthw,0,1,
			sthw,zzz,1,
			0,zzz,1,
			0,0,-1
		DEL 2
	ENDIF

	IF gs_gutter_h_profile_num=4 OR gs_gutter_h_profile_num=5 THEN !!(Bar 1-2)
		HangerWidth=sthh
		ELSE
		HangerWidth=sthh+gs_gutter_p_w/2
	ENDIF

	ADD	0, gs_gutter_p_w/2+sth,ZZZ
	ROTX gs_gutter_roof_angle
		POLY_ 5,
		0,0,1,
		sthw,0,1,
		sthw,HangerWidth,1,
		0,HangerWidth,1,
		0,0,-1
	DEL 2

DEL 1
IF gs_gutter_h_profile_num=6 THEN DEL 1	!!(Bar 3)
RETURN

