

! *** Gutter and Roof Slope ****************************************************
if LibraryLangCode = "USA" then

	VALUES "gs_gutter_profile" `Полукруглый - 2.5"`,`Полукруглый - 3"`,
							`K-образный - 5"*3.5"`,`K-образный - 6"*4"`,
							`U-образный - 5"*3.5"`,`U-образный - 6"*4"`,
							`Специальный - Полукруглый`,`Специальный - K-образный`,`Специальный - U-образный`

	values "gs_gutter_p_num" 1, 2, 3

	if gs_gutter_profile = `Полукруглый - 2.5"` | gs_gutter_profile = `Полукруглый - 3"` | gs_gutter_profile = `Специальный - Полукруглый` then
		parameters gs_gutter_p_num = 1
	endif

	if gs_gutter_profile = `K-образный - 5"*3.5"` | gs_gutter_profile = `K-образный - 6"*4"` | gs_gutter_profile = `Специальный - K-образный` then
		parameters gs_gutter_p_num = 2
	endif
	if gs_gutter_profile = `U-образный - 5"*3.5"` | gs_gutter_profile = `U-образный - 6"*4"` | gs_gutter_profile = `Специальный - U-образный` then
		parameters gs_gutter_p_num = 3
	endif

else

	! --- Gutter Profile ---
	values "gs_gutter_profile" `Полукруглый`,`K-стиль`,`U-стиль`
	values "gs_gutter_p_num" 1, 2, 3

	if GLOB_MODPAR_NAME = "gs_gutter_profile" then
		gs_gutter_p_num = 1
		if gs_gutter_profile = `K-стиль` 	then gs_gutter_p_num = 2
		if gs_gutter_profile = `U-стиль` 	then gs_gutter_p_num = 3
		parameters gs_gutter_p_num = gs_gutter_p_num
	else
		gs_gutter_profile = `Полукруглый`
		if gs_gutter_p_num = 2	then gs_gutter_profile = `K-стиль`
		if gs_gutter_p_num = 3	then gs_gutter_profile = `U-стиль`
		parameters gs_gutter_profile = gs_gutter_profile
	endif

endif

! --- Slope ---
values "gs_slope_mode" `°`,`%`
values "gs_slope_mode_m" 1, 2

if GLOB_MODPAR_NAME = "gs_slope_mode" then
	gs_slope_mode_m = 1
	if gs_slope_mode = `%`	 then gs_slope_mode_m = 2
	parameters gs_slope_mode_m = gs_slope_mode_m
else
	gs_slope_mode = `°`
	if gs_slope_mode_m = 2 then gs_slope_mode = `%`
	parameters gs_slope_mode = gs_slope_mode
endif


values "gs_gutter_h_profile" stHangerProfileValues
values "gs_gutter_h_profile_num" 1, 2, 3, 4, 5, 6

if GLOB_MODPAR_NAME = "gs_gutter_h_profile" then
	gs_gutter_h_profile_num = 1
	for i = 2 to vardim1(stHangerProfileValues)
		if gs_gutter_h_profile = stHangerProfileValues[i] then
			gs_gutter_h_profile_num = i
			i = vardim1(stHangerProfileValues)
		endif
	next i
	parameters gs_gutter_h_profile_num = gs_gutter_h_profile_num
else
	actGutterProfileNum = max(1, min(vardim1(stHangerProfileValues), gs_gutter_h_profile_num))
	gs_gutter_h_profile = stHangerProfileValues[actGutterProfileNum]
	parameters gs_gutter_h_profile = gs_gutter_h_profile
endif

! -------------------------------------------------------------------------------
! Downspout Profile Type
! -------------------------------------------------------------------------------
if LibraryLangCode = "USA" then

	values "gs_gutter_ds_profile" 	`Круглый 3"`,`Круглый 4"`,
								`Прямоугольный 2"*3"`,`Прямоугольный 3"*2"`,`Прямоугольный 3"*4"`,`Прямоугольный 4"*3"`,
								`Закругленный 2"*3"`,`Закругленный 3"*2"`,`Закругленный 3"*4"`,`Закругленный 4"*3"`,
								`Специальный - Круглый`,`Специальный - Прямоугольный`,`Специальный - Закругленный`

	values "gs_gutter_ds_profile_num" PROFILE_ROUND, PROFILE_RECTANGULAR, PROFILE_CORRUGATED

	if gs_gutter_ds_profile=`Круглый 3"` OR gs_gutter_ds_profile=`Круглый 4"` OR gs_gutter_ds_profile=`Специальный - Круглый` THEN
		parameters gs_gutter_ds_profile_num = PROFILE_ROUND
	endif

	if gs_gutter_ds_profile=`Прямоугольный 2"*3"` OR gs_gutter_ds_profile=`Прямоугольный 3"*2"` OR gs_gutter_ds_profile=`Прямоугольный 3"*4"` OR gs_gutter_ds_profile=`Прямоугольный 4"*3"` OR gs_gutter_ds_profile=`Специальный - Прямоугольный` THEN
		parameters gs_gutter_ds_profile_num = PROFILE_RECTANGULAR
	endif

	if gs_gutter_ds_profile=`Закругленный 2"*3"` OR gs_gutter_ds_profile=`Закругленный 3"*2"` OR gs_gutter_ds_profile=`Закругленный 3"*4"` OR gs_gutter_ds_profile=`Закругленный 4"*3"` OR gs_gutter_ds_profile=`Специальный - Закругленный` THEN
		parameters gs_gutter_ds_profile_num = PROFILE_CORRUGATED
	endif

else

	values "gs_gutter_ds_profile" `Круглый`,`Прямоугольный`,`Закругленный`
	values "gs_gutter_ds_profile_num" PROFILE_ROUND, PROFILE_RECTANGULAR, PROFILE_CORRUGATED

	if GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
		gs_gutter_ds_profile_num = PROFILE_RECTANGULAR
		if gs_gutter_ds_profile = `Круглый`				then gs_gutter_ds_profile_num = PROFILE_ROUND
		if gs_gutter_ds_profile = `Закругленный`			then gs_gutter_ds_profile_num = PROFILE_CORRUGATED
		parameters gs_gutter_ds_profile_num = gs_gutter_ds_profile_num
	else
		gs_gutter_ds_profile = `Прямоугольный`
		if gs_gutter_ds_profile_num = PROFILE_ROUND			then gs_gutter_ds_profile = `Круглый`
		if gs_gutter_ds_profile_num = PROFILE_CORRUGATED	then gs_gutter_ds_profile = `Закругленный`
		parameters gs_gutter_ds_profile = gs_gutter_ds_profile
	endif

endif


	IF gs_gutter_p_num <> 1 THEN	! Half Round
		HIDEPARAMETER "gs_gutter_p_r"
	ENDIF

	IF gs_gutter_p_num <> 3 AND  gs_gutter_p_num <> 2 THEN		!!(U Style , K Style)
		HIDEPARAMETER "gs_gutter_p_w"
		HIDEPARAMETER "gs_gutter_p_h"
	ENDIF

	IF gs_slope_mode_m = 1 THEN 	!!(°)
	   HIDEPARAMETER "gs_gutter_slope_in_percent"

	   HIDEPARAMETER "gs_gutter_roof_pitch_in_perc"
	ENDIF

	IF gs_slope_mode_m = 2 THEN 	!!(%)
	   HIDEPARAMETER "gs_gutter_slope_in_degree"

	   HIDEPARAMETER "gs_gutter_roof_angle"
	ENDIF

	IF GLOB_MODPAR_NAME = "gs_gutter_slope_in_degree" THEN
		PARAMETERS gs_gutter_slope_in_percent = SLOPE_PERC		!!!TAN ( SLOPE_ANG) * 100
	ENDIF


	IF GLOB_MODPAR_NAME = "gs_gutter_slope_in_percent" THEN
		PARAMETERS gs_gutter_slope_in_degree = SLOPE_ANG		!!!ATN ( gs_gutter_slope_in_percent / 100)
	ENDIF


	IF GLOB_MODPAR_NAME = "gs_gutter_roof_angle" THEN
		PARAMETERS gs_gutter_roof_pitch_in_perc = PITCH_PERC
	ENDIF

	IF GLOB_MODPAR_NAME = "gs_gutter_roof_pitch_in_perc" THEN
		PARAMETERS gs_gutter_roof_angle = PITCH_ANG
	ENDIF

	VALUES "gs_gutter_p_w" RANGE (0,)
	VALUES "gs_gutter_p_h" RANGE (0,)
	VALUES "gs_gutter_p_r" RANGE (0,)

	VALUES "gs_gutter_roof_angle" RANGE [0,90)
	VALUES "gs_gutter_slope_in_degree" RANGE [0,90)
	VALUES "gs_gutter_lgth" RANGE (0,)
	VALUES "gs_gutter_endcap_l_ang" RANGE (-90,90)
	VALUES "gs_gutter_endcap_r_ang" RANGE (-90,90)

	! *** End Cap Angles ***************************************************

	If GLOB_MODPAR_NAME="gs_gutter_endcap_l_ang" Then parameters gs_gutter_endcap_l_ang2=gs_gutter_endcap_l_ang+90
	If GLOB_MODPAR_NAME="gs_gutter_endcap_l_ang2" Then parameters gs_gutter_endcap_l_ang=gs_gutter_endcap_l_ang2-90

	If GLOB_MODPAR_NAME="gs_gutter_endcap_r_ang" Then parameters gs_gutter_endcap_r_ang2=gs_gutter_endcap_r_ang+90
	If GLOB_MODPAR_NAME="gs_gutter_endcap_r_ang2" Then parameters gs_gutter_endcap_r_ang=gs_gutter_endcap_r_ang2-90

	IF gs_gutter_endcap_l_ang2 <270 AND gs_gutter_endcap_l_ang2 > 179 THEN PARAMETERS gs_gutter_endcap_l_ang2=179
	IF gs_gutter_endcap_l_ang2 >270 AND gs_gutter_endcap_l_ang2 < 360 THEN PARAMETERS gs_gutter_endcap_l_ang2=1

	IF gs_gutter_endcap_r_ang2 <270 AND gs_gutter_endcap_r_ang2 > 179 THEN PARAMETERS gs_gutter_endcap_r_ang2=179
	IF gs_gutter_endcap_r_ang2 >270 AND gs_gutter_endcap_r_ang2 < 360 THEN PARAMETERS gs_gutter_endcap_r_ang2=1


	IF gs_gutter_guard=0 THEN HIDEPARAMETER "gs_gutter_guard_mat"


	! *** Length for 3D Hotspot ********************************************

	if GLOB_MODPAR_NAME = "gs_gutter_lgth" then
		parameters gs_gutter_lgth2 = gs_gutter_lgth/(COS(gs_gutter_slope_in_degree))
		parameters gs_gutter_lgthZ = (gs_gutter_lgth/2 * TAN(gs_gutter_slope_in_degree))
	endif

	if GLOB_MODPAR_NAME = "gs_gutter_lgth2" then
		parameters gs_gutter_lgth = gs_gutter_lgth2 * COS(gs_gutter_slope_in_degree)
		parameters gs_gutter_lgthZ = (gs_gutter_lgth2/2 * SIN(gs_gutter_slope_in_degree))
	endif

	if GLOB_MODPAR_NAME = "gs_gutter_slope_in_degree" then
		parameters gs_gutter_lgth2 = gs_gutter_lgth/(COS(gs_gutter_slope_in_degree))
		parameters gs_gutter_lgthZ= gs_gutter_lgth/2 * TAN(gs_gutter_slope_in_degree)
	endif

	if GLOB_MODPAR_NAME = "gs_gutter_slope_in_percent" then
		parameters gs_gutter_lgth2 = SQR(gs_gutter_lgth^2 + (gs_gutter_lgth * gs_gutter_slope_in_percent/100)^2)
		parameters gs_gutter_lgthZ = SLOPE/2
	endif

	if GLOB_MODPAR_NAME = "gs_gutter_lgthZ" then
		parameters gs_gutter_slope_in_degree = ATN(gs_gutter_lgthZ/(gs_gutter_lgth/2))
		parameters gs_gutter_lgth2 = gs_gutter_lgthZ * 2/SIN(gs_gutter_slope_in_degree)
		parameters gs_gutter_slope_in_percent = gs_gutter_lgthZ/(gs_gutter_lgth/2) * 100
	endif

! *** A,B,ZZYZX ****************************************************************

	parameters A = gs_gutter_lgth
	lock "A"
	parameters B = gs_gutter_p_w
	lock "B"
	if gs_gutter_p_num = 1 then		!!(Half Round)
		parameters ZZYZX = gs_gutter_p_r
		else
		parameters ZZYZX = gs_gutter_p_h
	endif
	lock "ZZYZX"
	ac_bottomlevel = ac_toplevel - zzyzx
	parameters	ac_bottomlevel = ac_bottomlevel 

! *** Hanger *******************************************************************

	if gs_gutter_h_profile_num = 1 or gs_gutter_h_profile_num = 2  or gs_gutter_h_profile_num = 3 then
		hideparameter "gs_gutter_hanger_v"	!!(Strap 1,Strap 2,Spike & Ferrule)
	endif

	values "gs_gutter_hanger_off" range [0,]
	values "gs_gutter_hanger_v" range [0,]

	if not(gs_gutter_hanger) then
		hideparameter "gs_gutter_h_profile",
						"gs_gutter_h_profile_num",
						"gs_gutter_hanger_v",
						"gs_gutter_hanger_dist",
						"gs_gutter_hanger_off",
						"gs_gutter_hanger_pos",
						"gs_gutter_hanger_mat",
						"gs_gutter_roof_angle",
						"gs_gutter_roof_pitch_in_perc",
						"gs_gutter_ds_h_positioning"

		lock "gs_gutter_h_profile",
						"gs_gutter_h_profile_num",
						"gs_gutter_hanger_v",
						"gs_gutter_hanger_dist",
						"gs_gutter_hanger_off",
						"gs_gutter_hanger_pos",
						"gs_gutter_hanger_mat",
						"gs_gutter_roof_angle",
						"gs_gutter_roof_pitch_in_perc",
						"gs_gutter_ds_h_positioning"

	endif

! *** Downspout ****************************************************************

	IF GLOB_MODPAR_NAME = "gs_gutter_aa" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" THEN
		PARAMETERS 	gs_gutter_aa2 = gs_gutter_aa*2
					gs_gutter_aa2 = gs_gutter_aa*2
	ENDIF

	IF GLOB_MODPAR_NAME = "gs_gutter_aa2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" THEN
		PARAMETERS 	gs_gutter_aa = gs_gutter_aa2/2
					gs_gutter_aa = gs_gutter_aa2/2
	ENDIF

	IF GLOB_MODPAR_NAME = "gs_gutter_bb" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" THEN
		PARAMETERS 	gs_gutter_bb2 = gs_gutter_bb*2
					gs_gutter_bb2 = gs_gutter_bb*2
	ENDIF

	IF GLOB_MODPAR_NAME = "gs_gutter_bb2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" THEN
		PARAMETERS 	gs_gutter_bb = gs_gutter_bb2/2
					gs_gutter_bb = gs_gutter_bb2/2
	ENDIF

	IF GLOB_MODPAR_NAME = "gs_gutter_dd" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" THEN
		PARAMETERS 	gs_gutter_dd2 = gs_gutter_dd*2
					gs_gutter_dd2 = gs_gutter_dd*2
	ENDIF

	IF GLOB_MODPAR_NAME = "gs_gutter_dd2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" THEN
		PARAMETERS 	gs_gutter_dd = gs_gutter_dd2/2
					gs_gutter_dd = gs_gutter_dd2/2
	ENDIF

	IF gs_gutter_p_num=1 THEN		!!(Half Round)
		VALUES "gs_gutter_aa" RANGE (0,gs_gutter_lgth-0.01]
		VALUES "gs_gutter_aa2" RANGE (0,gs_gutter_lgth-0.01]
		VALUES "gs_gutter_bb" RANGE (0,gs_gutter_p_r]
		VALUES "gs_gutter_bb2" RANGE (0,gs_gutter_p_r*2]
		VALUES "gs_gutter_dd" RANGE (0,gs_gutter_p_r]
		VALUES "gs_gutter_dd2" RANGE (0,gs_gutter_p_r*2]
		ELSE
		VALUES "gs_gutter_aa" RANGE (0,gs_gutter_lgth-0.01]
		VALUES "gs_gutter_aa2" RANGE (0,gs_gutter_lgth-0.01]
		VALUES "gs_gutter_bb" RANGE (0,gs_gutter_p_w/2]
		VALUES "gs_gutter_bb2" RANGE (0,gs_gutter_p_w]
		VALUES "gs_gutter_dd" RANGE (0,gs_gutter_p_w/2]
		VALUES "gs_gutter_dd2" RANGE (0,gs_gutter_p_w]
	ENDIF

	IF gs_gutter_ds_profile_num = 1 THEN
		HIDEPARAMETER "gs_gutter_aa2"
		HIDEPARAMETER "gs_gutter_bb2"
	ELSE
		HIDEPARAMETER "gs_gutter_dd2"
	ENDIF

	! *** Hole x Position ***
	IF gs_gutter_ds_profile_num=1 THEN	!!! Round Profile
	VALUES "gs_gutter_downspout_x_pos" RANGE [MAX(gs_gutter_dd+0.01,gs_gutter_dd+0.01-gs_gutter_p_w*TAN(gs_gutter_endcap_r_ang)),MIN(gs_gutter_lgth-gs_gutter_dd-0.01,gs_gutter_lgth-gs_gutter_dd-0.01-gs_gutter_p_w*TAN(gs_gutter_endcap_l_ang))]
	ELSE
	VALUES "gs_gutter_downspout_x_pos" RANGE [MAX(gs_gutter_aa+0.01,gs_gutter_aa+0.01-gs_gutter_p_w*TAN(gs_gutter_endcap_r_ang)),MIN(gs_gutter_lgth-gs_gutter_aa-0.01,gs_gutter_lgth-gs_gutter_aa-0.01-gs_gutter_p_w*TAN(gs_gutter_endcap_l_ang))]
	ENDIF

	if not(gs_gutter_downspout) then
		hideparameter "gs_gutter_ds_profile",
						"gs_gutter_ds_profile_num",
						"gs_gutter_downspout_x_pos",
						"gs_gutter_strainer",
						"gs_gutter_strainer_mat",
						"gs_gutter_aa2",
						"gs_gutter_bb2",
						"gs_gutter_dd2"
		lock "gs_gutter_ds_profile",
						"gs_gutter_downspout_x_pos",
						"gs_gutter_ds_profile_num",
						"gs_gutter_strainer",
						"gs_gutter_strainer_mat",
						"gs_gutter_aa2",
						"gs_gutter_bb2",
						"gs_gutter_dd2"
	endif

! *** Strainer *****************************************************************

	IF gs_gutter_strainer=0 THEN HIDEPARAMETER "gs_gutter_strainer_mat"

! *** 3D Representation ********************************************************

	VALUES "gs_resol" RANGE [4,)
	IF gs_detlevel_3D_m = 1  THEN		! Simple
		LOCK	"gs_gutter_guard","gs_gutter_guard_mat"
		LOCK	"gs_gutter_hanger_mat"
		LOCK	"gs_gutter_strainer","gs_gutter_strainer_mat"
		LOCK	"gs_resol"
	ENDIF

! *** 2D Representation ********************************************************

	IF gs_cont_pen= 0 THEN PARAMETERS gs_cont_pen=2
	IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=91

	IF gs_detlevel_3D_m = 1 and gs_detlevel_2D_m = 1  THEN	!!2D-Simple and 3D-Simple
		HIDEPARAMETER "gs_gutter_hanger","gs_gutter_h_profile","gs_gutter_hanger_v","gs_gutter_hanger_dist","gs_gutter_hanger_off","gs_gutter_hanger_pos"
	ENDIF

