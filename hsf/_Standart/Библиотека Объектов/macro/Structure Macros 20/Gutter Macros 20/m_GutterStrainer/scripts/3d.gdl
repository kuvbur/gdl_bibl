
IF gs_detlevel_3D_m=0 OR gs_detlevel_3D_m=1 THEN END

PEN gs_cont_pen
MATERIAL gs_gutter_strainer_mat

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

IF gs_gutter_ds_profile_num=1 THEN

		!!!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
		!!!>>> Strainer for Round Profile >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

		offset = gs_gutter_dd2 / 50
		alsorad = gs_gutter_dd2 / 2 - offset
		fejrad = alsorad * 1.2
		IF gs_gutter_p_num=2 THEN
			alsomag = fejrad * 1.3 * 2
			ELSE
			alsomag = fejrad * 1.3
		ENDIF
		drotatm = gs_gutter_dd2 / 25
		felsorad = gs_gutter_dd2 / 7
		felsovast = drotatm / 2
		gymag = gs_gutter_dd2 / 5
		gyvast = drotatm / 2

		a = SQR(fejrad^2-alsorad^2)
		alfa = ASN(alsorad/fejrad)
		b = SQR(fejrad^2-felsorad^2)
		beta = ASN(felsorad/fejrad)


		belogas = alsomag / 3

		ADDZ COS(alfa)*fejrad
		!ADDZ -gs_gutter_p_r+a+alsomag-belogas
		ROTY 90

				egyseg = (180 - beta - alfa) / fejszegm

				fok = 360 / drotszam

				FOR k = 0 TO drotszam

				PLANE_          5,
					a + gymag, alsorad,  drotatm/2, 1,
					a + alsomag, alsorad,  drotatm/2, 1,
					a + alsomag, alsorad, -drotatm/2, 1,
					a + gymag, alsorad, -drotatm/2, 0,
					a + gymag, alsorad,  drotatm/2, 0

				mask = 0
				mask2 = 0

						FOR i = 0 TO fejszegm-1

						IF i = fejszegm - 1 THEN mask = 1

						IF i = 0 THEN mask2 = 1
						IF i > 0 THEN mask2 = 0


						PLANE_                               5,
							COS(i*egyseg + alfa)*fejrad,     SIN(i*egyseg + alfa)*fejrad, drotatm/2, 1,
							COS((i+1)*egyseg + alfa)*fejrad, SIN((i+1)*egyseg + alfa)*fejrad, drotatm/2, mask,
							COS((i+1)*egyseg + alfa)*fejrad, SIN((i+1)*egyseg + alfa)*fejrad, -drotatm/2, 1,
							COS(i*egyseg + alfa)*fejrad,     SIN(i*egyseg + alfa)*fejrad, -drotatm/2, mask2,
							COS(i*egyseg + alfa)*fejrad,     SIN(i*egyseg + alfa)*fejrad, drotatm/2, 0

						NEXT i

				ROTX fok

				NEXT k

				DEL drotszam + 1

			!!!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
			!!!>>> TOP >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

				ADDX COS(180)*b - felsovast/2
				ROTY 90

					RESOL drotszam
					ADDz felsovast/2
						CIRCLE  felsorad+(felsorad-felsorad*COS(18))
					DEL 1
				DEL 2

			!!!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
			!!!>>> RING >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>


			ADDX COS(alfa)*fejrad

				!!>>> Ring
				ROTY 90
				RESOL drotszam

				EXTRUDE 2, 0,0,gymag, 16+32,
					0,     0,  901,
					alsorad + gyvast,   360, 4000

				DEL 1


			DEL 1

		DEL 2

ELSE

	!!!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
	!!!>>> Strainer for Rectangular and Corrugated Profile >>>>>>>>>>>>>>>>>>>>>>>>

	gymag = MIN(gs_gutter_aa,gs_gutter_BB) / 5
	drotatm = MIN(gs_gutter_aa,gs_gutter_BB) / 10

	IF gs_gutter_p_num=2 THEN
		LegLength=MIN(gs_gutter_aa,gs_gutter_BB)*2/3 * 2
		ELSE
		LegLength=MIN(gs_gutter_aa,gs_gutter_BB)*2/3
	ENDIF

!	LegLength=MIN(gs_gutter_aa,gs_gutter_BB)*2/3

	!!!>>> TOP >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

	ADDz MIN(gs_gutter_aa,gs_gutter_BB)+gymag
	POLY_ 5,
		-gs_gutter_aa/5, -gs_gutter_bb/5,1,
		gs_gutter_aa/5, -gs_gutter_bb/5,1,
		gs_gutter_aa/5, gs_gutter_bb/5,1,
		-gs_gutter_aa/5, gs_gutter_bb/5,1,
		-gs_gutter_aa/5, -gs_gutter_bb/5,-1

	DEL 1

	!!!>>> TOP RINGS >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

	ADDZ gymag*SIN(45)
		EXTRUDE 9, 0,0,drotatm, 16+32,
			-gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),0,
			gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),0,
			gs_gutter_aa/2+gymag*SIN(45), -gs_gutter_bb/5,0,
			gs_gutter_aa/2+gymag*SIN(45), gs_gutter_bb/5,0,
			gs_gutter_aa/5, gs_gutter_bb/2+gymag*SIN(45),0,
			-gs_gutter_aa/5, gs_gutter_bb/2+gymag*SIN(45),0,
			-gs_gutter_aa/2-gymag*SIN(45), gs_gutter_bb/5,0,
			-gs_gutter_aa/2-gymag*SIN(45), -gs_gutter_bb/5,0,
			-gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),-1
	DEL 1

	ADDz gymag*SIN(45)+(MIN(gs_gutter_aa,gs_gutter_BB)+gymag-gymag*SIN(45)-gymag)/2-drotatm/2
		EXTRUDE 9, 0,0,drotatm, 16+32,
			-gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),0,
			gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),0,
			gs_gutter_aa/2+gymag*SIN(45), -gs_gutter_bb/5,0,
			gs_gutter_aa/2+gymag*SIN(45), gs_gutter_bb/5,0,
			gs_gutter_aa/5, gs_gutter_bb/2+gymag*SIN(45),0,
			-gs_gutter_aa/5, gs_gutter_bb/2+gymag*SIN(45),0,
			-gs_gutter_aa/2-gymag*SIN(45), gs_gutter_bb/5,0,
			-gs_gutter_aa/2-gymag*SIN(45), -gs_gutter_bb/5,0,
			-gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),-1
	DEL 1

	ADDZ MIN(gs_gutter_aa,gs_gutter_BB)-drotatm
		EXTRUDE 9, 0,0,drotatm, 16+32,
			-gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),0,
			gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),0,
			gs_gutter_aa/2+gymag*SIN(45), -gs_gutter_bb/5,0,
			gs_gutter_aa/2+gymag*SIN(45), gs_gutter_bb/5,0,
			gs_gutter_aa/5, gs_gutter_bb/2+gymag*SIN(45),0,
			-gs_gutter_aa/5, gs_gutter_bb/2+gymag*SIN(45),0,
			-gs_gutter_aa/2-gymag*SIN(45), gs_gutter_bb/5,0,
			-gs_gutter_aa/2-gymag*SIN(45), -gs_gutter_bb/5,0,
			-gs_gutter_aa/5, -gs_gutter_bb/2-gymag*SIN(45),-1
	DEL 1

	ADDZ -gymag

		!!!>>> BOTTOM RING >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

		EXTRUDE 9, 0,0,gymag, 16+32,
			-gs_gutter_aa/2+drotatm*COS(45), -gs_gutter_bb/2,0,
			gs_gutter_aa/2-drotatm*COS(45), -gs_gutter_bb/2,0,
			gs_gutter_aa/2, -gs_gutter_bb/2+drotatm*COS(45),0,
			gs_gutter_aa/2, gs_gutter_bb/2-drotatm*COS(45),0,
			gs_gutter_aa/2-drotatm*COS(45), gs_gutter_bb/2,0,
			-gs_gutter_aa/2+drotatm*COS(45), gs_gutter_bb/2,0,
			-gs_gutter_aa/2, gs_gutter_bb/2-drotatm*COS(45),0,
			-gs_gutter_aa/2, -gs_gutter_bb/2+drotatm*COS(45),0,
			-gs_gutter_aa/2+drotatm*COS(45), -gs_gutter_bb/2,-1


		!!!>>> HEAD AND LEGS >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

		FOR kk=1 to 2
			ADDx gs_gutter_aa/2
				ROTy 90
					POLY_	5,				!!!>>>LEG
						0,-drotatm/2,1,
						LegLength,-drotatm/2,1,
						LegLength,drotatm/2,1,
						0,drotatm/2,1,
						0,-drotatm/2,-1
				DEL 2


				ADDz gymag					!!!>>>Head
				Addy gs_gutter_bb/5
				ROTx 90
					EXTRUDE 4, 0,0,drotatm, 16+32,
					gs_gutter_aa/2,0,0,
					gs_gutter_aa/2+gymag*SIN(45),gymag*SIN(45),0,
					gs_gutter_aa/2+gymag*SIN(45),MIN(gs_gutter_aa,gs_gutter_BB),0,
					gs_gutter_aa/5,MIN(gs_gutter_aa,gs_gutter_BB)+gymag,0
				DEL 3

				ADDz gymag					!!!>>>Head
				Addy -gs_gutter_bb/5+drotatm
				ROTx 90
					EXTRUDE 4, 0,0,drotatm, 16+32,
					gs_gutter_aa/2,0,0,
					gs_gutter_aa/2+gymag*SIN(45),gymag*SIN(45),0,
					gs_gutter_aa/2+gymag*SIN(45),MIN(gs_gutter_aa,gs_gutter_BB),0,
					gs_gutter_aa/5,MIN(gs_gutter_aa,gs_gutter_BB)+gymag,0
				DEL 3

			ROTz 180
		NEXT kk
		DEL 2

		FOR kk=1 to 2

				ADDy gs_gutter_bb/2
				ROTz 90
				ROTy 90
					POLY_	5,					!!!>>>LEG
						0,-drotatm/2,1,
						LegLength,-drotatm/2,1,
						LegLength,drotatm/2,1,
						0,drotatm/2,1,
						0,-drotatm/2,-1
				DEL 3

				ADDz gymag					!!!>>>Head
				ADDx -gs_gutter_aa/5
				ROTy 90
				ROTz 90
					EXTRUDE 4, 0,0,drotatm, 16+32,
					gs_gutter_bb/2,0,0,
					gs_gutter_bb/2+gymag*SIN(45),gymag*SIN(45),0,
					gs_gutter_bb/2+gymag*SIN(45),MIN(gs_gutter_aa,gs_gutter_BB),0,
					gs_gutter_bb/5,MIN(gs_gutter_aa,gs_gutter_BB)+gymag,0
				DEL 4

				ADDz gymag					!!!>>>Head
				ADDx gs_gutter_aa/5-drotatm
				ROTy 90
				ROTz 90
					EXTRUDE 4, 0,0,drotatm, 16+32,
					gs_gutter_bb/2,0,0,
					gs_gutter_bb/2+gymag*SIN(45),gymag*SIN(45),0,
					gs_gutter_bb/2+gymag*SIN(45),MIN(gs_gutter_aa,gs_gutter_BB),0,
					gs_gutter_bb/5,MIN(gs_gutter_aa,gs_gutter_BB)+gymag,0
				DEL 4

			ROTz 180
		NEXT kk
		DEL 2

		FOR tt=1 TO 2
			FOR kk=1 TO 2
				ADD gs_gutter_aa/2-drotatm*COS(45)/2,gs_gutter_bb/2-drotatm*COS(45)/2,0
				ROTz 45
				ROTy 90
					POLY_	5,
						0,-drotatm/2,1,
						LegLength,-drotatm/2,1,
						LegLength,drotatm/2,1,
						0,drotatm/2,1,
						0,-drotatm/2,-1
				DEL 3
			MULx -1
			NEXT kk
			DEL 2
		MULy -1
		NEXT tt
		DEL 2
	DEL 1

ENDIF
