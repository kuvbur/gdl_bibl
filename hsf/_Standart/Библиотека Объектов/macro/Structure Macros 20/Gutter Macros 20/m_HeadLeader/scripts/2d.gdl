
pen gs_cont_pen
line_type gs_line_type
fill gs_fill_type

! *****************************************************************************
! Head Leader
! *****************************************************************************

ACCUTPLANEHEIGHT = (GLOB_CSTORY_ELEV+GLOB_CUTPLANES_INFO [1]) - vertical_offset

if  gs_gutter_ds_profile_num=1 then								! Round
	gs_gutter_temp=gs_gutter_dd
else
	gs_gutter_temp=gs_gutter_bb
endif

! -----------------------------------------------------------------------------
! FOOR PLAN DISPLAY TYPE
! -----------------------------------------------------------------------------
! -----------------------------------------------------------------------------
! Projected
! -----------------------------------------------------------------------------
if iFloorPlanDisplayType = DISPLAY_PROJECTED then
	if ACCUTPLANEHEIGHT < 0 then
		end
	else
		line_type gs_line_type
		add2 -HL_Width/2, -HL_Depth+gs_gutter_temp
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,		0,		1,
			HL_Width,	0,		1,
			HL_Width,	HL_Depth,	1,
			0,		HL_Depth,	1,
			0,		0,		-1
		del 1
	endif
endif

! -----------------------------------------------------------------------------
! Projected With Overhead
! -----------------------------------------------------------------------------
if ifloorPlanDisplayType = DISPLAY_OVERHEAD_PROJ then

	if ACCUTPLANEHEIGHT < 0 then
		line_type overhead_line_type
		_frame_fill = 1+4
	else
		line_type gs_line_type
		_frame_fill = 1+2+4+64
	endif

	add2 -HL_Width/2, -HL_Depth+gs_gutter_temp
		poly2_b 5, _frame_fill, gs_fill_pen, gs_back_pen,
			0,		0,		1,
			HL_Width,	0,		1,
			HL_Width,	HL_Depth,	1,
			0,		HL_Depth,	1,
			0,		0,		-1
	del 1

	line_type gs_line_type
endif

! -----------------------------------------------------------------------------
! Symbolic
! -----------------------------------------------------------------------------
if iFloorPlanDisplayType = DISPLAY_SYMBOLIC then

endif

! -----------------------------------------------------------------------------
! Outlines Only
! -----------------------------------------------------------------------------
if iFloorPlanDisplayType = DISPLAY_OUTLINES then
	line_type gs_line_type
	add2 -HL_Width/2, -HL_Depth+gs_gutter_temp
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,		0,		1,
			HL_Width,	0,		1,
			HL_Width,	HL_Depth,	1,
			0,		HL_Depth,	1,
			0,		0,		-1
	del 1
endif

! -----------------------------------------------------------------------------
! Overhead All
! -----------------------------------------------------------------------------
if ifloorPlanDisplayType = DISPLAY_OVERHEAD_ALL then
	line_type overhead_line_type
	add2 -HL_Width/2, -HL_Depth+gs_gutter_temp
	poly2_b 5, 1+4+64, gs_fill_pen, gs_back_pen,
		0,		0,		1,
		HL_Width,	0,		1,
		HL_Width,	HL_Depth,	1,
		0,		HL_Depth,	1,
		0,		0,		-1
	del 1
endif


! *****************************************************************************
! Downspout
! *****************************************************************************
if ACCUTPLANEHEIGHT < 0 or ifloorPlanDisplayType = DISPLAY_OVERHEAD_ALL then
	line_type overhead_line_type
	_frame_fill = 1+4
else
	line_type gs_line_type
	_frame_fill = 1+2+4+64
endif

CALL "m_DSProfiles" PARAMETERS gs_gutter_ds_profile_num=gs_gutter_ds_profile_num,
									 gs_mask=0,
									 gs_gutter_aa=gs_gutter_aa,
									 gs_gutter_bb=gs_gutter_bb,
									 gs_gutter_dd=gs_gutter_dd

poly2_b{2} nsp/3,	_frame_fill,	gs_fill_pen, gs_back_pen,
	0,0,0,
	get (nsp)


! *****************************************************************************
! Hotspots
! *****************************************************************************

unID=gs_unID

if iFloorPlanDisplayType <> DISPLAY_SYMBOLIC then
	if gs_HtpsMacro=0 then HOTSPOT2 0, 0, unID : unID=unID+1		! Hotspots not used in  Downspout Complex

	! -----------------------------------------------------------------------------
	! Head Leader Width
	! -----------------------------------------------------------------------------
	add2 0,gs_gutter_temp
		HOTSPOT2 0,		0,	unID,	HL_Width2,	1+128	: unID=unID+1
		HOTSPOT2 -1,	0,	unID,	HL_Width2,	3		: unID=unID+1
		HOTSPOT2 HL_Width2,	0,	unID,	HL_Width2,	2		: unID=unID+1

		HOTSPOT2 0,		0,	unID,	HL_Width2,	1+128	: unID=unID+1
		HOTSPOT2 1,		0,	unID,	HL_Width2,	3		: unID=unID+1
		HOTSPOT2 -HL_Width2,	0,	unID,	HL_Width2,	2		: unID=unID+1
	del 1

	! -----------------------------------------------------------------------------
	! Head Leader Depth
	! -----------------------------------------------------------------------------
	add2 HL_Width/2,gs_gutter_temp
		HOTSPOT2 0, 0,		unID, HL_Depth, 1+128	: unID=unID+1
		HOTSPOT2 0, 1,		unID, HL_Depth, 3		: unID=unID+1
		HOTSPOT2 0, -HL_Depth,	unID, HL_Depth, 2		: unID=unID+1
	del 1

	add2 -HL_Width/2,gs_gutter_temp
		HOTSPOT2 0, 0,		unID, HL_Depth, 1+128	: unID=unID+1
		HOTSPOT2 0, 1,		unID, HL_Depth, 3		: unID=unID+1
		HOTSPOT2 0, -HL_Depth,	unID, HL_Depth, 2		: unID=unID+1
	del 1
endif

! -----------------------------------------------------------------------------
! Downspout
! -----------------------------------------------------------------------------
if gs_HtpsMacro=0 then 										! Hotspots not used in  Downspout Complex
	if  gs_gutter_ds_profile_num=1 then						! Round

		FOR kk=1 to 4
			! --- RADIUS ---
			HOTSPOT2 0, 0, unID, gs_gutter_dd, 1+128, gs_gutter_dd2 : unID=unID+1
			HOTSPOT2 -1, 0, unID, gs_gutter_dd, 3, gs_gutter_dd2 : unID=unID+1
			HOTSPOT2 gs_gutter_dd, 0, unID, gs_gutter_dd, 2, gs_gutter_dd2 : unID=unID+1
			ROT2 90
			NEXT kk
		del 4

	else

		HOTSPOT2 0, gs_gutter_bb, unID : unID=unID+1

		! --- WIDTH ---
		add2 0,gs_gutter_bb
			HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 -1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1

			HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 -gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1
		del 1
		add2 0,-gs_gutter_bb
			HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 -1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1

			HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
			HOTSPOT2 -gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1
		del 1

		! --- LENGTH ---
		add2 gs_gutter_aa,0
			HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, -1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1

			HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, 1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, -gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1
		del 1
		add2 -gs_gutter_aa,0
			HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, -1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1

			HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, 1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
			HOTSPOT2 0, -gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1
		del 1

	endif
endif
