
!!Rail Post with Balusters

EPS=0.0001


IF pt_m=1 THEN
	dpost=pw/2
ELSE
	dpost=pd/2
ENDIF


!!!!!! Railing Length

IF LengthSize = 1 THEN
	lra2=(lra - (hroL + hroR))
	lrah2=(lrah - (hroLDiagonal + hroRDiagonal))
	templra=lra2
	templrah=lrah2
	XStartPoslra=hrol
	ZStartPoslra=hrol*tan(ani)
	XStartPoslrah=hroLDiagonal
ELSE
	templra=lra
	templrah=lrah
	XStartPoslra=0
	ZStartPoslra=0
	XStartPoslrah=0
ENDIF



!!PARAMETER INPUT CALCULATION!!

IF ptyp_m=1 THEN
	HIDEPARAMETER "ab"
	HIDEPARAMETER "rb"
	HIDEPARAMETER "res"
ENDIF

IF ptyp_m=2 THEN
	IF ABS(rb)>EPS THEN
		PARAMETERS ab=(lra*180)/(rb*pi)
		LOCK "ab"
	ENDIF
ENDIF

IF ptyp_m=3 THEN
	PARAMETERS lra=(rb*pi*ab)/180
	LOCK "lra"
ENDIF


!!POST AND BOARD CALCULATION!!
!!POST SPACING CALCULATION FOR STRAIGHT AND CURVED RAILS

IF rom_m=2 AND rmm_m=2 THEN
	distpost=templrah/(np-1)
ELSE
	distpost=templra/(np-1)
ENDIF

IF ABS(ab-360)<EPS and ABS(ani)<EPS THEN
	anglepost=ab/np
ELSE
	anglepost=ab/(np-1)
ENDIF


!!BOARD SPACING CALCULATION FOR STRAIGHT AND CURVED RAILS

IF pt_m=1 THEN
	IF ABS(rb)>EPS THEN		!!rb<>0
		dpostangle=180*(pw/2)/(rb*pi)
	ELSE
		dpostangle=0
	ENDIF
ELSE
	IF ABS(rb)>EPS THEN		!!rb<>0
		dpostangle=180*(pd/2)/(rb*pi)
	else
		dpostangle=0
	ENDIF
ENDIF

IF bt_m=1 THEN
	dboard=bw
	IF ABS(rb)>EPS THEN		!!rb<>0
		dboardangle=180*bw/(rb*pi)
	else
		dboardangle=0
	ENDIF
ELSE
	dboard=bd
	IF ABS(rb)>EPS THEN		!!rb<>0
		dboardangle=180*bd/(rb*pi)
	ELSE
		dboardangle=0
	ENDIF
ENDIF

distboard=(distpost-2*dpost-nb*dboard)/(nb+1)
distboardangle=(anglepost-2*dpostangle-nb*dboardangle)/(nb+1)



!!ADDITIONAL CALCULATION FOR CURVED AND INCLINED RAILS!!

IF ptyp_m<>1 and ABS(ab)>EPS or ABS(rb)>EPS THEN
	!!! *** General data

		alphahroL=(hroL*180)/(rb*pi) 			!Overhung angle
		alphahroR=(hroR*180)/(rb*pi) 			!Overhung angle
	deltaz=templra*sin(ani)					!Total height to rise

	!!! *** For segment points
	nalphaincl=int(ab/(360/res))+1		!number of segment points without overhung
	alphaincl=ab/nalphaincl  			!angle between segment points

		nalphahroL=int(alphahroL/alphaincl)	!number of segment points at handrail overhung
		nalphahroR=int(alphahroR/alphaincl)	!number of segment points at handrail overhung
	deltazincl=deltaz/nalphaincl 		!vertical step between segment points

	!!! *** For posts
	deltazpost=deltaz/(np-1)			!vertical step between posts

	!!! *** For Boards
	deltazboard0=((dpostangle+distboardangle+dboardangle/2)*deltaz)/ab 	!vertical first step between boards
	deltazboard=((distboardangle+dboardangle)*deltaz)/ab				!vertical step between boards
ENDIF
