
!Rail Post with Balusters

EPS=0.0001

!!! *** Post Width *** !!! ***********************************

IF pt_m=1 THEN
	dpost=pw/2
	ELSE
	dpost=pd/2
ENDIF


!!! *** Railing Length *** !!! *******************************

IF LengthSize = 1 THEN
	lra2=(lra - (hroL + hroR))
	lrah2=(lrah - (hroLDiagonal + hroRDiagonal))
	templra=lra2
	templrah=lrah2
	XStartPoslra=hrol
	ZStartPoslra=hrol*tan(ani)
	XStartPoslrah=hroLDiagonal
ELSE
	templra=lra
	templrah=lrah
	XStartPoslra=0
	ZStartPoslra=0
	XStartPoslrah=0
ENDIF


!!! *** PARAMETER INPUT CALCULATION *** !!! *******************

IF ptyp_m=1 THEN
	HIDEPARAMETER "ab"
	HIDEPARAMETER "rb"
	HIDEPARAMETER "res"
ENDIF

IF ptyp_m=2 THEN
	IF ABS(rb)>EPS THEN
		PARAMETERS ab=(lra*180)/(rb*pi)
		LOCK "ab"
	ENDIF
ENDIF

IF ptyp_m=3 THEN
	PARAMETERS lra=(rb*pi*ab)/180
	LOCK "lra"
ENDIF


!!! *** POST AND BOARD CALCULATION *** !!! *******************
!!! *** POST SPACING CALCULATION FOR STRAIGHT AND CURVED RAILS

IF rom_m=2 AND rmm_m=2 THEN
	distpost=templrah/(np-1)
ELSE
	distpost=templra/(np-1)
ENDIF

IF ABS(ab-360)<EPS and ABS(ani)<EPS THEN
	anglepost=ab/np
else
	anglepost=ab/(np-1)
endif


!!! *** BOARD SPACING CALCULATION FOR STRAIGHT AND CURVED RAILS

dpostangle = 0
if pt_m=1 then
	IF ABS(rb)>EPS THEN
		dpostangle=180*(pw/2)/(rb*pi)
	endif
else
	IF ABS(rb)>EPS THEN
		dpostangle=180*(pd/2)/(rb*pi)
	endif
endif

dboardangle = 0
if bt_m=1 then
	dboard=bw
	IF ABS(rb)>EPS THEN
		dboardangle=180*bw/(rb*pi)
	endif
else
	dboard=bd
	IF ABS(rb)>EPS THEN
		dboardangle=180*bd/(rb*pi)
	endif
endif

distboard=(distpost-2*dpost-nb*dboard)/(nb+1)
distboardangle=(anglepost-2*dpostangle-nb*dboardangle)/(nb+1)

!Sub-board height calculation

!sbhh1 	Lower Sub-board Elevation (horizontal)
!sbhh2 	Upper Sub-board Elevation (horizontal)
!sbh1	Lower Sub-board Height (vertical)
!sbh1	Upper Sub-board Height (vertical)

if bt_m=1 then
	sbhh1=ds+bw+sbh
	sbh1=sbh

	if hrt_m=1 then
		sbhh2=ds+hr-hrh-sbh-bw
		sbh2=sbh
	else
		sbhh2=ds+hr-hrd-sbh-bw
		sbh2=sbh+hrd/2
	endif
else
	sbhh1=ds+bd+sbh+bd/2
	sbh1=sbh+bd

	if hrt_m=1 then
		sbhh2=ds+hr-hrh-sbh-bd/2
		sbh2=sbh+bd/2
	else
		sbhh2=ds+hr-hrd-sbh-bd/2
		sbh2=sbh+bd/2+hrd/2
	endif
endif


!!! *** ADDITIONAL CALCULATION FOR CURVED AND INCLINED RAILS!!

if ptyp_m<>1 and ABS(ab)>EPS then

	!!! *** General data
		alphahroL=(hroL*180)/(rb*pi) 		!Overhung angle
		alphahroR=(hroR*180)/(rb*pi) 		!Overhung angle
	deltaz=lra*sin(ani)						!Total height to rise

	!!! *** For segment points
	nalphaincl=int(ab/(360/res))+1 				!number of segment points without overhung
	alphaincl=ab/nalphaincl  					!angle between segment points
		nalphahroL=int(alphahroL/alphaincl)		!number of segment points at handrail overhung
		nalphahroR=int(alphahroR/alphaincl)	!number of segment points at handrail overhung
	deltazincl=deltaz/nalphaincl 				!vertical step between segment points

	!!! *** For posts
	deltazpost=deltaz/(np-1)					!vertical step between posts

	!!! *** For Boards
	deltazboard0=((dpostangle+distboardangle+dboardangle/2)*deltaz)/ab 		!vertical first step between boards
	deltazboard0sub=((dpostangle+distboardangle/2)*deltaz)/ab 				!vertical first step between subboards
	deltazboard=((distboardangle+dboardangle)*deltaz)/ab					!vertical step between boards

endif
