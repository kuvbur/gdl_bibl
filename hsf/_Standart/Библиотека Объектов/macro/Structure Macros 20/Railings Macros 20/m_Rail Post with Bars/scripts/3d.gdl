
IF gs_detlevel_3D_m=0 THEN end

IF gs_shadow=0 THEN SHADOW OFF

templra = max(templra, EPS)
PEN gs_cont_pen
unID=1

	IF hrt_m=1 THEN		!!! HANDRAIL WIDTH
		fth=pth
		HandrailWidth=hrw
		ELSE
		fth=pd
		HandrailWidth=hrd
	ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** SIMPLE 3D *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!!!CALCULATION FOR CONNECTING ANGLE

XPosL0=(HandrailWidth/2)/tan(gs_AngleL)
XPosR0=(HandrailWidth/2)/tan(gs_AngleR)

XPosL=ABS((HandrailWidth/2)/tan(gs_AngleL))
XPosR=ABS((HandrailWidth/2)/tan(gs_AngleR))
YPosL=(XPosL*tan(ani))
YPosR=(XPosR*tan(ani))


IF gs_detlevel_3D_m=1 THEN		!!Simple

	MATERIAL pmat
	RESOL res


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)<EPS and ptyp_m=1 THEN		!!ptyp=Straight

			ADDx XStartPoslra
			ADDz ds
				PRISM_ 5,hr,
					XPosL0,fth/2,15,
					-XPosL0,-fth/2,15,
					templra+XPosR0,-fth/2,15,
					templra-XPosR0,fth/2,15,
					XPosL0,fth/2,-1
			DEL 2

		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)<EPS and ptyp_m<>1 THEN

		!!!!FRAME

			IF ab<360 THEN
					ADDz ds
					PRISM_ 10,hr,
						0,fth/2,15,
						0,-fth/2+0.00005,15,
						0,-fth/2,79,
						1,0,800,
						(rb+(fth/2))*sin(ab),rb-(rb+(fth/2))*cos(ab),1015,
						(rb-(fth/2)+0.00005)*sin(ab),rb-(rb-(fth/2)+0.00005)*cos(ab),15,
						(rb-(fth/2))*sin(ab),rb-(rb-(fth/2))*cos(ab),79,
						-cos(ab),-sin(ab),800,
						0,fth/2,1015,
						0,fth/2,-1
					DEL 1

			ELSE	!!!ANGLE=360

				ADDz ds
					PRISM_ 12,hr,
						0,-fth/2,79,
						1,0,800,
						0,2*rb+fth/2,1079,
						-1,0,800,
						0,-fth/2,1079,
						0,-fth/2,-1,

						0,fth/2,79,
						1,0,800,
						0,2*rb-fth/2,1079,
						-1,0,800,
						0,fth/2,1079,
						0,fth/2,-1
				DEL 1

			ENDIF
		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m=1 THEN			!!ptyp=Straight

			IF rom_m=1 THEN					!!!!VERTICAL Orientation

				ADD XStartPoslra,0,ZStartPoslra

					IF rmm_m=1 THEN			!!!!VERTICAL Measurement
						temp_ds=ds
						temp_hr=hr
					ELSE					!!Perpendicular Measurement
						temp_ds=dsPerp
						temp_hr=hrPerp
					ENDIF



						ROTz -90+gs_AngleL		!! CONNECTING ANGLE
						ROTy -90
							CUTPLANE
						DEL 2

						ADDx templra
						ROTz 90-gs_AngleR
						ROTy 90
							CUTPLANE
						DEL 3


							ROTx 90
							ADDz -fth/2

								PRISM_ 5,fth,
									-XPosL,temp_ds-YPosL,15,
									templra+XPosR,temp_ds+templra*tan(ani)+YPosR,15,
									templra+XPosR,temp_ds+temp_hr+templra*tan(ani)+YPosR,15,
									-XPosL,temp_ds+temp_hr-YPosL,15,
									-XPosL,temp_ds-YPosL,-1

							DEL 2

						CUTEND
						CUTEND

				DEL 1

			ELSE						!Perpendicular Orientation
										!Measurement is always perpendicular to inclination in this case

				ROTy -ani
				ADDx XStartPoslra

					ROTx 90
					ADDz -fth/2
						!!FRAME

						PRISM_ 5,fth,
						0,ds,15,
						templrah,ds,15,
						templrah,ds+hr,15,
						0,ds+hr,15,
						0,ds,-1
					DEL 2

				DEL 2

			ENDIF


		ENDIF


	!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m<>1 THEN

			!!!!FRAME

			alphai=-alphaincl
			deltazi=-deltazincl
			FOR i=1 to nalphaincl+3 step 1
			put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
			alphai=alphai+alphaincl
			deltazi=deltazi+deltazincl
			NEXT i

			TUBEA 5,nalphaincl+3,1+2+16+32,
				-fth/2,0,0,
				fth/2,0,0,
				fth/2,hr,0,
				-fth/2,hr,0,
				-fth/2,0,-1,
				get (nsp)

		ENDIF


ENDIF		!!END of SIMPLE 3D!!







!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** DETAILED 3D *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


IF gs_detlevel_3D_m=2 THEN


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


IF ABS(ani)<EPS and ptyp_m=1 THEN

	ADDX XStartPoslra

	!!3D FEEDBACK AT HOTSPOT EDITING
	IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

			ADDz ds
			MATERIAL fmat
			PRISM_ 5,hr,
				(fth/2)/tan(gs_AngleL),fth/2,15,
				-(fth/2)/tan(gs_AngleL),-fth/2,15,
				templra+(fth/2)/tan(gs_AngleR),-fth/2,15,
				templra-(fth/2)/tan(gs_AngleR),fth/2,15,
				(fth/2)/tan(gs_AngleL),fth/2,-1
			DEL 1

	ELSE	!!! DETAILED MODEL



		!! *** HANDRAIL *** !! *****************************************************

		MATERIAL fmat

		ADDx -(hroL)
		ROTz -90+gs_AngleL		!! CONNECTING ANGLE
		ROTy -90
			CUTPLANE
		DEL 3

		ADDx templra+(hroR)
		ROTz 90-gs_AngleR
		ROTy 90
			CUTPLANE
		DEL 3


		RESOL rescs

				MATERIAL fmat

				GROUP "handrail round straight"			!Groups
					ADD  -hroL-XPosL,0,ds+hr-hrd/2
					ROTy 90
					CYLIND templra+(hroL)+XPosL+(hroR)+XPosR,hrd/2
					DEL 2
				ENDGROUP


				IF hrt_m=1 THEN

					ADDz ds+hr-hrh
						PRISM_ 5,hrh,
							-(hroL)-XPosL,hrw/2,15,
							-(hroL)-XPosL,-hrw/2,15,
							templra+(hroR)+XPosR,-hrw/2,15,
							templra+(hroR)+XPosR,hrw/2,15,
							-(hroL)-XPosL,hrw/2,-1
					DEL 1

					ELSE
					PLACEGROUP "handrail round straight"
				ENDIF

		CUTEND
		CUTEND


		!! *** POST *** !! *****************************************************

		MATERIAL pmat

		ADDx -(hroL)-LengthSize*dpost
		ROTz -90+gs_AngleL		!! CONNECTING ANGLE
		ROTy -90
			CUTPLANE
		DEL 3

		ADDx templra+(hroR)+LengthSize*dpost
		ROTz 90-gs_AngleR
		ROTy 90
			CUTPLANE
		DEL 3

				MATERIAL pmat

				IF hrt_m=1 THEN			!Post height calculation
				ph=hr+ds-hrh+gs_PostBaseOverhang
				ELSE
					IF pt_m=1 THEN
					ph=hr+ds-hrd+gs_PostBaseOverhang
					ELSE
					ph=hr+ds-hrd/2+gs_PostBaseOverhang
					ENDIF
				ENDIF


				GROUP "post round straight"			!Groups
				ADDz -gs_PostBaseOverhang
					FOR i=1 to np step 1
					CYLIND ph,pd/2
					addx distpost
					NEXT i
					DEL np
				DEL 1
				ENDGROUP

				GROUP "post rect straight"
				ADDZ -gs_PostBaseOverhang
					FOR i=1 to np step 1
					PRISM_ 5,ph,
						-pw/2,-pth/2,15,
						 pw/2,-pth/2,15,
						 pw/2,pth/2,15,
						-pw/2,pth/2,15,
						-pw/2,-pth/2,-1
					addx distpost
					NEXT i
					DEL np
				DEL 1
				ENDGROUP

				IF pt_m=1 THEN
					PLACEGROUP "post rect straight"
				ELSE
					IF hrt_m=1 THEN
					PLACEGROUP "post round straight"
					ELSE
					PLACEGROUP subgroup ("post round straight","handrail round straight")
					ENDIF
				ENDIF

		CUTEND
		CUTEND



		!! *** Boards *** !! *****************************************************

		MATERIAL bmat

		ADDx -(hroL)-LengthSize*dpost
		ROTz -90+gs_AngleL		!! CONNECTING ANGLE
		ROTy -90
			CUTPLANE
		DEL 3

		ADDx templra+(hroR)+LengthSize*dpost
		ROTz 90-gs_AngleR
		ROTy 90
			CUTPLANE
		DEL 3

				!!!!!HORIZONTAL

				GROUP "board rect straight H"			!Groups
					ADDz  ds
					PRISM_ 5,bw,
						0,-bth/2,15,
						templra,-bth/2,15,
						templra,bth/2,15,
						0,bth/2,15,
						0,-bth/2,-1
					DEL 1
				ENDGROUP

				GROUP "board round straight H"
					ADDz  ds+bd/2
					ROTy 90
					CYLIND templra,bd/2
					DEL 2
				ENDGROUP

				GROUP "subboard rect straight H"
					ADDz  sbhh1
					PRISM_ 5,bw,
						0,-bth/2,15,
						templra,-bth/2,15,
						templra,bth/2,15,
						0,bth/2,15,
						0,-bth/2,-1
					DEL 1

					ADDz sbhh2
					PRISM_ 5,bw,
						0,-bth/2,15,
						templra,-bth/2,15,
						templra,bth/2,15,
						0,bth/2,15,
						0,-bth/2,-1
					DEL 1
				ENDGROUP

				GROUP "subboard round straight H"
					ADDz  sbhh1
					ROTy 90
						CYLIND templra,bd/2
					DEL 2

					ADDz  sbhh2
					ROTy 90
						CYLIND templra,bd/2
					DEL 2
				ENDGROUP


				IF bt_m= 1 THEN
					IF pt_m=1 THEN
					PLACEGROUP subgroup ("board rect straight H","post rect straight")
					ELSE
					PLACEGROUP subgroup ("board rect straight H","post round straight")
					ENDIF
				ELSE
					IF pt_m=1 THEN
					PLACEGROUP subgroup ("board round straight H","post rect straight")
					ELSE
					PLACEGROUP subgroup ("board round straight H","post round straight")
					ENDIF
				ENDIF


				!!!VERTICAL

				IF hrt_m=1 THEN			!Board height calculation
					IF bt_m=1 THEN
					bh=hr-bw-hrh
					ELSE
					bh=hr-bd/2-hrh
					ENDIF
				ELSE
					IF bt_m=1 THEN
					bh=hr-bw-hrd
					ELSE
					bh=hr-bd/2-hrd/2
					ENDIF
				ENDIF


				GROUP "board round straight V"			!Groups
					FOR k=1 to np-1 step 1
						ADD dpost+distboard+bd/2,0,ds+bd/2
						FOR i=1 to nb step 1

							CYLIND bh,bd/2

						addx distboard+bd
						NEXT i
						DEL nb+1
					addx distpost
					NEXT k
					DEL np-1
				ENDGROUP

				GROUP "board rect straight V"
					FOR l=1 to np-1 step 1
						ADD dpost+distboard+bw/2,0,ds+bw
						FOR i=1 to nb step 1

						PRISM_ 5,bh,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1

						addx distboard+bw
						NEXT i
						DEL nb+1
					addx distpost
					NEXT l
					DEL np-1
				ENDGROUP


				GROUP "subboard round straight V"
					FOR k=1 to np-1 step 1
						ADD dpost+distboard+bd/2,0,ds+bd/2
						addx -(distboard+bd)/2
						FOR i=1 to nb+1 step 1

							CYLIND sbh1,bd/2

						addx distboard+bd
						NEXT i
						DEL nb+3
					addx distpost
					NEXT k
					DEL np-1
					FOR k=1 to np-1 step 1
						ADD dpost+distboard+bd/2,0,sbhh2
						addx -(distboard+bd)/2
						FOR i=1 to nb+1 step 1

							CYLIND sbh2,bd/2

						addx distboard+bd
						NEXT i
						DEL nb+3
					addx distpost
					NEXT k
					DEL np-1
				ENDGROUP

				GROUP "subboard rect straight V"
					FOR l=1 to np-1 step 1
						ADD dpost+distboard+bw/2,0,ds+bw
						addx -(distboard+bw)/2
						FOR i=1 to nb+1 step 1

						PRISM_ 5,sbh1,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1

						addx distboard+bw
						NEXT i
						DEL nb+3
					addx distpost
					NEXT l
					DEL np-1
					FOR l=1 to np-1 step 1
						ADD dpost+distboard+bw/2,0,sbhh2+bw
						addx -(distboard+bw)/2
						FOR i=1 to nb+1 step 1

						PRISM_ 5,sbh2,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1

						addx distboard+bw
						NEXT i
						DEL nb+3
					addx distpost
					NEXT l
					DEL np-1
				ENDGROUP


				IF bt_m=1 THEN
					IF hrt_m=1 THEN
						PLACEGROUP "board rect straight V"
						PLACEGROUP "subboard rect straight V"
						ELSE
						PLACEGROUP subgroup ("board rect straight V","handrail round straight")
						PLACEGROUP subgroup ("subboard rect straight V","handrail round straight")
					ENDIF
					IF pt_m=1 THEN
						PLACEGROUP subgroup (subgroup ("subboard rect straight H","post rect straight"),"board rect straight V")
						ELSE
						PLACEGROUP subgroup (subgroup ("subboard rect straight H","post round straight"),"board rect straight V")
					ENDIF
				ELSE
					IF hrt_m=1 THEN
						PLACEGROUP subgroup ("board round straight V","board round straight H")
						PLACEGROUP subgroup (subgroup ("subboard round straight V","subboard round straight H"),"board round straight H")
						ELSE
						PLACEGROUP subgroup (subgroup ("board round straight V","handrail round straight"),"board round straight H")
						PLACEGROUP subgroup (subgroup (subgroup ("subboard round straight V","subboard round straight H"),"board round straight H"),"handrail round straight")
					ENDIF
					IF pt_m=1 THEN
						PLACEGROUP subgroup (subgroup ("subboard round straight H","post rect straight"),"board round straight V")
						ELSE
						PLACEGROUP subgroup (subgroup ("subboard round straight H","post round straight"),"board round straight V")
					ENDIF
				ENDIF

		CUTEND
		CUTEND

			KILLGROUP "handrail round straight"
			KILLGROUP "post round straight"
			KILLGROUP "post rect straight"
			KILLGROUP "board rect straight H"
			KILLGROUP "board round straight H"
			KILLGROUP "subboard rect straight H"
			KILLGROUP "subboard round straight H"
			KILLGROUP "board rect straight V"
			KILLGROUP "board round straight V"
			KILLGROUP "subboard rect straight V"
			KILLGROUP "subboard round straight V"

	ENDIF

	DEL 1

ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


	IF ABS(ani)<EPS and ptyp_m<>1 THEN


		!!3D FEEDBACK AT HOTSPOT EDITING
		IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

			!!!!FRAME
			MATERIAL fmat

				IF ab<360 THEN
						ADDz ds
						PRISM_ 10,hr,
							0,fth/2,15,
							0,-fth/2+0.00005,15,
							0,-fth/2,79,
							1,0,800,
							(rb+(fth/2))*sin(ab),rb-(rb+(fth/2))*cos(ab),1015,
							(rb-(fth/2)+0.00005)*sin(ab),rb-(rb-(fth/2)+0.00005)*cos(ab),15,
							(rb-(fth/2))*sin(ab),rb-(rb-(fth/2))*cos(ab),79,
							-cos(ab),-sin(ab),800,
							0,fth/2,1015,
							0,fth/2,-1
						DEL 1

				ELSE	!!!ANGLE=360

					ADDz ds
						PRISM_ 12,hr,
							0,-fth/2,79,
							1,0,800,
							0,2*rb+fth/2,1079,
							-1,0,800,
							0,-fth/2,1079,
							0,-fth/2,-1,

							0,fth/2,79,
							1,0,800,
							0,2*rb-fth/2,1079,
							-1,0,800,
							0,fth/2,1079,
							0,fth/2,-1
					DEL 1

				ENDIF

		ELSE
			!!! *** HANDRAIL *** !!! *************************************

			MATERIAL fmat
			RESOL res

			if (ab + alphahroL + alphahroR) < 360 then

					group "handrail round curved"				!Groups
							gamma0 = 360 / rescs
							gamma = 0
							for i = 1 to rescs step 1
								put hrd/2 * cos(gamma), rb + hrd/2 * sin(gamma), 1
								gamma = gamma + gamma0
							next i
						add  0, rb, ds + hr - hrd/2
						roty 90
						rotx 180 - ab - alphahroR
							revolve rescs+1, ab + alphahroL + alphahroR, 1+2+4+8+16+32,
							get (nsp),
								hrd/2, rb, -1
						del 3
					endgroup
					handrailgroupname = "handrail round curved"

					IF hrt_m=1 THEN
							ADDz ds+hr-hrh
							PRISM_ 9,hrh,
							(rb-(hrw/2))*sin(-alphahroL),rb-(rb-(hrw/2))*cos(alphahroL),15,
							(rb+(hrw/2)-0.00005)*sin(-alphahroL),rb-(rb+(hrw/2)-0.000005)*cos(alphahroL),15,
							(rb+(hrw/2))*sin(-alphahroL),rb-(rb+(hrw/2))*cos(alphahroL),79,
							cos(alphahroL),sin(-alphahroL),800,
							(rb+(hrw/2))*sin(ab+alphahroR),rb-(rb+(hrw/2))*cos(ab+alphahroR),1015,
							(rb-(hrw/2)+0.00005)*sin(ab+alphahroR),rb-(rb-(hrw/2)+0.00005)*cos(ab+alphahroR),15,
							(rb-(hrw/2))*sin(ab+alphahroR),rb-(rb-(hrw/2))*cos(ab+alphahroR),79,
							-cos(ab+alphahroR),-sin(ab+alphahroR),800,
							(rb-(hrw/2))*sin(-alphahroL),rb-(rb-(hrw/2))*cos(alphahroL),1015
							DEL 1
					ELSE
							PLACEGROUP "handrail round curved"
					ENDIF

			else

					group "handrail round curved 360"
							gamma0 = 360 / rescs
							gamma = 0
							for i=1 to rescs step 1
								put hrd/2 * cos(gamma), rb + hrd/2 * sin(gamma), 1
								gamma = gamma + gamma0
							next i
							add  0,rb,ds+hr-hrd/2
							roty 90
							rotx 180 - ab
							revolve rescs + 1, 360, 0,
								get (nsp),
								hrd/2, rb, -1
							del 3
					endgroup
					handrailgroupname = "handrail round curved 360"

					if hrt_m = 1 then
						addz ds + hr - hrh
							prism_ 12, hrh,
								0, -hrw/2, 79,
								1, 0, 800,
								0, 2*rb + hrw/2, 1079,
								-1, 0, 800,
								0, -hrw/2, 1079,
								0, -hrw/2, -1,

								0, hrw/2, 79,
								1, 0, 800,
								0, 2*rb - hrw/2, 1079,
								-1, 0, 800,
								0, hrw/2, 1079,
								0, hrw/2, -1
						del 1
					else
						placegroup "handrail round curved 360"
					endif

			endif

			!!other parts are shorter, circularity checked differently
			if ab < 360 then

					!!! *** POSTS *** !!! *******************************

					MATERIAL pmat
					RESOL rescs

					IF hrt_m=1 THEN			!Post height calculation
					ph=hr+ds-hrh+gs_PostBaseOverhang
					ELSE
						IF pt_m=1 THEN
						ph=hr+ds-hrd+gs_PostBaseOverhang
						ELSE
						ph=hr+ds-hrd/2+gs_PostBaseOverhang
						ENDIF
					ENDIF

					GROUP "post round curved"
					ADDZ -gs_PostBaseOverhang
						FOR i=1 to np step 1
						CYLIND ph,pd/2
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT i
						DEL 3*np
					DEL 1
					ENDGROUP

					GROUP "post rect curved"
					ADDZ -gs_PostBaseOverhang
						FOR i=1 to np step 1

						PRISM_ 5,ph,
							-pw/2,-pth/2,15,
							 pw/2,-pth/2,15,
							 pw/2,pth/2,15,
							-pw/2,pth/2,15,
							-pw/2,-pth/2,-1

						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT i
						DEL 3*np
					DEL 1
					ENDGROUP

					IF pt_m=1 THEN
						PLACEGROUP "post rect curved"
					ELSE
						IF hrt_m=1 THEN
							PLACEGROUP "post round curved"
						ELSE
							PLACEGROUP subgroup ("post round curved", handrailgroupname)
						ENDIF
					ENDIF



					!!! *** BOARDS *** !!! ********************************

					MATERIAL bmat

					!!!HORIZONTAL
					RESOL res

					GROUP "board rect curved H"			!Groups
					ADDz  ds
					PRISM_ 10,bw,
						0,bth/2,15,
						0,-bth/2+0.00005,15,
						0,-bth/2,79,
						1,0,800,
						(rb+(bth/2))*sin(ab),rb-(rb+(bth/2))*cos(ab),1015,
						(rb-(bth/2)+0.00005)*sin(ab),rb-(rb-(bth/2)+0.00005)*cos(ab),15,
						(rb-(bth/2))*sin(ab),rb-(rb-(bth/2))*cos(ab),79,
						-cos(ab),-sin(ab),800,
						0,bth/2,1015,
						0,bth/2,-1
					DEL 1
					ENDGROUP

					GROUP "board round curved H"
							gamma0=360/rescs
							gamma=0
							FOR i=1 to rescs step 1
							put bd/2*cos(gamma),rb+bd/2*sin(gamma),1
							gamma=gamma+gamma0
							NEXT i
						ADD  0,rb,ds+bd/2
						ROTy 90
						ROTx 180-ab
						REVOLVE rescs+1,ab,1+2+4+8+16+32,
						get (nsp),
						bd/2,rb,-1
						DEL 3
					ENDGROUP

					GROUP "subboard rect curved H"
						ADDz  sbhh1
						PRISM_ 10,bw,
							0,bth/2,15,
							0,-bth/2+0.00005,15,
							0,-bth/2,79,
							1,0,800,
							(rb+(bth/2))*sin(ab),rb-(rb+(bth/2))*cos(ab),1015,
							(rb-(bth/2)+0.00005)*sin(ab),rb-(rb-(bth/2)+0.00005)*cos(ab),15,
							(rb-(bth/2))*sin(ab),rb-(rb-(bth/2))*cos(ab),79,
							-cos(ab),-sin(ab),800,
							0,bth/2,1015,
							0,bth/2,-1
						DEL 1
						ADDz  sbhh2
						PRISM_ 10,bw,
							0,bth/2,15,
							0,-bth/2+0.00005,15,
							0,-bth/2,79,
							1,0,800,
							(rb+(bth/2))*sin(ab),rb-(rb+(bth/2))*cos(ab),1015,
							(rb-(bth/2)+0.00005)*sin(ab),rb-(rb-(bth/2)+0.00005)*cos(ab),15,
							(rb-(bth/2))*sin(ab),rb-(rb-(bth/2))*cos(ab),79,
							-cos(ab),-sin(ab),800,
							0,bth/2,1015,
							0,bth/2,-1
						DEL 1
					ENDGROUP

					GROUP "subboard round curved H"
							gamma0=360/rescs
							gamma=0
							FOR i=1 to rescs step 1
							put bd/2*cos(gamma),rb+bd/2*sin(gamma),1
							gamma=gamma+gamma0
							NEXT i
						ADD  0,rb,sbhh1
						ROTy 90
						ROTx 180-ab
						REVOLVE rescs+1,ab,1+2+4+8+16+32,
						use (nsp),
						bd/2,rb,-1
						DEL 3
						ADD  0,rb,sbhh2
						ROTy 90
						ROTx 180-ab
						REVOLVE rescs+1,ab,1+2+4+8+16+32,
						get (nsp),
						bd/2,rb,-1
						DEL 3
					ENDGROUP


					IF bt_m= 1 THEN
						IF pt_m=1 THEN
						PLACEGROUP subgroup ("board rect curved H","post rect curved")
						ELSE
						PLACEGROUP subgroup ("board rect curved H","post round curved")
						ENDIF
					ELSE
						IF pt_m=1 THEN
						PLACEGROUP subgroup ("board round curved H","post rect curved")
						ELSE
						PLACEGROUP subgroup ("board round curved H","post round curved")
						ENDIF
					ENDIF



					!!!VERTICAL

					RESOL rescs

					IF hrt_m=1 THEN			!Board height calculation
						IF bt_m=1 THEN
						bh=hr-bw-hrh
						ELSE
						bh=hr-bd/2-hrh
						ENDIF
					ELSE
						IF bt_m=1 THEN
						bh=hr-bw-hrd
						ELSE
						bh=hr-bd/2-hrd/2
						ENDIF
					ENDIF


					GROUP "board round curved V"			!Groups
						FOR k=1 to np-1 step 1
							ADD 0,rb,ds+bd/2
							ROTz dpostangle+distboardangle+dboardangle/2
							ADDy -rb
							FOR i=1 to nb step 1
							CYLIND bh,bd/2
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*nb+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT k
						DEL 3*(np-1)
					ENDGROUP

					GROUP "board rect curved V"
						FOR l=1 to np-1 step 1
							ADD 0,rb,ds+bw
							ROTz dpostangle+distboardangle+dboardangle/2
							ADDy -rb
							FOR i=1 to nb step 1
							PRISM_ 5,bh,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*nb+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT l
						DEL 3*(np-1)
					ENDGROUP

					GROUP "subboard round curved V"
						FOR k=1 to np-1 step 1
							ADD 0,rb,ds+bd/2
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							CYLIND sbh1,bd/2
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT k
						DEL 3*(np-1)
						FOR k=1 to np-1 step 1
							ADD 0,rb,sbhh2
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							CYLIND sbh2,bd/2
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT k
						DEL 3*(np-1)
					ENDGROUP

					GROUP "subboard rect curved V"
						FOR l=1 to np-1 step 1
							ADD 0,rb,ds+bw
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							PRISM_ 5,sbh1,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT l
						DEL 3*(np-1)
						FOR l=1 to np-1 step 1
							ADD 0,rb,sbhh2+bw
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							PRISM_ 5,sbh2,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT l
						DEL 3*(np-1)
					ENDGROUP


					IF bt_m=1 THEN
						IF hrt_m=1 THEN
							PLACEGROUP "board rect curved V"
							PLACEGROUP "subboard rect curved V"
						ELSE
							PLACEGROUP subgroup ("board rect curved V", handrailgroupname)
							PLACEGROUP subgroup ("subboard rect curved V", handrailgroupname)
						ENDIF
						IF pt_m=1 THEN
							PLACEGROUP subgroup (subgroup ("subboard rect curved H","post rect curved"),"board rect curved V")
							ELSE
							PLACEGROUP subgroup (subgroup ("subboard rect curved H","post round curved"),"board rect curved V")
						ENDIF
					ELSE
						IF hrt_m=1 THEN
							PLACEGROUP subgroup ("board round curved V","board round curved H")
							PLACEGROUP subgroup (subgroup ("subboard round curved V","subboard round curved H"),"board round curved H")
						ELSE
							PLACEGROUP subgroup (subgroup ("board round curved V", handrailgroupname),"board round curved H")
							PLACEGROUP subgroup (subgroup (subgroup ("subboard round curved V","subboard round curved H"),"board round curved H"), handrailgroupname)
						ENDIF
						IF pt_m=1 THEN
							PLACEGROUP subgroup (subgroup ("subboard round curved H","post rect curved"),"board round curved V")
							ELSE
							PLACEGROUP subgroup (subgroup ("subboard round curved H","post round curved"),"board round curved V")
						ENDIF
					ENDIF


					KILLGROUP "post round curved"
					KILLGROUP "post rect curved"
					KILLGROUP "board rect curved H"
					KILLGROUP "board round curved H"
					KILLGROUP "subboard rect curved H"
					KILLGROUP "subboard round curved H"
					KILLGROUP "board rect curved V"
					KILLGROUP "board round curved V"
					KILLGROUP "subboard rect curved V"
					KILLGROUP "subboard round curved V"


			ELSE	!!! angle=360

					!!! *** POSTS *** !!! *******************************

					MATERIAL pmat
					RESOL rescs

					IF hrt_m=1 THEN			!Post height calculation
					ph=hr+ds-hrh+gs_PostBaseOverhang
					ELSE
						IF pt_m=1 THEN
						ph=hr+ds-hrd+gs_PostBaseOverhang
						ELSE
						ph=hr+ds-hrd/2+gs_PostBaseOverhang
						ENDIF
					ENDIF

					GROUP "post round curved 360"
					ADDZ -gs_PostBaseOverhang
						FOR i=1 to np step 1
						CYLIND ph,pd/2
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT i
						DEL 3*np
					DEL 1
					ENDGROUP

					GROUP "post rect curved 360"
					ADDz -gs_PostBaseOverhang
						FOR i=1 to np step 1
						PRISM_ 5,ph,
						-pw/2,-pth/2,15,
						 pw/2,-pth/2,15,
						 pw/2,pth/2,15,
						-pw/2,pth/2,15,
						-pw/2,-pth/2,-1
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT i
						DEL 3*np
					DEL 1
					ENDGROUP

					IF pt_m=1 THEN
						PLACEGROUP "post rect curved 360"
					ELSE
						IF hrt_m=1 THEN
							PLACEGROUP "post round curved 360"
						ELSE
							PLACEGROUP subgroup ("post round curved 360", handrailgroupname)
						ENDIF
					ENDIF


					!!! *** BOARDS *** !!! ******************************

					MATERIAL bmat
					RESOL res

					!!!HORIZONTAL

					GROUP "board rect curved H 360"			!Groups
					ADDz  ds
					PRISM_ 12,bw,
							0,-bth/2,79,
							1,0,800,
							0,2*rb+bth/2,1079,
							-1,0,800,
							0,-bth/2,1079,
							0,-bth/2,-1,

							0,bth/2,79,
							1,0,800,
							0,2*rb-bth/2,1079,
							-1,0,800,
							0,bth/2,1079,
							0,bth/2,-1
					DEL 1
					ENDGROUP

					GROUP "board round curved H 360"
							gamma0=360/rescs
							gamma=0
							FOR i=1 to rescs step 1
							put bd/2*cos(gamma),rb+bd/2*sin(gamma),1
							gamma=gamma+gamma0
							NEXT i
						ADD  0,rb,ds+bd/2
						ROTy 90
						ROTx 180-ab
						REVOLVE rescs+1,ab,1+2+4+8+16+32,
						get (nsp),
						bd/2,rb,-1
						DEL 3
					ENDGROUP


					GROUP "subboard rect curved H 360"
					ADDz  sbhh1
					PRISM_ 12,bw,
							0,-bth/2,79,
							1,0,800,
							0,2*rb+bth/2,1079,
							-1,0,800,
							0,-bth/2,1079,
							0,-bth/2,-1,

							0,bth/2,79,
							1,0,800,
							0,2*rb-bth/2,1079,
							-1,0,800,
							0,bth/2,1079,
							0,bth/2,-1
					DEL 1
					ADDz  sbhh2
					PRISM_ 12,bw,
							0,-bth/2,79,
							1,0,800,
							0,2*rb+bth/2,1079,
							-1,0,800,
							0,-bth/2,1079,
							0,-bth/2,-1,

							0,bth/2,79,
							1,0,800,
							0,2*rb-bth/2,1079,
							-1,0,800,
							0,bth/2,1079,
							0,bth/2,-1
					DEL 1
					ENDGROUP

					GROUP "subboard round curved H 360"
							gamma0=360/rescs
							gamma=0
							FOR i=1 to rescs step 1
							put bd/2*cos(gamma),rb+bd/2*sin(gamma),1
							gamma=gamma+gamma0
							NEXT i
						ADD  0,rb,sbhh1
						ROTy 90
						ROTx 180-ab
						REVOLVE rescs+1,ab,1+2+4+8+16+32,
						use (nsp),
						bd/2,rb,-1
						DEL 3
						ADD  0,rb,sbhh2
						ROTy 90
						ROTx 180-ab
						REVOLVE rescs+1,ab,1+2+4+8+16+32,
						get (nsp),
						bd/2,rb,-1
						DEL 3
					ENDGROUP


					IF bt_m= 1 THEN
						IF pt_m=1 THEN
						PLACEGROUP subgroup ("board rect curved H 360","post rect curved 360")
						ELSE
						PLACEGROUP subgroup ("board rect curved H 360","post round curved 360")
						ENDIF
					ELSE
						IF pt_m=1 THEN
						PLACEGROUP subgroup ("board round curved H 360","post rect curved 360")
						ELSE
						PLACEGROUP subgroup ("board round curved H 360","post round curved 360")
						ENDIF
					ENDIF



					!!!VERTICAL

					RESOL rescs

					IF hrt_m=1 THEN			!Board height calculation
						IF bt_m=1 THEN
						bh=hr-bw-hrh
						ELSE
						bh=hr-bd/2-hrh
						ENDIF
					ELSE
						IF bt_m=1 THEN
						bh=hr-bw-hrd
						ELSE
						bh=hr-bd/2-hrd/2
						ENDIF
					ENDIF


					GROUP "board round curved V 360"			!Groups
						FOR k=1 to np step 1
							ADD 0,rb,ds+bd/2
							ROTz dpostangle+distboardangle+dboardangle/2
							ADDy -rb
							FOR i=1 to nb step 1
							CYLIND bh,bd/2
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*nb+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT k
						DEL 3*np
					ENDGROUP

					GROUP "board rect curved V 360"
						FOR l=1 to np step 1
							ADD 0,rb,ds+bw
							ROTz dpostangle+distboardangle+dboardangle/2
							ADDy -rb
							FOR i=1 to nb step 1
							PRISM_ 5,bh,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*nb+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT l
						DEL 3*np
					ENDGROUP

					GROUP "subboard round curved V 360"
						FOR k=1 to np step 1
							ADD 0,rb,ds+bd/2
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							CYLIND sbh1,bd/2
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT k
						DEL 3*np
						FOR k=1 to np step 1
							ADD 0,rb,sbhh2
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							CYLIND sbh2,bd/2
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT k
						DEL 3*np
					ENDGROUP

					GROUP "subboard rect curved V 360"
						FOR l=1 to np step 1
							ADD 0,rb,ds+bw
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							PRISM_ 5,sbh1,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT l
						DEL 3*np
						FOR l=1 to np step 1
							ADD 0,rb,sbhh2+bw
							ROTz dpostangle+distboardangle+dboardangle/2-(distboardangle+dboardangle/2)/2
							ADDy -rb
							FOR i=1 to nb+1 step 1
							PRISM_ 5,sbh2,
							-bw/2,-bth/2,15,
							 bw/2,-bth/2,15,
							 bw/2,bth/2,15,
							-bw/2,bth/2,15,
							-bw/2,-bth/2,-1
							ADDy rb
							ROTz distboardangle+dboardangle
							ADDy -rb
							NEXT i
							DEL 3*(nb+1)+3
						ADDy rb
						ROTz anglepost
						ADDy -rb
						NEXT l
						DEL 3*np
					ENDGROUP


					IF bt_m=1 THEN
						IF hrt_m=1 THEN
							PLACEGROUP "board rect curved V 360"
							PLACEGROUP "subboard rect curved V 360"
						ELSE
							PLACEGROUP subgroup ("board rect curved V 360", handrailgroupname)
							PLACEGROUP subgroup ("subboard rect curved V 360", handrailgroupname)
						ENDIF
						IF pt_m=1 THEN
							PLACEGROUP subgroup (subgroup ("subboard rect curved H 360","post rect curved 360"),"board rect curved V 360")
							ELSE
							PLACEGROUP subgroup (subgroup ("subboard rect curved H 360","post round curved 360"),"board rect curved V 360")
						ENDIF
					ELSE
						IF hrt_m=1 THEN
							PLACEGROUP subgroup ("board round curved V 360","board round curved H 360")
							PLACEGROUP subgroup (subgroup ("subboard round curved V 360","subboard round curved H 360"),"board round curved H 360")
						ELSE
							PLACEGROUP subgroup (subgroup ("board round curved V 360", handrailgroupname),"board round curved H 360")
							PLACEGROUP subgroup (subgroup (subgroup ("subboard round curved V 360","subboard round curved H 360"),"board round curved H 360"), handrailgroupname)
						ENDIF
						IF pt_m=1 THEN
							PLACEGROUP subgroup (subgroup ("subboard round curved H 360","post rect curved 360"),"board round curved V 360")
							ELSE
							PLACEGROUP subgroup (subgroup ("subboard round curved H 360","post round curved 360"),"board round curved V 360")
						ENDIF
					ENDIF


					KILLGROUP "post round curved 360"
					KILLGROUP "post rect curved 360"
					KILLGROUP "board rect curved H 360"
					KILLGROUP "board round curved H 360"
					KILLGROUP "subboard rect curved H 360"
					KILLGROUP "subboard round curved H 360"
					KILLGROUP "board rect curved V 360"
					KILLGROUP "board round curved V 360"
					KILLGROUP "subboard rect curved V 360"
					KILLGROUP "subboard round curved V 360"


			ENDIF
			KILLGROUP  handrailgroupname
		ENDIF	!! END OF 3D FEEDBACK MODE
	ENDIF



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


	IF ABS(ani)>EPS and ptyp_m=1 THEN

		RESOL rescs

		IF rom_m=1 THEN									!!!VERTICAL Orientation

				IF rmm_m=2 THEN		!Perpendicular Measurement Extra Values
					hrh=hrh/cos(ani)
					hrd=hrd/cos(ani)
					sbh1=sbh1/cos(ani)
					sbh2=sbh2/cos(ani)
					sbhh1=sbhh1/cos(ani)

					if bt_m=1 then
						if hrt_m=1 then
							sbhh2 = (ds + hr - hrh * cos(ani) - sbh - bw * cos(ani)) / cos(ani)
						else
							sbhh2 = (ds + hr - hrd * cos(ani) - sbh - bw * cos(ani)) / cos(ani)
						endif
					else
						if hrt_m=1 then
							sbhh2 = (ds + hr - hrh * cos(ani) - sbh - bd/2 * cos(ani)) / cos(ani)
						else
							sbhh2 = (ds + hr - hrd * cos(ani) - sbh - bd/2 * cos(ani)) / cos(ani)
						endif
					endif
				ENDIF

				IF rmm_m=1 THEN			!!Vertical Measurement
					temp_ds=ds
					temp_hr=hr
					temp_PostBaseOverhang=gs_PostBaseOverhang
				ELSE					!!Perpendicular Measurement
					temp_ds=dsPerp
					temp_hr=hrPerp
					temp_PostBaseOverhang=gs_PostBaseOverhangPerp
				ENDIF


				!!3D FEEDBACK AT HOTSPOT EDITING
				IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				ADD XStartPoslra,0,ZStartPoslra

						MATERIAL fmat

						ROTz -90+gs_AngleL		!!! CONNECTING ANGLE
						ROTy -90
							CUTPLANE
						DEL 2

						ADDx templra
						ROTz 90-gs_AngleR
						ROTy 90
							CUTPLANE
						DEL 3

								ROTx 90
								ADDz -fth/2

									MATERIAL fmat
									PRISM_ 5,fth,
										-XPosL,temp_ds-YPosL,15,
										templra+XPosR,temp_ds+templra*tan(ani)+YPosR,15,
										templra+XPosR,temp_ds+temp_hr+templra*tan(ani)+YPosR,15,
										-XPosL,temp_ds+temp_hr-YPosL,15,
										-XPosL,temp_ds-YPosL,-1
								DEL 2

						CUTEND
						CUTEND

				DEL 1

				ELSE

				ADD XStartPoslra,0,ZStartPoslra

				!!! *** Handrail *** !!! *****************************************

				MATERIAL fmat

				ADDx -(hroL)
				ROTz -90+gs_AngleL		!! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 3

				ADDx templra+(hroR)
				ROTz 90-gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3

						GROUP "handrail round straight inclined"
							ADD -(hroL)-XPosL,0,temp_ds+temp_hr-hrd/2-(hroL)*tan(ani)-YPosL
							ROTy 90-ani
							CONE (templra+hroL+hroR)/cos(ani)+XPosL+XPosR,hrd/2,hrd/2,90+ani,90+ani
							DEL 1
						ENDGROUP

						IF hrt_m=1 THEN
							ROTx 90
							ADDz -hrw/2
								PRISM_ 5,hrw,
								-(hroL)-XPosL,temp_ds+temp_hr-hrh-(hroL)*tan(ani)-YPosL,15,
								templra+(hroR)+XPosR,temp_ds+temp_hr-hrh+(templra+(hroR))*tan(ani)+YPosR,15,
								templra+(hroR)+XPosR,temp_ds+temp_hr+(templra+(hroR))*tan(ani)+YPosR,15,
								-(hroL)-XPosL,temp_ds+temp_hr-(hroL)*tan(ani)-YPosL,15,
								-(hroL)-XPosL,temp_ds+temp_hr-hrh-(hroL)*tan(ani)-YPosL,-1
							DEL 2
						ELSE
						PLACEGROUP "handrail round straight inclined"
						ENDIF

				CUTEND
				CUTEND


				!!! *** Posts *** !!! **********************************************

				MATERIAL pmat

				ADDx -(hroL)-LengthSize*dpost
				ROTz -90+gs_AngleL		!! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 3

				ADDx templra+(hroR)+LengthSize*dpost
				ROTz 90-gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3

						MATERIAL pmat

						IF hrt_m=1 THEN			!Post height calculation
						ph=temp_hr+temp_ds-hrh+temp_PostBaseOverhang
						ELSE
							IF pt_m=1 THEN
							ph=temp_hr+temp_ds-hrd+temp_PostBaseOverhang
							ELSE
							ph=temp_hr+temp_ds-hrd/2+temp_PostBaseOverhang
							ENDIF
						ENDIF

						GROUP "post round straight inclined"
						ADDZ -temp_PostBaseOverhang
							FOR i=1 to np step 1
							cone ph,pd/2,pd/2,90,90-ani
							ADD distpost,0,distpost*tan(ani)
							NEXT i
							DEL np
						DEL 1
						ENDGROUP

						GROUP "post rect straight inclined"
						ADDz -temp_PostBaseOverhang
							ROTx 90
							ADDz -pth/2
							FOR i=1 to np step 1

							PRISM_ 5,pth,
								-pw/2,0,15,
								 pw/2, 0,15,
								 pw/2, pw/2*tan(ani)+ph,15,
								-pw/2,-pw/2*tan(ani)+ph,15,
								-pw/2,0,-1

							ADD distpost,distpost*tan(ani),0
							NEXT i
							DEL np+2
						DEL 1
						ENDGROUP

						IF pt_m=1 THEN
							IF hrt_m=1 THEN
							PLACEGROUP "post rect straight inclined"
							ELSE
							PLACEGROUP subgroup ("post rect straight inclined","handrail round straight inclined")
							ENDIF
						ELSE
							IF hrt_m=1 THEN
							PLACEGROUP "post round straight inclined"
							ELSE
							PLACEGROUP subgroup ("post round straight inclined","handrail round straight inclined")
							ENDIF
						ENDIF

				CUTEND
				CUTEND


				!!! *** Boards *** !!! *****************************************

				MATERIAL bmat

				ADDx -(hroL)-LengthSize*dpost
				ROTz -90+gs_AngleL		!! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 3

				ADDx templra+(hroR)+LengthSize*dpost
				ROTz 90-gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3

						MATERIAL bmat

						!!!HORIZONTAL

						GROUP "board rect straight H inclined"			!Groups
						ROTx 90
						ADDz -bth/2
						PRISM_ 5,bth,
							0,temp_ds,15,
							templra,temp_ds+templra*tan(ani),15,
							templra,temp_ds+bw+templra*tan(ani),15,
							0,temp_ds+bw,15,
							0,temp_ds,-1
						DEL 2
						ENDGROUP

						GROUP "board round straight H inclined"
							ADDz temp_ds+bd/2
							ROTy 90-ani
							cone templra/cos(ani),bd/2,bd/2,90+ani,90+ani
							DEL 2
						ENDGROUP

						GROUP "subboard rect straight H inclined"
						ROTx 90
						ADDz -bth/2
						PRISM_ 5,bth,
							0,temp_ds+sbh1+bw,15,
							templra,temp_ds+sbh1+bw+templra*tan(ani),15,
							templra,temp_ds+sbh1+2*bw+templra*tan(ani),15,
							0,temp_ds+sbh1+2*bw,15,
							0,temp_ds+sbh1+bw,-1
						PRISM_ 5,bth,
							0,sbhh2,15,
							templra,sbhh2+templra*tan(ani),15,
							templra,sbhh2+bw+templra*tan(ani),15,
							0,sbhh2+bw,15,
							0,sbhh2,-1
						DEL 2
						ENDGROUP

						GROUP "subboard round straight H inclined"
							ADDz sbhh1
							ROTy 90-ani
							cone templra/cos(ani),bd/2,bd/2,90+ani,90+ani
							DEL 2
							ADDz sbhh2
							ROTy 90-ani
							cone templra/cos(ani),bd/2,bd/2,90+ani,90+ani
							DEL 2
						ENDGROUP



						IF bt_m= 1 THEN
							IF pt_m=1 THEN
							PLACEGROUP subgroup ("board rect straight H inclined","post rect straight inclined")
							ELSE
							PLACEGROUP subgroup ("board rect straight H inclined","post round straight inclined")
							ENDIF
						ELSE
							IF pt_m=1 THEN
							PLACEGROUP subgroup ("board round straight H inclined","post rect straight inclined")
							ELSE
							PLACEGROUP subgroup ("board round straight H inclined","post round straight inclined")
							ENDIF
						ENDIF


						!!!VERTICAL

						IF hrt_m=1 THEN			!Board height calculation
							IF bt_m=1 THEN
							bh=temp_hr-bw-hrh
							ELSE
							bh=temp_hr-bd/2-hrh
							ENDIF
						ELSE
							IF bt_m=1 THEN
							bh=temp_hr-bw-hrd
							ELSE
							bh=temp_hr-bd/2-hrd/2
							ENDIF
						ENDIF


						GROUP "board round straight V inclined"			!Groups
							FOR k=1 to np-1 step 1
								ADD dpost+distboard+bd/2,0,temp_ds+bd/2+(dpost+distboard+bd/2)*tan(ani)
								FOR i=1 to nb step 1
								cone bh,bd/2,bd/2,90-ani,90-ani
								ADD distboard+bd,0,(distboard+bd)*tan(ani)
								NEXT i
								DEL nb+1
							ADD distpost,0,distpost*tan(ani)
							NEXT k
							DEL np-1
						ENDGROUP

						GROUP "board rect straight V inclined"
							FOR l=1 to np-1 step 1
								ROTx 90
								ADD dpost+distboard+bw/2,temp_ds+bw+(dpost+distboard+bw/2)*tan(ani),-bth/2
								FOR i=1 to nb step 1
								PRISM_ 5,bth,
								-bw/2,-bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani)+bh,15,
								-bw/2,-bw/2*tan(ani)+bh,15,
								-bw/2,-bw/2*tan(ani),-1
								ADD distboard+bw,(distboard+bw)*tan(ani),0
								NEXT i
								DEL nb+2
							ADD distpost,0,distpost*tan(ani)
							NEXT l
							DEL np-1
						ENDGROUP


						GROUP "subboard round straight V inclined"
							FOR k=1 to np-1 step 1
								ADD dpost+distboard/2,0,temp_ds+bd/2+(dpost+distboard/2)*tan(ani)
								FOR i=1 to nb+1 step 1
								cone sbh1,bd/2,bd/2,90-ani,90-ani
								ADD distboard+bd,0,(distboard+bd)*tan(ani)
								NEXT i
								DEL nb+2
							ADD distpost,0,distpost*tan(ani)
							NEXT k
							DEL np-1
							FOR k=1 to np-1 step 1
								ADD dpost+distboard/2,0,sbhh2+(dpost+distboard/2)*tan(ani)
								FOR i=1 to nb+1 step 1
								cone sbh2,bd/2,bd/2,90-ani,90-ani
								ADD distboard+bd,0,(distboard+bd)*tan(ani)
								NEXT i
								DEL nb+2
							ADD distpost,0,distpost*tan(ani)
							NEXT k
							DEL np-1
						ENDGROUP

						GROUP "subboard rect straight V inclined"
							FOR l=1 to np-1 step 1
								ROTx 90
								ADD dpost+distboard/2,temp_ds+bw+(dpost+distboard/2)*tan(ani),-bth/2
								FOR i=1 to nb+1 step 1
								PRISM_ 5,bth,
								-bw/2,-bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani)+sbh1,15,
								-bw/2,-bw/2*tan(ani)+sbh1,15,
								-bw/2,-bw/2*tan(ani),-1
								ADD distboard+bw,(distboard+bw)*tan(ani),0
								NEXT i
								DEL nb+3
							ADD distpost,0,distpost*tan(ani)
							NEXT l
							DEL np-1
							FOR l=1 to np-1 step 1
								ROTx 90
								ADD dpost+distboard/2,sbhh2+bw+(dpost+distboard/2)*tan(ani),-bth/2
								FOR i=1 to nb+1 step 1
								PRISM_ 5,bth,
								-bw/2,-bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani)+sbh2,15,
								-bw/2,-bw/2*tan(ani)+sbh2,15,
								-bw/2,-bw/2*tan(ani),-1
								ADD distboard+bw,(distboard+bw)*tan(ani),0
								NEXT i
								DEL nb+3
							ADD distpost,0,distpost*tan(ani)
							NEXT l
							DEL np-1
						ENDGROUP


						IF bt_m=1 THEN
							IF hrt_m=1 THEN
								PLACEGROUP "board rect straight V inclined"
								PLACEGROUP "subboard rect straight V inclined"
							ELSE
								PLACEGROUP subgroup ("board rect straight V inclined","handrail round straight inclined")
								PLACEGROUP subgroup ("subboard rect straight V inclined","handrail round straight inclined")
							ENDIF
							IF pt_m=1 THEN
								PLACEGROUP subgroup (subgroup ("subboard rect straight H inclined","post rect straight inclined"),"board rect straight V inclined")
							ELSE
								PLACEGROUP subgroup (subgroup ("subboard rect straight H inclined","post round straight inclined"),"board rect straight V inclined")
							ENDIF
						ELSE
							IF hrt_m=1 THEN
								PLACEGROUP subgroup ("board round straight V inclined","board round straight H inclined")
								PLACEGROUP subgroup (subgroup ("subboard round straight V inclined","subboard round straight H inclined"),"board round straight H inclined")
								ELSE
								PLACEGROUP subgroup (subgroup ("board round straight V inclined","handrail round straight inclined"),"board round straight H inclined")
								PLACEGROUP subgroup (subgroup (subgroup ("subboard round straight V inclined","subboard round straight H inclined"),"board round straight H inclined"),"handrail round straight inclined")
							ENDIF
							IF pt_m=1 THEN
								PLACEGROUP subgroup (subgroup ("subboard round straight H inclined","post rect straight inclined"),"board round straight V inclined")
								ELSE
								PLACEGROUP subgroup (subgroup ("subboard round straight H inclined","post round straight inclined"),"board round straight V inclined")
							ENDIF
						ENDIF

				CUTEND
				CUTEND
						KILLGROUP "handrail round straight inclined"
						KILLGROUP "post round straight inclined"
						KILLGROUP "post rect straight inclined"
						KILLGROUP "board rect straight H inclined"
						KILLGROUP "board round straight H inclined"
						KILLGROUP "subboard rect straight H inclined"
						KILLGROUP "subboard round straight H inclined"
						KILLGROUP "board rect straight V inclined"
						KILLGROUP "board round straight V inclined"
						KILLGROUP "subboard rect straight V inclined"
						KILLGROUP "subboard round straight V inclined"

				ENDIF 	!!! END OF 3D FEEDBACK MODE

			DEL 1

		ELSE			!Perpendicular Orientation
						!Measurement is always perpendicular to inclination in this case

				ROTy -ani
				ADDX XStartPoslrah

					!!3D FEEDBACK AT HOTSPOT EDITING
					IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

						ROTx 90
						ADDz -fth/2
							!!FRAME
							MATERIAL fmat
							PRISM_ 5,fth,
								0,ds,15,
								templrah,ds,15,
								templrah,ds+hr,15,
								0,ds+hr,15,
								0,ds,-1
						DEL 2


					ELSE		!! 3D DETAILED MODEL


						RESOL rescs
						!!! *** Handrail *** !!! *******************************************
						MATERIAL fmat

						GROUP "handrail round straight perp"			!Groups
						ADD  -hroLDiagonal,0,ds+hr-hrd/2
						ROTy 90
						CYLIND templrah+hroLDiagonal+hroRDiagonal,hrd/2
						DEL 2
						ENDGROUP


						IF hrt_m=1 THEN
							ROTx 90
							ADDz -hrw/2
								PRISM_ 5,hrw,
									-hroLDiagonal,ds+hr-hrh,15,
									templrah+hroRDiagonal,ds+hr-hrh,15,
									templrah+hroRDiagonal,ds+hr,15,
									-hroLDiagonal,ds+hr,15,
									-hroLDiagonal,ds+hr-hrh,-1
							DEL 2
						ELSE
							PLACEGROUP "handrail round straight perp"
						ENDIF


						!!! *** Post *** !!! *************************************************
						MATERIAL pmat

						IF hrt_m=1 THEN			!Post height calculation
						ph=hr+ds-hrh+gs_PostBaseOverhang
						ELSE
							IF pt_m=1 THEN
							ph=hr+ds-hrd+gs_PostBaseOverhang
							ELSE
							ph=hr+ds-hrd/2+gs_PostBaseOverhang
							ENDIF
						ENDIF

						GROUP "post round straight perp"			!Groups
						ADDZ -gs_PostBaseOverhang
							FOR i=1 to np step 1
							CYLIND ph,pd/2
							addx distpost
							NEXT i
							DEL np
						DEL 1
						ENDGROUP

						GROUP "post rect straight perp"
						ADDZ -gs_PostBaseOverhang
							FOR i=1 to np step 1
							PRISM_ 5,ph,
								-pw/2,-pth/2,15,
								 pw/2,-pth/2,15,
								 pw/2,pth/2,15,
								-pw/2,pth/2,15,
								-pw/2,-pth/2,-1
							addx distpost
							NEXT i
							DEL np
						DEL 1
						ENDGROUP

						IF pt_m=1 THEN
							PLACEGROUP "post rect straight perp"
						ELSE
							IF hrt_m=1 THEN
							PLACEGROUP "post round straight perp"
							ELSE
							PLACEGROUP subgroup ("post round straight perp","handrail round straight perp")
							ENDIF
						ENDIF


						!!! *** BOARDS *** !!! ***********************************************

						MATERIAL bmat

						!!!HORIZONTAL

						GROUP "board rect straight H perp"			!Groups
						ADDz  ds

						PRISM_ 5,bw,
							0,-bth/2,15,
							templrah,-bth/2,15,
							templrah,bth/2,15,
							0,bth/2,15,
							0,-bth/2,-1

						DEL 1
						ENDGROUP

						GROUP "board round straight H perp"
						ADDz  ds+bd/2
						ROTy 90
							CYLIND templrah,bd/2
						DEL 2
						ENDGROUP

						GROUP "subboard rect straight H perp"
							ADDz  sbhh1

							PRISM_ 5,bw,
								0,-bth/2,15,
								templrah,-bth/2,15,
								templrah,bth/2,15,
								0,bth/2,15,
								0,-bth/2,-1

							DEL 1

							ADDz sbhh2

							PRISM_ 5,bw,
								0,-bth/2,15,
								templrah,-bth/2,15,
								templrah,bth/2,15,
								0,bth/2,15,
								0,-bth/2,-1
							DEL 1

						ENDGROUP

						GROUP "subboard round straight H perp"
							ADDz  sbhh1
							ROTy 90
								CYLIND templrah,bd/2
							DEL 2
							ADDz  sbhh2
							ROTy 90
								CYLIND templrah,bd/2
							DEL 2
						ENDGROUP


						IF bt_m= 1 THEN
							IF pt_m=1 THEN
							PLACEGROUP subgroup ("board rect straight H perp","post rect straight perp")
							ELSE
							PLACEGROUP subgroup ("board rect straight H perp","post round straight perp")
							ENDIF
						ELSE
							IF pt_m=1 THEN
							PLACEGROUP subgroup ("board round straight H perp","post rect straight perp")
							ELSE
							PLACEGROUP subgroup ("board round straight H perp","post round straight perp")
							ENDIF
						ENDIF


						!!!VERTICAL

						IF hrt_m=1 THEN			!Board height calculation
							IF bt_m=1 THEN
							bh=hr-bw-hrh
							ELSE
							bh=hr-bd/2-hrh
							ENDIF
						ELSE
							IF bt_m=1 THEN
							bh=hr-bw-hrd
							ELSE
							bh=hr-bd/2-hrd/2
							ENDIF
						ENDIF


						GROUP "board round straight V perp"			!Groups
							FOR k=1 to np-1 step 1
								ADD dpost+distboard+bd/2,0,ds+bd/2
								FOR i=1 to nb step 1
									CYLIND bh,bd/2
								addx distboard+bd
								NEXT i
								DEL nb+1
							addx distpost
							NEXT k
							DEL np-1
						ENDGROUP

						GROUP "board rect straight V perp"
							FOR l=1 to np-1 step 1
								ADD dpost+distboard+bw/2,0,ds+bw
								FOR i=1 to nb step 1

								PRISM_ 5,bh,
									-bw/2,-bth/2,15,
									 bw/2,-bth/2,15,
									 bw/2,bth/2,15,
									-bw/2,bth/2,15,
									-bw/2,-bth/2,-1

								addx distboard+bw
								NEXT i
								DEL nb+1
							addx distpost
							NEXT l
							DEL np-1
						ENDGROUP


						GROUP "subboard round straight V perp"
							FOR k=1 to np-1 step 1
								ADD dpost+distboard+bd/2,0,ds+bd/2
								addx -(distboard+bd)/2
								FOR i=1 to nb+1 step 1
									CYLIND sbh1,bd/2
								addx distboard+bd
								NEXT i
								DEL nb+3
							addx distpost
							NEXT k
							DEL np-1
							FOR k=1 to np-1 step 1
								ADD dpost+distboard+bd/2,0,sbhh2
								addx -(distboard+bd)/2
								FOR i=1 to nb+1 step 1
									CYLIND sbh2,bd/2
								addx distboard+bd
								NEXT i
								DEL nb+3
							addx distpost
							NEXT k
							DEL np-1
						ENDGROUP

						GROUP "subboard rect straight V perp"
							FOR l=1 to np-1 step 1
								ADD dpost+distboard+bw/2,0,ds+bw
								addx -(distboard+bw)/2
								FOR i=1 to nb+1 step 1

								PRISM_ 5,sbh1,
									-bw/2,-bth/2,15,
									 bw/2,-bth/2,15,
									 bw/2,bth/2,15,
									-bw/2,bth/2,15,
									-bw/2,-bth/2,-1

								addx distboard+bw
								NEXT i
								DEL nb+3
							addx distpost
							NEXT l
							DEL np-1
							FOR l=1 to np-1 step 1
								ADD dpost+distboard+bw/2,0,sbhh2+bw
								addx -(distboard+bw)/2
								FOR i=1 to nb+1 step 1

								PRISM_ 5,sbh2,
									-bw/2,-bth/2,15,
									 bw/2,-bth/2,15,
									 bw/2,bth/2,15,
									-bw/2,bth/2,15,
									-bw/2,-bth/2,-1

								addx distboard+bw
								NEXT i
								DEL nb+3
							addx distpost
							NEXT l
							DEL np-1
						ENDGROUP


						IF bt_m=1 THEN
							IF hrt_m=1 THEN
								PLACEGROUP "board rect straight V perp"
								PLACEGROUP "subboard rect straight V perp"
								ELSE
								PLACEGROUP subgroup ("board rect straight V perp","handrail round straight perp")
								PLACEGROUP subgroup ("subboard rect straight V perp","handrail round straight perp")
							ENDIF
							IF pt_m=1 THEN
								PLACEGROUP subgroup (subgroup ("subboard rect straight H perp","post rect straight perp"),"board rect straight V perp")
								ELSE
								PLACEGROUP subgroup (subgroup ("subboard rect straight H perp","post round straight perp"),"board rect straight V perp")
							ENDIF
						ELSE
							IF hrt_m=1 THEN
								PLACEGROUP subgroup ("board round straight V perp","board round straight H perp")
								PLACEGROUP subgroup (subgroup ("subboard round straight V perp","subboard round straight H perp"),"board round straight H perp")
								ELSE
								PLACEGROUP subgroup (subgroup ("board round straight V perp","handrail round straight perp"),"board round straight H perp")
								PLACEGROUP subgroup (subgroup (subgroup ("subboard round straight V perp","subboard round straight H perp"),"board round straight H perp"),"handrail round straight perp")
							ENDIF
							IF pt_m=1 THEN
								PLACEGROUP subgroup (subgroup ("subboard round straight H perp","post rect straight perp"),"board round straight V perp")
								ELSE
								PLACEGROUP subgroup (subgroup ("subboard round straight H perp","post round straight perp"),"board round straight V perp")
							ENDIF
						ENDIF


						KILLGROUP "handrail round straight perp"
						KILLGROUP "post round straight perp"
						KILLGROUP "post rect straight perp"
						KILLGROUP "board rect straight H perp"
						KILLGROUP "board round straight H perp"
						KILLGROUP "subboard rect straight H perp"
						KILLGROUP "subboard round straight H perp"
						KILLGROUP "board rect straight V perp"
						KILLGROUP "board round straight V perp"
						KILLGROUP "subboard rect straight V perp"
						KILLGROUP "subboard round straight V perp"

					ENDIF	!! END OF 3D MODEL
			DEL 1		!! END OF DRAG
			DEL 1		!! END OF ROTATION
		ENDIF
	ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!


	IF ABS(ani)>EPS and ptyp_m<>1 THEN

			RESOL rescs

			!!!HANDRAIL

			MATERIAL fmat


			!!3D FEEDBACK AT HOTSPOT EDITING
			IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				!!!!FRAME
				MATERIAL fmat

				alphai=-alphaincl
				deltazi=-deltazincl
				for i=1 to nalphaincl+3 step 1
				put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
				alphai=alphai+alphaincl
				deltazi=deltazi+deltazincl
				NEXT i

				TUBEA 5,nalphaincl+3,1+2+16+32,
					-fth/2,0,0,
					fth/2,0,0,
					fth/2,hr,0,
					-fth/2,hr,0,
					-fth/2,0,-1,
					get (nsp)

			ELSE


				RestAngleL=alphahroL-nalphahroL*alphaincl		!!Rest Angle= Overhang Angle- Segments Points Angle
				LOHkerul=(rb*pi*alphahroL)/180					!!Perimeter of Handrail
				deltazLOH=LOHkerul*sin(ani)						!!vertical distance of Handrail
				deltara=deltazLOH-(deltazincl*nalphahroL)		!!vertical step of Rest Angle

				RestAngleR=alphahroR-nalphahroR*alphaincl
				ROHkerul=(rb*pi*alphahroR)/180
				deltazROH=ROHkerul*sin(ani)
				deltaraR=deltazROH-(deltazincl*nalphahroR)


				alphai=-(nalphahroL+1)*alphaincl
				deltazi=-(nalphahroL+1)*deltazincl

				if ABS(hroR)<EPS THEN
				tempStep=1
				ELSE
				tempStep=0
				ENDIF

					PUT rb*sin(-alphahroL-restangleL),rb-rb*cos(-alphahroL-restangleL),ds-deltazLOH-deltara
					PUT rb*sin(-alphahroL),rb-rb*cos(-alphahroL),ds-deltazLOH
					PUT rb*sin(-alphahroL+restangleL),rb-rb*cos(-alphahroL+restangleL),ds-deltazLOH+deltara

				if abs(nalphahroL)>EPS then		!! Left Handrail
				FOR i=1 to nalphahroL step 1
					PUT rb*sin(-alphahroL+restangleL+i*alphaincl),rb-rb*cos(-alphahroL+restangleL+i*alphaincl),ds-deltazLOH+deltara+i*deltazincl
				Next i
				endif

				FOR i=0 to nalphaincl+tempStep step 1		!! Handrail between Posts
					PUT rb*sin(i*alphaincl),rb-rb*cos(i*alphaincl),ds+i*deltazincl
				NEXT i

			IF ABS(hroR)>EPS THEN				!! Right Handrail

				if abs(nalphahroR)>EPS then
				FOR i=1 to nalphahroR step 1
					PUT rb*sin((nalphaincl+i)*alphaincl),rb-rb*cos((nalphaincl+i)*alphaincl),ds+(nalphaincl+i)*deltazincl
				Next i
				endif

					PUT rb*sin((nalphaincl+nalphahroR)*alphaincl+restangleR),rb-rb*cos((nalphaincl+nalphahroR)*alphaincl+restangleR),ds+(nalphaincl+nalphahroR)*deltazincl+deltaraR
					PUT rb*sin((nalphaincl+nalphahroR)*alphaincl+2*restangleR),rb-rb*cos((nalphaincl+nalphahroR)*alphaincl+2*restangleR),ds+(nalphaincl+nalphahroR)*deltazincl+2*deltaraR
			ENDIF


				GROUP "handrail round inclined curved"
					ADDz hr-hrd/2
					tubea 6,nsp/3,1+2+16+32,
						-hrd/2,0,1,
						 0,-1,801,
						 hrd/2,0,1001,
						 0,1,801,
						-hrd/2,0,1001,
						-hrd/2,0,-1,
						use (nsp)
					DEL 1
				ENDGROUP

				GROUP "handrail rect inclined curved"
					ADDz hr-hrh
					tubea 5,nsp/3,1+2+16+32,
						-hrw/2,0,0,
						 hrw/2,0,0,
						 hrw/2,hrh,0,
						-hrw/2,hrh,0,
						-hrw/2,0,-1,
						get (nsp)
					DEL 1
				ENDGROUP


				IF hrt_m=1 THEN
				PLACEGROUP "handrail rect inclined curved"
				ELSE
				PLACEGROUP "handrail round inclined curved"
				ENDIF


				!!! *** POSTS *** !!! *******************************************************

				MATERIAL pmat

				IF hrt_m=1 THEN			!Post height calculation
				ph=hr+ds-hrh/2+gs_PostBaseOverhang
				ELSE
					IF pt_m=1 THEN
					ph=hr+ds-hrd/2+gs_PostBaseOverhang
					ELSE
					ph=hr+ds-hrd/2+gs_PostBaseOverhang
					ENDIF
				ENDIF

				GROUP "post round inclined curved"			!Groups
				ADDZ -gs_PostBaseOverhang
					FOR i=1 to np step 1
					CYLIND ph,pd/2
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT i
					DEL 4*np
				DEL 1
				ENDGROUP

				GROUP "post rect inclined curved"
				ADDz -gs_PostBaseOverhang
					FOR i=1 to np step 1
					PRISM_ 5,ph,
						-pw/2,-pth/2,15,
						 pw/2,-pth/2,15,
						 pw/2,pth/2,15,
						-pw/2,pth/2,15,
						-pw/2,-pth/2,-1
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT i
					DEL 4*np
				DEL 1
				ENDGROUP

				IF pt_m=1 THEN
					IF hrt_m=1 THEN
					PLACEGROUP subgroup ("post rect inclined curved","handrail rect inclined curved")
					ELSE
					PLACEGROUP subgroup ("post rect inclined curved","handrail round inclined curved")
					ENDIF
				ELSE
					IF hrt_m=1 THEN
					PLACEGROUP subgroup ("post round inclined curved","handrail rect inclined curved")
					ELSE
					PLACEGROUP subgroup ("post round inclined curved","handrail round inclined curved")
					ENDIF
				ENDIF





				!!! *** BOARDS *** !!! ******************************************************

				MATERIAL bmat

				!!!HORIZONTAL

				alphai=-alphaincl
				deltazi=-deltazincl
				FOR i=1 to nalphaincl+3 step 1
				put rb*sin(alphai),rb-rb*cos(alphai),deltazi
				alphai=alphai+alphaincl
				deltazi=deltazi+deltazincl
				NEXT i

				GROUP "board round inclined curved H"
					ADDz ds+bd/2
					tubea 6,nalphaincl+3,1+2+16+32,
					-bd/2,0,1,
					 0,-1,801,
					 bd/2,0,1001,
					 0,1,801,
					-bd/2,0,1001,
					-bd/2,0,-1,
					use (nsp)
					DEL 1
				ENDGROUP

				GROUP "board rect inclined curved H"
					ADDz ds
					tubea 5,nalphaincl+3,1+2+16+32,
					-bth/2,0,0,
					 bth/2,0,0,
					 bth/2,bw,0,
					-bth/2,bw,0,
					-bth/2,0,-1,
					use (nsp)
					DEL 1
				ENDGROUP


				GROUP "subboard round inclined curved H"
					ADDz sbhh1
					tubea 6,nalphaincl+3,1+2+16+32,
					-bd/2,0,1,
					 0,-1,801,
					 bd/2,0,1001,
					 0,1,801,
					-bd/2,0,1001,
					-bd/2,0,-1,
					use (nsp)
					DEL 1
					ADDz sbhh2
					tubea 6,nalphaincl+3,1+2+16+32,
					-bd/2,0,1,
					 0,-1,801,
					 bd/2,0,1001,
					 0,1,801,
					-bd/2,0,1001,
					-bd/2,0,-1,
					use (nsp)
					DEL 1
				ENDGROUP

				GROUP "subboard rect inclined curved H"
					ADDz sbhh1
					tubea 5,nalphaincl+3,1+2+16+32,
					-bth/2,0,0,
					 bth/2,0,0,
					 bth/2,bw,0,
					-bth/2,bw,0,
					-bth/2,0,-1,
					use (nsp)
					DEL 1
					ADDz sbhh2
					tubea 5,nalphaincl+3,1+2+16+32,
					-bth/2,0,0,
					 bth/2,0,0,
					 bth/2,bw,0,
					-bth/2,bw,0,
					-bth/2,0,-1,
					get (nsp)
					DEL 1
				ENDGROUP

				IF bt_m= 1 THEN
					IF pt_m=1 THEN
					PLACEGROUP subgroup ("board rect inclined curved H","post rect inclined curved")
					ELSE
					PLACEGROUP subgroup ("board rect inclined curved H","post round inclined curved")
					ENDIF
				ELSE
					IF pt_m=1 THEN
					PLACEGROUP subgroup ("board round inclined curved H","post rect inclined curved")
					ELSE
					PLACEGROUP subgroup ("board round inclined curved H","post round inclined curved")
					ENDIF
				ENDIF




				!!!VERTICAL

				IF hrt_m=1 THEN			!Board height calculation
					IF bt_m=1 THEN
					bh=hr-bw/2-hrh/2
					ELSE
					bh=hr-bd/2-hrh/2
					ENDIF
				ELSE
					IF bt_m=1 THEN
					bh=hr-bw/2-hrd/2
					ELSE
					bh=hr-bd/2-hrd/2
					ENDIF
				ENDIF


				GROUP "board round inclined curved V"			!Groups
					FOR k=1 to np-1 step 1
						ADD 0,rb,ds+bd/2
						ROTz dpostangle+distboardangle+dboardangle/2
						ADDy -rb
						ADDz deltazboard0
						FOR i=1 to nb step 1
						CYLIND bh,bd/2
						ADDy rb
						ROTz distboardangle+dboardangle
						ADDy -rb
						ADDz deltazboard
						NEXT i
						DEL 4*nb+4
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT k
					DEL 4*(np-1)
				ENDGROUP

				GROUP "board rect inclined curved V"
					FOR l=1 to np-1 step 1
						ADD 0,rb,ds+bw/2
						ROTz dpostangle+distboardangle+dboardangle/2
						ADDy -rb
						ADDz deltazboard0
						FOR i=1 to nb step 1
						PRISM_ 5,bh,
						-bw/2,-bth/2,15,
						 bw/2,-bth/2,15,
						 bw/2,bth/2,15,
						-bw/2,bth/2,15,
						-bw/2,-bth/2,-1
						ADDy rb
						ROTz distboardangle+dboardangle
						ADDy -rb
						ADDz deltazboard
						NEXT i
						DEL 4*nb+4
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT l
					DEL 4*(np-1)
				ENDGROUP

				GROUP "subboard round inclined curved V"
					FOR k=1 to np-1 step 1
						ADD 0,rb,ds+bd/2
						ROTz dpostangle+distboardangle/2
						ADDy -rb
						ADDz deltazboard0sub
						FOR i=1 to nb+1 step 1
						CYLIND sbh1,bd/2
						ADDy rb
						ROTz distboardangle+dboardangle
						ADDy -rb
						ADDz deltazboard
						NEXT i
						DEL 4*(nb+1)+4
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT k
					DEL 4*(np-1)
					FOR k=1 to np-1 step 1
						ADD 0,rb,sbhh2
						ROTz dpostangle+distboardangle/2
						ADDy -rb
						ADDz deltazboard0sub
						FOR i=1 to nb+1 step 1
						CYLIND sbh1,bd/2
						ADDy rb
						ROTz distboardangle+dboardangle
						ADDy -rb
						ADDz deltazboard
						NEXT i
						DEL 4*(nb+1)+4
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT k
					DEL 4*(np-1)
				ENDGROUP

				GROUP "subboard rect inclined curved V"
					FOR l=1 to np-1 step 1
						ADD 0,rb,ds+bw/2
						ROTz dpostangle+distboardangle/2
						ADDy -rb
						ADDz deltazboard0sub
						FOR i=1 to nb+1 step 1
						PRISM_ 5,sbh1+bw,
						-bw/2,-bth/2,15,
						 bw/2,-bth/2,15,
						 bw/2,bth/2,15,
						-bw/2,bth/2,15,
						-bw/2,-bth/2,-1
						ADDy rb
						ROTz distboardangle+dboardangle
						ADDy -rb
						ADDz deltazboard
						NEXT i
						DEL 4*(nb+1)+4
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT l
					DEL 4*(np-1)
					FOR l=1 to np-1 step 1
						ADD 0,rb,sbhh2+bw/2
						ROTz dpostangle+distboardangle/2
						ADDy -rb
						ADDz deltazboard0sub
						FOR i=1 to nb+1 step 1
						PRISM_ 5,sbh2+bw/2,
						-bw/2,-bth/2,15,
						 bw/2,-bth/2,15,
						 bw/2,bth/2,15,
						-bw/2,bth/2,15,
						-bw/2,-bth/2,-1
						ADDy rb
						ROTz distboardangle+dboardangle
						ADDy -rb
						ADDz deltazboard
						NEXT i
						DEL 4*(nb+1)+4
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT l
					DEL 4*(np-1)
				ENDGROUP



				IF bt_m=1 THEN
					IF hrt_m=1 THEN
						PLACEGROUP subgroup (subgroup("board rect inclined curved V","handrail rect inclined curved"),"board rect inclined curved H")
						PLACEGROUP subgroup (subgroup ("subboard rect inclined curved V","subboard rect inclined curved H"),"board rect inclined curved H")
						ELSE
						PLACEGROUP subgroup ("board rect inclined curved V","handrail round inclined curved")
						PLACEGROUP subgroup ("subboard rect inclined curved V","handrail round inclined curved")
					ENDIF
					IF pt_m=1 THEN
						PLACEGROUP subgroup (subgroup ("subboard rect inclined curved H","post rect inclined curved"),"board rect inclined curved V")
						ELSE
						PLACEGROUP subgroup (subgroup ("subboard rect inclined curved H","post round inclined curved"),"board rect inclined curved V")
					ENDIF
				ELSE
					IF hrt_m=1 THEN
						PLACEGROUP subgroup ("board round inclined curved V","board round inclined curved H")
						PLACEGROUP subgroup (subgroup ("subboard round inclined curved V","subboard round inclined curved H"),"board round inclined curved H")
						ELSE
						PLACEGROUP subgroup (subgroup ("board round inclined curved V","handrail round inclined curved"),"board round inclined curved H")
						PLACEGROUP subgroup (subgroup (subgroup ("subboard round inclined curved V","subboard round inclined curved H"),"board round inclined curved H"),"handrail round inclined curved")
					ENDIF
					IF pt_m=1 THEN
						PLACEGROUP subgroup (subgroup ("subboard round inclined curved H","post rect inclined curved"),"board round inclined curved V")
						ELSE
						PLACEGROUP subgroup (subgroup ("subboard round inclined curved H","post round inclined curved"),"board round inclined curved V")
					ENDIF
				ENDIF


				KILLGROUP "handrail round inclined curved"
				KILLGROUP "handrail rect inclined curved"
				KILLGROUP "post round inclined curved"
				KILLGROUP "post rect inclined curved"
				KILLGROUP "board rect inclined curved H"
				KILLGROUP "board round inclined curved H"
				KILLGROUP "subboard rect inclined curved H"
				KILLGROUP "subboard round inclined curved H"
				KILLGROUP "board rect inclined curved V"
				KILLGROUP "board round inclined curved V"
				KILLGROUP "subboard rect inclined curved V"
				KILLGROUP "subboard round inclined curved V"


		ENDIF

	ENDIF


ENDIF


!!DETAILED 3D END!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! ** HOTSPOTS ** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF LengthSize = 0 THEN			!!!OVERHANG OF HANDRAIL
		temphroL = -hroL
		temphroR = hroR
		RefPoint1 = 1
		RefPoint2 = -1

		InclRefPoint = -tan(ani)

		temphroLDiag = -hroLDiagonal
		temphroRDiag = hroRDiagonal
	ELSE
		temphroL = hroL
		temphroR = -hroR
		RefPoint1 = -1
		RefPoint2 = 1

		InclRefPoint = tan(ani)

		temphroLDiag = hroLDiagonal
		temphroRDiag = -hroRDiagonal
	ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF cfs=0 THEN
		IF ABS(ani)<EPS and ptyp_m=1 THEN

				!!!LENGHT
				HOTSPOT 0,0,ds+hr,unID,lra,1+256 : unID=unID+1
				HOTSPOT lra,0,ds+hr,unID,lra,2 : unID=unID+1
				HOTSPOT -1,0,ds+hr,unID,lra,3 : unID=unID+1

				IF LengthSize = 0 THEN
					HOTSPOT 0,0,0,unID,lra,1+256 : unID=unID+1
					HOTSPOT lra,0,0,unID,lra,2 : unID=unID+1
					HOTSPOT -1,0,0,unID,lra,3 : unID=unID+1

					HOTSPOT 0,0,ds,unID,lra,1+256 : unID=unID+1
					HOTSPOT lra,0,ds,unID,lra,2 : unID=unID+1
					HOTSPOT -1,0,ds,unID,lra,3 : unID=unID+1
				ENDIF

				!!!DISTANCE FROM FLOOR
				HOTSPOT templra/2+LengthSize*hroL,0,0,unID,ds,1+128 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,-1,unID,ds,3 : unID=unID+1

				!!!HEIGHT OF RAILING
				HOTSPOT templra/2+LengthSize*hroL,0,ds,unID,hr,1 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,-1,unID,hr,3 : unID=unID+1

				!!!POST BASE OVERHANG
				HOTSPOT LengthSize*hroL,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
				HOTSPOT LengthSize*hroL,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT LengthSize*hroL,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				HOTSPOT lra-LengthSize*hroR,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
				HOTSPOT lra-LengthSize*hroR,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT lra-LengthSize*hroR,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				IF gs_detlevel_3D_m=2 THEN		!!DETAILED
					!!!OVERHANG OF RAILING
					HOTSPOT 0,0,ds+hr,unID,hroL,1+128 : unID=unID+1
					HOTSPOT temphroL,0,ds+hr,unID,hroL,2 : unID=unID+1
					HOTSPOT RefPoint1,0,ds+hr,unID,hroL,3 : unID=unID+1

					HOTSPOT lra,0,ds+hr,unID,hroR,1+128 : unID=unID+1
					HOTSPOT lra+temphroR,0,ds+hr,unID,hroR,2 : unID=unID+1
					HOTSPOT lra+RefPoint2,0,ds+hr,unID,hroR,3 : unID=unID+1
				ENDIF

		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)<EPS and ptyp_m<>1 THEN
			IF ab<360 THEN

					IF ptyp_m=3 THEN
					!!!ROTATE
						HOTSPOT 0,rb,ds,unID,ab,6 : unID=unID+1
						HOTSPOT 0,0,ds,unID,ab,4+256 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ab,5 : unID=unID+1

						HOTSPOT 0,rb,ds+hr,unID,ab,6 : unID=unID+1
						HOTSPOT 0,0,ds+hr,unID,ab,4+256 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr,unID,ab,5 : unID=unID+1

					!!!FIX HOTSPOT AT FLOOR
						HOTSPOT 0,0,0,unID : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID : unID=unID+1
					ENDIF

					!!!HEIGHT OF RAILING
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,hr,1+128 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr,unID,hr,2 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

					!!! DISTANCE FROM FLOOR
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),0,unID,ds,1 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,ds,2 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

					!!!POST BASE OVERHANG
						HOTSPOT 0,0,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
						HOTSPOT 0,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT 0,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

						HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					IF ptyp_m=2 THEN
						!!!HEIGHT OF RAILING
							HOTSPOT 0,0,ds,unID,hr,1+128 : unID=unID+1
							HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
							HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

							HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,hr,1+128 : unID=unID+1
							HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr,unID,hr,2 : unID=unID+1
							HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

						!!! DISTANCE FROM FLOOR
							HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
							HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
							HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

							HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID,ds,1 : unID=unID+1
							HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ds,2 : unID=unID+1
							HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
					ENDIF


			ELSE	!!!ANGLE=360


				!!!HOTSPOTS FOR GRAPHICAL EDITING
				!!!HEIGHT

					HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
					HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

					HOTSPOT rb,rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT rb,rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT rb,rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT 0,2*rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT 0,2*rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,2*rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT -rb,rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT -rb,rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT -rb,rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

					HOTSPOT rb,rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT rb,rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT rb,rb,-1,unID,ds,3 : unID=unID+1

					HOTSPOT 0,2*rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,2*rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,2*rb,-1,unID,ds,3 : unID=unID+1

					HOTSPOT -rb,rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT -rb,rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT -rb,rb,-1,unID,ds,3 : unID=unID+1

					!!!POST BASE OVERHANG
					HOTSPOT 0,0,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT 0,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT 0,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					HOTSPOT rb,rb,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT rb,rb,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT rb,rb,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					HOTSPOT 0,2*rb,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT 0,2*rb,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT 0,2*rb,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					HOTSPOT -rb,rb,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT -rb,rb,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT -rb,rb,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

			ENDIF
		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m=1 THEN			!!ptyp=Straight
			IF rom_m=1 THEN					!!!!VERTICAL Orientation

					IF rmm_m=2 THEN			!!Perpendicular Measurement
						ftw=ftw/cos(ani)
						fbw=fbw/cos(ani)
					ENDIF

					IF rmm_m=1 THEN			!!!!VERTICAL Measurement
						temp_ds=ds
						temp_hr=hr
					ELSE					!!Perpendicular Measurement
						temp_ds=dsPerp
						temp_hr=hrPerp
					ENDIF


					!!!	HOTSPOTS FOR GRAPHICAL EDITING
					!!! LENGTH OF RAILING

						HOTSPOT 0,	0,	temp_ds+temp_hr,				unID, lrah, 1+256 : unID=unID+1
						HOTSPOT -1, 0,	temp_ds+temp_hr-tan(ani),		unID, lrah, 3 : unID=unID+1
						HOTSPOT lra,0, 	temp_ds+temp_hr+lra*tan(ani),	unID, lrah, 2 : unID=unID+1

					IF LengthSize = 0 THEN
						HOTSPOT 0,	0,	0,				unID, lrah, 1+256 : unID=unID+1
						HOTSPOT -1, 0,	0-tan(ani),		unID, lrah, 3 : unID=unID+1
						HOTSPOT lra,0, 	0+lra*tan(ani),	unID, lrah, 2 : unID=unID+1

						HOTSPOT 0,	0,	temp_ds,				unID, lrah, 1+256 : unID=unID+1
						HOTSPOT -1, 0,	temp_ds-tan(ani),		unID, lrah, 3 : unID=unID+1
						HOTSPOT lra,0, 	temp_ds+lra*tan(ani),	unID, lrah, 2 : unID=unID+1
					ENDIF

					IF rmm_m=1 THEN
						!!! DISTANCE FROM FLOOR
						HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani),	unID, ds, 1+128 : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, ds, 3 : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0, 	(templra/2+LengthSize*hroL)*tan(ani)+temp_ds,	unID, ds, 2 : unID=unID+1

						!!! HEIGHT of RAILING
						HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani),	unID, hr, 1+128 : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, hr, 3 : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0, 	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)+temp_hr,unID, hr, 2 : unID=unID+1

						!!!POST BASE OVERHANG
						HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani),unID,gs_PostBaseOverhang,1 : unID=unID+1
						HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1

						HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani),unID,gs_PostBaseOverhang,1 : unID=unID+1
						HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					ELSE
						!!! DISTANCE FROM FLOOR
						HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani),	unID, dsPerp, 1+128, ds : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, dsPerp, 3, ds : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0, 	(templra/2+LengthSize*hroL)*tan(ani)+temp_ds,	unID, dsPerp, 2, ds : unID=unID+1

						!!! HEIGHT of RAILING
						HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani),	unID, hrPerp, 1+128, hr : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, hrPerp, 3, hr  : unID=unID+1
						HOTSPOT templra/2+LengthSize*hroL,	0, 	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)+temp_hr,unID, hrPerp, 2, hr  : unID=unID+1

						!!!POST BASE OVERHANG
						HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani),unID,gs_PostBaseOverhangPerp,1,gs_PostBaseOverhang : unID=unID+1
						HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)-temp_PostBaseOverhang,unID,gs_PostBaseOverhangPerp,2,gs_PostBaseOverhang : unID=unID+1
						HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)+1,unID,gs_PostBaseOverhangPerp,3,gs_PostBaseOverhang : unID=unID+1

						HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani),unID,gs_PostBaseOverhangPerp,1,gs_PostBaseOverhang : unID=unID+1
						HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)-temp_PostBaseOverhang,unID,gs_PostBaseOverhangPerp,2,gs_PostBaseOverhang : unID=unID+1
						HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)+1,unID,gs_PostBaseOverhangPerp,3,gs_PostBaseOverhang : unID=unID+1

					ENDIF

					IF gs_detlevel_3D_m=2 THEN		!!DETAILED

					!!!OVERHANG OF RAILING
						HOTSPOT 0,0,	temp_ds+temp_hr,				unID,hroLDiagonal,1+128,hroL : unID=unID+1
						HOTSPOT temphroL,0,	temp_ds+temp_hr+temphroL*tan(ani),	unID,hroLDiagonal,2	,hroL	: unID=unID+1
						HOTSPOT RefPoint1,0,temp_ds+temp_hr-InclRefPoint,		unID,hroLDiagonal,3	,hroL	: unID=unID+1

						HOTSPOT lra,0,		temp_ds+temp_hr+lra*tan(ani),				unID,hroRDiagonal,1+128,hroR : unID=unID+1
						HOTSPOT lra+temphroR,0,	temp_ds+temp_hr+lra*tan(ani)+temphroR*tan(ani),	unID,hroRDiagonal,2	,hroR	: unID=unID+1
						HOTSPOT lra+RefPoint2,0,	temp_ds+temp_hr+lra*tan(ani)+InclRefPoint,		unID,hroRDiagonal,3	,hroR	: unID=unID+1

					ENDIF

			ELSE			!Perpendicular Orientation
							!Measurement is always perpendicular to inclination in this case

				ROTy -ani

						!!! LENGTH OF RAILING

							HOTSPOT 0,0,ds+hr,unID,lrah,1+256 : unID=unID+1
							HOTSPOT lrah,0,ds+hr,unID,lrah,2 	: unID=unID+1
							HOTSPOT -1,0,ds+hr,unID,lrah,3 	: unID=unID+1

						IF LengthSize = 0 THEN
							HOTSPOT 0,0,0,unID,lrah,1+256 : unID=unID+1
							HOTSPOT lrah,0,0,unID,lrah,2 	: unID=unID+1
							HOTSPOT -1,0,0,unID,lrah,3 	: unID=unID+1

							HOTSPOT 0,0,ds,unID,lrah,1+256 : unID=unID+1
							HOTSPOT lrah,0,ds,unID,lrah,2 	: unID=unID+1
							HOTSPOT -1,0,ds,unID,lrah,3 	: unID=unID+1
						ENDIF


						!!!DISTANCE FROM FLOOR
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,0,unID,ds,1+128 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,ds,unID,ds,2 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,-1,unID,ds,3 : unID=unID+1

						!!! HEIGHT of RAILING
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,ds,unID,hr,1 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,ds+hr,unID,hr,2 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,-1,unID,hr,3 : unID=unID+1

						!!!POST BASE OVERHANG
						HOTSPOT LengthSize*hroLDiagonal,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

						HOTSPOT LengthSize*hroLDiagonal+templrah,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal+templrah,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal+templrah,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					IF gs_detlevel_3D_m=2 THEN		!!DETAILED
						!!!OVERHANG OF RAILING
						!!! OVERHANG
						HOTSPOT lrah,0,ds+hr,unID,hroRDiagonal,1+128 , hroR : unID=unID+1
						HOTSPOT lrah+temphroRDiag,0,ds+hr,unID,hroRDiagonal,2 , hroR 	: unID=unID+1
						HOTSPOT lrah+RefPoint2,0,ds+hr,unID,hroRDiagonal,3 , hroR 	: unID=unID+1

						HOTSPOT 0,0,ds+hr,unID,hroLDiagonal,1+128 , hroL: unID=unID+1
						HOTSPOT temphroLDiag,0,ds+hr,unID,hroLDiagonal,2, hroL 	: unID=unID+1
						HOTSPOT RefPoint1,0,ds+hr,unID,hroLDiagonal,3, hroL 	: unID=unID+1
					ENDIF

				DEL 1

			ENDIF

		ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m<>1 THEN

			!!!HOTSPOTS FOR GRAPHICAL EDITING

			IF ptyp_m=3 THEN
				!!!ROTATE
				HOTSPOT 0,rb,ds+lra*sin(ani),unID,ab,6 : unID=unID+1
				HOTSPOT 0,0,ds+lra*sin(ani),unID,ab,4+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ab,5 : unID=unID+1

				HOTSPOT 0,rb,ds+hr+lra*sin(ani),unID,ab,6 : unID=unID+1
				HOTSPOT 0,0,ds+hr+lra*sin(ani),unID,ab,4+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr+lra*sin(ani),unID,ab,5 : unID=unID+1
			ENDIF

				!!!HEIGHT OF RAILING
				HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
				HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,hr,1 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr+(lra/2)*sin(ani),unID,hr,2 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,hr,3 : unID=unID+1

				!!!DISTANCE FROM FLOOR
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani),unID,ds,1 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,ds,2 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

				HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
				HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

				!!!FIX HOTSPOT AT FLOOR
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),0+lra*sin(ani), unID : unID=unID+1

			IF ptyp_m=2 THEN
				!!!HEIGHT OF RAILING
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,hr,1 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr+lra*sin(ani),unID,hr,2 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

				!!!DISTANCE FROM FLOOR
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani),unID,ds,1+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ds,2 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
			ENDIF

				!!!POST BASE OVERHANG
				HOTSPOT 0,0,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
				HOTSPOT 0,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT 0,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani),unID,gs_PostBaseOverhang,1+128 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani),unID,gs_PostBaseOverhang,1+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1

		ENDIF

	ENDIF
end
