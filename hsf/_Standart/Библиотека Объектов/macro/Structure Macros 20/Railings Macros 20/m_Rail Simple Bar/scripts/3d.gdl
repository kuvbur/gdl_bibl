
!Rail Simple Bar

if gs_detlevel_3D_m=0 then end

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then 		!! for Simple Model and in 3D Windoow
gs_detlevel_3D_m=1
endif

if gs_shadow=0 then SHADOW OFF

IF ABS(lra)<EPS THEN END

pen gs_cont_pen
unID=1


If gs_detlevel_3D_m=1 or gs_detlevel_3D_m=2 then

XPosL0=(fth/2)/tan(gs_AngleL)
XPosR0=(fth/2)/tan(gs_AngleR)

XPosL=ABS((fth/2)/tan(gs_AngleL))
XPosR=ABS((fth/2)/tan(gs_AngleR))
YPosL=(XPosL*tan(ani))
YPosR=(XPosR*tan(ani))

material fmat

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if ABS(ani)<EPS and ptyp_m=1 then

	resol rescs

			!!!FRAME
			MATERIAL fmat
			if btyp_m=1 then

				ADDz ds+hr-bh
					PRISM_ 5,bh,
						XPosL0,fth/2,15,
						-XPosL0,-fth/2,15,
						lra+XPosR0,-fth/2,15,
						lra-XPosR0,fth/2,15,
						XPosL0,fth/2,-1
				DEL 1

			else

				ROTz -90+gs_AngleL		!! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 2

				ADDx lra
				ROTz 90-gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3

						add -XPosL0, 0, ds+hr-bd/2
						roty 90
						cylind lra+XPosL0+XPosR0,bd/2
						del 2
				CUTEND
				CUTEND

			endif

			IF cfs=0 THEN
				!!!LENGHT

				HOTSPOT 0,0,0,unID,lra,1+256 : unID=unID+1
				HOTSPOT lra,0,0,unID,lra,2 : unID=unID+1
				HOTSPOT -1,0,0,unID,lra,3 : unID=unID+1

				HOTSPOT 0,0,ds+hr,unID,lra,1+256 : unID=unID+1
				HOTSPOT lra,0,ds+hr,unID,lra,2 : unID=unID+1
				HOTSPOT -1,0,ds+hr,unID,lra,3 : unID=unID+1

				!!!DISTANCE FROM FLOOR
				HOTSPOT lra,0,0,unID,ds,1+128 : unID=unID+1
				HOTSPOT lra,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT lra,0,-1,unID,ds,3 : unID=unID+1

				HOTSPOT 0,0,0,unID,ds,1+128 : unID=unID+1
				HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

				!!!HEIGHT OF RAILING
				HOTSPOT lra/2,0,ds,unID,hr,1+128 : unID=unID+1
				HOTSPOT lra/2,0,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT lra/2,0,-1,unID,hr,3 : unID=unID+1

			ENDIF
	endif


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


	if ABS(ani)<EPS and ptyp_m<>1 then

	!FRAME
	material fmat
	resol res

	if ab<360 then

			if btyp_m=1 then
				addz ds+hr-bh
					PRISM_ 10,bh,
						0,fth/2,15,
						0,-fth/2+0.00005,15,
						0,-fth/2,79,
						1,0,800,
						(rb+(fth/2))*sin(ab),rb-(rb+(fth/2))*cos(ab),1015,
						(rb-(fth/2)+0.00005)*sin(ab),rb-(rb-(fth/2)+0.00005)*cos(ab),15,
						(rb-(fth/2))*sin(ab),rb-(rb-(fth/2))*cos(ab),79,
						-cos(ab),-sin(ab),800,
						0,fth/2,1015,
						0,fth/2,-1
				del 1
			else
					gamma0=360/rescs
					gamma=0
					for i=1 to rescs step 1
					put bd/2*cos(gamma),rb+bd/2*sin(gamma),1
					gamma=gamma+gamma0
					next i

					add  0,rb,ds+hr-bd/2
					roty 90
					rotx 180-ab
						REVOLVE rescs+1,ab,1+2+4+8+16+32,
						get (nsp),
					bd/2,rb,-1
					del 3

			endif

			IF cfs=0 THEN
				!!!HOTSPOTS FOR GRAPHICAL EDITING
				!!!ROTATE
				IF ptyp_m=3 THEN
					HOTSPOT 0,rb,ds,unID,ab,6 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ab,4+256 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ab,5 : unID=unID+1

					!!!FIX HOTSPOT AT FLOOR
					HOTSPOT 0,0,0,unID : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID : unID=unID+1
				ENDIF
				!!!HEIGHT
					HOTSPOT 0,0,ds,unID,hr,1+128 : unID=unID+1
					HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,hr,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,hr,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),0,unID,ds,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,ds,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

					!!!FIX HOTSPOT AT FLOOR
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),0,unID : unID=unID+1

				IF ptyp_m=2 THEN
					HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

					HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID,ds,1 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ds,2 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
				ENDIF
			ENDIF

	else	!!!ANGLE=360

			if btyp_m=1 then
				addz ds+hr-bh
					prism_ 12,bh,
					0,-fth/2,79,
					1,0,800,
					0,2*rb+fth/2,1079,
					-1,0,800,
					0,-fth/2,1079,
					0,-fth/2,-1,

					0,fth/2,79,
					1,0,800,
					0,2*rb-fth/2,1079,
					-1,0,800,
					0,fth/2,1079,
					0,fth/2,-1
				del 1
			else
					gamma0=360/rescs
					gamma=0
					for i=1 to rescs step 1
					put bd/2*cos(gamma),rb+bd/2*sin(gamma),1
					gamma=gamma+gamma0
					next i
					add  0,rb,ds+hr-bd/2
					roty 90
					rotx 180-ab
					revolve rescs+1,360,0,
					get (nsp),
					bd/2,rb,-1
					del 3
			endif

			IF cfs=0 THEN

			!!!HOTSPOTS FOR GRAPHICAL EDITING
			!!!HEIGHT

				HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
				HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

				HOTSPOT rb,rb,ds,unID,hr,1 : unID=unID+1
				HOTSPOT rb,rb,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT rb,rb,-1,unID,hr,3 : unID=unID+1

				HOTSPOT 0,2*rb,ds,unID,hr,1 : unID=unID+1
				HOTSPOT 0,2*rb,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT 0,2*rb,-1,unID,hr,3 : unID=unID+1

				HOTSPOT -rb,rb,ds,unID,hr,1 : unID=unID+1
				HOTSPOT -rb,rb,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT -rb,rb,-1,unID,hr,3 : unID=unID+1

				HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
				HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

				HOTSPOT rb,rb,0,unID,ds,1 : unID=unID+1
				HOTSPOT rb,rb,ds,unID,ds,2 : unID=unID+1
				HOTSPOT rb,rb,-1,unID,ds,3 : unID=unID+1

				HOTSPOT 0,2*rb,0,unID,ds,1 : unID=unID+1
				HOTSPOT 0,2*rb,ds,unID,ds,2 : unID=unID+1
				HOTSPOT 0,2*rb,-1,unID,ds,3 : unID=unID+1

				HOTSPOT -rb,rb,0,unID,ds,1 : unID=unID+1
				HOTSPOT -rb,rb,ds,unID,ds,2 : unID=unID+1
				HOTSPOT -rb,rb,-1,unID,ds,3 : unID=unID+1
			ENDIF

		endif

	endif


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


if ABS(ani)>EPS and ptyp_m=1 then

	RESOL rescs

		if rom_m=1 then				!Vertical Orientation
			if rmm_m=2 then		!Perpendicular Measurement Extra Values
				ftw=ftw/cos(ani)
				fbw=fbw/cos(ani)
			endif

			IF rmm_m=1 THEN			!!Vertical Measurement
				temp_ds=ds
				temp_hr=hr
				temp_bd=(bd/2)*cos(ani)
			ELSE					!!Perpendicular Measurement
				temp_ds=dsPerp
				temp_hr=hrPerp
				temp_bd=bd/2
			ENDIF


			ROTz -90+gs_AngleL		!!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 2

			ADDx lra
			ROTz 90-gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3

				!!!FRAME
				material fmat
				if btyp_m=1 then
					rotx 90
					addz -fth/2
					PRISM_ 5,fth,
						-XPosL,temp_ds+temp_hr-bh-YPosL,15,
						lra+XPosR,temp_ds+temp_hr-bh+lra*tan(ani)+YPosR,15,
						lra+XPosR,temp_ds+temp_hr+lra*tan(ani)+YPosR,15,
						-XPosL,temp_ds+temp_hr-YPosL,15,
						-XPosL,temp_ds+temp_hr-bh-YPosL,-1
					del 2
				else
					add -XPosL,0,temp_ds+temp_hr-bd/2
					roty 90-ani
						CONE lra/cos(ani)+XPosL+XPosR,temp_bd,temp_bd,90+ani,90+ani
					del 2
				endif

			CUTEND
			CUTEND

			IF cfs=0 THEN
					!!!HOTSPOTS FOR GRAPHICAL EDITING

					!!! LENGTH of RAILING
					HOTSPOT 0,	0,	0,				unID, lrah, 1+256 : unID=unID+1
					HOTSPOT -1, 0,	0-tan(ani),		unID, lrah, 3 : unID=unID+1
					HOTSPOT lra,0, 	0+lra*tan(ani),	unID, lrah, 2 : unID=unID+1

					HOTSPOT 0,	 0,	temp_ds+temp_hr,				unID, lrah, 1+256 : unID=unID+1
					HOTSPOT -1,  0,	temp_ds+temp_hr-tan(ani),		unID, lrah, 3 : unID=unID+1
					HOTSPOT lra, 0, temp_ds+temp_hr+lra*tan(ani),	unID, lrah, 2 : unID=unID+1


				IF rmm_m=1 THEN

					!!! DISTANCE FROM FLOOR

					HOTSPOT 0,	0,	0,	unID, ds, 1+128 : unID=unID+1
					HOTSPOT 0,	0,	-1,	unID, ds, 3 : unID=unID+1
					HOTSPOT 0,	0, 	temp_ds,	unID, ds, 2 : unID=unID+1

					HOTSPOT lra,	0,	lra*tan(ani),	unID, ds, 1+128 : unID=unID+1
					HOTSPOT lra,	0,	lra*tan(ani)-1,	unID, ds, 3 : unID=unID+1
					HOTSPOT lra,	0, 	lra*tan(ani)+temp_ds,	unID, ds, 2 : unID=unID+1

					!!! HEIGHT of RAILING
					HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani),	unID, hr, 1+128 : unID=unID+1
					HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani)-1,	unID, hr, 3 : unID=unID+1
					HOTSPOT lra/2,	0, 	temp_ds+lra/2*tan(ani)+temp_hr,unID, hr, 2 : unID=unID+1

				ELSE

					!!! DISTANCE FROM FLOOR
					HOTSPOT 0,	0,	0,	unID, dsPerp, 1+128, ds : unID=unID+1
					HOTSPOT 0,	0,	0-1,	unID, dsPerp, 3, ds : unID=unID+1
					HOTSPOT 0,	0, 	0+temp_ds,	unID, dsPerp, 2, ds : unID=unID+1

					HOTSPOT lra,	0,	lra*tan(ani),	unID, dsPerp, 1+128, ds : unID=unID+1
					HOTSPOT lra,	0,	lra*tan(ani)-1,	unID, dsPerp, 3, ds : unID=unID+1
					HOTSPOT lra,	0, 	lra*tan(ani)+temp_ds,	unID, dsPerp, 2, ds : unID=unID+1

					!!! HEIGHT of RAILING
					HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani),	unID, hrPerp, 1+128, hr : unID=unID+1
					HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani)-1,	unID, hrPerp, 3, hr  : unID=unID+1
					HOTSPOT lra/2,	0, 	temp_ds+lra/2*tan(ani)+temp_hr,unID, hrPerp, 2, hr  : unID=unID+1

				ENDIF
			ENDIF

	else						!Perpendicular Orientation
								!Measurement is always perpendicular to inclination in this case

		roty -ani

			!FRAME
			material fmat
			if btyp_m=1 then
				rotx 90
				addz -fth/2
					prism_ 5,fth,
						0,ds+hr-bh,15,
						lrah,ds+hr-bh,15,
						lrah,ds+hr,15,
						0,ds+hr,15,
						0,ds+hr-bh,-1
				del 2
			else
				addz ds+hr-(bd/2)
				roty 90
					cylind lrah,bd/2
				del 2
			endif

			IF cfs=0 THEN
				!!! LENGTH OF RAILING
				HOTSPOT 0,0,0,unID,lrah,1+256 : unID=unID+1
				HOTSPOT lrah,0,0,unID,lrah,2 	: unID=unID+1
				HOTSPOT -1,0,0,unID,lrah,3 	: unID=unID+1

				HOTSPOT 0,0,ds+hr,unID,lrah,1+256 : unID=unID+1
				HOTSPOT lrah,0,ds+hr,unID,lrah,2 	: unID=unID+1
				HOTSPOT -1,0,ds+hr,unID,lrah,3 	: unID=unID+1

				!!!DISTANCE FROM FLOOR
				HOTSPOT 0,0,0,unID,ds,1+128 : unID=unID+1
				HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

				HOTSPOT lrah,0,0,unID,ds,1+128 : unID=unID+1
				HOTSPOT lrah,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT lrah,0,-1,unID,ds,3 : unID=unID+1

				!!! HEIGHT of RAILING
				HOTSPOT lrah/2,0,ds,unID,hr,1+128 : unID=unID+1
				HOTSPOT lrah/2,0,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT lrah/2,0,-1,unID,hr,3 : unID=unID+1

			ENDIF

		del 1

	endif


endif



!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!


	if ABS(ani)>EPS and ptyp_m<>1 then

	resol rescs

		!FRAME
		material fmat

		alphai=-alphaincl
		deltazi=-deltazincl
		for i=1 to nalphaincl+3 step 1
		put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
		alphai=alphai+alphaincl
		deltazi=deltazi+deltazincl
		next i

		if btyp_m=1 then
			addz hr-bh
			tubea 5,nalphaincl+3,1+2+16+32,
			-fth/2,0,0,
			fth/2,0,0,
			fth/2,bh,0,
			-fth/2,bh,0,
			-fth/2,0,-1,
			get (nsp)
			del 1
		else
			addz hr-bd/2
			tubea 6,nalphaincl+3,1+2+16+32,
			-bd/2,0,1,
			0,-1,801,
			bd/2,0,1001,
			0,1,801,
			-bd/2,0,1001,
			-bd/2,0,-1,
			get (nsp)
			del 1
		endif

		IF cfs=0 THEN
			!!!HOTSPOTS FOR GRAPHICAL EDITING
			!!!ROTATE
			IF ptyp_m=3 THEN
				HOTSPOT 0,rb,ds+lra*sin(ani),unID,ab,6 : unID=unID+1
				HOTSPOT 0,0,ds+lra*sin(ani),unID,ab,4+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ab,5 : unID=unID+1
			ENDIF

			!!!HEIGHT
			HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
			HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
			HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

			HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,hr,1 : unID=unID+1
			HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr+(lra/2)*sin(ani),unID,hr,2 : unID=unID+1
			HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,hr,3 : unID=unID+1

			HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,hr,1 : unID=unID+1
			HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr+lra*sin(ani),unID,hr,2 : unID=unID+1
			HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

			!!!DISTANCE FROM FLOOR

			HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani),unID,ds,1+128 : unID=unID+1
			HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,ds,2 : unID=unID+1
			HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

			HOTSPOT 0,0,0,unID,ds,1+128 : unID=unID+1
			HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
			HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

			!!!FIX HOTSPOT AT FLOOR
			HOTSPOT 0,0,0, unID : unID=unID+1
			HOTSPOT rb*sin(ab),rb-rb*cos(ab),0+lra*sin(ani), unID : unID=unID+1
			HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),0+(lra/2)*sin(ani), unID : unID=unID+1

			IF ptyp_m=2 THEN
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani),unID,ds,1+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ds,2 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
			ENDIF

		ENDIF

	endif


endif

!!SIMPLE 3D END!!

end


!!SUBROUTINES!!
