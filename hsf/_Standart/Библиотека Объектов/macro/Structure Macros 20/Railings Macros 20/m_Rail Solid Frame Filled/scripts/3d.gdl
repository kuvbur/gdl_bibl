
if gs_detlevel_3D_m=0 then end
if not(gs_shadow) then SHADOW OFF

IF ABS(lra)<EPS THEN END

pen gs_cont_pen
unID=1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** SIMPLE 3D *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!CALCULATION FOR CONNECTING ANGLE

XPosL0=(fth/2)/tan(gs_AngleL)
XPosR0=(fth/2)/tan(gs_AngleR)

if gs_frameStyle_m = 2 then
	if abs (gs_AngleL - 90) > EPS then
		XPosL = gs_frameDiameter / 2
	else
		XPosL = 0
	endif
	if abs (gs_AngleR - 90) > EPS then
		XPosR = gs_frameDiameter / 2
	else
		XPosR = 0
	endif
else
XPosL=ABS((fth/2)/tan(gs_AngleL))
XPosR=ABS((fth/2)/tan(gs_AngleR))
endif

YPosL=(XPosL*tan(ani))
YPosR=(XPosR*tan(ani))

IF gs_detlevel_3D_m=1 THEN		!!Simple

	resol res

	MATERIAL fmat

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF ABS(ani)<EPS and ptyp_m=1 THEN		!!ptyp=Straight

		ADDz ds

			PRISM_ 5,hr,
				XPosL0,fth/2,15,
				-XPosL0,-fth/2,15,
				lra+XPosR0,-fth/2,15,
				lra-XPosR0,fth/2,15,
				XPosL0,fth/2,-1

		DEL 1
	ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF ABS(ani)<EPS and ptyp_m<>1 THEN

		!FRAME
		IF ab<360 THEN
				ADDz ds
				PRISM_ 10,hr,
					0,fth/2,15,
					0,-fth/2+0.00005,15,
					0,-fth/2,79,
					1,0,800,
					(rb+(fth/2))*sin(ab),rb-(rb+(fth/2))*cos(ab),1015,
					(rb-(fth/2)+0.00005)*sin(ab),rb-(rb-(fth/2)+0.00005)*cos(ab),15,
					(rb-(fth/2))*sin(ab),rb-(rb-(fth/2))*cos(ab),79,
					-cos(ab),-sin(ab),800,
					0,fth/2,1015,
					0,fth/2,-1

				DEL 1

		ELSE	!!!ANGLE=360

			ADDz ds

				PRISM_ 12,hr,
					0,-fth/2,79,
					1,0,800,
					0,2*rb+fth/2,1079,
					-1,0,800,
					0,-fth/2,1079,
					0,-fth/2,-1,

					0,fth/2,79,
					1,0,800,
					0,2*rb-fth/2,1079,
					-1,0,800,
					0,fth/2,1079,
					0,fth/2,-1

			DEL 1

		ENDIF
	ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF ABS(ani)>EPS and ptyp_m=1 THEN	!ptyp=Straight
		IF rom_m=1 THEN					!Vertical Orientation

				IF rmm_m=1 THEN			!Vertical Measurement
					temp_ds=ds
					temp_hr=hr
				ELSE					!Perpendicular Measurement
					temp_ds=dsPerp
					temp_hr=hrPerp
				ENDIF

				ROTz -90+gs_AngleL		! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 2

				ADDx lra
				ROTz 90-gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3


					ROTx 90
					ADDz -fth/2

						PRISM_ 5,fth,
							-XPosL,temp_ds-YPosL,15,
							lra+XPosR,temp_ds+lra*tan(ani)+YPosR,15,
							lra+XPosR,temp_ds+temp_hr+lra*tan(ani)+YPosR,15,
							-XPosL,temp_ds+temp_hr-YPosL,15,
							-XPosL,temp_ds-YPosL,-1

					DEL 2

				CUTEND
				CUTEND

		ELSE						!Perpendicular Orientation
									!Measurement is always perpendicular to inclination in this case

			ROTy -ani

				ROTx 90
				ADDz -fth/2
				!FRAME
				MATERIAL fmat
				PRISM_ 5,fth,
					0,ds,15,
					lrah,ds,15,
					lrah,ds+hr,15,
					0,ds+hr,15,
					0,ds,-1
				DEL 2

			DEL 1

		ENDIF
	ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF ABS(ani)>EPS and ptyp_m<>1 THEN

		!FRAME
		MATERIAL fmat

		alphai=-alphaincl
		DELtazi=-DELtazincl
		for i=1 to nalphaincl+3
			put rb*sin(alphai),rb-rb*cos(alphai),ds+DELtazi
			alphai=alphai+alphaincl
			DELtazi=DELtazi+DELtazincl
		next i

		TUBEA 5,nalphaincl+3,1+2+16+32,
				-fth/2,0,0,
				fth/2,0,0,
				fth/2,hr,0,
				-fth/2,hr,0,
				-fth/2,0,-1,
				get (nsp)

	ENDIF
ENDIF		!!END of SIMPLE 3D!!


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** DETAILED 3D *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if gs_detlevel_3D_m=2 then

	if cfs then resol res else resol rescs

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if ABS(ani)<EPS and ptyp_m=1 then			!ptyp=Straight

		!3D FEEDBACK AT HOTSPOT EDITING
		IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				ADDz ds
				MATERIAL fmat
				PRISM_ 5,hr,
					XPosL0,fth/2,15,
					-XPosL0,-fth/2,15,
					lra+XPosR0,-fth/2,15,
					lra-XPosR0,fth/2,15,
					XPosL0,fth/2,-1
				DEL 1

		ELSE	! 3D DETAILED MODEL

			MATERIAL fmat

			ROTz -90+gs_AngleL		!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 2

			ADDx lra
			ROTz 90-gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3

				rotx 90
			! FRAME

			if gs_frameStyle_m = 2 then
				tube 2, 7, 1+2,
					0, 0, 900,
					gs_frameDiameter/2, 360, 4001,

					-XPosL + gs_frameDiameter/2,		ds + hr - gs_frameDiameter/2,	0, 0,
					-XPosL + gs_frameDiameter/2,		ds + gs_frameDiameter/2,		0, 0,
					lra + XPosR - gs_frameDiameter/2,	ds + gs_frameDiameter/2,		0, 0,
					lra + XPosR - gs_frameDiameter/2,	ds + hr - gs_frameDiameter/2,	0, 0,
					-XPosL + gs_frameDiameter/2,		ds + hr - gs_frameDiameter/2,	0, 0,
					-XPosL + gs_frameDiameter/2,		ds + gs_frameDiameter/2,		0, 0,
					lra + XPosR - gs_frameDiameter/2,	ds + gs_frameDiameter/2,		0, 0
			else
				addz -fth/2
				MATERIAL fmat
				prism_ 10,fth,
						-XPosL,ds,15,
						lra+XPosR,ds,15,
						lra+XPosR,ds+hr,15,
						-XPosL,ds+hr,15,
						-XPosL,ds,-1,
						fsw,ds+fbw,15,
						lra-fsw,ds+fbw,15,
						lra-fsw,ds+hr-ftw,15,
						fsw,ds+hr-ftw,15,
						fsw,ds+fbw,-1

				BODY -1
				del 1
			endif
			del 1

			CUTEND
			CUTEND

			MATERIAL bmat

			ROTz -90+gs_AngleL		!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 2

			ADDx lra
			ROTz 90-gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3

				rotx 90
				addz -fth/2

					!!! *** FILL
					MATERIAL bmat
					add fsw+fdf,ds+fbw+fdf,(fth-bth)/2
					block fillw,fillh,bth
					del 1
				del 2

			CUTEND
			CUTEND
		ENDIF
	ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if ABS(ani)<EPS and ptyp_m<>1 then		!ptyp=Straight

		!!3D FEEDBACK AT HOTSPOT EDITING
		IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

			resol res

			!!!!FRAME
			MATERIAL fmat

				IF ab<360 THEN
						ADDz ds
						PRISM_ 10,hr,
							0,fth/2,15,
							0,-fth/2+0.00005,15,
							0,-fth/2,79,
							1,0,800,
							(rb+(fth/2))*sin(ab),rb-(rb+(fth/2))*cos(ab),1015,
							(rb-(fth/2)+0.00005)*sin(ab),rb-(rb-(fth/2)+0.00005)*cos(ab),15,
							(rb-(fth/2))*sin(ab),rb-(rb-(fth/2))*cos(ab),79,
							-cos(ab),-sin(ab),800,
							0,fth/2,1015,
							0,fth/2,-1
						DEL 1

				ELSE	!!!ANGLE=360

					ADDz ds
					PRISM_ 12,hr,
							0,-fth/2,79,
							1,0,800,
							0,2*rb+fth/2,1079,
							-1,0,800,
							0,-fth/2,1079,
							0,-fth/2,-1,

							0,fth/2,79,
							1,0,800,
							0,2*rb-fth/2,1079,
							-1,0,800,
							0,fth/2,1079,
							0,fth/2,-1
					DEL 1

				ENDIF

		ELSE

			!FRAME
			material fmat

			if ab<360 then

				if gs_frameStyle_m = 2 then		! Rounded Frame

					startAngle = asn ((gs_frameDiameter/2) / rb)
					add 0, rb, ds + gs_frameDiameter/2
					betaincl = (ab - 2 * startAngle) / nalphaincl

					actAngle = startAngle
					put rb * sin (actAngle), -rb * cos (actAngle), hr - gs_frameDiameter, 0

					for i = 1 to nalphaincl + 1
						put rb * sin (actAngle), -rb * cos (actAngle), 0, 0
						actAngle = actAngle + betaincl
					next i
					actAngle = actAngle - betaincl

					for i = 1 to nalphaincl + 1
						put rb * sin (actAngle), -rb * cos (actAngle), hr - gs_frameDiameter, 0
						actAngle = actAngle - betaincl
					next i
					actAngle = actAngle + betaincl

					for i = 1 to 2
						put rb * sin (actAngle), -rb * cos (actAngle), 0, 0
						actAngle = actAngle + betaincl
					next i

					tube 2, nsp/4, 1+2,
						0, 0, 900,
						gs_frameDiameter/2, 360, 4000,
						get (nsp)

					del 1
				else
					resol res

					temp_rb=((rb-fth/2)*pi*ab)/180

					ADDz ds
					ADDy fth/2
					ROTx 90
					BPRISM_ fmat,fmat,fmat,
						10, fth, rb-fth/2,
						0,0,15,
						temp_rb,0,15,
						temp_rb,hr,15,
						0,hr,15,
						0,0,-1,
						fsw, fbw,15,
						temp_rb-fsw,fbw,15,
						temp_rb-fsw,hr-ftw,15,
						fsw, hr-ftw,15,
						fsw, fbw,-1
					DEL 3
				endif

					resol res

					!FILL
					material bmat
					add 0,rb,ds+fbw+fdf
					rotz alphafills-90

						prism_ 10,fillh,
							rb-bth/2,0,15,
							rb+bth/2-0.00005,0,15,
							rb+bth/2,0,79,
							0,1,800,
							(rb+bth/2)*cos(alphafillm),(rb+bth/2)*sin(alphafillm),1015,
							(rb-bth/2+0.00005)*cos(alphafillm),(rb-bth/2+0.00005)*sin(alphafillm),15,
							(rb-bth/2)*cos(alphafillm),(rb-bth/2)*sin(alphafillm),79,
							sin(alphafillm),-cos(alphafillm),800,
							rb-bth/2,0,1015,
							rb-bth/2,0,-1

					del 2

			ELSE	!!! ANGLE=360
				if gs_frameStyle_m = 2 then		! Rounded Frame

					addz ds + gs_frameDiameter/2
					alphai = -alphaincl
					for i = 1 to nalphaincl+3
						put rb*sin(alphai), rb-rb*cos(alphai), 0
						alphai = alphai + alphaincl
					next i

					tubea 2, nsp/3, 1+2,
						0, 0, 900,
						gs_frameDiameter/2, 360, 4001,
						use (nsp)

					addz hr - gs_frameDiameter

					tubea 2, nsp/3, 1+2,
						0, 0, 900,
						gs_frameDiameter/2, 360, 4001,
						get (nsp)

					del 2

				else
					resol res

					addz ds
						prism_ 12,fbw,
							0,-fth/2,79,
							1,0,800,
							0,2*rb+fth/2,1079,
							-1,0,800,
							0,-fth/2,1079,
							0,-fth/2,-1,

							0,fth/2,79,
							1,0,800,
							0,2*rb-fth/2,1079,
							-1,0,800,
							0,fth/2,1079,
							0,fth/2,-1

					addz hr-ftw
						prism_ 12,ftw,
							0,-fth/2,79,
							1,0,800,
							0,2*rb+fth/2,1079,
							-1,0,800,
							0,-fth/2,1079,
							0,-fth/2,-1,

							0,fth/2,79,
							1,0,800,
							0,2*rb-fth/2,1079,
							-1,0,800,
							0,fth/2,1079,
							0,fth/2,-1
					del 2
				endif
					resol res

					!FILL
					material bmat
					addz ds+fbw+fdf
						prism_ 12,fillh,
							0,-bth/2,79,
							1,0,800,
							0,2*rb+bth/2,1079,
							-1,0,800,
							0,-bth/2,1079,
							0,-bth/2,-1,

							0,bth/2,79,
							1,0,800,
							0,2*rb-bth/2,1079,
							-1,0,800,
							0,bth/2,1079,
							0,bth/2,-1
					del 1
			endif
		endif
	endif


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


	if ABS(ani)>EPS and ptyp_m=1 then				!ptyp=Straight
		if rom_m=1 then						!Vertical Orientation
			if rmm_m=2 then					!Perpendicular Measurement Extra Values
				ftw=ftw/cos(ani)
				fbw=fbw/cos(ani)
				fdf=fdf/cos(ani)
				fillh=fillh/cos(ani)
			endif

			IF rmm_m=1 THEN			!!Vertical Measurement
				temp_ds=ds
				temp_hr=hr
			ELSE					!!Perpendicular Measurement
				temp_ds=dsPerp
				temp_hr=hrPerp
			ENDIF


			MATERIAL fmat

			ROTz -90+gs_AngleL		!!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 2

			ADDx lra
			ROTz 90-gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3

			!!3D FEEDBACK AT HOTSPOT EDITING
			IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

					ROTx 90
					ADDz -fth/2
						!FRAME
						MATERIAL fmat
						PRISM_ 5,fth,
							-XPosL,temp_ds-YPosL,15,
							lra+XPosR,temp_ds+lra*tan(ani)+YPosR,15,
							lra+XPosR,temp_ds+temp_hr+lra*tan(ani)+YPosR,15,
							-XPosL,temp_ds+temp_hr-YPosL,15,
							-XPosL,temp_ds-YPosL,-1

					DEL 2

			ELSE	! 3D DETAILED MODEL

				MATERIAL fmat
				rotx 90
				if gs_frameStyle_m = 2 then		! Rounded Frame

					length			= lra + XPosR + XPosL - gs_frameDiameter
					bottomHeight	= temp_ds
					topHeight		= temp_ds + temp_hr
					yCorrBottom 	= (gs_frameDiameter/2) / cos(ani) + (gs_frameDiameter/2) * tan(ani)
					yCorrTop		= (gs_frameDiameter/2) / cos(ani) - (gs_frameDiameter/2) * tan(ani)

					tube 2, 7, 1+2,
						0, 0, 900,
						gs_frameDiameter/2, 360, 4000,

						-XPosL + gs_frameDiameter/2,		-YPosL + topHeight - yCorrTop,								0, 0,
						-XPosL + gs_frameDiameter/2,		-YPosL + bottomHeight + yCorrBottom,						0, 0,
						 lra + XPosR - gs_frameDiameter/2,	-YPosL + bottomHeight + yCorrBottom + length * tan(ani),	0, 0,
						 lra + XPosR - gs_frameDiameter/2,	-YPosL + topHeight - yCorrTop + length * tan(ani),			0, 0,
						-XPosL + gs_frameDiameter/2,		-YPosL + topHeight - yCorrTop,								0, 0,
						-XPosL + gs_frameDiameter/2,		-YPosL + bottomHeight + yCorrBottom,						0, 0,
						 lra + XPosR - gs_frameDiameter/2,	-YPosL + bottomHeight + yCorrBottom + length * tan(ani),	0, 0

				else
				addz -fth/2
				!FRAME
				PRISM_ 10,fth,
						-XPosL,temp_ds-YPosL,15,
						lra+XPosR,temp_ds+lra*tan(ani)+YPosR,15,
						lra+XPosR,temp_ds+temp_hr+lra*tan(ani)+YPosR,15,
						-XPosL,temp_ds+temp_hr-YPosL,15,
						-XPosL,temp_ds-YPosL,-1,

						fsw,temp_ds+fbw+fsw*tan(ani),15,
						lra-fsw,temp_ds+fbw+(lra-fsw)*tan(ani),15,
						lra-fsw,temp_ds+temp_hr-ftw+(lra-fsw)*tan(ani),15,
						fsw,temp_ds+temp_hr-ftw+fsw*tan(ani),15,
						fsw,temp_ds+fbw+fsw*tan(ani),-1
				BODY -1
					del 1
				endif

				del 1

			CUTEND
			CUTEND


			MATERIAL bmat

			ROTz -90+gs_AngleL		!!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 2

			ADDx lra
			ROTz 90-gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3


				!!3D FEEDBACK AT HOTSPOT EDITING
				IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				ELSE	!!! 3D DETAILED MODEL

					rotx 90
					addz -fth/2

						!FILL
						material bmat
						add fsw+fdf,temp_ds+fbw+fdf+(fsw+fdf)*tan(ani),(fth-bth)/2
							prism_ 5,bth,
							0,0,15,
							fillw,fillw*tan(ani),15,
							fillw,fillw*tan(ani)+fillh,15,
							0,fillh,15,
							0,0,-1
						del 1
					del 2
				ENDIF
			endif

			CUTEND
			CUTEND

	ELSE						!Perpendicular Orientation
								!Measurement is always perpendicular to inclination in this case
		roty -ani

			!!3D FEEDBACK AT HOTSPOT EDITING
			IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				ROTx 90
				ADDz -fth/2
					!!FRAME
					MATERIAL fmat
					PRISM_ 5,fth,
						0,ds,15,
						lrah,ds,15,
						lrah,ds+hr,15,
						0,ds+hr,15,
						0,ds,-1
				DEL 2

			ELSE		!! 3D DETAILED MODEL

				rotx 90

					material fmat
				if gs_frameStyle_m = 2 then		! Rounded Frame
					tube 2, 7, 1+2,
						0, 0, 900,
						gs_frameDiameter/2, 360, 4000,

						gs_frameDiameter/2,			ds + hr - gs_frameDiameter/2,	0, 0,
						gs_frameDiameter/2,			ds + gs_frameDiameter/2,		0, 0,
						lrah - gs_frameDiameter/2,	ds + gs_frameDiameter/2,		0, 0,
						lrah - gs_frameDiameter/2,	ds + hr - gs_frameDiameter/2,	0, 0,
						gs_frameDiameter/2,			ds + hr - gs_frameDiameter/2,	0, 0,
						gs_frameDiameter/2,			ds + gs_frameDiameter/2,		0, 0,
						lrah - gs_frameDiameter/2,	ds + gs_frameDiameter/2,		0, 0

					addz -fth/2
				else
				addz -fth/2
					prism_ 10,fth,
						0,ds,15,
						lrah,ds,15,
						lrah,ds+hr,15,
						0,ds+hr,15,
						0,ds,-1,

						fsw,ds+fbw,15,
						lrah-fsw,ds+fbw,15,
						lrah-fsw,ds+hr-ftw,15,
						fsw,ds+hr-ftw,15,
						fsw,ds+fbw,-1
					BODY -1

				endif

					!FILL
					material bmat
					add fsw+fdf,ds+fbw+fdf,(fth-bth)/2
					block fillw,fillh,bth
				del 3

			endif
		del 1
	endif
endif


!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!


if ABS(ani)>EPS and ptyp_m<>1 then		!ptyp=Straight

		!FRAME
		material fmat

		!!3D FEEDBACK AT HOTSPOT EDITING
		IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

			!FRAME
			alphai=-alphaincl
			deltazi=-deltazincl
			for i=1 to nalphaincl+3 step 1
			put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
			alphai=alphai+alphaincl
			deltazi=deltazi+deltazincl
			NEXT i

			TUBEA 5,nalphaincl+3,1+2+16+32,
				-fth/2,0,0,
				fth/2,0,0,
				fth/2,hr,0,
				-fth/2,hr,0,
				-fth/2,0,-1,
				get (nsp)

		ELSE
			if gs_frameStyle_m = 2 then		! Rounded Frame

				startAngle = asn ((gs_frameDiameter/2) / rb)
				betaincl = (ab - 2 * startAngle) / nalphaincl
				startZ = (gs_frameDiameter/2) * sin(ani)
				dZ = betaincl / ab * deltaz		!!! / nalphaincl

				add 0, rb, ds + gs_frameDiameter/2

				actAngle = startAngle
				actZ = startZ
				put rb * sin (actAngle), -rb * cos (actAngle), actZ + hr - gs_frameDiameter, 0

				for i = 1 to nalphaincl + 1
					put rb * sin (actAngle), -rb * cos (actAngle), actZ, 0
					actAngle = actAngle + betaincl
					actZ = actZ + dZ
				next i
				actAngle = actAngle - betaincl
				actZ = actZ - dZ

				for i = 1 to nalphaincl + 1
					put rb * sin (actAngle), -rb * cos (actAngle), actZ + hr - gs_frameDiameter, 0
					actAngle = actAngle - betaincl
					actZ = actZ - dZ
				next i
				actAngle = actAngle + betaincl
				actZ = actZ + dZ

				for i = 1 to 2
					put rb * sin (actAngle), -rb * cos (actAngle), actZ, 0
					actAngle = actAngle + betaincl
					actZ = actZ + dZ
				next i

				tube 2, nsp/4, 1+2,
					0, 0, 900,
					gs_frameDiameter/2, 360, 4000,
					get (nsp)

				del 1
			else
			alphai=-alphaincl
				deltazi=-deltazincl
			for i=1 to nalphaincl+3 step 1
					put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
			alphai=alphai+alphaincl
					deltazi=deltazi+deltazincl
			next i

			group "ciframe"
					tubea 5,nalphaincl+3,1+2+16+32,
				-fth/2,0,0,
				fth/2,0,0,
				fth/2,hr,0,
				-fth/2,hr,0,
				-fth/2,0,-1,
				get (nsp)
			endgroup

			alphai=m*alphaincl
			DELtazi=m*DELtazincl
			for i=1 to (nalphaincl-2*m) step 1
			put rb*sin(alphai),rb-rb*cos(alphai),ds+DELtazi
			alphai=alphai+alphaincl
			DELtazi=DELtazi+DELtazincl
			next i

			group "cifill"
					tubea 5,nalphaincl-2*m+4,1+2+16+32,
				-2*fth,fbw,0,
				 fth,fbw,0,
				 fth,hr-ftw,0,
				-2*fth,hr-ftw,0,
				-2*fth,fbw,-1,

				rb*sin((m-1)*alphaincl),rb-rb*cos((m-1)*alphaincl),ds+(m-1)*DELtazincl,
				rb*sin(alphafsw),rb-rb*cos(alphafsw),ds+fsw*sin(ani),
				get (nsp),
				rb*sin(ab-alphafsw),rb-rb*cos(ab-alphafsw),ds+(lra-fsw)*sin(ani),
				rb*sin(ab-(m-1)*alphaincl),rb-rb*cos(ab-(m-1)*alphaincl),ds+DELtaz-(m-1)*DELtazincl
			endgroup

			placegroup subgroup("ciframe","cifill")

			killgroup "ciframe"
			killgroup "cifill"
			endif


			!FILL
			material bmat

			alphai=mm*alphaincl
			deltazi=mm*deltazincl
			for i=1 to (nalphaincl-2*mm) step 1
			put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
			alphai=alphai+alphaincl
			deltazi=deltazi+deltazincl
			next i

			TUBEA 5,nalphaincl-2*mm+4,1+2+16+32,
				-bth/2,fbw+fdf,0,
				 bth/2,fbw+fdf,0,
				 bth/2,hr-ftw-fdf,0,
				-bth/2,hr-ftw-fdf,0,
				-bth/2,fbw+fdf,-1,

			rb*sin((mm-1)*alphaincl),rb-rb*cos((mm-1)*alphaincl),ds+(mm-1)*deltazincl,
			rb*sin(alphafills),rb-rb*cos(alphafills),ds+(fsw+fdf)*sin(ani),
			get (nsp),
			rb*sin(ab-alphafills),rb-rb*cos(ab-alphafills),ds+(lra-fsw-fdf)*sin(ani),
			rb*sin(ab-(mm-1)*alphaincl),rb-rb*cos(ab-(mm-1)*alphaincl),ds+deltaz-(mm-1)*deltazincl

		endif
	endif
endif


!!DETAILED 3D END!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! ** HOTSPOTS ** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF cfs=0 THEN
			IF ABS(ani)<EPS and ptyp_m=1 THEN

					!!!LENGHT
					HOTSPOT 0,0,0,unID,lra,1+256 : unID=unID+1
					HOTSPOT lra,0,0,unID,lra,2 : unID=unID+1
					HOTSPOT -1,0,0,unID,lra,3 : unID=unID+1

					HOTSPOT 0,0,ds,unID,lra,1+256 : unID=unID+1
					HOTSPOT lra,0,ds,unID,lra,2 : unID=unID+1
					HOTSPOT -1,0,ds,unID,lra,3 : unID=unID+1

					HOTSPOT 0,0,ds+hr,unID,lra,1+256 : unID=unID+1
					HOTSPOT lra,0,ds+hr,unID,lra,2 : unID=unID+1
					HOTSPOT -1,0,ds+hr,unID,lra,3 : unID=unID+1

					!!!DISTANCE FROM FLOOR
					HOTSPOT lra/2,0,0,unID,ds,1+128 : unID=unID+1
					HOTSPOT lra/2,0,ds,unID,ds,2 : unID=unID+1
					HOTSPOT lra/2,0,-1,unID,ds,3 : unID=unID+1

					!!!HEIGHT OF RAILING
					HOTSPOT lra/2,0,ds,unID,hr,1 : unID=unID+1
					HOTSPOT lra/2,0,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT lra/2,0,-1,unID,hr,3 : unID=unID+1


		IF gs_detlevel_3D_m = 2 and gs_frameStyle_m < 2 THEN

						!!!Frame Width Bottom
						HOTSPOT lra/2,0,ds,unID,fbw,1+128 : unID=unID+1
						HOTSPOT lra/2,0,ds+fbw,unID,fbw,2 : unID=unID+1
						HOTSPOT lra/2,0,ds-1,unID,fbw,3 : unID=unID+1

						!!!Frame Width Top
						HOTSPOT lra/2,0,ds+hr,unID,ftw,1+128 : unID=unID+1
						HOTSPOT lra/2,0,ds+hr-ftw,unID,ftw,2 : unID=unID+1
						HOTSPOT lra/2,0,ds+hr+1,unID,ftw,3 : unID=unID+1

						!!!Frame Width Side
						HOTSPOT 0,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,1+128 : unID=unID+1
						HOTSPOT fsw,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,2 : unID=unID+1
						HOTSPOT -1,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,3 : unID=unID+1

						HOTSPOT lra,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,1+128 : unID=unID+1
						HOTSPOT lra-fsw,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,2 : unID=unID+1
						HOTSPOT lra+1,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,3 : unID=unID+1

					ENDIF
			ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)<EPS and ptyp_m<>1 THEN
			IF ab<360 THEN

				!!!HEIGHT OF RAILING
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,hr,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

				!!! DISTANCE FROM FLOOR
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),0,unID,ds,1 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,ds,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

				IF ptyp_m=3 THEN
				!!!ROTATE
					HOTSPOT 0,rb,ds,unID,ab,6 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ab,4+256 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ab,5 : unID=unID+1

					HOTSPOT 0,rb,ds+hr,unID,ab,6 : unID=unID+1
					HOTSPOT 0,0,ds+hr,unID,ab,4+256 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr,unID,ab,5 : unID=unID+1

				!!!FIX HOTSPOT AT FLOOR
					HOTSPOT 0,0,0,unID : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID : unID=unID+1
				ENDIF


				IF ptyp_m=2 THEN
				!!!HEIGHT OF RAILING
					HOTSPOT 0,0,ds,unID,hr,1+128 : unID=unID+1
					HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,hr,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

				!!! DISTANCE FROM FLOOR
					HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

					HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID,ds,1 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ds,2 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
				ENDIF

			IF gs_detlevel_3D_m = 2 and gs_frameStyle_m < 2 THEN
					!!!Frame Width Bottom
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,fbw,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+fbw,unID,fbw,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds-1,unID,fbw,3 : unID=unID+1

					!!!Frame Width Top
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr,unID,ftw,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr-ftw,unID,ftw,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr+1,unID,ftw,3 : unID=unID+1
				ENDIF

			ELSE	!!!ANGLE=360

				!!!HOTSPOTS FOR GRAPHICAL EDITING
				!!!HEIGHT OF RAILING
					HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
					HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

					HOTSPOT rb,rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT rb,rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT rb,rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT 0,2*rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT 0,2*rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,2*rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT -rb,rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT -rb,rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT -rb,rb,-1,unID,hr,3 : unID=unID+1

				!!! DISTANCE FROM FLOOR
					HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

					HOTSPOT rb,rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT rb,rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT rb,rb,-1,unID,ds,3 : unID=unID+1

					HOTSPOT 0,2*rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,2*rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,2*rb,-1,unID,ds,3 : unID=unID+1

					HOTSPOT -rb,rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT -rb,rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT -rb,rb,-1,unID,ds,3 : unID=unID+1

			ENDIF
		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m=1 THEN	!ptyp=Straight
			IF rom_m=1 THEN					!Vertical Orientation

					IF rmm_m=1 THEN			!Vertical Measurement
						temp_ds=ds
						temp_hr=hr
						temp_fbw=fbw
						temp_ftw=ftw
					ELSE					!Perpendicular Measurement
						temp_ds=dsPerp
						temp_hr=hrPerp
						temp_fbw=fbwPerp
						temp_ftw=ftwPerp
					ENDIF


					!!!	HOTSPOTS FOR GRAPHICAL EDITING
					!!! LENGTH OF RAILING

						HOTSPOT 0,	0,	0,				unID, lrah, 1+256 : unID=unID+1
						HOTSPOT -1, 0,	0-tan(ani),		unID, lrah, 3 : unID=unID+1
						HOTSPOT lra,0, 	0+lra*tan(ani),	unID, lrah, 2 : unID=unID+1

						HOTSPOT 0,	0,	temp_ds,				unID, lrah, 1+256 : unID=unID+1
						HOTSPOT -1, 0,	temp_ds-tan(ani),		unID, lrah, 3 : unID=unID+1
						HOTSPOT lra,0, 	temp_ds+lra*tan(ani),	unID, lrah, 2 : unID=unID+1

						HOTSPOT 0,	0,	temp_ds+temp_hr,				unID, lrah, 1+256 : unID=unID+1
						HOTSPOT -1, 0,	temp_ds+temp_hr-tan(ani),		unID, lrah, 3 : unID=unID+1
						HOTSPOT lra,0, 	temp_ds+temp_hr+lra*tan(ani),	unID, lrah, 2 : unID=unID+1


					IF rmm_m=1 THEN
						!!! DISTANCE FROM FLOOR
						HOTSPOT lra/2,	0,	lra/2*tan(ani),	unID, ds, 1+128 : unID=unID+1
						HOTSPOT lra/2,	0,	lra/2*tan(ani)-1,	unID, ds, 3 : unID=unID+1
						HOTSPOT lra/2,	0, 	lra/2*tan(ani)+temp_ds,	unID, ds, 2 : unID=unID+1

						!!! HEIGHT of RAILING
						HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani),	unID, hr, 1+128 : unID=unID+1
						HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani)-1,	unID, hr, 3 : unID=unID+1
						HOTSPOT lra/2,	0, 	temp_ds+lra/2*tan(ani)+temp_hr,unID, hr, 2 : unID=unID+1

				IF gs_detlevel_3D_m = 2 and gs_frameStyle_m < 2 THEN
							!!!Frame Width Bottom
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds,		unID, fbw, 1+128 : unID=unID+1
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds-1,	unID, fbw, 3 : unID=unID+1
							HOTSPOT lra/2,	0, 	lra/2*tan(ani)+temp_ds+temp_fbw,unID, fbw, 2 : unID=unID+1

							!!!Frame Width Top
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds+temp_hr,		unID, ftw, 1+128 : unID=unID+1
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds+temp_hr+1,	unID, ftw, 3 : unID=unID+1
							HOTSPOT lra/2,	0, 	lra/2*tan(ani)+temp_ds+temp_hr-temp_ftw,unID, ftw, 2 : unID=unID+1
						ENDIF

					ELSE
						!!! DISTANCE FROM FLOOR
						HOTSPOT lra/2,	0,	lra/2*tan(ani),	unID, dsPerp, 1+128, ds : unID=unID+1
						HOTSPOT lra/2,	0,	lra/2*tan(ani)-1,	unID, dsPerp, 3, ds : unID=unID+1
						HOTSPOT lra/2,	0, 	lra/2*tan(ani)+temp_ds,	unID, dsPerp, 2, ds : unID=unID+1

						!!! HEIGHT of RAILING
						HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani),	unID, hrPerp, 1+128, hr : unID=unID+1
						HOTSPOT lra/2,	0,	temp_ds+lra/2*tan(ani)-1,	unID, hrPerp, 3, hr  : unID=unID+1
						HOTSPOT lra/2,	0, 	temp_ds+lra/2*tan(ani)+temp_hr,unID, hrPerp, 2, hr  : unID=unID+1

				IF gs_detlevel_3D_m = 2 and gs_frameStyle_m < 2 THEN
							!!!Frame Width Bottom
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds,		unID, fbwPerp, 1+128, fbw : unID=unID+1
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds-1,	unID, fbwPerp, 3, fbw : unID=unID+1
							HOTSPOT lra/2,	0, 	lra/2*tan(ani)+temp_ds+temp_fbw,unID, fbwPerp, 2, fbw : unID=unID+1

							!!!Frame Width Top
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds+temp_hr,		unID, ftwPerp, 1+128, ftw : unID=unID+1
							HOTSPOT lra/2,	0,	lra/2*tan(ani)+temp_ds+temp_hr+1,	unID, ftwPerp, 3, ftw : unID=unID+1
							HOTSPOT lra/2,	0, 	lra/2*tan(ani)+temp_ds+temp_hr-temp_ftw,unID, ftwPerp, 2, ftw : unID=unID+1
						ENDIF
					ENDIF

			IF gs_detlevel_3D_m = 2 and gs_frameStyle_m < 2 THEN
							!!!Frame Width Side
							HOTSPOT 0,	0,	temp_ds+temp_fbw+(temp_hr-temp_ftw-temp_fbw)/2,				unID, fswPerp, 1+128, fsw : unID=unID+1
							HOTSPOT -1, 0,	temp_ds+temp_fbw+(temp_hr-temp_ftw-temp_fbw)/2-tan(ani),		unID, fswPerp, 3, fsw : unID=unID+1
							HOTSPOT fsw,0, 	temp_ds+temp_fbw+(temp_hr-temp_ftw-temp_fbw)/2+fsw*tan(ani),	unID, fswPerp, 2, fsw : unID=unID+1

							HOTSPOT lra,	0,	temp_ds+temp_fbw+(temp_hr-temp_ftw-temp_fbw)/2+lra*tan(ani),				unID, fswPerp, 1+128, fsw : unID=unID+1
							HOTSPOT lra+1, 0,	temp_ds+temp_fbw+(temp_hr-temp_ftw-temp_fbw)/2+lra*tan(ani)+tan(ani),		unID, fswPerp, 3, fsw : unID=unID+1
							HOTSPOT lra-fsw,0, 	temp_ds+temp_fbw+(temp_hr-temp_ftw-temp_fbw)/2+lra*tan(ani)-fsw*tan(ani),	unID, fswPerp, 2, fsw : unID=unID+1
					ENDIF

			ELSE			!Perpendicular Orientation
							!Measurement is always perpendicular to inclination in this case
				ROTy -ani

						!!! LENGTH OF RAILING
						HOTSPOT 0,0,0,unID,lrah,1+256 : unID=unID+1
						HOTSPOT lrah,0,0,unID,lrah,2 	: unID=unID+1
						HOTSPOT -1,0,0,unID,lrah,3 	: unID=unID+1

						HOTSPOT 0,0,ds,unID,lrah,1+256 : unID=unID+1
						HOTSPOT lrah,0,ds,unID,lrah,2 	: unID=unID+1
						HOTSPOT -1,0,ds,unID,lrah,3 	: unID=unID+1

						HOTSPOT 0,0,ds+hr,unID,lrah,1+256 : unID=unID+1
						HOTSPOT lrah,0,ds+hr,unID,lrah,2 	: unID=unID+1
						HOTSPOT -1,0,ds+hr,unID,lrah,3 	: unID=unID+1

						!!!DISTANCE FROM FLOOR
						HOTSPOT lrah/2,0,0,unID,ds,1+128 : unID=unID+1
						HOTSPOT lrah/2,0,ds,unID,ds,2 : unID=unID+1
						HOTSPOT lrah/2,0,-1,unID,ds,3 : unID=unID+1

						!!! HEIGHT of RAILING
						HOTSPOT lrah/2,0,ds,unID,hr,1 : unID=unID+1
						HOTSPOT lrah/2,0,ds+hr,unID,hr,2 : unID=unID+1
						HOTSPOT lrah/2,0,-1,unID,hr,3 : unID=unID+1

			IF gs_detlevel_3D_m = 2 and gs_frameStyle_m < 2 THEN
							!!!Frame Width Bottom
							HOTSPOT lrah/2,0,ds,unID,fbw,1+128 : unID=unID+1
							HOTSPOT lrah/2,0,ds+fbw,unID,fbw,2 : unID=unID+1
							HOTSPOT lrah/2,0,ds-1,unID,fbw,3 : unID=unID+1

							!!!Frame Width Top
							HOTSPOT lrah/2,0,ds+hr,unID,ftw,1+128 : unID=unID+1
							HOTSPOT lrah/2,0,ds+hr-ftw,unID,ftw,2 : unID=unID+1
							HOTSPOT lrah/2,0,ds+hr+1,unID,ftw,3 : unID=unID+1

							!!!Frame Width Side
							HOTSPOT 0,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,1+128 : unID=unID+1
							HOTSPOT fsw,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,2 : unID=unID+1
							HOTSPOT -1,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,3 : unID=unID+1

							HOTSPOT lrah,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,1+128 : unID=unID+1
							HOTSPOT lrah-fsw,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,2 : unID=unID+1
							HOTSPOT lrah+1,0,ds+fbw+(hr-ftw-fbw)/2,unID,fsw,3 : unID=unID+1
						ENDIF

				DEL 1
			ENDIF
		ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m<>1 THEN
				!!!HOTSPOTS FOR GRAPHICAL EDITING
				IF ptyp_m=3 THEN
					!!!ROTATE
					HOTSPOT 0,rb,ds+lra*sin(ani),unID,ab,6 : unID=unID+1
					HOTSPOT 0,0,ds+lra*sin(ani),unID,ab,4+128 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ab,5 : unID=unID+1

					HOTSPOT 0,rb,ds+hr+lra*sin(ani),unID,ab,6 : unID=unID+1
					HOTSPOT 0,0,ds+hr+lra*sin(ani),unID,ab,4+128 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr+lra*sin(ani),unID,ab,5 : unID=unID+1
				ENDIF

					!!!HEIGHT OF RAILING
					HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
					HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,hr,1 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr+(lra/2)*sin(ani),unID,hr,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,hr,3 : unID=unID+1

					!!!DISTANCE FROM FLOOR
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani),unID,ds,1 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,ds,2 : unID=unID+1
					HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

					HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

					!!!FIX HOTSPOT AT FLOOR
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),0+lra*sin(ani), unID : unID=unID+1

				IF ptyp_m=2 THEN
					!!!HEIGHT OF RAILING
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,hr,1 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr+lra*sin(ani),unID,hr,2 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

					!!!DISTANCE FROM FLOOR
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani),unID,ds,1+128 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ds,2 : unID=unID+1
					HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
				ENDIF
		ENDIF
	ENDIF

end
