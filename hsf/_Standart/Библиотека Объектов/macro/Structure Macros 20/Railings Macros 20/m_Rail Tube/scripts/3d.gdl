
!Rail Post with Balusters

if gs_detlevel_3D_m=0 then end

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then 		!! for Simple Model and in 3D Windoow
gs_detlevel_3D_m=1
endif

IF gs_shadow=0 THEN SHADOW OFF


if ABS(lra)<EPS then end	!!lra=0
pen gs_cont_pen
material rmat


!!SIMPLE 3D!!

If gs_detlevel_3D_m=1 then
	rescs=5
	res=20
endif



!!Handrail!!

if sb=0 then 			! without subboard
	resol rescs
	
	group "handrail body"
	tube 6,6,0,
	 hrd/2,0,1,
	 0,1,800,
	-hrd/2,0,1001,
	 0,-1,800,
	 hrd/2,0,1001,
	 hrd/2,0,-1,
	
	-hro-1,0,hr-hrd/2,0,
	-hro+hrd/2,0,hr-hrd/2,0,
	 0,0,hr-hrd/2,0,
	 lra,0,tvd+hr-hrd/2,0,
	 lra+hro-hrd/2,0,tvd+hr-hrd/2,0,
	 lra+hro+1,0,tvd+hr-hrd/2,0
	endgroup
	
	placegroup "handrail body"
	
	add -hro+hrd/2,0,hr-hrd/2
	mulx -1
	revolve 5,360,0,
	hrd/2,0,1,
	0,1,800,
	0,hrd/2,1001,
	0,hrd/2,1,
	-hrd/2,hrd/4,1
	del 2
	add lra+hro-hrd/2,0,tvd+hr-hrd/2
	revolve 5,360,0,
	hrd/2,0,1,
	0,1,800,
	0,hrd/2,1001,
	0,hrd/2,1,
	-hrd/2,hrd/4,1
	del 1
else					!With subboard
	resol rescs
	
	alpha=90
	for i=1 to nresstep step 1
	put -hro+sbh/2+(rcurve)*cos(alpha),0,hr-sbh/2+(rcurve)*sin(alpha),0
	alpha=alpha+resstep
	next i
	
	alpha=270
	for i=1 to nresstep step 1
	put lra+hro-sbh/2+(rcurve)*cos(alpha),0,tvd+hr-sbh/2+(rcurve)*sin(alpha),0
	alpha=alpha+resstep
	next i
	
	alpha=90+resstep
	
	group "handrail body subboard"
	tube 6,11+nsp/4,0,
	 hrd/2,0,1,
	 0,1,800,
	-hrd/2,0,1001,
	 0,-1,800,
	 hrd/2,0,1001,
	 hrd/2,0,-1,
	
	 0,0,hr-hrd/2,0,
	-hro+sbh/2,0,hr-hrd/2,0,
	 get (nsp/2),
	-hro+sbh/2,0,hr-sbh+hrd/2,0,
	 0,0,hr-sbh+hrd/2,0,
	 lra,0,tvd+hr-sbh+hrd/2,0,
	 lra+hro-sbh/2,0,tvd+hr-sbh+hrd/2,0,
	 get (nsp),
	 lra+hro-sbh/2,0,tvd+hr-hrd/2,0,
	 lra,0,tvd+hr-hrd/2,0,
	 0,0,hr-hrd/2,0,
	-hro+sbh/2,0,hr-hrd/2,0,
	-hro+sbh/2+(rcurve)*cos(alpha),0,hr-sbh/2+(rcurve)*sin(alpha),0
	endgroup
	
	placegroup "handrail body subboard"
endif


if bSupport then
	!!Posts!!
	if iSupportType = SUPPORT_POST then
		resol rescs
		
		group "posts"
		for i=1 to np step 1
		cylind hr-hrd/2,hrd/2
		add hstep,0,vstep
		next i
		del np
		endgroup
	
	
		if sb=0 then
			placegroup subgroup ("posts","handrail body")
		else
			placegroup subgroup ("posts","handrail body subboard")
		endif
	endif

	!!Wall Brackets!!
	if iSupportType = SUPPORT_WALLBRACKET then
		if sb=0 then
			group "wab1"
			add -0.015,0,hr-hrd/2-0.015
			for i=1 to np step 1
			block 0.03,hrd/2+wabh,0.03
			add hstep,0,vstep
			next i
			del np+1
			endgroup
			
			placegroup subgroup ("wab1","handrail body")
		else
			
			group "wab2"
			add -0.015,0,hr-hrd/2-0.015
			for i=1 to np step 1
			block 0.03,hrd/2+wabh,0.03
			addz -sbh+hrd
			block 0.03,hrd/2+wabh,0.03
			del 1
			add hstep,0,vstep
			next i
			del np+1
			endgroup
			
			placegroup subgroup ("wab2","handrail body subboard")		
		endif
	endif
endif

!!END of MODEL!!


!!3D HOTSPOTS!!

hotspot 0,0,0,1,hr,1
hotspot 0,0,hr,2,hr,2
hotspot 0,0,-1,3,hr,3

hotspot lra,0,tvd,4,hr,1+128
hotspot lra,0,tvd+hr,5,hr,2
hotspot lra,0,-1,6,hr,3

if sb=1 then
	hotspot 0,0,hr,7,sbh,1+128
	hotspot 0,0,hr-sbh,8,sbh,2
	hotspot 0,0,hr+1,9,sbh,3

	hotspot lra,0,tvd+hr,10,sbh,1+128
	hotspot lra,0,tvd+hr-sbh,11,sbh,2
	hotspot lra,0,tvd+hr+1,12,sbh,3

	hotspot 0,0,hr-sbh/2,19,hro,1+128
	hotspot -hro,0,hr-sbh/2,20,hro,2
	hotspot 1,0,hr-sbh/2,21,hro,3

	hotspot lra,0,tvd+hr-sbh/2,22,hro,1+128
	hotspot lra+hro,0,tvd+hr-sbh/2,23,hro,2
	hotspot lra-1,0,tvd+hr-sbh/2,24,hro,3
else
	hotspot 0,0,hr-hrd/2,16,hro,1+128
	hotspot -hro,0,hr-hrd/2,17,hro,2
	hotspot 1,0,hr-hrd/2,18,hro,3

	hotspot lra,0,tvd+hr-hrd/2,25,hro,1+128
	hotspot lra+hro,0,tvd+hr-hrd/2,26,hro,2
	hotspot lra-1,0,tvd+hr-hrd/2,27,hro,3
endif

hotspot lra,0,0,13,tvd,1+128
hotspot lra,0,tvd,14,tvd,2
hotspot lra,0,-1,15,tvd,3


end
