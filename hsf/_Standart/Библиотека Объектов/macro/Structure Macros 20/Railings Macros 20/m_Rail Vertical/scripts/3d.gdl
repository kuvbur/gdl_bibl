
! ==============================================================================
! m_Rail Vertical
! ==============================================================================


IF gs_detlevel_3D_m=0 THEN end
IF gs_shadow=0 THEN SHADOW OFF

templra = max(templra, EPS)
PEN gs_cont_pen
unID=1


IF hrt_m=1 THEN		!!! HANDRAIL WIDTH
	fth=pth
	HandrailWidth=hrw
ELSE
	fth=pd
	HandrailWidth=hrd
ENDIF

pmat = fmat
bmat = fmat
gs_railMat = fmat


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** SIMPLE 3D *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!!!CALCULATION FOR CONNECTING ANGLE

XPosL0=(HandrailWidth/2)/tan(gs_AngleL)
XPosR0=(HandrailWidth/2)/tan(gs_AngleR)

XPosL=ABS((HandrailWidth/2)/tan(gs_AngleL))
XPosR=ABS((HandrailWidth/2)/tan(gs_AngleR))
YPosL=(XPosL*tan(ani))
YPosR=(XPosR*tan(ani))


IF gs_detlevel_3D_m=1 THEN		!!Simple

	MATERIAL pmat
	RESOL res


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)<EPS and ptyp_m=1 THEN		!!ptyp=Straight

			ADDx XStartPoslra
			ADDz ds
				PRISM_ 5,hr,
					XPosL0,fth/2,15,
					-XPosL0,-fth/2,15,
					templra+XPosR0,-fth/2,15,
					templra-XPosR0,fth/2,15,
					XPosL0,fth/2,-1
			DEL 2

		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)<EPS and ptyp_m<>1 THEN

		!!!!FRAME

			IF ab<360 THEN
					ADDz ds
					PRISM_ 10,hr,
						0,fth/2,15,
						0,-fth/2+0.00005,15,
						0,-fth/2,79,
						1,0,800,
						(rb+(fth/2))*sin(ab),rb-(rb+(fth/2))*cos(ab),1015,
						(rb-(fth/2)+0.00005)*sin(ab),rb-(rb-(fth/2)+0.00005)*cos(ab),15,
						(rb-(fth/2))*sin(ab),rb-(rb-(fth/2))*cos(ab),79,
						-cos(ab),-sin(ab),800,
						0,fth/2,1015,
						0,fth/2,-1
					DEL 1

			ELSE	!!!ANGLE=360

				ADDz ds
					PRISM_ 12,hr,
						0,-fth/2,79,
						1,0,800,
						0,2*rb+fth/2,1079,
						-1,0,800,
						0,-fth/2,1079,
						0,-fth/2,-1,

						0,fth/2,79,
						1,0,800,
						0,2*rb-fth/2,1079,
						-1,0,800,
						0,fth/2,1079,
						0,fth/2,-1
				DEL 1

			ENDIF
		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m=1 THEN			!!ptyp=Straight

			IF rom_m=1 THEN					!!Vertical Orientation

				ADD XStartPoslra,0,ZStartPoslra

					IF rmm_m=1 THEN			!!Vertical Measurement
						temp_ds=ds
						temp_hr=hr
					ELSE					!!Perpendicular Measurement
						temp_ds=dsPerp
						temp_hr=hrPerp
					ENDIF

						ROTz -90+gs_AngleL		!! CONNECTING ANGLE
						ROTy -90
							CUTPLANE
						DEL 2

						ADDx templra
						ROTz 90-gs_AngleR
						ROTy 90
							CUTPLANE
						DEL 3


							ROTx 90
							ADDz -fth/2

								PRISM_ 5,fth,
									-XPosL,temp_ds-YPosL,15,
									templra+XPosR,temp_ds+templra*tan(ani)+YPosR,15,
									templra+XPosR,temp_ds+temp_hr+templra*tan(ani)+YPosR,15,
									-XPosL,temp_ds+temp_hr-YPosL,15,
									-XPosL,temp_ds-YPosL,-1

							DEL 2

						CUTEND
						CUTEND
				DEL 1

			ELSE						!Perpendicular Orientation
										!Measurement is always perpendicular to inclination in this case

				ROTy -ani
				ADDx XStartPoslra

					ROTx 90
					ADDz -fth/2
						!!FRAME

						PRISM_ 5,fth,
						0,ds,15,
						templrah,ds,15,
						templrah,ds+hr,15,
						0,ds+hr,15,
						0,ds,-1
					DEL 2

				DEL 2

			ENDIF


		ENDIF


	!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m<>1 THEN

			!!!!FRAME

			alphai=-alphaincl
			deltazi=-deltazincl
			for i=1 to nalphaincl+3 step 1
			put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
			alphai=alphai+alphaincl
			deltazi=deltazi+deltazincl
			NEXT i

			TUBEA 5,nalphaincl+3,1+2+16+32,
				-fth/2,0,0,
				fth/2,0,0,
				fth/2,hr,0,
				-fth/2,hr,0,
				-fth/2,0,-1,
				get (nsp)

		ENDIF


ENDIF		!!END of SIMPLE 3D!!



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** DETAILED 3D *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


IF gs_detlevel_3D_m=2 THEN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

IF ABS(ani)<EPS and ptyp_m=1 THEN

	ADDX XStartPoslra

		!!3D FEEDBACK AT HOTSPOT EDITING
		IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				ADDz ds
				MATERIAL fmat
				PRISM_ 5,hr,
					(fth/2)/tan(gs_AngleL),fth/2,15,
					-(fth/2)/tan(gs_AngleL),-fth/2,15,
					templra+(fth/2)/tan(gs_AngleR),-fth/2,15,
					templra-(fth/2)/tan(gs_AngleR),fth/2,15,
					(fth/2)/tan(gs_AngleL),fth/2,-1
				DEL 1

		ELSE	!!! DETAILED MODEL

			!! *** HANDRAIL *** !! *****************************************************

			MATERIAL fmat

			ADDx -(hroL)
			ROTz -90 + gs_AngleL		!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 3

			ADDx templra+(hroR)
			ROTz 90 - gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3

			RESOL rescs

			MATERIAL fmat

			ADDz ds+hr-hrh
			PRISM_ 5,hrh,
				-(hroL)-XPosL,hrw/2,15,
				-(hroL)-XPosL,-hrw/2,15,
				templra+(hroR)+XPosR,-hrw/2,15,
				templra+(hroR)+XPosR,hrw/2,15,
				-(hroL)-XPosL,hrw/2,-1
			DEL 1

			ADDz ds+hr-hrh - gs_railDist - gs_railHeight
			PRISM_ 5, gs_railHeight,
				-(hroL)-XPosL, gs_railWidth/2,15,
				-(hroL)-XPosL, -gs_railWidth/2,15,
				templra+(hroR)+XPosR, -gs_railWidth/2,15,
				templra+(hroR)+XPosR, gs_railWidth/2,15,
				-(hroL)-XPosL, gs_railWidth/2,-1
			DEL 1

			ADDz ds
			PRISM_ 5, lcbh,
				-(hroL)-XPosL, lcbw/2,15,
				-(hroL)-XPosL, -lcbw/2,15,
				templra+(hroR)+XPosR, -lcbw/2,15,
				templra+(hroR)+XPosR, lcbw/2,15,
				-(hroL)-XPosL, lcbw/2,-1
			DEL 1

			GROUP "post cutting straight"
				ADDz ds
				PRISM_ 5, hr - hrh - gs_railDist,
					-(hroL)-XPosL, pth,15,
					-(hroL)-XPosL, -pth,15,
					templra+(hroR)+XPosR, -pth,15,
					templra+(hroR)+XPosR, pth,15,
					-(hroL)-XPosL, pth,-1
				DEL 1
			ENDGROUP

			CUTEND
			CUTEND

			!! *** POST *** !! *****************************************************

			MATERIAL pmat

			ADDx -(hroL)-LengthSize*dpost
			ROTz -90 + gs_AngleL		!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 3

			ADDx templra+(hroR)+LengthSize*dpost
			ROTz 90 - gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3

			MATERIAL pmat

			ph = hr + ds - hrh + gs_PostBaseOverhang

			GROUP "post rect straight"
				addz -gs_PostBaseOverhang
				for i=1 to np step 1
				PRISM_ 5,ph,
					-pw/2,-pth/2,15,
					 pw/2,-pth/2,15,
					 pw/2,pth/2,15,
					-pw/2,pth/2,15,
					-pw/2,-pth/2,-1
				ADDx distpost
				NEXT i
				DEL np+1
			ENDGROUP

			PLACEGROUP subgroup ("post rect straight", "post cutting straight")

			CUTEND
			CUTEND

			!! *** Balusters *** !! *****************************************************

			MATERIAL bmat

			ADDx -(hroL)-LengthSize*dpost
			ROTz -90 + gs_AngleL		!! CONNECTING ANGLE
			ROTy -90
				CUTPLANE
			DEL 3

			ADDx templra+(hroR)+LengthSize*dpost
			ROTz 90 - gs_AngleR
			ROTy 90
				CUTPLANE
			DEL 3

			!Vertical balusters
			MATERIAL bmat

			bh = hr
			if bt_m = 1	then	! rectangular ballusters
				bh = bh - bw
			else
				bh = bh - bd / 2
			endif
			if hrt_m = 1 then	!rectangular handrail
				bh = bh - hrh
				if gs_horRailStyle_m = 1 then bh = bh - gs_railDist - gs_railHeight
				if gs_horRailStyle_m = 2 then bh = bh - gs_railDist - gs_railDiameter/2
			else
				bh = bh - hrd/2
				if gs_horRailStyle_m = 1 then bh = bh - hrd/2 - gs_railDist - gs_railHeight
				if gs_horRailStyle_m = 2 then bh = bh - hrd/2 - gs_railDist - gs_railDiameter/2
			endif
			bh = hr - hrh - gs_railDist - gs_railHeight - lcbh

			ADD -bw/2*0, 0, ds + lcbh
			for i=1 to nb + 2
				PRISM_ 5,bh,
					-bw/2,-bth/2,15,
					 bw/2,-bth/2,15,
					 bw/2,bth/2,15,
					-bw/2,bth/2,15,
					-bw/2,-bth/2,-1
			ADDx distboard+bw
			NEXT i
			DEL nb + 2 + 1


			CUTEND
			CUTEND

			KILLGROUP "post rect straight"

		ENDIF
	DEL 1
ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

IF ABS(ani) < EPS and ptyp_m <> 1 THEN

	!!3D FEEDBACK AT HOTSPOT EDITING
	IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

		!!!!FRAME
		MATERIAL fmat

			IF ab<360 THEN

					ADDz ds
					PRISM_ 10,hr,
						0,fth/2,15,
						0,-fth/2+0.00005,15,
						0,-fth/2,79,
						1,0,800,
						(rb+(fth/2))*sin(ab),rb-(rb+(fth/2))*cos(ab),1015,
						(rb-(fth/2)+0.00005)*sin(ab),rb-(rb-(fth/2)+0.00005)*cos(ab),15,
						(rb-(fth/2))*sin(ab),rb-(rb-(fth/2))*cos(ab),79,
						-cos(ab),-sin(ab),800,
						0,fth/2,1015,
						0,fth/2,-1
					DEL 1

			ELSE	!!!ANGLE=360

				ADDz ds
					PRISM_ 12,hr,
						0,-fth/2,79,
						1,0,800,
						0,2*rb+fth/2,1079,
						-1,0,800,
						0,-fth/2,1079,
						0,-fth/2,-1,

						0,fth/2,79,
						1,0,800,
						0,2*rb-fth/2,1079,
						-1,0,800,
						0,fth/2,1079,
						0,fth/2,-1
				DEL 1

			ENDIF

	ELSE

		!!! *** handrail *** !!! *****************************************

		MATERIAL fmat
		resol res

		if (ab + alphahroL + alphahroR) < 360 then

			ADDz ds+hr-hrh
			PRISM_ 9,hrh,
			(rb-(hrw/2))*sin(-alphahroL),rb-(rb-(hrw/2))*cos(alphahroL),15,
			(rb+(hrw/2)-0.00005)*sin(-alphahroL),rb-(rb+(hrw/2)-0.000005)*cos(alphahroL),15,
			(rb+(hrw/2))*sin(-alphahroL),rb-(rb+(hrw/2))*cos(alphahroL),79,
			cos(alphahroL),sin(-alphahroL),800,
			(rb+(hrw/2))*sin(ab+alphahroR),rb-(rb+(hrw/2))*cos(ab+alphahroR),1015,
			(rb-(hrw/2)+0.00005)*sin(ab+alphahroR),rb-(rb-(hrw/2)+0.00005)*cos(ab+alphahroR),15,
			(rb-(hrw/2))*sin(ab+alphahroR),rb-(rb-(hrw/2))*cos(ab+alphahroR),79,
			-cos(ab+alphahroR),-sin(ab+alphahroR),800,
			(rb-(hrw/2))*sin(-alphahroL),rb-(rb-(hrw/2))*cos(alphahroL),1015
			DEL 1

			ADDz ds + hr - hrh - gs_railDist - gs_railHeight
			PRISM_ 9, gs_railHeight,
				(rb-(gs_railWidth/2))*sin(-alphahroL),rb-(rb-(gs_railWidth/2))*cos(alphahroL),15,
				(rb+(gs_railWidth/2)-0.00005)*sin(-alphahroL),rb-(rb+(gs_railWidth/2)-0.000005)*cos(alphahroL),15,
				(rb+(gs_railWidth/2))*sin(-alphahroL),rb-(rb+(gs_railWidth/2))*cos(alphahroL),79,
				cos(alphahroL),sin(-alphahroL),800,
				(rb+(gs_railWidth/2))*sin(ab+alphahroR),rb-(rb+(gs_railWidth/2))*cos(ab+alphahroR),1015,
				(rb-(gs_railWidth/2)+0.00005)*sin(ab+alphahroR),rb-(rb-(gs_railWidth/2)+0.00005)*cos(ab+alphahroR),15,
				(rb-(gs_railWidth/2))*sin(ab+alphahroR),rb-(rb-(gs_railWidth/2))*cos(ab+alphahroR),79,
				-cos(ab+alphahroR),-sin(ab+alphahroR),800,
				(rb-(gs_railWidth/2))*sin(-alphahroL),rb-(rb-(gs_railWidth/2))*cos(alphahroL),1015
			DEL 1

			ADDz ds
			PRISM_ 9, lcbh,
				(rb-(lcbw/2))*sin(-alphahroL),rb-(rb-(lcbw/2))*cos(alphahroL),15,
				(rb+(lcbw/2)-0.00005)*sin(-alphahroL),rb-(rb+(lcbw/2)-0.000005)*cos(alphahroL),15,
				(rb+(lcbw/2))*sin(-alphahroL),rb-(rb+(lcbw/2))*cos(alphahroL),79,
				cos(alphahroL),sin(-alphahroL),800,
				(rb+(lcbw/2))*sin(ab+alphahroR),rb-(rb+(lcbw/2))*cos(ab+alphahroR),1015,
				(rb-(lcbw/2)+0.00005)*sin(ab+alphahroR),rb-(rb-(lcbw/2)+0.00005)*cos(ab+alphahroR),15,
				(rb-(lcbw/2))*sin(ab+alphahroR),rb-(rb-(lcbw/2))*cos(ab+alphahroR),79,
				-cos(ab+alphahroR),-sin(ab+alphahroR),800,
				(rb-(lcbw/2))*sin(-alphahroL),rb-(rb-(lcbw/2))*cos(alphahroL),1015
			DEL 1

			GROUP "post cutting curved"
				ADDz ds
				PRISM_ 9, hr - hrh - gs_railDist,
					(rb-(gs_railWidth/2))*sin(-alphahroL),rb-(rb-(gs_railWidth/2))*cos(alphahroL),15,
					(rb+(gs_railWidth/2)-0.00005)*sin(-alphahroL),rb-(rb+(gs_railWidth/2)-0.000005)*cos(alphahroL),15,
					(rb+(gs_railWidth/2))*sin(-alphahroL),rb-(rb+(gs_railWidth/2))*cos(alphahroL),79,
					cos(alphahroL),sin(-alphahroL),800,
					(rb+(gs_railWidth/2))*sin(ab+alphahroR),rb-(rb+(gs_railWidth/2))*cos(ab+alphahroR),1015,
					(rb-(gs_railWidth/2)+0.00005)*sin(ab+alphahroR),rb-(rb-(gs_railWidth/2)+0.00005)*cos(ab+alphahroR),15,
					(rb-(gs_railWidth/2))*sin(ab+alphahroR),rb-(rb-(gs_railWidth/2))*cos(ab+alphahroR),79,
					-cos(ab+alphahroR),-sin(ab+alphahroR),800,
					(rb-(gs_railWidth/2))*sin(-alphahroL),rb-(rb-(gs_railWidth/2))*cos(alphahroL),1015
				DEL 1
			ENDGROUP
			postcuttinggroupname = "post cutting curved"

		else

			addz ds + hr - hrh
				prism_ 12, hrh,
					0, -hrw/2, 79,
					1, 0, 800,
					0, 2*rb + hrw/2, 1079,
					-1, 0, 800,
					0, -hrw/2, 1079,
					0, -hrw/2, -1,

					0, hrw/2, 79,
					1, 0, 800,
					0, 2*rb - hrw/2, 1079,
					-1, 0, 800,
					0, hrw/2, 1079,
					0, hrw/2, -1
			del 1

			addz ds + hr - hrh - gs_railDist - gs_railHeight
				prism_ 12, gs_railHeight,
					0, -gs_railWidth/2, 79,
					1, 0, 800,
					0, 2*rb + gs_railWidth/2, 1079,
					-1, 0, 800,
					0, -gs_railWidth/2, 1079,
					0, -gs_railWidth/2, -1,

					0, gs_railWidth/2, 79,
					1, 0, 800,
					0, 2*rb - gs_railWidth/2, 1079,
					-1, 0, 800,
					0, gs_railWidth/2, 1079,
					0, gs_railWidth/2, -1
			del 1

			addz ds
				prism_ 12, lcbh,
					0, -lcbw/2, 79,
					1, 0, 800,
					0, 2*rb+lcbw/2, 1079,
					-1, 0, 800,
					0, -lcbw/2, 1079,
					0, -lcbw/2, -1,
	
					0, lcbw/2, 79,
					1, 0, 800,
					0, 2*rb-lcbw/2, 1079,
					-1, 0, 800,
					0, lcbw/2, 1079,
					0, lcbw/2, -1
			del 1

			group "post cutting curved 360"
				addz ds
					prism_ 12, hr - hrh - gs_railDist,
						0, -lcbw/2, 79,
						1, 0, 800,
						0, 2*rb + lcbw/2, 1079,
						-1, 0, 800,
						0, -lcbw/2, 1079,
						0, -lcbw/2, -1,

						0, lcbw/2, 79,
						1, 0, 800,
						0, 2*rb - lcbw/2, 1079,
						-1, 0, 800,
						0, lcbw/2, 1079,
						0, lcbw/2, -1
				del 1
			endgroup
			postcuttinggroupname = "post cutting curved 360"

		endif

		!!other parts are shorter, circularity checked differently
		if ab < 360 then

			!!! *** Posts *** !!! *********************************

			MATERIAL pmat
			resol rescs

			IF hrt_m=1 THEN			!Post height calculation
				ph=hr+ds-hrh+gs_PostBaseOverhang
			ELSE
				IF pt_m=1 THEN
				ph=hr+ds-hrd+gs_PostBaseOverhang
				ELSE
				ph=hr+ds-hrd/2+gs_PostBaseOverhang
				ENDIF
			ENDIF

			GROUP "post rect curved"
				addz -gs_PostBaseOverhang
				for i=1 to np step 1
					PRISM_ 5,ph,
						-pw/2,-pth/2,15,
						 pw/2,-pth/2,15,
						 pw/2,pth/2,15,
						-pw/2,pth/2,15,
						-pw/2,-pth/2,-1
				ADDy rb
				ROTz anglepost
				ADDy -rb
				next i
				DEL 3 * np + 1
			ENDGROUP

			PLACEGROUP subgroup ("post rect curved", postcuttinggroupname)

			!!! *** Balusters *** !!! ********************************

			MATERIAL bmat

			!!! *** Vertical *** !!! **********************************

			resol rescs

			bh = hr
			if bt_m = 1	then	! rectangular ballusters
				bh = bh - bw
			else
				bh = bh - bd / 2
			endif
			if hrt_m = 1 then	!rectangular handrail
				bh = bh - hrh
				if gs_horRailStyle_m = 1 then bh = bh - gs_railDist - gs_railHeight
				if gs_horRailStyle_m = 2 then bh = bh - gs_railDist - gs_railDiameter/2
			else
				bh = bh - hrd/2
				if gs_horRailStyle_m = 1 then bh = bh - hrd/2 - gs_railDist - gs_railHeight
				if gs_horRailStyle_m = 2 then bh = bh - hrd/2 - gs_railDist - gs_railDiameter/2
			endif
			bh = hr - hrh - gs_railDist - gs_railHeight - lcbh

			ADD 0, rb, ds + lcbh
			ROTz dpostangle - dboardangle/2
			ADDy -rb
			for i=1 to nb + 2
				PRISM_ 5,bh,
					-bw/2,-bth/2,15,
					 bw/2,-bth/2,15,
					 bw/2,bth/2,15,
					-bw/2,bth/2,15,
					-bw/2,-bth/2,-1
			ADDy rb
			ROTz distboardangle+dboardangle
			ADDy -rb
			NEXT i
			DEL 3 * (nb + 2) + 3

			KILLGROUP "post rect curved"

		else	!!!ANGLE=360

			!!! *** Posts *** !!! *********************************************

			MATERIAL pmat
			resol rescs

			ph=hr+ds-hrh+gs_PostBaseOverhang

			GROUP "post rect curved 360"
				for i=1 to np step 1
				PRISM_ 5,ph,
				-pw/2,-pth/2,15,
				 pw/2,-pth/2,15,
				 pw/2,pth/2,15,
				-pw/2,pth/2,15,
				-pw/2,-pth/2,-1
				ADDy rb
				ROTz anglepost
				ADDy -rb
				NEXT i
				DEL 3*np
			ENDGROUP

			PLACEGROUP subgroup ("post rect curved 360", postcuttinggroupname)

			!!! *** Balusters *** !!! ****************************************

			MATERIAL bmat
			resol res

			!Vertical

			resol rescs

			bh = hr - hrh - gs_railDist - gs_railHeight - lcbh

			ADD 0, rb, ds + lcbh
			ROTz dpostangle - dboardangle/2
			ADDy -rb
			for i=1 to nb*2 + 2
				PRISM_ 5,bh,
					-bw/2,-bth/2,15,
					 bw/2,-bth/2,15,
					 bw/2,bth/2,15,
					-bw/2,bth/2,15,
					-bw/2,-bth/2,-1
			ADDy rb
			ROTz distboardangle + dboardangle
			ADDy -rb
			NEXT i
			DEL 3*nb*2+3

			KILLGROUP "post rect curved 360"
		ENDIF

		KILLGROUP postcuttinggroupname
	ENDIF
ENDIF


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


	IF ABS(ani)>EPS and ptyp_m=1 THEN

		RESOL rescs

		IF rom_m=1 THEN									!Vertical Orientation

				IF rmm_m=2 THEN		!Perpendicular Measurement Extra Values
					hrh=hrh/cos(ani)
					hrd=hrd/cos(ani)
				ENDIF

				IF rmm_m=1 THEN			!!Vertical Measurement
					temp_ds=ds
					temp_hr=hr
					temp_PostBaseOverhang=gs_PostBaseOverhang
				temp_railDist = gs_railDist
				ELSE					!!Perpendicular Measurement
					temp_ds=dsPerp
					temp_hr=hrPerp
					temp_PostBaseOverhang=gs_PostBaseOverhangPerp
				temp_railDist = gs_railDistPerp
				ENDIF


				!!3D FEEDBACK AT HOTSPOT EDITING
				IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				ADD XStartPoslra,0,ZStartPoslra

					MATERIAL fmat

					ROTz -90 + gs_AngleL		!!! CONNECTING ANGLE
					ROTy -90
						CUTPLANE
					DEL 2

					ADDx templra
					ROTz 90 - gs_AngleR
					ROTy 90
						CUTPLANE
					DEL 3

							ROTx 90
							ADDz -fth/2

								PRISM_ 5,fth,
									-XPosL,temp_ds-YPosL,15,
									templra+XPosR,temp_ds+templra*tan(ani)+YPosR,15,
									templra+XPosR,temp_ds+temp_hr+templra*tan(ani)+YPosR,15,
									-XPosL,temp_ds+temp_hr-YPosL,15,
									-XPosL,temp_ds-YPosL,-1
							DEL 2

					CUTEND
					CUTEND

				DEL 1

				ELSE

				ADD XStartPoslra,0,ZStartPoslra

				!!! *** Handrail *** !!! *****************************************

				MATERIAL fmat

				ADDx -(hroL)
				ROTz -90 + gs_AngleL		!! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 3

				ADDx templra+(hroR)
				ROTz 90 - gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3

				GROUP "handrail round straight inclined"
					ADD -(hroL)-XPosL,0,temp_ds+temp_hr-hrd/2-(hroL)*tan(ani)-YPosL
					ROTy 90-ani
					CONE (templra+hroL+hroR)/cos(ani)+XPosL+XPosR,hrd/2,hrd/2,90+ani,90+ani
					DEL 1
				ENDGROUP

				GROUP "handrail rect straight inclined"
					ROTx 90
					ADDz -hrw/2
					PRISM_ 5,hrw,
						-(hroL)-XPosL,temp_ds+temp_hr-hrh-(hroL)*tan(ani)-YPosL,15,
						templra+(hroR)+XPosR,temp_ds+temp_hr-hrh+(templra+(hroR))*tan(ani)+YPosR,15,
						templra+(hroR)+XPosR,temp_ds+temp_hr+(templra+(hroR))*tan(ani)+YPosR,15,
						-(hroL)-XPosL,temp_ds+temp_hr-(hroL)*tan(ani)-YPosL,15,
						-(hroL)-XPosL,temp_ds+temp_hr-hrh-(hroL)*tan(ani)-YPosL,-1
					DEL 2

					ROTx 90
					ADDz -gs_railWidth/2
					PRISM_ 5, gs_railWidth,
						-(hroL)-XPosL,temp_ds+temp_hr-hrh - gs_railDist - gs_railHeight-(hroL)*tan(ani)-YPosL,15,
						templra+(hroR)+XPosR,temp_ds+temp_hr-hrh - gs_railDist - gs_railHeight+(templra+(hroR))*tan(ani)+YPosR,15,
						templra+(hroR)+XPosR,temp_ds+temp_hr-hrh - gs_railDist+(templra+(hroR))*tan(ani)+YPosR,15,
						-(hroL)-XPosL,temp_ds+temp_hr-hrh - gs_railDist-(hroL)*tan(ani)-YPosL,15,
						-(hroL)-XPosL,temp_ds+temp_hr-hrh - gs_railDist - gs_railHeight-(hroL)*tan(ani)-YPosL,-1
					DEL 2

					ROTx 90
					ADDz -lcbw/2
					PRISM_ 5, lcbw,
						-(hroL)-XPosL, temp_ds - (hroL)*tan(ani)-YPosL,15,
						templra+(hroR)+XPosR, temp_ds + (templra+(hroR))*tan(ani)+YPosR,15,
						templra+(hroR)+XPosR, temp_ds + lcbh + (templra+(hroR))*tan(ani)+YPosR,15,
						-(hroL)-XPosL, temp_ds + lcbh - (hroL)*tan(ani)-YPosL,15,
						-(hroL)-XPosL, temp_ds - (hroL)*tan(ani)-YPosL,-1
					DEL 2
				endgroup

				GROUP "post cutting straight inclined"
					ROTx 90
					ADD 0, gs_PostBaseOverhang, -gs_railWidth/2
					PRISM_ 5, gs_railWidth,
						-(hroL)-XPosL, temp_ds-(hroL)*tan(ani)-YPosL,15,
						templra+(hroR)+XPosR, temp_ds + (templra+(hroR))*tan(ani)+YPosR,15,
						templra+(hroR)+XPosR, temp_ds + temp_hr - hrh - gs_railDist + (templra+(hroR))*tan(ani)+YPosR,15,
						-(hroL)-XPosL, temp_ds + temp_hr - hrh - gs_railDist - (hroL)*tan(ani)-YPosL,15,
						-(hroL)-XPosL, temp_ds - (hroL)*tan(ani)-YPosL,-1
					DEL 2
				endgroup

				IF hrt_m=1 THEN
					PLACEGROUP "handrail rect straight inclined"
				ELSE
					PLACEGROUP "handrail round straight inclined"
				ENDIF

				CUTEND
				CUTEND


				!!! *** Posts *** !!! **********************************************


				MATERIAL pmat

				ADDx -(hroL)-LengthSize*dpost
				ROTz -90 + gs_AngleL		!! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 3

				ADDx templra+(hroR)+LengthSize*dpost
				ROTz 90 - gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3

						MATERIAL pmat

						IF hrt_m=1 THEN			!Post height calculation
						ph=temp_hr+temp_ds-hrh+temp_PostBaseOverhang
						ELSE
							IF pt_m=1 THEN
							ph=temp_hr+temp_ds-hrd+temp_PostBaseOverhang
							ELSE
							ph=temp_hr+temp_ds-hrd/2+temp_PostBaseOverhang
							ENDIF
						ENDIF

						GROUP "post round straight inclined"			!Groups
						for i=1 to np step 1
						cone ph,pd/2,pd/2,90,90-ani
						ADD distpost,0,distpost*tan(ani)
						NEXT i
						DEL np
						ENDGROUP

						GROUP "post rect straight inclined"
							ROTx 90
							ADDz -pth/2
							for i=1 to np step 1
								PRISM_ 5,pth,
									-pw/2,0,15,
									 pw/2, 0,15,
									 pw/2, pw/2*tan(ani)+ph,15,
									-pw/2,-pw/2*tan(ani)+ph,15,
									-pw/2,0,-1
							ADD distpost,distpost*tan(ani),0
							next i
							DEL np+2
						ENDGROUP

						ADDz -temp_PostBaseOverhang
							IF pt_m=1 THEN
								IF hrt_m=1 THEN
								PLACEGROUP subgroup ("post rect straight inclined","post cutting straight inclined")

								ELSE
								PLACEGROUP subgroup ("post rect straight inclined","handrail round straight inclined")
								ENDIF
							ELSE
								IF hrt_m=1 THEN
								PLACEGROUP "post round straight inclined"
								ELSE
								PLACEGROUP subgroup ("post round straight inclined","handrail round straight inclined")
								ENDIF
							ENDIF

						DEL 1

				CUTEND
				CUTEND


				!!! *** Boards *** !!! *****************************************

				MATERIAL bmat

				ADDx -(hroL)-LengthSize*dpost
				ROTz -90 + gs_AngleL		!! CONNECTING ANGLE
				ROTy -90
					CUTPLANE
				DEL 3

				ADDx templra+(hroR)+LengthSize*dpost
				ROTz 90 - gs_AngleR
				ROTy 90
					CUTPLANE
				DEL 3

						MATERIAL bmat

						!Horizontal

						GROUP "board rect straight H inclined"			!Groups
						ROTx 90
						ADDz -bth/2
						PRISM_ 5,bth,
							0,temp_ds,15,
							templra,temp_ds+templra*tan(ani),15,
							templra,temp_ds+bw+templra*tan(ani),15,
							0,temp_ds+bw,15,
							0,temp_ds,-1
						DEL 2
						ENDGROUP

						GROUP "board round straight H inclined"
							ADDz temp_ds+bd/2
							ROTy 90-ani
							cone templra/cos(ani),bd/2,bd/2,90+ani,90+ani
							DEL 2
						ENDGROUP

!						IF bt_m= 1 THEN
!							IF pt_m=1 THEN
!							PLACEGROUP subgroup ("board rect straight H inclined","post rect straight inclined")
!							ELSE
!							PLACEGROUP subgroup ("board rect straight H inclined","post round straight inclined")
!							ENDIF
!						ELSE
!							IF pt_m=1 THEN
!							PLACEGROUP subgroup ("board round straight H inclined","post rect straight inclined")
!							ELSE
!							PLACEGROUP subgroup ("board round straight H inclined","post round straight inclined")
!							ENDIF
!						ENDIF


				!Horizontal rail under handrail

				if gs_horRailStyle_m > 0 then
					material gs_railMat

					if gs_horRailStyle_m = 1 then
						if hrt_m = 1 then
							railPosY = temp_ds + temp_hr - hrh - temp_railDist - gs_railHeight
						else
							railPosY = temp_ds + temp_hr - hrd - temp_railDist - gs_railHeight
						endif

						GROUP "horizontalRailRect inclined"			!Groups
							ROTx 90
							ADDz -gs_railWidth/2
							PRISM_ 5, gs_railWidth,
								0,			railPosY,										15,
								templra,	railPosY + templra * tan(ani),					15,
								templra,	railPosY + gs_railHeight + templra * tan(ani),	15,
								0,			railPosY + gs_railHeight,						15,
								0,			railPosY,										-1
							DEL 2
						ENDGROUP
					endif

					if gs_horRailStyle_m = 2 then
						GROUP "horizontalRailRound inclined"
							if hrt_m = 1 then
								addz temp_ds + temp_hr - hrh - gs_railDist - gs_railDiameter/2
							else
								addz temp_ds + temp_hr - hrd - gs_railDist - gs_railDiameter/2
							endif
							ROTy 90-ani
							cone templra/cos(ani), gs_railDiameter/2, gs_railDiameter/2, 90+ani, 90+ani
							DEL 2
						ENDGROUP
					endif

!					IF gs_horRailStyle_m= 1 THEN
!						IF pt_m=1 THEN
!							PLACEGROUP subgroup ("horizontalRailRect inclined", "post rect straight inclined")
!						ELSE
!							PLACEGROUP subgroup ("horizontalRailRect inclined", "post round straight inclined")
!						ENDIF
!					ELSE
!						IF pt_m=1 THEN
!							PLACEGROUP subgroup ("horizontalRailRound inclined", "post rect straight inclined")
!						ELSE
!							PLACEGROUP subgroup ("horizontalRailRound inclined", "post round straight inclined")
!						ENDIF
!					ENDIF
				endif


				!Vertical

				bh = temp_hr
				if bt_m = 1	then	! rectangular ballusters
					bh = bh - bw
				else
					bh = bh - bd / 2
				endif
				if hrt_m = 1 then	!rectangular handrail
					bh = bh - hrh
					if gs_horRailStyle_m = 1 then bh = bh - temp_railDist - gs_railHeight
					if gs_horRailStyle_m = 2 then bh = bh - temp_railDist - gs_railDiameter/2
				else
					bh = bh - hrd/2
					if gs_horRailStyle_m = 1 then bh = bh - hrd/2 - temp_railDist - gs_railHeight
					if gs_horRailStyle_m = 2 then bh = bh - hrd/2 - temp_railDist - gs_railDiameter/2
				endif
				bh = temp_hr - hrh - gs_railDist - gs_railHeight - lcbh


						GROUP "board round straight V inclined"			!Groups
							for k=1 to np-1 step 1
								ADD dpost+distboard+bd/2,0,temp_ds+bd/2+(dpost+distboard+bd/2)*tan(ani)
								for i=1 to nb step 1
								cone bh,bd/2,bd/2,90-ani,90-ani
								ADD distboard+bd,0,(distboard+bd)*tan(ani)
								NEXT i
								DEL nb+1
							ADD distpost,0,distpost*tan(ani)
							NEXT k
							DEL np-1
						ENDGROUP

						GROUP "board rect straight V inclined"
!							for l=1 to np-1 step 1
								ROTx 90
								ADD dpost+distboard*0-bw/2,temp_ds+lcbh+(dpost+distboard*0-bw/2)*tan(ani),-bth/2
								for i=1 to nb + 2
								PRISM_ 5,bth,
								-bw/2,-bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani),15,
								 bw/2, bw/2*tan(ani)+bh,15,
								-bw/2,-bw/2*tan(ani)+bh,15,
								-bw/2,-bw/2*tan(ani),-1
								ADD distboard+bw,(distboard+bw)*tan(ani),0
								NEXT i
								DEL nb + 2 + 2
!							ADD distpost,0,distpost*tan(ani)
!							NEXT l
!							DEL np-1
						ENDGROUP

				IF bt_m = 1 THEN			! rect balusters
					IF hrt_m = 1 THEN		! rect handrail
						if gs_horRailStyle_m = 2 then		! round horizontal rail
							PLACEGROUP subgroup ("board rect straight V inclined", "horizontalRailRound inclined")
						else
							PLACEGROUP "board rect straight V inclined"
						endif
					ELSE					! round handrail
						if gs_horRailStyle_m = 0 then PLACEGROUP subgroup ("board rect straight V inclined", "handrail round straight inclined")
						if gs_horRailStyle_m = 1 then PLACEGROUP "board rect straight V inclined"
						if gs_horRailStyle_m = 2 then PLACEGROUP subgroup ("board rect straight V inclined", "horizontalRailRound inclined")
							ENDIF
				ELSE						! round balusters
					IF hrt_m = 1 THEN		! rect handrail
						if gs_horRailStyle_m = 0 then PLACEGROUP subgroup ("board round straight V inclined", "board round straight H inclined")
						if gs_horRailStyle_m = 1 then PLACEGROUP subgroup ("board round straight V inclined", "board round straight H inclined")
						if gs_horRailStyle_m = 2 then PLACEGROUP subgroup (subgroup("board round straight V inclined", "board round straight H inclined"), "horizontalRailRound inclined")
					ELSE					! round handrail
						if gs_horRailStyle_m = 0 then PLACEGROUP subgroup (subgroup ("board round straight V inclined", "handrail round straight inclined"),"board round straight H inclined")
						if gs_horRailStyle_m = 1 then PLACEGROUP subgroup ("board round straight V inclined", "board round straight H inclined")
						if gs_horRailStyle_m = 2 then PLACEGROUP subgroup (subgroup ("board round straight V inclined", "board round straight H inclined"), "horizontalRailRound inclined")
							ENDIF
						ENDIF

				CUTEND
				CUTEND

					KILLGROUP "handrail round straight inclined"
					KILLGROUP "post round straight inclined"
					KILLGROUP "post rect straight inclined"
					KILLGROUP "board rect straight H inclined"
					KILLGROUP "board round straight H inclined"
					KILLGROUP "board rect straight V inclined"
					KILLGROUP "board round straight V inclined"
				if gs_horRailStyle_m = 1 then KILLGROUP "horizontalRailRect inclined"
				if gs_horRailStyle_m = 2 then KILLGROUP "horizontalRailRound inclined"

			DEL 1
			ENDIF

		ELSE						!Perpendicular Orientation
									!Measurement is always perpendicular to inclination in this case
			ROTy -ani
			ADDX XStartPoslrah

				!!3D FEEDBACK AT HOTSPOT EDITING
				IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

					ROTx 90
					ADDz -fth/2
						!!FRAME
						MATERIAL fmat
						PRISM_ 5,fth,
							0,ds,15,
							templrah,ds,15,
							templrah,ds+hr,15,
							0,ds+hr,15,
							0,ds,-1
					DEL 2

				ELSE		!! 3D DETAILED MODEL

					!!! *** Handrail *** !!! ******************************************
					MATERIAL fmat

					GROUP "handrail round straight perp"			!Groups
					ADD  -hroLDiagonal,0,ds+hr-hrd/2
					ROTy 90
					CYLIND templrah+hroLDiagonal+hroRDiagonal,hrd/2
					DEL 2
					ENDGROUP

					GROUP "handrail rect straight perp"
						ROTx 90
						ADDz -hrw/2
							PRISM_ 5,hrw,
								-hroLDiagonal,ds+hr-hrh,15,
								templrah+hroRDiagonal,ds+hr-hrh,15,
								templrah+hroRDiagonal,ds+hr,15,
								-hroLDiagonal,ds+hr,15,
								-hroLDiagonal,ds+hr-hrh,-1
						DEL 2

						ROTx 90
						ADDz -gs_railWidth/2
							PRISM_ 5, gs_railWidth,
								-hroLDiagonal, ds + hr - hrh - gs_railDist - gs_railHeight, 15,
								templrah+hroRDiagonal, ds + hr - hrh - gs_railDist - gs_railHeight, 15,
								templrah+hroRDiagonal, ds + hr - hrh - gs_railDist, 15,
								-hroLDiagonal, ds + hr - hrh - gs_railDist, 15,
								-hroLDiagonal, ds + hr - hrh - gs_railDist - gs_railHeight, -1
						DEL 2

						ROTx 90
						ADDz -lcbw/2
							PRISM_ 5, lcbw,
								-hroLDiagonal, ds, 15,
								templrah+hroRDiagonal, ds, 15,
								templrah+hroRDiagonal, ds + lcbh, 15,
								-hroLDiagonal, ds + lcbh, 15,
								-hroLDiagonal, ds, -1
						DEL 2
					endgroup

					GROUP "post cutting straight perp"
						ROTx 90
						ADD 0, gs_PostBaseOverhang, -lcbw/2
							PRISM_ 5, lcbw,
								-hroLDiagonal, ds, 15,
								templrah+hroRDiagonal, ds, 15,
								templrah+hroRDiagonal, ds + hr - hrh - gs_railDist, 15,
								-hroLDiagonal, ds + hr - hrh - gs_railDist, 15,
								-hroLDiagonal, ds, -1
						DEL 2
					endgroup

					IF hrt_m=1 THEN
						PLACEGROUP "handrail rect straight perp"
					ELSE
						PLACEGROUP "handrail round straight perp"
					ENDIF


					!!! *** Post *** !!! **********************************************
					MATERIAL pmat

					IF hrt_m=1 THEN			!Post height calculation
					ph=hr+ds-hrh+gs_PostBaseOverhang
					ELSE
						IF pt_m=1 THEN
						ph=hr+ds-hrd+gs_PostBaseOverhang
						ELSE
						ph=hr+ds-hrd/2+gs_PostBaseOverhang
						ENDIF
					ENDIF

					GROUP "post round straight perp"			!Groups
					for i=1 to np step 1
					CYLIND ph,pd/2
					ADDx distpost
					NEXT i
					DEL np
					ENDGROUP

					GROUP "post rect straight perp"
						for i=1 to np step 1
							PRISM_ 5,ph,
							-pw/2,-pth/2,15,
							 pw/2,-pth/2,15,
							 pw/2,pth/2,15,
							-pw/2,pth/2,15,
							-pw/2,-pth/2,-1
						ADDx distpost
						NEXT i
						DEL np
					ENDGROUP

					ADDz -gs_PostBaseOverhang
						IF pt_m=1 THEN
							PLACEGROUP subgroup ("post rect straight perp", "post cutting straight perp")
						ELSE
							IF hrt_m=1 THEN
							PLACEGROUP "post round straight perp"
							ELSE
							PLACEGROUP subgroup ("post round straight perp","handrail round straight perp")
							ENDIF
						ENDIF

					DEL 1

					!!! *** Boards *** !!! ********************************************

					MATERIAL bmat

					!!! Horizontal

					GROUP "board rect straight H perp"			!Groups
					ADDz  ds
						PRISM_ 5,bw,
							0,-bth/2,15,
							templrah,-bth/2,15,
							templrah,bth/2,15,
							0,bth/2,15,
							0,-bth/2,-1
					DEL 1
					ENDGROUP

					GROUP "board round straight H perp"
						ADDz  ds+bd/2
						ROTy 90
							CYLIND templrah,bd/2
						DEL 2
					ENDGROUP

!					IF bt_m= 1 THEN
!						IF pt_m=1 THEN
!						PLACEGROUP subgroup ("board rect straight H perp","post rect straight perp")
!						ELSE
!						PLACEGROUP subgroup ("board rect straight H perp","post round straight perp")
!						ENDIF
!					ELSE
!						IF pt_m=1 THEN
!						PLACEGROUP subgroup ("board round straight H perp","post rect straight perp")
!						ELSE
!						PLACEGROUP subgroup ("board round straight H perp","post round straight perp")
!						ENDIF
!					ENDIF


				!Horizontal rail under handrail

				if gs_horRailStyle_m > 0 then
					material gs_railMat

					if gs_horRailStyle_m = 1 then
						GROUP "horizontalRailRect perp"			!Groups
							if hrt_m = 1 then
								addz  ds + hr - hrh - gs_railDist - gs_railHeight
							else
								addz  ds + hr - hrd - gs_railDist - gs_railHeight
							endif
							PRISM_ 5, gs_railHeight,
								0,			-gs_railWidth/2,	15,
								templrah,	-gs_railWidth/2,	15,
								templrah,	 gs_railWidth/2,	15,
								0,			 gs_railWidth/2,	15,
								0,			-gs_railWidth/2,	-1
							DEL 1
						ENDGROUP
					endif

					if gs_horRailStyle_m = 2 then
						GROUP "horizontalRailRound perp"
							if hrt_m = 1 then
								addz  ds + hr - hrh - gs_railDist - gs_railDiameter/2
							else
								addz  ds + hr - hrd - gs_railDist - gs_railDiameter/2
							endif
							ROTy 90
							CYLIND templrah, gs_railDiameter/2
							DEL 2
						ENDGROUP
					endif

!					IF gs_horRailStyle_m = 1 THEN
!						IF pt_m = 1 THEN
!							PLACEGROUP subgroup ("horizontalRailRect perp","post rect straight perp")
!						ELSE
!							PLACEGROUP subgroup ("horizontalRailRect perp","post round straight perp")
!						ENDIF
!					ELSE
!						IF pt_m = 1 THEN
!							PLACEGROUP subgroup ("horizontalRailRound perp","post rect straight perp")
!						ELSE
!							PLACEGROUP subgroup ("horizontalRailRound perp","post round straight perp")
!						ENDIF
!					ENDIF
				endif


				!!! Vertical

				bh = hr
				if bt_m = 1	then	! rectangular ballusters
					bh = bh - bw
				else
					bh = bh - bd / 2
				endif
				if hrt_m = 1 then	!rectangular handrail
					bh = bh - hrh
					if gs_horRailStyle_m = 1 then bh = bh - gs_railDist - gs_railHeight
					if gs_horRailStyle_m = 2 then bh = bh - gs_railDist - gs_railDiameter/2
				else
					bh = bh - hrd/2
					if gs_horRailStyle_m = 1 then bh = bh - hrd/2 - gs_railDist - gs_railHeight
					if gs_horRailStyle_m = 2 then bh = bh - hrd/2 - gs_railDist - gs_railDiameter/2
				endif
				bh = hr - hrh - gs_railDist - gs_railHeight - lcbh


					GROUP "board round straight V perp"			!Groups
						for k=1 to np-1 step 1
							ADD dpost+distboard+bd/2,0,ds+bd/2
							for i=1 to nb step 1
							CYLIND bh,bd/2
							ADDx distboard+bd
							NEXT i
							DEL nb+1
						ADDx distpost
						NEXT k
						DEL np-1
					ENDGROUP

					GROUP "board rect straight V perp"
!						for l=1 to np-1 step 1
							ADD dpost+distboard*0-bw/2, 0, ds + lcbh
							for i=1 to nb + 2
							PRISM_ 5,bh,
								-bw/2,-bth/2,15,
								 bw/2,-bth/2,15,
								 bw/2,bth/2,15,
								-bw/2,bth/2,15,
								-bw/2,-bth/2,-1
							ADDx distboard+bw
							NEXT i
							DEL nb+1
!						ADDx distpost
!						NEXT l
!						DEL np-1
					ENDGROUP

				IF bt_m = 1 THEN			! rect balusters
					IF hrt_m = 1 THEN		! rect handrail
						if gs_horRailStyle_m = 2 then		! round horizontal rail
							PLACEGROUP subgroup ("board rect straight V perp", "horizontalRailRound perp")
						else
							PLACEGROUP "board rect straight V perp"
						endif
					ELSE					! round handrail
						if gs_horRailStyle_m = 0 then PLACEGROUP subgroup ("board rect straight V perp", "handrail round straight perp")
						if gs_horRailStyle_m = 1 then PLACEGROUP "board rect straight V perp"
						if gs_horRailStyle_m = 2 then PLACEGROUP subgroup ("board rect straight V perp", "horizontalRailRound perp")
						ENDIF
				ELSE						! round balusters
					IF hrt_m = 1 THEN		! rect handrail
						if gs_horRailStyle_m = 0 then PLACEGROUP subgroup ("board round straight V perp", "board round straight H perp")
						if gs_horRailStyle_m = 1 then PLACEGROUP subgroup ("board round straight V perp", "board round straight H perp")
						if gs_horRailStyle_m = 2 then PLACEGROUP subgroup (subgroup("board round straight V perp", "board round straight H perp"), "horizontalRailRound perp")
					ELSE					! round handrail
						if gs_horRailStyle_m = 0 then PLACEGROUP subgroup (subgroup ("board round straight V perp", "handrail round straight perp"),"board round straight H perp")
						if gs_horRailStyle_m = 1 then PLACEGROUP subgroup ("board round straight V perp", "board round straight H perp")
						if gs_horRailStyle_m = 2 then PLACEGROUP subgroup (subgroup ("board round straight V perp", "board round straight H perp"), "horizontalRailRound perp")
					ENDIF
				ENDIF

					KILLGROUP "handrail round straight perp"
					KILLGROUP "post round straight perp"
					KILLGROUP "post rect straight perp"
					KILLGROUP "board rect straight H perp"
					KILLGROUP "board round straight H perp"
					KILLGROUP "board rect straight V perp"
					KILLGROUP "board round straight V perp"
				if gs_horRailStyle_m = 1 then KILLGROUP "horizontalRailRect perp"
				if gs_horRailStyle_m = 2 then KILLGROUP "horizontalRailRound perp"

				ENDIF	!! END OF 3D MODEL
			DEL 1		!! END OF DRAG
			DEL 1		!! END OF ROTATION
		ENDIF
	ENDIF




!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!


	IF ABS(ani)>EPS and ptyp_m<>1 THEN

			RESOL rescs

			MATERIAL fmat



			!!3D FEEDBACK AT HOTSPOT EDITING
			IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

				!!!!FRAME
				MATERIAL fmat

				alphai=-alphaincl
				deltazi=-deltazincl

				for i=1 to nalphaincl+3 step 1
					put rb*sin(alphai),rb-rb*cos(alphai),ds+deltazi
					alphai=alphai+alphaincl
					deltazi=deltazi+deltazincl
			next i

				TUBEA 5,nalphaincl+3,1+2+16+32,
					-fth/2,0,0,
					fth/2,0,0,
					fth/2,hr,0,
					-fth/2,hr,0,
					-fth/2,0,-1,
					get (nsp)

			ELSE

				RestAngleL=alphahroL-nalphahroL*alphaincl		!!Rest Angle= Overhang Angle- Segments Points Angle
				LOHkerul=(rb*pi*alphahroL)/180					!!Perimeter of Handrail
				deltazLOH=LOHkerul*sin(ani)						!!vertical distance of Handrail
				deltara=deltazLOH-(deltazincl*nalphahroL)		!!vertical step of Rest Angle

				RestAngleR=alphahroR-nalphahroR*alphaincl
				ROHkerul=(rb*pi*alphahroR)/180
				deltazROH=ROHkerul*sin(ani)
				deltaraR=deltazROH-(deltazincl*nalphahroR)


				alphai=-(nalphahroL+1)*alphaincl
				deltazi=-(nalphahroL+1)*deltazincl

				if ABS(hroR)<EPS THEN
				tempStep=1
				ELSE
				tempStep=0
				ENDIF

					PUT rb*sin(-alphahroL-restangleL),rb-rb*cos(-alphahroL-restangleL),ds-deltazLOH-deltara
					PUT rb*sin(-alphahroL),rb-rb*cos(-alphahroL),ds-deltazLOH
					PUT rb*sin(-alphahroL+restangleL),rb-rb*cos(-alphahroL+restangleL),ds-deltazLOH+deltara

				if abs(nalphahroL)>EPS then		!! Left Handrail
				for i=1 to nalphahroL step 1
					PUT rb*sin(-alphahroL+restangleL+i*alphaincl),rb-rb*cos(-alphahroL+restangleL+i*alphaincl),ds-deltazLOH+deltara+i*deltazincl
				next i
				endif

				FOR i=0 to nalphaincl+tempStep step 1		!! Handrail between Posts
					PUT rb*sin(i*alphaincl),rb-rb*cos(i*alphaincl),ds+i*deltazincl
				NEXT i

			IF ABS(hroR)>EPS THEN				!! Right Handrail

				if abs(nalphahroR)>EPS then
					for i=1 to nalphahroR step 1
					PUT rb*sin((nalphaincl+i)*alphaincl),rb-rb*cos((nalphaincl+i)*alphaincl),ds+(nalphaincl+i)*deltazincl
					next i
				endif

					PUT rb*sin((nalphaincl+nalphahroR)*alphaincl+restangleR),rb-rb*cos((nalphaincl+nalphahroR)*alphaincl+restangleR),ds+(nalphaincl+nalphahroR)*deltazincl+deltaraR
					PUT rb*sin((nalphaincl+nalphahroR)*alphaincl+2*restangleR),rb-rb*cos((nalphaincl+nalphahroR)*alphaincl+2*restangleR),ds+(nalphaincl+nalphahroR)*deltazincl+2*deltaraR
			ENDIF

				GROUP "handrail round inclined curved"
					ADDz hr-hrd/2
					tubea 6,nsp/3,1+2+16+32,
						-hrd/2,0,1,
						 0,-1,801,
						 hrd/2,0,1001,
						 0,1,801,
						-hrd/2,0,1001,
						-hrd/2,0,-1,
						use (nsp)
					DEL 1
				ENDGROUP

				GROUP "handrail rect inclined curved"
					ADDz hr-hrh
					tubea 5,nsp/3,1+2+16+32,
						-hrw/2,0,0,
						 hrw/2,0,0,
						 hrw/2,hrh,0,
						-hrw/2,hrh,0,
						-hrw/2,0,-1,
						use (nsp)
					DEL 1

					ADDz hr - hrh - gs_railDist - gs_railHeight
					tubea 5,nsp/3,1+2+16+32,
						-gs_railWidth/2,0,0,
						 gs_railWidth/2,0,0,
						 gs_railWidth/2, gs_railHeight,0,
						-gs_railWidth/2, gs_railHeight,0,
						-gs_railWidth/2,0,-1,

						use (nsp)
					DEL 1

					ADDz ds*0
					tubea 5,nsp/3,1+2+16+32,
						-lcbw/2,0,0,
						 lcbw/2,0,0,
						 lcbw/2, lcbh,0,
						-lcbw/2, lcbh,0,
						-lcbw/2,0,-1,

						use (nsp)
					DEL 1
				ENDGROUP

				GROUP "post cutting inclined curved"
					ADDz hr-hrh
					tubea 5,nsp/3,1+2+16+32,
						-hrw/2,0,0,
						 hrw/2,0,0,
						 hrw/2,hrh * 10,0,
						-hrw/2,hrh * 10,0,
						-hrw/2,0,-1,
						use (nsp)
					DEL 1

					ADDz hr - hrh - gs_railDist - gs_railHeight
					tubea 5,nsp/3,1+2+16+32,
						-gs_railWidth/2,0,0,
						 gs_railWidth/2,0,0,
						 gs_railWidth/2, gs_railHeight,0,
						-gs_railWidth/2, gs_railHeight,0,
						-gs_railWidth/2,0,-1,

						use (nsp)
					DEL 1

					ADDz ds*0
					tubea 5,nsp/3,1+2+16+32,
						-lcbw/2,0,0,
						 lcbw/2,0,0,
						 lcbw/2, hr - hrh - gs_railDist,0,
						-lcbw/2, hr - hrh - gs_railDist,0,
						-lcbw/2,0,-1,

						use (nsp)
					DEL 1
				ENDGROUP

				GROUP "bar cutting inclined curved"
					ADDz hr - hrh - gs_railDist - gs_railHeight
					tubea 5,nsp/3,1+2+16+32,
						-gs_railWidth/2,0,0,
						 gs_railWidth/2,0,0,
						 gs_railWidth/2, gs_railHeight*10,0,
						-gs_railWidth/2, gs_railHeight*10,0,
						-gs_railWidth/2,0,-1,

						use (nsp)
					DEL 1

					ADDz ds*0 - 9 * lcbh
					tubea 5,nsp/3,1+2+16+32,
						-lcbw/2,0,0,
						 lcbw/2,0,0,
						 lcbw/2, lcbh*10,0,
						-lcbw/2, lcbh*10,0,
						-lcbw/2,0,-1,

						get (nsp)
					DEL 1
				ENDGROUP
				IF hrt_m=1 THEN
					PLACEGROUP "handrail rect inclined curved"
!					PLACEGROUP "post cutting inclined curved"

				ELSE
					PLACEGROUP "handrail round inclined curved"
				ENDIF


				!! *** POST *** !! *****************************************************

				MATERIAL pmat

				IF hrt_m=1 THEN			!Post height calculation
				ph=hr+ds-hrh/2+gs_PostBaseOverhang
				ELSE
					IF pt_m=1 THEN
					ph=hr+ds-hrd/2+gs_PostBaseOverhang
					ELSE
					ph=hr+ds-hrd/2+gs_PostBaseOverhang
					ENDIF
				ENDIF

				GROUP "post round inclined curved"
				ADDz -gs_PostBaseOverhang
					for i=1 to np step 1
					CYLIND ph,pd/2
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT i
					DEL 4*np
				DEL 1
				ENDGROUP

				GROUP "post rect inclined curved"
				ADDz -gs_PostBaseOverhang
					for i=1 to np step 1
					PRISM_ 5,ph,
					-pw/2,-pth/2,15,
					 pw/2,-pth/2,15,
					 pw/2,pth/2,15,
					-pw/2,pth/2,15,
					-pw/2,-pth/2,-1
					ADDy rb
					ROTz anglepost
					ADDy -rb
					ADDz deltazpost
					NEXT i
					DEL 4*np
				DEL 1
				ENDGROUP

					IF pt_m=1 THEN
						IF hrt_m=1 THEN
							PLACEGROUP subgroup ("post rect inclined curved","post cutting inclined curved")
						ELSE
							PLACEGROUP subgroup ("post rect inclined curved","handrail round inclined curved")
						ENDIF
					ELSE
						IF hrt_m=1 THEN
							PLACEGROUP subgroup ("post round inclined curved","handrail rect inclined curved")
						ELSE
							PLACEGROUP subgroup ("post round inclined curved","handrail round inclined curved")
						ENDIF
					ENDIF


				!!! *** Balusters *** !!! *********************************************

				MATERIAL bmat

				!!!Horizontal

				alphai=-alphaincl
				deltazi=-deltazincl
				for i=1 to nalphaincl+3 step 1
				put rb*sin(alphai),rb-rb*cos(alphai),deltazi
				alphai=alphai+alphaincl
				deltazi=deltazi+deltazincl
			next i

				GROUP "board round inclined curved H"
					ADDz ds+bd/2
					tubea 6,nalphaincl+3,1+2+16+32,
					-bd/2,0,1,
					 0,-1,801,
					 bd/2,0,1001,
					 0,1,801,
					-bd/2,0,1001,
					-bd/2,0,-1,
					use (nsp)
					DEL 1
				ENDGROUP

				GROUP "board rect inclined curved H"
					ADDz ds
					tubea 5,nalphaincl+3,1+2+16+32,
					-bth/2,0,0,
					 bth/2,0,0,
					 bth/2,bw,0,
					-bth/2,bw,0,
					-bth/2,0,-1,
					get (nsp)
					DEL 1
				ENDGROUP

!				IF bt_m= 1 THEN
!					IF pt_m=1 THEN
!					PLACEGROUP subgroup ("board rect inclined curved H","post rect inclined curved")
!					ELSE
!					PLACEGROUP subgroup ("board rect inclined curved H","post round inclined curved")
!					ENDIF
!				ELSE
!					IF pt_m=1 THEN
!					PLACEGROUP subgroup ("board round inclined curved H","post rect inclined curved")
!					ELSE
!					PLACEGROUP subgroup ("board round inclined curved H","post round inclined curved")
!					ENDIF
!				ENDIF


			!Horizontal rail under handrail


			if gs_horRailStyle_m > 0 then
				material gs_railMat

				alphai=-alphaincl
				deltazi=-deltazincl
				for i=1 to nalphaincl+3 step 1
					put rb*sin(alphai),rb-rb*cos(alphai),deltazi
					alphai=alphai+alphaincl
					deltazi=deltazi+deltazincl
				next i

				if gs_horRailStyle_m = 1 then
					GROUP "horizontalRailRect inclined curved"
						if hrt_m = 1 then
							addz  ds + hr - hrh - gs_railDist - gs_railHeight
						else
							addz  ds + hr - hrd - gs_railDist - gs_railHeight
						endif

						tubea 5,nalphaincl+3,1+2+16+32,
							-gs_railWidth/2,0,0,
							 gs_railWidth/2,0,0,
							 gs_railWidth/2,gs_railHeight,0,
							-gs_railWidth/2,gs_railHeight,0,
							-gs_railWidth/2,0,-1,
							get (nsp)

						DEL 1
					ENDGROUP
				endif

				if gs_horRailStyle_m = 2 then
					GROUP "horizontalRailRound inclined curved"
						if hrt_m = 1 then
							addz  ds + hr - hrh - gs_railDist - gs_railDiameter/2
						else
							addz  ds + hr - hrd - gs_railDist - gs_railDiameter/2
						endif
						tubea 6,nalphaincl+3,1+2+16+32,
							-gs_railDiameter/2,	 0,	1,
							 0,					-1,	801,
							 gs_railDiameter/2,	 0,	1001,
							 0,					 1,	801,
							-gs_railDiameter/2,	 0,	1001,
							-gs_railDiameter/2,	 0,	-1,
							get (nsp)
						DEL 1
					ENDGROUP
				endif

!				IF gs_horRailStyle_m= 1 THEN
!					IF pt_m=1 THEN
!						PLACEGROUP subgroup ("horizontalRailRect inclined curved","post rect inclined curved")
!					ELSE
!						PLACEGROUP subgroup ("horizontalRailRect inclined curved","post round inclined curved")
!					ENDIF
!				ELSE
!					IF pt_m=1 THEN
!						PLACEGROUP subgroup ("horizontalRailRound inclined curved","post rect inclined curved")
!					ELSE
!						PLACEGROUP subgroup ("horizontalRailRound inclined curved","post round inclined curved")
!					ENDIF
!				ENDIF
			endif


			!Vertical

			bh = hr
			if bt_m = 1	then	! rectangular ballusters
				bh = bh - bw
			else
				bh = bh - bd / 2
			endif
			if hrt_m = 1 then	!rectangular handrail
				bh = bh - hrh
				if gs_horRailStyle_m = 1 then bh = bh - gs_railDist - gs_railHeight
				if gs_horRailStyle_m = 2 then bh = bh - gs_railDist - gs_railDiameter/2
			else
				bh = bh - hrd/2
				if gs_horRailStyle_m = 1 then bh = bh - hrd/2 - gs_railDist - gs_railHeight
				if gs_horRailStyle_m = 2 then bh = bh - hrd/2 - gs_railDist - gs_railDiameter/2
			endif
			bh = hr - hrh - gs_railDist - gs_railHeight*0 - lcbh


			GROUP "board round inclined curved V"			!Groups
				for k=1 to np-1 step 1
					ADD 0,rb,ds+bd/2
					ROTz dpostangle+distboardangle+dboardangle/2
					ADDy -rb
					ADDz deltazboard0
					for i=1 to nb step 1
					CYLIND bh,bd/2
					ADDy rb
					ROTz distboardangle+dboardangle
					ADDy -rb
					ADDz deltazboard
					NEXT i
					DEL 4*nb+4
				ADDy rb
				ROTz anglepost
				ADDy -rb
				ADDz deltazpost
				NEXT k
				DEL 4*(np-1)
			ENDGROUP

			GROUP "board rect inclined curved V"
!				for l=1 to np-1 step 1
					ADD 0,rb,ds+bw/2*0
					ROTz dpostangle+distboardangle*0-dboardangle/2
					ADDy -rb
					ADDz deltazboard0 * 0
					for i=1 to nb + 2
					PRISM_ 5,bh,
					-bw/2,-bth/2,15,
					 bw/2,-bth/2,15,
					 bw/2,bth/2,15,
					-bw/2,bth/2,15,
					-bw/2,-bth/2,-1
					ADDy rb
					ROTz distboardangle+dboardangle
					ADDy -rb
					ADDz deltazboard
					NEXT i
					DEL 4 * (nb + 2) + 4
!				ADDy rb
!				ROTz anglepost
!				ADDy -rb
!				ADDz deltazpost
!				NEXT l
!				DEL 4*(np-1)
			ENDGROUP

			IF bt_m = 1 THEN			! rect balusters
				IF hrt_m = 1 THEN		! rect handrail
					if gs_horRailStyle_m = 2 then		! round horizontal rail
						PLACEGROUP subgroup ("board rect inclined curved V", "horizontalRailRound inclined curved")
					else
						PLACEGROUP subgroup ("board rect inclined curved V", "bar cutting inclined curved")
					endif
				ELSE					! round handrail
					if gs_horRailStyle_m = 0 then PLACEGROUP subgroup ("board rect inclined curved V", "handrail round inclined curved")
					if gs_horRailStyle_m = 1 then PLACEGROUP "board rect inclined curved V"
					if gs_horRailStyle_m = 2 then PLACEGROUP subgroup ("board rect inclined curved V", "horizontalRailRound inclined curved")
					ENDIF
			ELSE						! round balusters
				IF hrt_m = 1 THEN		! rect handrail
					if gs_horRailStyle_m = 0 then PLACEGROUP subgroup ("board round inclined curved V", "board round inclined curved H")
					if gs_horRailStyle_m = 1 then PLACEGROUP subgroup ("board round inclined curved V", "board round inclined curved H")
					if gs_horRailStyle_m = 2 then PLACEGROUP subgroup (subgroup("board round inclined curved V", "board round inclined curved H"), "horizontalRailRound inclined curved")
				ELSE					! round handrail
					if gs_horRailStyle_m = 0 then PLACEGROUP subgroup (subgroup ("board round inclined curved V", "handrail round inclined curved"),"board round inclined curved H")
					if gs_horRailStyle_m = 1 then PLACEGROUP subgroup ("board round inclined curved V", "board round inclined curved H")
					if gs_horRailStyle_m = 2 then PLACEGROUP subgroup (subgroup ("board round inclined curved V", "board round inclined curved H"), "horizontalRailRound inclined curved")
				ENDIF
			ENDIF


				KILLGROUP "handrail round inclined curved"
				KILLGROUP "handrail rect inclined curved"
				KILLGROUP "post round inclined curved"
				KILLGROUP "post rect inclined curved"
				KILLGROUP "board rect inclined curved H"
				KILLGROUP "board round inclined curved H"
				KILLGROUP "board rect inclined curved V"
				KILLGROUP "board round inclined curved V"
			if gs_horRailStyle_m = 1 then KILLGROUP "horizontalRailRect inclined curved"
			if gs_horRailStyle_m = 2 then KILLGROUP "horizontalRailRound inclined curved"

		ENDIF
	ENDIF
ENDIF


!!DETAILED 3D END!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! ** HOTSPOTS ** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF LengthSize = 0 THEN			!!!OVERHANG OF HANDRAIL
		temphroL = -hroL
		temphroR = hroR
		RefPoint1 = 1
		RefPoint2 = -1

		InclRefPoint = -tan(ani)

		temphroLDiag = -hroLDiagonal
		temphroRDiag = hroRDiagonal
	ELSE
		temphroL = hroL
		temphroR = -hroR
		RefPoint1 = -1
		RefPoint2 = 1

		InclRefPoint = tan(ani)

		temphroLDiag = hroLDiagonal
		temphroRDiag = -hroRDiagonal
	ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	IF cfs=0 THEN
		IF ABS(ani)<EPS and ptyp_m=1 THEN

				!!!LENGHT
				HOTSPOT 0,0,ds+hr,unID,lra,1+256 : unID=unID+1
				HOTSPOT lra,0,ds+hr,unID,lra,2 : unID=unID+1
				HOTSPOT -1,0,ds+hr,unID,lra,3 : unID=unID+1

				IF LengthSize = 0 THEN
					HOTSPOT 0,0,0,unID,lra,1+256 : unID=unID+1
					HOTSPOT lra,0,0,unID,lra,2 : unID=unID+1
					HOTSPOT -1,0,0,unID,lra,3 : unID=unID+1

					HOTSPOT 0,0,ds,unID,lra,1+256 : unID=unID+1
					HOTSPOT lra,0,ds,unID,lra,2 : unID=unID+1
					HOTSPOT -1,0,ds,unID,lra,3 : unID=unID+1
				ENDIF

				!!!DISTANCE FROM FLOOR
				HOTSPOT templra/2+LengthSize*hroL,0,0,unID,ds,1+128 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,-1,unID,ds,3 : unID=unID+1

				!!!HEIGHT OF RAILING
				HOTSPOT templra/2+LengthSize*hroL,0,ds,unID,hr,1 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT templra/2+LengthSize*hroL,0,-1,unID,hr,3 : unID=unID+1

				!!!POST BASE OVERHANG
				HOTSPOT LengthSize*hroL,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
				HOTSPOT LengthSize*hroL,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT LengthSize*hroL,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				HOTSPOT lra-LengthSize*hroR,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
				HOTSPOT lra-LengthSize*hroR,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT lra-LengthSize*hroR,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				!!!OVERHANG OF RAILING
				HOTSPOT 0,0,ds+hr,unID,hroL,1+128 : unID=unID+1
				HOTSPOT temphroL,0,ds+hr,unID,hroL,2 : unID=unID+1
				HOTSPOT RefPoint1,0,ds+hr,unID,hroL,3 : unID=unID+1

				HOTSPOT lra,0,ds+hr,unID,hroR,1+128 : unID=unID+1
				HOTSPOT lra+temphroR,0,ds+hr,unID,hroR,2 : unID=unID+1
				HOTSPOT lra+RefPoint2,0,ds+hr,unID,hroR,3 : unID=unID+1

		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)<EPS and ptyp_m<>1 THEN
			IF ab<360 THEN

					IF ptyp_m=3 THEN
					!!!ROTATE
						HOTSPOT 0,rb,ds,unID,ab,6 : unID=unID+1
						HOTSPOT 0,0,ds,unID,ab,4+256 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ab,5 : unID=unID+1

						HOTSPOT 0,rb,ds+hr,unID,ab,6 : unID=unID+1
						HOTSPOT 0,0,ds+hr,unID,ab,4+256 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr,unID,ab,5 : unID=unID+1

					!!!FIX HOTSPOT AT FLOOR
						HOTSPOT 0,0,0,unID : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID : unID=unID+1
					ENDIF

					!!!HEIGHT OF RAILING
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,hr,1+128 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr,unID,hr,2 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

					!!! DISTANCE FROM FLOOR
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),0,unID,ds,1 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds,unID,ds,2 : unID=unID+1
						HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

					!!!POST BASE OVERHANG
						HOTSPOT 0,0,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
						HOTSPOT 0,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT 0,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

						HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					IF ptyp_m=2 THEN

					!!!HEIGHT OF RAILING
						HOTSPOT 0,0,ds,unID,hr,1+128 : unID=unID+1
						HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
						HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

						HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,hr,1+128 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr,unID,hr,2 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

					!!! DISTANCE FROM FLOOR
						HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
						HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
						HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

						HOTSPOT rb*sin(ab),rb-rb*cos(ab),0,unID,ds,1 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds,unID,ds,2 : unID=unID+1
						HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
					ENDIF


			ELSE	!!!ANGLE=360


				!!!HOTSPOTS FOR GRAPHICAL EDITING
				!!!HEIGHT

					HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
					HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

					HOTSPOT rb,rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT rb,rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT rb,rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT 0,2*rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT 0,2*rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT 0,2*rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT -rb,rb,ds,unID,hr,1 : unID=unID+1
					HOTSPOT -rb,rb,ds+hr,unID,hr,2 : unID=unID+1
					HOTSPOT -rb,rb,-1,unID,hr,3 : unID=unID+1

					HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

					HOTSPOT rb,rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT rb,rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT rb,rb,-1,unID,ds,3 : unID=unID+1

					HOTSPOT 0,2*rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT 0,2*rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT 0,2*rb,-1,unID,ds,3 : unID=unID+1

					HOTSPOT -rb,rb,0,unID,ds,1 : unID=unID+1
					HOTSPOT -rb,rb,ds,unID,ds,2 : unID=unID+1
					HOTSPOT -rb,rb,-1,unID,ds,3 : unID=unID+1

					!!!POST BASE OVERHANG
					HOTSPOT 0,0,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT 0,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT 0,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					HOTSPOT rb,rb,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT rb,rb,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT rb,rb,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					HOTSPOT 0,2*rb,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT 0,2*rb,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT 0,2*rb,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

					HOTSPOT -rb,rb,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
					HOTSPOT -rb,rb,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
					HOTSPOT -rb,rb,1,unID,gs_PostBaseOverhang,3 : unID=unID+1
			ENDIF
		ENDIF

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** STRAIGHT INCLINED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m=1 THEN	!!ptyp=Straight
			IF rom_m=1 THEN					!!Vertical Orientation

					IF rmm_m=2 THEN			!!Perpendicular Measurement
						ftw=ftw/cos(ani)
						fbw=fbw/cos(ani)
					ENDIF

					IF rmm_m=1 THEN			!!Vertical Measurement
						temp_ds=ds
						temp_hr=hr
					ELSE					!!Perpendicular Measurement
						temp_ds=dsPerp
						temp_hr=hrPerp
					ENDIF

						!!!	HOTSPOTS FOR GRAPHICAL EDITING
						!!! LENGTH OF RAILING

							HOTSPOT 0,	0,	temp_ds+temp_hr,				unID, lrah, 1+256 : unID=unID+1
							HOTSPOT -1, 0,	temp_ds+temp_hr-tan(ani),		unID, lrah, 3 : unID=unID+1
							HOTSPOT lra,0, 	temp_ds+temp_hr+lra*tan(ani),	unID, lrah, 2 : unID=unID+1

						IF LengthSize = 0 THEN
							HOTSPOT 0,	0,	0,				unID, lrah, 1+256 : unID=unID+1
							HOTSPOT -1, 0,	0-tan(ani),		unID, lrah, 3 : unID=unID+1
							HOTSPOT lra,0, 	0+lra*tan(ani),	unID, lrah, 2 : unID=unID+1

							HOTSPOT 0,	0,	temp_ds,				unID, lrah, 1+256 : unID=unID+1
							HOTSPOT -1, 0,	temp_ds-tan(ani),		unID, lrah, 3 : unID=unID+1
							HOTSPOT lra,0, 	temp_ds+lra*tan(ani),	unID, lrah, 2 : unID=unID+1
						ENDIF

						IF rmm_m=1 THEN
							!!! DISTANCE FROM FLOOR
							HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani),	unID, ds, 1+128 : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, ds, 3 : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0, 	(templra/2+LengthSize*hroL)*tan(ani)+temp_ds,	unID, ds, 2 : unID=unID+1

							!!! HEIGHT of RAILING
							HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani),	unID, hr, 1+128 : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, hr, 3 : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0, 	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)+temp_hr,unID, hr, 2 : unID=unID+1

							!!!POST BASE OVERHANG
							HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani),unID,gs_PostBaseOverhang,1 : unID=unID+1
							HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
							HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1

							HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani),unID,gs_PostBaseOverhang,1 : unID=unID+1
							HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
							HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1

						ELSE
							!!! DISTANCE FROM FLOOR
							HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani),	unID, dsPerp, 1+128, ds : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0,	(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, dsPerp, 3, ds : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0, 	(templra/2+LengthSize*hroL)*tan(ani)+temp_ds,	unID, dsPerp, 2, ds : unID=unID+1

							!!! HEIGHT of RAILING
							HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani),	unID, hrPerp, 1+128, hr : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0,	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)-1,	unID, hrPerp, 3, hr  : unID=unID+1
							HOTSPOT templra/2+LengthSize*hroL,	0, 	temp_ds+(templra/2+LengthSize*hroL)*tan(ani)+temp_hr,unID, hrPerp, 2, hr  : unID=unID+1

							!!!POST BASE OVERHANG
							HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani),unID,gs_PostBaseOverhangPerp,1,gs_PostBaseOverhang : unID=unID+1
							HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)-temp_PostBaseOverhang,unID,gs_PostBaseOverhangPerp,2,gs_PostBaseOverhang : unID=unID+1
							HOTSPOT LengthSize*hroL,0,LengthSize*hroL*tan(ani)+1,unID,gs_PostBaseOverhangPerp,3,gs_PostBaseOverhang : unID=unID+1

							HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani),unID,gs_PostBaseOverhangPerp,1,gs_PostBaseOverhang : unID=unID+1
							HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)-temp_PostBaseOverhang,unID,gs_PostBaseOverhangPerp,2,gs_PostBaseOverhang : unID=unID+1
							HOTSPOT lra-LengthSize*hroR,0,LengthSize*hroL*tan(ani)+templra*tan(ani)+1,unID,gs_PostBaseOverhangPerp,3,gs_PostBaseOverhang : unID=unID+1

						ENDIF

							!!!OVERHANG OF RAILING
							HOTSPOT 0,0,	temp_ds+temp_hr,				unID,hroLDiagonal,1+128,hroL : unID=unID+1
							HOTSPOT temphroL,0,	temp_ds+temp_hr+temphroL*tan(ani),	unID,hroLDiagonal,2	,hroL	: unID=unID+1
							HOTSPOT RefPoint1,0,temp_ds+temp_hr-InclRefPoint,		unID,hroLDiagonal,3	,hroL	: unID=unID+1

							HOTSPOT lra,0,		temp_ds+temp_hr+lra*tan(ani),				unID,hroRDiagonal,1+128,hroR : unID=unID+1
							HOTSPOT lra+temphroR,0,	temp_ds+temp_hr+lra*tan(ani)+temphroR*tan(ani),	unID,hroRDiagonal,2	,hroR	: unID=unID+1
							HOTSPOT lra+RefPoint2,0,	temp_ds+temp_hr+lra*tan(ani)+InclRefPoint,		unID,hroRDiagonal,3	,hroR	: unID=unID+1


			ELSE			!Perpendicular Orientation
							!Measurement is always perpendicular to inclination in this case

				ROTy -ani

						!!! LENGTH OF RAILING

							HOTSPOT 0,0,ds+hr,unID,lrah,1+256 : unID=unID+1
							HOTSPOT lrah,0,ds+hr,unID,lrah,2 	: unID=unID+1
							HOTSPOT -1,0,ds+hr,unID,lrah,3 	: unID=unID+1

						IF LengthSize = 0 THEN
							HOTSPOT 0,0,0,unID,lrah,1+256 : unID=unID+1
							HOTSPOT lrah,0,0,unID,lrah,2 	: unID=unID+1
							HOTSPOT -1,0,0,unID,lrah,3 	: unID=unID+1

							HOTSPOT 0,0,ds,unID,lrah,1+256 : unID=unID+1
							HOTSPOT lrah,0,ds,unID,lrah,2 	: unID=unID+1
							HOTSPOT -1,0,ds,unID,lrah,3 	: unID=unID+1
						ENDIF


						!!!DISTANCE FROM FLOOR
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,0,unID,ds,1+128 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,ds,unID,ds,2 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,-1,unID,ds,3 : unID=unID+1

						!!! HEIGHT of RAILING
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,ds,unID,hr,1 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,ds+hr,unID,hr,2 : unID=unID+1
						HOTSPOT templrah/2+LengthSize*hroLDiagonal,0,-1,unID,hr,3 : unID=unID+1

						!!!POST BASE OVERHANG
						HOTSPOT LengthSize*hroLDiagonal,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

						HOTSPOT LengthSize*hroLDiagonal+templrah,0,0,unID,gs_PostBaseOverhang,1 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal+templrah,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
						HOTSPOT LengthSize*hroLDiagonal+templrah,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

						!!!OVERHANG OF RAILING
						HOTSPOT lrah,0,ds+hr,unID,hroRDiagonal,1+128 , hroR : unID=unID+1
						HOTSPOT lrah+temphroRDiag,0,ds+hr,unID,hroRDiagonal,2 , hroR 	: unID=unID+1
						HOTSPOT lrah+RefPoint2,0,ds+hr,unID,hroRDiagonal,3 , hroR 	: unID=unID+1

						HOTSPOT 0,0,ds+hr,unID,hroLDiagonal,1+128 , hroL: unID=unID+1
						HOTSPOT temphroLDiag,0,ds+hr,unID,hroLDiagonal,2, hroL 	: unID=unID+1
						HOTSPOT RefPoint1,0,ds+hr,unID,hroLDiagonal,3, hroL 	: unID=unID+1

				DEL 1

			ENDIF

		ENDIF

!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** INCLINED AND CURVED *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!

		IF ABS(ani)>EPS and ptyp_m<>1 THEN

			!!!HOTSPOTS FOR GRAPHICAL EDITING

			IF ptyp_m=3 THEN
				!!!ROTATE
				HOTSPOT 0,rb,ds+lra*sin(ani),unID,ab,6 : unID=unID+1
				HOTSPOT 0,0,ds+lra*sin(ani),unID,ab,4+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ab,5 : unID=unID+1

				HOTSPOT 0,rb,ds+hr+lra*sin(ani),unID,ab,6 : unID=unID+1
				HOTSPOT 0,0,ds+hr+lra*sin(ani),unID,ab,4+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr+lra*sin(ani),unID,ab,5 : unID=unID+1
			ENDIF

				!!!HEIGHT OF RAILING
				HOTSPOT 0,0,ds,unID,hr,1 : unID=unID+1
				HOTSPOT 0,0,ds+hr,unID,hr,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,hr,3 : unID=unID+1

				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,hr,1 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+hr+(lra/2)*sin(ani),unID,hr,2 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,hr,3 : unID=unID+1

				!!!DISTANCE FROM FLOOR
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani),unID,ds,1 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),ds+(lra/2)*sin(ani),unID,ds,2 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),-1,unID,ds,3 : unID=unID+1

				HOTSPOT 0,0,0,unID,ds,1 : unID=unID+1
				HOTSPOT 0,0,ds,unID,ds,2 : unID=unID+1
				HOTSPOT 0,0,-1,unID,ds,3 : unID=unID+1

				!!!FIX HOTSPOT AT FLOOR
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),0+lra*sin(ani), unID : unID=unID+1

			IF ptyp_m=2 THEN
				!!!HEIGHT OF RAILING
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,hr,1 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+hr+lra*sin(ani),unID,hr,2 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,hr,3 : unID=unID+1

				!!!DISTANCE FROM FLOOR
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani),unID,ds,1+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),ds+lra*sin(ani),unID,ds,2 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),-1,unID,ds,3 : unID=unID+1
			ENDIF

				!!!POST BASE OVERHANG
				HOTSPOT 0,0,0,unID,gs_PostBaseOverhang,1+128 : unID=unID+1
				HOTSPOT 0,0,-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT 0,0,1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani),unID,gs_PostBaseOverhang,1+128 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT rb*sin(ab/2),rb-rb*cos(ab/2),(lra/2)*sin(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1

				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani),unID,gs_PostBaseOverhang,1+128 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani)-gs_PostBaseOverhang,unID,gs_PostBaseOverhang,2 : unID=unID+1
				HOTSPOT rb*sin(ab),rb-rb*cos(ab),lra*sin(ani)+1,unID,gs_PostBaseOverhang,3 : unID=unID+1
		ENDIF

	ENDIF
end
