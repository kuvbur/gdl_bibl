
templra = max(templra, EPS)

PEN gs_cont_pen
FILL gs_fill_type

IF iHandRailStyle = 1 THEN		!!!WIDTH OF HANDRAIL
	railWidth = handRailWidth
ELSE
	railWidth = handRailDiameter
ENDIF

if NOT(bEnable2DPosts) then
	hroL		= 0
	hroR		= 0

	alphahroL	= 0
	alphahroR	= 0
endif

IF LengthSize = 0 THEN			!!!OVERHANG OF HANDRAIL
	temphroL = -hroL
	temphroR = hroR
	RefPoint1 = 1
	RefPoint2 = -1
ELSE
	temphroL = hroL
	temphroR = -hroR
	RefPoint1 = -1
	RefPoint2 = 1
ENDIF

unID=1
hLineID = 1000
hArcID = 2000

! ==============================================================================
! STRAIGHT
! ==============================================================================

IF ptyp_m=1 and rom_m=1 THEN		! Not Inclined
	ADD2 XStartPoslra,0

		XPosL			= (railWidth / 2) / tan(gs_AngleL)
		XPosR			= (railWidth / 2) / tan(gs_AngleR)

		breakLineXPos	= (railWidth) * tan(breakLineAngle)

		drawindex 30
		!!FRAME
		if bBreakRailLine then
			if bShowRailBelowBrL then
				line_type railLineType
				poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					-(hroL) + XPosL, railWidth / 2, 1,
					-(hroL) - XPosL, -railWidth / 2, 1,
					-(hroL) + breakLineDist, -railWidth / 2, 1,
					-(hroL) + breakLineDist - breakLineXPos, railWidth / 2, 1
			endif
			if bShowRailAboveBrL then
				line_type dash_line
				poly2_b 4, 1 + 2 * gs_FillAbove + 4 + 64, gs_fill_pen, gs_back_pen,
					-(hroL) + breakLineDist - breakLineXPos + breakLineWidth, railWidth / 2, 1,
					-(hroL) + breakLineDist + breakLineWidth, -railWidth / 2, 1,
					 templra + (hroR) + XPosR, -railWidth / 2, 1,
					 templra + (hroR) - XPosR, railWidth / 2, 1
			endif
		else
			line_type railLineType * NOT(bRailAboveBreakLine) + (dash_line) * bRailAboveBreakLine
			POLY2_b 4, 1 + 2 * (NOT(bRailAboveBreakLine) + bRailAboveBreakLine * gs_FillAbove) + 4 + 64, gs_fill_pen, gs_back_pen,
				-(hroL) + XPosL, railWidth / 2, 1,
				-(hroL) - XPosL, -railWidth / 2, 1,
				 templra + hroR + XPosR, -railWidth / 2, 1,
				 templra + hroR - XPosR, railWidth / 2, 1
		endif

		drawindex 50
		!!AXIS
		IF sa=1 THEN
			pen gs_RailAxisPen
			line_type al

			if bBreakRailLine then
				if bShowRailBelowBrL then
					LINE2 -(hroL),0,-(hroL) + breakLineDist - breakLineXPos / 2,0
					HOTLINE2 -(hroL),0,-(hroL) + breakLineDist - breakLineXPos / 2,0, hLineID
				endif
				if bShowRailAboveBrL then
					LINE2 -(hroL) + breakLineDist + breakLineWidth - breakLineXPos / 2,0,templra+(hroR),0
					HOTLINE2 -(hroL) + breakLineDist + breakLineWidth - breakLineXPos / 2,0,templra+(hroR),0, hLineID
				endif
			else
				LINE2 -(hroL),0,templra+(hroR),0
				HOTLINE2 -(hroL),0,templra+(hroR),0, hLineID
			endif

			pen gs_cont_pen
			line_type 1
		ENDIF
		hLineID = hLineID + 1

	DEL 1

	!!SELECTION HOTSPOTS
	IF cfs=0 THEN

		!!!HOTSPOTS FOR GRAPHICAL EDITING
		!!LENGTH
		HOTSPOT2 0,0,unID,lra,1+256 : unID=unID+1
		HOTSPOT2 lra,0,unID,lra,2 : unID=unID+1
		HOTSPOT2 -1,0,unID,lra,3 : unID=unID+1

		if bEnable2DPosts then
			!!!OVERHANG OF HANDRAIL
			HOTSPOT2 0,0,unID,hroL,1+128 : unID=unID+1
			HOTSPOT2 temphroL,0,unID,hroL,2 : unID=unID+1
			HOTSPOT2 RefPoint1,0,unID,hroL,3 : unID=unID+1

			HOTSPOT2 lra,0,unID,hroR,1+128 : unID=unID+1
			HOTSPOT2 lra+temphroR,0,unID,hroR,2 : unID=unID+1
			HOTSPOT2 lra+RefPoint2,0,unID,hroR,3 : unID=unID+1
		else
			HOTSPOT2 lra/2,0,UniD : unID=unID+1
		endif

		!!! ROTATION
		fthRadiusL=(railWidth/2)/SIN(gs_AngleL)
		fthRadiusR=(railWidth/2)/SIN(gs_AngleR)

		ADD2 XStartPoslra,0
			HOTSPOT2 -hroL, 0, unID, gs_AngleL, 6: unID=unID+1
			HOTSPOT2 fthRadiusL, 0, unID, gs_AngleL, 4+128: unID=unID+1
			HOTSPOT2 fthRadiusL*COS(gs_AngleL)-(hroL), fthRadiusL*SIN(gs_AngleL), unID, gs_AngleL, 5: unID=unID+1

			HOTSPOT2 -hroL, 0, unID, gs_AngleL, 6: unID=unID+1
			HOTSPOT2 -fthRadiusL-1, 0, unID, gs_AngleL, 4+128: unID=unID+1
			HOTSPOT2 -fthRadiusL*COS(gs_AngleL)-(hroL), -fthRadiusL*SIN(gs_AngleL), unID, gs_AngleL, 5: unID=unID+1
		DEL 1

		ADD2 XStartPoslra,0
			HOTSPOT2 templra+hroR, 0, unID, gs_AngleR, 6+512: unID=unID+1
			HOTSPOT2 templra-fthRadiusR, 0, unID, gs_AngleR, 4+128: unID=unID+1
			HOTSPOT2 templra-fthRadiusR*COS(gs_AngleR)+(hroR), fthRadiusR*SIN(gs_AngleR), unID, gs_AngleR, 5: unID=unID+1

			HOTSPOT2 templra+hroR, 0, unID, gs_AngleR, 6+512: unID=unID+1
			HOTSPOT2 templra+fthRadiusR+1, 0, unID, gs_AngleR, 4+128: unID=unID+1
			HOTSPOT2 templra+fthRadiusR*COS(gs_AngleR)+(hroR), -fthRadiusR*SIN(gs_AngleR), unID, gs_AngleR, 5: unID=unID+1
		DEL 1

	ENDIF

	!!!POSTS
	ADD2 XStartPoslra, 0
	FOR i = 1 to np
		if postPos[i] >= 0 and postPos[i] <= (templra + hroL + hroR) then
			if bBreakRailLine then
				if postPos[i] < breakLineDist and bShowRailBelowBrL then
					if NOT(cfs) then
						if i > 1 and i < np then HOTSPOT2 0, 0, UniD : unID=unID+1
					endif
					if drawThePost then gosub "posts2D"
				endif
				if postPos[i] > (breakLineDist + breakLineWidth) and bShowRailAboveBrL then
					if NOT(cfs) then
						if i > 1 and i < np then HOTSPOT2 0, 0, UniD : unID=unID+1
					endif
					if drawThePost then gosub "posts2D"
				endif
			else
				if NOT(cfs) then
					if i > 1 and i < np then HOTSPOT2 0, 0, UniD : unID=unID+1
				endif
				if drawThePost then gosub "posts2D"
			endif
		endif

		ADD2 distpost, 0
	NEXT i
	DEL np
	del 1
ENDIF

IF ptyp_m=1 and rom_m<>1 THEN				! Inclined

	ADD2 XStartPoslra,0

		drawindex 30
		!!FRAME
		IF ani >=0 THEN
			StartPoint=-(ds+hr)*sin(ani)-hroLDiagonal*cos(ani)
			EndPoint=templra
			IF ABS(hroL)<EPS THEN LINE2 templra-(ds+hr)*sin(ani),railWidth/2,templra-(ds+hr)*sin(ani),-railWidth/2
		ELSE
			StartPoint=0
			EndPoint=templra-(ds+hr)*sin(ani)+ hroRDiagonal*cos(ani)
			IF ABS(hroR)<EPS THEN LINE2 -(ds+hr)*sin(ani),railWidth/2,-(ds+hr)*sin(ani),-railWidth/2
		ENDIF

		XPosL			= (railWidth / 2) / tan(gs_AngleL)
		XPosR			= (railWidth / 2) / tan(gs_AngleR)

		breakLineXPos	= (railWidth) * tan(breakLineAngle)

		if bBreakRailLine then
			if bShowRailBelowBrL then
				line_type railLineType
				poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					StartPoint, railWidth / 2, 1,
					StartPoint, -railWidth / 2, 1,
					StartPoint + breakLineDist, -railWidth / 2, 1,
					StartPoint + breakLineDist - breakLineXPos, railWidth / 2, 1
			endif
			if bShowRailAboveBrL then
				line_type dash_line
				poly2_b 4, 1 + 2 * gs_FillAbove + 4 + 64, gs_fill_pen, gs_back_pen,
					StartPoint + breakLineDist - breakLineXPos + breakLineWidth, railWidth / 2, 1,
					StartPoint + breakLineDist + breakLineWidth, -railWidth / 2, 1,
					 EndPoint, -railWidth / 2, 1,
					 EndPoint, railWidth / 2, 1
			endif
		else
			line_type railLineType * NOT(bRailAboveBreakLine) + dash_line * bRailAboveBreakLine
			POLY2_b 4, 1 + 2 * (NOT(bRailAboveBreakLine) + bRailAboveBreakLine * gs_FillAbove) + 4 + 64, gs_fill_pen, gs_back_pen,
				StartPoint, railWidth / 2, 1,
				StartPoint, -railWidth / 2, 1,
				EndPoint, -railWidth / 2, 1,
				EndPoint, railWidth / 2, 1
		endif

		drawindex 50
		!!AXIS
		IF sa=1 THEN
			pen gs_RailAxisPen
			line_type al

			IF ani >=0 THEN
				if bBreakRailLine then
					if bShowRailBelowBrL then
						LINE2 -(ds+hr)*sin(ani)-hroLDiagonal*cos(ani),0,-(ds+hr)*sin(ani)-hroLDiagonal*cos(ani) + breakLineDist - breakLineXPos / 2,0
						HOTLINE2 -(ds+hr)*sin(ani)-hroLDiagonal*cos(ani),0,-(ds+hr)*sin(ani)-hroLDiagonal*cos(ani) + breakLineDist - breakLineXPos / 2,0, hLineID
					endif
					if bShowRailAboveBrL then
						LINE2 -(ds+hr)*sin(ani)-hroLDiagonal*cos(ani) + breakLineDist + breakLineWidth - breakLineXPos / 2,0, templra,0
						HOTLINE2 -(ds+hr)*sin(ani)-hroLDiagonal*cos(ani) + breakLineDist + breakLineWidth - breakLineXPos / 2,0, templra,0, hLineID
					endif
				else
					LINE2 -(ds+hr)*sin(ani)-hroLDiagonal*cos(ani),0,templra,0
					HOTLINE2 -(ds+hr)*sin(ani)-hroLDiagonal*cos(ani),0,templra,0, hLineID
				endif
			ELSE
				LINE2 0,0,templra-(ds+hr)*sin(ani)+ hroRDiagonal*cos(ani),0
			ENDIF
			hLineID = hLineID + 1
			HOTLINE2 0,0,templra,0, hLineID

			pen gs_cont_pen
			line_type 1
		else
			hLineID = hLineID + 2
		ENDIF

	DEL 1

	IF cfs=0 THEN

		!LENGTH
		HOTSPOT2 0,0,unID,lra,1+256 : unID=unID+1
		HOTSPOT2 lra,0,unID,lra,2 : unID=unID+1
		HOTSPOT2 -1,0,unID,lra,3 : unID=unID+1

		ADD2 XStartPoslra,0
			HOTSPOT2 EndPoint,railWidth/2, UniD : unID=unID+1
			HOTSPOT2 EndPoint,-railWidth/2, UniD : unID=unID+1

			HOTSPOT2 StartPoint,railWidth/2, UniD : unID=unID+1
			HOTSPOT2 StartPoint,-railWidth/2, UniD : unID=unID+1

			IF ani >=0 THEN
				HOTSPOT2 StartPoint,0, UniD : unID=unID+1
				IF ABS(hroL)<EPS THEN HOTSPOT2 templra-(ds+hr)*sin(ani),0, UniD : unID=unID+1
			ELSE
				HOTSPOT2 EndPoint,0, UniD : unID=unID+1
				IF ABS(hroR)<EPS THEN HOTSPOT2 -(ds+hr)*sin(ani),0, UniD : unID=unID+1
			ENDIF
		DEL 1

	ENDIF

	!!!POSTS
	ADD2 XStartPoslra + StartPoint + hroLDiagonal * cos(ani), 0
	FOR i = 1 to np
		if postPos[i] >= 0 and postPos[i] <= (templrah + hroLDiagonal + hroRDiagonal) then
			if bBreakRailLine then
				if postPos[i] < breakLineDist and bShowRailBelowBrL then
					if NOT(cfs) then
						HOTSPOT2 0, 0, UniD : unID=unID+1
					endif

					if drawThePost then gosub "posts2D"
				endif
				if postPos[i] > (breakLineDist + breakLineWidth) and bShowRailAboveBrL then
					if NOT(cfs) then
						HOTSPOT2 0, 0, UniD : unID=unID+1
					endif

					if drawThePost then gosub "posts2D"
				endif
			else
				if NOT(cfs) then
					HOTSPOT2 0, 0, UniD : unID=unID+1
				endif

				if drawThePost then gosub "posts2D"
			endif
		endif

		ADD2 distpost * cos(ani), 0
	NEXT i
	DEL np
	del 1

ENDIF

! ==============================================================================
! CURVED
! ==============================================================================

IF ptyp_m<>1 THEN

	!!!POSTS
	add2 0, rb
	for i = 1 to np
		add2 0, -rb
			if postPos[i] >= 0 and postPos[i] <= (ab + alphahroL + alphahroR) then
				if bBreakRailLine then
					if postPos[i] < breakLineDistAngle and bShowRailBelowBrL then
						if not(cfs) then
							if i > 1 and i < np then hotspot2 0, 0, UniD : unID = unID+1
						endif

						if drawThePost then gosub "posts2D"
					endif
					if postPos[i] > (breakLineDistAngle + breakLineWidthAngle) and bShowRailAboveBrL then
						if not(cfs) then
							if i > 1 and i < np then hotspot2 0, 0, UniD : unID = unID+1
						endif

						if drawThePost then gosub "posts2D"
					endif
				else
					if not(cfs) then
						if i > 1 and i < np then hotspot2 0, 0, UniD : unID = unID+1
					endif

					if drawThePost then gosub "posts2D"
				endif
			endif
		del 1

		rot2 anglepost
	next i
	del np+1

	IF (ab + alphahroL + alphahroR) < 360 THEN
		drawindex 30
		!!FRAME
		if bBreakRailLine then
			if bShowRailBelowBrL then
				line_type railLineType
				POLY2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
					(rb-(railWidth/2))*sin(-alphahroL),rb-(rb-(railWidth/2))*cos(-alphahroL),1,
					(rb+(railWidth/2))*sin(-alphahroL),rb-(rb+(railWidth/2))*cos(-alphahroL),1,
					cos(-alphahroL),sin(-alphahroL),800,
					(rb+(railWidth/2))*sin(breakLineDistAngle-alphahroL),rb-(rb+(railWidth/2))*cos(breakLineDistAngle-alphahroL),1001,
					(rb-(railWidth/2))*sin(breakLineDistAngle-alphahroL),rb-(rb-(railWidth/2))*cos(breakLineDistAngle-alphahroL),1,
					-cos(breakLineDistAngle-alphahroL),-sin(breakLineDistAngle-alphahroL),800,
					(rb-(railWidth/2))*sin(-alphahroL),rb-(rb-(railWidth/2))*cos(-alphahroL),1000
			endif
			if bShowRailAboveBrL then
				line_type dash_line
				POLY2_b 7, 1 + 2 * gs_FillAbove + 4 + 64, gs_fill_pen, gs_back_pen,
					(rb-(railWidth/2))*sin(breakLineDistAngle - alphahroL + breakLineWidthAngle),rb-(rb-(railWidth/2))*cos(breakLineDistAngle - alphahroL + breakLineWidthAngle),1,
					(rb+(railWidth/2))*sin(breakLineDistAngle - alphahroL + breakLineWidthAngle),rb-(rb+(railWidth/2))*cos(breakLineDistAngle - alphahroL + breakLineWidthAngle),1,
					cos(breakLineDistAngle - alphahroL + breakLineWidthAngle),sin(breakLineDistAngle - alphahroL + breakLineWidthAngle),800,
					(rb+(railWidth/2))*sin(ab + alphahroL),rb-(rb+(railWidth/2))*cos(ab + alphahroL),1001,
					(rb-(railWidth/2))*sin(ab + alphahroL),rb-(rb-(railWidth/2))*cos(ab + alphahroL),1,
					-cos(ab + alphahroL),-sin(ab + alphahroL),800,
					(rb-(railWidth/2))*sin(breakLineDistAngle - alphahroL + breakLineWidthAngle),rb-(rb-(railWidth/2))*cos(breakLineDistAngle - alphahroL + breakLineWidthAngle),1000
			endif
		else
			line_type railLineType * NOT(bRailAboveBreakLine) + dash_line * bRailAboveBreakLine
			POLY2_b 7, 1 + 2 * (NOT(bRailAboveBreakLine) + bRailAboveBreakLine * gs_FillAbove) + 4 + 64, gs_fill_pen, gs_back_pen,
				(rb-(railWidth/2))*sin(-alphahroL),rb-(rb-(railWidth/2))*cos(-alphahroL),1,
				(rb+(railWidth/2))*sin(-alphahroL),rb-(rb+(railWidth/2))*cos(-alphahroL),1,
				cos(-alphahroL),sin(-alphahroL),800,
				(rb+(railWidth/2))*sin(ab+alphahroR),rb-(rb+(railWidth/2))*cos(ab+alphahroR),1001,
				(rb-(railWidth/2))*sin(ab+alphahroR),rb-(rb-(railWidth/2))*cos(ab+alphahroR),1,
				-cos(ab+alphahroR),-sin(ab+alphahroR),800,
				(rb-(railWidth/2))*sin(-alphahroL),rb-(rb-(railWidth/2))*cos(-alphahroL),1000
		endif

		drawindex 50
		!!AXIS
		IF sa=1 THEN
			pen gs_RailAxisPen
			line_type al

			if bBreakRailLine then
				if bShowRailBelowBrL then
					arc2 0,rb,rb,-90-alphahroL,-90-alphahroL + breakLineDistAngle
					hotarc2 0,rb,rb,-90-alphahroL,-90-alphahroL + breakLineDistAngle, hArcID
				endif
				if bShowRailAboveBrL then
					arc2 0,rb,rb,-90-alphahroL + breakLineDistAngle + breakLineWidthAngle,ab-90+alphahroR
					hotarc2 0,rb,rb,-90-alphahroL + breakLineDistAngle + breakLineWidthAngle,ab-90+alphahroR, hArcID
				endif
			else
				arc2 0,rb,rb,-90-alphahroL,ab-90+alphahroR
				hotarc2 0,rb,rb,-90-alphahroL,ab-90+alphahroR, hArcID
			endif

			pen gs_cont_pen
			line_type 1
		ENDIF
		hArcID = hArcID + 1

		IF cfs=0 THEN

			!!!SELECTION HOTSPOTS
			HOTSPOT2 0,rb, unID : unID=unID+1

			!!!HOTSPOTS FOR GRAPHICAL EDITING
			HOTSPOT2 0,0,unID,rb,1+256 : unID=unID+1
			HOTSPOT2 0,rb,unID,rb,2 : unID=unID+1
			HOTSPOT2 0,-1,unID,rb,3 : unID=unID+1

			HOTSPOT2 rb*sin(ab),rb-rb*cos(ab),unID : unID=unID+1

			HOTSPOT2 rb*sin(ab/2),rb-rb*cos(ab/2),unID: unID=unID+1

			HOTSPOT2 (rb-(railWidth/2))*sin(-alphahroL),rb-(rb-(railWidth/2))*cos(-alphahroL),unID : unID=unID+1
			HOTSPOT2 (rb+(railWidth/2))*sin(-alphahroL),rb-(rb+(railWidth/2))*cos(-alphahroL),unID : unID=unID+1
			HOTSPOT2 (rb-(railWidth/2))*sin(ab+alphahroR),rb-(rb-(railWidth/2))*cos(ab+alphahroR),unID : unID=unID+1
			HOTSPOT2 (rb+(railWidth/2))*sin(ab+alphahroR),rb-(rb+(railWidth/2))*cos(ab+alphahroR),unID : unID=unID+1
			HOTSPOT2 (rb)*sin(ab+alphahroR),rb-(rb)*cos(ab+alphahroR),unID : unID=unID+1
			HOTSPOT2 (rb)*sin(-alphahroL),rb-(rb)*cos(alphahroL),unID : unID=unID+1

			IF ptyp_m=3 THEN
				!!!HOTSPOTS FOR GRAPHICAL EDITING
				HOTSPOT2 0,rb,unID,ab,6: unID=unID+1
				HOTSPOT2 0,0,unID,ab,4+256: unID=unID+1
				HOTSPOT2 rb*sin(ab),rb-rb*cos(ab),unID,ab,5: unID=unID+1
			ENDIF

		ENDIF

	ELSE

		!!FRAME
		POLY2_b 12, 1+2+64,gs_fill_pen,gs_back_pen,
			0,-railWidth/2,1,
			1,0,800,
			0,2*rb+railWidth/2,1001,
			-1,0,800,
			0,-railWidth/2,1001,
			0,-railWidth/2,-1,

			0,railWidth/2,1,
			1,0,800,
			0,2*rb-railWidth/2,1001,
			-1,0,800,
			0,railWidth/2,1001,
			0,railWidth/2,-1


		!!FRAME END LINE
		if abs(ani) >= EPS then
			line_type railLineType * not(bRailAboveBreakLine) + dash_line * bRailAboveBreakLine
			line2		(rb + (railWidth/2)) * sin(ab + alphahroL), rb - (rb + (railWidth/2)) * cos(ab + alphahroL),
						(rb - (railWidth/2)) * sin(ab + alphahroL), rb - (rb - (railWidth/2)) * cos(ab + alphahroL)
		ENDIF

		!!AXIS
		IF sa=1 THEN
			pen gs_RailAxisPen
			line_type al

			arc2 0,rb,rb,-90,ab-90
			hotarc2 0,rb,rb,-90,ab-90, hArcID

			pen gs_cont_pen
			line_type 1
		ENDIF
		hArcID = hArcID + 1

		!!!SELECTION HOTSPOTS
		IF cfs=0 THEN

			!!!HOTSPOTS FOR GRAPHICAL EDITING
			HOTSPOT2 0,0,unID,rb,1+256 : unID=unID+1
			HOTSPOT2 0,rb,unID,rb,2 : unID=unID+1
			HOTSPOT2 0,-1,unID,rb,3 : unID=unID+1

			HOTSPOT2 0,-railWidth/2,unID : unID=unID+1
			HOTSPOT2 0,railWidth/2,unID : unID=unID+1

			IF ptyp_m=3 THEN
				HOTSPOT2 (rb+(railWidth/2))*sin(ab/2),rb-(rb+(railWidth/2))*cos(ab/2),unID : unID=unID+1
				HOTSPOT2 (rb)*sin(ab/2),rb-(rb)*cos(ab/2),unID : unID=unID+1
				HOTSPOT2 (rb-(railWidth/2))*sin(ab/2),rb-(rb-(railWidth/2))*cos(ab/2),unID : unID=unID+1

				HOTSPOT2 (rb+(railWidth/2))*sin(ab/4),rb-(rb+(railWidth/2))*cos(ab/4),unID : unID=unID+1
				HOTSPOT2 (rb)*sin(ab/4),rb-(rb)*cos(ab/4),unID : unID=unID+1
				HOTSPOT2 (rb-(railWidth/2))*sin(ab/4),rb-(rb-(railWidth/2))*cos(ab/4),unID : unID=unID+1

				HOTSPOT2 (rb+(railWidth/2))*sin(3*ab/4),rb-(rb+(railWidth/2))*cos(3*ab/4),unID : unID=unID+1
				HOTSPOT2 (rb)*sin(3*ab/4),rb-(rb)*cos(3*ab/4),unID : unID=unID+1
				HOTSPOT2 (rb-(railWidth/2))*sin(3*ab/4),rb-(rb-(railWidth/2))*cos(3*ab/4),unID : unID=unID+1
			ENDIF

			IF ptyp_m=2 THEN
				HOTSPOT2 rb-railWidth/2,rb,unID : unID=unID+1
				HOTSPOT2 rb,rb,unID : unID=unID+1
				HOTSPOT2 rb+railWidth/2,rb,unID : unID=unID+1

				HOTSPOT2 0,2*rb-railWidth/2,unID : unID=unID+1
				HOTSPOT2 0,2*rb,unID : unID=unID+1
				HOTSPOT2 0,2*rb+railWidth/2,unID : unID=unID+1

				HOTSPOT2 -rb-railWidth/2,rb,unID : unID=unID+1
				HOTSPOT2 -rb,rb,unID : unID=unID+1
				HOTSPOT2 -rb+railWidth/2,rb,unID : unID=unID+1
			ENDIF

		ENDIF

	ENDIF

ENDIF

!================================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
!================================================================================

"posts2D":

	if bSimple2DPosts then
		if pt_m = 1 then
			add2 -pw/2, -pth/2
			rect2 0, 0, pw, pth
			del 1
		else
			circle2 0, 0, pd/2
		endif
	else
		pen postPen
		line_type postLineType
		drawindex 10 + bPostAboveHandRail * 30

		if iPostType2D = POST_CIRCULAR then
			poly2_b 2, 1 + 2 * (NOT(bRailAboveBreakLine) + bRailAboveBreakLine * gs_FillAbove) + 4 + 64, gs_fill_pen, gs_back_pen,
					0, 0, 901,
					postDia2D / 2, 360, 4001
		else
			poly2_b 4, 1 + 2 * (NOT(bRailAboveBreakLine) + bRailAboveBreakLine * gs_FillAbove) + 4 + 64, gs_fill_pen, gs_back_pen,
					-postWidth2D / 2, -postThk2D / 2, 1,
					postWidth2D / 2, -postThk2D / 2, 1,
					postWidth2D / 2, postThk2D / 2, 1,
					-postWidth2D / 2, postThk2D / 2, 1
		endif

		pen gs_cont_pen
		line_type 1
		drawindex 50
	endif

return

