
! ======================================================================
! UI Page IDs for Dinamic Page Handling
! ======================================================================

tabIdx = 1
TABID_ROOT			= -1

TABID_DIMENSION		= tabIdx	: tabIdx = tabIdx + 1
TABID_DIMENSION		= tabIdx	: tabIdx = tabIdx + 1
TABID_PLACEMENT		= tabIdx	: tabIdx = tabIdx + 1
TABID_CUTTING		= tabIdx	: tabIdx = tabIdx + 1
TABID_APPEARANCE	= tabIdx	: tabIdx = tabIdx + 1
TABID_PROPERTIES	= tabIdx	: tabIdx = tabIdx + 1

if iUIStructureType = STRUCTURE_BEAM then
	stDialogText =`Параметры балки`
	_bShowPlacementPage = 1
else
	stDialogText =`Параметры Колонны`
	_bShowPlacementPage = 0
endif

toolTipWidth		= `Ширина (мм)`
toolTipDepth		= `Глубина (мм)`
toolTipThickness 	= `Толщина (мм)`
toolTipBottomWidth	= `Ширина Внизу (мм)`
toolTipTopWidth		= `Ширина вверху (мм)`
toolTipWidthSect 	= `Ширина профиля (мм)`
toolTipDepthSect 	= `Высота Профиля (мм)`
toolTipWebThk		= `Толщина стенки (мм)`
toolTipRootRad		= `Радиус основания (мм)`
toolTipFlangeThk	= `Толщина фланца (мм)`
toolTipDepthFillets	= `Глубина между скругл. (мм)`
toolTipInnerDepth	= `Внутр. глуб. между скругл. (мм)`
toolTipSerialSize	= `Серийный Размер (мм)`
toolTipToeRad		= `Радиус подошвы (мм)`
toolTipWallThk		= `Толщина стенки (мм)`
toolTipDiameterSect	= `Диаметр в Сечении (мм)`
toolTipLip			= `Загибание (мм)`

! ==============================================================================
! Display Active UI pages
! ==============================================================================
ui_current_page gs_ui_current_page
UI_dialog stDialogText

ui_page TABID_DIMENSION, TABID_ROOT, `Размеры`, "uiTab_Dimension_flat(1)"
gosub "dimensionPage"

if _bShowPlacementPage then
	ui_page TABID_PLACEMENT, TABID_ROOT, `Размещение`, "uiTab_BeamPlacement_flat(1)"
	gosub "placementPage"
endif

ui_page TABID_CUTTING, TABID_ROOT, `Сечение`, "uiTab_BeamCutting_flat(1)"
gosub "cuttingPage"

ui_page TABID_APPEARANCE, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
listFieldAppID = TABID_APPEARANCE
gosub "appearancePage"

ui_page TABID_PROPERTIES, TABID_ROOT, `Свойства и Описания`, "uiTab_Descriptions_flat(1)"
listFieldPropID = TABID_PROPERTIES
gosub "propertiesPage"


! ======================================================================
end
! ======================================================================

"dimensionPage":	
! ------------------------------------------------------------------------------
! Dimensions
! ------------------------------------------------------------------------------
	x1 = 0
	x2 = 120
	x3 = 210
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 360
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1

	if iStandardType = STANDARD_EN then
		stUIProfile = `Размер (Стандарт EN)`
	else
		if iStandardType = STANDARD_BS then
			stUIProfile = `Размер (Станд. BS)`
		else
			stUIProfile = `Серийный Размер`
		endif
	endif
	
	ui_style 0, 1
	
	ui_outfield	stUIProfile ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_style 0,0
	ui_infield{2} "Profile", 	 x2,   yCurrent, 	x3 - x2, infieldHeight

	if iUIStructureType = STRUCTURE_BEAM then
		ui_outfield	`Длина Элемента`, x4, yCurrent+4, x5 - x4, outfieldHeight
		ui_infield	"A", x6, yCurrent,xEnd - x6, infieldHeight ui_tooltip `Длина Секции`
	
		if iStandardType = STANDARD_EN & (iUISectionType = SECT_T | iUISectionType = SECT_L) then
			yCurrent = yCurrent + dy

			ui_outfield	`Расположение Полки`,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight
			ui_infield{3} "iFlangePos", 	 x2,   yCurrent, 	x3 - x2, infieldHeight,
						8, "", 1, 1,
						0, 0, 0, 0, 
						"", stFlangePosValues[1], FLANGEPOS_TOP,
						"", stFlangePosValues[2], FLANGEPOS_BOTTOM   ui_tooltip `Расположение Полки`
		

		endif	
	else
		ui_outfield	`Высота Колонны`, x4, yCurrent+4, x5 - x4, outfieldHeight
		ui_infield	"ZZYZX", x6, yCurrent,xEnd - x6, infieldHeight ui_tooltip `Высота Сечения`
	endif

	yCurrent = yCurrent + dy


	ui_separator 0, yCurrent, xEnd, yCurrent

	pageStartY = 31						!control some infield position with this
	yCurr = pageStartY + 11
	x1 = 16
	x2 = 31
	infieldWidth = 99
	mod_1 = 0
	mod_2 = 0
		
	!ui_infield	"iStructureCode", x2, yCurr, infieldWidth, 19 ui_tooltip toolTipSerialSize
	yCurr = yCurr + 28
		
	picHeight	= 170
	picWidth	= 126
	picPosX		= 150
	picPosY		= yCurrent + 40
	infieldWidth = 69
	if iUISectionType = SECT_LIPC then
		picIndex = 7		! "UI_LipC.png"
		posSectWidth = 270
		gosub "generalInfields"
		ui_infield	"sectLip",	picPosX+picWidth+2,	picPosY + 18,	infieldWidth,	19 	ui_tooltip toolTipLip
	endif
	
	if iUISectionType = SECT_PLAINC then
		picIndex = 9		! "UI_PlainC.png"
		posSectWidth = 270
		gosub "generalInfields"
	endif
	
	if iUISectionType = SECT_Z2 then
		picIndex = 13		! "UI_Z2.png"
	
	
		ui_infield	"TopWidth",		picPosX+50,	pageStartY + 11,		infieldWidth,	19 	ui_tooltip toolTipTopWidth
		ui_infield	"BottomWidth",	picPosX+15,	picPosY+picHeight+2,	infieldWidth,	19 	ui_tooltip toolTipBottomWidth
		ui_infield	"sectDepth",	picPosX-infieldWidth-2,	pageStartY + 105,	infieldWidth,	19 	ui_tooltip toolTipDepth
		ui_infield	"sectThick",	picPosX+picWidth+2,	pageStartY + 75,	infieldWidth,	19 	ui_tooltip toolTipThickness
		ui_infield	"sectLip",		picPosX+picWidth+2,	pageStartY + 52,		infieldWidth,	19 	ui_tooltip toolTipLip
	endif
	
	if iUISectionType = SECT_Z1 then
		mod_1 = 20
		picIndex = 12		! "UI_Z1.png"
		picWidth = 164
		picPosX	= 300-picWidth
		posSectWidth = 290
		gosub "generalInfields"
		ui_infield	"flangeThick",	picPosX+picWidth+2,	pageStartY + 165,	infieldWidth,	19 ui_tooltip toolTipFlangeThk
		ui_infield	"Rad",			picPosX+picWidth+2,	pageStartY + 133,	infieldWidth,	19 ui_tooltip toolTipRootRad
	
	endif
	
	if iUISectionType = SECT_C1 then
		picIndex = 1		! "UI_C1.png"
		posSectWidth = 270
		gosub "generalInfields"
		gosub "generalInfields2"
	endif
	
	if iUISectionType = SECT_C2 then
		picIndex = 2		! "UI_C2.png"
		posSectWidth = 270
		gosub "generalInfields"
		gosub "generalInfields2"
		ui_infield	"toeRad",	picPosX+picWidth+2,	pageStartY + 173,	infieldWidth,	19 ui_tooltip toolTipToeRad
	endif
	
	if iUISectionType = SECT_I1 then
		picWidth = 180
		picPosX	= 300-picWidth
		picIndex = 3		! "UI_I1.png"
		posSectWidth = 255
		gosub "generalInfields"
		gosub "generalInfields2"
		ui_infield	"toeRad",	picPosX+picWidth+2,	pageStartY + 173,	infieldWidth,	19 ui_tooltip toolTipToeRad
	endif
	
	if iUISectionType = SECT_I2 then
		picWidth = 180
		picPosX	= 300-picWidth
		picIndex = 4		! "UI_I2.png"
		posSectWidth = 255
		gosub "generalInfields"
		gosub "generalInfields2"
	endif
	
	if iUISectionType = SECT_I3 then
		picWidth = 180
		picPosX	= 300-picWidth
		picIndex = 5		! "UI_I3.png"
		posSectWidth = 255
		gosub "generalInfields"
		gosub "generalInfields2"
		ui_infield	"flangeDepth",	picPosX+picWidth+2,	pageStartY + 95,	infieldWidth,	19 ui_tooltip toolTipInnerDepth
	endif
	
	if iUISectionType = SECT_L then
		mod_1 = -6
		mod_2 = -6
		picPosY	= yCurrent + 25

		picWidth = 110
		picPosX	= 280-picWidth
		picIndex = 6		! "UI_L1.png"
		posSectWidth = 285
		gosub "generalInfields"
		ui_infield	"Rad",		picPosX+picWidth+2,	picPosY + 97,	infieldWidth,	19 ui_tooltip toolTipRootRad
		ui_infield	"toeRad",	picPosX+picWidth+2,	picPosY + 137,	infieldWidth,	19 ui_tooltip toolTipToeRad
	endif
	
	if iUISectionType = SECT_O1 then
		picWidth = 180
		picPosX	= 300-picWidth
		picIndex = 8		! "UI_O1.png"
	
		ui_infield	"sectDepth",		picPosX+picWidth+2,	pageStartY + 161,	infieldWidth,	19 ui_tooltip toolTipDiameterSect
		ui_infield	"sectThick",		picPosX+picWidth+2,	pageStartY + 57,	infieldWidth,	19 ui_tooltip toolTipWallThk
	endif
	
	if iUISectionType = SECT_R1 then
		picWidth = 180
		picPosX	= 300-picWidth
		picIndex = 10		! "UI_R1.png"
	
		ui_infield	"sectWidth",		picPosX+65,	picPosY+picHeight+2,	infieldWidth,	19 ui_tooltip toolTipWidthSect
		ui_infield	"sectDepth",		picPosX-infieldWidth-2,	pageStartY + 105,	infieldWidth,	19 ui_tooltip toolTipDepthSect
		ui_infield	"sectThick",		picPosX+picWidth+2,	pageStartY + 47,		infieldWidth,	19 ui_tooltip toolTipWallThk
	endif
	
	if iUISectionType = SECT_T then
		picPosY		= yCurrent + 25

		picWidth = 164
		picPosX	= 300-picWidth
		picIndex = 11		! "UI_T1.png"
		posSectWidth = picPosX+50
		ui_infield	"sectWidth",	posSectWidth,			picPosY - 21,			infieldWidth, 19 ui_tooltip toolTipWidth
		ui_infield	"sectDepth",	picPosX-infieldWidth-2,	picPosY + 72, 			infieldWidth, 19 ui_tooltip toolTipDepth
		ui_infield	"sectThick",	posSectWidth,			picPosY + picHeight+2,	infieldWidth, 19 ui_tooltip toolTipThickness
		ui_infield	"flangeThick",	picPosX+picWidth+2,		picPosY + 22,			infieldWidth, 19 ui_tooltip toolTipFlangeThk
		ui_infield	"Rad",			picPosX+picWidth+2,		picPosY + 52,			infieldWidth, 19 ui_tooltip toolTipRootRad
	endif
	
	ui_pict	picIndex, picPosX, picPosY, picWidth, picHeight, 1

return

"placementPage":
! ------------------------------------------------------------------------------
! Placement
! ------------------------------------------------------------------------------
	x1 = 0
	x2 = 120
	x3 = 170
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 360
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	ui_style 0,0
	
	ui_outfield	`Ось Поворота` ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_infield{3} "iRotAxis", 	 x3,   yCurrent, 	x6 - x3, infieldHeight,
				8, "", 1, 1,
				0, 0, 0, 0, 
				"", stRotAxisValues[1], ROT_AXIS,
				"", stRotAxisValues[2], ROT_CROSSECT

	yCurrent = yCurrent + dy

	ui_outfield	`Точка Вставки`, x1, yCurrent+4, x5 - x4, outfieldHeight
	ui_infield{3}	"iInsertPoint", 	 x3,   yCurrent, 	x6 - x3, infieldHeight,
				8, "", 1, 1,
				0, 0, 0, 0, 
				"", stInsertPointValues[1], INSERTPOINT_TOP,
				"", stInsertPointValues[2], INSERTPOINT_CENTER,
				"", stInsertPointValues[3], INSERTPOINT_BOTTOM

	yCurrent = yCurrent + dy

	ui_separator 0, yCurrent, xEnd, yCurrent

	pageStartY = 5

	yCurr = pageStartY + 33
	x1 = 16
	x2 = 31
	infieldWidth = 130
	
	yCurr = yCurr + 18

	yMod = 0
	if iRotAxis = ROT_AXIS then
		if iInsertPoint = INSERTPOINT_TOP then
			yMod = 60
		endif
		if iInsertPoint = INSERTPOINT_CENTER then
			yMod = 80
		endif
		if iInsertPoint = INSERTPOINT_BOTTOM then
			yMod = 100
		endif
	else
		if iInsertPoint = INSERTPOINT_TOP then
			yMod = 80
		endif
		if iInsertPoint = INSERTPOINT_CENTER then
			yMod = 90
		endif
		if iInsertPoint = INSERTPOINT_BOTTOM then
			yMod = 100
		endif
	endif
	ui_infield "Angle", x5-10 , yCurr+yMod, x7-x5, 19

	picPosX = 110
	picPosY = pageStartY + 100

	if iRotAxis = ROT_AXIS then
		if iInsertPoint = INSERTPOINT_TOP then
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 then
				pictName = 32	! gs_AxisRotUp_C.png
			endif
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictName = 33	! gs_AxisRotUp_I.png
			endif
			if iUISectionType = SECT_L		then pictName = 34	! gs_AxisRotUp_L.png
			if iUISectionType = SECT_LIPC	then pictName = 35	! gs_AxisRotUp_LippedC.png
			if iUISectionType = SECT_O1	| iUISectionType = SECT_R1 then
				pictName = 36	! gs_AxisRotUp_O.png
			endif
			if iUISectionType = SECT_PLAINC then pictName = 37	! gs_AxisRotUp_PlainC.png
			if iUISectionType = SECT_T		then pictName = 38	! gs_AxisRotUp_T.png
			if iUISectionType = SECT_Z1		then pictName = 39	! gs_AxisRotUp_Z.png
			if iUISectionType = SECT_Z2		then pictName = 40	! gs_AxisRotUp_Zsect.png
		endif
		if iInsertPoint = INSERTPOINT_CENTER  then
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 then
				pictName = 23	! gs_AxisRotCentre_C.png
			endif
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictName = 24	! gs_AxisRotCentre_I.png
			endif
			if iUISectionType = SECT_L		then pictName = 25	! gs_AxisRotCentre_L.png
			if iUISectionType = SECT_LIPC	then pictName = 26	! gs_AxisRotCentre_LippedC.png
			if iUISectionType = SECT_O1	| iUISectionType = SECT_R1 then
				pictName = 27	! gs_AxisRotCentre_O.png
			endif
			if iUISectionType = SECT_PLAINC then pictName = 28	! gs_AxisRotCentre_PlainC.png
			if iUISectionType = SECT_T		then pictName = 29	! gs_AxisRotCentre_T.png
			if iUISectionType = SECT_Z1		then pictName = 30	! gs_AxisRotCentre_Z.png
			if iUISectionType = SECT_Z2		then pictName = 31	! gs_AxisRotCentre_Zsect.png
		endif
		if iInsertPoint = INSERTPOINT_BOTTOM then
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 then
				pictName = 14	! gs_AxisRotBottom_C.png
			endif
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictName = 15	! gs_AxisRotBottom_I.png
			endif
			if iUISectionType = SECT_L		then pictName = 16	! gs_AxisRotBottom_L.png
			if iUISectionType = SECT_LIPC	then pictName = 17	! gs_AxisRotBottom_LippedC.png
			if iUISectionType = SECT_O1	| iUISectionType = SECT_R1 then
				pictName = 18	! gs_AxisRotBottom_O.png
			endif
			if iUISectionType = SECT_PLAINC then pictName = 19	! gs_AxisRotBottom_PlainC.png
			if iUISectionType = SECT_T		then pictName = 20	! gs_AxisRotBottom_T.png
			if iUISectionType = SECT_Z1		then pictName = 21	! gs_AxisRotBottom_Z.png
			if iUISectionType = SECT_Z2		then pictName = 22	! gs_AxisRotBottom_Zsect.png
		endif
		viewTitle = `(Сечение)`
	else
		if iInsertPoint = INSERTPOINT_TOP then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictName = 53	! gs_SectRotUp_I.png
			endif
			if iUISectionType = SECT_L		then pictName = 54	! gs_SectRotUp_L.png
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
				pictName = 55	! gs_SectRotUp_O.png
			endif
			if iUISectionType = SECT_T		then pictName = 56	! gs_SectRotUp_T.png
			if iUISectionType = SECT_Z1		then pictName = 57	! gs_SectRotUp_Z.png
			if iUISectionType = SECT_Z2		then pictName = 58	! gs_SectRotUp_Zsect.png
		endif
		if iInsertPoint = INSERTPOINT_CENTER  then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictName = 47	! gs_SectRotCentre_I.png
			endif
			if iUISectionType = SECT_L		then pictName = 48	! gs_SectRotCentre_L.png
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
				pictName = 49	! gs_SectRotCentre_O.png
			endif
			if iUISectionType = SECT_T		then pictName = 50	! gs_SectRotCentre_T.png
			if iUISectionType = SECT_Z1		then pictName = 51	! gs_SectRotCentre_Z.png
			if iUISectionType = SECT_Z2		then pictName = 52	! gs_SectRotCentre_Zsect.png
		endif
		if iInsertPoint = INSERTPOINT_BOTTOM then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictName = 41	! gs_SectRotBottom_I.png
			endif
			if iUISectionType = SECT_L		then pictName = 42	! gs_SectRotBottom_L.png
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
				pictName = 43	! gs_SectRotBottom_O.png
			endif
			if iUISectionType = SECT_T		then pictName = 44	! gs_SectRotBottom_T.png
			if iUISectionType = SECT_Z1		then pictName = 45	! gs_SectRotBottom_Z.png
			if iUISectionType = SECT_Z2		then pictName = 46	! gs_SectRotBottom_Zsect.png
		endif
		viewTitle = `(Вид Сбоку)`
	endif
	
	ui_pict pictName,      picPosX, picPosY,  200,80, 1
	ui_outfield viewTitle, picPosX, picPosY+80+2,  200, 14, 2
return

"cuttingPage":
! ------------------------------------------------------------------------------
! Cutting
! ------------------------------------------------------------------------------
	pageStartY = 5

	if iUIStructureType = STRUCTURE_BEAM then
		x1 = 10
		x2 = 67
		infieldWidth = 149
		y0 = pageStartY + 8
		
		ui_separator 222, pageStartY + 1, 222, pageStartY + 225
		
		ui_style 0, 1
		ui_infield{3} "bCutPlanes", x1, y0-4, 150, 19,
				7, "", 2, 1,
				0, 0, 0, 0,
				1, `Секущие Плоскости`, 0,
				1, `Секущие Плоскости`, 1
		ui_style 0, 0
		yCurr = y0 + 26
	
		ui_outfield `Тип Обрезки (Нижний Конец)`, x1, yCurr, 150, 15, 4*not(bCutPlanes)
		yCurr = yCurr + 18
		ui_infield{3} "iCutModeBottom", x2 , yCurr, infieldWidth, 19,
					8, "", 1, 1,
					0, 0, 0, 0, 
					"", stCutModeValues[1], CUTMODE_PERPENDICULAR,
					"", stCutModeValues[2], CUTMODE_ALONGVERTICAL,
					"", stCutModeValues[3], CUTMODE_ALONGHORIZONTAL
		
		yCurr = yCurr + 28
	
		ui_outfield `Тип Обрезки (Верхний Конец)`, x1, yCurr, 150, 15, 4*not(bCutPlanes)
		yCurr = yCurr + 18
		ui_infield{3} "iCutModeTop", x2 , yCurr, infieldWidth, 19,
					8, "", 1, 1,
					0, 0, 0, 0, 
					"", stCutModeValues[1], CUTMODE_PERPENDICULAR,
					"", stCutModeValues[2], CUTMODE_ALONGVERTICAL,
					"", stCutModeValues[3], CUTMODE_ALONGHORIZONTAL
	
		pictNameLeft1 	= 59	! inicialize
		pictNameRight1 	= 96	! inicialize
		pictNameLeft2 	= 65	! inicialize
		pictNameRight2 	= 102	! inicialize
		
		if iCutModeBottom = CUTMODE_PERPENDICULAR then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictNameLeft1 = 59	! gs_Cut1Bottom_I.png
				pictNameRight1 = 96	! gs_AngleCut12Bottom_I.png
			endif
			if iUISectionType = SECT_L then
				pictNameLeft1 = 60	! gs_Cut1Bottom_L.png
				pictNameRight1 = 97	! gs_AngleCut12Bottom_L.png
			endif
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
				
				pictNameLeft1 = 61	! gs_Cut1Bottom_O.png
	
				if  iUISectionType = SECT_R1	| iUISectionType = SECT_O1 then
					pictNameRight1 = 98	! gs_AngleCut12Bottom_O.png
				else
					pictNameRight1 = 95	! gs_AngleCut12Bottom_C.png
				endif
			endif
			if iUISectionType = SECT_T then
				pictNameLeft1 = 62	! gs_Cut1Bottom_T.png
				pictNameRight1 = 96	! gs_AngleCut12Bottom_I.png
			endif
			if iUISectionType = SECT_Z1 then
				pictNameLeft1 = 63	! gs_Cut1Bottom_Z.png
				pictNameRight1 = 99	! gs_AngleCut12Bottom_Z.png
			endif
			if iUISectionType = SECT_Z2 then
				pictNameLeft1 = 64		! gs_Cut1Bottom_Zsect.png
				pictNameRight1 = 100	! gs_AngleCut12Bottom_Zsect.png
			endif
		endif
	
		if iCutModeBottom = CUTMODE_ALONGVERTICAL then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictNameLeft1 = 71	! gs_Cut2Bottom_I.png
				pictNameRight1 = 96	! gs_AngleCut12Bottom_I.png
			endif
			if iUISectionType = SECT_L then
				pictNameLeft1 = 72	! gs_Cut2Bottom_L.png
				pictNameRight1 = 97	! gs_AngleCut12Bottom_L.png
			endif
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
					pictNameLeft1 = 73	! gs_Cut2Bottom_O.png
	
				if  iUISectionType = SECT_R1	| iUISectionType = SECT_O1 then
					pictNameRight1 = 98	! gs_AngleCut12Bottom_O.png
				else
					pictNameRight1 = 95	! gs_AngleCut12Bottom_C.png
				endif
			endif
			if iUISectionType = SECT_T then
				pictNameLeft1 = 74	! gs_Cut2Bottom_T.png
				pictNameRight1 = 96	! gs_AngleCut12Bottom_I.png
			endif
			if iUISectionType = SECT_Z1 then
				pictNameLeft1 = 75	! gs_Cut2Bottom_Z.png
				pictNameRight1 = 99	! gs_AngleCut12Bottom_Z.png
			endif
			if iUISectionType = SECT_Z2 then
				pictNameLeft1 = 76		! gs_Cut2Bottom_Zsect.png
				pictNameRight1 = 100	! gs_AngleCut12Bottom_Zsect.png
			endif
		endif
	
		if iCutModeBottom = CUTMODE_ALONGHORIZONTAL then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictNameLeft1 = 83		! gs_Cut3Bottom_I.png
				pictNameRight1 = 107	! gs_AngleCut3Bottom_I.png
			endif
			if iUISectionType = SECT_L then
				pictNameLeft1 = 84		! gs_Cut3Bottom_L.png
				pictNameRight1 = 108	! gs_AngleCut3Bottom_L.png
			endif
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
					pictNameLeft1 = 85		! gs_Cut3Bottom_O.png
					pictNameRight1 = 109	! gs_AngleCut3Bottom_O.png
			endif
			if iUISectionType = SECT_T then
				pictNameLeft1 = 86		! gs_Cut3Bottom_T.png
				pictNameRight1 = 110	! gs_AngleCut3Bottom_T.png
			endif
			if iUISectionType = SECT_Z1 then
				pictNameLeft1 = 87	! gs_Cut3Bottom_Z.png
				pictNameRight1 = 111	! gs_AngleCut3Bottom_Z.png
			endif
			if iUISectionType = SECT_Z2 then
				pictNameLeft1 = 88		! gs_Cut3Bottom_Zsect.png
				pictNameRight1 = 112	! gs_AngleCut3Bottom_Zsect.png
			endif
		endif
	
		ui_pict pictNameLeft1, 15, pageStartY + 120, 100, 80, 1
		ui_pict pictNameRight1, 235, pageStartY + 120, 100, 80, 1
	
		ui_outfield `(Вид Сбоку)`,15, pageStartY + 120+80+2, 200, 15, 2
		
	
		yCurr = y0
		dy = 26
		x1 = 234
		x2 = 254
		x3 = 344
		x4 = 364
		infieldWidth = 69
	
		ui_style 0,1
		ui_outfield `Углы Обрезки`, x1, yCurr, 150, 15
		ui_style 0,0
		yCurr = yCurr + dy
	
		ui_outfield `(1)`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang1_bot", x2, yCurr-4, infieldWidth, 19
	
		ui_outfield `(3)`, x3, yCurr, x4-x3-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang3_up", x4, yCurr-4, infieldWidth, 19
		yCurr = yCurr + dy
	
		ui_outfield `(2)`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang2_bot", x2, yCurr-4, infieldWidth, 19
	
		ui_outfield `(4)`, x3, yCurr, x4-x3-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang4_up", x4, yCurr-4, infieldWidth, 19
	
	
		if iCutModeTop = CUTMODE_PERPENDICULAR then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictNameLeft2 = 65		! gs_Cut1Up_I.png
				pictNameRight2 = 102	! gs_AngleCut12Up_I.png
			endif
			if iUISectionType = SECT_L then
				pictNameLeft2 = 66		! gs_Cut1Up_L.png
				pictNameRight2 = 103	! gs_AngleCut12Up_L.png
			endif
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
					pictNameLeft2 = 67	! gs_Cut1Up_O.png
	
				if iUISectionType = SECT_R1	| iUISectionType = SECT_O1 then
					pictNameRight2 = 104	! gs_AngleCut12Up_O.png
				else
					pictNameRight2 = 101	! gs_AngleCut12Up_C.png
				endif
			endif
			if iUISectionType = SECT_T then
				pictNameLeft2 = 68		! gs_Cut1Up_T.png
				pictNameRight2 = 102	! gs_AngleCut12Up_I.png
			endif
			if iUISectionType = SECT_Z1 then
				pictNameLeft2 = 69		! gs_Cut1Up_Z.png
				pictNameRight2 = 105	! gs_AngleCut12Up_Z.png   gs_AngleCut12Up_Zsect
			endif
			if iUISectionType = SECT_Z2 then
				pictNameLeft2 = 70		! gs_Cut1Up_Zsect.png
				pictNameRight2 = 106	! gs_AngleCut12Up_Zsect.png
			endif
		endif
	
		if iCutModeTop = CUTMODE_ALONGVERTICAL then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictNameLeft2 = 77		! gs_Cut2Up_I.png
				pictNameRight2 = 102	! gs_AngleCut12Up_I.png
			endif
			if iUISectionType = SECT_L then
				pictNameLeft2 = 78		! gs_Cut2Up_L.png
				pictNameRight2 = 103	! gs_AngleCut12Up_L.png
			endif
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
					pictNameLeft2 = 79	! gs_Cut2Up_O.png
	
				if iUISectionType = SECT_R1	| iUISectionType = SECT_O1 then
					pictNameRight2 = 104	! gs_AngleCut12Up_O.png
				else
					pictNameRight2 = 101	! gs_AngleCut12Up_C.png
				endif
			endif
			if iUISectionType = SECT_T then
				pictNameLeft2 = 80		! gs_Cut2Up_T.png
				pictNameRight2 = 102	! gs_AngleCut12Up_I.png
			endif
			if iUISectionType = SECT_Z1 then
				pictNameLeft2 = 81		! gs_Cut2Up_Z.png
				pictNameRight2 = 105	! gs_AngleCut12Up_Z.png   gs_AngleCut12Up_Zsect
			endif
			if iUISectionType = SECT_Z2 then
				pictNameLeft2 = 82	 ! gs_Cut2Up_Zsect.png
				pictNameRight2 = 106	! gs_AngleCut12Up_Zsect.png
			endif
		endif
	
		if iCutModeTop = CUTMODE_ALONGHORIZONTAL then
			if iUISectionType = SECT_I1	| iUISectionType = SECT_I2 | iUISectionType = SECT_I3 then
				pictNameLeft2 = 89		! gs_Cut3Up_I.png
				pictNameRight2 = 113	! gs_AngleCut3Up_I.png
			endif
			if iUISectionType = SECT_L then
				pictNameLeft2 = 90		! gs_Cut3Up_L.png
				pictNameRight2 = 114	! gs_AngleCut3Up_L.png
			endif
			if iUISectionType = SECT_C1	| iUISectionType = SECT_C2 |\
				iUISectionType = SECT_R1	| iUISectionType = SECT_O1 |\
				iUISectionType = SECT_LIPC	| iUISectionType = SECT_PLAINC then
					pictNameLeft2 = 91		! gs_Cut3Up_O.png
					pictNameRight2 = 115	! gs_AngleCut3Up_O.png
			endif
			if iUISectionType = SECT_T then
				pictNameLeft2 = 92		! gs_Cut3Up_T.png
				pictNameRight2 = 116	! gs_AngleCut3Up_T.png
			endif
			if iUISectionType = SECT_Z1 then
				pictNameLeft2 = 93		! gs_Cut3Up_Z.png
				pictNameRight2 = 117	! gs_AngleCut3Up_Z.png
			endif
			if iUISectionType = SECT_Z2 then
				pictNameLeft2 = 94		! gs_Cut3Up_Zsect.png
				pictNameRight2 = 118	! gs_AngleCut3Up_Zsect.png
			endif
		endif
	
		ui_pict pictNameLeft2, 115, pageStartY + 120, 100, 80, 1
		ui_pict pictNameRight2, 335, pageStartY + 120, 100, 80, 1
	
		ui_outfield `(Вид Сверху)`, 235, pageStartY + 120+80+2, 200, 15, 2
	else
		x1 = 140
		x2 = 304
		infieldWidth = 120
		infieldWidth2 = 75

		if iCutModeColTop = CUTMODE_ALONG_X THEN
			ui_pict	123,	18,	pageStartY + 15,	80,	100, 1		! UI_ColCutXT.png
		else
			ui_pict	125,	18,	pageStartY + 15,	80,	100, 1		! UI_ColCutYT.png
		endif

		if iCutModeColBottom = CUTMODE_ALONG_X THEN
			ui_pict	122,	18,	pageStartY + 115,	80,	100, 1		! UI_ColCutXB.png
			ui_pict	120,	100,pageStartY + 176,	20,	16, 1		! UI_AxisY.png
		else
			ui_pict	124,	18,	pageStartY + 115,	80,	100, 1		! UI_ColCutYB.png
			ui_pict	119,	100,pageStartY + 176,	20,	16, 1		! UI_AxisX.png
		endif
		ui_pict	121,	53,	pageStartY - 1,	16,	20, 1	! UI_AxisZ.png

		y0 = pageStartY + 15
		
		ui_style 0, 1
		ui_infield{3} "bCutPlanes", x1-10, y0-4, x2-x1, 19,
				7, "", 2, 1,
				0, 0, 0, 0,
				1, `Секущие Плоскости`, 0,
				1, `Секущие Плоскости`, 1
		ui_style 0, 0
		yCurr = y0 + 26
	
		ui_outfield `Верхний Угол Обрезки`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield{3} "iCutModeColTop", x2 , yCurr-4, infieldWidth, 19,
					8, "", 1, 1,
					0, 0, 0, 0, 
					"", stCutModeColValues[1], CUTMODE_ALONG_X,
					"", stCutModeColValues[2], CUTMODE_ALONG_Y
		
		yCurr = yCurr + 26

		ui_outfield `(1)`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang1_bot", x2, yCurr-4, infieldWidth2, 19
		yCurr = yCurr + 26


		ui_outfield `(2)`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang2_bot", x2, yCurr-4, infieldWidth2, 19
		yCurr = yCurr + 26
	
		ui_outfield `Нижний Угол Обрезки`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield{3} "iCutModeColBottom", x2 , yCurr-4, infieldWidth, 19,
					8, "", 1, 1,
					0, 0, 0, 0, 
					"", stCutModeColValues[1], CUTMODE_ALONG_X,
					"", stCutModeColValues[2], CUTMODE_ALONG_Y

		yCurr = yCurr + 26

		ui_outfield `(3)`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang3_bot", x2, yCurr-4, infieldWidth2, 19
		yCurr = yCurr + 26

		ui_outfield `(4)`, x1, yCurr, x2-x1-1, 15, 4*not(bCutPlanes)
		ui_infield "gs_cutang4_bot", x2, yCurr-4, infieldWidth2, 19
	endif
return

"propertiesPage":
! ------------------------------------------------------------------------------
! Properties
! ------------------------------------------------------------------------------
	pageStartY = 5

	if iUIStructureType = STRUCTURE_BEAM then
		stPropGroupName = `Свойства Балки`
	else
		stPropGroupName = `Свойства Колонны`
	endif

	itemID = 1
	ui_listitem itemID,	listFieldPropID, "", 0,	"", stPropGroupName 			! Group Title
	itemID = itemID + 1

	! Properties
	for i = 1 to vardim1(stPropParamNames)
		ui_listitem itemID,	listFieldPropID, stPropParamNames[i], 1
		itemID = itemID + 1
	next i

	! Listing Parameters

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = listFieldPropID,
										bDrawField = 0,
										bDrawListGroupLine = 1,
										firstItemID	= itemID,
										stDescHeader = `Характеристики`,
										stParamNames = stParamNames,
							returned_parameters lastUsedListItemID


	ui_listfield listFieldPropID, 0, pageStartY, 444, 230, 0
return

"appearancePage":
! ------------------------------------------------------------------------------
! Representation and Surfaces
! ------------------------------------------------------------------------------
	pageStartY = 0
	itemID = 1

	listPicName = ""
	listPicName2 = ""
	listPicName3 = ""
	if iUIStructureType = STRUCTURE_BEAM then
		listPicName = "ui_list_axis2D_flat(1)"
		listPicName2 = "ui_list_axisLineType_flat(1)"
		listPicName3 = "ui_list_axisLinePen_flat(1)"
	else
		listPicName = "ui_list_axis2D_2_flat(1)"
		listPicName2 = "ui_list_axisLineType_2_flat(1)"
		listPicName3 = "ui_list_axisLinePen_2_flat(1)"
	endif
	file_dependence "ui_list_axis2D_flat(1)"
	file_dependence "ui_list_axisLineType_flat(1)"
	file_dependence "ui_list_axisLinePen_flat(1)"
	file_dependence "ui_list_axis2D_2_flat(1)"
	file_dependence "ui_list_axisLineType_2_flat(1)"
	file_dependence "ui_list_axisLinePen_2_flat(1)"

	ui_listfield listFieldAppID, 0, pageStartY, 444, 265 

	ui_listitem itemID,	listFieldAppID, "gs_2D_representation",	0								: itemID = itemID + 1

	ui_listitem itemID, listFieldAppID, "axis",					1,	listPicName				: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "only_axis",			1,	listPicName				: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "axis_line_type",		1,	listPicName2			: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "axis_line_pen",		1,	listPicName3			: itemID = itemID + 1

	if iUIStructureType = STRUCTURE_BEAM then
		ui_listitem itemID,	listFieldAppID, "DashedLineType",	1,	"ui_list_axisLineType_flat(1)" : itemID = itemID + 1
	else
		itemID = itemID + 1
	endif

	if iUIStructureType = STRUCTURE_COLUMN then
		ui_listitem itemID,	listFieldAppID, "axis_XDim",		1,	"ui_list_axisDimX_flat(1)" 	: itemID = itemID + 1
		ui_listitem itemID,	listFieldAppID, "axis_YDim",		1,	"ui_list_axisDimY_flat(1)"	: itemID = itemID + 1
	else
		itemID = itemID + 2
	endif

	if iUIStructureType = STRUCTURE_COLUMN & (iStructureCode # CODECOL_CHSHOT) & (iStructureCode # CODECOL_CHSCOLD) then
		ui_listitem itemID,	listFieldAppID, "gs_detlevel_2D",	1,	"ui_list_detailLevel2D_flat(1)"	: itemID = itemID + 1
	else
		itemID = itemID + 1
	endif

	ui_listitem itemID, listFieldAppID, "gs_ShowMoreHtps2D",	1,	"ui_list_extraHS_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "gs_cont_pen",			1,	"ui_list_contourPen_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "gs_fill_type",			1,	"ui_list_fillType_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "gs_fill_pen",			1,	"ui_list_fillPen_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "gs_back_pen",			1,	"ui_list_fillBgPen_flat(1)"		: itemID = itemID + 1

	ui_listitem itemID, listFieldAppID, "gs_3D_representation",	0								: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "gs_detlevel_3D",		1,	"ui_list_detailLevel3D_flat(1)"	: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "gs_resol",				1,	"ui_list_resol_flat(1)"			: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "gs_shadow",			1,	"ui_list_shadow_flat(1)"			: itemID = itemID + 1

	ui_listitem itemID, listFieldAppID, "",						0,	"",		`Покрытия`			: itemID = itemID + 1
	ui_listitem itemID, listFieldAppID, "col_mat",				1,	"ui_list_surface_flat(1)"		: itemID = itemID + 1
return


"generalInfields":
	ui_infield	"sectWidth",	picPosX+picWidth/2-infieldWidth/2+10+mod_1,	picPosY+picHeight+2, infieldWidth,	19 	ui_tooltip toolTipWidth
	ui_infield	"sectDepth",	picPosX-infieldWidth-2,	picPosY + 70, infieldWidth,	19 	ui_tooltip toolTipDepth
	ui_infield	"sectThick",	picPosX+picWidth/2-infieldWidth/2+10+mod_2,	picPosY - 20,		infieldWidth,	19 	ui_tooltip toolTipThickness
return

"generalInfields2":
	ui_infield	"flangeThick",	picPosX+picWidth+2,	pageStartY + 47,		infieldWidth,	19 ui_tooltip toolTipFlangeThk
	ui_infield	"Rad",			picPosX+picWidth+2,	pageStartY + 133,		infieldWidth,	19 ui_tooltip toolTipRootRad
	ui_infield	"filletDepth",	picPosX-infieldWidth-2,	pageStartY + 147,	infieldWidth,	19 ui_tooltip toolTipDepthFillets
return




