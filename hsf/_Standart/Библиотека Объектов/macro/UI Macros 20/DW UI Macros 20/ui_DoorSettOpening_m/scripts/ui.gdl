
dim stDetail[6]
	stDetail[1] = `Зависит от Масштаба`
	stDetail[2] = `Детальный`
	stDetail[3] = `Простой`
	stDetail[4] = `Откл.`
	stDetail[5] = `Китайский Простой`
	stDetail[6] = `Китайский Детальный`

dim stDetail2D[9]
	stDetail2D[1] = `по ПМВ`
	stDetail2D[2] = `Зависит от Масштаба`
	stDetail2D[3] = `1:50`
	stDetail2D[4] = `1:100`
	stDetail2D[5] = `1:200`
	stDetail2D[6] = `Откл.`
	stDetail2D[7] = `Китайский Простой`
	stDetail2D[8] = `Китайский Детальный`
	stDetail2D[9] = `1:20`

pageStartY 	= 6
y0 			= pageStartY 
dy			= 23

x1			= 3
x2			= 165
x3 			= 225
xSep1		= x3 - 6
xSep2		= x3

picPopupWidth = 165
textPopupWidth = 165
picPopupHeight1 = 62
picPopupHeight2 = 120
heightOutf	= 15
heightInf	= 19

bGarageDoor = (gs_doorcode = "Gar1" | gs_doorcode = "Gar2" | gs_doorcode = "Gar3" | gs_doorcode = "Gar4")

! ======================================================================
! Door Leaf Panel
! ======================================================================

if gs_doorcode <> "HSD" & gs_doorcode <> "HSRD" & not(bBombShelter) then

		! --- Not High-Speed Garage doors
	
		ui_style 0, 1
		ui_outfield `Полотно Двери`, x1, y0, x2, heightOutf
		y0 = y0 + dy
		
		ui_style 0,0
	
	if gs_doorcode = "Gar1" | gs_doorcode = "Gar2" | gs_doorcode = "Gar3" then
	! --- Sectional Garage doors
		gosub "GarageLeafPanels"
	else
		if gs_doorcode = "Gar4" then
		! --- Overhead Garage door
			gosub "GarageSectPanels"
		else
			! --- Other doors
			gosub "DoorLeafPanels"
		endif
	endif
	
	y0 = y0 + picPopupHeight2 - 23
	if bGarageDoor then
		ui_pict_button ui_function, "", "uiFunctionDoorLeaf_flat(1)", picPopupWidth + x1 + 10, y0, 36, 22, -iUiID_LeafSettingsGar ! Set GLOB_UI_BUTTON_ID
	else
		ui_pict_button ui_function, "", "uiFunctionDoorLeaf_flat(1)", picPopupWidth + x1 + 10, y0, 36, 22, -iUiID_LeafTypeHandle ! Set GLOB_UI_BUTTON_ID
	endif
endif

! ======================================================================
! Door Handle
! ======================================================================

if gs_doorcode <> "HSD" & gs_doorcode <> "HSRD" then
		! --- Not High-Speed Garage doors
	if bBombShelter then
		_xHandle = xSep1 - 5 - picPopupWidth
		gosub "HandleTypes"
		gosub "HandleParsBombShelter"
	else
		y0 = pageStartY + 167
		_xHandle = x1
		gosub "HandleTypes"
		
		y0 = y0 + picPopupHeight1 - 23
		ui_pict_button ui_function, "", "uiFunctionDoorHandle_flat(1)", picPopupWidth + x1 + 10, y0, 36, 22, -iUiID_HandleAndKickBoard ! Set GLOB_UI_BUTTON_ID
	endif
	ui_separator xSep1, pageStartY-3, xSep1, 265
endif

! ======================================================================
! Opening Type, Number of Leaves, Shape, Frame, Number of Sections, Panel
! ======================================================================

if 	iOpeningMethod = OPENMETHOD_OPEN_SIMP |\ 
	iOpeningMethod = OPENMETHOD_OPEN_DOUB then	

	! --- Opening Type for hinged doors
	
	gosub "OpeningTypesTextPopup"
	if bBombShelter then
		y0 = y0 + 26
	else
		y0 = y0 + 70
	endif
	
	ui_separator xSep2, y0, 444, y0

	if bBombShelter then
		gosub "BombShelterShape"
	endif
else
	if iOpeningMethod = OPENMETHOD_SLIDE_INT then
		! --- Opening Type for sliding doors
	
		gosub "OpeningTypesPicPopup"
		y0 = y0 + 28
		
		ui_separator xSep2, y0, 444, y0
	endif

	if iOpeningMethod = OPENMETHOD_SLIDFOLD_SIMP | iOpeningMethod = OPENMETHOD_SLIDFOLD_BIFOLD then
		! --- Number of leaves for sliding door
		
		gosub "NumberOfLeaves"
	endif

	if iOpeningMethod = OPENMETHOD_PIVOT then
		! --- Pivot Door
	
		gosub "PivotShape"
	endif

	if iOpeningMethod = OPENMETHOD_POCKET_SIMP | iOpeningMethod = OPENMETHOD_POCKET_DOUB then
		! --- Pocket Frame
	
		gosub "PocketFrame"
	endif

	if gs_doorcode="Gar3" then
		! --- Garage Shape

		gosub "GarageShape"
	endif

	if gs_doorcode="Gar4" then
		! --- Garage Sections

		gosub "GarageSection"
	endif

	if gs_doorcode="HSD" | gs_doorcode="HSRD" then
		! --- High speed Panel

		gosub "GaragePanel"
	endif
endif

! ======================================================================
! Frame Style
! ======================================================================

if iOpeningMethod = OPENMETHOD_OPEN_SIMP |\
	iOpeningMethod = OPENMETHOD_OPEN_DOUB |\
	iOpeningMethod = OPENMETHOD_SLIDE_INT then

	gosub "FrameStyle"
	if not(bBombShelter) then
		ui_separator xSep2, pageStartY + 190, 444, pageStartY + 190
	endif
endif

! ======================================================================
! Natural Ventilation
! ======================================================================

if 	iOpeningMethod <> OPENMETHOD_SLIDFOLD_SIMP & iOpeningMethod <> OPENMETHOD_SLIDFOLD_BIFOLD then 
	y0 = pageStartY + 145
else
	y0 = pageStartY + 102
endif

if	gs_doorcode = "Gar1" then
	y0 = pageStartY 
endif

if bEnableVentilationProfile then
	gosub "VentilationType"
endif

! ======================================================================
! Detail Level
! ======================================================================

if (iOpeningMethod = OPENMETHOD_SLIDE_EXT_SIMP | iOpeningMethod = OPENMETHOD_SLIDE_EXT_DOUB) then
	y0 = pageStartY
else
	y0 = pageStartY + 196
endif

if gs_doorcode = "HSD" | gs_doorcode = "HSRD" then
		y0 = pageStartY + 177
		x3 = x1
endif

if	gs_doorcode = "Gar2" then
	y0 = pageStartY
endif

if	gs_doorcode = "Gar1" then
	y0 = pageStartY + 50
endif

gosub "DetailLevel"


! ======================================================================
end
! ======================================================================

"DoorLeafPanels":

	ui_infield{3} "iDoorPanelType", x1, y0, picPopupWidth, picPopupHeight2,
			2, 1,											! DoorPanelTypes.png							
			76, 19,													
			90, 80, 90, 80,
			28,stDoorTypes[46],46,		! No Grill
			2, stDoorTypes[47],47,		! H-V Grid
			39,stDoorTypes[45],45,		! French Panel
			49,stDoorTypes[48],48,		! Prairie Style
			1, stDoorTypes[ 1],1,		! Style 1
			9, stDoorTypes[ 5],5,		! Style 2
			9, stDoorTypes[ 8],8,		! Style 3
			10,stDoorTypes[ 6],6,		! Style 4
			10,stDoorTypes[ 9],9,		! Style 5
			8, stDoorTypes[ 7],7,		! Style 6
			11,stDoorTypes[10],10,		! Style 7
			12,stDoorTypes[11],11,		! Style 8
			13,stDoorTypes[12],12,		! Style 9
			58,stDoorTypes[57],57,		! Style 10
			59,stDoorTypes[58],58,		! Style 11
			60,stDoorTypes[59],59,		! Style 12
			61,stDoorTypes[60],60,		! Style 13
			74,stDoorTypes[73],73,		! Style 14
			70,stDoorTypes[69],69,		! Style 15
			75,stDoorTypes[74],74,		! Style 16
			30,stDoorTypes[28],28,		! Style 17
			4, stDoorTypes[ 3],3,		! Style 18
			5, stDoorTypes[ 4],4,		! Style 19
			37,stDoorTypes[34],34,		! Style 20
			32,stDoorTypes[29],29,		! Style 21
			34,stDoorTypes[31],31,		! Style 22
			71,stDoorTypes[70],70,		! Style 23
			67,stDoorTypes[66],66,		! Style 24
			68,stDoorTypes[67],67,		! Style 25
			69,stDoorTypes[68],68,		! Style 26
			3, stDoorTypes[ 2],2,		! Style 27
			57,stDoorTypes[56],56,		! Style 28
			72,stDoorTypes[71],71,		! Style 29
			73,stDoorTypes[72],72,		! Style 30
			62,stDoorTypes[61],61,		! Style 31
			63,stDoorTypes[62],62,		! Style 32
			64,stDoorTypes[63],63,		! Style 33
			14,stDoorTypes[13],13,		! Style 34
			15,stDoorTypes[14],14,		! Style 35
			17,stDoorTypes[16],16,		! Style 36
			33,stDoorTypes[30],30,		! Style 37
			35,stDoorTypes[32],32,		! Style 38
			36,stDoorTypes[33],33,		! Style 39
			16,stDoorTypes[15],15,		! Style 40
			18,stDoorTypes[17],17,		! Style 41
			19,stDoorTypes[18],18,		! Style 42
			20,stDoorTypes[19],19,		! Style 43
			65,stDoorTypes[64],64,		! Style 44
			21,stDoorTypes[20],20,		! Style 45
			22,stDoorTypes[21],21,		! Style 46
			66,stDoorTypes[65],65,		! Style 47
			52,stDoorTypes[51],51,		! Style 48
			53,stDoorTypes[52],52,		! Style 49
			54,stDoorTypes[53],53,		! Style 50
			56,stDoorTypes[55],55,		! Style 51
			55,stDoorTypes[54],54,		! Style 52
			23,stDoorTypes[22],22,		! Style 53
			24,stDoorTypes[23],23,		! Style 54
			25,stDoorTypes[24],24,		! Style 55
			26,stDoorTypes[25],25,		! Style 56
			27,stDoorTypes[26],26,		! Style 57
			29,stDoorTypes[27],27,		! Style 58
			6, stDoorTypes[76],76,		! Style 59
			7, stDoorTypes[77],77,		! Style 60
			38,stDoorTypes[35],35,		! 59
			40,stDoorTypes[36],36,		! 60
			41,stDoorTypes[37],37,		! 61
			42,stDoorTypes[38],38,		! 62
			76,stDoorTypes[75],75,		! 63
			43,stDoorTypes[39],39,		! 64
			44,stDoorTypes[40],40,		! 65
			45,stDoorTypes[41],41,		! 66
			46,stDoorTypes[42],42,		! 67
			47,stDoorTypes[43],43,		! 68
			48,stDoorTypes[44],44,		! 69
			50,stDoorTypes[49],49,		! Custom Panel
			51,stDoorTypes[50],0		! Empty

return

"GarageLeafPanels":

	ui_infield{3} "iDoorPanelType", x1, y0, picPopupWidth, picPopupHeight2,
		2, 3,											! GaragePanelTypes.png					
		15, 5,													
		95, 110, 90, 80,
		1,`Стиль 1`,1,
		2,`Стиль 2`,2,
		3,`Стиль 3`,3,
		4,`Стиль 4`,4,
		5,`Стиль 5`,5,
		6,`Стиль 6`,10,
		7,`Стиль 7`,11,
		8,`Стиль 8`,12,
		15,`Стиль 9`,14,
		14,`Специальное Полотно Двери`,31,
		9,`Пусто`,0
return

"GarageSectPanels":

	ui_infield{3} "iDoorPanelType", x1, y0, picPopupWidth, picPopupHeight2,
		2, 3,											! GaragePanelTypes.png					
		15, 5,													
		95, 110, 90, 90,
		1,`Стиль 1`,1,
		2,`Стиль 2`,2,
		8,`Стиль 3`,3,
		14,`Специальное Полотно Двери`,31,
		9,`Пусто`,0
return

"HandleTypes":
	ui_style 0, 1
	ui_outfield `Ручка`, x1, y0, xSep1-x1-1, heightOutf
	ui_style 0, 0
	y0 = y0 + dy

	gosub "handleTypePicPopup"
return


"OpeningTypesTextPopup":

	y0 = pageStartY
	
	ui_style 0,1
	ui_outfield `Тип Открывания`, x3, y0, x2, heightOutf		! Opening Type
	
	y0 = y0 + 18
	
	ui_style 0,0
	

	ui_infield{3} "gs_leaf_optype_m", x3, y0, x2, heightInf,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", stLeafOpeningType[1], 1,		! Side Hung
			"", stLeafOpeningType[2], 2,		! Side Hung Mirrored
			"", stLeafOpeningType[3], 3,		! Double Acting
			"", stLeafOpeningType[4], 4,		! Double Acting Mirrored
			"", stLeafOpeningType[5], 5,		! Side Hung + " "
			"", stLeafOpeningType[6], 6,		! Double Acting + " "
			"", stLeafOpeningType[7], 7,		! Double Egress
			"", stLeafOpeningType[8], 8,		! Double Egress Mirrored
			"", stLeafOpeningType[9], 9,		! Pivot
			"", stLeafOpeningType[10], 10,		! Pivot Mirrored
			"", stLeafOpeningType[11], 11,		! 2 Leaves 1 Sliding
			"", stLeafOpeningType[12], 12,		! 2 Leaves 1 Sliding Mirrored
			"", stLeafOpeningType[13], 13,		! 2 Sliding Leaves
			"", stLeafOpeningType[14], 14,		! 2 Sliding Leaves Mirrored
			"", stLeafOpeningType[15], 15,		! 3 Leaves 2 Sliding
			"", stLeafOpeningType[16], 16,		! 3 Leaves 2 Sliding Mirrored
			"", stLeafOpeningType[17], 17,		! 4 Leaves 2 Sliding
			"", stLeafOpeningType[18], 18,		! 4 Leaves 3 Sliding
			"", stLeafOpeningType[19], 19,		! 4 Leaves 3 Sliding Mirrored
			"", stLeafOpeningType[20], 20,		! 4 Sliding Leaves
			"", stLeafOpeningType[21], 21,		! 4 Sliding Leaves Mirrored
			"", stLeafOpeningType[22], 22,		! External Sliding
			"", stLeafOpeningType[23], 23,		! External Sliding Mirrored
			"", stLeafOpeningType[24], 24,		! External 2 Sliding
			"", stLeafOpeningType[25], 25,		! Pocket
			"", stLeafOpeningType[26], 26,		! Pocket Mirrored
			"", stLeafOpeningType[27], 27,		! Double Pocket
			"", stLeafOpeningType[28], 28,		! Sliding Folding Multi-panel
			"", stLeafOpeningType[29], 29,		! Sliding Folding Multi-panel Mirrored
			"", stLeafOpeningType[30], 30		! Double Sliding Folding Multi-panel
	
	ui_pict_button ui_function, "", "uiFunctionDoorOpening_flat(1)", picPopupWidth + x3 + 10, y0, 36, 22, -iUiID_OpeningType ! Set GLOB_UI_BUTTON_ID
return

"OpeningTypesPicPopup":

	y0 = pageStartY
	
	ui_style 0,1
	ui_outfield `Тип Открывания`, x3, y0, x2, heightOutf		! Opening Type
	
	y0 = y0 + 18
	
	ui_style 0,0
	
	ui_infield{3} "gs_leaf_optype_m", x3, y0, picPopupWidth, picPopupHeight1,
			2, 4, 6, 6,														! SlidingOpTypes.png			70, 70, 50, 50,
			79, 62, 77, 60,
			0, stLeafOpeningType[1], 1,		! Side Hung
			0, stLeafOpeningType[2], 2,		! Side Hung Mirrored
			0, stLeafOpeningType[3], 3,		! Double Acting
			0, stLeafOpeningType[4], 4,		! Double Acting Mirrored
			0, stLeafOpeningType[5], 5,		! Side Hung` + " "
			0, stLeafOpeningType[6], 6,		! Double Acting` + " "
			0, stLeafOpeningType[7], 7,		! Double Egress
			0, stLeafOpeningType[8], 8,		! Double Egress Mirrored
			0, stLeafOpeningType[9], 9,		! Pivot
			0, stLeafOpeningType[10], 10,		! Pivot Mirrored
			1, stLeafOpeningType[11], 11,		! 2 Leaves 1 Sliding
			1, stLeafOpeningType[12], 12,		! 2 Leaves 1 Sliding Mirrored
			2, stLeafOpeningType[13], 13,		! 2 Sliding Leaves
			2, stLeafOpeningType[14], 14,		! 2 Sliding Leaves Mirrored
			3, stLeafOpeningType[15], 15,		! 3 Leaves 2 Sliding
			3, stLeafOpeningType[16], 16,		! 3 Leaves 2 Sliding Mirrored
			4, stLeafOpeningType[17], 17,		! 4 Leaves 2 Sliding
			5, stLeafOpeningType[18], 18,		! 4 Leaves 3 Sliding
			5, stLeafOpeningType[19], 19,		! 4 Leaves 3 Sliding Mirrored
			6, stLeafOpeningType[20], 20,		! 4 Sliding Leaves
			6, stLeafOpeningType[21], 21,		! 4 Sliding Leaves Mirrored
			0, stLeafOpeningType[22], 22,		! External Sliding
			0, stLeafOpeningType[23], 23,		! External Sliding Mirrored
			0, stLeafOpeningType[24], 24,		! External 2 Sliding
			0, stLeafOpeningType[25], 25,		! Pocket
			0, stLeafOpeningType[26], 26,		! Pocket Mirrored
			0, stLeafOpeningType[27], 27,		! Double Pocket
			0, stLeafOpeningType[28], 28,		! Sliding Folding Multi-panel
			0, stLeafOpeningType[29], 29,		! Sliding Folding Multi-panel Mirrored
			0, stLeafOpeningType[30], 30		! Double Sliding Folding Multi-panel

	y0 = y0 + 42
	
	ui_pict_button ui_function, "", "uiFunctionDoorOpening_flat(1)", picPopupWidth + x3 + 10, y0, 36, 22, -iUiID_OpeningType ! Set GLOB_UI_BUTTON_ID
return



"NumberOfLeaves":

	if	gs_leaf_optype_m = LEAF_1L_MULTIPANEL |\
		gs_leaf_optype_m = LEAF_1L_MULTIPANEL_M |\
		gs_leaf_optype_m = LEAF_2L_MULTIPANEL then

		y0 = pageStartY
		widthOutf = 145
		
		ui_style 0,1
		ui_outfield `Количество Полотен`, x3, y0, widthOutf, heightOutf	
		
		ui_style 0,0

		if gs_leaf_optype_m = LEAF_2L_MULTIPANEL then y0 = y0 + 23

		ui_infield "gs_leaf_num", x3 + widthOutf + 2, y0-4, 60, heightInf

		y0 = y0 + 23

		if gs_leaf_optype_m = LEAF_2L_MULTIPANEL then y0 = y0 + 23

		ui_infield{3} "bMiddleStacking", x3, y0-4, x2-2, heightInf,
				7, "",
				0,
				0, 0, 0, 0, 0,
				"", `Складываются Посередине`, 0,
				"", `Складываются Посередине`, 1

		y0 = y0 - 48

		if gs_leaf_optype_m = LEAF_2L_MULTIPANEL then

			ui_outfield `Сторона 1`, x3, y0, widthOutf, heightOutf
			y0 = y0 + 23

			ui_outfield `Сторона 2`, x3, y0, widthOutf, heightOutf
			ui_infield "gs_leaf_num2",  x3 + widthOutf + 2, y0-4, 60, heightInf
		endif

	ui_separator xSep2, pageStartY + 96, 444, pageStartY + 96

	endif

return

"PivotShape":

	x3 		= 225
	x4 		= 268
	y0 		= pageStartY + 8
	picx 	= 82
	picy 	= 113
	cx 		= 80

	ui_pict 5, x4, y0, picx, picy, 1															! PivotShape.png
	ui_infield "gs_pivotOffset", x4 + 85, y0 + 103, cx, heightInf ui_tooltip `Смещение Оси Поворота`

	ui_separator xSep2, pageStartY + 137, 444, pageStartY + 137

return

"PocketFrame":

	if	gs_leaf_optype_m = LEAF_1L_POCKET	|\
		gs_leaf_optype_m = LEAF_1L_POCKET_M	|\
		gs_leaf_optype_m = LEAF_2L_POCKET	then
	
		x4 = 220
		y0 = pageStartY
	
		ui_style 0,1
		ui_outfield `Коробка`, x3, y0, x4-2, heightOutf	
	
		y0 = y0 + 21
	
		ui_style 0,0
		ui_infield{3} "gs_pocket_door_frame", x3, y0, x4-2, heightInf,
			7, "",
			0,
			0, 0, 0, 0, 0,
			"", `Коробка Двери`, 0,
			"", `Коробка Двери`, 1
	
		y0 = y0 + 21
	
		ui_infield{3} "gs_pocket_frame", x3, y0, x4-2, heightInf,
			7, "",
			0,
			0, 0, 0, 0, 0,
			"", `Рама Кармана`, 0,
			"", `Рама Кармана`, 1
	
		y0 = y0 + 21
	
		ui_infield{3} "gs_frame_wthk", x3, y0, x4-2, heightInf,
			7, "",
			0,
			0, 0, 0, 0, 0,
			"", `Толщина Коробки=Толщине Стены`, 0,
			"", `Толщина Коробки=Толщине Стены`, 1
	
		y0 = y0 + 21
	
		ui_infield{3} "gs_leaf_center", x3, y0, x4-2, heightInf,
			7, "",
			0,
			0, 0, 0, 0, 0,
			"", `Полотно по Центру`, 0,
			"", `Полотно по Центру`, 1

		y0 = y0 + 21
		ui_pict_button ui_function, "", "uiFunctionFrameLeaf_flat(1)", picPopupWidth + x3 + 10, y0, 36, 22, -iUiID_FrameSettings ! Set GLOB_UI_BUTTON_ID

		ui_separator xSep2, pageStartY + 190, 444, pageStartY + 190

	endif

return

"GarageShape":

	x4 = 260
	y0 = pageStartY + 7

	pict_x = 110
	pict_y = 120

	ui_pict 6, x4, y0, pict_x, pict_y, 1											! GarageDoorShape.png

	y0 = y0 + 104
	ui_infield "gs_rttl", x4 + 113, y0, 30, 20 ui_tooltip `Правое Полотно/Общая Ширина`
	ui_outfield "%", x4 + 145, y0 + 4, 10, 20

	ui_separator xSep2, pageStartY + 138, 444, pageStartY + 138
return

"GarageSection":

	x4 = 280
	y0 = pageStartY + 23
	widthOutf = 162

	ui_style 0, 1
	if iDoorPanelType = 0 then bFlag = 4 else bFlag = 0
	ui_outfield `Количество Секций`, x3 + 5, y0, widthOutf, heightOutf, bFlag
	ui_style 0, 0
	ui_infield "gs_section_num", x3 + 5 + widthOutf + 2, y0-4, 45, heightInf

	y0= y0 + 23
	ui_style 0, 1
	ui_infield{3} "gs_passdoor", x3 + 5, y0, 209, heightInf,
		7, "",
		0,
		0, 0, 0, 0, 0,
		"", `Дверь`,	1,
		"", `Дверь`,	2

	ui_separator xSep2, pageStartY + 190, 444, pageStartY + 190
return

"GaragePanel":

	x4 = 135
	x5 = 283
	y0 = pageStartY + 3
	cx = 65

	if gs_doorcode = "HSD" then
		!--- only high-speed door

		ui_style 0, 0
		ui_infield{3} "gs_bGlazedDoor", x4, y0, 150, heightInf,
			7, "",
			0,
			0, 0, 0, 0, 0,
			"", `С Остеклением`,	1,
			"", `С Остеклением`,	2

		y0 = y0 + 36

		if gs_bGlazedDoor then
			pict_x = 142
			pict_y = 110
			ui_pict "ui_door_high_speed_g(1)", x4, y0, pict_x, pict_y, 1

			ui_infield "gs_glass_section_width", x5, y0+42, cx, heightInf ui_tooltip `Ширина Стеклянной Секции`
			ui_infield "gs_glass_section_elev", x5, y0+75, cx, heightInf ui_tooltip `Возвышение Стеклянной Секции`
		else
			pict_x = 120
			pict_y = 110
			ui_pict "ui_door_high_speed(1)", x4, y0, pict_x, pict_y, 1
		endif
	endif

	if gs_doorcode = "HSRD" then
		!--- only high-speed roll up door

		ui_style 0, 0
		ui_infield{3} "gs_bGlazedDoor", x4, y0, 150, heightInf,
			7, "",
			0,
			0, 0, 0, 0, 0,
			"", `С Остеклением`,	1,
			"", `С Остеклением`,	2

		y0 = y0 + 36

		if gs_bGlazedDoor then
			pict_x = 142
			pict_y = 110
			ui_pict "ui_door_high_speed_roll_up_g(1)", x4, y0, pict_x, pict_y, 1

			ui_infield "gs_glass_section_width", x5, y0+42, cx, heightInf ui_tooltip `Ширина Стеклянной Секции`
			ui_infield "gs_glass_section_elev", x5, y0+75, cx, heightInf ui_tooltip `Возвышение Стеклянной Секции`
		else
			pict_x = 120
			pict_y = 110
			ui_pict "ui_door_high_speed_roll_up(1)", x4, y0, pict_x, pict_y, 1
		endif
	endif
	ui_separator 0, pageStartY + 167, 444, pageStartY + 167
return


"FrameStyle":
	if bBombShelter then
		_xStart = x1
		y0 = 266 - 3*22
	else
		_xStart = x3
		y0 = pageStartY + 98
	endif
		
	ui_style 0,1
	ui_outfield `Коробка`, _xStart, y0, textPopupWidth, heightOutf, 4*bForceToShowFrameStyle
	
	y0 = y0 + 20
	
	ui_style 0,0

	if not(bFrameWOutRebate) | bForceToShowFrameStyle | bEnableNEDFrameStyles then
	ui_infield{3} "gs_frame_style_m", _xStart, y0, textPopupWidth, heightInf,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", gs_stDoorFrame[1], FRAME_STYLE_BLOCK,				! Block
			"", gs_stDoorFrame[2], FRAME_STYLE_DOUBLE_REBATE,		! Double Rebate
			"", gs_stDoorFrame[3], FRAME_STYLE_SIMPLE_REBATE,		! Simple Rebate
			"", gs_stDoorFrame[4], FRAME_STYLE_METAL,				! USA
			"", gs_stDoorFrame[5], FRAME_STYLE_DOUBLE_EGRESS_METAL,	! USA
			"", gs_stDoorFrame[6], FRAME_STYLE_EXTRUSION,			! Extrusion
			"", gs_stDoorFrame[7], FRAME_STYLE_WRAPPED_EXTRUSION		! Wrapped Extrusion

		_yJumpButton = y0
	else
		_yJumpButton = y0 - 20	
	endif

	ui_pict_button ui_function, "", "uiFunctionFrameLeaf_flat(1)", textPopupWidth + _xStart + 10, _yJumpButton, 36, 22, -iUiID_FrameSettings ! Set GLOB_UI_BUTTON_ID
return


"VentilationType":
	_xStart = x3

	ui_style 0,1
	
	ui_infield{3} "bVentilationProfile", _xStart, y0, textPopupWidth + 46, heightInf,
		7, "",
		0,
		0, 0, 0, 0, 0,
		"", `Приточная Вентиляция`, 0,	
		"", `Приточная Вентиляция`, 1		
	
	y0 = y0 + 20

	ui_style 0, 0

	ui_infield{3} "iVentilationProfileType", _xStart, y0, textPopupWidth, heightInf,
			8, "",
			0,
			0, 0, 0, 0, 0,
			"", stVentilationProfileType[2], VENTPROFILE_OVER_FRAME,		! Over Frame
			"", stVentilationProfileType[3], VENTPROFILE_OVER_GLAZING	! Over Glazing
	


	if bGarageDoor then
		ui_pict_button ui_function, "", "uiFunctionVentilation_flat(1)", textPopupWidth + _xStart + 10, y0, 36, 22, -iUiID_VentProfileGar ! Set GLOB_UI_BUTTON_ID
	else
		ui_pict_button ui_function, "", "uiFunctionVentilation_flat(1)", textPopupWidth + _xStart + 10, y0, 36, 22, -iUiID_VentProfile ! Set GLOB_UI_BUTTON_ID
	endif
	ui_separator x3-5, y0 + 25, 442, y0 + 25

return

"DetailLevel":

	widthOutf = 106
	widthInf = 106

	if bBombShelter then
		ui_separator xSep2, y0-9, 444, y0-9
	endif
	ui_style 0,1
	ui_outfield `Детализация`, x3, y0, x2-2, heightOutf

	y0 = y0 + 23

	ui_style 0,0

	ui_outfield `3D-детализация`, x3,y0, widthOutf, heightOutf	
	ui_infield{3} "iDWDet3D",x3 + widthOutf + 2, y0-4, widthOutf,heightInf,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stDetail[1],	DETLEVEL_DW3D_SCALESENS,
			"",	stDetail[2],	DETLEVEL_DW3D_DETAILED,
			"",	stDetail[3],	DETLEVEL_DW3D_SIMPLE,
			"",	stDetail[4],	DETLEVEL_DW3D_OFF,
			"",	stDetail[5],	DETLEVEL_DW3D_CHI_SIMPLE,
			"",	stDetail[6],	DETLEVEL_DW3D_CHI_DETAILED

	y0 = y0 + 23

	ui_outfield `2D-детализация`, x3, y0, widthOutf, heightOutf	
	ui_infield{3} "iDWDet2D", x3 + widthOutf + 2, y0-4, widthOutf, heightInf,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stDetail2D[1],	DETLEVEL_DW2D_MVO,
			"",	stDetail2D[2],	DETLEVEL_DW2D_SCSENS,
			"",	stDetail2D[3],	DETLEVEL_DW2D_50,
			"",	stDetail2D[4],	DETLEVEL_DW2D_100,
			"",	stDetail2D[5],	DETLEVEL_DW2D_200,
			"",	stDetail2D[6],	DETLEVEL_DW2D_OFF,
			"",	stDetail2D[7],	DETLEVEL_DW2D_CHISMPL,
			"",	stDetail2D[8],	DETLEVEL_DW2D_CHIDET,
			"",	stDetail2D[9],	DETLEVEL_DW2D_20

	y0 = y0 + 23
return



"HandleParsBombShelter":
	y0 = y0 + picPopupHeight1 + 10
	_xInfield = xSep1 - 5 - 75
	ui_outfield `Высота`, x1, y0, _xInfield-x1-1, heightOutf, 4*(gs_handle_type_m = 0)
	ui_infield "gs_han_height", _xInfield, y0-4, 75, heightInf
	y0 = y0 + dy
	ui_outfield `Расположение`, x1, y0, _xInfield-x1-1, heightOutf, 4*(gs_handle_type_m = 0)
	ui_infield "gs_han_pos", _xInfield, y0-4, 75, heightInf
	y0 = y0 + dy
	ui_outfield `Имя Спец. Ручки`, x1, y0, xSep1-x1-1, heightOutf, 4*(gs_handle_type_m <> 99 | gs_handle_type_m = 0)
	y0 = y0 + dy
	ui_infield "gs_cust_handle", xSep1 - 5 - picPopupWidth, y0-4, picPopupWidth, heightInf

	y0 = y0 + dy
	ui_separator 0, y0, xSep1 - 5, y0
	y0 = y0 + 5
return


"BombShelterShape":
	_xPic = 270
	_yPic = pageStartY + 50
	widthOutf = 106
	widthInf = 106

	if iOpeningMethod = OPENMETHOD_OPEN_DOUB then
		ui_pict 7, _xPic, _yPic, 88, 115, 1		! bombShelterShapeDouble.png
		ui_infield "gs_SecondLeaf_w", _xPic + 88 + 5, _yPic + 115 - 22, 75, heightInf ui_tooltip `Ширина Полотна со Стороны 1`

		_y = _yPic + 115 + 5
		ui_outfield `Полотна`, x3, _y, widthOutf, heightOutf
 		ui_infield{3} "gs_iLeavesConfig", x3 + widthOutf + 2, _y-4, widthOutf, heightInf,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",`Одинаковые Полотна`, LEAVESCONFIG_UNIFORM,
			"", `Асимметрично`, LEAVESCONFIG_ASYMM

	else
		ui_pict 8, _xPic + 30, _yPic + 15, 62, 103, 1		! bombShelterShape.png
		
	endif
return




"handleTypePicPopup":
	ui_infield{3} "gs_handle_type_m", _xHandle, y0, picPopupWidth, picPopupHeight1,
			2, 2, 27, 9,														! HandleTypes.png
			70, 70, 55, 48,
			 27,stHandleTypesNone, 0,
			 1,stHandleTypes[ 1], 1,
			 2,stHandleTypes[ 2], 2,
			 3,stHandleTypes[ 3], 3,
			 4,stHandleTypes[ 4], 4,
			 5,stHandleTypes[ 5], 5,
			 6,stHandleTypes[ 6], 6,
			 7,stHandleTypes[ 7], 7,
			 8,stHandleTypes[ 8], 8,
			 9,stHandleTypes[ 9], 9,
			10,stHandleTypes[10], 10,
			11,stHandleTypes[11], 11,
			12,stHandleTypes[12], 12,
			13,stHandleTypes[13], 13,
			14,stHandleTypes[14], 14,
			15,stHandleTypes[15], 15,
			16,stHandleTypes[16], 16,
			17,stHandleTypes[17], 17,
			18,stHandleTypes[18], 18,
			19,stHandleTypes[19], 19,
			20,stHandleTypes[20], 20,
			21,stHandleTypes[21], 21,
			22,stHandleTypes[22], 22,
			23,stHandleTypes[23], 23,
			24,stHandleTypes[24], 24,
			25,stHandleTypes[25], 25,
			26,stHandleTypesCustom, 99 ui_tooltip `Выбор Ручки`
return

