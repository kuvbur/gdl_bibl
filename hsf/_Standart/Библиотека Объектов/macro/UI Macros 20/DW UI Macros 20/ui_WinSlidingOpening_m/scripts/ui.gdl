
if iWindowType = WIN_W4HorizontalSlide then
	stMainSash1 = `Основная Створка`
	stMainSash2 = `Боковая Створка`

	if iUTrans = 2 then					! Type 2
		stUpperSash1 = `Верхняя Створка 1`
		stUpperSash2 = `Верхняя Створка 2`
	else
		stUpperSash1 = `Верхняя Створка`
		stUpperSash2 = `Верхняя Боковая Створка`
	endif
else
	stMainSash1 = `Тип Основных Створок`

	if iUTrans = 4 then					! Type 2
		stUpperSash1 = `Верхние Створки`
	else
		stUpperSash1 = `Верхняя Средняя Створка`
	endif
	stUpperSash2 = `Верхние Боковые Створки`
endif

if bShowAngleValues then
	gosub "OpeningSettings"
else
	pageStartY 	= 4
	x1 			= 174
	x2 			= 316
	y0 			= pageStartY
	dy 			= 25

	ui_style 0, 1
	ui_outfield `Тип Открывания`, x1, y0, 240, 15	
	y0 = y0 + 23
	ui_style 0, 0

	if iWindowType = WIN_W2HorizontalSlide then
		ui_outfield `Основная Створка`, x1, y0, 140, 15
		y0 = y0 + dy - 5
		_x2 = x2 - 60
		_yCurr = y0
		gosub "opTypePopup2Sash"

		y0 = y0 + 66 + 5
	endif

	if iWindowType = WIN_W3HorizontalSlide then
		ui_outfield `Основные Створки`, x1, y0, 140, 15
		y0 = y0 + dy - 5

		_x2 = x2 - 60
		_yCurr = y0
		gosub "opTypePopup3Sash"

		y0 = y0 + 66 + 5
	endif

	if iWindowType = WIN_W4HorizontalSlide then
		ui_outfield `Основные Створки`, x1, y0, 140, 15
		y0 = y0 + dy - 5

		_x2 = x2 - 60
		_yCurr = y0
		gosub "opTypePopup4Sash"

		y0 = y0 + 66 + 5
	endif

	if iWindowType = WIN_W6HorizontalSlide then
		ui_outfield `Число Направляющих`, x1, y0, 140, 15
		y0 = y0 + dy - 5

		_x2 = x2 - 60
		_yCurr = y0
		gosub "opTypePopup6Sash"

		y0 = y0 + 66 + 5
	endif

	if iUTrans > 0 then
		_x1 = x1
		_xInf = x2
		_popupWidth = 126
		_yCurr = y0
		_outText = stUpperSash1
		_optypeParamName = "iOpeningTypeTransMain"
		gosub "transomOpTypePopup"

		y0 = y0 + dy

		if iUTrans <> 4 then
			_yCurr = y0
			_outText = stUpperSash2
			_optypeParamName = "iOpeningTypeTransSide"
			gosub "transomOpTypePopup"
		endif
		y0 = 170
	endif
	
	ui_pict_button ui_function, "", "uiFunctionWindowOpening_flat(1)", 406, y0-4, 36, 22, -iUiID_OpeningType ! Set GLOB_UI_BUTTON_ID
endif

! ================================================================================
end
! ================================================================================


"OpeningSettings":
	pageStartY = 0
	
	ui_style 0, 0
	dx = 222
	_x1 = 10
	_x2 = 256
	_yCurr = pageStartY
	_dy = 26

	ui_style 0, 1
	ui_outfield `Тип Основных Створок`, _x1, _yCurr, _x2-_x1-2, 15
	ui_style 0, 0
	_yCurr = _yCurr + 16
	
	if iWindowType = WIN_W2HorizontalSlide then
		gosub "opTypePopup2Sash"
	endif
	if iWindowType = WIN_W3HorizontalSlide then
		gosub "opTypePopup3Sash"
	endif
	if iWindowType = WIN_W4HorizontalSlide then
		gosub "opTypePopup4Sash"
	endif
	if iWindowType = WIN_W6HorizontalSlide then
		gosub "opTypePopup6Sash"
	endif

	_yCurr	= 88
	_yTemp = _yCurr
	_dy		= 21
	_infWidth = 80
	_x2		= _x1 + 10
	_x3		= _x1 + 122
	_xEnd	= _x3 + _infWidth

	dim _stOpLengthParNames[6]

	! 2D Opening Width
	_stColumnTitle = `Ширина Открывания 2D`
	_stOpLengthParNames[1] = "openingLength2D_01"
	_stOpLengthParNames[2] = "openingLength2D_02"
	_stOpLengthParNames[3] = "openingLength2D_03"
	_stOpLengthParNames[4] = "openingLength2D_04"
	_stOpLengthParNames[5] = "openingLength2D_05"
	_stOpLengthParNames[6] = "openingLength2D_06"

	gosub "opLengthColumns"
	if iUTrans > 0 then
		gosub "transomMainOptype"
	endif


	! 3D Opening Width
	_x1		= 232
	_x2		= _x1 + 10
	_x3		= _x1 + 130
	_xEnd	= _x3 + _infWidth
	_yCurr	= _yTemp

	_stColumnTitle = `Ширина Открывания 3D`
	_stOpLengthParNames[1] = "openingLength_01"
	_stOpLengthParNames[2] = "openingLength_02"
	_stOpLengthParNames[3] = "openingLength_03"
	_stOpLengthParNames[4] = "openingLength_04"
	_stOpLengthParNames[5] = "openingLength_05"
	_stOpLengthParNames[6] = "openingLength_06"

	gosub "opLengthColumns"

	if iUTrans <> 4 and iUTrans > 0 then
		gosub "transomSideOptype"
	endif
return


"opLengthColumns":
	ui_outfield _stColumnTitle, _x1, _yCurr, _xEnd-_x1, 15
	_yCurr = _yCurr + _dy

	if iWindowType = WIN_W2HorizontalSlide then
		ui_outfield `Сторона 1`, _x2, _yCurr, _x3-_x2-2, 15
		ui_infield{4} _stOpLengthParNames[1], _x3, _yCurr-4, _infWidth, 19
		_yCurr = _yCurr + _dy
		
		if iOpeningTypeMain = OPENINGTYPE_MAIN_DOUBLE then
			ui_outfield `Сторона 2`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[3], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
		endif
	endif

	if iWindowType = WIN_W3HorizontalSlide then
		if	iOpeningTypeMain = OPENINGTYPE_MAIN_2SASHES |\
			iOpeningTypeMain = OPENINGTYPE_MAIN_3SASHES |\
			iOpeningTypeMain = OPENINGTYPE_MAIN_1SIDE then

			ui_outfield `Сторона 1`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[3], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
		endif	

		if	iOpeningTypeMain = OPENINGTYPE_MAIN_3SASHES  |\
			iOpeningTypeMain = OPENINGTYPE_MAIN_1MIDDLE then

			ui_outfield `Средняя`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[1], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
		endif

		if	iOpeningTypeMain = OPENINGTYPE_MAIN_2SASHES |\
			iOpeningTypeMain = OPENINGTYPE_MAIN_3SASHES then

			ui_outfield `Сторона 2`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[4], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
		endif
	endif

	if iWindowType = WIN_W4HorizontalSlide then
		ui_outfield `Средняя 1`, _x2, _yCurr, _x3-_x2-2, 15
		ui_infield{4} _stOpLengthParNames[1], _x3, _yCurr-4, _infWidth, 19
		_yCurr = _yCurr + _dy

		ui_outfield `Средняя 2`, _x2, _yCurr, _x3-_x2-2, 15
		ui_infield{4} _stOpLengthParNames[2], _x3, _yCurr-4, _infWidth, 19
		_yCurr = _yCurr + _dy

		if	iOpeningTypeMain = OPENINGTYPE_MAIN_4SASHES then

			ui_outfield `Сторона 1`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[3], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy

			ui_outfield `Сторона 2`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[4], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
		endif
	endif

	if iWindowType = WIN_W6HorizontalSlide then
		ui_outfield `Средняя 1`, _x2, _yCurr, _x3-_x2-2, 15
		ui_infield{4} _stOpLengthParNames[1], _x3, _yCurr-4, _infWidth, 19
		_yCurr = _yCurr + _dy

		ui_outfield `Средняя 2`, _x2, _yCurr, _x3-_x2-2, 15
		ui_infield{4} _stOpLengthParNames[2], _x3, _yCurr-4, _infWidth, 19
		_yCurr = _yCurr + _dy

		if iNumberOfRails = RAILSNUMBER_2 then
			ui_outfield `Сторона 1/1`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[3], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
	
			ui_outfield `Сторона 1/2`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[4], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
	
	
			ui_outfield `Сторона 2/1`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[5], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
	
			ui_outfield `Сторона 2/2`, _x2, _yCurr, _x3-_x2-2, 15
			ui_infield{4} _stOpLengthParNames[6], _x3, _yCurr-4, _infWidth, 19
			_yCurr = _yCurr + _dy
		endif
	endif
return

"transomMainOptype":
	_yCurr = 195

	ui_separator 0, _yCurr, 444, _yCurr

	if iUTrans = 3 then
		ui_separator 222, _yCurr + 4, 222, 264
	endif
	_yCurr = _yCurr + 4
	
	ui_style 0, 1
	ui_outfield stUpperSash1, _x1, _yCurr, 180, 15
	ui_style 0, 0
	_yCurr = _yCurr + 26

	_outText = `Тип Створки`
	_optypeParamName = "iOpeningTypeTransMain"
	_xInf = _x3 - 20
	_popupWidth = 100

	gosub "transomOpTypePopup"
	_yCurr = _yCurr + 21

	if iOpeningTypeTransMain = 1 then
		__flagOpAngle = 4
	else
		__flagOpAngle = 0
	endif
	
	ui_outfield `Угол Открывания в 3D`, _x1, _yCurr, _x3-_x1-2, 15, __flagOpAngle
	ui_infield "openingAngle_05", _x3, _yCurr-4, 80, 19
return


"transomSideOptype":
	_yCurr = 195

	_yCurr = _yCurr + 4
	ui_style 0, 1
	ui_outfield stUpperSash2, _x1, _yCurr, 180, 15
	ui_style 0, 0
	_yCurr = _yCurr + 26

	_outText = `Тип Створки`
	_optypeParamName = "iOpeningTypeTransSide"
	_xInf = _x3 - 20
	_popupWidth = 100
	gosub "transomOpTypePopup"
	_yCurr = _yCurr + 21

	if iOpeningTypeTransSide = 1 then
		__flagOpAngle = 4
	else
		__flagOpAngle = 0
	endif

	ui_outfield `Угол Открывания в 3D`, _x1, _yCurr, _x3-_x1-2, 15, __flagOpAngle
	ui_infield "openingAngle_07", _x3, _yCurr-4, 80, 19

return


"transomOpTypePopup":
	ui_outfield _outText, _x1, _yCurr, _xInf-_x1-2, 15
	ui_infield{4} _optypeParamName, _xInf, _yCurr-4, _popupWidth, 19,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", st_optype[1],	SASH_FIXED_SL4,
				"", st_optype[2],	SASH_SLIDING_SL4,
				"", st_optype[3],	SASH_SIDE_HUNG_SL4,
				"", st_optype[4],	SASH_SIDE_HUNG_M_SL4,
				"", st_optype[5],	SASH_BOTTOM_HUNG_SL4,
				"", st_optype[6],	SASH_TOP_HUNG_SL4,
				"", st_optype[7],	SASH_TILT_TURN_SL4,
				"", st_optype[8],	SASH_TILT_TURN_M_SL4,
				"", st_optype[9],	SASH_VER_PIVOT_SL4
return


"opTypePopup2Sash":
	ui_infield{4} "iOpeningTypeMain", _x2, _yCurr, 186, 66,
			2, 2,					! W2HorizSlideOpTypes.png
			2, 1,
			77, 60, 52, 52,
			1, stOpeningTypeMain[1],	OPENINGTYPE_MAIN_SINGLE,
			2, stOpeningTypeMain[2],	OPENINGTYPE_MAIN_DOUBLE

return


"opTypePopup3Sash":
	ui_infield{4} "iOpeningTypeMain", _x2, _yCurr, 186, 66,
			2, 1,					! W3HorizSlideOpTypes.png
			4, 1,
			77, 60, 52, 52,
			1, stOpeningTypeMain[3],	OPENINGTYPE_MAIN_1SIDE,
			2, stOpeningTypeMain[4],	OPENINGTYPE_MAIN_1MIDDLE,
			3, stOpeningTypeMain[1],	OPENINGTYPE_MAIN_2SASHES,
			4, stOpeningTypeMain[2],	OPENINGTYPE_MAIN_3SASHES
return


"opTypePopup4Sash":
	ui_infield{4} "iOpeningTypeMain", _x2, _yCurr, 186, 66,
			2, 3,					! W4HorizSlideOpTypes.png
			3, 1,
			77, 60, 52, 52,
			1, stOpeningTypeMain[1],	OPENINGTYPE_MAIN_SAMERAIL,
			2, stOpeningTypeMain[2],	OPENINGTYPE_MAIN_DIFFRAIL,
			3, stOpeningTypeMain[3],	OPENINGTYPE_MAIN_4SASHES
return


"opTypePopup6Sash":
	ui_infield{4} "iNumberOfRails", _x2, _yCurr, 186, 66,
			2, 4,					! W6HorizSlideOpTypes.png
			2, 1,
			77, 60, 77, 52,
			1, stNumberOfRails[1],	RAILSNUMBER_2,
			2, stNumberOfRails[2],	RAILSNUMBER_3

return

