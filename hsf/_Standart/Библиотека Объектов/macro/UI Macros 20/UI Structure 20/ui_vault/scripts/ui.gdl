


!iVaultType
VAULT_BARREL 		= 1
VAULT_3CENTERED 	= 2
VAULT_CROSS 		= 3
VAULT_DOMINICAL 	= 4
VAULT_CROSS_2 		= 5
VAULT_3CENTCROSS 	= 6

! const values for parameter: gs_detlevel_3D_m
DLEV3D_OFF      = 0
DLEV3D_SIMPLE   = 1
DLEV3D_DETAILED = 2

dim stInfVault[]
	stInfVault[1] = `С одной стороны`
	stInfVault[2] = `Обе Стороны`

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 		= -1
_idxTab = 1
TABID_DIMENSIONS	= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES 		= _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Свода`
ui_current_page gs_ui_current_page

if 	iVaultType = VAULT_CROSS | iVaultType = VAULT_DOMINICAL then
	dimensionTabTitle = `Размеры`
	dimensionTabPict = "uiTab_Dimension_flat(1)"
else
	dimensionTabTitle = `Стиль и Размеры`
	dimensionTabPict = "uiTab_Dimension_flat(1)"
endif

ui_page TABID_DIMENSIONS, TABID_ROOT, dimensionTabTitle, dimensionTabPict 
gosub "dimensionsPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"


!--------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ======================================================================
end
! ======================================================================

"dimensionsPage":

	x1 = 0
	x2 = 120
	x3 = 210
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 360
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	
	ui_style 0, 0

	if iVaultType = VAULT_BARREL | iVaultType = VAULT_3CENTERED then
		ui_infield{3} "gs_wallcut",	x1,		yCurrent,		xEnd-x1,	infieldHeight,
			7,"",0,0,
			0,0,0,0,
			"",`Выравнивание по Стене`, 0,
			"",`Выравнивание по Стене`, 1
	
		yCurrent = yCurrent + dy
	
		ui_separator 0, yCurrent, xEnd, yCurrent
			
	
		uiPictX = 100
		uiPictY = 80
		uiPictWdt = 253
		uiPictHgt = 117
		uiPictMargin = 5
		uiPictInfWdt = 60
		infieldHeight 		= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

		if iVaultType = VAULT_BARREL then 
			if not(gs_wallcut) then
				ui_pict 1, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
			else
				ui_pict 2, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
			endif
		else
			if not(gs_wallcut) then
				ui_pict 3, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
			else
				ui_pict 4, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
			endif		
		endif

		ui_infield "A", uiPictX+100, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Длина Свода`	
	
		ui_infield "shell_thk", uiPicRightStart, uiPictY-5, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина Свода`
		if iVaultType = VAULT_BARREL then
			ui_infield "radius_1", uiPicRightStart, uiPictY+45, uiPictInfWdt, infieldHeight UI_TOOLTIP `Внешний Радиус Свода`
		endif

		if iVaultType = VAULT_3CENTERED then
			ui_infield "B", uiPicLeftStart, uiPictY+45, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Свода`
			ui_infield "ZZYZX", uiPicRightStart, uiPictY+45, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Свода`
			ui_infield "radius_1", uiPicRightStart, uiPictY+89, uiPictInfWdt, infieldHeight UI_TOOLTIP `Радиус Меньшей Окружности`
		endif
	endif

	if iVaultType = VAULT_CROSS then
		uiPictX = 100
		uiPictY = 10
		uiPictWdt = 125
		uiPictHgt = 52
		uiPictMargin = 5
		uiPictInfWdt = 90
		infieldHeight 		= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

		ui_pict 5, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
	
		ui_infield "ZZYZX", uiPicLeftStart, uiPictY+20, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота`
		ui_infield "thicknessA", uiPicRightStart, uiPictY-5, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина над Аркой A`
		ui_infield "radiusA", uiPicRightStart, uiPictY+20, uiPictInfWdt, infieldHeight UI_TOOLTIP `Радиус Арки A`

		uiPictY = uiPictY + uiPictHgt + infieldHeight + uiPictMargin + 20

		uiPictWdt = 125
		uiPictHgt = 125
		uiPictMargin = 5
		infieldHeight 		= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin 
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

		ui_pict 6, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
	
		ui_infield "B", uiPicLeftStart, uiPictY+53, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Свода`
		ui_infield "A", uiPictX+32, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Длина Свода`
		ui_infield "offsetX", uiPictX+32, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Смещение Арки A (X)`
		ui_infield "offsetY", uiPicRightStart, uiPictY+53, uiPictInfWdt, infieldHeight UI_TOOLTIP `Смещение Арки B (Y)`

		uiPictX =  uiPictX + uiPictWdt + uiPictInfWdt + uiPictMargin

		uiPictWdt = 54
		uiPictHgt = 125
		uiPictMargin = 5
		infieldHeight 		= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

		ui_pict 7, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
	
		ui_infield "radiusB", uiPictX-65, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Радиус Арки B`
		ui_infield "thicknessB", uiPictX+30, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина над Аркой B`

	endif

	if iVaultType = VAULT_DOMINICAL then
		uiPictX = 140
		uiPictY = 10
		uiPictWdt = 172
		uiPictHgt = 242
		uiPictMargin = 5
		uiPictInfWdt = 60
		infieldHeight 		= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

		ui_pict 8, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
	
		ui_infield "half_a", uiPicLeftStart, uiPictY+130, uiPictInfWdt, infieldHeight UI_TOOLTIP `Смещение Плоскости Сечения от Центра`
		ui_infield "shell_thk", uiPicRightStart, uiPictY+82, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина Свода`
		ui_infield "radius_1", uiPicRightStart, uiPictY+120, uiPictInfWdt, infieldHeight UI_TOOLTIP `Внешний Радиус Свода`
	endif

	if iVaultType = VAULT_CROSS_2  | iVaultType = VAULT_3CENTCROSS then
		ui_outfield	`Кол-во Внутренних Сводов`, x1,    yCurrent+4, 	x2 - x1 + 30,	outfieldHeight	
		ui_infield "gs_f_num", 	 x2+35,   yCurrent, 	x3 - x2 - 35, infieldHeight

			ui_outfield	`Внутренние Своды`, x4,    yCurrent+4, 	x6 - x4 - 6 - 2,	outfieldHeight	
			ui_infield{3} "gs_f_type_m", 	 x6 - 6,   yCurrent, 	xEnd - x6 + 6, infieldHeight,
						8, "", 0, 0,
						0, 0, 0, 0,
						"", stInfVault[1], 1,
						"", stInfVault[2], 2 ui_tooltip `Расположение Внутренних Сводов`
	
		
		yCurrent = yCurrent + dy

		ui_infield{3} "gs_f_simm",	x1,		yCurrent,		xEnd-x1,	infieldHeight,
			7, "", 0, 0,
			0, 0, 0, 0,
			"", `Симметричные Внутренние Своды`, 0,
			"", `Симметричные Внутренние Своды`, 1
	
		yCurrent = yCurrent + dy
	
		ui_separator 0, yCurrent-2, xEnd, yCurrent-2
			
	
		uiPictX = 95
		uiPictY = 73
		uiPictWdt = 253
		uiPictHgt = 171
		uiPictMargin = 5
		uiPictInfWdt = 90
		infieldHeight 	= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin - 3
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin
		

		if iVaultType = VAULT_CROSS_2 then
			if not(gs_f_simm) then
				if gs_f_type_m = 1 then
					ui_pict 11, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				else
					ui_pict 9, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				endif
			else
				if gs_f_type_m = 2 then
					ui_pict 10, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				else
					ui_pict 12, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				endif
			endif
		else
			if not(gs_f_simm) then
				if gs_f_type_m = 1 then
					ui_pict 15, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				else
					ui_pict 13, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				endif
			else
				if gs_f_type_m = 2 then
					ui_pict 14, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				else
					ui_pict 16, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
				endif
			endif
		endif

		_stSmallerRad = `Радиус Меньшей Окружности`

		if iVaultType = VAULT_CROSS_2 | iVaultType = VAULT_3CENTCROSS then
			ui_infield "gs_b2", uiPicLeftStart, uiPictY-10, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Внутреннего Свода`
		endif

		ui_infield "gs_f_length", uiPicLeftStart, uiPictY+12, uiPictInfWdt, infieldHeight UI_TOOLTIP `Проекция Внутренних Сводов`
		ui_infield "B", uiPicLeftStart, uiPictY+75, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина`

		ui_infield "gs_shell_thk2", uiPictX+60, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина Внутренних Сводов`
		if iVaultType = VAULT_CROSS_2 then
			ui_infield "gs_radius_2", uiPictX+153, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Внешний Радиус Внутренних Сводов`
		else
			ui_infield "gs_h2", uiPictX+153, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Внутреннего Свода`
			ui_infield "gs_radius_2", uiPictX+246, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP _stSmallerRad
		endif

		if not(gs_f_simm) then
			ui_infield "gs_firstaxis", uiPictX+5, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Расстояние Оси Первого Свода`
		endif

		ui_infield "gs_ax_length", uiPictX+110, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Расстояние Осей Внутренних Сводов`

		ui_infield "shell_thk", uiPicRightStart, uiPictY+25, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина Основного Свода`
		if iVaultType = VAULT_CROSS_2 then
			ui_infield "radius_1", uiPicRightStart, uiPictY+75, uiPictInfWdt, infieldHeight UI_TOOLTIP `Внешний Радиус Основного Свода`
		else
			ui_infield "gs_h1", uiPicRightStart, uiPictY+75, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Основного Свода`
			ui_infield "radius_1", uiPicRightStart, uiPictY+120, uiPictInfWdt, infieldHeight UI_TOOLTIP _stSmallerRad
		endif
	
	endif


return
	
"AppearancePage":
	dim st2DParamNames[][]
		i = 1
		if	iVaultType # VAULT_DOMINICAL &\
			iVaultType # VAULT_CROSS then
				st2DParamNames[i][1] = "gs_axis" 		: st2DParamNames[i][2] = "ui_list_axis2D_2_flat(1)"			: i = i + 1
				st2DParamNames[i][1] = "gs_axis_line" 	: st2DParamNames[i][2] = "ui_list_axisLineType_2_flat(1)"	: i = i + 1
				st2DParamNames[i][1] = "gs_axis_pen" 	: st2DParamNames[i][2] = "ui_list_axisLinePen_2_flat(1)"	: i = i + 1
		endif
		st2DParamNames[i][1] = "gs_arc" 				: st2DParamNames[i][2] = ""									: i = i + 1
		st2DParamNames[i][1] = "gs_DashedLine" 			: st2DParamNames[i][2] = ""									: i = i + 1

		st2DParamNames[i][1] = "gs_cont_pen" 			: st2DParamNames[i][2] = "ui_list_contourPen_flat(1)"		: i = i + 1
		st2DParamNames[i][1] = "gs_fill_type" 			: st2DParamNames[i][2] = "ui_list_fillType_flat(1)"			: i = i + 1
		st2DParamNames[i][1] = "gs_fill_pen" 			: st2DParamNames[i][2] = "ui_list_fillPen_flat(1)"			: i = i + 1
		st2DParamNames[i][1] = "gs_back_pen" 			: st2DParamNames[i][2] = "ui_list_fillBgPen_flat(1)"		: i = i + 1

		st2DParamNames[i][1] = "gs_sectcont" 			: st2DParamNames[i][2] = "ui_list_contourPen_flat(1)"		: i = i + 1
		st2DParamNames[i][1] = "gs_sectfill" 			: st2DParamNames[i][2] = "ui_list_fillType_flat(1)"			: i = i + 1
		st2DParamNames[i][1] = "gs_sectfillpn" 			: st2DParamNames[i][2] = "ui_list_fillPen_flat(1)"			: i = i + 1
		st2DParamNames[i][1] = "gs_sectfillbkg" 		: st2DParamNames[i][2] = "ui_list_fillBgPen_flat(1)"		: i = i + 1

	dim st3DParamNames[][]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_resol"			: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
		st3DParamNames[3][1] = "gs_shadow"		: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[][]
		stSurfaceParamNames[1][1] = "gs_prim_mat"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	

	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_axis2D_2_flat(1)"
	file_dependence "ui_list_axisLineType_2_flat(1)"
	file_dependence "ui_list_axisLinePen_2_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

return

"descriptionPage":

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return

