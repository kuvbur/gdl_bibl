
BASECLASS_BASECORNICE 	= 1	! Bookshelf 01, Credenza, Cupboard, Wardrobe Modular, Wardrobe, Wardrobe Variable 
BASECLASS_CORNICE 		= 2	! Wall Cabinet
BASECLASS_BASE 			= 3	! Flexible Shelf, Lockers, Underdesk Drawer

ITEMCLASS_OTHER			 	= 0
ITEMCLASS_FLEXIBLE_SHELF 	= 1
ITEMCLASS_UNDERDESK_DRAWER 	= 2
ITEMCLASS_LOCKERS 			= 3
ITEMCLASS_NO_SIDEOVERHANG	= 4	! Wardrobe, Wardrobe Variable, Wardrobe Modular, Book Shelf 01, 

_infHeight 	= 19
_outfHeight = 15
_yOffset 	= 4

x0 = 0
x1 = 76
x2 = 0 + 230
x3 = 123 + 230
dy = 24

_outfFlag1			= 0
_outfFlag2			= 0
_outfFlagOverhang 	= 0
_outfFlagBase 		= 0
_yCurr = pageStartY


if iBaseClass = BASECLASS_BASECORNICE then
	_actbBase 		= "bBase"
	_actBaseType 	= "gs_leg_type_m"
	_actbaseHeight 	= "leg_h"
	_actBaseWidth 	= "leg_w"
	_actBaseMat 	= "gs_leg_mat"

	if not(bCornice) then _outfFlag1 = 4
	if not(bBase) then _outfFlag2 = 4
	if gs_lining_m = 4 or not(bCornice) then _outfFlagOverhang = 4
	if not(bBase) then _outfFlagBase = 4

	gosub "BaseCornice"
endif

if iBaseClass = BASECLASS_CORNICE then
	if not(bCornice) then _outfFlag1 = 4
	if gs_lining_m = 4 or not(bCornice) then _outfFlagOverhang = 4

	gosub "Cornice"
endif

if iBaseClass = BASECLASS_BASE then 
	if iItemClass = ITEMCLASS_FLEXIBLE_SHELF then
		_actbBase 		= "bLeg"
		_actBaseType 	= "iLegType"
		_actbaseHeight 	= "gs_legHeight"
		_actBaseWidth 	= "wBase"
		_actBaseMat 	= "gs_leg_mat"

		if not(bLeg) then _outfFlag2 = 4
		if iLegType = 1 or not(bLeg) then _outfFlagBase = 4	! Toe
	endif
	if iItemClass = ITEMCLASS_UNDERDESK_DRAWER then
		_actbBase 		= "bBase"
		_actBaseType 	= "gs_baseType_m"
		_actbaseHeight	= "gs_baseHeight"
		_actBaseWidth 	= "baseWidth"
		_actBaseMat 	= "gs_base_mat"
		if not(bBase) then _outfFlag2 = 4
		if gs_baseType_m = 2 or not(bBase) then _outfFlagBase = 4	! Toe
	endif
	if iItemClass = ITEMCLASS_LOCKERS then
		_actbBase 		= "bBase"
		_actBaseType 	= "iLegType"
		_actbaseHeight 	= "legHeight"
		_actBaseWidth	= "baseWidth"
		_actBaseMat 	= "matLeg"
		if not(bBase) then _outfFlag2 = 4
		if iLegType = 3 or not(bBase) then _outfFlagBase = 4	! Toe
	endif

	gosub "Base"
endif

end



"BaseCornice":
	x1 = 76
	x2 = 0
	x3 = 123
	gosub "Cornice"

	ui_separator 222, 0, 222, 266

	x0 = 0 + 230
	x1 = 76 + 230
	x2 = 0 + 230
	x3 = 123 + 230
	_yCurr = pageStartY

	gosub "Base"

	_yCurr = pageStartY + _yOffset + _infHeight + 2
	ui_infield{3} "gs_leg_type_m", x1, _yCurr, 135, 96,
		2, 3, 8, 2,
		120, 90, 120, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,	4,
		5, ``, 	5,
		6, ``,	6 ui_tooltip `Стиль Основания`

return

"Cornice":
	_yCurr = _yCurr + _yOffset
	ui_style 0, 1
	ui_infield{3} "bCornice", x0, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Карниз`, 0,
					"", `Карниз`, 1
	ui_style 0, 0

	_yCurr = _yCurr + _infHeight + 2
	ui_infield{3} "gs_cornice_type_m", x1, _yCurr, 135, 96,
		2, 2, 4, 1,
		120, 90, 120, 90,
			1, ``,	1,
			2, ``,	2,
			3, ``, 	3,
			4, ``,	4 ui_tooltip `Стиль Карниза`

	if iBaseClass = BASECLASS_BASECORNICE then
		_yCurr = _yCurr + dy * 4 + 5
	endif
	ui_infield{3} "gs_lining_m", x3, _yCurr, 90, 32,
			2, 1, 4, 2,
			120, 90, 78, 28,
			1, `Спереди`,	4,
			2, `Спереди и Слева`,	2,
			3, `Спереди и Справа`, 	3,
			4, `Спереди и по Бокам`,	1 ui_tooltip `Свес`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Свес`, x2, _yCurr, 120, _outfHeight, _outfFlag1

	_yCurr = _yCurr + 37 - _yOffset
	ui_infield{3} "cot", x3, _yCurr, 90, _infHeight ui_tooltip `Толщина Карниза`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Толщина`, x2, _yCurr, 120, _outfHeight, _outfFlag1

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "cooh", x3, _yCurr, 90, _infHeight ui_tooltip `Величина Свеса Спереди`

	_yCurr = _yCurr + _yOffset
	if iItemClass <> ITEMCLASS_NO_SIDEOVERHANG then
		ui_outfield `Свес Спереди`, x2, _yCurr, 120, _outfHeight, _outfFlag1

		_yCurr = _yCurr + dy - _yOffset
		ui_infield{3} "side_overhang", x3, _yCurr, 90, _infHeight ui_tooltip `Величина Свеса Сбоку`

		_yCurr = _yCurr + _yOffset
		ui_outfield `Выступ Сбоку`, x2, _yCurr, 120, _outfHeight, _outfFlagOverhang
	else
		ui_outfield `Свес`, x2, _yCurr, 120, _outfHeight, _outfFlag1
	endif

	if iBaseClass = BASECLASS_BASECORNICE then
		if iItemClass = ITEMCLASS_NO_SIDEOVERHANG then
			_yCurr = _yCurr + 48
		else
			_yCurr = _yCurr + 24
		endif
	else
		_yCurr = _yCurr + 38
	endif
	ui_infield{3} "gs_cornice_mat", x2, _yCurr, 213, 25 ui_tooltip `Карниз`
return


"Base":
	_yCurr = _yCurr + _yOffset
	ui_style 0, 1
	ui_infield{4} _actbBase, x0, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Основание`, 0,
					"", `Основание`, 1
	ui_style 0, 0

	_yCurr = _yCurr + _infHeight + 2
	if iBaseClass <> BASECLASS_BASECORNICE then
		if iItemClass = ITEMCLASS_UNDERDESK_DRAWER then
			ui_infield{4} _actBaseType, x1, _yCurr, 135, 96,
				2, 5, 2, 1,
				120, 90, 120, 90,
				1, `Ролики`,	1,
				2, `Основание`,		2 ui_tooltip `Стиль Основания`
		else
			if iItemClass = ITEMCLASS_LOCKERS then
				ui_infield{4} _actBaseType, x1, _yCurr, 135, 96,
					2, 4, 4, 1,
					120, 90, 120, 90,
					3, ``,	1,
					2, ``,	2,
					1, ``, 	3 ui_tooltip `Стиль Основания`
			else
				ui_infield{4} _actBaseType, x1, _yCurr, 135, 96,
					2, 4, 4, 1,
					120, 90, 120, 90,
					1, ``,	1,
					2, ``,	2,
					3, ``, 	3,
					4, ``,	4 ui_tooltip `Стиль Основания`
			endif
		endif
	endif

	if iBaseClass = BASECLASS_BASECORNICE then
		_yCurr = _yCurr + dy * 4 + 5
	endif
	ui_infield{4} _actbaseHeight, x3, _yCurr, 90, _infHeight ui_tooltip `Высота Основания`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Высота`, x2, _yCurr, 120, _outfHeight, _outfFlag2

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{4} _actBaseWidth, x3, _yCurr, 90, _infHeight ui_tooltip `Ширина Основания`

	_yCurr = _yCurr + _yOffset
	if 	(iItemClass = ITEMCLASS_FLEXIBLE_SHELF and (iLegType = 3 or iLegType = 4))|\
		(iItemClass = ITEMCLASS_LOCKERS and iLegType = 1)|\
		(iBaseClass = BASECLASS_BASECORNICE and (gs_leg_type_m = 2 or gs_leg_type_m = 3))then
		ui_outfield `Диаметр`, x2, _yCurr, 120, _outfHeight, _outfFlag2
	else
		ui_outfield `Ширина`, x2, _yCurr, 120, _outfHeight, _outfFlagBase 
	endif

	if iBaseClass = BASECLASS_BASECORNICE then
			_yCurr = _yCurr + 85
	else
		_yCurr = _yCurr + dy * 3 - _yOffset
	endif
	ui_infield{4} _actBaseMat, x2, _yCurr, 213, 25 ui_tooltip `Основание`
return
