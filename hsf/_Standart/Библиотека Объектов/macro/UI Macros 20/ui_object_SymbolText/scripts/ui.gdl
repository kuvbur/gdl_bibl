
! call with bRoofMaker = true for roofmaker elements' Display Lumber Size tabpage
! 			dt = true for enabling text style parameters
! call with bRoofMaker = false for non-roofmaker elements' Text Style tabpage
! 			dt is also an input parameter in this case

!Positions for UI elements
x1	= 0
x2	= 168
x3	= 170
x4	= 370
x5	= 266
x6	= 273

_yCurr		= pageStartY
_dy			= 24
_infHeight 	= 19
_outfHeight = 15
_offsetY	= 4

if not(bRoofMaker) then
	if bIsTextDisplay then
		ui_style 0, 1
		ui_infield{3} "dt",					x1,	_yCurr - _offsetY,	x2-x1,	_infHeight,
			7, "",
			0, 0, 0, 0, 0, 0,
			"",`Текст`, 0,
			"",`Текст`, 1
		ui_style 0, 0

		if bParName_stDisplayText then
			ui_infield "stDisplayText",			x3,	_yCurr - _offsetY,	x4-x3,	_infHeight
		else
			ui_infield "txt",					x3,	_yCurr - _offsetY,	x4-x3,	_infHeight
		endif
		_yCurr = _yCurr + _dy
	else
		ui_style 0, 1
		ui_outfield `Стиль Текста`, x1,	_yCurr,	444,	_outfHeight
		ui_style 0, 0
	
		_yCurr = _yCurr + _dy
	endif
endif

ui_outfield `Тип Шрифта`, 			x1,	_yCurr, 			x2-x1,	_outfHeight,	4*not(dt)
ui_infield "fontType",				x3,	_yCurr - _offsetY,	x4-x3,	_infHeight

_yCurr = _yCurr + _dy

ui_outfield `Размер Шрифта`, 			x1,	_yCurr,				x2-x1,	_outfHeight,	4*not(dt)
if bParName_FontSize then
	ui_infield "fontSize",			x3,	_yCurr - _offsetY,	x5-x3,	_infHeight
else
	ui_infield "fsz",				x3,	_yCurr - _offsetY,	x5-x3,	_infHeight
endif
_yCurr = _yCurr + _dy

ui_outfield `Стиль Шрифта`, 			x1,	_yCurr,				x2-x1,	_outfHeight,	4*not(dt)
ui_infield{3} "gs_text_style_bold",	x3,	_yCurr - _offsetY,	21,		_infHeight,
	6, "ui_bold_flat(1)",
	1, 1, 16, 16, 16, 16,
	1, "",	0,
	1, "",	1
ui_infield{3} "gs_text_style_italic",	x3 + 25,	_yCurr - _offsetY,	21,		_infHeight,
	6, "ui_italic_flat(1)",
	1, 1, 16, 16, 16, 16,
	1, "",	0,
	1, "",	1
ui_infield{3} "gs_text_style_underline",	x3 + 2*25,	_yCurr - _offsetY,	21,		_infHeight,
	6, "ui_underline_flat(1)",
	1, 1, 16, 16, 16, 16,
	1, "",	0,
	1, "",	1

if bStrikeText then
	ui_infield{3} "gs_text_style_strike",	x3 + 3*25,	_yCurr - _offsetY,	21,		_infHeight,
		6, "ui_text_strikethrough_flat(1)",
		1, 1, 16, 16, 16, 16,
		1, "",	0,
		1, "",	1
endif

_yCurr = _yCurr + _dy

ui_outfield `Цвет Пера`, 				x1,	_yCurr,				x2-x1,	_outfHeight,	4*not(dt)
if not(bRoofMaker) then
	if bParName_PenText then
		_stParName = "penText"
	else
		_stParName = "tpen"
	endif
else
		_stParName = "txt_pen"
endif
ui_infield _stParName,					x3,	_yCurr - _offsetY,	33,		_infHeight

if biTextPen then
	ui_infield "iTextPen",				x3 + 38,	_yCurr - _offsetY,	33,		_infHeight
endif

_yCurr = _yCurr + _dy

if bOrientationShortName then
	_orientationTitle = `Ориентация`
else
	_orientationTitle = `Ориентация в Символе`
endif

ui_outfield _orientationTitle,	x1,	_yCurr,					x2-x1,	_outfHeight,	4*not(dt)
ui_infield{3} "typeTextRotation_m",		x3,	_yCurr - _offsetY,	x5-x3,	_infHeight,
	8, "",
	0, 0, 0, 0, 0, 0,
	0, `Читаемый`,			2,
	0, `Всегда Горизонтально`,	1,
	0, `Выровнять по Символу`,	0

_yCurr = _yCurr + _dy

if bTextPos then
	if bRoofMaker then
		_stParNameX = "txt_pos_x"
		_stParNameY = "txt_pos_y"
	else
		_stParNameX = "txtx"
		_stParNameY = "txty"
	endif

	ui_outfield `Расположение по X`, 			x1,	_yCurr,				x2-x1,	_outfHeight,	4*not(dt)
	ui_infield _stParNameX,				x3,	_yCurr - _offsetY,	x5-x3,	_infHeight
	
	_yCurr = _yCurr + _dy
	
	ui_outfield `Расположение по Y`, 			x1,	_yCurr,				x2-x1,	_outfHeight,	4*not(dt)
	ui_infield _stParNameY,				x3,	_yCurr - _offsetY,	x5-x3,	_infHeight

	_yCurr = _yCurr + _dy
endif
