
UI_DIALOG `Параметры Офисного Стола`

ui_current_page gs_ui_current_page


! ======================================================================
! UI Page IDs for Dinamic Page Handling
! ======================================================================

iPageIDRoot			= -1
tabIdx = 1

iPageIDPreset		= tabIdx	: tabIdx = tabIdx + 1
iPageIDDim			= tabIdx	: tabIdx = tabIdx + 1
iPageIDAccessories	= tabIdx	: tabIdx = tabIdx + 1
iPageIDMaterials	= tabIdx	: tabIdx = tabIdx + 1
iPageIDRepMinSpace	= tabIdx	: tabIdx = tabIdx + 1
iPageIDDrawer1		= tabIdx	: tabIdx = tabIdx + 1
iPageIDDrawer2		= tabIdx

_strDrawerTitle1 = `Ящики`
_strDrawerTitle2 = `Ящик - Стор. 2`

_bShowDimPage = 0
_bShowDrawerPage1 = 0
_bShowDrawerPage2 = 0

if not(bOfficeCab1) & not(bOfficeCab2) then
	if bLType then
		_bShowDimPage = 1
	endif
else
	if bOfficeCab1 exor bOfficeCab2 then
		if bLType then
			_bShowDimPage = 1
			_bShowDrawerPage1 = 1
		else
			if bOfficeCab1 then
				_bShowDrawerPage1 = 1
			else
				_bShowDrawerPage2 = 1
			endif
		endif
	else
		_bShowDrawerPage1 = 1
		_bShowDrawerPage2 = 1

		_strDrawerTitle1 = `Ящик - Стор. 1`
		_strDrawerTitle2 = `Ящик - Стор. 2`
	endif
endif

textElevInfield		= 4
textElevBoolean		= 2
textElevMaterial	= 4
infieldHeight 		= 19
outfieldHeight		= 15

pageStartY = 5

! ==============================================================================
! Display Active UI pages
! ==============================================================================

ui_page iPageIDPreset, iPageIDRoot, `Стиль Стола`, "uiTab_Style_flat(1)"
	gosub "presentPage"

if _bShowDimPage then
	ui_page iPageIDDim, iPageIDRoot, `Размеры`, "uiTab_Dimension_flat(1)"
		gosub "dimPage"
endif

ui_page iPageIDAccessories, iPageIDRoot, `Аксессуары`, "uiTab_Cabinet_flat(1)"
	gosub "accessoriesPage"

ui_page iPageIDMaterials, iPageIDRoot, `Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "materialsPage"

ui_page iPageIDRepMinSpace, iPageIDRoot, `Отображение и Минимальное Пространство`, "uiTab_2DRepr_flat(1)"
	gosub "repMinSpacePage"

if _bShowDrawerPage1 then
	ui_page iPageIDDrawer1, iPageIDRoot, _strDrawerTitle1, "uiTab_Drawer_flat(1)"
		gosub "drawer1Page"
endif

if _bShowDrawerPage2 then
	ui_page iPageIDDrawer2, iPageIDRoot, _strDrawerTitle2, "uiTab_Drawer_flat(1)"
		gosub "drawer2Page"
endif

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"presentPage":
! ------------------------------------------------------------------------------

	IF bLType THEN
		UI_INFIELD{3} "gs_presetStyle_m",0,pageStartY,443,230,
			1,2,6,2,			!tableStyles_L.png
			138,110,135,90,
			1,`Стиль 1`,1,
			2,`Стиль 2`,2,
			3,`Стиль 3`,3,
			4,`Стиль 4`,4,
			5,`Стиль 5`,6,
			6,`Специальный`,5
	ELSE
		UI_INFIELD{3} "gs_presetStyle_m",0,pageStartY,443,230,
			1,1,6,2,			!tableStyles_rect.png
			138,110,135,90,
			1,`Стиль 1`,1,
			2,`Стиль 2`,2,
			4,`Стиль 3`,3,
			5,`Стиль 4`,4,
			3,`Специальный`,5
	ENDIF


return

! -----------------------------------------------------------------------------
"dimPage":
! -----------------------------------------------------------------------------

	if bLType then
		xCenter	= 160
		yCenter	= pageStartY + 110

		xPict		= 102
		yPict		= 120
		if iDeskAngle = 90 then
			if gs_iTableType = 1 then ui_pict 3, xCenter - xPict / 2, yCenter - yPict / 2, xPict, yPict, 1		!ui_officeDesk_dim_90_straight.png
			if gs_iTableType = 2 then ui_pict 4, xCenter - xPict / 2, yCenter - yPict / 2, xPict, yPict, 1		!ui_officeDesk_dim_90_rounded.png
		else
			if gs_iTableType = 1 then ui_pict 5, xCenter - xPict / 2, yCenter - yPict / 2, xPict, yPict, 1		!ui_officeDesk_dim_120_straight.png
			if gs_iTableType = 2 then ui_pict 6, xCenter - xPict / 2, yCenter - yPict / 2, xPict, yPict, 1		!ui_officeDesk_dim_120_rounded.png
		endif

		yCurrent = yCenter - yPict / 2 - 25
		ui_infield "tableDepth2", xCenter - xPict / 2 + 10, yCurrent, 100, 20			ui_tooltip `Глубина Стола 2`

		yCurrent = yCenter + yPict / 2 + 5
		ui_infield "tableLength1", xCenter - xPict / 2 + 10, yCurrent, 100, 20	ui_tooltip `Длина Стола 1`

		yCurrent = yCenter + 10 + 25 * (iDeskAngle = 120)
		ui_infield "tableDepth1", xCenter + xPict / 2 + 5, yCurrent, 100, 20			ui_tooltip `Глубина Стола 1`

		yCurrent = yCenter - 15
		ui_infield "tableLength2", xCenter - xPict / 2 - 105, yCurrent, 100, 20	ui_tooltip `Длина Стола 2`

		ui_separator xCenter * 2, pageStartY  - 3, xCenter * 2, 265

		xLeftStr1	= 330
		yLeftStr1	= pageStartY + 10
		yCurrent	= yLeftStr1

		ui_outfield `Поворот Стола`, xLeftStr1, yCurrent, 100, outfieldHeight
		yCurrent = yCurrent + 17
		ui_infield{3} "iDeskAngle", xLeftStr1, yCurrent, 100, 20,
			8, "",
			2,
			1, 100, 20, 100, 20,
			"", `90°`, 90,
			"", `120°`, 120
		yCurrent = yCurrent + 24

		ui_outfield `Высота Стола`, xLeftStr1, yCurrent, 100, outfieldHeight
		yCurrent = yCurrent + 17
		ui_infield "zzyzx", xLeftStr1, yCurrent, 100, infieldHeight
		yCurrent = yCurrent + 24

		ui_outfield `Толщ. Столешницы`, xLeftStr1, yCurrent, 100, outfieldHeight
		yCurrent = yCurrent + 17
		ui_infield "topThk", xLeftStr1, yCurrent, 100, infieldHeight
		yCurrent = yCurrent + 24

		ui_outfield `Внутренний Угол`, xLeftStr1, yCurrent, 100, outfieldHeight
		yCurrent = yCurrent + 17
		ui_infield{3} "iInnerEdge", xLeftStr1, yCurrent, 100, infieldHeight,
			8, "",
			2,
			1, 100, 20, 100, 20,
			"", `Прямой`, 1,
			"", `Закругленный`, 2
		yCurrent = yCurrent + 24

		ui_outfield `Радиус`, xLeftStr1, yCurrent, 100, outfieldHeight
		yCurrent = yCurrent + 17
		ui_infield "innerEdgeRad", xLeftStr1, yCurrent, 100, infieldHeight
		yCurrent = yCurrent + 24
	endif

return

! -----------------------------------------------------------------------------
"accessoriesPage":
! -----------------------------------------------------------------------------

	xCenter	= 222
	yCenter	= pageStartY + 85

	xLeftStr1	= 230 - 220
	Strlength1	= 110
	xLeftStr2	= 345 - 220
	Strlength2	= 90
	yLeftStr1	= pageStartY
	yCurrent	= yLeftStr1

	ui_style 0, 1
	ui_outfield `Стиль Ножек`, xLeftStr1, yCurrent, Strlength1, 16, 4 * (bOfficeCab1 and bOfficeCab2)
	ui_style 0, 0
	ui_infield{3} "gs_leg_type_m", xLeftStr2, yCurrent, Strlength2 - 20, 110,
		2, "ui_legtypes(1)", 10, 5,		!ui_legtypes(1).png
		70, 90, 70, 90,
		1, `Стиль 1`, 1,
		2, `Стиль 2`, 2,
		4, `Стиль 3`, 3,
		5, `Стиль 4`, 4,
		6, `Стиль 5`, 5,
		8, `Стиль 6`, 6
	yCurrent = yCurrent + 120

	ui_style 0, 1
	ui_infield{3} "gs_bFrontPanel", xLeftStr1, yCurrent, Strlength1, 20,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Передняя Панель`, 0,
		1, `Передняя Панель`, 1
	ui_style 0, 0
	yCurrent = yCurrent + 25

	ui_outfield `Высота`, xLeftStr1 + 16, yCurrent, Strlength1 - 16, outfieldHeight, 4 - 4 * gs_bFrontPanel
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_frontPanelHeight", xLeftStr2, yCurrent, Strlength2, infieldHeight
	yCurrent = yCurrent + 25

	ui_outfield `Отметка Верха`, xLeftStr1 + 16, yCurrent, Strlength1 - 16, outfieldHeight, 4 - 4 * gs_bFrontPanel
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_frontPanelTop", xLeftStr2, yCurrent, Strlength2, infieldHeight
	yCurrent = yCurrent + 25

	ui_style 0, 1
	ui_outfield `Перегородка`, xLeftStr1, yCurrent, Strlength1, 16
	ui_style 0, 0
	yCurrent = yCurrent - textElevInfield
	if bLType then
		ui_infield{3} "gs_partitionType_m", xLeftStr2, yCurrent, 90, 20,
			8, "", 0, 0,
			0, 0, 0, 0,
			0, `Нет`, 0,
			0, `Спереди`, 1,
			0, `Сбоку`, 2,
			0, `Оба`, 3
	else
		ui_infield{3} "gs_partitionType_m", xLeftStr2, yCurrent, 90, 20,
			8, "", 0, 0,
			0, 0, 0, 0,
			0, `Нет`, 0,
			0, `Спереди`, 1,
			0, `Cо Стороны 1`, 2,
			0, `Cо Стороны 2`, 3,
			0, `Со Стороны 1 и Спереди`, 4,
			0, `Со Стороны 2 и Спереди`, 5,
			0, `С Обеих Сторон`, 6,
			0, `Спереди и По Бокам`, 7
	endif
	yCurrent = yCurrent + 25

	ui_outfield `Высота`, xLeftStr1 + 16, yCurrent, Strlength1 - 16, outfieldHeight, 4 * (gs_partitionType_m = 0)
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_partitionHeight", xLeftStr2, yCurrent, Strlength2, infieldHeight
	yCurrent = yCurrent + 25

	ui_separator xCenter, pageStartY - 3, xCenter, 265

	xLeftStr1	= 230
	Strlength1	= 110
	xLeftStr2	= 345
	Strlength2	= 90
	yLeftStr1	= pageStartY
	yCurrent	= yLeftStr1

	if bLType then
		ui_style 0, 1
		ui_infield{3} "gs_bOfficeCab", xLeftStr1, yCurrent, Strlength1, 20,
			7, "",
			2,
			1, 20, 20, 20, 20,
			0, `Тумбочка`, 0,
			1, `Тумбочка`, 1
		ui_style 0, 0
	else
		ui_style 0, 1
		ui_outfield `Тумбочка`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
		ui_style 0, 0
		yCurrent = yCurrent - textElevInfield
		ui_infield{3} "gs_officeCab_m", xLeftStr2, yCurrent, 90, 20,
			8, "", 0, 0,
			0, 0, 0, 0,
			0, `Нет`, 0,
			0, `Cо Стороны 1`, 1,
			0, `Cо Стороны 2`, 2,
			0, `С Обеих Сторон`, 3
	endif
	yCurrent = yCurrent + 25

	ui_infield{3} "bCabBase", xLeftStr1, yCurrent, Strlength1, 20,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Основание`, 0,
		1, `Основание`, 1
	yCurrent = yCurrent + 25

	ui_outfield `Высота Основания`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 - 4 * ((bOfficeCab1 or bOfficeCab2) and bCabBase)
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_baseHeight", xLeftStr2, yCurrent, Strlength2, infieldHeight
	yCurrent = yCurrent + 25

	ui_infield{3} "gs_bevel", xLeftStr1, yCurrent, Strlength1, 20,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Фаска Торца`, 0,
		1, `Фаска Торца`, 1
	yCurrent = yCurrent + 25

	ui_outfield `Стиль Ручки`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 - 4 * (bOfficeCab1 or bOfficeCab2)
	ui_infield{3} "gs_knob_type_m", xLeftStr2, yCurrent - 25 * (bOfficeCab1 and bOfficeCab2) , 60, 88,
		2, "Knob_Types_N(13)", 12, 4,		!Knob_Types_N(13).png
		60, 88, 60, 80,
		1, `Стиль 1`, 1,
		2, `Стиль 2`, 2,
		3, `Стиль 3`, 3,
		4, `Стиль 4`, 4,
		5, `Стиль 5`, 5,
		6, `Стиль 6`, 6,
		7, `Стиль 7`, 7,
		8, `Стиль 8`, 8,
		9, `Нет`, 0
	yCurrent = yCurrent + 92 - 23 * (bOfficeCab1 and bOfficeCab2)

	if not(bOfficeCab2) then
		ui_outfield `По Горизонт.`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 - 4 * (bOfficeCab1 or bOfficeCab2)
		yCurrent = yCurrent - textElevInfield
		ui_infield "gs_knobHorPos", xLeftStr2, yCurrent, Strlength2, infieldHeight
		yCurrent = yCurrent + 25
	endif
	if not(bOfficeCab1) and bOfficeCab2 then
		ui_outfield `По Горизонт.`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 - 4 * (bOfficeCab1 or bOfficeCab2)
		yCurrent = yCurrent - textElevInfield
		ui_infield "gs_knobHorPos_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
		yCurrent = yCurrent + 25
	endif
	if bOfficeCab1 and bOfficeCab2 then
		ui_outfield `По Гориз. - Сторона 1`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 - 4 * (bOfficeCab1 or bOfficeCab2)
		yCurrent = yCurrent - textElevInfield
		ui_infield "gs_knobHorPos", xLeftStr2, yCurrent, Strlength2, infieldHeight
		yCurrent = yCurrent + 25

		ui_outfield `По Гориз. - Сторона 2`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 - 4 * (bOfficeCab1 or bOfficeCab2)
		yCurrent = yCurrent - textElevInfield
		ui_infield "gs_knobHorPos_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
		yCurrent = yCurrent + 25
	endif

	ui_outfield `По Вертик.`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 - 4 * (bOfficeCab1 or bOfficeCab2)
	yCurrent = yCurrent - textElevInfield
	ui_infield{3} "gs_knobVerPos_m", xLeftStr2, yCurrent, 90, 20,
		8, "", 0, 0,
		0, 0, 0, 0,
		0, `Верх`, 1,
		0, `Середина`, 0,
		0, `Низ`, -1

return


! -----------------------------------------------------------------------------
"materialsPage":
! -----------------------------------------------------------------------------
	x1 = 3
	x2 = 100
	x3 = 217

	x4 = x1+222
	x5 = x2+222
	x6 = x3+222

	y0 = pageStartY + 10
	dy = 32

	y = y0

	bHideCabinetMat = 0
	IF NOT(bOfficeCab1) AND NOT(bOfficeCab2) THEN bHideCabinetMat = 1

	bHideKnobMat = 0
	IF bHideCabinetMat OR gs_knob_type_m = 0 THEN bHideKnobMat = 1

	bHideBaseMat = 0
	If bHideCabinetMat OR gs_baseType_m = 0 THEN bHideBaseMat = 1

	UI_OUTFIELD `Столешница`, x1, y, x2-x1-1, outfieldHeight
	UI_INFIELD "gs_top_mat", x2, y - textElevMaterial, x3-x2, 24

	UI_OUTFIELD `Тумбочка`, x4, y, x5-x4-1, outfieldHeight, 4 * bHideCabinetMat
	UI_INFIELD "gs_cabinet_mat", x5, y - textElevMaterial, x6-x5, 24

	y = y + dy
	UI_OUTFIELD `Ножки`, x1, y, x2-x1-1, outfieldHeight, 4 * (bOfficeCab1 and bOfficeCab2)
	UI_INFIELD "gs_leg_mat", x2, y - textElevMaterial, x3-x2, 24

	UI_OUTFIELD `Ящики`, x4, y, x5-x4-1, outfieldHeight, 4 * bHideCabinetMat
	UI_INFIELD "gs_door_mat", x5, y - textElevMaterial, x6-x5, 24

	y = y + dy
	UI_OUTFIELD `Опоры Ножек`, x1, y, x2-x1-1, outfieldHeight, 4 * (gs_leg_type_m = 3 OR gs_leg_type_m = 4 OR gs_leg_type_m = 6 OR (bOfficeCab1 and bOfficeCab2))
	UI_INFIELD "legbase_mat", x2, y - textElevMaterial, x3-x2, 24

	UI_OUTFIELD `Ручка`, x4, y, x5-x4-1, outfieldHeight, 4 * bHideKnobMat
	UI_INFIELD "gs_knob_mat", x5, y - textElevMaterial, x6-x5, 24

	y = y + dy
	UI_OUTFIELD `Обвязка Экрана`, x1, y, x2-x1-1, outfieldHeight, 4-4*(gs_partitionType_m>0)
	UI_INFIELD "gs_partitionFrame_mat", x2, y - textElevMaterial, x3-x2, 24

	UI_OUTFIELD `Основание`, x4, y, x5-x4-1, outfieldHeight, 4 * bHideBaseMat
	UI_INFIELD "gs_base_mat", x5, y - textElevMaterial, x6-x5, 24

	y = y + dy
	UI_OUTFIELD `Перегородка`, x1, y, x2-x1-1, outfieldHeight, 4-4*(gs_partitionType_m>0)
	UI_INFIELD "gs_partition_mat", x2, y - textElevMaterial, x3-x2, 24

	y = y + dy
	UI_OUTFIELD `Передняя Панель`, x1, y, x2-x1-1, outfieldHeight, 4-4*gs_bFrontPanel
	UI_INFIELD "gs_frontPanel_mat", x2, y - textElevMaterial, x3-x2, 24

return

! -----------------------------------------------------------------------------
"repMinSpacePage":
! -----------------------------------------------------------------------------

	xCenter	= 222
	yCenter	= pageStartY + 85

	xLeftStr1	= 230 - 220
	Strlength1	= 110
	xLeftStr2	= 345 - 220
	Strlength2	= 90
	penLength	= 40
	fillLength	= 90
	yLeftStr1	= pageStartY
	yCurrent	= yLeftStr1

	ui_style 0, 1
	ui_outfield `2D-отображение`, xLeftStr1, yCurrent, xLeftStr2 + Strlength2 - xLeftStr1, 16
	ui_style 0, 0
	yCurrent = yCurrent + 25

	ui_outfield `Перо Контура`, xLeftStr1, yCurrent, Strlength1 + Strlength2 - penLength, 16
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_cont_pen", xLeftStr2 + Strlength2 - penLength, yCurrent, penLength, 22
	yCurrent = yCurrent + 27

	ui_outfield `Тип Штриховки`, xLeftStr1, yCurrent, Strlength1 + Strlength2 - fillLength, 16
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_fill_type", xLeftStr2 + Strlength2 - fillLength, yCurrent, fillLength, 22
	yCurrent = yCurrent + 27

	ui_outfield `Перо Штриховки`, xLeftStr1, yCurrent, Strlength1 + Strlength2 - penLength, 16
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_fill_pen", xLeftStr2 + Strlength2 - penLength, yCurrent, penLength, 22
	yCurrent = yCurrent + 27

	ui_outfield `Перо Фона Штриховки`, xLeftStr1, yCurrent, Strlength1 + Strlength2 - penLength, 16
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_back_pen", xLeftStr2 + Strlength2 - penLength, yCurrent, penLength, 22
	yCurrent = yCurrent + 27

	ui_separator xCenter, pageStartY - 3, xCenter, yCurrent - 2
	ui_separator 5, yCurrent, 440, yCurrent

	yCurrent = pageStartY + 132
	ui_style 0, 1
	ui_outfield `3D-отображение`, xLeftStr1, yCurrent, xLeftStr2 + Strlength2 - xLeftStr1, 16
	ui_style 0, 0
	yCurrent = yCurrent + 25

	ui_outfield `3D`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
	yCurrent = yCurrent - textElevInfield
	ui_infield{3} "gs_detlevel_3D_m", xLeftStr2, yCurrent, 90, 20,
		8, "", 0, 0,
		0, 0, 0, 0,
		0, `Детальный`, 2,
		0, `Простой`, 1,
		0, `Откл.`, 0
	yCurrent = yCurrent + 25

	bHideLegResol = gs_detlevel_3D_m < 2 or ((gs_knob_type_m = 0 or NOT(bOfficeCab1) and NOT(bOfficeCab2)) and (gs_leg_type_m = 2 or gs_leg_type_m = 4))
	ui_outfield `Разрешение`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 * bHideLegResol
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_leg_resol", xLeftStr2, yCurrent, Strlength2, infieldHeight
	yCurrent = yCurrent + 25

	IF bLType THEN
		bHideResol = (gs_detlevel_3D_m < 2 or gs_iTableType = 1)
		ui_outfield `Разрешение Столешницы`, xLeftStr1, yCurrent, Strlength1, outfieldHeight, 4 * bHideResol
		yCurrent = yCurrent - textElevInfield
		ui_infield "gs_resol", xLeftStr2, yCurrent, Strlength2, infieldHeight
		yCurrent = yCurrent + 25
	endif

	xLeftStr1	= 230
	Strlength1	= 110
	xLeftStr2	= 345
	Strlength2	= 90
	yLeftStr1	= pageStartY
	yCurrent	= yLeftStr1

	ui_style 0, 1
	ui_outfield `Минимальное Пространство`, xLeftStr1, yCurrent, xLeftStr2 + Strlength2 - xLeftStr1, 16
	ui_style 0, 0
	yCurrent = yCurrent + 25

	ui_outfield `Спереди`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
	yCurrent = yCurrent - textElevInfield
	ui_infield "MSFront", xLeftStr2, yCurrent, Strlength2, infieldHeight
	yCurrent = yCurrent + 25

	if NOT(bLType) then
		ui_outfield `Длина`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
		yCurrent = yCurrent - textElevInfield
		ui_infield "MSLength", xLeftStr2, yCurrent, Strlength2, infieldHeight
	endif

	yCurrent = pageStartY + 132 + 23
	ui_infield{3} "gs_shadow", xLeftStr1, yCurrent, Strlength1, 20,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Отбрасывание Теней`, 0,
		1, `Отбрасывание Теней`, 1
	yCurrent = yCurrent + 25

return

! -----------------------------------------------------------------------------
"drawer1Page":
! -----------------------------------------------------------------------------

	CALL "OfficeCabinet01_m" PARAMETERS ALL iCabIndex = 1,
											bOnlyDrawerUI = 1,
											iPageIDDrawer = iPageIDDrawer1,
											stDrawerTabTitles = _strDrawerTitle1
return

! -----------------------------------------------------------------------------
"drawer2Page":
! -----------------------------------------------------------------------------

	CALL "OfficeCabinet01_m" PARAMETERS ALL iCabIndex = 2,
											bOnlyDrawerUI = 1,
											iPageIDDrawer = iPageIDDrawer2,
											stDrawerTabTitles = _strDrawerTitle2

return
