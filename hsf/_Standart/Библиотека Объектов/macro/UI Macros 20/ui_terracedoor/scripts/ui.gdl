


! =============================================================================
! Tabpage Structure Definition
! =============================================================================

! call all available page fix IDs
call "ui_tabIDListHierarchy_m" parameters iUITreeStandardType = 1,		!TERRACEDOOR_UI
					returned_parameters iUiID_Root,
										iUiID_General,		iParID_General,
										iUiID_ShapeOpening,	iParID_ShapeOpening,
										iUiID_OpLines,		iParID_OpLines,
										iUiID_WindowFrame,	iParID_WindowFrame,
										iUiID_SashLeaf,		iParID_SashLeaf,
										iUiID_Grid,			iParID_Grid,
										iUiID_SillBoard,	iParID_SillBoard,
										iUiID_VenetianBlind, iParID_VenetianBlind,
										iUiID_Shutter,		iParID_Shutter,
										iUiID_Closure,		iParID_Closure,
										iUiID_ModelAttrib,	iParID_ModelAttrib,
										iUiID_FloorPlanSection, iParID_FloorPlanSection,
										iUiID_Descriptions,	iParID_Descriptions




! =============================================================================
! Tabpages
! =============================================================================

ui_current_page gs_ui_current_page

for iTabs = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[iTabs][1] = iUiID_WindowFrame then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Коробка Окна`, "uiTab_DoorFrameStyle_flat(1)"
		if gs_ui_current_page = iUiID_WindowFrame then
			gosub "pageWindowFrame"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_SashLeaf then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Створка/Полотно`, "uiTab_DoorLeafSash_flat(1)"
		if gs_ui_current_page = iUiID_SashLeaf then
			gosub "pageSashLeaf"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_ShapeOpening then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Форма и Проем`, "uiTab_WindowShape_flat(1)"
		if gs_ui_current_page = iUiID_ShapeOpening then
			gosub "pageShapeOpening"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_Grid then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Переплеты`, "uitab_WindowSash_flat(1)"
		if gs_ui_current_page = iUiID_Grid then
			gosub "pageGrid"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_SillBoard then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Отлив и Доска`, "uiTab_WindowSillBoard_flat(1)"
		if gs_ui_current_page = iUiID_SillBoard then
			gosub "pageSillBoard"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_VenetianBlind then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Жалюзи`, "uiTab_BalconyVenetianBlinds_flat(1)"
		if gs_ui_current_page = iUiID_VenetianBlind then
			gosub "pageVenetianBlind"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_Shutter then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Ставни`, "uiTab_OpeningSunshade_flat(1)"
		if gs_ui_current_page = iUiID_Shutter then
			gosub "pageShutter"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_Descriptions then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Характеристики`, "uiTab_Descriptions_flat(1)"
		if gs_ui_current_page = iUiID_Descriptions then
			gosub "pageCallListing"
		endif
	endif
next iTabs

file_dependence "uiTab_DoorFrameStyle_flat(1)"
file_dependence "uiTab_DoorLeafSash_flat(1)"
file_dependence "uiTab_WindowShape_flat(1)"
file_dependence "uitab_WindowSash_flat(1)"
file_dependence "uiTab_WindowSillBoard_flat(1)"
file_dependence "uiTab_BalconyVenetianBlinds_flat(1)"
file_dependence "uiTab_OpeningSunshade_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

end

! =============================================================================
! 2. Window Frame
! =============================================================================

"pageWindowFrame":

		!********** horizontal section

		px = 0
		py = 130 + pageStartY

		xs = 240
		ys = 75


		if WIDO_REVEAL_SIDE then
			ui_pict "balc_fr_1(13)", px, py, xs, ys, 1
		else
			ui_pict "balc_fr_out_1(13)", px, py, xs, ys, 1
		endif

		ui_separator px+xs+10, pageStartY, px+xs+10, 230 + pageStartY

		ui_style 0, 1

		dy=pageStartY
		
		ui_infield "gs_stockb", 10, dy, 15, 20
		ui_outfield `Различная Ширина`, 31, dy + 3, 159, 20
		
		ui_style 0, 0

		dy=dy+12


		!*** frame parameters

		formatt = " "

		n = request("Working_length_unit", "", formatt)

		if WIDO_REVEAL_SIDE then

			if gs_stockb = 0 then
				ui_infield "gs_frame_width", px+10, py-21, 50, 20 ui_tooltip `Ширина Коробки`

				ui_outfield str(formatt, gs_frame_width), px+xs/2-15, py-21, 50, 20 ui_tooltip `Ширина Коробки`
				ui_outfield str(formatt, gs_frame_width), px+xs-42, py-21, 50, 20 ui_tooltip `Ширина Коробки`
			else
				ui_infield "gs_stb_r", px+10, py-21, 50, 20 ui_tooltip `Ширина Коробки Слева`
				ui_infield "gs_stb_m", px+xs/2-20, py-21, 50, 20 ui_tooltip `Ширина Коробки Посередине`
				ui_infield "gs_stb_l", px+xs-50, py-21, 50, 20 ui_tooltip `Ширина Коробки Справа`
			endif

		else

			if gs_stockb = 0 then
				ui_infield "gs_frame_width", px+10, py+ys+5, 50, 20 ui_tooltip `Ширина Коробки`

				ui_outfield str(formatt, gs_frame_width), px+xs/2-15, py+ys+5, 50, 20 ui_tooltip `Ширина Коробки`
				ui_outfield str(formatt, gs_frame_width), px+xs-42, py+ys+5, 50, 20 ui_tooltip `Ширина Коробки`
			else
				ui_infield "gs_stb_l", px+10, py+ys+5, 50, 20 ui_tooltip `Ширина Коробки Слева`
				ui_infield "gs_stb_m", px+xs/2-20, py+ys+5, 50, 20 ui_tooltip `Ширина Коробки Посередине`
				ui_infield "gs_stb_r", px+xs-50, py+ys+5, 50, 20 ui_tooltip `Ширина Коробки Справа`
			endif

		endif


		!********** vertical section

		xsv = 72
		ysv = 230

		pxv = px+xs+20
		pyv = pageStartY

		if WIDO_REVEAL_SIDE then
			ui_pict "balc_fr_v_1(13)", pxv, pyv, xsv, ysv, 1
		else
			ui_pict "balc_fr_out_v_2(13)", pxv, pyv, xsv, ysv, 1
		endif

		ady = pageStartY - 30

		ui_infield "gs_frame_thk", pxv+xsv+10, pageStartY - 2, 50, 20 ui_tooltip `Толщина Коробки`

		if gs_stockb = 0 then
			ui_outfield str(formatt, gs_frame_width), pxv+xsv+10, 57+ady, 50-10, 20 ui_tooltip `Ширина Коробки`
			ui_outfield str(formatt, gs_frame_width), pxv+xsv+10, 130+ady, 50-10, 20 ui_tooltip `Ширина Коробки`
			ui_outfield str(formatt, gs_frame_width), pxv+xsv+61, 215+ady, 50, 20 ui_tooltip `Ширина Коробки`
		else
			ui_infield "gs_stb_ob", pxv+xsv+10, 57+ady, 50, 20 ui_tooltip `Ширина Коробки Вверху`
			ui_infield "gs_frame_width", pxv+xsv+10, 130+ady, 50, 20 ui_tooltip `Ширина Коробки`
			ui_infield "stb_w", pxv+xsv+61, 215+ady, 50, 20 ui_tooltip `Ширина Коробки Окна Внизу`
		endif

		ui_outfield `Окно`, pxv+xsv+5, 215+ady, 55, 20
		ui_outfield `Дверь`, pxv+xsv+5, 239+ady, 55, 20

		ui_infield "gs_stb_u", pxv+xsv+61, 236+ady, 50, 20 ui_tooltip `Ширина Коробки Двери Внизу`
return



! =============================================================================
! 3. Leaf / Sash Settings
! =============================================================================

"pageSashLeaf":

		pageStartY = 3

		x_v = 50

		!********** horizontal section

		px = 0
		py = 132 + pageStartY

		xs = 240
		ys = 75

		ui_pict "balc_sash_1(13)", px, py, xs, ys, 1

		ui_separator px+xs+10, pageStartY, px+xs+10, 230 + pageStartY

		ui_style 0, 1

		dy = pageStartY
		ui_infield "gs_ver_r", 10, dy, 20, 20
		ui_outfield `Различная Ширина`, 31, dy + 3, 135, 20
		dy = dy - 15

		ui_style 0, 0

		!*** frame parameters

		formatt = " "

		n = request("Working_length_unit", "", formatt)

		ui_infield "gs_sash_width", px+20, py+ys+5, 50, 20 ui_tooltip `Ширина Обвязки Полотна`

		ui_infield "gs_vb", px+xs-50, py-21, 50, 20 ui_tooltip `Ширина Наложения Коробки/Полотна`
		ui_infield "gs_vt", px+xs-50, py+ys+5, 50, 20 ui_tooltip `Толщина Наложения Коробки/Полотна`



		!********** vertical section

		xsv = 72
		ysv = 230

		pxv = px+xs+20
		pyv = pageStartY

		ui_pict "balc_sash_v_1(13)", pxv, pyv, xsv, ysv, 1

		ui_infield "gs_sash_thk", pxv+xsv+8, 5 + pageStartY, 50, 20 ui_tooltip `Толщина Полотна`

		if gs_ver_r = 0 then
			ui_outfield str(formatt, gs_sash_width), pxv+xsv+10, 35 + pageStartY, 50, 20 ui_tooltip `Ширина Обвязки Полотна`
			ui_outfield str(formatt, gs_sash_width), pxv+xsv+10, 115 + pageStartY, 50, 20 ui_tooltip `Ширина Обвязки Полотна`
			ui_outfield str(formatt, gs_sash_width), pxv+xsv+5+50+1, 170 + pageStartY, 50, 20 ui_tooltip `Ширина Обвязки Полотна`
		else
			ui_outfield str(formatt, gs_rab_ob), pxv+xsv+10, 35 + pageStartY, 50, 20 ui_tooltip `Ширина Верхней Обвязки Полотна`
			ui_infield "gs_rab_ob", pxv+xsv+10, 115 + pageStartY, 50, 20 ui_tooltip `Ширина Верхней Обвязки Полотна`
			ui_infield "gs_rab_u", pxv+xsv+5+50+1, 165 + pageStartY, 50, 20 ui_tooltip `Ширина Рамы Створки Окна Внизу`
		endif

		ui_outfield `Окно`, pxv+xsv+5, 170 + pageStartY, 50, 20
		ui_outfield `Дверь`, pxv+xsv+5, 195 + pageStartY, 50, 20

		ui_outfield str(formatt, gs_sash_width), pxv+xsv+10, 82 + pageStartY, 50, 20 ui_tooltip `Ширина Обвязки Полотна`
		ui_infield "rab_u2", pxv+xsv+5+50+1, 193 + pageStartY, 50, 20 ui_tooltip `Ширина Нижней Обвязки Полотна`
return


! =============================================================================
! 4. Elevation and Opening Tabpage
! =============================================================================

"pageShapeOpening":
	pageStartY = 3

	ui_style 0, 0

	sep1x = 270
	seply = 50 + pageStartY

	x = sep1x-210
	y = seply+10

	cx = 55

	dy = 20

	ui_separator sep1x, pageStartY, sep1x, 230 + pageStartY
	ui_separator 0, seply, sep1x-2, seply

	if gs_kamp = 1 then
		ui_pict "ui_balc_1_t(13)", x, y, 140, 140, 1
		ui_infield "gs_k", x+140+5, y+60, cx, 20 ui_tooltip `Высота Фрамуги в`
	else
		ui_pict "ui_balc_1(13)", x, y, 140, 140, 1
	endif

	ui_infield "parapeth", 4, y+100, cx, 20 ui_tooltip `Высота Ниши`
	ui_infield "bf1", x+20, y+140+5, cx, 20 ui_tooltip `Ширина Окна`

	y = 3 + pageStartY

	ui_infield{3} "gs_kamp",	5,		y,		130,	18,
		7,"",2,1,
		0,0,0,0,
		1,`Фрамуга`, 0,
		2,`Фрамуга`, 1

	y = y + dy

	ui_infield{3} "symm",	5,	y,	130,	25,
		7,"",2,1,
		0,0,0,0,
		1,`Равная Ширина Двери и Окна`, 0,
		2,`Равная Ширина Двери и Окна`, 1



!************************ Opening parameters

	y = 6 + pageStartY

	ui_style 0, 1
	ui_outfield `Открывание`, sep1x+10, y, 80, 18
	y = y + dy - 5

	ui_separator sep1x+2, 30 + pageStartY, 440, 30 + pageStartY


	if gs_ui_sub_tabpage2 = 1 then

		!*** 3D Opening

		ui_infield{3} "gs_ui_sub_tabpage2", 365, pageStartY, 64, 25,
			4, "uiTabsub_Terrace_opening_flat(1)", 2, 1,
			0,0, 28,21,
			1, "",	1,
			2, "",	2 ui_tooltip `Выбор Параметров Проема в 3D/2D`

		y = y + dy - 9


		!*** opening type

		ui_style 0, 1

		ui_outfield `Тип`, sep1x+30, y, 85, 18
		ui_outfield `Угол`, sep1x+120, y, 70, 18
		y = y + dy + 4

		ui_style 0, 1
		ui_outfield `1.`, sep1x+10, y, 20, 18
		ui_style 0, 0
		ui_infield{3} "iWindowOpeningType", sep1x+30, y-3, 80, 18
		ui_infield "gs_op", sep1x+120, y-3, 55, 18
		y = y + dy

		ui_style 0, 1
		ui_outfield `2.`, sep1x+10, y, 20, 18
		ui_style 0, 0
		ui_infield{3} "iDoorOpeningType", sep1x+30, y-3, 80, 18
		ui_infield "op2", sep1x+120, y-3, 55, 18
		y = y + dy

		ui_style 0, 1
		ui_outfield `3.`, sep1x+10, y, 20, 18
		ui_style 0, 0
		ui_infield{3} "iTransomOpeningType", sep1x+30, y-3, 80, 18
		ui_infield "gs_opt", sep1x+120, y-3, 55, 18
		y = y + dy

		ui_style 0, 1
		ui_outfield `4.`, sep1x+10, y, 20, 18
		ui_style 0, 0
		ui_infield{3} "iDoorTransomOpeningType", sep1x+30, y-3, 80, 18
		ui_infield "opt2", sep1x+120, y-3, 55, 18
		y = y + dy


		!*** opening angle / line type

		x_t = 45
		x_v = 50
		x_v2 = 52

		y = y + dy
		y = y + dy
		y = y + dy
		y = y + dy
		y = y + dy

	endif

	if gs_ui_sub_tabpage2 = 2 then

		!*** 2D Opening

		ui_infield{3} "gs_ui_sub_tabpage2", 365, pageStartY, 64, 25,
			4, "uiTabsub_Terrace_opening_flat(1)", 2, 1,
			0,0, 28,21,
			1, "",	1,
			2, "",	2 ui_tooltip `Выбор Параметров Проема в 3D/2D`

		xt = 85
		xv1 = 60
		xv2 = 75
		ys = 23

		ui_style 0, 1

		ui_outfield `Дверь`, sep1x+10, y+dy+3, 85, ys
		dy = dy + ys + 1

		ui_style 0, 0

		ui_outfield `Угол в 2D`, sep1x+10, y+dy+3, 85, ys
		ui_infield "gs_ow21", sep1x+10+85+1, y+dy, xv1, ys
		dy = dy + ys + 1

		dy = dy + ys + 1

		ui_style 0, 1

		ui_outfield `Окно`, sep1x+10, y+dy+3, 85, ys
		dy = dy + ys + 1

		ui_style 0, 0

		ui_outfield `Угол в 2D`, sep1x+10, y+dy+3, 85, ys
		ui_infield "gs_ow22", sep1x+10+85+1, y+dy, xv1, ys
		dy = dy + ys + 1

		dy = dy + ys + 1

	endif
return



! =============================================================================
! 5. Grid Settings Tabpage
! =============================================================================

"pageGrid":
	pageStartY = 8

	x = 30
	y = pageStartY

	sep1x = 200
	seply = 167 + pageStartY

	xt = 55
	xv = 45

	dy = 3

	ui_separator sep1x, pageStartY - 5, sep1x, seply-1
	ui_separator 0, seply, 444, seply

	ui_pict "ui_balc_1_t_gr(13)", x, y, 140, 140, 1

	ui_style 0, 1

	ui_outfield `Переплеты`, 5, seply+3, 88, 18
	dy = dy + 19

	ui_style 0, 0

	ui_outfield `Ширина`, 10, seply+dy, 80, 18
	ui_infield "gs_spb", 95, seply+dy-3, 50, 20
	dy=dy+21
	ui_outfield `Толщина`, 10, seply+dy, 80, 18
	ui_infield "gs_sps", 95, seply+dy-3, 50, 20


	!**** panes number

	ui_style 0, 1

	dy = 0

	ui_outfield `1. Количество Стекол Створки Окна`, sep1x+10, y+dy, 230, 18
	dy = dy + 21

	ui_style 0, 0

	ui_outfield `Вертикаль`, sep1x+20, y+dy+3, 55, 20
	ui_infield "gs_sh", sep1x+20+55+2, y+dy, xv, 20
	ui_outfield `По Вертик.`, sep1x+20+55+2+xv+15, y+dy+3, 55, 20
	ui_infield "gs_sv", sep1x+20+55+2+xv+15+55+2, y+dy, xv, 20

	dy = dy + 22

	ui_style 0, 1

	ui_outfield `2. Количество Стекол Полотна Двери`, sep1x+10, y+dy, 230, 18
	dy = dy + 19

	ui_style 0, 0

	ui_outfield `Вертикаль`, sep1x+20, y+dy+3, 55, 20
	ui_infield "sh2", sep1x+20+55+2, y+dy, xv, 20
	ui_outfield `По Вертик.`, sep1x+20+55+2+xv+15, y+dy+3, 55, 20
	ui_infield "sv2", sep1x+20+55+2+xv+15+55+2, y+dy, xv, 20

	dy = dy + 22

	ui_style 0, 1

	ui_outfield `3. Количество Стекол Фрамуги 1`, sep1x+10, y+dy, 230, 18
	dy = dy + 19

	ui_style 0, 0

	ui_outfield `Вертикаль`, sep1x+20, y+dy+3, 55, 20
	ui_infield "gs_sho", sep1x+20+55+2, y+dy, xv, 20
	ui_outfield `По Вертик.`, sep1x+20+55+2+xv+15, y+dy+3, 55, 20
	ui_infield "gs_svo", sep1x+20+55+2+xv+15+55+2, y+dy, xv, 20

	dy = dy + 22

	ui_style 0, 1

	ui_outfield `4. Количество Стекол Фрамуги 2`, sep1x+10, y+dy, 230, 18
	dy = dy + 19

	ui_style 0, 0

	ui_outfield `Вертикаль`, sep1x+20, y+dy+3, 55, 20
	ui_outfield str((gs_sho+1), 2, 0), sep1x+20+55+2, y+dy+3, xv, 20
	ui_outfield `По Вертик.`, sep1x+20+55+2+xv+15, y+dy+3, 55, 20
	ui_infield "svo2", sep1x+20+55+2+xv+15+55+2, y+dy, xv, 20
return


! =============================================================================
! 6. Sill Settings Tabpage
! =============================================================================

"pageSillBoard":
	pageStartY = 3

	px = 50
	py = 25 + pageStartY

	xs = 156
	ys = 100

	xv = 45
	x_sep = px+xs+55

	ui_separator x_sep, pageStartY - 3, x_sep, 230 + pageStartY


	!*** sill type
	ui_style 0, 1
	ui_outfield `Тип`, 10, py+110, 120, 20

	ui_style 0, 0
	ui_infield{3} "iSillType", 131, py+105, 45, 25,
		2, "sill_type_bal(13)",
		2, 1,
		45, 25, 32, 22,
		1,	`Сплошной`,	1,	! Solid
		2,	`Металл`,	2	! Metal


	!*** sill pict

	ui_style 0, 1
	ui_outfield `Отлив`, 10, pageStartY, 110, 20

	ui_style 0, 0

	if iSillType = 1 then		! Solid sill

		!*** solid sill

		ui_pict "balc_sill(13)", px, py, xs, ys, 1

		ui_infield "gs_w", px+xs+2, py+54, xv, 20 ui_tooltip `Высота Капельника`
		ui_infield "gs_nd", px+xs+2, py+ys-15, xv, 20 ui_tooltip `Глубина Капельника`
		ui_infield "gs_nh", px+xs+2, py+33, xv, 20 ui_tooltip `Высота Отлива Снаружи`

	else

		!*** metal sill

		ui_pict "balc_sill_m(13)", px, py, xs, ys, 1

		ui_infield "gs_nh", px+xs+2, py+50, xv, 20 ui_tooltip `Высота Отлива Снаружи`

	endif

	ui_infield "gs_fbl", px+xs+2, py-4, xv, 20 ui_tooltip `Глубина Отлива`
	ui_infield "gs_fbh", 0, py+40, xv, 20 ui_tooltip `Высота Отлива Внутри`


	!*** board pict

	ui_style 0, 1
	ui_outfield `Доска`, x_sep+10, pageStartY, 120, 20

	ui_pict "balc_board_wallins(13)", x_sep+10+xv+1, py, 120, 100, 1

	ui_style 0, 0
	ui_infield "gs_sfbi", x_sep+10, py+45, xv, 20 ui_tooltip `Высота Доски`
	ui_infield "gs_siv", x_sep+10, py-5, xv, 20 ui_tooltip `Глубина Доски`
	ui_infield "gs_thn", x_sep+10, py+ys-15, xv, 20 ui_tooltip `Глубина Подоконной Ниши`


	!*** Wall Inset

	ui_infield "gs_hn", x_sep+10, py+100+12, 20, 20
	ui_outfield `Подоконная Ниша`, x_sep+31, py+100+14, 120, 20


	!*** overhang parameters + materials

	ui_style 0, 0

	pyy = py+100+38
	dy=0

	ui_outfield `Выступ Сбоку`, 10, pyy, 120, 20
	ui_infield "gs_suba_l", 131, pyy-3, 50, 20
	dy=dy+21
	ui_outfield `Выступ Сбоку 2`, 10, pyy+dy, 120, 20
	ui_infield "gs_suba_r", 131, pyy-3+dy, 50, 20

	dy=0
	ui_outfield `Выступ Сбоку`, x_sep+10, pyy, 120, 20
	ui_infield "gs_subi_l", x_sep+131, pyy-3, 50, 20
	dy=dy+21
	ui_outfield `Выступ Сбоку 2`, x_sep+10, pyy+dy, 120, 20
	ui_infield "gs_subi_r", x_sep+131, pyy-3+dy, 50, 20

return


! =============================================================================
! 7. Venetian Blinds Settings Tabpage
! =============================================================================

"pageVenetianBlind":
	pageStartY = 16

	px = 20
	py = 52 + pageStartY
	xd = 92
	yd = 115
	dy = 0

	sep_x = px + xd + 10

	ui_style 0, 1

	ui_infield "sonn", 20, pageStartY, 20, 20
	ui_outfield `На Окне`, 41, pageStartY + 3, 110, 20

	ui_infield "sonn2", 20, 21 + pageStartY, 20, 20
	ui_outfield `На Двери`, 41, 23 + pageStartY, 110, 20

	ui_style 0, 0

	if sonn+sonn2>0 then
		ui_pict "1_Flugelf_sonn_44(13)", px, py, xd, yd, 1
	else
		ui_pict "1_Flugelf_sonn_5(13)", px, py, xd, yd, 1
	endif

	ui_outfield "hl:", sep_x+10, py+3, 15, 20
	ui_outfield `Ширина Ламелей`, sep_x+10+17, py+3, 95, 20
	ui_infield "ws", sep_x+151, py, 50, 22

	dy = dy + 23

	ui_outfield `Поворот Ламелей - %`, sep_x+10, py+3+dy, 140, 20
	ui_infield "rotation", sep_x+151, py+dy, 50, 22
	dy = dy + 23

	ui_outfield `Открытие в 3D - %`, sep_x+10, py+3+dy, 140, 20
	ui_infield "offlam", sep_x+151, py+dy, 50, 22
	dy = dy + 23

return



! =============================================================================
! 8. Shutter Settings Tabpage
! =============================================================================

"pageShutter":
	pageStartY = 3

	py = 62 + pageStartY
	x1 = 10

	ui_style 0, 1

	ui_infield "t_sonn", x1, pageStartY, 20, 20
	ui_outfield `Ставни`, x1+21, pageStartY+3, 120, 20

	ui_outfield `Стиль Полотна`, x1, 24+pageStartY+3, 100, 20
	ui_infield "shutterStyle", x1+102, 24+pageStartY, 100, 20

	ui_style 0, 0

	if iShutterStyle = SHUTTER_HOLES then
		ui_pict "1-Flu_schiebe_sn_gel(13)", x1, py, 92, 115, 1
	endif

	if iShutterStyle = SHUTTER_FIX or iShutterStyle = SHUTTER_LOUVER then
		ui_pict "1-Flu_schiebe_sn_lam(13)", x1, py, 92, 115, 1
	endif

	if iShutterStyle = SHUTTER_FRAMED then
		ui_pict "1-Flu_schiebe_sn_rah(13)", x1, py, 92, 115, 1
	endif

	if iShutterStyle = SHUTTER_SOLID then
		ui_pict "1-Flu_schiebe_sn_vol(13)", x1, py, 92, 115, 1
	endif


	!*** parameters

	xt = 100
	xv = 50
	dy = 0
	xa = 25

	xx = x1+100

	ui_outfield "rb:", xx, py, 25, 20
	ui_outfield `Ширина Коробки`, xx+25+1, py, 100, 20
	ui_infield "rab_s", xx+25+100+2, py-5, 50, 20
	dy = dy + 21

	ui_outfield `Ширина Коробки Внизу`, xx+25+1, py+dy, 100, 20
	ui_infield "rabu_s", xx+25+100+2, py-5+dy, 50, 20
	dy = dy + 21

	ui_outfield "rs:", xx, py+dy, 25, 20
	ui_outfield `Толщина Коробки`, xx+25+1, py+dy, 100, 20
	ui_infield "ras_s", xx+25+100+2, py-5+dy, 50, 20
	dy = dy + 21

	ui_outfield "hl:", xx, py+dy, 25, 20
	ui_outfield `Ширина Ламелей`, xx+25+1, py+dy, 100, 20
	ui_infield "hlbsc", xx+25+100+2, py-5+dy, 50, 20
	dy = dy + 21

	ui_outfield `Поворот Ламелей`, xx+25+1, py+dy, 100, 20
	ui_infield "alpha", xx+25+100+2, py-5+dy, 50, 20
	dy = dy + 21

	ui_outfield `Открытие в 3D - %`, xx+25+1, py+dy, 100, 20
	ui_infield "offladen", xx+25+100+2, py-5+dy, 50, 20
	dy = dy + 21

	ui_outfield "sb:", xx, py+dy, 25, 20
	ui_outfield `Толщина Направляющей`, xx+25+1, py+dy, 100, 20
	ui_infield "sck", xx+25+100+2, py-5+dy, 50, 20
	dy = dy + 21


	!*** parameters 2

	x2 = 310
	y = 59 + pageStartY
	dy = 0

	ui_infield "bedec", x2, y+dy, 20, 20
	ui_outfield `Отлив`, x2+21, y+3+dy, 84, 20
	dy = dy + 21

	ui_outfield `Отв. по Гориз.`, x2, y+3+dy, 84, 20
	ui_infield "nnsc", x2+85, y+dy, 40, 20
	dy = dy + 21

	ui_outfield `Отв. по Верт.`, x2, y+3+dy, 84, 20
	ui_infield "mmsc", x2+85, y+dy, 40, 20
	dy = dy + 21

	ui_outfield `Радиус Отверстия`, x2, y+3+dy, 84, 20
	ui_infield "rsc", x2+85, y+dy, 40, 20
	dy = dy + 21

	ui_outfield `Разрешение Отверстия`, x2, y+3+dy, 84, 20
	ui_infield "res", x2+85, y+dy, 40, 20
	dy = dy + 21 + 1

return

"pageCallListing":
	call "ui_Description_m" parameters	iDWObjectType			= 3,		! TERRACE_D_OBJECT
										gs_show_UKListParams	= 0,
										listFieldID 			= iUiID_Descriptions,
										gs_bOpeningToZone		= gs_bOpeningToZone,
										gs_list_cost			= gs_list_cost,
										gs_list_manufacturer	= gs_list_manufacturer,
										gs_list_note			= gs_list_note,
										gs_list_location		= gs_list_location,
										gs_list_location_as		= gs_list_location_as,
										gs_list_accessories		= gs_list_accessories,
										gs_list_glazing			= gs_list_glazing,
										gs_list_headdet			= gs_list_headdet,
										gs_list_jambdet			= gs_list_jambdet,
										gs_list_silldet			= gs_list_silldet,
										gs_list_hwsetno			= gs_list_hwsetno,
										gs_list_optype			= gs_list_optype,
										gs_list_finish			= gs_list_finish,
										gs_list_elevation		= gs_list_elevation,
										gs_list_hings			= gs_list_hings,
										gs_list_lockset			= gs_list_lockset,
										gs_list_glassarea_tmp	= gs_list_glassarea_tmp,
										gs_list_glassarea_override	= gs_list_glassarea_override,
										gs_list_glassarea		= gs_list_glassarea,
										gs_list_glassperimeter	= gs_list_glassperimeter,
										gs_list_heattransfer	= gs_list_heattransfer,
										gs_list_firerating		= gs_list_firerating,
										gs_list_acousticrating	= gs_list_acousticrating,
										FM_Type					= FM_Type,
										iFMType					= iFMType,
										FM_InventoryNumber		= FM_InventoryNumber,
										FM_SerialNumber			= FM_SerialNumber,
										FM_ProductionYear		= FM_ProductionYear,
										FM_ObjectWeight			= FM_ObjectWeight,
										FM_ObjectWeightUnit		= FM_ObjectWeightUnit,
										gs_list_orientationDef_m = gs_list_orientationDef_m,
										ifc_optypestr			= ifc_optypestr,
										ifc_optype				= ifc_optype							
return


