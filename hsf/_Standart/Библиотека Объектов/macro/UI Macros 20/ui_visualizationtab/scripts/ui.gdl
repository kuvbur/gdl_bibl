
!---------- MEP objects visualization tab -----------
pageStartY = 5

if ui_appearance_type_m = 0 then

	UI_SEPARATOR 222, pageStartY - 1, 222, pageStartY + 230

	add_Y = 24

	x1 = 5
	x2 = x1 + 30
	x3 = x2 + 150
	x4 = x1 + 117
	x5 = x3 + 27
	x6 = x2 + 27
	y = pageStartY - 3

	UI_STYLE 0,1
	UI_OUTFIELD `2D-отображение`, x1, y, x3-x2,18		! --- Plan Symbol ---
	UI_STYLE 0, 0
	y = y + add_Y

	cx = 90
	IF bEnable2dDetail THEN
		if iObjectType = SINK_BELFAST | iObjectType = SINK_CORNER | iObjectType = SINK_GENERAL then
			ui_outfield `Тип Символа Мойки`, x1, y, x4-x1-1,   15
			ui_infield{3} "SinkSymbol", x4, y-4, cx, 20,
					8, "",
					2,
					1, -x3-1, 20, -x3-1, 20,
					"", `Детали Слива`, `Детали Слива`,
					"", `Детальный`, `Детальный`,
					"", `Простой`, `Простой`
			y = y + add_Y
		else
			if iObjectType <> TAP & iObjectType <> SHOWER_TAP then
				ui_outfield `2D-детализация`, x1, y, x4-x1-1,   15

				dim stDetail2d[]

				call "2dDetailLevel" parameters	all,
						returned_parameters stDetail2d

				ui_infield{3} "gs_detlevel_2D_m", x4, y-4, cx, 20,
						8, "",
						2,
						1, -x3-1, 20, -x3-1, 20,
						"", stDetail2d[1], 0,
						"", stDetail2d[2], 3,
						"", stDetail2d[3], 2,
						"", stDetail2d[4], 1

				y = y + add_Y
			endif
		endif

		if	iObjectType = TAP | iObjectType = SHOWER_TAP then
			ui_outfield `Тип 2D-символа`, x1, y, x4-x1-1,   15
			if iObjectType = SHOWER_TAP then
				ui_infield{3} "TapSymbol", x4, y-4, cx, 20,
						8, "",
						4,
						1, -x3-1, 20, -x3-1, 20,
						"", `Детальный`, `Детальный`,
						"", `Простой`, `Простой`,
						"", `Символический 1`, `Символический 1`,
						"", `Символический 2`, `Символический 2`
			else
				ui_infield{3} "TapSymbol", x4, y-4, cx, 20,
						8, "",
						2,
						1, -x3-1, 20, -x3-1, 20,
						"", `Детальный`, `Детальный`,
						"", `Простой`, `Простой`,
						"", `Символический`, `Символический`
			endif
			y = y + add_Y
		endif

		if	iObjectType = MOP_SINK		|\
			iObjectType = SINK_BELFAST	|\
			iObjectType = SINK_CORNER	|\
			iObjectType = SINK_GENERAL	then
			ui_outfield `Тип Символа Смесителя`, x1, y, x4-x1-1,   15
			ui_infield{3} "TapSymbol", x4, y-4, cx, 20,
					8, "",
					2,
					1, -x3-1, 20, -x3-1, 20,
					"", `Детальный`, `Детальный`,
					"", `Простой`, `Простой`,
					"", `Символический`, `Символический`,
					"", `Откл.`, `Откл.`
			y = y + add_Y
		endif
	ENDIF

	if iObjectType = SHOWER_CABIN then
		ui_infield{3} "bOpenDoor2D", x1, y-3, 120, 20,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, `Дверь откр.в 2D`, 0,
				1, `Дверь откр.в 2D`, 1
		y = y + add_Y
	endif

	cx = 40
	UI_PICT 4, x1, y-3, 24,17, 1		! ui_frame_pen_MEP.png
	UI_OUTFIELD `Перо Контура`, x2,y, x5-x2-cx-1,18	! Contour Pen
	UI_INFIELD "gs_cont_pen",	 x5-cx,y-4, cx,22
	y = y + add_Y

	UI_PICT 3, x1, y-3, 13,17, 1		! ui_fill_type_MEP.png

	UI_OUTFIELD `Тип Штриховки`, x2,y, x3-x2,15, 0			! Fill Type
	UI_INFIELD "gs_fill_type",		 x6,y+16, x3-x2,25
	y = y + add_Y*2

	cx = 40
	UI_PICT 2, x1, y-3, 24,17, 1		! ui_fill_pen_MEP.png

	UI_OUTFIELD `Перо Штриховки`, x2,y, x5-x2-cx-1,18, 0	! Fill Pen
	UI_INFIELD "gs_fill_pen",	 x5-cx,y-6, cx,23
	y = y + add_Y

	UI_PICT 1, x1, y-3, 24,17, 1		! ui_back_pen_MEP.png

	UI_OUTFIELD `Перо Фона Штриховки`, x2,y, x5-x2-cx-1,18, 0	! Background Pen
	UI_INFIELD "gs_back_pen",	  x5-cx,y-6, cx,23
	y = y + add_Y

	if bEnableDashedLineType then
		UI_PICT "ui_contour_line_flat(1)", x1, y-3, 20, 20, 1

		UI_OUTFIELD `Тип Штриховой Линии`, x2,y, x3-x2,15, 0	
		UI_INFIELD "gs_DashedLine",	 x6,y+16, x3-x2,27
		y = y + add_Y
	endif

	IF bEnableCenterlinePars THEN
		y = y + 5

		UI_PICT 34, x1, y-2, 24,17, 1

		UI_OUTFIELD `Тип Линии Оси`, x2,y, x3-x2,15, 0		! Centerline Type
		UI_INFIELD "MEP_cline_type",		 x2,y+16, x3-x2,25
		y = y + 19 + add_Y

		cx = 40
		UI_PICT 35, x1, y, 24,17, 1

		UI_OUTFIELD `Перо Линии Оси`, x2,y+3, x3-x2-cx-1,18, 0	! Centerline Pen
		UI_INFIELD "MEP_cline_pen",	 x3-cx,y, cx,22
		y = y + add_Y
	ENDIF


	x1 = 228
	x2 = x1 + 18
	x3 = x2 + 100
	xEnd = 440
	y = pageStartY - 3

	UI_STYLE 0,1
	UI_OUTFIELD `3D-отображение`, x1, y, xEnd-x1,18		! --- 3D View ---
	y = y + 24
	UI_STYLE 0, 0

	IF bEnable3dDetail THEN
		UI_OUTFIELD `3D-детализация`, x1, y, x3-x1-1,   15
		UI_INFIELD{3} "gs_detlevel_3D_m", x3, y-4, xEnd-x3-1, 20,
				8, "",
				2,
				1, xEnd-x3-1, 20, xEnd-x3-1, 20,
				"", `Детальный`, 2,
				"", `Простой`, 1,
				"", `Упрощенный`, 3,
				"", `Откл.`, 0

		y = y + 24
	ENDIF

	if bResol then
		UI_OUTFIELD	`Аппроксимация`,	x1,		y,	x3-x1-2,		13
		UI_INFIELD	"gs_resol",		x3,		y-4,	xEnd-x3-1,	18
		y = y + 24
	ENDIF

	if bEnableShadow then
		ui_infield{3} "gs_shadow", 		x1, y-3, xEnd-x1, 	20,			! Shadow
			7, "",
			2,
			1, 20, 20, 20, 20,
			0, `Отбрасывание Теней`, 0,
			1, `Отбрасывание Теней`, 1
		y = y + 24
	endif

	IF bEnableUseSystemMat THEN
		stUseSysMat = " "+`Покрытие Системы`
		UI_INFIELD{3} "useSysMat", x1, y-3, xEnd-x1, 20,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, stUseSysMat, 0,
				1, stUseSysMat, 1
		y = y + 24
	ENDIF

	nMaterials = 0
	for i = 1 TO vardim1(ui_matpar_names)
		if ui_matpar_names[i][2] <> "" then nMaterials = nMaterials + 1
	next i

	nMaterialRows = 9 - bEnable3dDetail - bResol - bEnableShadow - bEnableUseSystemMat

	bOneRowMaterials = 1
	if nMaterialRows >= (nMaterials * 2) then bOneRowMaterials = 0

	bMoreMaterialOptions = 0
	if nMaterials > nMaterialRows then bMoreMaterialOptions = 1

	for i = 1 TO MIN(vardim1(ui_matpar_names), nMaterialRows)
		if ui_matpar_names[i][2] <> "" then
			bLocked = 0
			if vardim1(ui_matpar_locked) >= i then
				bLocked = ui_matpar_locked[i]
			endif

			if not(i = nMaterialRows and bMoreMaterialOptions) then
				if bLocked then
					ui_pict 5, x1, y-2, 12,17, 1			! ui_mat_lock_MEP.png
				else
					ui_pict 6, x1, y-2, 12,17, 1			! ui_mat_MEP.png
				endif
			endif

			if bOneRowMaterials then
				if i = nMaterialRows and bMoreMaterialOptions then
					ui_style 1,0
					ui_outfield `Доп. настройки Покрытий во вкладке Параметров`, x1, y, 20+xText+125, 15
					ui_style 0,0
				else
					xText = 71

					matPlace = STRSTR(ui_matpar_names[i][1], " " + `Покрытие`)
					if matPlace > 0 then ui_matpar_names[i][1] = STRSUB(ui_matpar_names[i][1], 1, matPlace-1)

					ui_outfield ui_matpar_names[i][1], x1 + 15, y, xText, 15, 4 * bLocked
					ui_infield  ui_matpar_names[i][2], x1 + 20 + xText, y-4, 120, 23
					y = y + 24
				endif
			else
				ui_outfield ui_matpar_names[i][1] + " ", x1 + 18, y, xEnd-x1-31, 15, 4 * bLocked
				y = y + 24
				ui_infield  ui_matpar_names[i][2], x2, y-6, 195, 23
				y = y + 24
			endif
		endif
	next i

	y = y + 2 + ui_3dviewpar_rowspace
endif

! ---------- Appliance visualization tab -----------
if ui_appearance_type_m = 2 then
	UI_SEPARATOR	5,		pageStartY + 135,	440,	pageStartY + 135
	UI_SEPARATOR	222,	pageStartY + 140,	222,	pageStartY + 225

	x1 = 5
	x2 = 120
	x3 = 235
	x4 = 360
	x5 = 400
	x6 = 440
	yCurrent = pageStartY + 25
	ui_style 0,0

	ui_outfield	`Перо Контура`,		x3,	yCurrent,	x5-x3-2,	15
	ui_infield	"gs_cont_pen",		x5,	yCurrent-4,	x6-x5,		22
	yCurrent = yCurrent +27

	ui_outfield	`Тип Штриховки`,		x3,	yCurrent,	x4-x3-2,	15
	ui_infield	"gs_fill_type",		x4,	yCurrent-4,	x6-x4,		22
	yCurrent = yCurrent +27

	ui_outfield	`Перо Штриховки`,			x3,	yCurrent,	x5-x3-2,	15
	ui_infield	"gs_fill_pen",		x5,	yCurrent-4,	x6-x5,		22
	yCurrent = yCurrent +27

	ui_outfield	`Перо Фона`,	x3,	yCurrent,	x5-x3-2,	15
	ui_infield	"gs_back_pen",		x5,	yCurrent-4,	x6-x5,		22

	if bEnableMaterials then
		UI_SEPARATOR	222,	pageStartY,	222,	pageStartY + 132

		yCurrent = pageStartY

		ui_style 0,1
		ui_outfield	`2D-отображение`,	x3,	yCurrent,	x6-x3,	15
		ui_outfield	`Покрытия`,			x1,	yCurrent,	215-x1,	15
		ui_style 0,0
		yCurrent = pageStartY + 25

		for i = 1 TO vardim1(ui_matpar_names)
			if ui_matpar_names[i][2] <> "" THEN
				ui_outfield	ui_matpar_names[i][1],		x1,		yCurrent,	x2-x1-2, 15
				ui_infield	ui_matpar_names[i][2],		x2,		yCurrent-4,	215-x2,	24
				yCurrent = yCurrent +27
			endif
		next i
	endif

	yCurrent = pageStartY + 139

	UI_STYLE 0,1
	ui_outfield	`Минимальное Пространство`,		x3,	yCurrent,	x6-x3,	15
	ui_outfield	`3D-отображение`,	x1,	yCurrent,	215-x1,	15
	ui_style 0,0
	yCurrent = yCurrent +24

	ui_outfield	`Спереди`,			x3,	yCurrent,	x4-x3-2,	15
	ui_infield	"MSFront",			x4,	yCurrent-4,	x6-x4,		22
	yCurrent = yCurrent +23

	ui_outfield	`Сбоку 1`,		x3,	yCurrent,	x4-x3-2,	15
	ui_infield	"MSSide",			x4,	yCurrent-4,	x6-x4,		22
	yCurrent = yCurrent +23

	ui_outfield	`Сбоку 2`,	x3,	yCurrent,	x4-x3-2,	15
	ui_infield	"MSSide2",			x4,	yCurrent-4,	x6-x4,		22

	yCurrent = pageStartY + 163

	ui_outfield	`3D`,				x1,	yCurrent,	x2-x1-2,	15
	ui_infield	"gs_detlevel_3D",	x2,	yCurrent-4,	215-x2,		22
	yCurrent = yCurrent +24

	if bResol then
		UI_OUTFIELD	`Аппроксимация`,	x1,		yCurrent,	x2-x1-2,		13
		UI_INFIELD	"gs_resol",		x2,		yCurrent-4,	215-x2,	18
	ENDIF

	yCurrent = yCurrent +24

	if bEnableShadow then
		ui_infield{3} "gs_shadow", 		x1, yCurrent-2, 215-x1, 	15,			! Shadow
					7, "",
					2,
					1, 20, 20, 20, 20,
					0, `Отбрасывание Теней`, 0,
					1, `Отбрасывание Теней`, 1
	endif
endif
