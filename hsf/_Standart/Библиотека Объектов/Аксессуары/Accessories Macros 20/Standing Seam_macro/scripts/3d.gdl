
material sheet_mat

if ac_pitch>=90 or ac_pitch<=-90 then ac_pitch=0
length=b/cos(ac_pitch)

rotx -90+ac_pitch

plate_thk=0.001

xleft=plate_thk/2
xright=plate_thk/2
xhook=1/2"
xribh=1/2"
xribw=1/2"
xribd=(seam_space-xhook/2-seam_rib*xribw)/(seam_rib+1)

if gs_detlevel_3Dm=2 then
	put -plate_thk/2,-(seam_height-xhook/2),15
	put  plate_thk/2,-(seam_height-xhook/2),15
	put  plate_thk/2,-plate_thk/2,15
	if seam_rib > 0 then
		for i=1 to seam_rib
			xr=i*xribd+(i-1)*xribw
			put xr,-plate_thk/2,15
			put xr+xribw/2,-xribh-plate_thk/2,15
			put xr+xribw,-plate_thk/2,15
		next i
	endif
	put seam_space-xhook/2-plate_thk/2,-plate_thk/2,15
	put seam_space-xhook/2-plate_thk/2,-seam_height-plate_thk/2,15
	put seam_space+xhook/2+plate_thk/2,-seam_height-plate_thk/2,15
	put seam_space+xhook/2+plate_thk/2,-seam_height/2,15
	put seam_space+xhook/2-plate_thk/2,-seam_height/2,15
	put seam_space+xhook/2-plate_thk/2,-seam_height+plate_thk/2,15
	put seam_space-xhook/2+plate_thk/2,-seam_height+plate_thk/2,15
	put seam_space-xhook/2+plate_thk/2,+plate_thk/2,15
	if seam_rib > 0 then
		for i=1 to seam_rib
			xr=i*xribd+(i-1)*xribw+0
			put seam_space-xhook/2-xr,plate_thk/2,15
			put seam_space-xhook/2-xr-xribw/2,-xribh+plate_thk/2,15
			put seam_space-xhook/2-xr-xribw,plate_thk/2,15
		next i
	endif
	put  -plate_thk/2,plate_thk/2,15
	put -plate_thk/2,-(seam_height-xhook/2),-1

	for sumw=-xleft to a+xright step seam_space
		addx sumw
		prism_	nsp/3, length,
				use(nsp)
		del 1
	next sumw
else
	put 0,-(seam_height-xhook/2),0
	put 0,0,0
	if seam_rib > 0 then
		for i=1 to seam_rib
			xr=i*xribd+(i-1)*xribw
			put xr,0,0
			put xr+xribw/2,-xribh,0
			put xr+xribw,0,0
		next i
	endif
	put seam_space-xhook/2,0,0
	put seam_space-xhook/2,-seam_height,0
	put seam_space+xhook/2,-seam_height,0
	put seam_space+xhook/2,-seam_height/2,0

	for sumw=-xleft to a+xright step seam_space
		addx sumw
		extrude nsp/3,        !number of polyline nodes
			0,0,length,         !dx,dy,dz extrusion direction vector
			32+16,              !mask:  Base and top edges visible
			use(nsp)            !xi,yi,si polygon points		del 1
		del 1
	next sumw
endif

if seam_joint<>0 then
	k=0
	n=int(length/joint_dist)

	for sumw=0 to a step seam_space
		k=k+1
		for i=1 to n
			if k/2=int(k/2) then
				addz joint_dist
			else
				if i=1 then
					addz joint_dist/2
				else
					addz joint_dist
				endif
			endif

			plane   4,
					xribw/2, -xribh/2, -0.005,
					seam_space-xribw/2, -xribh/2, -0.005,
					seam_space-xribw/2, -xribh/2, 0.005,
					xribw/2, -xribh/2, 0.005

		next i
		del n
		addx seam_space
	next sumw
	del 1
endif
del 1

end

