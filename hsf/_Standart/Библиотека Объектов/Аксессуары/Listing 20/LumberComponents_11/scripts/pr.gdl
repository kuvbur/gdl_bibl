
!!--------------------------!!
!!---- Operation Method ----!!
!!--------------------------!!

If not(op_met) Then
	gosub 1000+pack_type
Else
	If vardim2(wood_pack)>2 Then
		For i=1 To vardim1(wood_pack)
			H=wood_pack[i][1]
			B=wood_pack[i][2]
			L=wood_pack[i][3]
			QTY=1

			gosub 1000+pack_type
		Next i
	EndIf
EndIf

End
!-----------------------------

1001:
!! Pack Type : USA

SQUOT="'"
DQUOTE='"'
TOL=0.0625          ! cut length tolerance is 1/16 in. 05/21 fl
MTI=1000/25.4       ! meter to inches
MTF=MTI/12          ! meter to feet

WI=B*MTI
HI=H*MTI
if (WI-INT(WI))>TOL THEN WI=INT(WI)+1   ! width is rounded up to nearest inch
if (HI-INT(HI))>TOL THEN HI=INT(HI)+1   ! height is rounded up to nearest inch

LINFT=L*MTF
LININ=LINFT-int(LINFT)
LININ=LININ*12
LFRAC=LININ-INT(LININ)
!
! cut length is rounded up to the nearest quarter in.
!
IF LFRAC>(0.75+TOL) THEN
	IF LININ > 10.5 THEN    !05/21 FL
		LININ=0
		LINFT=LINFT+1
	ELSE
		LININ=INT(LININ)+1
	ENDIF
	LQTR=""
ELSE
	IF LFRAC>(0.5+TOL) THEN
		LININ=INT(LININ)
		LQTR=" 3/4"
	ELSE
		IF LFRAC>(0.25+TOL) THEN
			LININ=INT(LININ)
			LQTR=" 1/2"
		ELSE
			IF LFRAC>(TOL) THEN
				LININ=INT(LININ)
				LQTR=" 1/4"
			ELSE
				LININ=INT(LININ)
				LQTR=""
			ENDIF
		ENDIF
	ENDIF
ENDIF

!++++
BDFT=HI*WI*LINFT/12*QTY
WSTR=STR(WI,2,0)
HSTR=STR(HI,2,0)
LSTRF=STR(LINFT,2,0)
LSTRI=STR(LININ,2,0)
CUTSTR=WSTR+" x"+HSTR+" x "+LSTRF+SQUOT+"-"+LSTRI+LQTR+DQUOTE
COMPONENT "LUMBER OVERALL",BDFT,"Bdft",1,"01","80000"
COMPONENT CUTSTR,QTY,unit[pack_type],1,CUTSTR,"80000"

return

1002:
!! Pack Type : INT

Database_set "AC_10_INT"

volume=B*H*L*QTY
cname=str(B*1000,1,0)+"/"+str(H*1000,1,0)+" - "+str(L*1000,3,0)

COMPONENT cname,L*1000,unit[pack_type],1,"001","80000"
COMPONENT "LUMBER OVERALL",volume,"m3",1,"001","80000"

return
