
if gs_detlevel_3D=`Откл.` then End

if shadow_cast=1 then
	shadow on
else
	shadow off
endif

if gs_gdl_debug then
	pen 10
	addz ac_ref_height
	roty 90
	arrow=sqr((ac_ref_coords[1][3]-ac_ref_coords[1][1])^2+(ac_ref_coords[1][4]-ac_ref_coords[1][2])^2)
	cylind	arrow-0.20, 0.02
	addz arrow-0.20
	cone 0.20, 0.05, 0, 90, 90
	del 3
endif

pen gs_cont_pen

! -----------------------------
! Do the covering with the cuts
! -----------------------------

diff_x=ac_whole_poly[1][7]-ac_whole_poly[1][1]
diff_y=ac_whole_poly[1][8]-ac_whole_poly[1][2]

!print "diff_x=",diff_x
!print "diff_y=",diff_y

if abs(diff_y)>eps And abs(diff_x)>eps then
	if ac_ref_coords[1][1]>ac_ref_coords[1][3] then extra=180
	alfa=atn(diff_y/diff_x)+extra
else
	if abs(diff_x)<eps then
		if diff_y>0 then
			alfa=90
		else
			alfa=-90
		endif
	endif
	if abs(diff_y)<eps then
		if diff_x>0 then
			alfa=0
		else
			alfa=180
		endif
	endif
endif

!print "alfa=",alfa

! ---------------
! Do the covering
! ---------------

!u=whole_loc_poly[1][1]
!v=whole_loc_poly[1][2]
a_size=whole_loc_poly[1][7]-whole_loc_poly[1][1]-2*oversize
b_size=whole_loc_poly[1][6]-whole_loc_poly[1][2]-2*oversize
ins_diff=(whole_loc_poly[1][2]+oversize)*tan(ac_pitch)

addz fudgez
for i=1 to vardim1(ac_coords)
	hotspot ac_coords[i][2],ac_coords[i][3],ac_coords[i][3]*tan(ac_pitch)
next i
del 1

group "geometry"
	put	whole_loc_poly[1][1], whole_loc_poly[1][2], 15
	put	whole_loc_poly[1][3], whole_loc_poly[1][4], 15
	put whole_loc_poly[1][5], whole_loc_poly[1][6], 15
	put whole_loc_poly[1][7], whole_loc_poly[1][8], 15
	put whole_loc_poly[1][1], whole_loc_poly[1][2], -1

	for i=1 to crd_begend[1][2]
		if i<>crd_begend[1][2] then
			put ac_coords[i][2], ac_coords[i][3], 15
		else
			put ac_coords[i][2], ac_coords[i][3], -1
		endif
	next i

	addz ac_ref_height+ac_thickness+fudgez+ins_diff-sgn(ac_pitch)*0.50 ! whole_loc_poly[1][1],whole_loc_poly[1][2],
	prism_	nsp/3, temp_zz+(whole_loc_poly[1][6]-whole_loc_poly[1][2]-2*oversize)*tan(ac_pitch)+sgn(ac_pitch)*1,
			get(nsp)

	if vardim1(crd_begend)>1 then
		for j=2 to vardim1(crd_begend)
			for i=crd_begend[j][1] to crd_begend[j][2]
				if i<>crd_begend[1][2] then
					put ac_coords[i][2], ac_coords[i][3], 15
				else
					put ac_coords[i][2], ac_coords[i][3], -1
				endif
			next i

			prism_	nsp/3, temp_zz+(whole_loc_poly[1][6]-whole_loc_poly[1][2]-2*oversize)*tan(ac_pitch)+sgn(ac_pitch)*1,
					get(nsp)

		next j
	endif

	del 1
endgroup

if gs_gdl_debug then
	model wire
	placegroup "geometry"
	model solid
endif

!!!!print " (a) a_size=",a_size," (b) b_size=",b_size," ac_pitch=",ac_pitch,"rooftype=",rooftype

group "covering"
	add whole_loc_poly[1][1]+oversize,whole_loc_poly[1][2]+oversize,ac_ref_height+ac_thickness+fudgez+ins_diff

	if rooftype=`Волнистый Лист` then
		call "Corrugated Sheet_macro" parameters a=a_size, b=b_size, wawe_h=wawe_h, wawe_l=wawe_l, ac_pitch=ac_pitch, sheet_mat=wawe_mat, res=seam_res
	endif

	if rooftype=`Профилированный Лист` then
		call "Trapezoid Sheet_macro" parameters a=a_size, b=b_size, wawe_h=wawe_h, wawe_l=wawe_l, ac_pitch=ac_pitch, sheet_mat=wawe_mat
	endif

	if rooftype=`Стоячий Фальц` then
		call "Standing Seam_macro" parameters a=a_size, b=b_size, ac_pitch=ac_pitch, seam_space=seam_space,
										seam_height=seam_height, seam_rib=seam_rib, sheet_mat=sheet_mat,
										seam_joint=seam_joint, joint_dist=joint_dist, gs_detlevel_3Dm=gs_detlevel_3Dm
	endif

	if rooftype=`Обычная Черепица` or rooftype=`Скругленная Черепица` or rooftype=`Итальянская Черепица` then
		call "Tile Covering" parameters a=a_size, b=b_size, tile_type=tile_type, tile_width=tile_width, tile_length=tile_length, random_mat=random_mat,
										tile_thk=tile_thk, tile_space=tile_space, res=tile_res, ac_pitch=ac_pitch, gs_detlevel_3Dm=gs_detlevel_3Dm,
										mat_1=tile_mat_1,mat_2=tile_mat_2,mat_3=tile_mat_3
	endif
	del 1
endgroup

!!!!if ac_accessory_debug then
!!!!	add whole_loc_poly[1][1]+oversize,whole_loc_poly[1][2]+oversize,ac_ref_height+ac_thickness+fudgez+ins_diff
!!!!	prism_	5,
!!!!endif

!!!!placegroup "covering"

finish=subgroup("covering","geometry")
placegroup finish
