
ui_dialog `Параметры Покрытия Кровли`,444,266

ui_page 1
page=1

! ------------------------------------------------------------------------------
! General Settings
! ------------------------------------------------------------------------------

row=25

col_1=95
col_2=80
col_3=60

pos_x=0
pos_y=5

ui_style 2,1
ui_outfield `Общие Параметры:`,pos_x,pos_y,col_1+col_2,19
ui_style 1,0

! Pictures
pos_y=pos_y+row
ui_style 0,0
ui_infield "rooftype",pos_x,pos_y-1,col_1+col_2,15
ui_style 1,0

pos_y=pos_y+row
ui_outfield `3D-детализация:`,pos_x,pos_y,col_1,15
ui_infield "gs_detlevel_3D",pos_x+col_1,pos_y-1,col_2,15

pos_y=pos_y+row
ui_outfield `2D-детализация`,pos_x,pos_y,col_1,15
ui_infield "gs_detlevel_2D",pos_x+col_1,pos_y-1,col_2,15

pos_y=pos_y+row
ui_outfield `Отбрасывание Теней:`,pos_x,pos_y,col_1,15
ui_infield "shadow_cast",pos_x+col_1,pos_y-1,col_2,15

pos_y=pos_y+row+10
ui_style 2,1
ui_outfield `2D-отображение`,pos_x,pos_y,col_1+col_2,19
ui_style 1,0

pos_y=pos_y+row
ui_outfield `Тип Штриховки:`,pos_x,pos_y,col_1,15
ui_infield "gs_fill_type",pos_x+col_1,pos_y-4,col_2,21

col_2=35
pos_y=pos_y+row
ui_outfield `Перо Контура:`,pos_x,pos_y,col_1,15
ui_infield "gs_cont_pen",pos_x+col_1,pos_y-4,col_2,21

pos_y=pos_y+row
ui_outfield `Перо Штриховки:`,pos_x,pos_y,col_1,15
ui_infield "gs_fill_pen",pos_x+col_1,pos_y-4,col_2,21

pos_y=pos_y+row
ui_outfield `Перо Фона:`,pos_x,pos_y,col_1,15
ui_infield "gs_back_pen",pos_x+col_1,pos_y-4,col_2,21

! ------------------------------------------------------------------------------
! Geometry
! ------------------------------------------------------------------------------

pos_x=190
pos_y=5

ui_style 2,1
ui_outfield `Геометрия:`,pos_x,pos_y,150,19
ui_style 1,0

col_1=50
col_2=50
col_3=80
col_5=80	! Infield Width(Pictures)
par_id=0

if rooftype=`Стоячий Фальц` then
	pos_y=pos_y+row : par_id=par_id+1 : seam_sp_id=par_id
	ui_infield "seam_space",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : seam_hgt_id=par_id
	ui_infield "seam_height",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : seam_dst_id=par_id
	ui_infield "seam_dist",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : seam_rib_id=par_id
	ui_infield "seam_rib",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : seam_joi_id=par_id
	ui_infield "seam_joint",pos_x,pos_y-1,17,15
	ui_pict "SheetJoint",pos_x+17,pos_y-1, 35, 15,1
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	if seam_joint then
		pos_y=pos_y+row : par_id=par_id+1 : joi_dst_id=par_id
		ui_infield "joint_dist",pos_x,pos_y-1,col_1,15
		ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15
	endif

	ifs_mat="sheet_mat"

	! --------------------------------------------------------------------------
	! Figure
	! --------------------------------------------------------------------------

	pos_y=35
	pos_x=444-180
	if seam_joint then
		ui_pict "SSSheetSectionJoint",444-180,pos_y, 180, 140, 1
	else
		ui_pict "SSSheetSection",444-180,pos_y, 180, 140, 1
	endif
	ui_pict "StandingSeam",pos_x+5,pos_y, 142, 15, 1

	pict_name="number_"+str(seam_dst_id,1,0)
	ui_pict pict_name,pos_x+130,pos_y+95, 7, 9, 1

	pict_name="number_"+str(joi_dst_id,1,0)
	ui_pict pict_name,pos_x+93,pos_y+68, 7, 9, 1

	sp_x=120/(seam_rib+1)
	rib_sp = 0
	pict_name="number_"+str(seam_rib_id,1,0)
	for i=1 to seam_rib
		rib_sp=rib_sp+sp_x
		ui_pict "SSRib",pos_x+11+rib_sp,pos_y+8, 8, 9, 1
		ui_pict pict_name,pos_x+11+rib_sp,pos_y-5, 7, 9, 1
	next i

	call "ui_arrow_02" parameters	dir=1, pixel=121, x=pos_x+15, y=pos_y+35, type=3,
								auto_type=1, num=seam_sp_id, num_pos=2, sec_pos=2

	call "ui_arrow_02" parameters	dir=0, pixel=15, x=pos_x+158, y=pos_y, type=3,
								auto_type=1, num=seam_hgt_id, num_pos=6, sec_pos=6
endif

if rooftype=`Волнистый Лист` or rooftype=`Профилированный Лист` then
	pos_y=pos_y+row : par_id=par_id+1 : wawe_h_id=par_id
	ui_infield "wawe_h",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : wawe_l_id=par_id
	ui_infield "wawe_l",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : wawe_dst_id=par_id
	ui_infield "wawe_dist",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	if rooftype=`Волнистый Лист` then
		pos_y=pos_y+row : par_id=par_id+1 : seam_res_id=par_id
		ui_infield "seam_res",pos_x,pos_y-1,col_1,15
		ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

		pos_y=pos_y+row
		ui_outfield "("+str(par_id,1,0)+") * "+`Аппроксимация`,pos_x,pos_y,col_1+col_2,15
	endif

	ifs_mat="wawe_mat"

	! --------------------------------------------------------------------------
	! Figure
	! --------------------------------------------------------------------------

	pos_y=35
	pos_x=444-180
	ui_pict "SSSheetSection",444-180,pos_y, 180, 140, 1
	if rooftype=`Волнистый Лист` then
		ui_pict "CorrugatedSheet",pos_x+5,pos_y, 108, 32, 1
	else
		ui_pict "TrapezoidSheet",pos_x+5,pos_y, 108, 32, 1
	endif

	pict_name="number_"+str(wawe_dst_id,1,0)
	ui_pict pict_name,pos_x+130,pos_y+95, 7, 9, 1

	pict_name="number_"+str(joi_dst_id,1,0)
	ui_pict pict_name,pos_x+93,pos_y+68, 7, 9, 1

	call "ui_arrow_02" parameters	dir=0, pixel=32, x=pos_x+135, y=pos_y, type=3,
								auto_type=1, num=wawe_h_id, num_pos=4, sec_pos=4

	call "ui_arrow_02" parameters	dir=1, pixel=60, x=pos_x+29, y=pos_y+50, type=3,
								auto_type=1, num=wawe_l_id, num_pos=2, sec_pos=2
endif

if rooftype=`Обычная Черепица` or rooftype=`Скругленная Черепица` or rooftype=`Итальянская Черепица` then
	pos_y=pos_y+row : par_id=par_id+1 : tile_w_id=par_id
	ui_infield "tile_width",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : tile_l_id=par_id
	ui_infield "tile_length",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : tile_thk_id=par_id
	ui_infield "tile_thk",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : tile_sp_id=par_id
	ui_infield "tile_space",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	pos_y=pos_y+row : par_id=par_id+1 : bat_hgt_id=par_id
	ui_infield "batten_height",pos_x,pos_y-1,col_1,15
	ui_outfield "("+str(par_id,1,0)+")",pos_x+col_1+5,pos_y,col_2,15

	if rooftype=`Скругленная Черепица` or rooftype=`Итальянская Черепица` then
		pos_y=pos_y+row : par_id=par_id+1 : tile_res_id=par_id
		ui_infield "tile_res",pos_x,pos_y-1,col_1,15
		ui_outfield "("+str(par_id,1,0)+") *",pos_x+col_1+5,pos_y,col_2,15

		pos_y=pos_y+row
		ui_outfield "("+str(par_id,1,0)+") * "+`Аппроксимация`,pos_x,pos_y,col_1+col_2,15
	endif

	ifs_mat="tile_mat_1"
	ifs_mat_2="tile_mat_2"
	ifs_mat_3="tile_mat_3"

	! --------------------------------------------------------------------------
	! Figure
	! --------------------------------------------------------------------------

	pos_y=30
	pos_x=444-180
	if rooftype=`Обычная Черепица` or rooftype=`Скругленная Черепица` then
		ui_pict "NRTileSection",pos_x,pos_y, 180, 140, 1
		if rooftype=`Обычная Черепица` then
			ui_pict "NormalTile",pos_x+5,pos_y, 58, 72, 1
		else
			ui_pict "RoundedTile",pos_x+5,pos_y, 58, 72, 1
			if GLOB_MODPAR_NAME="tile_res" then ui_pict "RoundedTileResol",pos_x+2,pos_y+35, 38, 38, 1
		endif

		pict_name="number_"+str(tile_sp_id,1,0)
		ui_pict pict_name,pos_x+132,pos_y+34, 7, 9, 1

		pict_name="number_"+str(bat_hgt_id,1,0)
		ui_pict pict_name,pos_x+110,pos_y+108, 7, 9, 1

		call "ui_arrow_02" parameters	dir=0, pixel=72, x=pos_x+80, y=pos_y, type=3,
									auto_type=1, num=tile_l_id, num_pos=6, sec_pos=6

		call "ui_arrow_02" parameters	dir=1, pixel=3, x=pos_x+59, y=pos_y+36, type=3,
									auto_type=1, num=tile_thk_id, num_pos=1, sec_pos=1

		call "ui_arrow_02" parameters	dir=1, pixel=32, x=pos_x+5, y=pos_y+90, type=3,
									auto_type=1, num=tile_w_id, num_pos=2, sec_pos=2
	else
		ui_pict "ITTileSection",pos_x,pos_y, 180, 140, 1
		ui_pict "ItalianTile",pos_x+5,pos_y, 91, 26, 1

		pict_name="number_"+str(tile_l_id,1,0)
		ui_pict pict_name,pos_x+113,pos_y+30, 7, 9, 1

		pict_name="number_"+str(bat_hgt_id,1,0)
		ui_pict pict_name,pos_x+139,pos_y+90, 7, 9, 1

		pict_name="number_"+str(tile_sp_id,1,0)
		ui_pict pict_name,pos_x+60,pos_y+61, 7, 9, 1

		call "ui_arrow_02" parameters	dir=1, pixel=53, x=pos_x+24, y=pos_y+50, type=3,
									auto_type=1, num=tile_w_id, num_pos=2, sec_pos=2

		call "ui_arrow_02" parameters	dir=0, pixel=3, x=pos_x+50, y=pos_y+22, type=3,
									auto_type=1, num=tile_thk_id, num_pos=2, sec_pos=2
	endif

endif

! Material Settings
pos_x=190
pos_y=215
if rooftype<>`Профилированный Лист` and rooftype<>`Волнистый Лист` and rooftype<>`Стоячий Фальц` then
	ui_style 2,1
	if random_mat then
		ui_outfield `Покрытия`,pos_x,pos_y,col_1+col_2,19
	else
		ui_outfield `Покрытие`,pos_x,pos_y,col_1+col_2,19
	endif
	ui_style 1,0

	ui_outfield `Перемешать Покрытия`,444-130,pos_y,120,15
	ui_infield "random_mat",444-17,pos_y,17,15

	pos_y=pos_y+row
	if random_mat then
		col_mat=(444-pos_x)/3
		ui_infield ifs_mat_2,pos_x+col_mat,pos_y-4,col_mat,21
		ui_infield ifs_mat_3,pos_x+2*col_mat,pos_y-4,col_mat,21
	else
		col_mat=(444-pos_x)
	endif
else
	ui_style 2,1
	ui_outfield `Покрытие`,pos_x,pos_y,col_1+col_2,19
	ui_style 1,0

	pos_y=pos_y+row
	col_mat=(444-pos_x)
endif

ui_infield ifs_mat,pos_x,pos_y-4,col_mat,21

file_dependence "number_1"
file_dependence "number_2"
file_dependence "number_3"
file_dependence "number_4"
file_dependence "number_5"
file_dependence "number_6"
file_dependence "number_7"
file_dependence "number_8"
file_dependence "number_9"
file_dependence "number_10"

