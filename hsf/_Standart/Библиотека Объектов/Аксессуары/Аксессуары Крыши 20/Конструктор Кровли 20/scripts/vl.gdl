
call "FM_types_Accessories" parameters

if GLOB_MODPAR_NAME="A" then
	parameters ac_rooflength=A/cos(ac_pitch)
	parameters zzyzx=A*tan(ac_pitch)
endif
if GLOB_MODPAR_NAME="ZZYZX" then
	parameters ac_rooflength=sqr(A^2+zzyzx^2)
	parameters ac_pitch=atn(zzyzx/A)
endif
if GLOB_MODPAR_NAME="ac_pitch" then
	parameters zzyzx=tan(ac_pitch)*A
	parameters ac_rooflength=A/cos(ac_pitch)
endif
if GLOB_MODPAR_NAME="ac_rooflength" then
	parameters A=ac_rooflength*cos(ac_pitch)
	parameters zzyzx=ac_rooflength*sin(ac_pitch)
endif


if not(ac_accessory_debug) then hideparameter "gs_accessory_addon"

values "gs_detlevel_2D" `Детальный`,`Простой`
values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`

values "rooftype" `Стоячий Фальц`,`Волнистый Лист`,`Профилированный Лист`,`Обычная Черепица`,`Скругленная Черепица`,`Итальянская Черепица`

if rooftype=`Стоячий Фальц` then
	hideparameter "gs_corrugated","gs_tile"
	if not(seam_joint) then hideparameter "joint_dist"
else
	if rooftype=`Волнистый Лист` or rooftype=`Профилированный Лист` then
		hideparameter "gs_standing_seam","gs_tile"
	else
		hideparameter "gs_standing_seam","gs_corrugated"
	endif
endif

if GLOB_MODPAR_NAME="rooftype" and (rooftype=`Обычная Черепица` or rooftype=`Скругленная Черепица`) then
	parameters tile_width=0.15, tile_thk=0.02, tile_length=0.40, tile_space=0.15
endif

if GLOB_MODPAR_NAME="rooftype" and rooftype=`Итальянская Черепица` then
	parameters tile_width=0.30, tile_thk=0.02, tile_length=0.40, tile_space=0.325
endif

if not(random_mat) then
	hideparameter "tile_mat_2","tile_mat_3"
endif

if GLOB_MODPAR_NAME="wawe_mat" then
	parameters sheet_mat=wawe_mat
endif
if GLOB_MODPAR_NAME="sheet_mat" then
	parameters wawe_mat=sheet_mat
endif


if GLOB_MODPAR_NAME="wawe_dist" then
	parameters seam_dist=wawe_dist
endif
if GLOB_MODPAR_NAME="seam_dist" then
	parameters wawe_dist=seam_dist
endif

