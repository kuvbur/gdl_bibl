
ui_dialog `Параметры RoofMaker`

! =============================================================================
! Tabpage System
! =============================================================================

ui_current_page gs_ui_current_page

TABID_ROOT				= -1

TABID_REPRESENTATION	= 3
TABID_SYMBOLTEXT		= 4
TABID_DESCRIPTIONS		= 5

! pages 1, 2 handled in parent script

ui_page TABID_REPRESENTATION, TABID_ROOT,				`Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
if gs_ui_current_page = TABID_REPRESENTATION then gosub "representationPage"

ui_page TABID_SYMBOLTEXT, TABID_REPRESENTATION,			`Показать Размер Пиломатериала`, "uiTab_Text_flat(1)"
if gs_ui_current_page = TABID_SYMBOLTEXT then gosub		"symbolTextPage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT,					`Описание`, "uiTab_Descriptions_flat(1)"
if gs_ui_current_page = TABID_DESCRIPTIONS then	gosub	"descriptionPage"


file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Text_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ======================================================================
end
! ======================================================================


"representationPage":

	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]
	iconAxisLineType = "ui_list_axisLineType_flat(1)"
	iconAxisLinePen = "ui_list_axisLinePen_flat(1)"


	_idx = 1
	paramNameReprArray 	[_idx] = "gs_2D_representation"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""

	if bEnableRafter then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "rm_bRafterVisible"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_axis2D_flat(1)"
	endif

	if (bEnableRafter & rm_bRafterVisible) | not(bEnableRafter) then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_symb_display_option"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = ""

		if bEnableSymbol & AC_symb_display_option = DISPLAY_SYMBOLIC then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "rm_symbolType"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_axis2D_2_flat(1)"

			iconAxisLineType = "ui_list_axisLineType_2_flat(1)"
			iconAxisLinePen = "ui_list_axisLinePen_2_flat(1)"
		endif
	
		if AC_symb_display_option <>  DISPLAY_SYMBOLIC & AC_symb_display_option <> DISPLAY_OUTLINES & AC_symb_display_option <> DISPLAY_OVERHEAD_ALL then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_symb_show_projection_to"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = ""
		endif

		if AC_symb_display_option <> DISPLAY_OVERHEAD_ALL then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_fill_type"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_fill_pen"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_back_pen"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"

			if AC_symb_display_option = DISPLAY_PROJECTED | AC_symb_display_option = DISPLAY_OVERHEAD_PROJ then
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "gs_cut_fill_type"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "gs_cut_fill_pen"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "gs_cut_back_pen"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
			endif
	
			if AC_symb_display_option = DISPLAY_PROJECTED | AC_symb_display_option = DISPLAY_OVERHEAD_PROJ then
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "AC_cut_linetype"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_contLineType_flat(1)"
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "gs_cut_pen"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
			endif
	
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_cont_line"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_contLineType_flat(1)"
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_cont_pen"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
		endif
	endif

	if	(bEnableSymbol & AC_symb_display_option = DISPLAY_SYMBOLIC & rm_iSymbolType = SYMBOLTYPE_AXES) |\
		(bEnableRafter & rm_bRafterVisible & AC_symb_display_option <> DISPLAY_OUTLINES) |\
		(bEnableRafter & not(rm_bRafterVisible)) \
	then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "rm_AxisLineType"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = iconAxisLineType
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_axis_line_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = iconAxisLinePen
	endif

	if	((bEnableRafter & rm_bRafterVisible) | not(bEnableRafter)) &\
		(AC_symb_display_option = DISPLAY_OVERHEAD_PROJ | AC_symb_display_option = DISPLAY_OVERHEAD_ALL) \
	then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "AC_overhead_linetype"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contLineType_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "AC_overhead_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
	endif

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_3D_representation"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""

	if bEnableResolution then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_res"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
	endif

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "pen3D"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_3DPen_flat(1)"

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_material"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "rm_mat"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID  	= TABID_REPRESENTATION,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray
	file_dependence "ui_list_surface_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_axis2D_flat(1)"
	file_dependence "ui_list_axisLineType_flat(1)"
	file_dependence "ui_list_axisLinePen_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_3DPen_flat(1)"
	file_dependence "ui_list_axis2D_2_flat(1)"
	file_dependence "ui_list_axisLineType_2_flat(1)"
	file_dependence "ui_list_axisLinePen_2_flat(1)"

return


"symbolTextPage":

	!column positions according to ui_object_SymbolText UI script
	x1	= 0
	x2	= 168
	x3	= 170
	x4	= 370
	x5	= 266
	x6	= 273
	xEnd = 444

	_dy = 24
	_yCurr = 5
	_infHeight 	= 19
	_outfHeight = 15
	_offsetY	= 4

	ui_style 0, 1
	ui_infield{3} txt_show,					x1,	_yCurr - _offsetY,	xEnd - x1,	_infHeight,
		7, "",
		0, 0, 0, 0, 0, 0,
		"", `Показ Размера Пиломатериала в Символе`, 	0,
		"", `Показ Размера Пиломатериала в Символе`, 	1
	ui_style 0, 0
	_yCurr = _yCurr + _dy

	ui_outfield `Размер для Показа`, 		x1,	_yCurr, x2 - x1, _outfHeight, 4 * not(txt_show)
	ui_infield{3} "iDisplaySize",			x3,	_yCurr - _offsetY,	x4 - x3,	_infHeight,
		8, "",
		0, 0, 0, 0, 0, 0,
		0, stDisplaySize[1],	DISPLAY_CUT_SIZE,
		0, stDisplaySize[2],	DISPLAY_ROUNDED_SIZE
	_yCurr = _yCurr + _dy

	ui_outfield `Величина Округления`, 			x1,	_yCurr, x2 - x1, _outfHeight, 4 * not(txt_show)
	dim picIdArray[]
	dim idxArray[]

	for _idx = 1 to not(bIsMetricLength) * vardim1(stRoundValuesUSA) + bIsMetricLength * vardim1(stRoundValuesMetric)
		idxArray[_idx]		= _idx - 1
		picIdArray[_idx]	= 0
	next _idx
	if bIsMetricLength then
		ui_infield{3} "iRoundingValue",		x3,	_yCurr - _offsetY,	x5 - x3,	_infHeight,
			8, "", 0, 0, 0, 0, 0, 0,
			picIdArray, stRoundValuesMetric, idxArray
	else
		ui_infield{3} "iRoundingValueUSA",	x3,	_yCurr - _offsetY,	x5 - x3,	_infHeight,
			8, "", 0, 0, 0, 0, 0, 0,
			picIdArray, stRoundValuesUSA, idxArray
	endif
	if	(    bIsMetricLength  & (iRoundingValue    = (vardim1(stRoundValuesMetric) - 1))) |\
		(not(bIsMetricLength) & (iRoundingValueUSA = (vardim1(stRoundValuesUSA) - 1)) 	) \
	then
		ui_infield "roundValue",	x5 + 3,	_yCurr - _offsetY,	x4 - x5 - 3,	_infHeight
	endif
	_yCurr = _yCurr + _dy

	ui_separator x1, _yCurr, xEnd, _yCurr
	_yCurr = _yCurr + _outfHeight


	call "ui_object_SymbolText" parameters all	pageStartY				= _yCurr,
												dt						= txt_show,		!read-only, for setting hidden flag
												bParName_FontSize		= 0,
												bTextPos				= 1,
												bRoofMaker				= 1
return

"descriptionPage":

	dim stParamNames[]								: _idx = 1

	stParamNames[_idx] = "lumberSize"				: _idx = _idx + 1
	stParamNames[_idx] = "roundedLumberSize"		: _idx = _idx + 1
	if gs_iDisplayStandards = LANGUAGE_USA then
		stParamNames[_idx] = "numBDFT"				: _idx = _idx + 1
	endif
	stParamNames[_idx] = "gs_list_cost"				: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_manufacturer"		: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_note"				: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_location"			: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_accessories"		: _idx = _idx + 1
	stParamNames[_idx] = "FM_Type"					: _idx = _idx + 1
	stParamNames[_idx] = "FM_InventoryNumber"		: _idx = _idx + 1
	stParamNames[_idx] = "FM_SerialNumber"			: _idx = _idx + 1
	stParamNames[_idx] = "FM_ProductionYear"		: _idx = _idx + 1
	stParamNames[_idx] = "FM_ObjectWeight"			: _idx = _idx + 1
	stParamNames[_idx] = "FM_ObjectWeightUnit"		: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_custom1"			: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_custom2"			: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_custom3"			: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_custom4"			: _idx = _idx + 1
	stParamNames[_idx] = "gs_list_custom5"			: _idx = _idx + 1

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
