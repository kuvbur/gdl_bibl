
pen gs_cont_pen
line_type gs_cont_line
fill gs_fill_type


! ==============================================================================
! Roofmaker Macro
! ==============================================================================

call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										txt_BasePointX			= -A/2,
										txt_BasePointY			= -rm_ProfileWidth/2,
										txt_BaseAngle			= 0,
				returned_parameters 	gs_cutplane_height,
										gs_top_range_height,
										gs_bottom_range_height,
										doCutplane


! ==============================================================================
! Listing
! ==============================================================================
if GLOB_CONTEXT = 6 then
	project2{2} 4, 270, 3+32,
		gs_back_pen,0,0,0

	end
endif


! ==============================================================================
! 2D Hotspots
! ==============================================================================
unID = 1
hotspot2 0,			0,					unID : unID=unID+1
hotspot2 -_swoopX,	0,					unID : unID=unID+1
hotspot2 -_swoopX, -rm_ProfileWidth, 	unID : unID=unID+1

! --- Centre ------------------------------------------------------------------
	hotspot2 -A/2, -rm_ProfileWidth/2, 100

! --- Profile Width ------------------------------------------------------------
	unID = 1000
	hotspot2 0, 0,				unID, rm_ProfileWidth, 1+256	: unID=unID+1
	hotspot2 0, 1,				unID, rm_ProfileWidth, 3		: unID=unID+1
	hotspot2 0,-rm_ProfileWidth,unID, rm_ProfileWidth, 2		: unID=unID+1

	unID = 1100
	hotspot2 -A,  0,				unID, rm_ProfileWidth, 1+256	: unID=unID+1
	hotspot2 -A,  1,				unID, rm_ProfileWidth, 3		: unID=unID+1
	hotspot2 -A, -rm_ProfileWidth,	unID, rm_ProfileWidth, 2		: unID=unID+1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	hotspot2  0, -rm_ProfileWidth/2, unID, A, 1+256	: unID=unID+1	!!! AXIS (Length)
	hotspot2  1, -rm_ProfileWidth/2, unID, A, 3		: unID=unID+1
	hotspot2 -A, -rm_ProfileWidth/2, unID, A, 2		: unID=unID+1


! ==============================================================================
! Floor Plan View
! ==============================================================================

if AC_symb_display_option = DISPLAY_SYMBOLIC then

	! ----- Symbolic 2D --------------------------------------------------------

	rect2 0, 0, -A, -rm_ProfileWidth
	line2 -_swoopX, 0, -_swoopX, -rm_ProfileWidth

	fill gs_fill_type
	poly2_b{2}	5, 2+64,gs_fill_pen, gs_back_pen,
				0,0,0,
				0,0,1,
				0,-rm_ProfileWidth,1,
				-A,-rm_ProfileWidth,1,
				-A,0,1,
				0,0,1
else

	! ----- View Edges ---------------------------------------------------------
	pen gs_cont_pen
	line_type gs_cont_line

	if AC_symb_display_option = DISPLAY_OVERHEAD_ALL then
		pen AC_overhead_pen
		line_type AC_overhead_linetype
	endif

	project2{3} 3,270,2 + 32 * (AC_symb_display_option = DISPLAY_OVERHEAD_ALL | not(gs_use_3D_view_attributes)), 8,
		parameters	gs_2D_projection		= doCutplane,
					gs_2D_projection_type	= 1,
					gs_cutplane_height		= gs_cutplane_height,
					gs_top_range_height		= gs_top_range_height,
					gs_bottom_range_height	= gs_bottom_range_height


	if AC_symb_display_option <> DISPLAY_OVERHEAD_ALL then
		! ----- View Polygons --------------------------------------------------
		pen gs_fill_pen
		fill gs_fill_type

		project2{3} 3,270,3+32+1024+2048, 4,
						gs_back_pen,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 2,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif


	if AC_symb_display_option = DISPLAY_OVERHEAD_PROJ then

		! ----- Overhead View -----------------------------------------------------
		pen AC_overhead_pen
		line_type AC_overhead_linetype

		project2{3} 3,270,2+32, 2+8,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 3,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif

	if doCutplane then

		! ----- Cut Lines ---------------------------------------------------------
		pen gs_cut_pen
		line_type AC_cut_linetype
		line_property 2

		project2{3} 3,270,2+16+32+256+4096, 2,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 4,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height


		! ----- Cut Polygons ------------------------------------------------------
		pen gs_cut_fill_pen
		fill gs_cut_fill_type

		project2{3} 3,270,3+32+512, 1,
						gs_cut_back_pen,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 5,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif
endif


