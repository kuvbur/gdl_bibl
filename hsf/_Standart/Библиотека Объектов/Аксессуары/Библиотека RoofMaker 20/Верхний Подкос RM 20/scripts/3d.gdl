
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================

! --- Profile Width ------------------------------------------------------------
	unID = 1000
	!hotspot 0,  0,						0,	unID, rm_ProfileWidth, 1+256		: unID=unID+1
	!hotspot 0,  1,						0,	unID, rm_ProfileWidth, 3			: unID=unID+1
	!hotspot 0, -rm_ProfileWidth,		0,	unID, rm_ProfileWidth, 2			: unID=unID+1

	hotspot -_swoopX,  0,				0,	unID, rm_ProfileWidth, 1+256		: unID=unID+1
	hotspot -_swoopX,  1,				0,	unID, rm_ProfileWidth, 3			: unID=unID+1
	hotspot -_swoopX, -rm_ProfileWidth,	0,	unID, rm_ProfileWidth, 2			: unID=unID+1

	unID = 1100
	!hotspot -A,  0,					-_height, unID, rm_ProfileWidth, 1+256 	: unID=unID+1
	!hotspot -A,  1,					-_height, unID, rm_ProfileWidth, 3		: unID=unID+1
	!hotspot -A, -rm_ProfileWidth,	-_height, unID, rm_ProfileWidth, 2		: unID=unID+1

	hotspot -A,  0,					-_height+_swoopY, unID, rm_ProfileWidth, 1+256	: unID=unID+1
	hotspot -A,  1,					-_height+_swoopY, unID, rm_ProfileWidth, 3		: unID=unID+1
	hotspot -A, -rm_ProfileWidth,	-_height+_swoopY, unID, rm_ProfileWidth, 2		: unID=unID+1

! --- Profile Height ------------------------------------------------------------
	unID = 2000
	add -A/2, 0, -_height/2
	roty -rm_Angle
		hotspot 0, 0,  0,				unID, rm_ProfileHeight, 1+128	: unID=unID+1
		hotspot 0, 0, -1,				unID, rm_ProfileHeight, 3		: unID=unID+1
		hotspot 0, 0, rm_ProfileHeight,	unID, rm_ProfileHeight, 2		: unID=unID+1
	del 2

addy -rm_ProfileWidth/2
	! --- Horizontal Length ------------------------------------------------------------
		unID = 3000
		hotspot  0,	0, 0,	unID, A, 1+256	: unID=unID+1
		hotspot  1,	0, 0,	unID, A, 3		: unID=unID+1
		hotspot -A,	0, 0,	unID, A, 2		: unID=unID+1

	! --- Structural Length ------------------------------------------------------------
		unID = 3500
		addx -_swoopX/2
		roty -rm_Angle
			hotspot  0,					0, 0, unID, structuralLength, 1+256, rm_StructuralLength	: unID=unID+1
			hotspot  1,					0, 0, unID, structuralLength, 3, rm_StructuralLength		: unID=unID+1
			hotspot -structuralLength,	0, 0, unID, structuralLength, 2, rm_StructuralLength		: unID=unID+1
		del 2

	! --- Height ------------------------------------------------------------
		unID = 4000
		hotspot -A,	0,  0,		unID, ZZYZX, 1+128	: unID=unID+1
		hotspot -A,	0,  1,		unID, ZZYZX, 3		: unID=unID+1
		hotspot -A,	0, -ZZYZX,	unID, ZZYZX, 2		: unID=unID+1

	! --- Pitch ------------------------------------------------------------
		unID = 5000
		hotspot  0,										-rm_ProfileWidth/2,	0,										unID,rm_Angle,6	: unID=unID+1
		hotspot -1,										-rm_ProfileWidth/2,	0,										unID,rm_Angle,4+128	: unID=unID+1
		hotspot  0,										-1,					-rm_ProfileWidth/2,						unID,rm_Angle,7	: unID=unID+1
		hotspot -rm_StructuralLength/2*cos(rm_Angle),	-rm_ProfileWidth/2,	-rm_StructuralLength/2*sin(rm_Angle),	unID,rm_Angle,5	: unID=unID+1
del 1


! ==============================================================================
! Roofmaker Element
! ==============================================================================
rotx 90
add -A, -_height, 0

base
	prism 5, rm_ProfileWidth,
			0,			0,
			A,			_height,
			A-_swoopX,	_height,
			0,			_swoopY,
			0,			0

	vert 0,0,0
	vert A,_height,0
	vert -_height,A,0
	vert 0,0,-1

	coor 2,-1,-2,-3,-4
body 1

del 2


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i



