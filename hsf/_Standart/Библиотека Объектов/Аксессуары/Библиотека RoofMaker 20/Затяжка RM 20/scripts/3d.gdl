
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================

! --- Profile Width ------------------------------------------------------------
	unID = 1000
	hotspot _startX1,  0,					rm_RelativeHeight, unID, profileWidthHalf, 1+128,	rm_ProfileWidth		: unID=unID+1
	hotspot _startX1, -profileWidthHalf,	rm_RelativeHeight, unID, profileWidthHalf, 2,		rm_ProfileWidth		: unID=unID+1
	hotspot _startX1,  1,					rm_RelativeHeight, unID, profileWidthHalf, 3,		rm_ProfileWidth		: unID=unID+1

	hotspot _startX1,  0,					rm_RelativeHeight, unID, profileWidthHalf, 1+128,	rm_ProfileWidth 	: unID=unID+1
	hotspot _startX1,  profileWidthHalf,	rm_RelativeHeight, unID, profileWidthHalf, 2,		rm_ProfileWidth		: unID=unID+1
	hotspot _startX1, -1,					rm_RelativeHeight, unID, profileWidthHalf, 3,		rm_ProfileWidth		: unID=unID+1

	unID = 1100
	hotspot rm_ProjectedLength-_startX2,  0,				rm_RelativeHeight, unID, profileWidthHalf, 1+128,	rm_ProfileWidth		: unID=unID+1
	hotspot rm_ProjectedLength-_startX2, -profileWidthHalf,	rm_RelativeHeight, unID, profileWidthHalf, 2,		rm_ProfileWidth		: unID=unID+1
	hotspot rm_ProjectedLength-_startX2,  1,				rm_RelativeHeight, unID, profileWidthHalf, 3,		rm_ProfileWidth		: unID=unID+1

	hotspot rm_ProjectedLength-_startX2,  0,				rm_RelativeHeight, unID, profileWidthHalf, 1+128,	rm_ProfileWidth 		: unID=unID+1
	hotspot rm_ProjectedLength-_startX2,  profileWidthHalf,	rm_RelativeHeight, unID, profileWidthHalf, 2,		rm_ProfileWidth		: unID=unID+1
	hotspot rm_ProjectedLength-_startX2, -1,				rm_RelativeHeight, unID, profileWidthHalf, 3,		rm_ProfileWidth		: unID=unID+1

! --- Profile Height ------------------------------------------------------------
	unID = 2000
	add (_startX1+rm_ProjectedLength-_startX2)/2, 0, rm_RelativeHeight
	hotspot 0, 0,  0,				unID,rm_ProfileHeight,1+128	: unID=unID+1
	hotspot 0, 0, -1,				unID,rm_ProfileHeight,3		: unID=unID+1
	hotspot 0, 0,  rm_ProfileHeight,unID,rm_ProfileHeight,2		: unID=unID+1
	del 1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	hotspot  0,						0,	0,	unID,	rm_ProjectedLength,	1+256	: unID=unID+1
	hotspot -1,						0,	0,	unID,	rm_ProjectedLength,	3		: unID=unID+1
	hotspot  rm_ProjectedLength,	0,	0,	unID,	rm_ProjectedLength,	2		: unID=unID+1

! --- Distance from Pivot Plane ------------------------------------------------------------
	unID = 4500
	addx (_startX1+rm_ProjectedLength-_startX2)/2
	hotspot 0, 0,  0,					unID,	rm_RelativeHeight,	1+128	: unID=unID+1
	hotspot 0, 0, -1,					unID,	rm_RelativeHeight,	3		: unID=unID+1
	hotspot 0, 0,  rm_RelativeHeight,	unID,	rm_RelativeHeight,	2		: unID=unID+1
	del 1

! --- 1. Roof Pitch ------------------------------------------------------------
	unID = 5000
	_tempL1=(rm_ProfileHeight+rm_RelativeHeight)/sin(rm_PitchAngle_1)
	hotspot 0,								0, 0,								unID, rm_PitchAngle_1, 6		: unID=unID+1
	hotspot _tempL1,						0, 0,								unID, rm_PitchAngle_1, 4+128	: unID=unID+1
	hotspot _tempL1*cos(rm_PitchAngle_1),	0, _tempL1*sin(rm_PitchAngle_1),	unID, rm_PitchAngle_1, 5		: unID=unID+1

! --- 2. Roof Pitch ------------------------------------------------------------
	unID = 5500
	_tempL2=(rm_ProfileHeight+rm_RelativeHeight)/sin(rm_PitchAngle_2)
	addx rm_ProjectedLength
	hotspot  0,								0, 0,								unID, rm_PitchAngle_2, 6		: unID=unID+1
	hotspot -_tempL2,						0, 0,								unID, rm_PitchAngle_2, 4+128	: unID=unID+1
	hotspot -_tempL2*cos(rm_PitchAngle_2),	0, _tempL2*sin(rm_PitchAngle_2),	unID, rm_PitchAngle_2, 5		: unID=unID+1
	del 1


! ==============================================================================
! Roofmaker Element
! ==============================================================================
rotx 90
addz -rm_ProfileWidth/2
	base
	prism 5, rm_ProfileWidth,
			_startX1, rm_RelativeHeight,
			rm_ProjectedLength-_startX2, rm_RelativeHeight,
			rm_ProjectedLength-_startX2-_swoopX2, rm_RelativeHeight+rm_ProfileHeight,
			_startX1+_swoopX1, rm_RelativeHeight+rm_ProfileHeight,
			_startX1, rm_RelativeHeight

	vert 0,0,0
	vert 1,0,0
	vert 0,1,0
	vert 0,0,1

	coor 2,-1,-2,-3,-4
	body 1
del 2


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i


