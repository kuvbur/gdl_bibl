
! =============================================================================
! Localization
! =============================================================================
dim stGeometry[8]
	stGeometry[1]	= `Расстояние от Опорной Плоскости`
	stGeometry[2]	= `Угол Наклона 1`
	stGeometry[3]	= `Угол Наклона 2`
	stGeometry[4]	= `Ширина Профиля`
	stGeometry[5]	= `Высота Профиля`
	stGeometry[6]	= `Длина Элемента`
	stGeometry[7]	= `Конструктивная Длина Пролета`
	stGeometry[8]	= `Опорная Плоск.`



! =============================================================================
! Tabpages
! =============================================================================

TABID_ROOT					= -1
TABID_GEOMETRY				= 1

call "RoofMaker_macro"	parameters all	iObjectType				= 13,
										bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards

ui_page TABID_GEOMETRY, TABID_ROOT, `Геометрия`, "uiTab_Dimension_flat(1)"
if gs_ui_current_page = TABID_GEOMETRY then
	gosub "geometryPage"
endif

! ======================================================================
end
! ======================================================================

"geometryPage":
! =============================================================================
! Geometry
! =============================================================================

	pageStartY		= 5
	picX			= 29
	picY			= pageStartY + 30
	xInfieldSmall	= 70
	xInfieldBig		= 90
	yInfield		= 19

	ui_pict 2,	picX+288, picY-25, 32,  42, 1					!tiebeam_pict1.png
	ui_pict 3,	picX+55, picY-2, 225, 93, 1						!tiebeam_pict2.png
	ui_pict 4,	picX-1, picY+115, 323, 47, 1					!tiebeam_pict3.png
	ui_pict 5,	picX+31, picY+91, 36,  24, 1					!tiebeam_pict4.png
	ui_pict 6,	picX+228, picY+91, 35,  24, 1					!tiebeam_pict5.png
	ui_pict 7,	picX+280, picY+69, 27, 46, 1					!tiebeam_pict6.png


	ui_infield "rm_RelativeHeight", picX+323, picY+100, xInfieldBig, yInfield	ui_tooltip stGeometry[1]
	ui_pict 1, picX-28, picY+168, 24, 17, 1										!ui_rm_angle_1.png
	ui_infield "rm_PitchAngle_1", picX-1, picY+165, xInfieldBig, yInfield		ui_tooltip stGeometry[2]
	ui_pict 1, picX+240, picY+168, 24, 17, 1									!ui_rm_angle_1.png
	ui_infield "rm_PitchAngle_2", picX+267, picY+165, xInfieldBig, yInfield		ui_tooltip stGeometry[3]
	ui_infield "rm_ProfileWidth", picX+321, picY+4, xInfieldBig, yInfield		ui_tooltip stGeometry[4]
	ui_infield "rm_ProfileHeight", picX+321, picY-21, xInfieldBig, yInfield		ui_tooltip stGeometry[5]
	ui_infield "rm_StructuralLength", picX+103, picY+94, xInfieldBig, yInfield	ui_tooltip stGeometry[6]
	ui_infield "rm_ProjectedLength", picX+117, picY+165, xInfieldBig, yInfield	ui_tooltip stGeometry[7]

	ui_outfield stGeometry[8], picX+326, picY+131, 444-(picX+326), yInfield

return
