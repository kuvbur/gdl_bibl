
! =================================================================================
! Tie Beam parameters set by the RoofMaker add-on:
! "rm_ProjectedLength"		Structural Length of Span
! "rm_RelativeHeight"		Distance from Pivot Plane
! "rm_ProfileWidth"			Profile Width
! "rm_ProfileHeight"		Profile Height
! "rm_PitchAngle_1"			Pitch Angle 1
! "rm_PitchAngle_2"			Pitch Angle 2
! =================================================================================

values "rm_PitchAngle_1"	range (0, 90)
values "rm_PitchAngle_2"	range (0, 90)
values "rm_ProfileWidth"	range (0,)
values "profileWidthHalf"	range (0,)
values "rm_ProfileHeight"	range (0,)

if GLOB_MODPAR_NAME = "profileWidthHalf" then
	rm_ProfileWidth = profileWidthHalf*2
	parameters rm_ProfileWidth = rm_ProfileWidth
else
	profileWidthHalf = rm_ProfileWidth/2
	parameters profileWidthHalf = profileWidthHalf
endif

!if rm_ProfileHeight < EPS then
!	rm_ProfileHeight = 0.001
!	parameters rm_ProfileHeight = rm_ProfileHeight
!endif

if GLOB_MODPAR_NAME = "rm_ProfileHeight" then
	if _sectY-rm_ProfileHeight < rm_RelativeHeight then
		rm_ProfileHeight = _sectY - rm_RelativeHeight
		parameters rm_ProfileHeight = rm_ProfileHeight
	endif
endif

values "rm_RelativeHeight"		range [0, _sectY-rm_ProfileHeight]
values "rm_StructuralLength"	range [_swoopX1 + _swoopX2,]
values "rm_ProjectedLength"		range [_swoopX1 + _swoopX2 + _startX1 + _startX2,]
if GLOB_MODPAR_NAME = "rm_StructuralLength" then
	rm_ProjectedLength = rm_StructuralLength + _startX1 + _startX2
	parameters rm_ProjectedLength = rm_ProjectedLength
else
	rm_StructuralLength = rm_ProjectedLength - _startX1 - _startX2
	parameters rm_StructuralLength = rm_StructuralLength
endif


! ==============================================================================
! Lumber Size
! ==============================================================================
lumberSize = rm_StructuralLength
parameters lumberSize = lumberSize

! ==============================================================================
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iObjectType				= 13


! ==============================================================================
! Bounding Box
! ==============================================================================
parameters A		= rm_StructuralLength	: lock "A"
parameters B		= rm_ProfileWidth		: lock "B"
parameters ZZYZX	= rm_ProfileHeight		: lock "ZZYZX"


! ==============================================================================
! Levels
! ==============================================================================
parameters ac_toplevel 		= rm_RelativeHeight + rm_ProfileHeight
parameters ac_bottomlevel	= rm_RelativeHeight


