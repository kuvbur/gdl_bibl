
pen gs_cont_pen
line_type gs_cont_line
fill gs_fill_type
hLineID = 2500

! ==============================================================================
! Roofmaker Macro
! ==============================================================================

if SYMB_MIRRORED then rot2 180

call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										txt_BasePointX			= 0,
										txt_BasePointY			= 0,
										txt_BaseAngle			= 90,
				returned_parameters 	gs_cutplane_height,
										gs_top_range_height,
										gs_bottom_range_height,
										doCutplane

if SYMB_MIRRORED then del 1

! ==============================================================================
! Listing
! ==============================================================================
if GLOB_CONTEXT = 6 then
	project2{2} 4, 0, 3+32,
		gs_back_pen,0,0,0

	end
endif


! ==============================================================================
! 2D Hotspots
! ==============================================================================
! --- Centre ------------------------------------------------------------------
	hotspot2 0, 0, 100

! --- Profile Width ------------------------------------------------------------
	if rm_bRafterVisible then
		unID = 1000
		hotspot2  0,					_horizontalLength/2, unID,profileWidthHalf, 1+128,	rm_ProfileWidth		: unID=unID+1
		hotspot2  profileWidthHalf,		_horizontalLength/2, unID,profileWidthHalf, 2,		rm_ProfileWidth		: unID=unID+1
		hotspot2 -profileWidthHalf-1,	_horizontalLength/2, unID,profileWidthHalf, 3,		rm_ProfileWidth		: unID=unID+1

		hotspot2  0,					_horizontalLength/2, unID, profileWidthHalf, 1+128, rm_ProfileWidth 	: unID=unID+1
		hotspot2 -profileWidthHalf,		_horizontalLength/2, unID, profileWidthHalf, 2,		rm_ProfileWidth 	: unID=unID+1
		hotspot2  profileWidthHalf+1,	_horizontalLength/2, unID, profileWidthHalf, 3,		rm_ProfileWidth 	: unID=unID+1

		unID = 1100
		hotspot2  0,					-_horizontalLength/2, unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
		hotspot2  profileWidthHalf,		-_horizontalLength/2, unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1
		hotspot2 -profileWidthHalf-1,	-_horizontalLength/2, unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1

		hotspot2  0,					-_horizontalLength/2, unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
		hotspot2 -profileWidthHalf,		-_horizontalLength/2, unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1
		hotspot2  profileWidthHalf+1,	-_horizontalLength/2, unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1
	endif

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	rot2 90
		hotspot2  0,					0, unID, projectedLengthHalf, 1+128,	rm_ProjectedLength	: unID=unID+1
		hotspot2 -1,					0, unID, projectedLengthHalf, 3,		rm_ProjectedLength	: unID=unID+1
		hotspot2  projectedLengthHalf,	0, unID, projectedLengthHalf, 2,		rm_ProjectedLength	: unID=unID+1
	del 1

	rot2 -90
		hotspot2  0,					0, unID, projectedLengthHalf, 1+128,	rm_ProjectedLength	: unID=unID+1
		hotspot2 -1,					0, unID, projectedLengthHalf, 3,		rm_ProjectedLength	: unID=unID+1
		hotspot2  projectedLengthHalf,	0, unID, projectedLengthHalf, 2,		rm_ProjectedLength	: unID=unID+1
	del 1


! --- Overhang ------------------------------------------------------------
	unID = 6000
	rot2 90
	add2 rm_ProjectedLength/2,0
		hotspot2  0,			0, unID, rm_OverHang, 1+128 : unID=unID+1
		hotspot2 -1,			0, unID, rm_OverHang, 3		: unID=unID+1
		hotspot2  rm_OverHang, 	0, unID, rm_OverHang, 2		: unID=unID+1
	del 2

	unID = 6500
	rot2 -90
	add2 rm_ProjectedLength/2,0
		hotspot2  0,			0, unID, rm_OverHang, 1+128 : unID=unID+1
		hotspot2 -1,			0, unID, rm_OverHang, 3		: unID=unID+1
		hotspot2  rm_OverHang, 	0, unID, rm_OverHang, 2		: unID=unID+1
	del 2



! ==============================================================================
! Axis
! ==============================================================================
if not(rm_bRafterVisible) | AC_symb_display_option <> DISPLAY_OUTLINES then
	line_type rm_AxisLineType
	pen gs_axis_line_pen

	line2 0 , -rm_ProjectedLength/2-rm_OverHang,  0 , rm_ProjectedLength/2+rm_OverHang

	hotline2 0 , -rm_ProjectedLength/2,  0 , rm_ProjectedLength/2, hLineID : hLineID = hLineID + 1		!! Span
	hotline2 0 , -rm_ProjectedLength/2,  0 , -rm_ProjectedLength/2-rm_OverHang, hLineID : hLineID = hLineID + 1	!! Overhang
	hotline2 0 , rm_ProjectedLength/2,  0 , rm_ProjectedLength/2+rm_OverHang, hLineID : hLineID = hLineID + 1	!! Overhang

	if not(rm_bRafterVisible) then end
else
	hLineID = hLineID + 3
endif


! ==============================================================================
! Floor Plan View
! ==============================================================================
pen gs_cont_pen
line_type gs_cont_line

if AC_symb_display_option = DISPLAY_SYMBOLIC then
	if rm_bRafterVisible then
		rect2 -rm_ProfileWidth/2, -_horizontalLength/2, rm_ProfileWidth/2, _horizontalLength/2

		fill gs_fill_type
			poly2_b{2}	5, 2+64,gs_fill_pen, gs_back_pen,
					0,0,0,
					-rm_ProfileWidth/2, -_horizontalLength/2,1,
					 rm_ProfileWidth/2, -_horizontalLength/2,1,
					 rm_ProfileWidth/2, _horizontalLength/2,1,
					-rm_ProfileWidth/2, _horizontalLength/2,1,
					-rm_ProfileWidth/2, -_horizontalLength/2,1
	endif

else

	! ----- View Edges ---------------------------------------------------------
	pen gs_cont_pen
	line_type gs_cont_line

	if AC_symb_display_option = DISPLAY_OVERHEAD_ALL then
		pen AC_overhead_pen
		line_type AC_overhead_linetype
	endif

	project2{3} 3,270,2 + 32 * (AC_symb_display_option = DISPLAY_OVERHEAD_ALL | not(gs_use_3D_view_attributes)), 8,
		parameters	gs_2D_projection		= doCutplane,
					gs_2D_projection_type	= 1,
					gs_cutplane_height		= gs_cutplane_height,
					gs_top_range_height		= gs_top_range_height,
					gs_bottom_range_height	= gs_bottom_range_height


	if AC_symb_display_option <> DISPLAY_OVERHEAD_ALL then
		! ----- View Polygons --------------------------------------------------
		pen gs_fill_pen
		fill gs_fill_type

		project2{3} 3,270,3+32+1024+2048, 4,
						gs_back_pen,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 2,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif


	if AC_symb_display_option = DISPLAY_OVERHEAD_PROJ then

		! ----- Overhead View -----------------------------------------------------
		pen AC_overhead_pen
		line_type AC_overhead_linetype

		project2{3} 3,270,2+32, 2+8,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 3,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif

	if doCutplane then

		! ----- Cut Lines ---------------------------------------------------------
		pen gs_cut_pen
		line_type AC_cut_linetype
		line_property 2

		project2{3} 3,270,2+16+32+256+4096, 2,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 4,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height


		! ----- Cut Polygons ------------------------------------------------------
		pen gs_cut_fill_pen
		fill gs_cut_fill_type

		project2{3} 3,270,3+32+512, 1,
						gs_cut_back_pen,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 5,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif
endif
