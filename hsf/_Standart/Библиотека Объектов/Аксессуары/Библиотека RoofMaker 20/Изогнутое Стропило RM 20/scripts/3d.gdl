
pen pen3D
material rm_mat
resol gs_res


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================
! --- Overhang ------------------------------------------------------------
	unID = 6000
	hotspot 0, -rm_ProjectedLength/2,				-_heightUnder+_profH, unID, rm_OverHang, 1+128	: unID=unID+1
	hotspot 0, -rm_ProjectedLength/2+1,				-_heightUnder+_profH, unID, rm_OverHang, 3		: unID=unID+1
	hotspot 0, -rm_ProjectedLength/2-rm_OverHang, 	-_heightUnder+_profH, unID, rm_OverHang, 2		: unID=unID+1

	unID = 6500
	hotspot 0, rm_ProjectedLength/2,				-_heightUnder+_profH, unID, rm_OverHang, 1+128	: unID=unID+1
	hotspot 0, rm_ProjectedLength/2-1,				-_heightUnder+_profH, unID, rm_OverHang, 3		: unID=unID+1
	hotspot 0, rm_ProjectedLength/2+rm_OverHang, 	-_heightUnder+_profH, unID, rm_OverHang, 2		: unID=unID+1

! --- Profile Width ------------------------------------------------------------
	unID = 1000
	hotspot  0,					_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot  profileWidthHalf,	_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot -profileWidthHalf-1,_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	hotspot  0,					_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot -profileWidthHalf,	_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot  profileWidthHalf+1,_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	unID = 1100
	hotspot  0,					-_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot  profileWidthHalf,	-_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot -profileWidthHalf-1,-_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	hotspot  0,					-_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot -profileWidthHalf,	-_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot  profileWidthHalf+1,-_bottomLength, -_heightUnder+_underPlus, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

! --- Profile Height ------------------------------------------------------------
	unID = 2000
	addz _heightAbove
		hotspot 0, 0,  0,					unID, rm_ProfileHeight, 1+128	: unID=unID+1
		hotspot 0, 0, -1,					unID, rm_ProfileHeight, 3		: unID=unID+1
		hotspot 0, 0,  rm_ProfileHeight,	unID, rm_ProfileHeight, 2		: unID=unID+1
	del 1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	rotz 90
		hotspot  0,						0, 0, unID, projectedLengthHalf, 1+128,	rm_ProjectedLength	: unID=unID+1
		hotspot -1,						0, 0, unID, projectedLengthHalf, 3,		rm_ProjectedLength	: unID=unID+1
		hotspot  projectedLengthHalf,	0, 0, unID, projectedLengthHalf, 2,		rm_ProjectedLength	: unID=unID+1
	del 1

	rotz -90
		hotspot  0,						0, 0, unID, projectedLengthHalf, 1+128,	rm_ProjectedLength	: unID=unID+1
		hotspot -1,						0, 0, unID, projectedLengthHalf, 3,		rm_ProjectedLength	: unID=unID+1
		hotspot  projectedLengthHalf,	0, 0, unID, projectedLengthHalf, 2,		rm_ProjectedLength	: unID=unID+1
	del 1

! --- Height of Arc ------------------------------------------------------------
	unID = 11000
	hotspot 0, 0,  0,				unID, radiusHeight, 1+128,	rm_Radius	: unID=unID+1
	hotspot 0, 0,  radiusHeight,	unID, radiusHeight, 2,		rm_Radius	: unID=unID+1
	hotspot 0, 0, -1,				unID, radiusHeight, 3,		rm_Radius	: unID=unID+1


! ==============================================================================
! Roofmaker Element
! ==============================================================================

addx -rm_ProfileWidth/2
addz -sqr(rm_Radius*rm_Radius-rm_ProjectedLength/2*rm_ProjectedLength/2)

! -------------- Vertical Cut --------------
nCut = 0

addy rm_ProjectedLength/2+rm_OverHang
cutplane 1, 0, 1
nCut = nCut + 1
del 1

muly -1
addy rm_ProjectedLength/2+rm_OverHang
cutplane 1, 0, 1
nCut = nCut + 1
del 2

_beta = _alpha
if rm_iRoofEdgeAngle = EDGE_PERDENDICULAR then _beta = _alpha2

rotx 90-_beta/2
	base
		revolve 5, _beta, 1+2+4+8+16+32,
				0, rm_Radius, 0,
				rm_ProfileWidth, rm_Radius, 0,
				rm_ProfileWidth, rm_Radius+rm_ProfileHeight, 0,
				0, rm_Radius+rm_ProfileHeight, 0,
				0, rm_Radius, 0

		vert 0,0,0
		vert 0,-rm_ProjectedLength/2,rm_ProjectedLength/2
		vert 0,rm_ProjectedLength/2,-rm_ProjectedLength/2
		vert 1,0,0

		coor 3, -1, -2, -3, -4
	body 1
del 1

for i = 1 to nCut
	cutend
next i

del 2


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i

END
