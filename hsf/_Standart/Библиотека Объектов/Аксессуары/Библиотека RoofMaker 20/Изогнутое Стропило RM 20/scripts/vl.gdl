
! =============================================================================
! Parameters set by the RoofMaker add-on:
! None
! =============================================================================

values "rm_RoofEdgeAngle" stEdgeType
values "rm_iRoofEdgeAngle" 1,2

if GLOB_MODPAR_NAME = "rm_RoofEdgeAngle" then
	rm_iRoofEdgeAngle = 1
	for i = 2 to vardim1(stEdgeType)
		if rm_RoofEdgeAngle = stEdgeType[i] then
			rm_iRoofEdgeAngle = i
			i = vardim1(stEdgeType)
		endif
	next i
	parameters rm_iRoofEdgeAngle = rm_iRoofEdgeAngle
else
	rm_iRoofEdgeAngle = max(min(rm_iRoofEdgeAngle,vardim1(stEdgeType)),1)
	parameters rm_RoofEdgeAngle = stEdgeType[rm_iRoofEdgeAngle]
endif

values "rm_Overhang"		range [0,rm_Radius-rm_ProjectedLength/2]
values "rm_ProfileHeight"	range (0,)
!values "rm_ProfileWidth"	range (0,)
values "profileWidthHalf"	range (0,)
if rm_ProfileWidth < EPS then
	rm_ProfileWidth = 0.001
	parameters rm_ProfileWidth = rm_ProfileWidth
endif
if GLOB_MODPAR_NAME = "profileWidthHalf" then
	rm_ProfileWidth = profileWidthHalf*2
	parameters rm_ProfileWidth = rm_ProfileWidth
else
	profileWidthHalf = rm_ProfileWidth/2
	parameters profileWidthHalf = profileWidthHalf
endif

values "projectedLengthHalf"	range (0,(rm_Radius-rm_OverHang)]
values "rm_ProjectedLength"		range (0,(rm_Radius-rm_OverHang)*2]
if GLOB_MODPAR_NAME = "projectedLengthHalf" then
	rm_ProjectedLength = projectedLengthHalf*2
	parameters rm_ProjectedLength = rm_ProjectedLength
else
	projectedLengthHalf = rm_ProjectedLength/2
	parameters projectedLengthHalf = projectedLengthHalf
endif


values "radiusHeight"	range (0, rm_ProjectedLength/2]
values "rm_Radius"		range [sqr((rm_ProjectedLength/2+rm_OverHang)^2),)
values "ZZYZX"			range (rm_ProfileHeight, rm_ProjectedLength/2 + rm_OverHang + rm_ProfileHeight]
if GLOB_MODPAR_NAME = "radiusHeight" | GLOB_MODPAR_NAME = "ZZYZX" then
	if GLOB_MODPAR_NAME = "radiusHeight" then
		rm_Radius	= ((rm_ProjectedLength/2)^2+radiusHeight^2)/2/radiusHeight
		ZZYZX 		= rm_Radius-sqr(abs(rm_Radius^2-(rm_ProjectedLength/2+rm_OverHang)^2)) + rm_ProfileHeight
		parameters rm_Radius = rm_Radius, ZZYZX = ZZYZX
	else
		_height			= ZZYZX - rm_ProfileHeight
		_diagonalLength	= rm_ProjectedLength/2+rm_OverHang
		_angle			= atn(_height / _diagonalLength) * 2
		rm_Radius		=_diagonalLength / sin(_angle)
		radiusHeight 	= rm_Radius-sqr(abs(rm_Radius^2-(rm_ProjectedLength/2)^2))
		parameters rm_Radius = rm_Radius, radiusHeight = radiusHeight
	endif
else
	radiusHeight 	= rm_Radius-sqr(abs(rm_Radius^2-(rm_ProjectedLength/2)^2))
	ZZYZX 			= rm_Radius-sqr(abs(rm_Radius^2-(rm_ProjectedLength/2+rm_OverHang)^2)) + rm_ProfileHeight
	parameters radiusHeight = radiusHeight, ZZYZX = ZZYZX
endif

if rm_Radius<sqr((rm_ProjectedLength/2+rm_OverHang)^2) then
	rm_Radius		= sqr((rm_ProjectedLength/2+rm_OverHang)^2)
	radiusHeight	= rm_Radius-sqr(abs(rm_Radius^2-(rm_ProjectedLength/2)^2))
	ZZYZX 			= rm_Radius-sqr(abs(rm_Radius^2-(rm_ProjectedLength/2+rm_OverHang)^2)) + rm_ProfileHeight
	parameters rm_Radius = rm_Radius, radiusHeight = radiusHeight, ZZYZX = ZZYZX
endif

parameters rm_StructuralLength = rm_StructuralLength
lock "rm_StructuralLength"


! ==============================================================================
! Lumber Size
! ==============================================================================
lumberSize = rm_StructuralLength
parameters lumberSize = lumberSize

! ==============================================================================
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iObjectType				= 2


! ==============================================================================
! Bounding Box
! ==============================================================================
parameters B			= rm_ProjectedLength+2*rm_OverHang		: lock "B"
parameters A			= rm_ProfileWidth						: lock "A"
parameters innerHeight	= _height								: lock "innerHeight"

! ==============================================================================
! Levels
! ==============================================================================
parameters ac_toplevel 		= _heightAbove+rm_ProfileHeight
parameters ac_bottomlevel	= -_heightUnder+_underPlus
