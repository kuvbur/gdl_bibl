
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================
! --- Height Difference ------------------------------------------------------------
	unID = 4000
	addz -profileHeight/2
	hotspot 0, 0,  0,				unID, rm_HeightDiff, 1+256	: unID=unID+1
	hotspot 0, 0, -1,				unID, rm_HeightDiff, 3		: unID=unID+1
	hotspot 0, 0,  rm_HeightDiff,	unID, rm_HeightDiff, 2+128	: unID=unID+1

	addx A
	hotspot 0, 0,  0,				unID, rm_HeightDiff, 1+128	: unID=unID+1
	hotspot 0, 0, -1,				unID, rm_HeightDiff, 3		: unID=unID+1
	hotspot 0, 0,  rm_HeightDiff,	unID, rm_HeightDiff, 2		: unID=unID+1
	del 2

! --- Profile Width ------------------------------------------------------------
	unID = 1000
	hotspot 0,   0,					-profileHeight,	unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
	hotspot 0,  -1,					-profileHeight,	unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1
	hotspot 0,   rm_ProfileWidth/2, -profileHeight,	unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1

	hotspot 0,  0,					-profileHeight,	unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
	hotspot 0,  1,					-profileHeight,	unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1
	hotspot 0,  -rm_ProfileWidth/2, -profileHeight,	unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1

	unID = 1100
	hotspot A,   0,					rm_HeightDiff-profileHeight,	unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
	hotspot A,  -1,					rm_HeightDiff-profileHeight,	unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1
	hotspot A,   rm_ProfileWidth/2, rm_HeightDiff-profileHeight,	unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1

	hotspot A,  0,					rm_HeightDiff-profileHeight,	unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
	hotspot A,  1,					rm_HeightDiff-profileHeight,	unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1
	hotspot A,  -rm_ProfileWidth/2, rm_HeightDiff-profileHeight,	unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1

	!unID = 1200
	!hotspot A/2,   0,					rm_HeightDiff/2-profileHeight,	unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
	!hotspot A/2,  -1,					rm_HeightDiff/2-profileHeight,	unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1
	!hotspot A/2,   rm_ProfileWidth/2,	rm_HeightDiff/2-profileHeight,	unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1

	!hotspot A/2,  0,					rm_HeightDiff/2-profileHeight,	unID, profileWidthHalf, 1+128,	rm_ProfileWidth	: unID=unID+1
	!hotspot A/2,  1,					rm_HeightDiff/2-profileHeight,	unID, profileWidthHalf, 3,		rm_ProfileWidth	: unID=unID+1
	!hotspot A/2,  -rm_ProfileWidth/2, 	rm_HeightDiff/2-profileHeight,	unID, profileWidthHalf, 2,		rm_ProfileWidth	: unID=unID+1


! --- Profile Height ------------------------------------------------------------
	unID = 2000
	hotspot 0,  rm_ProfileWidth/2,  0,				unID, profileHeight, 1+256,	rm_ProfileHeight	: unID=unID+1
	hotspot 0,  rm_ProfileWidth/2,  1,				unID, profileHeight, 3,		rm_ProfileHeight	: unID=unID+1
	hotspot 0,  rm_ProfileWidth/2, -profileHeight,	unID, profileHeight, 2+128,	rm_ProfileHeight	: unID=unID+1

	hotspot 0, -rm_ProfileWidth/2,  0,				unID, profileHeight, 1+256, rm_ProfileHeight	: unID=unID+1
	hotspot 0, -rm_ProfileWidth/2,  1,				unID, profileHeight, 3,		rm_ProfileHeight	: unID=unID+1
	hotspot 0, -rm_ProfileWidth/2, -profileHeight,	unID, profileHeight, 2+128,	rm_ProfileHeight	: unID=unID+1

	add A, 0, rm_HeightDiff
		hotspot 0,  rm_ProfileWidth/2,  0,				unID, profileHeight, 1+256,	rm_ProfileHeight	: unID=unID+1
		hotspot 0,  rm_ProfileWidth/2,  1,				unID, profileHeight, 3,		rm_ProfileHeight	: unID=unID+1
		hotspot 0,  rm_ProfileWidth/2, -profileHeight,	unID, profileHeight, 2+128,	rm_ProfileHeight	: unID=unID+1

		hotspot 0, -rm_ProfileWidth/2,  0,				unID, profileHeight, 1+256, rm_ProfileHeight	: unID=unID+1
		hotspot 0, -rm_ProfileWidth/2,  1,				unID, profileHeight, 3,		rm_ProfileHeight	: unID=unID+1
		hotspot 0, -rm_ProfileWidth/2, -profileHeight,	unID, profileHeight, 2+128,	rm_ProfileHeight	: unID=unID+1
	del 1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	addz -profileHeight/2
	hotspot  0, 0, 0, unID, A, 1+256	: unID=unID+1
	hotspot -1, 0, 0, unID, A, 3		: unID=unID+1
	hotspot  A, 0, 0, unID, A, 2+128	: unID=unID+1
	del 1

	addz rm_HeightDiff-profileHeight/2
	hotspot  0, 0, 0, unID, A, 1+128	: unID=unID+1
	hotspot -1, 0, 0, unID, A, 3		: unID=unID+1
	hotspot  A, 0, 0, unID, A, 2		: unID=unID+1
	del 1





! ==============================================================================
! Roofmaker Element
! ==============================================================================
profileHeight		= rm_ProfileHeight / cos(_angle)
profileThicknessZ	= profileThickness * (profileHeight / rm_ProfileHeight)
addz -profileHeight/2
rotz 90
rotx 90
nTrans = 3

if rm_iProfileType = PROFILE_TYPE_RECTANGULAR then
	put  rm_ProfileWidth/2,  profileHeight/2, 0,
		-rm_ProfileWidth/2,  profileHeight/2, 0,
		-rm_ProfileWidth/2, -profileHeight/2, 0,
		 rm_ProfileWidth/2, -profileHeight/2, 0
else
	if rm_iProfileType = PROFILE_TYPE_I_BEAM then
		put	-rm_ProfileWidth/2,  -profileHeight/2, 0,
			 rm_ProfileWidth/2,  -profileHeight/2, 0,
			 rm_ProfileWidth/2,  -profileHeight/2+profileThicknessZ, 0,
			 profileThickness/2, -profileHeight/2+profileThicknessZ, 0,
			 profileThickness/2,  profileHeight/2-profileThicknessZ, 0,
			 rm_ProfileWidth/2,   profileHeight/2-profileThicknessZ, 0,
			 rm_ProfileWidth/2,   profileHeight/2, 0,
			-rm_ProfileWidth/2,   profileHeight/2, 0,
			-rm_ProfileWidth/2,   profileHeight/2-profileThicknessZ, 0,
			-profileThickness/2,  profileHeight/2-profileThicknessZ, 0,
			-profileThickness/2, -profileHeight/2+profileThicknessZ, 0,
			-rm_ProfileWidth/2,  -profileHeight/2+profileThicknessZ, 0,
			-rm_ProfileWidth/2,  -profileHeight/2, 0
	endif

	if rm_iProfileType = PROFILE_TYPE_L_BEAM then
		put	-rm_ProfileWidth/2, -profileHeight/2, 0,
			 rm_ProfileWidth/2, -profileHeight/2, 0,
			 rm_ProfileWidth/2, -profileHeight/2+profileThicknessZ, 0,
			-rm_ProfileWidth/2+profileThickness, -profileHeight/2+profileThicknessZ, 0,
			-rm_ProfileWidth/2+profileThickness, profileHeight/2, 0,
			-rm_ProfileWidth/2,  profileHeight/2, 0,
			-rm_ProfileWidth/2, -profileHeight/2, 0
	endif

	if rm_iProfileType = PROFILE_TYPE_C_BEAM then
		put	-rm_ProfileWidth/2, -profileHeight/2, 0,
			 rm_ProfileWidth/2, -profileHeight/2, 0,
			 rm_ProfileWidth/2, -profileHeight/2+profileThicknessZ, 0,
			-rm_ProfileWidth/2+profileThickness, -profileHeight/2+profileThicknessZ, 0,
			-rm_ProfileWidth/2+profileThickness,  profileHeight/2-profileThicknessZ, 0,
			 rm_ProfileWidth/2,  profileHeight/2-profileThicknessZ, 0,
			 rm_ProfileWidth/2,  profileHeight/2, 0,
			-rm_ProfileWidth/2,  profileHeight/2, 0,
			-rm_ProfileWidth/2, -profileHeight/2, 0
	endif

	if rm_iProfileType <> PROFILE_TYPE_I_BEAM then
		if not(bProfileMirrored) then
			mulx -1
			nTrans = nTrans + 1
		endif
	endif
endif

!base
if NSP > 11 then
	extrude NSP/3, 0, rm_HeightDiff, A, 1+2+4+16+32,
		get(NSP)
else
	n = NSP
	for i = 1 to n
		_t = get(1)
	next i
endif

rotx -_angle
!vert 0, 0, 0
!vert 0, 0, 1
!vert 0, 1, 0
!vert 1, 0, 0
!
!coor 2, -1, -2, -3, -4
!body 1

del 1+nTrans



! ==============================================================================
!for i = 1 to nProjectionCut
!	cutend
!next i

END
