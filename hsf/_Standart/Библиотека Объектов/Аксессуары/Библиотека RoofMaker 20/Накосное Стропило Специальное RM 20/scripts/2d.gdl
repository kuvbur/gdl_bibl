
pen gs_cont_pen
line_type gs_cont_line
fill gs_fill_type
hLineID = 2500

! ==============================================================================
! Roofmaker Macro
! ==============================================================================

call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										txt_BasePointX			= rm_ProjectedLength/2,
										txt_BasePointY			= 0,
										txt_BaseAngle			= 0,
				returned_parameters 	gs_cutplane_height,
										gs_top_range_height,
										gs_bottom_range_height,
										doCutplane


! ==============================================================================
! Listing
! ==============================================================================
if GLOB_CONTEXT = 6 then
	project2{2} 4, 270, 3+32+1024,
		gs_back_pen,0,0,0
	end
endif


! ==============================================================================
! 2D Hotspots
! ==============================================================================
unID=1

! --- Centre ------------------------------------------------------------------
	hotspot2 rm_ProjectedLength/2, 0, 100

	if rm_bRafterVisible then

	! --- Profile Width ------------------------------------------------------------
		unID = 1000
		hotspot2 -overHang,  0,					unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot2 -overHang, -profileWidthHalf,	unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot2 -overHang,  1,					unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

		hotspot2 -overHang,  0,					unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot2 -overHang,  profileWidthHalf,	unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot2 -overHang, -1,					unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	! --- 1. Cutting Angle ------------------------------------------------------------
		M1x		= (rm_ProfileWidth/2/tan(topJointAngle_1_Half))
		L1XY	= M1x/cos(topJointAngle_1_Half)

		add2 rm_ProjectedLength,0
			hotspot2  0,								0,									unID, topJointAngle_1_Half, 6+512,	rm_TopJointAngle_1 : unID=unID+1
			hotspot2 -L1XY,								0,									unID, topJointAngle_1_Half, 4+128,	rm_TopJointAngle_1 : unID=unID+1
			hotspot2 -cos(topJointAngle_1_Half) * L1XY, sin(topJointAngle_1_Half) * L1XY,	unID, topJointAngle_1_Half, 5,		rm_TopJointAngle_1 : unID=unID+1
		del 1

	! --- 2. Cutting Angle ------------------------------------------------------------
		M2x		= (rm_ProfileWidth/2/tan(topJointAngle_2_Half))
		L2XY	= M2x/cos(topJointAngle_2_Half)

		add2 rm_ProjectedLength,0
			hotspot2 0,									0,									unID, topJointAngle_2_Half, 6,		rm_TopJointAngle_2 : unID=unID+1
			hotspot2 -L2XY,								0,									unID, topJointAngle_2_Half, 4+128,	rm_TopJointAngle_2 : unID=unID+1
			hotspot2 -cos(topJointAngle_2_Half) * L2XY, -sin(topJointAngle_2_Half) * L2XY,  unID, topJointAngle_2_Half, 5,		rm_TopJointAngle_2 : unID=unID+1
		del 1
	endif

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	hotspot2  0,					0, unID, rm_ProjectedLength, 1+256	: unID=unID+1
	hotspot2  rm_ProjectedLength,	0, unID, rm_ProjectedLength, 2		: unID=unID+1
	hotspot2 -1,					0, unID, rm_ProjectedLength, 3		: unID=unID+1


! --- Overhang ------------------------------------------------------------
	unID = 6000
	!!hotspot2  0,				0, unID, rm_OverHang, 1+128	: unID=unID+1
	!!hotspot2 -rm_OverHang,	0, unID, rm_OverHang, 2		: unID=unID+1
	!!hotspot2  1,				0, unID, rm_OverHang, 3		: unID=unID+1

	hotspot2  0,		0, unID, overhang, 1+128,	rm_OverHang: unID=unID+1
	hotspot2 -overhang,	0, unID, overhang, 2,		rm_OverHang: unID=unID+1
	hotspot2  1,		0, unID, overhang, 3,		rm_OverHang: unID=unID+1


! ==============================================================================
! Axis
! ==============================================================================
if not(rm_bRafterVisible) | AC_symb_display_option <> DISPLAY_OUTLINES then
	line_type rm_AxisLineType
	pen gs_axis_line_pen

	line2		-overHang, 0, rm_ProjectedLength, 0
	hotline2	-overHang, 0, 0, 0, hLineID : hLineID = hLineID + 1
	hotline2	 0, 0, rm_ProjectedLength, 0, hLineID : hLineID = hLineID + 1

	if not(rm_bRafterVisible) then end
else
	hLineID = hLineID + 2
endif


! ==============================================================================
! Floor Plan View
! ==============================================================================

if AC_symb_display_option = DISPLAY_SYMBOLIC then

	! ----- Symbolic 2D --------------------------------------------------------
	pen gs_fill_pen
	fill gs_fill_type
	line_type gs_cont_line
	project2{2} 3, 270, 3+32+1024, gs_back_pen,0,0,0

	pen gs_cont_pen
	project2{3} 3,270,3+32+1024, 8

else

	! ----- View Edges ---------------------------------------------------------
	pen gs_cont_pen
	line_type gs_cont_line

	if AC_symb_display_option = DISPLAY_OVERHEAD_ALL then
		pen AC_overhead_pen
		line_type AC_overhead_linetype
	endif

	project2{3} 3,270,2 + 32 * (AC_symb_display_option = DISPLAY_OVERHEAD_ALL | not(gs_use_3D_view_attributes)), 8,
		parameters	gs_2D_projection		= doCutplane,
					gs_2D_projection_type	= 1,
					gs_cutplane_height		= gs_cutplane_height,
					gs_top_range_height		= gs_top_range_height,
					gs_bottom_range_height	= gs_bottom_range_height


	if AC_symb_display_option <> DISPLAY_OVERHEAD_ALL then
		! ----- View Polygons --------------------------------------------------
		pen gs_fill_pen
		fill gs_fill_type

		project2{3} 3,270,3+32+1024+2048, 4,
						gs_back_pen,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 2,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif


	if AC_symb_display_option = DISPLAY_OVERHEAD_PROJ then

		! ----- Overhead View -----------------------------------------------------
		pen AC_overhead_pen
		line_type AC_overhead_linetype

		project2{3} 3,270,2+32, 2+8,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 3,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif

	if doCutplane then

		! ----- Cut Lines ---------------------------------------------------------
		pen gs_cut_pen
		line_type AC_cut_linetype
		line_property 2

		project2{3} 3,270,2+16+32+256+4096, 2,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 4,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height


		! ----- Cut Polygons ------------------------------------------------------
		pen gs_cut_fill_pen
		fill gs_cut_fill_type

		project2{3} 3,270,3+32+512, 1,
						gs_cut_back_pen,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 5,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif
endif
