
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================
rm_Height	= rm_ProjectedLength*tan(rm_PitchAngle)
U			= abs(rm_VerticalTrimmerWidth/(2*sin(rm_VerticalTrimmerAngle)))
U1			= abs(rm_TrimmerWidth/(2*sin(rm_AngleRidge)))

! --- Roof Pitch ------------------------------------------------------------------
	unID = 5000
	!hotspot  0,										 0,  0,										unID,rm_PitchAngle, 6		: unID=unID+1
	!hotspot -rm_ProjectedLength/2,						 0,  0,										unID,rm_PitchAngle, 4+128	: unID=unID+1
	!hotspot  0,										-1,  0,										unID,rm_PitchAngle, 7		: unID=unID+1
	!hotspot -structuralLength/2*cos(rm_PitchAngle),	 0, -structuralLength/2*sin(rm_PitchAngle),	unID,rm_PitchAngle, 5		: unID=unID+1

	hotspot  0,											 0,  0,											unID, rm_PitchAngle, 6		: unID=unID+1
	hotspot -rm_ProjectedLength/2,						 0,  0,											unID, rm_PitchAngle, 4+128	: unID=unID+1
	hotspot  0,											-1,  0,											unID, rm_PitchAngle, 7		: unID=unID+1
	hotspot -(rm_ProjectedLength/2)*cos(rm_PitchAngle),	 0, -rm_ProjectedLength/2*sin(rm_PitchAngle),	unID, rm_PitchAngle, 5		: unID=unID+1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	roty -rm_PitchAngle
		hotspot  0,					0, 0, unID, structuralLength, 1+256,	rm_ProjectedLength : unID=unID+1
		hotspot  1,					0, 0, unID, structuralLength, 3,		rm_ProjectedLength : unID=unID+1
		hotspot -structuralLength,	0, 0, unID, structuralLength, 2,		rm_ProjectedLength : unID=unID+1
	del 1


! --- Profile Width ------------------------------------------------------------
	unID = 1000
	hotspot -rm_ProjectedLength/2,  0,					-rm_ProjectedLength/2*tan(rm_PitchAngle), unID,profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot -rm_ProjectedLength/2, -rm_ProfileWidth/2,	-rm_ProjectedLength/2*tan(rm_PitchAngle), unID,profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot -rm_ProjectedLength/2,  1,					-rm_ProjectedLength/2*tan(rm_PitchAngle), unID,profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	hotspot -rm_ProjectedLength/2,  0,					-rm_ProjectedLength/2*tan(rm_PitchAngle), unID,profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot -rm_ProjectedLength/2,  rm_ProfileWidth/2,	-rm_ProjectedLength/2*tan(rm_PitchAngle), unID,profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot -rm_ProjectedLength/2, -1,					-rm_ProjectedLength/2*tan(rm_PitchAngle), unID,profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

! --- Profile Height ------------------------------------------------------------
	unID = 2000
	hotspot -rm_ProjectedLength/2,										0, -rm_ProjectedLength/2*tan(rm_PitchAngle),								unID,rm_ProfileHeight, 1+128	: unID=unID+1
	hotspot -rm_ProjectedLength/2-rm_ProfileHeight*sin(rm_PitchAngle),	0, -rm_ProjectedLength/2*tan(rm_PitchAngle)+rm_ProfileHeight*cos(rm_PitchAngle),	unID,rm_ProfileHeight, 2		: unID=unID+1
	hotspot -rm_ProjectedLength/2+rm_ProfileHeight*sin(rm_PitchAngle),	0, -rm_ProjectedLength/2*tan(rm_PitchAngle)-rm_ProfileHeight*cos(rm_PitchAngle),	unID,rm_ProfileHeight, 3		: unID=unID+1

! --- Non Editable Hotspots ------------------------------------------------------------
	unID = 1
	if not(_bVertical) then
		hotspot -rm_ProjectedLength+U, 0, -rm_ProjectedLength*tan(rm_PitchAngle)+U*tan(rm_PitchAngle),					unID : unID=unID+1
		hotspot -rm_ProjectedLength+U, 0, -rm_ProjectedLength*tan(rm_PitchAngle)+U*tan(rm_PitchAngle)+_profileHeightZ,	unID : unID=unID+1
	else
		hotspot -rm_ProjectedLength+_bottomCutX1, 0, (-rm_ProjectedLength+_bottomCutX1)*tan(rm_PitchAngle),	unID : unID=unID+1
		hotspot -rm_ProjectedLength+_bottomCutX1-_bottomAplus, 0, (-rm_ProjectedLength+_bottomCutX1-_bottomAplus)*tan(rm_PitchAngle)+_profileHeightZ,	unID : unID=unID+1
	endif

	if not(rm_bIsTrimmerRotated) then
		hotspot -U1, 0, -U1*tan(rm_PitchAngle),					unID : unID=unID+1
		hotspot -U1, 0, -U1*tan(rm_PitchAngle)+_profileHeightZ,	unID : unID=unID+1
	else
		hotspot -_topCutXP, 0, -_topCutXP*tan(rm_PitchAngle),					unID : unID=unID+1
		hotspot -_topCutXP-_topAPlus, 0, (-_topCutXP-_topAPlus)*tan(rm_PitchAngle)+_profileHeightZ,	unID : unID=unID+1
	endif


! ==============================================================================
! Roofmaker Element
! ==============================================================================

if rm_VerticalTrimmerAngle < 0 then
	_bCutSide = 0
else
	_bCutSide = 1
endif

if rm_VerticalTrimmerAngle<=90.0001 & rm_VerticalTrimmerAngle>=89.9999 & rm_bIsTrimmerPerdendicular then
	_bVertical = 1
else
	_bVertical = 0
endif

addy rm_ProfileWidth/2
rotx 90

! --- Bottom Cut ------------------------------------------------------------------
	if not(_bVertical) then
		addx U-rm_ProjectedLength
		addz rm_ProfileWidth/2
		roty rm_VerticalTrimmerAngle
		cutplane 1, 1, 0, _bCutSide
		del 3
	else
		addx -rm_ProjectedLength
		addz rm_ProfileWidth/2
		addy _profileHeightZ/2-rm_Height
		rotz rm_PitchAngle
		addx rm_VerticalTrimmerWidth/2
		cutplane 0, 1, 1, _bCutSide
		del 5
	endif

! --- Top Cut ------------------------------------------------------------------
	if rm_bIsTrimmerRotated then
		addz rm_ProfileWidth/2
		addy _profileHeightZ/2
		rotz rm_PitchAngle
		roty rm_AngleRidge-90
		addx -rm_TrimmerWidth/2
		cutplane 0, 1, 1
		del 5
	else
		addz rm_ProfileWidth/2
		addy _profileHeightZ/2
		roty rm_AngleRidge-90
		addx -rm_TrimmerWidth/2
		cutplane 0, 1, 1
		del 4
	endif


! --- 3D Body ------------------------------------------------------------------
	base
		prism 5, rm_ProfileWidth,
				0, 0,
				0, _profileHeightZ,
				-rm_ProjectedLength, _profileHeightZ-rm_Height,
				-rm_ProjectedLength, -rm_Height,
				0, 0

		!if not(_bVertical) then
			vert 0, 0, 0
			vert rm_ProjectedLength, tan(rm_PitchAngle)*rm_ProjectedLength, 0
			vert -sin(rm_PitchAngle), cos(rm_PitchAngle), 0
			vert 0, 0, -1
		!else
		!	vert 0, 0, 0
		!	vert 1, 1, 0
		!	vert 0, 1, 0
		!	vert 0, 0, 1
		!endif

		coor 2, -1, -2, -3, -4
	body 1

	cutend
	cutend
del 2


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i

END



