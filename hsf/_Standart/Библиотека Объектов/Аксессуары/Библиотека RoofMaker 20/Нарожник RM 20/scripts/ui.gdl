
! =============================================================================
! Localization
! =============================================================================
dim stGeometry[5]
	stGeometry[1]	= `Угол Ската`
	stGeometry[2]	= `Исходная Длина`
	stGeometry[3]	= `Длина Элемента`
	stGeometry[4]	= `Ширина Профиля`
	stGeometry[5]	= `Высота Профиля`

dim stCutting[9]
	stCutting[1]	= `Распорка`
	stCutting[2]	= `Конек или Распорка Повернуты`
	stCutting[3]	= `Распорка Перпендикулярно`
	stCutting[4]	= `Толщина Конька или Распорки`
	stCutting[5]	= `Толщина Вертикального Стропила или Распорки`
	stCutting[6]	= `Углы Соединений`
	stCutting[7]	= `Угол к Вертикальному Стропилу`
	stCutting[8]	= `Угол к Коньку или Распорке`
	stCutting[9]	= `Для использования этой опции, угол к вертикальному стропилу должен быть равен 90,00°.`


! =============================================================================
! Variables
! =============================================================================

pageStartY		= 5
xInfieldSmall	= 70
xInfieldBig		= 90
yInfield		= 19
yOutfield		= 16

! =============================================================================
! Tabpages
! =============================================================================

TABID_ROOT					= -1
TABID_GEOMETRY				= 1
TABID_CUTTING				= 2

call "RoofMaker_macro"	parameters all	iObjectType				= 5,
										bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iUiType					= 1


ui_page TABID_GEOMETRY, TABID_ROOT, `Геометрия`, "uiTab_Dimension_flat(1)"
if gs_ui_current_page = TABID_GEOMETRY then
	gosub "geometryPage"
endif

ui_page TABID_CUTTING, TABID_ROOT, `Подрезка`, "uiTab_RMCut_flat(1)"
if gs_ui_current_page = TABID_CUTTING then
	gosub "cuttingPage"
endif

! ======================================================================
end
! ======================================================================

"geometryPage":
! =============================================================================
! Geometry
! =============================================================================

	picX			= 340
	picY			= pageStartY+20

	ui_pict 2,	picX-248, picY+64, 220,  116, 1										!jackrafter_pict1.png
	ui_pict 3,	picX-141, picY, 128, 64, 1											!jackrafter_pict2.png

	ui_pict 1, picX-28, picY+151, 24, 17, 1											!ui_rm_angle_1.png
	ui_infield "rm_PitchAngle", picX, picY+148, xInfieldBig, yInfield				ui_tooltip stGeometry[1]

	ui_infield "rm_ProjectedLength", picX-175, picY+185, xInfieldBig, yInfield		ui_tooltip stGeometry[2]
	ui_infield "rm_StructuralLength", picX-256, picY+41, xInfieldBig, yInfield		ui_tooltip stGeometry[3]
	ui_infield "rm_ProfileWidth", picX, picY+27, xInfieldBig, yInfield				ui_tooltip stGeometry[4]
	ui_infield "rm_ProfileHeight", picX, picY+2, xInfieldBig, yInfield				ui_tooltip stGeometry[5]

return

"cuttingPage":
! =============================================================================
! Cutting
! =============================================================================

	picX1		= 283
	picY1		= 28 + pageStartY
	dy			= 23
	yPos		= pageStartY

	ui_pict 8,	picX1, picY1, 63,  183, 1												!jackrafter_pict7.png
	ui_style 0, 1
	ui_outfield stCutting[6], 275, pageStartY - 2, 444-275, yOutfield
	ui_style 0, 0
	ui_infield "rm_AngleRidge", picX1+65, picY1+11, xInfieldBig, yInfield				ui_tooltip stCutting[8]
	ui_infield "rm_VerticalTrimmerAngle", picX1+65, picY1+160, xInfieldBig, yInfield	ui_tooltip stCutting[7]

	ui_separator 270, pageStartY, 270, pageStartY + 230

	ui_style 0, 1
	ui_outfield stCutting[1], 3, pageStartY - 2, 65, yOutfield
	ui_style 0, 0

	picX2	= 30
	picY2	= pageStartY + 15

	ui_infield{3} "rm_bIsTrimmerPerdendicular", 12, pageStartY + 213, 200, yInfield* 1.5,
		7, "", 0, 0,
		0,0,0,0,
		0, stCutting[3],	0,
		0, stCutting[3],	1	ui_tooltip stCutting[9]

	if not(rm_bIsTrimmerPerdendicular) then
		ui_pict 6,	picX2+7, picY2+97, 81,  77, 1										!jackrafter_pict5.png
		ui_infield "rm_VerticalTrimmerWidth", 28, pageStartY + 192, xInfieldBig, yInfield	ui_tooltip stCutting[5]
	else
		ui_pict 5,	picX2-1, picY2+97, 89,  60, 1										!jackrafter_pict4.png
		ui_infield "rm_VerticalTrimmerWidth", 63, pageStartY + 175, xInfieldBig, yInfield	ui_tooltip stCutting[5]
	endif

	ui_infield{3} "rm_bIsTrimmerRotated", 69, pageStartY-3, 200, yInfield * 1.5,
		7, "", 0, 0,
		0,0,0,0,
		0, stCutting[2],	0,
		0, stCutting[2],	1	ui_tooltip stCutting[2]

	if not(rm_bIsTrimmerRotated) then
		ui_pict 7,	picX2+38, picY2+12, 94, 85, 1										!jackrafter_pict6.png
		ui_infield "rm_TrimmerWidth", 165, pageStartY + 85, xInfieldBig, yInfield					ui_tooltip stCutting[4]
	else
		ui_pict 4,	picX2+38, picY2+17, 101, 80, 1										!jackrafter_pict3.png
		ui_infield "rm_TrimmerWidth", 172, pageStartY + 70, xInfieldBig, yInfield					ui_tooltip stCutting[4]
	endif


return

