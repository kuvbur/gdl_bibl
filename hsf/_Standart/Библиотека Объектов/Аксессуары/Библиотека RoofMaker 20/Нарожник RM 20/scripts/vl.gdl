
! =================================================================================
! Rafter parameters set by the RoofMaker Add-on:
! "rm_ProjectedLength"			Original Length
! "rm_ProfileWidth"				Profile Width
! "rm_ProfileHeight"			Profile Height
! "rm_Height"					Height Difference
! "rm_PitchAngle"				Pitch Angle
! "rm_TrimmerWidth"				Width of Ridge or Trimmer
! "rm_AngleRidge"				Angle to Ridge or Trimmer
! "rm_bIsTrimmerRotated"		Ridge or Trimmer Rotated
! "rm_VerticalTrimmerAngle"		Angle to Vertical Rafter
! "rm_VerticalTrimmerWidth"		Width of Vertical Rafter or Trimmer
! "rm_bIsTrimmerPerdendicular"	Trimmer Perpendicular
! "rm_bRafterVisible"			Rafter Visible in 2D
! "rm_AxisLineType"				Axis Line Type
!
! Parameters not by the RoofMaker Add-on:
! "rm_JointAngle"
! "rm_CutAxisDist"
! "rm_CutEndPointDist"
! "rm_OverHang"
! "rm_RoofEdgeAngle"
! "rm_iRoofEdgeAngle"
! "rm_jointWidth"
! "rm_bSoffit"
! "rm_SoffitAngle"
! =================================================================================

values "rm_PitchAngle"				range [0, 90)
values "rm_VerticalTrimmerAngle"	range (0, 180)
values "rm_AngleRidge"				range (0, 180)
values "profileWidthHalf"			range (0,)
values "rm_ProfileHeight"			range (0,)

if rm_ProfileWidth < EPS then
	rm_ProfileWidth = 0.001
	parameters rm_ProfileWidth = rm_ProfileWidth
endif

if GLOB_MODPAR_NAME = "profileWidthHalf" Then
	rm_ProfileWidth = profileWidthHalf*2
	parameters rm_ProfileWidth = rm_ProfileWidth
else
	profileWidthHalf = rm_ProfileWidth/2
	parameters profileWidthHalf = profileWidthHalf
endif

if rm_VerticalTrimmerWidth < 0 then
	rm_VerticalTrimmerWidth = 0
	parameters rm_VerticalTrimmerWidth = rm_VerticalTrimmerWidth
endif

if rm_TrimmerWidth < 0 then
	rm_TrimmerWidth = 0
	parameters rm_TrimmerWidth = rm_TrimmerWidth
endif

values "rm_Height"				range [0,)
values "structuralLength"		range (0,)
if rm_ProjectedLength < _topCutX+_bottomCutX	 then
	rm_ProjectedLength = _topCutX+_bottomCutX
	parameters rm_ProjectedLength = rm_ProjectedLength
endif
if rm_StructuralLength < _strutLengthPlus+EPS then
	rm_StructuralLength = _strutLengthPlus+0.001
	parameters rm_StructuralLength = rm_StructuralLength
endif

if GLOB_MODPAR_NAME="rm_StructuralLength" Then
	structuralLength	= rm_StructuralLength + _cuttingLength - _strutLengthPlus
	rm_ProjectedLength	= structuralLength*cos(rm_PitchAngle)
	rm_Height			= structuralLength*sin(rm_PitchAngle)
	parameters rm_Height = rm_Height, rm_ProjectedLength = rm_ProjectedLength, structuralLength = structuralLength
else
	if GLOB_MODPAR_NAME="structuralLength" Then
		rm_StructuralLength	= structuralLength - _cuttingLength + _strutLengthPlus
		rm_ProjectedLength	= structuralLength*cos(rm_PitchAngle)
		rm_Height			= structuralLength*sin(rm_PitchAngle)
		parameters rm_Height = rm_Height, rm_ProjectedLength = rm_ProjectedLength, rm_StructuralLength = rm_StructuralLength
	else
		if GLOB_MODPAR_NAME="rm_Height" then
			rm_ProjectedLength	= rm_Height/tan(rm_PitchAngle)
			structuralLength	= rm_ProjectedLength/cos(rm_PitchAngle)
			rm_StructuralLength	= structuralLength - _cuttingLength + _strutLengthPlus
			parameters rm_ProjectedLength = rm_ProjectedLength, rm_StructuralLength = rm_StructuralLength, structuralLength = structuralLength
		else
			! "rm_ProjectedLength" and "rm_PitchAngle" are the strongest parameters
			rm_Height			= rm_ProjectedLength*tan(rm_PitchAngle)
			structuralLength	= rm_ProjectedLength/cos(rm_PitchAngle)
			rm_StructuralLength	= structuralLength - _cuttingLength + _strutLengthPlus
			parameters rm_Height = rm_Height, rm_StructuralLength = rm_StructuralLength, structuralLength = structuralLength
		endif
	endif
endif


! ==============================================================================
! Lumber Size
! ==============================================================================
gosub "calcLength"
gosub "calcStuctLength"
lumberSize = rm_StructuralLength
parameters lumberSize = lumberSize

! ==============================================================================
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iObjectType				= 5


! ==============================================================================
! Bounding Box
! ==============================================================================
parameters A		= A						: lock "A"
parameters B		= rm_ProfileWidth		: lock "B"
parameters ZZYZX	= ZZYZX					: lock "ZZYZX"


! ==============================================================================
! Levels
! ==============================================================================
parameters ac_toplevel 		= _profH - _topCutX * tan(rm_PitchAngle)
parameters ac_bottomlevel	= -rm_Height + _bottomCutX * tan(rm_PitchAngle)
