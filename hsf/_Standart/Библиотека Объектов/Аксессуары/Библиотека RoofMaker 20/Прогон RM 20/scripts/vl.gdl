
! =================================================================================
! Purlin parameters set by the RoofMaker add-on:
! "A"						Horizontal Projection
! "rm_ProfileWidth"			Profile Width
! "rm_ProfileHeight"		Profile Height
! "rm_HeightDiff"			Height Difference
! "rm_RafterHeight"			Rafter Height
! "rm_iProfileType"			Profile
! "rm_bTopOnRafter"			Purlin on Top of Rafters
! "rm_bIsRotated"			Purlin Rotated
! "rm_RotAngle"				Rotation Angle
! =================================================================================


values "rm_ProfileType" stProfileTypes
values "rm_iProfileType" 1,2,3,4

if GLOB_MODPAR_NAME = "rm_ProfileType" then
	rm_iProfileType = PROFILE_TYPE_RECTANGULAR
	if rm_ProfileType = stProfileTypes[2] then rm_iProfileType = PROFILE_TYPE_I_BEAM
	if rm_ProfileType = stProfileTypes[3] then rm_iProfileType = PROFILE_TYPE_L_BEAM
	if rm_ProfileType = stProfileTypes[4] then rm_iProfileType = PROFILE_TYPE_C_BEAM
	parameters rm_iProfileType = rm_iProfileType
else
	rm_ProfileType = stProfileTypes[min(max(1, rm_iProfileType), vardim1(stProfileTypes))]
	parameters rm_ProfileType = rm_ProfileType
endif

if rm_iProfileType = PROFILE_TYPE_RECTANGULAR then
	hideparameter	"profileThickness", "bProfileMirrored"
	lock			"profileThickness", "bProfileMirrored"
endif
if rm_iProfileType = PROFILE_TYPE_I_BEAM then
	hideparameter	"bProfileMirrored"
	lock			"bProfileMirrored"
endif

values "rm_RotAngle" range (-90, 90)

if rm_ProfileWidth < EPS then
	rm_ProfileWidth = 0.001
	parameters rm_ProfileWidth = rm_ProfileWidth
endif

if profileThickness < EPS then
	profileThickness = 0.001
	parameters profileThickness = profileThickness
endif

if rm_RafterHeight < EPS then
	rm_RafterHeight = 0.001
	parameters rm_RafterHeight = rm_RafterHeight
endif

if GLOB_MODPAR_NAME = "profileWidthHalf" Then
	rm_ProfileWidth = profileWidthHalf*2
	parameters rm_ProfileWidth = rm_ProfileWidth
else
	profileWidthHalf = rm_ProfileWidth/2
	parameters profileWidthHalf = profileWidthHalf
endif

if ZZYZX >= A then
	ZZYZX = A-EPS
	parameters ZZYZX = ZZYZX
endif

if ZZYZX <= -A then
	ZZYZX = -A+EPS
	parameters ZZYZX = ZZYZX
endif

if GLOB_MODPAR_NAME = "ZZYZX" then
	tgyok = sqr(4*rm_ProfileHeight^4*ZZYZX^2 - 4*(rm_ProfileHeight^2-A^2)*(rm_ProfileHeight^2*A^2 + rm_ProfileHeight^2*ZZYZX^2))
	h11 = (2*rm_ProfileHeight^2*ZZYZX + tgyok) / (2*(rm_ProfileHeight^2-A^2))
	h12 = (2*rm_ProfileHeight^2*ZZYZX - tgyok) / (2*(rm_ProfileHeight^2-A^2))
	if abs(h12-rm_ProfileHeight) > abs(h11-rm_ProfileHeight) then
		_profileHeightZ	= h11
	else
		_profileHeightZ	= h12
	endif

	rm_HeightDiff	= ZZYZX - _profileHeightZ
	_angle			= atn(rm_HeightDiff / A)
	_strutLengthPlus= abs(rm_ProfileHeight/tan(90-_angle))
	parameters rm_HeightDiff = rm_HeightDiff
else
	ZZYZX = _profileHeightZ + abs(rm_HeightDiff)
	parameters ZZYZX = ZZYZX
endif

if GLOB_MODPAR_NAME = "rm_StructuralLength" then
	A = sqr((rm_StructuralLength-_strutLengthPlus)^2 - rm_HeightDiff^2)
	parameters A = A
else
	rm_StructuralLength = sqr(A^2 + rm_HeightDiff^2) + _strutLengthPlus
	parameters rm_StructuralLength = rm_StructuralLength
endif

values "profileWidthHalf"	range (0,)
values "rm_ProfileHeight"	range (0,)
values "profileHeight"		range (0,)

if GLOB_MODPAR_NAME = "profileHeight" then
	rm_ProfileHeight = profileHeight * cos(_angle)
	parameters rm_ProfileHeight = rm_ProfileHeight
else
	profileHeight = rm_ProfileHeight / cos(_angle)
	parameters profileHeight = profileHeight
endif

if not(rm_bTopOnRafter) & not(rm_bIsRotated) then
	hideparameter	"rm_RotAngle"
	lock			"rm_RotAngle"
endif

if not(rm_bTopOnRafter) then
	hideparameter	"rm_RafterHeight", "bIsPurlinRotated"
	lock			"rm_RafterHeight", "bIsPurlinRotated"
else
	hideparameter	"rm_bIsRotated"
	lock			"rm_bIsRotated"
	lock			"bIsPurlinRotated"
	parameters bIsPurlinRotated = 1
endif


! ==============================================================================
! Lumber Size
! ==============================================================================
lumberSize = rm_StructuralLength
parameters lumberSize = lumberSize

! ==============================================================================
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iObjectType				= 8

! ==============================================================================
! Bounding Box
! ==============================================================================
parameters B = B		: lock "B"

! ==============================================================================
! Levels
! ==============================================================================
parameters ac_toplevel = ac_toplevel, ac_bottomlevel = ac_bottomlevel
