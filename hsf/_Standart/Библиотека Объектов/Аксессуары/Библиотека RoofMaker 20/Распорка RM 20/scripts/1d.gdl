
EPS = 0.00001

bEnableRafter		= 0
bEnableSymbol		= 0
bEnableResolution	= 0


! ==============================================================================
! Definitions
! ==============================================================================
!gs_iDisplayStandards
	LANGUAGE_INT		= 1
	LANGUAGE_GER		= 2
	LANGUAGE_CHE		= 3
	LANGUAGE_AUT		= 4
	LANGUAGE_USA		= 5

!AC_symb_display_option
	DISPLAY_PROJECTED			= 1
	DISPLAY_OVERHEAD_PROJ		= 2
	DISPLAY_SYMBOLIC			= 3
	DISPLAY_OUTLINES			= 4
	DISPLAY_OVERHEAD_ALL		= 5

!AC_symb_show_projection_to
	PROJECTION_FLOOR_PLAN_RANGE	= 1
	PROJECTION_DISPLAY_LIMIT	= 2
	PROJECTION_ENTIRE_ELEMENT	= 3

dim stTrimmerPos[2]
	stTrimmerPos[1]	= `Перпендикулярно`
	stTrimmerPos[2]	= `Вертикально`

! rm_iTrimmerPos
	TRIMMER_PERDENDICULAR		= 1
	TRIMMER_VERTICAL			= 2


! ==============================================================================
! 2D Display Options
! ==============================================================================

dim stDisplayOptions[5]
	stDisplayOptions[1]		= `Сечение и Низ`
	stDisplayOptions[2]		= `Сечение и Верх`
	stDisplayOptions[3]		= `Символический Вид`
	stDisplayOptions[4]		= `Только Контуры`
	stDisplayOptions[5]		= `Вся Проекция`

dim stProjectionTypes[3]
	stProjectionTypes[1]	= `Согласно Диапазону Плана Этажа`
	stProjectionTypes[2]	= `Согласно Абсолютному Диапазону`
	stProjectionTypes[3]	= `Весь Элемент`

if GLOB_MODPAR_NAME = "gs_symb_display_option" | AC_symb_display_option = 0 then
	AC_symb_display_option = DISPLAY_PROJECTED
	if gs_symb_display_option = stDisplayOptions[2] then AC_symb_display_option = DISPLAY_OVERHEAD_PROJ
	if gs_symb_display_option = stDisplayOptions[3] then AC_symb_display_option = DISPLAY_SYMBOLIC
	if gs_symb_display_option = stDisplayOptions[4] then AC_symb_display_option = DISPLAY_OUTLINES
	if gs_symb_display_option = stDisplayOptions[5] then AC_symb_display_option = DISPLAY_OVERHEAD_ALL
	parameters AC_symb_display_option = AC_symb_display_option
else
	gs_symb_display_option = stDisplayOptions[min(max(1, AC_symb_display_option), vardim1(stDisplayOptions))]
	parameters gs_symb_display_option = gs_symb_display_option
endif

if GLOB_MODPAR_NAME = "gs_symb_show_projection_to" | AC_symb_show_projection_to = 0 then
	AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE
	if gs_symb_show_projection_to = stProjectionTypes[2] then AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT
	if gs_symb_show_projection_to = stProjectionTypes[3] then AC_symb_show_projection_to = PROJECTION_ENTIRE_ELEMENT
	parameters AC_symb_show_projection_to = AC_symb_show_projection_to
else
	gs_symb_show_projection_to = stProjectionTypes[min(max(1, AC_symb_show_projection_to), vardim1(stProjectionTypes))]
	parameters gs_symb_show_projection_to = gs_symb_show_projection_to
endif


! ==============================================================================
! Trimmer Position
! ==============================================================================
values "rm_TrimmerPos" stTrimmerPos
values "rm_iTrimmerPos" 1,2

if GLOB_MODPAR_NAME = "rm_TrimmerPos" then
	rm_iTrimmerPos = 1
	for i = 2 to vardim1(stTrimmerPos)
		if rm_TrimmerPos = stTrimmerPos[i] then
			rm_iTrimmerPos = i
			i = vardim1(stTrimmerPos)
		endif
	next i
	parameters rm_iTrimmerPos = rm_iTrimmerPos
else
	rm_iTrimmerPos = max(min(rm_iTrimmerPos,vardim1(stTrimmerPos)),1)
	parameters rm_TrimmerPos = stTrimmerPos[rm_iTrimmerPos]
endif

if GLOB_MODPAR_NAME = "profileHeightHalf" Then
	rm_ProfileHeight  = profileHeightHalf*2
	parameters rm_ProfileHeight = rm_ProfileHeight
else
	profileHeightHalf = rm_ProfileHeight/2
	parameters profileHeightHalf = profileHeightHalf
endif


! ==============================================================================
! Geometry Calculation
! ==============================================================================
_zPos			= rm_PivotDistX*tan(rm_PitchAngle)
_rafterHalfZ	= (rm_RafterHeight/2)/cos(rm_PitchAngle)
_centerPointX	= rm_PivotDistX
_centerPointY	= _zPos + _rafterHalfZ
_length			= rm_ProjectedLength-(rm_RafterWidth_2+rm_RafterWidth_1)/2

if rm_iTrimmerPos = TRIMMER_PERDENDICULAR then
	_pivotOffsetX	= sin(rm_PitchAngle) * (rm_ProfileHeight/2)
	_pivotOffsetY	= - cos(rm_PitchAngle) * (rm_ProfileHeight/2)
else
	_pivotOffsetX	= 0
	_pivotOffsetY	= - rm_ProfileHeight/2
endif


if GLOB_MODPAR_NAME = "pivotDistX" | GLOB_MODPAR_NAME = "pivotDistZ" then
	if GLOB_MODPAR_NAME = "pivotDistZ" then
		_centerPointY	= pivotDistZ- _pivotOffsetY
		if abs(tan(rm_PitchAngle)) > EPS then
			_centerPointX	= (_centerPointY - _rafterHalfZ)/tan(rm_PitchAngle)
		else
			_centerPointX	= 0
		endif
		rm_PivotDistX	= _centerPointX
		pivotDistX		= _centerPointX + _pivotOffsetX
		parameters pivotDistX = pivotDistX
	else
		rm_PivotDistX	= pivotDistX - _pivotOffsetX
		_zPos			= rm_PivotDistX*tan(rm_PitchAngle)
		_centerPointX	= rm_PivotDistX
		_centerPointY	= _zPos + _rafterHalfZ
		pivotDistZ		= _centerPointY + _pivotOffsetY
		parameters pivotDistZ = pivotDistZ
	endif

	parameters rm_PivotDistX = rm_PivotDistX
else
	pivotDistX	= _centerPointX + _pivotOffsetX
	pivotDistZ	= _centerPointY + _pivotOffsetY

	parameters pivotDistX = pivotDistX, pivotDistZ = pivotDistZ
endif



