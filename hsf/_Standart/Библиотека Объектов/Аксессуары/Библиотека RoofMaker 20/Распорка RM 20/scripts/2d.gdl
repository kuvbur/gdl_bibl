
pen gs_cont_pen
line_type gs_cont_line
fill gs_fill_type


! ==============================================================================
! Roofmaker Macro
! ==============================================================================
if SYMB_MIRRORED then
	rot2 180
	add2 -2 * pivotDistX, 0
endif
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										txt_BasePointX			= pivotDistX,
										txt_BasePointY			= 0,
										txt_BaseAngle			= 90,
				returned_parameters 	gs_cutplane_height,
										gs_top_range_height,
										gs_bottom_range_height,
										doCutplane

if SYMB_MIRRORED then del 2

! ==============================================================================
! Listing
! ==============================================================================
if GLOB_CONTEXT = 6 then
	project2{2} 9, 270, 3+32,
		gs_back_pen,0,0,0
	end
endif


! ==============================================================================
! 2D Hotspots
! ==============================================================================

! --- Width of Trimmer ------------------------------------------------------------------------
	if rm_iTrimmerPos = TRIMMER_PERDENDICULAR then
		dx1	=  (rm_ProfileWidth/2) * cos(rm_PitchAngle) + sin(rm_PitchAngle) * rm_ProfileHeight
		dx2	= (rm_ProfileWidth/2) * cos(rm_PitchAngle)

		unID = 1000
		hotspot2 pivotDistX+dx2,	rm_ProjectedLength/2-rm_RafterWidth_1/2,	unID : unID=unID+1
		hotspot2 pivotDistX-dx1,  	rm_ProjectedLength/2-rm_RafterWidth_1/2,	unID : unID=unID+1

		unID = 1100
		hotspot2 pivotDistX+dx2,	-rm_ProjectedLength/2+rm_RafterWidth_2/2,	unID : unID=unID+1
		hotspot2 pivotDistX-dx1,	-rm_ProjectedLength/2+rm_RafterWidth_2/2,	unID : unID=unID+1
	else
		unID = 1000
		add2 pivotDistX,rm_ProjectedLength/2-rm_RafterWidth_1/2
			hotspot2  0,				0, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
			hotspot2  profileWidthHalf,	0, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
			hotspot2 -1,				0, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

			hotspot2  0,				0, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
			hotspot2 -profileWidthHalf,	0, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
			hotspot2  1,				0, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
		del 1

		unID = 1100
		add2 pivotDistX,-rm_ProjectedLength/2+rm_RafterWidth_2/2
			hotspot2  0,				0, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
			hotspot2  profileWidthHalf,	0, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
			hotspot2 -1,				0, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

			hotspot2  0,				0, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
			hotspot2 -profileWidthHalf,	0, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
			hotspot2  1,				0, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
		del 1
	endif

! --- Y Dimension ------------------------------------------------------------------------
	unID = 3000
	hotspot2 0,  0,						unID, projectedLengthHalf, 1+128,	rm_ProjectedLength : unID=unID+1
	hotspot2 0,  projectedLengthHalf,	unID, projectedLengthHalf, 2,		rm_ProjectedLength : unID=unID+1
	hotspot2 0,- 1,						unID, projectedLengthHalf, 3,		rm_ProjectedLength : unID=unID+1

	hotspot2 0,  0,						unID, projectedLengthHalf, 1+128,	rm_ProjectedLength : unID=unID+1
	hotspot2 0, -projectedLengthHalf,	unID, projectedLengthHalf, 2,		rm_ProjectedLength : unID=unID+1
	hotspot2 0,  1,						unID, projectedLengthHalf, 3,		rm_ProjectedLength : unID=unID+1

! --- Thickness of Ending Rafter ------------------------------------------------------------------------
	unID = 8000
	hotspot2 pivotDistX, -rm_ProjectedLength/2,							unID, rafterWidth_2, 1+128,	rm_RafterWidth_2: unID=unID+1
	hotspot2 pivotDistX, -rm_ProjectedLength/2+rm_RafterWidth_2/2,		unID, rafterWidth_2, 2,		rm_RafterWidth_2: unID=unID+1
	hotspot2 pivotDistX, -rm_ProjectedLength/2-rm_RafterWidth_2/2-1,	unID, rafterWidth_2, 3,		rm_RafterWidth_2: unID=unID+1

! --- Thickness of Starting Rafter ------------------------------------------------------------------------
	unID = 8500
	hotspot2 pivotDistX, rm_ProjectedLength/2,						unID, rafterWidth_1, 1+128, rm_RafterWidth_1: unID=unID+1
	hotspot2 pivotDistX, rm_ProjectedLength/2-rm_RafterWidth_1/2,	unID, rafterWidth_1, 2,		rm_RafterWidth_1: unID=unID+1
	hotspot2 pivotDistX, rm_ProjectedLength/2+rm_RafterWidth_1/2+1,	unID, rafterWidth_1, 3,		rm_RafterWidth_1: unID=unID+1

! --- X Dimension ------------------------------------------------------------------------
	unID = 14000
	hotspot2  0,			0, unID, pivotDistX, 1+256	: unID=unID+1
	hotspot2  pivotDistX,	0, unID, pivotDistX, 2		: unID=unID+1
	hotspot2 -1,			0, unID, pivotDistX, 3		: unID=unID+1



! ==============================================================================
! Floor Plan View
! ==============================================================================

if AC_symb_display_option = DISPLAY_SYMBOLIC then
	! ----- Symbolic 2D --------------------------------------------------------
	if rm_iTrimmerPos = TRIMMER_PERDENDICULAR then
		pen gs_fill_pen
		fill gs_fill_type
		line_type gs_cont_line
		project2{2} 3, 270, 3+32, gs_back_pen,0,0,0

		pen gs_cont_pen
		project2 3, 270, 2
	else
		rect2 pivotDistX-rm_ProfileWidth/2, -rm_ProjectedLength/2+rm_RafterWidth_2/2, pivotDistX+rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2

		fill gs_fill_type
		poly2_b{2}	5, 2+64, gs_fill_pen, gs_back_pen,
					0,0,0,
					pivotDistX-rm_ProfileWidth/2, -rm_ProjectedLength/2+rm_RafterWidth_2/2,1,
					pivotDistX-rm_ProfileWidth/2,  rm_ProjectedLength/2-rm_RafterWidth_1/2,1,
					pivotDistX+rm_ProfileWidth/2,  rm_ProjectedLength/2-rm_RafterWidth_1/2,1,
					pivotDistX+rm_ProfileWidth/2, -rm_ProjectedLength/2+rm_RafterWidth_2/2,1,
					pivotDistX-rm_ProfileWidth/2, -rm_ProjectedLength/2+rm_RafterWidth_2/2,1
	endif
else

	! ----- View Edges ---------------------------------------------------------
	pen gs_cont_pen
	line_type gs_cont_line

	if AC_symb_display_option = DISPLAY_OVERHEAD_ALL then
		pen AC_overhead_pen
		line_type AC_overhead_linetype
	endif

	project2{3} 3,270,2 + 32 * (AC_symb_display_option = DISPLAY_OVERHEAD_ALL | not(gs_use_3D_view_attributes)), 8,
		parameters	gs_2D_projection		= doCutplane,
					gs_2D_projection_type	= 1,
					gs_cutplane_height		= gs_cutplane_height,
					gs_top_range_height		= gs_top_range_height,
					gs_bottom_range_height	= gs_bottom_range_height


	if AC_symb_display_option <> DISPLAY_OVERHEAD_ALL then
		! ----- View Polygons --------------------------------------------------
		pen gs_fill_pen
		fill gs_fill_type

		project2{3} 3,270,3+32+1024+2048, 4,
						gs_back_pen,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 2,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif


	if AC_symb_display_option = DISPLAY_OVERHEAD_PROJ then

		! ----- Overhead View -----------------------------------------------------
		pen AC_overhead_pen
		line_type AC_overhead_linetype

		project2{3} 3,270,2+32, 2+8,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 3,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif

	if doCutplane then

		! ----- Cut Lines ---------------------------------------------------------
		pen gs_cut_pen
		line_type AC_cut_linetype
		line_property 2

		project2{3} 3,270,2+16+32+256+4096, 2,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 4,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height


		! ----- Cut Polygons ------------------------------------------------------
		pen gs_cut_fill_pen
		fill gs_cut_fill_type

		project2{3} 3,270,3+32+512, 1,
						gs_cut_back_pen,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 5,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif
endif
