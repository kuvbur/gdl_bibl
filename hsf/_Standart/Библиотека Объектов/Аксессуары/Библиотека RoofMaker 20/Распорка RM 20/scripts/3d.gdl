
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================
_rotAngle	= 0
if rm_iTrimmerPos = TRIMMER_PERDENDICULAR then _rotAngle = -rm_PitchAngle

addx _centerPointX
addz _centerPointY
roty _rotAngle

! --- Thickness of Ending Rafter ------------------------------------------------------------------------
	unID = 1000
	hotspot rm_ProfileWidth/2, -rm_ProjectedLength/2,						-rm_ProfileHeight/2, unID, rafterWidth_2, 1+128,	rm_RafterWidth_2: unID=unID+1
	hotspot rm_ProfileWidth/2, -rm_ProjectedLength/2+rm_RafterWidth_2/2,	-rm_ProfileHeight/2, unID, rafterWidth_2, 2,		rm_RafterWidth_2: unID=unID+1
	hotspot rm_ProfileWidth/2, -rm_ProjectedLength/2-rm_RafterWidth_2/2-1,	-rm_ProfileHeight/2, unID, rafterWidth_2, 3,		rm_RafterWidth_2: unID=unID+1

	hotspot -rm_ProfileWidth/2, -rm_ProjectedLength/2,						-rm_ProfileHeight/2, unID, rafterWidth_2, 1+128,	rm_RafterWidth_2: unID=unID+1
	hotspot -rm_ProfileWidth/2, -rm_ProjectedLength/2+rm_RafterWidth_2/2,	-rm_ProfileHeight/2, unID, rafterWidth_2, 2,		rm_RafterWidth_2: unID=unID+1
	hotspot -rm_ProfileWidth/2, -rm_ProjectedLength/2-rm_RafterWidth_2/2-1,	-rm_ProfileHeight/2, unID, rafterWidth_2, 3,		rm_RafterWidth_2: unID=unID+1

! --- Thickness of Starting Rafter ------------------------------------------------------------------------
	unID = 1100
	hotspot -rm_ProfileWidth/2, rm_ProjectedLength/2,						-rm_ProfileHeight/2, unID,rafterWidth_1, 1+128,	rm_RafterWidth_1: unID=unID+1
	hotspot -rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2,	-rm_ProfileHeight/2, unID,rafterWidth_1, 2,		rm_RafterWidth_1: unID=unID+1
	hotspot -rm_ProfileWidth/2, rm_ProjectedLength/2+rm_RafterWidth_1/2+1,	-rm_ProfileHeight/2, unID,rafterWidth_1, 3,		rm_RafterWidth_1: unID=unID+1

	hotspot rm_ProfileWidth/2, rm_ProjectedLength/2,						-rm_ProfileHeight/2, unID,rafterWidth_1, 1+128,	rm_RafterWidth_1: unID=unID+1
	hotspot rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2,		-rm_ProfileHeight/2, unID,rafterWidth_1, 2,		rm_RafterWidth_1: unID=unID+1
	hotspot rm_ProfileWidth/2, rm_ProjectedLength/2+rm_RafterWidth_1/2+1,	-rm_ProfileHeight/2, unID,rafterWidth_1, 3,		rm_RafterWidth_1: unID=unID+1

! --- Height of Trimmer ------------------------------------------------------------------------
	unID = 2000
	hotspot rm_ProfileWidth/2,-rm_ProjectedLength/2+rm_RafterWidth_2/2,  0,					unID, profileHeightHalf, 1+128,	rm_ProfileHeight: unID=unID+1
	hotspot rm_ProfileWidth/2,-rm_ProjectedLength/2+rm_RafterWidth_2/2,  profileHeightHalf,	unID, profileHeightHalf, 2,		rm_ProfileHeight: unID=unID+1
	hotspot rm_ProfileWidth/2,-rm_ProjectedLength/2+rm_RafterWidth_2/2, -1,					unID, profileHeightHalf, 3,		rm_ProfileHeight: unID=unID+1

	hotspot -rm_ProfileWidth/2,-rm_ProjectedLength/2+rm_RafterWidth_2/2,  0,				unID, profileHeightHalf, 1+128,	rm_ProfileHeight: unID=unID+1
	hotspot -rm_ProfileWidth/2,-rm_ProjectedLength/2+rm_RafterWidth_2/2,  profileHeightHalf,unID, profileHeightHalf, 2,		rm_ProfileHeight: unID=unID+1
	hotspot -rm_ProfileWidth/2,-rm_ProjectedLength/2+rm_RafterWidth_2/2, -1,				unID, profileHeightHalf, 3,		rm_ProfileHeight: unID=unID+1

	unID = 2100
	hotspot rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2,  0,					unID, profileHeightHalf, 1+128,	rm_ProfileHeight: unID=unID+1
	hotspot rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2,  profileHeightHalf,	unID, profileHeightHalf, 2,		rm_ProfileHeight: unID=unID+1
	hotspot rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2, -1,					unID, profileHeightHalf, 3,		rm_ProfileHeight: unID=unID+1

	hotspot -rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2,  0,				unID, profileHeightHalf, 1+128,	rm_ProfileHeight: unID=unID+1
	hotspot -rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2,  profileHeightHalf,unID, profileHeightHalf, 2,		rm_ProfileHeight: unID=unID+1
	hotspot -rm_ProfileWidth/2, rm_ProjectedLength/2-rm_RafterWidth_1/2, -1,				unID, profileHeightHalf, 3,		rm_ProfileHeight: unID=unID+1

del 3

! --- Y Dimension ------------------------------------------------------------------------
	unID = 3000
	hotspot 0,  0,						0, unID, projectedLengthHalf, 1+128,	rm_ProjectedLength : unID=unID+1
	hotspot 0,  projectedLengthHalf,	0, unID, projectedLengthHalf, 2,		rm_ProjectedLength : unID=unID+1
	hotspot 0, -1,						0, unID, projectedLengthHalf, 3,		rm_ProjectedLength : unID=unID+1

	hotspot 0,  0,						0, unID, projectedLengthHalf, 1+128,	rm_ProjectedLength : unID=unID+1
	hotspot 0, -projectedLengthHalf,	0, unID, projectedLengthHalf, 2,		rm_ProjectedLength : unID=unID+1
	hotspot 0,  1,						0, unID, projectedLengthHalf, 3,		rm_ProjectedLength : unID=unID+1

! --- Z Dimension ------------------------------------------------------------------------
	unID = 4500
	hotspot pivotDistX, 0,  0,			unID, pivotDistZ, 1+128,	pivotDistZ : unID=unID+1
	hotspot pivotDistX, 0,  pivotDistZ,	unID, pivotDistZ, 2,		pivotDistZ : unID=unID+1
	hotspot pivotDistX, 0, -1,			unID, pivotDistZ, 3,		pivotDistZ : unID=unID+1

! --- X Dimension ------------------------------------------------------------------------
	unID = 14000
	hotspot  0,				0, pivotDistZ, unID, pivotDistX, 1+128	: unID=unID+1
	hotspot  pivotDistX,	0, pivotDistZ, unID, pivotDistX, 2		: unID=unID+1
	hotspot -1,				0, pivotDistZ, unID, pivotDistX, 3		: unID=unID+1


! ==============================================================================
! Roofmaker Element
! ==============================================================================

addx _centerPointX
addz _centerPointY
roty _rotAngle
addz -rm_ProfileHeight/2
addy -rm_ProjectedLength/2

base
	prism_ 5, rm_ProfileHeight,
		-rm_ProfileWidth/2, rm_RafterWidth_2/2, 15,
		-rm_ProfileWidth/2, rm_ProjectedLength-rm_RafterWidth_1/2, 15,
		 rm_ProfileWidth/2, rm_ProjectedLength-rm_RafterWidth_1/2, 15,
		 rm_ProfileWidth/2, rm_RafterWidth_2/2, 15,
		-rm_ProfileWidth/2, rm_RafterWidth_2/2, -1

	vert 0,0,0
	vert 0,1,0
	vert 0,0,1
	vert 1,0,0

	coor 2, -1, -2, -3, -4
body 1

del 5


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i

END
