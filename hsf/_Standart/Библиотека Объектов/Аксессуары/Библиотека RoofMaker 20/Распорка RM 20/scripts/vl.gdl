
! =================================================================================
! Trim parameters set by the RoofMaker Add-on:
! "rm_pivotDistX"			Horizontal Distance from Pivot Line
! "rm_ProjectedLength"		Distance of Rafters' Axes
! "rm_iTrimmerPos"			Trimmer Position
! "rm_RafterWidth_1"		Thickness of Ending Rafter
! "rm_RafterWidth_2"		Thickness of Starting Rafter
! "rm_RafterHeight"			Height of Rafters
! "rm_PitchAngle"			Pitch Angle
! "rm_ProfileWidth"			Profile Width
! "rm_ProfileHeight"		Profile Height
! =================================================================================

! ==============================================================================
! Geometry
! ==============================================================================
values "rm_RafterHeight"		range (0,)
values "rm_RafterWidth_1"		range (, (rm_ProjectedLength-rm_RafterWidth_2/2)*2)
values "rm_RafterWidth_2"		range (, (rm_ProjectedLength-rm_RafterWidth_1/2)*2)
values "rafterWidth_1"			range (, rm_ProjectedLength-rafterWidth_2)
values "rafterWidth_2"			range (, rm_ProjectedLength-rafterWidth_1)
values "rm_ProfileWidth"		range (0,)
values "profileWidthHalf"		range (0,)
values "rm_ProfileHeight"		range (0,)
values "profileHeightHalf"		range (0,)
values "rm_PitchAngle"			range [0, 90)
values "rm_pivotDistX"			range [0,)
values "pivotDistX"				range [0,)
values "pivotDistZ"				range [0,)
values "rm_ProjectedLength"		range (max(rm_RafterWidth_1/2 + rm_RafterWidth_2/2, 0),)
values "projectedLengthHalf"	range (max(rm_RafterWidth_1/2 + rm_RafterWidth_2/2, 0)/2,)
values "rm_StructuralLength"	range (0,)


if GLOB_MODPAR_NAME = "rafterWidth_1" then
	rm_RafterWidth_1  = rafterWidth_1*2
	parameters rm_RafterWidth_1 = rm_RafterWidth_1
else
	rafterWidth_1 = rm_RafterWidth_1/2
	parameters rafterWidth_1 = rafterWidth_1
endif

if GLOB_MODPAR_NAME = "rafterWidth_2" then
	rm_RafterWidth_2 = rafterWidth_2*2
	parameters rm_RafterWidth_2=rm_RafterWidth_2
else
	rafterWidth_2 = rm_RafterWidth_2/2
	parameters rafterWidth_2 = rafterWidth_2
endif

if GLOB_MODPAR_NAME = "profileWidthHalf" Then
	rm_ProfileWidth = profileWidthHalf*2
	parameters rm_ProfileWidth = rm_ProfileWidth
else
	profileWidthHalf = rm_ProfileWidth/2
	parameters profileWidthHalf = profileWidthHalf
endif

if GLOB_MODPAR_NAME = "projectedLengthHalf" then
	rm_ProjectedLength	= projectedLengthHalf*2
	_length				= rm_ProjectedLength-(rm_RafterWidth_2+rm_RafterWidth_1)/2
	rm_StructuralLength	= _length
	parameters rm_ProjectedLength = rm_ProjectedLength, rm_StructuralLength = rm_StructuralLength
else
	if GLOB_MODPAR_NAME = "rm_StructuralLength" then
		_length				= rm_StructuralLength
		rm_ProjectedLength	= rm_StructuralLength + (rm_RafterWidth_2+rm_RafterWidth_1)/2
		projectedLengthHalf = rm_ProjectedLength/2
		parameters projectedLengthHalf = projectedLengthHalf, rm_ProjectedLength = rm_ProjectedLength
	else
		projectedLengthHalf = rm_ProjectedLength/2
		rm_StructuralLength	= _length
		parameters projectedLengthHalf = projectedLengthHalf, rm_StructuralLength = rm_StructuralLength
	endif
endif

! ==============================================================================
! Lumber Size
! ==============================================================================
lumberSize = rm_StructuralLength
parameters lumberSize = lumberSize

! ==============================================================================
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iObjectType				= 15


! ==============================================================================
! Bounding Box
! ==============================================================================


if rm_iTrimmerPos = TRIMMER_PERDENDICULAR then
	ZZYZX	= (rm_ProfileWidth/2) * sin(rm_PitchAngle) * 2 + cos(rm_PitchAngle) * rm_ProfileHeight
	A		= (rm_ProfileWidth/2) * cos(rm_PitchAngle) * 2 + sin(rm_PitchAngle) * rm_ProfileHeight
else
	ZZYZX	= rm_ProfileHeight
	A		= rm_ProfileWidth
endif
parameters ZZYZX	= ZZYZX								: lock "ZZYZX"
parameters B		= _length							: lock "B"
parameters A		= A									: lock "A"


! ==============================================================================
! Levels
! ==============================================================================
if rm_iTrimmerPos = TRIMMER_PERDENDICULAR then
	ac_bottomlevel	= pivotDistZ - (rm_ProfileWidth/2) * sin(rm_PitchAngle)
	ac_toplevel		= pivotDistZ + (rm_ProfileWidth/2) * sin(rm_PitchAngle) + cos(rm_PitchAngle) * rm_ProfileHeight
else
	ac_bottomlevel	= pivotDistZ
	ac_toplevel		= pivotDistZ + rm_ProfileHeight
endif
parameters ac_toplevel 		= ac_toplevel
parameters ac_bottomlevel	= ac_bottomlevel
