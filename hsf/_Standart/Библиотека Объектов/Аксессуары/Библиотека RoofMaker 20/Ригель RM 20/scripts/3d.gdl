
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================

! --- Profile Height ------------------------------------------------------------
	unID = 2000
	add (_startX1-_rafterX1+rm_ProjectedLength-_startX2+_rafterX2)/2,0, rm_RelativeHeight
		hotspot 0, 0,  0,					unID, rm_ProfileHeight, 1+128	: unID=unID+1
		hotspot 0, 0,  1,					unID, rm_ProfileHeight, 3		: unID=unID+1
		hotspot 0, 0, -rm_ProfileHeight,	unID, rm_ProfileHeight, 2		: unID=unID+1
	del 1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	hotspot  0,						0,	0,	unID,	rm_ProjectedLength,	1+256	: unID=unID+1
	hotspot -1,						0,	0,	unID,	rm_ProjectedLength,	3		: unID=unID+1
	hotspot  rm_ProjectedLength,	0,	0,	unID,	rm_ProjectedLength,	2		: unID=unID+1

! --- Distance from Pivot Plane ------------------------------------------------------------
	unID = 4500
	addx (_startX1-_rafterX1+rm_ProjectedLength-_startX2+_rafterX2)/2
	hotspot 0, 0,  0,					unID,	rm_RelativeHeight,	1+128	: unID=unID+1
	hotspot 0, 0, -1,					unID,	rm_RelativeHeight,	3		: unID=unID+1
	hotspot 0, 0,  rm_RelativeHeight,	unID,	rm_RelativeHeight,	2		: unID=unID+1
	del 1

! --- 1. Roof Pitch ------------------------------------------------------------
	unID = 5000
	if abs(rm_RelativeHeight) < EPS then
		add _startX1-_rafterX1,0, rm_RelativeHeight-rm_ProfileHeight
			hotspot  0,			0, 0,					unID, rm_PitchAngle_1, 6		: unID=unID+1
			hotspot  1,			0, 0,					unID, rm_PitchAngle_1, 4+128	: unID=unID+1
			hotspot _swoopX1,	0, rm_ProfileHeight,	unID, rm_PitchAngle_1, 5		: unID=unID+1
		del 1
	else
		_tempL1 = rm_RelativeHeight / sin(rm_PitchAngle_1)
		add -_rafterX1,0, 0
			hotspot  0,								0,  0,								unID, rm_PitchAngle_1, 6		: unID=unID+1
			hotspot _tempL1,						0,  0,								unID, rm_PitchAngle_1, 4+128	: unID=unID+1
			hotspot _tempL1*cos(rm_PitchAngle_1),	0, _tempL1*sin(rm_PitchAngle_1),	unID, rm_PitchAngle_1, 5		: unID=unID+1
		del 1
	endif

! --- 2. Roof Pitch ------------------------------------------------------------
	unID = 5500
	if abs(rm_RelativeHeight) < EPS then
		add rm_ProjectedLength-_startX2+_rafterX2,0, rm_RelativeHeight-rm_ProfileHeight
			hotspot  0,			0, 0,					unID, rm_PitchAngle_2, 6		: unID=unID+1
			hotspot -1,			0, 0,					unID, rm_PitchAngle_2, 4+128	: unID=unID+1
			hotspot -_swoopX2,	0, rm_ProfileHeight,	unID, rm_PitchAngle_2, 5		: unID=unID+1
		del 1
	else
		_tempL2 = rm_RelativeHeight / sin(rm_PitchAngle_2)
		add rm_ProjectedLength+_rafterX2,0,0
			hotspot  0,								0,  0,								unID, rm_PitchAngle_2, 6		: unID=unID+1
			hotspot -_tempL2,						0,  0,								unID, rm_PitchAngle_2, 4+128	: unID=unID+1
			hotspot -_tempL2*cos(rm_PitchAngle_2),	0, _tempL2*sin(rm_PitchAngle_2),	unID, rm_PitchAngle_2, 5		: unID=unID+1
		del 1
	endif

! --- Height of First Rafter ------------------------------------------------------------
	unID = 9000
	add _startX1,0, rm_RelativeHeight-rm_ProfileHeight
		hotspot  0,						0, 0, unID,rafterHeight_1_Half, 1+128,	rm_RafterHeight_1 : unID=unID+1
		hotspot  1,						0, 0, unID,rafterHeight_1_Half, 3,		rm_RafterHeight_1 : unID=unID+1
		hotspot -rafterHeight_1_Half,	0, 0, unID,rafterHeight_1_Half, 2,		rm_RafterHeight_1 : unID=unID+1
	del 1

! --- Height of Second Rafter ------------------------------------------------------------
	unID = 9500
	add rm_ProjectedLength-_startX2,0, rm_RelativeHeight-rm_ProfileHeight
		hotspot  0,						0, 0, unID,rafterHeight_2_Half, 1+128,	rm_RafterHeight_2 : unID=unID+1
		hotspot -1,						0, 0, unID,rafterHeight_2_Half, 3,		rm_RafterHeight_2 : unID=unID+1
		hotspot  rafterHeight_2_Half,	0, 0, unID,rafterHeight_2_Half, 2,		rm_RafterHeight_2 : unID=unID+1
	del 1

! --- Profile Width ------------------------------------------------------------
	unID=1
	if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_LEFT then
		hotspot	_startX1-_rafterX1+_swoopX1, +rm_RafterWidth/2+rm_ProfileWidth,rm_RelativeHeight,unID	: unID=unID+1
		hotspot	rm_ProjectedLength-_startX2+_rafterX2-_swoopX2, +rm_RafterWidth/2+rm_ProfileWidth,rm_RelativeHeight,unID	: unID=unID+1
	endif
	if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_RIGHT then
		hotspot	rm_ProjectedLength-_startX2+_rafterX2-_swoopX2, -rm_RafterWidth/2-rm_ProfileWidth,rm_RelativeHeight,unID	: unID=unID+1
		hotspot	_startX1-_rafterX1+_swoopX1, -rm_RafterWidth/2-rm_ProfileWidth,rm_RelativeHeight,unID	: unID=unID+1
	endif

	unID = 1000
	add _startX1-_rafterX1, 0, rm_RelativeHeight-rm_ProfileHeight

	if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_LEFT then
		hotspot 0, rm_RafterWidth/2,					0, unID, rm_ProfileWidth, 1+128		: unID=unID+1
		hotspot 0, rm_RafterWidth/2-1,					0, unID, rm_ProfileWidth, 3			: unID=unID+1
		hotspot 0, rm_RafterWidth/2+rm_ProfileWidth,	0, unID, rm_ProfileWidth, 2			: unID=unID+1
	endif

	if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_RIGHT then
		hotspot 0, -rm_RafterWidth/2,					0, unID, rm_ProfileWidth, 1+128		: unID=unID+1
		hotspot 0, -rm_RafterWidth/2+1,					0, unID, rm_ProfileWidth, 3			: unID=unID+1
		hotspot 0, -rm_RafterWidth/2-rm_ProfileWidth,	0, unID, rm_ProfileWidth, 2			: unID=unID+1
	endif

	del 1

	unID = 1100
	add rm_ProjectedLength-_startX2+_rafterX2, 0, rm_RelativeHeight-rm_ProfileHeight

	if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_LEFT then
		hotspot 0, rm_RafterWidth/2,					0, unID, rm_ProfileWidth, 1+128		: unID=unID+1
		hotspot 0, rm_RafterWidth/2-1,					0, unID, rm_ProfileWidth, 3			: unID=unID+1
		hotspot 0, rm_RafterWidth/2+rm_ProfileWidth,	0, unID, rm_ProfileWidth, 2			: unID=unID+1
	endif

	if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_RIGHT then
		hotspot 0, -rm_RafterWidth/2,					0, unID, rm_ProfileWidth, 1+128		: unID=unID+1
		hotspot 0, -rm_RafterWidth/2+1,					0, unID, rm_ProfileWidth, 3			: unID=unID+1
		hotspot 0, -rm_RafterWidth/2-rm_ProfileWidth,	0, unID, rm_ProfileWidth, 2			: unID=unID+1
	endif

	del 1


! ==============================================================================
! Roofmaker Element
! ==============================================================================
rotx 90

if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_LEFT then
	addz -rm_ProfileWidth-rm_RafterWidth/2
	gosub "drawBeam"
	del 1
endif

if rm_iCollarType = TYPE_DOUBLE | rm_iCollarType = TYPE_RIGHT then
	addz rm_RafterWidth/2
	gosub "drawBeam"
	del 1
endif

del 1


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i

END

! ------------------------------------------------------------------------------
"drawBeam":
! ------------------------------------------------------------------------------
	base
		prism 5, rm_ProfileWidth,
				_startX1-_rafterX1, rm_RelativeHeight-rm_ProfileHeight,
				rm_ProjectedLength-_startX2+_rafterX2, rm_RelativeHeight-rm_ProfileHeight,
				rm_ProjectedLength-_startX2+_rafterX2-_swoopX2, rm_RelativeHeight,
				_startX1-_rafterX1+_swoopX1, rm_RelativeHeight,
				_startX1-_rafterX1, rm_RelativeHeight-rm_ProfileHeight

		vert 0, 0, 0

		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
	body 1
return
