
! =================================================================================
! Tie Beam parameters set by the RoofMaker add-on:
! "rm_ProjectedLength"		Structural Length of Span
! "rm_RelativeHeight"		Distance from Pivot Plane
! "rm_ProfileWidth"			Profile Width
! "rm_ProfileHeight"		Profile Height
! "rm_PitchAngle_1"			Pitch Angle 1
! "rm_PitchAngle_2"			Pitch Angle 2
! "rm_RafterWidth"			Rafter Width
! "rm_RafterHeight_1"		Height of First Rafter
! "rm_RafterHeight_2"		Height of Second Rafter
! "rm_iCollarType"			Collar Type
! =================================================================================


values "rm_PitchAngle_1"	range (0, 90]
values "rm_PitchAngle_2"	range (0, 90]
values "rm_ProfileWidth"	range (0,)
values "rm_ProfileHeight"	range (0,)

values "rm_collarType" stType
values "rm_iCollarType" 1,2,3

if GLOB_MODPAR_NAME = "rm_CollarType" then
	rm_iCollarType = 1
	for i = 2 to vardim1(stType)
		if rm_CollarType = stType[i] then
			rm_iCollarType = i
			i = vardim1(stType)
		endif
	next i
	parameters rm_iCollarType = rm_iCollarType
else
	rm_iCollarType = max(min(rm_iCollarType,vardim1(stType)),1)
	parameters rm_CollarType = stType[rm_iCollarType]
endif

values "rm_RafterWidth"		range (0,)
values "rafterWidthHalf"	range (0,)
if GLOB_MODPAR_NAME = "rafterWidthHalf" then
	rm_RafterWidth = rafterWidthHalf * 2
	parameters rm_RafterWidth = rm_RafterWidth
else
	rafterWidthHalf = rm_RafterWidth / 2
	parameters rafterWidthHalf = rafterWidthHalf
endif

values "rm_RafterHeight_1"		range (0,)
values "rafterHeight_1_Half"	range (0,)
if GLOB_MODPAR_NAME = "rafterHeight_1_Half" then
	rm_RafterHeight_1 = rafterHeight_1_Half * sin(rm_PitchAngle_1)
	parameters rm_RafterHeight_1 = rm_RafterHeight_1
else
	rafterHeight_1_Half = rm_RafterHeight_1 / sin(rm_PitchAngle_1)
	parameters rafterHeight_1_Half = rafterHeight_1_Half
endif

values "rm_RafterHeight_2"		range (0,)
values "rafterHeight_2_Half"	range (0,)
if GLOB_MODPAR_NAME = "rafterHeight_2_Half" then
	rm_RafterHeight_2 = rafterHeight_2_Half * sin(rm_PitchAngle_2)
	parameters rm_RafterHeight_2 = rm_RafterHeight_2
else
	rafterHeight_2_Half = rm_RafterHeight_2 / sin(rm_PitchAngle_2)
	parameters rafterHeight_2_Half = rafterHeight_2_Half
endif

if bValidHeight then
	values "rm_RelativeHeight"	range [0, _sectY]
else
	values "rm_RelativeHeight"	range [0, ]
endif
values "rm_StructuralLength"	range [_rafterX1 + _rafterX2 + _swoopX1 + _swoopX2,]
values "rm_ProjectedLength"		range [0,]		!_swoopX1 + _swoopX2
if GLOB_MODPAR_NAME = "rm_StructuralLength" then
	rm_ProjectedLength = rm_StructuralLength + _startX1 + _startX2 - _rafterX1 - _rafterX2
	parameters rm_ProjectedLength = rm_ProjectedLength
else
	rm_StructuralLength = rm_ProjectedLength - _startX1 + _rafterX1 - _startX2 + _rafterX2
	parameters rm_StructuralLength = rm_StructuralLength
endif


! ==============================================================================
! Lumber Size
! ==============================================================================
lumberSize = rm_StructuralLength
parameters lumberSize = lumberSize

! ==============================================================================
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iObjectType				= 3


! ==============================================================================
! Bounding Box
! ==============================================================================
parameters A		= rm_StructuralLength																					: lock "A"
parameters B		= rm_ProfileWidth*((rm_iCollarType = TYPE_DOUBLE)+1) + rm_RafterWidth*(rm_iCollarType = TYPE_DOUBLE)	: lock "B"
parameters ZZYZX	= rm_ProfileHeight																						: lock "ZZYZX"


! ==============================================================================
! Levels
! ==============================================================================
parameters ac_toplevel 		= rm_RelativeHeight
parameters ac_bottomlevel	= rm_RelativeHeight - rm_ProfileHeight
