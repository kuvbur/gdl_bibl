
pen gs_cont_pen
line_type gs_cont_line
fill gs_fill_type

! ==============================================================================
! Roofmaker Macro
! ==============================================================================

call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										txt_BasePointX			= 0,
										txt_BasePointY			= 0,
										txt_BaseAngle			= 0,
				returned_parameters 	gs_cutplane_height,
										gs_top_range_height,
										gs_bottom_range_height,
										doCutplane


! ==============================================================================
! Listing
! ==============================================================================
if GLOB_CONTEXT = 6 then
	project2 9, 270, 2
	end
endif


! ==============================================================================
! 2D Hotspots
! ==============================================================================
unID = 1

if AC_symb_display_option = DISPLAY_SYMBOLIC & rm_iSymbolType = SYMBOLTYPE_CIRCLE then
	hotspot2  2*rm_ProfileWidth, 0, unID : unID=unID+1
	hotspot2 -2*rm_ProfileWidth, 0, unID : unID=unID+1
	hotspot2 0, 2*rm_ProfileWidth,	unID : unID=unID+1
	hotspot2 0,-2*rm_ProfileWidth,	unID : unID=unID+1
endif

! --- Centre ------------------------------------------------------------------
	hotspot2 0, 0, 100

! --- Profile Width ------------------------------------------------------------

	unID = 1000
	hotspot2  0,				profileHeightHalf, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot2 -profileWidthHalf,	profileHeightHalf, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot2  1,				profileHeightHalf, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	hotspot2  0,				-profileHeightHalf, unID,profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot2 -profileWidthHalf,	-profileHeightHalf, unID,profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot2  1,				-profileHeightHalf, unID,profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	unID = 1100
	hotspot2  0,				profileHeightHalf, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot2  profileWidthHalf,	profileHeightHalf, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot2 -1,				profileHeightHalf, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	hotspot2  0,				-profileHeightHalf, unID,profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot2  profileWidthHalf,	-profileHeightHalf, unID,profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot2 -1,				-profileHeightHalf, unID,profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	unID = 1500
	hotspot2  0,				0, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot2  profileWidthHalf,	0, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot2 -1,				0, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

	hotspot2  0,				0, unID,profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
	hotspot2 -profileWidthHalf,	0, unID,profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
	hotspot2  1,				0, unID,profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

! --- Profile Height ------------------------------------------------------------

	unID = 2000
	hotspot2 -rm_ProfileWidth/2,  0,				unID, profileHeightHalf, 1+128, rm_ProfileHeight : unID=unID+1
	hotspot2 -rm_ProfileWidth/2,  profileHeightHalf,unID, profileHeightHalf, 2,		rm_ProfileHeight : unID=unID+1
	hotspot2 -rm_ProfileWidth/2, -1,				unID, profileHeightHalf, 3,		rm_ProfileHeight : unID=unID+1

	hotspot2 -rm_ProfileWidth/2,  0,				unID, profileHeightHalf, 1+128, rm_ProfileHeight : unID=unID+1
	hotspot2 -rm_ProfileWidth/2, -profileHeightHalf,unID, profileHeightHalf, 2,		rm_ProfileHeight : unID=unID+1
	hotspot2 -rm_ProfileWidth/2,  1,				unID, profileHeightHalf, 3,		rm_ProfileHeight : unID=unID+1

	unID = 2100
	hotspot2 rm_ProfileWidth/2,  0,					unID, profileHeightHalf, 1+128, rm_ProfileHeight : unID=unID+1
	hotspot2 rm_ProfileWidth/2,  profileHeightHalf,	unID, profileHeightHalf, 2,		rm_ProfileHeight : unID=unID+1
	hotspot2 rm_ProfileWidth/2, -1,					unID, profileHeightHalf, 3,		rm_ProfileHeight : unID=unID+1

	hotspot2 rm_ProfileWidth/2,  0,					unID, profileHeightHalf, 1+128, rm_ProfileHeight : unID=unID+1
	hotspot2 rm_ProfileWidth/2, -profileHeightHalf, unID, profileHeightHalf, 2,		rm_ProfileHeight : unID=unID+1
	hotspot2 rm_ProfileWidth/2,  1,					unID, profileHeightHalf, 3,		rm_ProfileHeight : unID=unID+1

	unID = 2500
	hotspot2 0,  0,					unID, profileHeightHalf, 1+128, rm_ProfileHeight : unID=unID+1
	hotspot2 0, -profileHeightHalf,	unID, profileHeightHalf, 2,		rm_ProfileHeight : unID=unID+1
	hotspot2 0,  1,					unID, profileHeightHalf, 3,		rm_ProfileHeight : unID=unID+1

	hotspot2 0,  0,					unID, profileHeightHalf, 1+128, rm_ProfileHeight : unID=unID+1
	hotspot2 0,  profileHeightHalf,	unID, profileHeightHalf, 2,		rm_ProfileHeight : unID=unID+1
	hotspot2 0, -1,					unID, profileHeightHalf, 3,		rm_ProfileHeight : unID=unID+1


! ==============================================================================
! Floor Plan View
! ==============================================================================

if AC_symb_display_option = DISPLAY_SYMBOLIC then

	! ----- Symbolic 2D
	pen gs_cont_pen
	line_type gs_cont_line

	add2 -rm_ProfileWidth/2, -rm_ProfileHeight/2
		rect2 0, 0,rm_ProfileWidth, rm_ProfileHeight

		fill gs_fill_type
		poly2_b   5, 2+32, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			rm_ProfileWidth, 0, 1,
			rm_ProfileWidth, rm_ProfileHeight, 1,
			0, rm_ProfileHeight, 1,
			0, 0, -1
	del 1

	if rm_iSymbolType = SYMBOLTYPE_CIRCLE then
		line2 -rm_ProfileWidth/2, -rm_ProfileHeight/2, rm_ProfileWidth/2,  rm_ProfileHeight/2
		line2 -rm_ProfileWidth/2,  rm_ProfileHeight/2, rm_ProfileWidth/2, -rm_ProfileHeight/2

		circle2 0, 0, 2*rm_ProfileWidth
	else
		pen gs_axis_line_pen
		line_type rm_AxisLineType
		line2 0, 2*rm_ProfileHeight, 0, -2*rm_ProfileHeight
		line2 2*rm_ProfileWidth, 0, -2*rm_ProfileWidth, 0
	endif

else

	! ----- View Edges ---------------------------------------------------------
	pen gs_cont_pen
	line_type gs_cont_line

	if AC_symb_display_option = DISPLAY_OVERHEAD_ALL then
		pen AC_overhead_pen
		line_type AC_overhead_linetype
	endif

	project2{3} 3,270,2 + 32 * (AC_symb_display_option = DISPLAY_OVERHEAD_ALL | not(gs_use_3D_view_attributes)), 8,
		parameters	gs_2D_projection		= doCutplane,
					gs_2D_projection_type	= 1,
					gs_cutplane_height		= gs_cutplane_height,
					gs_top_range_height		= gs_top_range_height,
					gs_bottom_range_height	= gs_bottom_range_height


	if AC_symb_display_option <> DISPLAY_OVERHEAD_ALL then
		! ----- View Polygons --------------------------------------------------
		pen gs_fill_pen
		fill gs_fill_type

		project2{3} 3,270,3+32+512+2048, 4,
						gs_back_pen,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 2,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif


	if AC_symb_display_option = DISPLAY_OVERHEAD_PROJ then

		! ----- Overhead View -----------------------------------------------------
		pen AC_overhead_pen
		line_type AC_overhead_linetype

		project2{3} 3,270,2+32, 2+8,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 3,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif

	if doCutplane then

		! ----- Cut Lines ---------------------------------------------------------
		pen gs_cut_pen
		line_type AC_cut_linetype
		line_property 2

		project2{3} 3,270,2+16+32+256+4096, 2,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 4,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height


		! ----- Cut Polygons ------------------------------------------------------
		pen gs_cut_fill_pen
		fill gs_cut_fill_type

		project2{3} 3,270,3+32+512, 1,
						gs_cut_back_pen,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 5,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif
endif

END


