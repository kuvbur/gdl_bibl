
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================

! --- Profile Width ------------------------------------------------------------
	unID = 1000
	if rm_iRoofEdgeAngle = EDGE_VERTICAL then
		hotspot -rm_OverHang,  0,				-rm_OverHang*tan(rm_PitchAngle), unID,  profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang, -profileWidthHalf,-rm_OverHang*tan(rm_PitchAngle), unID,	profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang,  1,				-rm_OverHang*tan(rm_PitchAngle), unID,  profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

		hotspot -rm_OverHang,  0,				-rm_OverHang*tan(rm_PitchAngle), unID,  profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang,  profileWidthHalf,-rm_OverHang*tan(rm_PitchAngle), unID,	profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang, -1,				-rm_OverHang*tan(rm_PitchAngle), unID,  profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
	endif

	if rm_iRoofEdgeAngle = EDGE_PERDENDICULAR then
		hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle),  0,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle), unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle), -profileWidthHalf,-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle), unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle),  1,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle), unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

		hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle),  0,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle), unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle),  profileWidthHalf,-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle), unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle), -1,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle), unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
	endif

	if rm_iRoofEdgeAngle = EDGE_RECTANGULAR then
		hotspot -rm_OverHang,  0,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang, -profileWidthHalf,-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang,  1,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

		hotspot -rm_OverHang,  0,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang,  profileWidthHalf,-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -rm_OverHang, -1,				-rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
	endif

! --- Profile Height ------------------------------------------------------------
	unID = 2000
	hotspot  0,										0,  0,									 unID,  rm_ProfileHeight, 1+128 : unID=unID+1
	hotspot -rm_ProfileHeight*sin(rm_PitchAngle),	0,  rm_ProfileHeight*cos(rm_PitchAngle), unID,	rm_ProfileHeight, 2		: unID=unID+1
	hotspot  rm_ProfileHeight*sin(rm_PitchAngle),	0, -rm_ProfileHeight*cos(rm_PitchAngle), unID,  rm_ProfileHeight, 3		: unID=unID+1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	!!roty -rm_PitchAngle
	!!	hotspot  0,					0, 0, unID, structuralLength, 1+256,	rm_ProjectedLength : unID=unID+1
	!!	hotspot -1,					0, 0, unID, structuralLength, 3,		rm_ProjectedLength : unID=unID+1
	!!	hotspot  structuralLength,	0, 0, unID, structuralLength, 2,		rm_ProjectedLength : unID=unID+1
	!del 1

	if rm_bIsTrimmerRotated then
		hotspot sin(rm_PitchAngle)*(rm_ProfileHeight/2),					0, (rm_ProjectedLength+sin(rm_PitchAngle)*(rm_ProfileHeight/2))*tan(rm_PitchAngle), unID, rm_ProjectedLength, 1+128	: unID=unID+1
		hotspot sin(rm_PitchAngle)*(rm_ProfileHeight/2)-1,					0, (rm_ProjectedLength+sin(rm_PitchAngle)*(rm_ProfileHeight/2))*tan(rm_PitchAngle), unID, rm_ProjectedLength, 3		: unID=unID+1
		hotspot rm_ProjectedLength+sin(rm_PitchAngle)*(rm_ProfileHeight/2),	0, (rm_ProjectedLength+sin(rm_PitchAngle)*(rm_ProfileHeight/2))*tan(rm_PitchAngle), unID, rm_ProjectedLength, 2		: unID=unID+1
	else
		hotspot  0,						0, rm_ProjectedLength*tan(rm_PitchAngle), unID, rm_ProjectedLength, 1+128	: unID=unID+1
		hotspot -1,						0, rm_ProjectedLength*tan(rm_PitchAngle), unID, rm_ProjectedLength, 3		: unID=unID+1
		hotspot  rm_ProjectedLength,	0, rm_ProjectedLength*tan(rm_PitchAngle), unID, rm_ProjectedLength, 2		: unID=unID+1
	endif

	hotspot  0,						0, 0, unID, rm_ProjectedLength, 1+256	: unID=unID+1
	hotspot -1,						0, 0, unID, rm_ProjectedLength, 3		: unID=unID+1
	hotspot  rm_ProjectedLength,	0, 0, unID, rm_ProjectedLength, 2+128	: unID=unID+1

	!hotspot  0,					0, rm_ProjectedLength*tan(rm_PitchAngle) + _profileHeightZ, unID, rm_ProjectedLength, 1+128	: unID=unID+1
	!hotspot -1,					0, rm_ProjectedLength*tan(rm_PitchAngle) + _profileHeightZ, unID, rm_ProjectedLength, 3		: unID=unID+1
	!hotspot  rm_ProjectedLength,	0, rm_ProjectedLength*tan(rm_PitchAngle) + _profileHeightZ, unID, rm_ProjectedLength, 2		: unID=unID+1

! --- Roof Pitch ------------------------------------------------------------------
	unID = 5000
	hotspot 0,						 0, 0,											unID, rm_PitchAngle, 6		: unID=unID+1
	hotspot rm_ProjectedLength/2,	 0, 0,											unID, rm_PitchAngle, 4+128	: unID=unID+1
	hotspot 0,						-1, 0,											unID, rm_PitchAngle, 7		: unID=unID+1
	hotspot rm_ProjectedLength/2,	 0, (rm_ProjectedLength/2)*tan(rm_PitchAngle),	unID, rm_PitchAngle, 5		: unID=unID+1

! --- Overhang ------------------------------------------------------------
	unID = 6000
	addz rm_ProfileHeight/cos (rm_PitchAngle)
	roty -rm_PitchAngle
		hotspot  0,			0, 0, unID, overHang, 1+128,	rm_OverHang : unID=unID+1
		hotspot  1,			0, 0, unID, overHang, 3,		rm_OverHang : unID=unID+1
		hotspot -overHang,	0, 0, unID, overHang, 2,		rm_OverHang : unID=unID+1
	del 2


! --- Soffit Angle ------------------------------------------------------------
	unID = 12000
	if rm_bSoffit then
		if rm_iRoofEdgeAngle = EDGE_VERTICAL then
			hotspot -rm_OverHang,	0, -rm_OverHang*tan(rm_PitchAngle),									unID,rm_SoffitAngle,6		: unID=unID+1
			hotspot  0,				0, -rm_OverHang*tan(rm_PitchAngle),									unID,rm_SoffitAngle,4+128	: unID=unID+1
			hotspot -rm_OverHang,	1, -rm_OverHang*tan(rm_PitchAngle),									unID,rm_SoffitAngle,7		: unID=unID+1
			hotspot  0,				0, -rm_OverHang*tan(rm_PitchAngle)-rm_OverHang*tan(rm_SoffitAngle),	unID,rm_SoffitAngle,5		: unID=unID+1
		endif

		if rm_iRoofEdgeAngle = EDGE_PERDENDICULAR then
			hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle),	0, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle),											unID,rm_SoffitAngle,6	: unID=unID+1
			hotspot  0,													0, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle),											unID,rm_SoffitAngle,4+128	: unID=unID+1
			hotspot -rm_OverHang+rm_ProfileHeight*sin(rm_PitchAngle),	1, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle),											unID,rm_SoffitAngle,7	: unID=unID+1
			hotspot  0,													0, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(rm_PitchAngle)-(rm_OverHang-rm_ProfileHeight*sin(rm_PitchAngle))*tan(rm_SoffitAngle),	unID,rm_SoffitAngle,5	: unID=unID+1
		endif

		if rm_iRoofEdgeAngle = EDGE_RECTANGULAR then
			hotspot -rm_OverHang,	0, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2,																unID,rm_SoffitAngle,6	: unID=unID+1
			hotspot  0,				0, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2,																unID,rm_SoffitAngle,4+128	: unID=unID+1
			hotspot -rm_OverHang,	1, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2,																unID,rm_SoffitAngle,7	: unID=unID+1
			hotspot  0,				0, -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2-(rm_OverHang-_profileHeightZ/2/tan(rm_PitchAngle))*tan(rm_SoffitAngle),	unID,rm_SoffitAngle,5	: unID=unID+1
		endif
	endif


! ==============================================================================
! Roofmaker Element
! ==============================================================================

if rm_iProfileType = PROFILE_TYPE_RECTANGULAR then
	put	-rm_ProfileWidth/2, -rm_ProfileHeight/2, 0,
		 rm_ProfileWidth/2, -rm_ProfileHeight/2, 0,
		 rm_ProfileWidth/2,  rm_ProfileHeight/2, 0,
		-rm_ProfileWidth/2,  rm_ProfileHeight/2, 0,
		-rm_ProfileWidth/2, -rm_ProfileHeight/2, 0
else
	if rm_iProfileType = PROFILE_TYPE_I_BEAM then
		put	-rm_ProfileWidth/2, -rm_ProfileHeight/2, 0,
			 rm_ProfileWidth/2, -rm_ProfileHeight/2, 0,
			 rm_ProfileWidth/2, -rm_ProfileHeight/2+profileThickness, 0,
			 profileThickness/2, -rm_ProfileHeight/2+profileThickness, 0,
			 profileThickness/2, rm_ProfileHeight/2-profileThickness, 0,
			 rm_ProfileWidth/2, rm_ProfileHeight/2-profileThickness, 0,
			 rm_ProfileWidth/2, rm_ProfileHeight/2, 0,
			-rm_ProfileWidth/2, rm_ProfileHeight/2, 0,
			-rm_ProfileWidth/2, rm_ProfileHeight/2-profileThickness, 0,
			-profileThickness/2, rm_ProfileHeight/2-profileThickness, 0,
			-profileThickness/2, -rm_ProfileHeight/2+profileThickness, 0,
			-rm_ProfileWidth/2, -rm_ProfileHeight/2+profileThickness, 0,
			-rm_ProfileWidth/2, -rm_ProfileHeight/2, 0
	endif

	if rm_iProfileType = PROFILE_TYPE_L_BEAM then
		put	-rm_ProfileWidth/2, rm_ProfileHeight/2, 0,
			 rm_ProfileWidth/2, rm_ProfileHeight/2, 0,
			 rm_ProfileWidth/2, rm_ProfileHeight/2-profileThickness, 0,
			-rm_ProfileWidth/2+profileThickness, rm_ProfileHeight/2-profileThickness, 0,
			-rm_ProfileWidth/2+profileThickness, -rm_ProfileHeight/2, 0,
			-rm_ProfileWidth/2, -rm_ProfileHeight/2, 0,
			-rm_ProfileWidth/2, rm_ProfileHeight/2, 0
	endif

	if rm_iProfileType = PROFILE_TYPE_C_BEAM then
		put	-rm_ProfileWidth/2, -rm_ProfileHeight/2, 0,
			 rm_ProfileWidth/2, -rm_ProfileHeight/2, 0,
			 rm_ProfileWidth/2, -rm_ProfileHeight/2+profileThickness, 0,
			-rm_ProfileWidth/2+profileThickness, -rm_ProfileHeight/2+profileThickness, 0,
			-rm_ProfileWidth/2+profileThickness, rm_ProfileHeight/2-profileThickness, 0,
			 rm_ProfileWidth/2, rm_ProfileHeight/2-profileThickness, 0,
			 rm_ProfileWidth/2, rm_ProfileHeight/2, 0,
			-rm_ProfileWidth/2, rm_ProfileHeight/2, 0,
			-rm_ProfileWidth/2, -rm_ProfileHeight/2, 0
	endif
endif


_bottomCutAngle		= 0
_topCutAngle		= 0
_nCut				= 0
if rm_bIsTrimmerRotated then _topCutAngle = rm_PitchAngle
if rm_iRoofEdgeAngle = EDGE_PERDENDICULAR	then _bottomCutAngle = 90-rm_PitchAngle
if rm_iRoofEdgeAngle = EDGE_VERTICAL		then _bottomCutAngle = 90


! --- Bottom Cut ------------------------------------------------------------------
	if rm_iRoofEdgeAngle = EDGE_RECTANGULAR then
		addy rm_ProfileWidth/2
		rotx 90
		addx -rm_OverHang
		addy -rm_OverHang*tan(rm_PitchAngle)+_profileHeightZ/2
		rotz 0
		muly -1
		cutplane 1, 0, 1           !Bottom Cut1
		_nCut = _nCut + 1
		del 4

		addx -rm_OverHang
		cutplane 0, 1, 1, 1         !Bottom Cut2
		_nCut = _nCut + 1
		del 3
	else
		addy rm_ProfileWidth/2
		rotx 90
		addx -rm_OverHang
		addy -rm_OverHang*tan(rm_PitchAngle)+rm_ProfileHeight/cos(rm_PitchAngle)
		rotz -_bottomCutAngle
		muly -1
		cutplane 1, 0, 1
		_nCut = _nCut + 1
		del 6
	endif


! --- Top Cut ------------------------------------------------------------------
	addy rm_ProfileWidth/2
	rotx 90
	addx rm_ProjectedLength
	addy rm_ProjectedLength*tan(rm_PitchAngle)+rm_ProfileHeight/2/cos(rm_PitchAngle)
	addz rm_ProfileWidth/2
	rotz _topCutAngle
	addx -(rm_JointWidth/2)/sin(rm_VerticalTrimmerAngle)
	roty rm_VerticalTrimmerAngle-90
	cutplane 0, 1, 1
	_nCut = _nCut + 1
	del 8


! --- 3D Body ------------------------------------------------------------------

	rotz -90
	rotx rm_PitchAngle-90
	addz -rm_OverHang/cos(rm_PitchAngle)-1
	addy -rm_ProfileHeight/2

	base
		if rm_bSoffit then
			extrude NSP/3, 0, 0, rm_ProjectedLength/cos(rm_PitchAngle)+rm_OverHang/cos(rm_PitchAngle)+3, 1+2+4+16+32,
					use(NSP)
		else
			extrude NSP/3, 0, 0, rm_ProjectedLength/cos(rm_PitchAngle)+rm_OverHang/cos(rm_PitchAngle)+3, 1+2+4+16+32,
					get(NSP)
		endif

		vert 0,0,0
		vert 0,0,rm_ProjectedLength
		vert 0,rm_ProjectedLength,0
		vert 1,0,0

		coor 2, -1,-2,-3,-4
	body 1

	for i = 1 to _nCut
		cutend
	next i

	del 4


! --- Soffit ------------------------------------------------------------------
	if rm_bSoffit then
		Y	= 0
		X	= 0

		if rm_iRoofEdgeAngle = EDGE_PERDENDICULAR then
			X	= cos(rm_PitchAngle)*tan(rm_PitchAngle)*rm_ProfileHeight
			Y	= sin(rm_PitchAngle)*tan(rm_PitchAngle)*rm_ProfileHeight
		endif

		if rm_iRoofEdgeAngle = EDGE_RECTANGULAR then
			X	= _profileHeightZ/2/tan(rm_PitchAngle)
			Y	= _profileHeightZ/2
		endif


		add -rm_OverHang+X, 0, -rm_OverHang*tan(rm_PitchAngle)+Y
		roty 90-rm_PitchAngle
		cutplane 0, 1, 1, 1         !Cut at rafter
		del 2
		cutplane 0, 1, 1


		add -rm_OverHang+X, 0, -rm_OverHang*tan(rm_PitchAngle)+Y
		rotz -90
		rotx 90-rm_SoffitAngle
		addy rm_ProfileHeight/2
		muly -1
		base
			extrude NSP/3, 0, 0, -rm_OverHang/cos(rm_SoffitAngle), 1+2+4+16+32,
					get(NSP)

			vert 0,0,0
			vert 0,0,rm_ProjectedLength
			vert 0,rm_ProjectedLength,0
			vert 1,0,0

			coor 2, -1,-2,-3,-4
		body 1

		del 5

		cutend
		cutend

	endif


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i

END





