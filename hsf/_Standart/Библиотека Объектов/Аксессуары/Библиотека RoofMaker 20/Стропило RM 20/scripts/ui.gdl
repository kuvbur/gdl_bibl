
! =============================================================================
! Localization
! =============================================================================
dim stGeometry[13]
	stGeometry[1]	= `Угол Наклона`
	stGeometry[2]	= `Конструктивная Длина Пролета`
	stGeometry[3]	= `Длина Элемента`
	stGeometry[4]	= `Ширина Профиля`
	stGeometry[5]	= `Высота Профиля`
	stGeometry[6]	= `Горизонтальная Проекция`
	stGeometry[7]	= `Относительная Высота Стропил`
	stGeometry[8]	= `Софит`
	stGeometry[9]	= `Угол Софита`
	stGeometry[10]	= `Свес`
	stGeometry[11]	= `Профиль`
	stGeometry[12]	= `Толщина Профиля`
	stGeometry[13]	= `Угол Торца Крыши`

dim stCutting[5]
	stCutting[1]	= `Распорка`
	stCutting[2]	= `Конек или Распорка Повернуты`
	stCutting[3]	= `Толщина Соединительного Элемента`
	stCutting[4]	= `Углы Соединений`
	stCutting[5]	= `Угол Соединения с Элементом`

! =============================================================================
! Variables
! =============================================================================

pageStartY		= 5
xInfieldSmall	= 70
xInfieldBig		= 90
yInfield		= 19
yOutfield		= 16

! =============================================================================
! Tabpages
! =============================================================================

TABID_ROOT					= -1
TABID_GEOMETRY				= 1
TABID_CUTTING				= 2

call "RoofMaker_macro"	parameters all	iObjectType				= 9,
										bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iUiType					= 1

ui_page TABID_GEOMETRY, TABID_ROOT, `Геометрия`, "uiTab_Dimension_flat(1)"
if gs_ui_current_page = TABID_GEOMETRY then
	gosub "geometryPage"
endif

ui_page TABID_CUTTING, TABID_ROOT, `Подрезка`, "uiTab_RMCut_flat(1)"
if gs_ui_current_page = TABID_CUTTING then
	gosub "cuttingPage"
endif

! ======================================================================
end
! ======================================================================

"geometryPage":
! =============================================================================
! Geometry
! =============================================================================

	picX			= 340
	picY			= pageStartY + 20

	if rm_bSoffit then
		ui_pict 7,	picX-245, picY+69, 222,  111, 1									!rafter_pict7.png
	else
		ui_pict 4,	picX-245, picY+69, 222,  111, 1									!rafter_pict4.png
	endif
	ui_pict 5,	picX-147, picY-3, 124, 72, 1										!rafter_pict5.png
	ui_pict 6,	picX-22, picY-12, 32, 39, 1											!rafter_pict6.png

	ui_pict 10, picX-15, picY+99, 24, 17, 1											!ui_rm_angle_1.png
	ui_infield "rm_PitchAngle", picX+12, picY+96, xInfieldBig, yInfield				ui_tooltip stGeometry[1]

	ui_infield "rm_ProjectedLength", picX-20, picY+177, xInfieldBig, yInfield		ui_tooltip stGeometry[2]
	ui_infield "rm_structuralLength", picX-338, picY+107, xInfieldBig, yInfield		ui_tooltip stGeometry[3]
	ui_infield "rm_ProfileWidth", picX+12, picY+16, xInfieldBig, yInfield			ui_tooltip stGeometry[4]
	ui_infield "rm_ProfileHeight", picX+12, picY-15, xInfieldBig, yInfield			ui_tooltip stGeometry[5]
	!ui_infield "A", picX-154, picY+189, xInfieldBig, yInfield						ui_tooltip stGeometry[6]
	ui_infield "rm_Height", picX-20, picY+62, xInfieldBig, yInfield					ui_tooltip stGeometry[7]

	ui_infield{3} "rm_bSoffit", picX-338, picY+130, xInfieldBig, yInfield,
		7, "", 0, 0,
		0,0,0,0,
		0, stGeometry[8],	0,
		0, stGeometry[8],	1	ui_tooltip stGeometry[8]

	ui_infield "rm_SoffitAngle", picX-338, picY+154, xInfieldBig, yInfield			ui_tooltip stGeometry[9]
	ui_infield "rm_OverHang", picX-338, picY+177, xInfieldBig, yInfield				ui_tooltip stGeometry[10]


	ui_outfield stGeometry[11], 4, pageStartY - 1, 114, yOutfield
	ui_infield{3} "rm_iProfileType", 119, pageStartY, 55, 30,
		2, 12, 4, 1,																!rm_profile2.png
		51,19,51,19,
		1, stProfileTypes[1],	1,
		2, stProfileTypes[2],	2,
		3, stProfileTypes[3],	3,
		4, stProfileTypes[4],	4 ui_tooltip stGeometry[11]

	ui_outfield stGeometry[12], 4, pageStartY + 39, 114, yOutfield, 4 * (rm_iProfileType = PROFILE_TYPE_RECTANGULAR)
	ui_infield "profileThickness", 120, pageStartY + 33, 53, yInfield						ui_tooltip stGeometry[12]

	ui_outfield stGeometry[13], 4, pageStartY + 64, 114, yOutfield
	ui_infield{3} "rm_iRoofEdgeAngle", 119, pageStartY + 58, 55, 30,
		2, 11, 3, 1,																!pict_roof edge_angle_1.png
		44,25,44,25,
		1, stEdgeType[1],	1,
		2, stEdgeType[2],	2,
		3, stEdgeType[3],	3 ui_tooltip stGeometry[13]

return

"cuttingPage":
! =============================================================================
! Cutting
! =============================================================================

	! -------- Trimmer ----------------------------------------------------------------------------------

	picX1	= 8
	picY1	= pageStartY + 77

	ui_style 0, 1
	ui_outfield stCutting[1], 10, pageStartY, 338-172, yOutfield
	ui_style 0, 0

	ui_infield{3} "rm_bIsTrimmerRotated", 12, pageStartY + 40, 200, yInfield * 1.5,
		7, "", 0, 0,
		0,0,0,0,
		0, stCutting[2],	0,
		0, stCutting[2],	1	ui_tooltip stCutting[2]

	if rm_bIsTrimmerRotated then
		ui_pict 3,	picX1, picY1, 86, 87, 1											!rafter_pict3.png
		ui_outfield stCutting[3], picX1+95, picY1+25, 260-picX1-95, yOutfield
		ui_infield "rm_JointWidth", picX1+95, picY1+46, xInfieldBig, yInfield		ui_tooltip stCutting[3]
	else
		ui_pict 2,	picX1, picY1-5, 79, 92, 1										!rafter_pict3.png
		ui_outfield stCutting[3], picX1+95, picY1+25, 260-picX1-95, yOutfield
		ui_infield "rm_JointWidth", picX1+95, picY1+46, xInfieldBig, yInfield		ui_tooltip stCutting[3]
	endif

	ui_separator 263, pageStartY, 263, pageStartY + 230


	! -------- Joining Angles ----------------------------------------------------------------------------------

	ui_style 0, 1
	ui_outfield stCutting[4], 271, pageStartY, 444-271, yOutfield
	ui_style 0, 0

	ui_pict 1,	382, pageStartY + 77, 41, 95, 1													!rafter_pict1.png

	ui_outfield stCutting[5], 282, pageStartY + 60, 444-282, yOutfield
	ui_infield "rm_VerticalTrimmerAngle", 282, pageStartY + 81, xInfieldSmall, yInfield			ui_tooltip stCutting[5]
return
