
! =================================================================================
! Rafter parameters set by the RoofMaker Add-on:
! "rm_ProjectedLength"			Horizontal Projection
! "rm_ProfileWidth"				Profile Width
! "rm_ProfileHeight"			Profile Height
! "rm_Height"					Relative Height of Rafter
! "rm_bRafterVisible"			Rafter Visible in 2D
! "rm_AxisLineType"				Axis Line Type
! "rm_OverHang"					Overhang
! "rm_PitchAngle"				Pitch Angle
! "rm_rm_iProfileType"			Profile
! "rm_iRoofEdgeAngle"			Roof Edge Angle
! "rm_bSoffit"					Soffit
! "rm_SoffitAngle"				Angle of Soffit
! "rm_JointWidth"				Thickness of Joining Element
! "rm_bIsTrimmerRotated"		Ridge or Trimmer Rotated
! "rm_VerticalTrimmerAngle"		Angle to Joining Element
! Parameters not by the RoofMaker Add-on:
! "rm_CutEndPointDist"
! =================================================================================

! ==============================================================================
! Profile
! ==============================================================================

values "rm_profileType" stProfileTypes
values "rm_iProfileType" 1,2,3,4

if GLOB_MODPAR_NAME = "rm_profileType" then
	rm_iProfileType = PROFILE_TYPE_RECTANGULAR
	if rm_profileType = stProfileTypes[2] then rm_iProfileType = PROFILE_TYPE_I_BEAM
	if rm_profileType = stProfileTypes[3] then rm_iProfileType = PROFILE_TYPE_L_BEAM
	if rm_profileType = stProfileTypes[4] then rm_iProfileType = PROFILE_TYPE_C_BEAM
	parameters rm_iProfileType = rm_iProfileType
else
	rm_profileType = stProfileTypes[min(max(1, rm_iProfileType), vardim1(stProfileTypes))]
	parameters rm_profileType = rm_profileType
endif

if rm_iProfileType = PROFILE_TYPE_RECTANGULAR then
	hideparameter	"profileThickness"
	lock			"profileThickness"
endif

if profileThickness < EPS then
	profileThickness = 0.001
	parameters profileThickness = profileThickness
endif

values "rm_RoofEdgeAngle" stEdgeType
values "rm_iRoofEdgeAngle" 1,2,3

if GLOB_MODPAR_NAME = "rm_RoofEdgeAngle" then
	rm_iRoofEdgeAngle = 1
	for i = 2 to vardim1(stEdgeType)
		if rm_RoofEdgeAngle = stEdgeType[i] then
			rm_iRoofEdgeAngle = i
			i = vardim1(stEdgeType)
		endif
	next i
	parameters rm_iRoofEdgeAngle = rm_iRoofEdgeAngle
else
	rm_iRoofEdgeAngle = max(min(rm_iRoofEdgeAngle,vardim1(stEdgeType)),1)
	parameters rm_RoofEdgeAngle = stEdgeType[rm_iRoofEdgeAngle]
endif

! ==============================================================================
! Geometry
! ==============================================================================
values "rm_PitchAngle"				range [0, 90)
values "rm_VerticalTrimmerAngle"	range (0,180)
values "profileWidthHalf"			range (0, )
values "rm_ProjectedLength"			range (0, )
values "rm_ProfileHeight"			range (0,)
values "rm_Overhang"				range [0,)		!-rm_ProjectedLength
values "overhang"					range [0,)
values "rm_SoffitAngle"				range [0,90)
values "structuralLength"			range (0,)

if rm_ProfileWidth < EPS then
	rm_ProfileWidth = 0.001
	parameters rm_ProfileWidth = rm_ProfileWidth
endif

if rm_JointWidth < 0 then
	rm_JointWidth = 0
	parameters rm_JointWidth = rm_JointWidth
endif

if GLOB_MODPAR_NAME = "profileWidthHalf" Then
	rm_ProfileWidth = profileWidthHalf*2
	parameters rm_ProfileWidth = rm_ProfileWidth
else
	profileWidthHalf = rm_ProfileWidth/2
	parameters profileWidthHalf = profileWidthHalf
endif

if GLOB_MODPAR_NAME="overHang" then
	rm_OverHang = overHang*cos(rm_PitchAngle)
	parameters rm_OverHang = rm_OverHang
else
	overHang = rm_OverHang/cos(rm_PitchAngle)
	parameters overHang = overHang
endif

if rm_StructuralLength < topStrutSize + overhangStrutSize then
	rm_StructuralLength = topStrutSize + overhangStrutSize
	parameters rm_StructuralLength = rm_StructuralLength
endif

if GLOB_MODPAR_NAME = "structuralLength" then
	rm_ProjectedLength	= structuralLength*cos(rm_PitchAngle)
	rm_Height			= structuralLength*sin(rm_PitchAngle)
	rm_StructuralLength	= structuralLength - topStrutSize + overhangStrutSize
	parameters rm_ProjectedLength = rm_ProjectedLength, rm_Height = rm_Height, rm_StructuralLength = rm_StructuralLength
else
	if GLOB_MODPAR_NAME = "rm_StructuralLength" then
		structuralLength	= rm_structuralLength + topStrutSize - overhangStrutSize
		rm_ProjectedLength	= structuralLength*cos(rm_PitchAngle)
		rm_Height			= structuralLength*sin(rm_PitchAngle)
		parameters rm_ProjectedLength = rm_ProjectedLength, rm_Height = rm_Height, structuralLength = structuralLength
	else
		if GLOB_MODPAR_NAME = "rm_Height" then
			structuralLength	= sqr(rm_ProjectedLength^2+rm_Height^2)
			rm_PitchAngle		= atn(rm_Height/rm_ProjectedLength)
			gosub "calcTopCut"
			gosub "calcOverhangStrutSize"
			rm_StructuralLength	= structuralLength - topStrutSize + overhangStrutSize
			parameters structuralLength = structuralLength, rm_PitchAngle = rm_PitchAngle,rm_StructuralLength = rm_StructuralLength
		else
			! "rm_ProjectedLength" is the strongest parameter
			structuralLength	= rm_ProjectedLength/cos(rm_PitchAngle)
			rm_Height			= rm_ProjectedLength*tan(rm_PitchAngle)
			gosub "calcTopCut"
			gosub "calcOverhangStrutSize"
			rm_StructuralLength	= structuralLength - topStrutSize + overhangStrutSize
			parameters structuralLength = structuralLength, rm_Height = rm_Height, rm_StructuralLength = rm_StructuralLength
		endif
	endif
endif


if not(rm_bSoffit) then lock "bAddToLumberSize", "rm_SoffitAngle"


! ==============================================================================
! Lumber Size
! ==============================================================================
gosub "calcTopCut"
gosub "calcOverhangStrutSize"
gosub "calcSoffitSize"
lumberSize = structuralLength - topStrutSize + overhangStrutSize + soffitSize * bAddToLumberSize
parameters lumberSize = lumberSize

! ==============================================================================
call "RoofMaker_macro"	parameters all	bEnableRafter			= bEnableRafter,
										bEnableSymbol			= bEnableSymbol,
										bEnableResolution		= bEnableResolution,
										gs_iDisplayStandards	= gs_iDisplayStandards,
										iObjectType				= 9

! ==============================================================================
! Bounding Box
! ==============================================================================
_underHeight = 0
if rm_iRoofEdgeAngle = EDGE_RECTANGULAR then
	_underHeight = sin(rm_PitchAngle) * overhangStrutSize - cos(rm_PitchAngle) * rm_ProfileHeight/2
else
	_underHeight = sin(rm_PitchAngle) * overhangStrutSize
endif

_underSoffit = 0
if rm_bSoffit then
	_underSoffit = sin(rm_SoffitAngle) * soffitSize
	if _underSoffit < 0 then
		_underSoffit = 0
	endif
endif

parameters A		= rm_ProjectedLength + rm_OverHang - _topCutX						: lock "A"
parameters B		= rm_ProfileWidth													: lock "B"
parameters ZZYZX	= rm_Height - _topHeight + _profH + _underHeight + _underSoffit		: lock "ZZYZX"


! ==============================================================================
! Levels
! ==============================================================================
parameters ac_toplevel		= rm_Height - _topHeight + _profH
parameters ac_bottomlevel	= -_underHeight - _underSoffit

