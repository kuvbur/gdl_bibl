
pen pen3D
material rm_mat


! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif


! ==============================================================================
! 3D Hotspots
! ==============================================================================
unID = 1
hotspot rm_ProjectedLength-_topJointPlus, 0, rm_ProjectedLength*tan(_pitchAngle)-_topJointPlus*tan(_pitchAngle),					unID : unID=unID+1
hotspot rm_ProjectedLength-_topJointPlus, 0, rm_ProjectedLength*tan(_pitchAngle)-_topJointPlus*tan(_pitchAngle)+_profileHeightZ,	unID : unID=unID+1

! --- Profile Width ------------------------------------------------------------
	unID = 1000
	if rm_iRoofEdgeAngle = EDGE_VERTICAL then
		hotspot -overhang,  0,					-overhang*tan(_pitchAngle), unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -overhang, -profileWidthHalf,	-overhang*tan(_pitchAngle), unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -overhang,  1,					-overhang*tan(_pitchAngle), unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

		hotspot -overhang,  0,					-overhang*tan(_pitchAngle), unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -overhang,  profileWidthHalf,	-overhang*tan(_pitchAngle), unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -overhang, -1,					-overhang*tan(_pitchAngle), unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
	endif

	if rm_iRoofEdgeAngle = EDGE_PERDENDICULAR then
		hotspot -overhang+rm_ProfileHeight*sin(_pitchAngle),  0,				-overhang*tan(_pitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(_pitchAngle), unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -overhang+rm_ProfileHeight*sin(_pitchAngle), -profileWidthHalf,	-overhang*tan(_pitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(_pitchAngle), unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -overhang+rm_ProfileHeight*sin(_pitchAngle),  1,				-overhang*tan(_pitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(_pitchAngle), unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

		hotspot -overhang+rm_ProfileHeight*sin(_pitchAngle),  0,				-overhang*tan(_pitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(_pitchAngle), unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -overhang+rm_ProfileHeight*sin(_pitchAngle),  profileWidthHalf,	-overhang*tan(_pitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(_pitchAngle), unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -overhang+rm_ProfileHeight*sin(_pitchAngle), -1,				-overhang*tan(_pitchAngle)+_profileHeightZ-rm_ProfileHeight*cos(_pitchAngle), unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
	endif

	if rm_iRoofEdgeAngle = EDGE_RECTANGULAR then
		hotspot -overhang,  0,					-overhang*tan(_pitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -overhang, -profileWidthHalf,	-overhang*tan(_pitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -overhang,  1,					-overhang*tan(_pitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1

		hotspot -overhang,  0,					-overhang*tan(_pitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 1+128,	rm_ProfileWidth : unID=unID+1
		hotspot -overhang,  profileWidthHalf,	-overhang*tan(_pitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 2,		rm_ProfileWidth : unID=unID+1
		hotspot -overhang, -1,					-overhang*tan(_pitchAngle)+_profileHeightZ/2, unID, profileWidthHalf, 3,		rm_ProfileWidth : unID=unID+1
	endif


! --- Profile Height ------------------------------------------------------------
	unID = 2000
	hotspot  0,										0,  0,								unID, rm_ProfileHeight, 1+128	: unID=unID+1
	hotspot -rm_ProfileHeight*sin(_pitchAngle),	0,  rm_ProfileHeight*cos(_pitchAngle),	unID, rm_ProfileHeight, 2		: unID=unID+1
	hotspot  rm_ProfileHeight*sin(_pitchAngle),	0, -rm_ProfileHeight*cos(_pitchAngle),	unID, rm_ProfileHeight, 3		: unID=unID+1

! --- Horizontal Length ------------------------------------------------------------
	unID = 3000
	hotspot  0,						0, rm_ProjectedLength*tan(_pitchAngle), unID, rm_ProjectedLength, 1+128	: unID=unID+1
	hotspot  rm_ProjectedLength,	0, rm_ProjectedLength*tan(_pitchAngle), unID, rm_ProjectedLength, 2		: unID=unID+1
	hotspot -1,						0, rm_ProjectedLength*tan(_pitchAngle), unID, rm_ProjectedLength, 3		: unID=unID+1

	hotspot  0,						0, 0, unID, rm_ProjectedLength, 1+256	: unID=unID+1
	hotspot  rm_ProjectedLength,	0, 0, unID, rm_ProjectedLength, 2+128	: unID=unID+1
	hotspot -1,						0, 0, unID, rm_ProjectedLength, 3		: unID=unID+1

! --- Height ------------------------------------------------------------
	hotspot rm_ProjectedLength,	0,	 0,			unID, rm_Height, 1+128	: unID=unID+1
	hotspot rm_ProjectedLength,	0,	-1,			unID, rm_Height, 3		: unID=unID+1
	hotspot rm_ProjectedLength,	0,	 rm_Height,	unID, rm_Height, 2		: unID=unID+1

	hotspot 0, 0,  0,			unID, rm_Height, 1+256	: unID=unID+1
	hotspot 0, 0, -1,			unID, rm_Height, 3		: unID=unID+1
	hotspot 0, 0,  rm_Height,	unID, rm_Height, 2+128	: unID=unID+1

! --- Overhang ------------------------------------------------------------
	unID = 6000
	hotspot  0,			0, -overhang*tan(_pitchAngle)+_profileHeightZ, unID, overhang, 1+128,	rm_OverHang	: unID=unID+1
	hotspot -overhang,	0, -overhang*tan(_pitchAngle)+_profileHeightZ, unID, overhang, 2,		rm_OverHang	: unID=unID+1
	hotspot  1,			0, -overhang*tan(_pitchAngle)+_profileHeightZ, unID, overhang, 3,		rm_OverHang	: unID=unID+1


! ==============================================================================
! Roofmaker Element
! ==============================================================================

_bottomCutAngle		= 0
_topCutAngle		= 0
_nCut				= 0
if rm_iRoofEdgeAngle = EDGE_PERDENDICULAR	then _bottomCutAngle = 90-_pitchAngle
if rm_iRoofEdgeAngle = EDGE_VERTICAL		then _bottomCutAngle = 90

! --- Bottom Cut ------------------------------------------------------------------
	if rm_iRoofEdgeAngle = EDGE_RECTANGULAR then
		addy rm_ProfileWidth/2
		rotx 90
		addx -overhang
		addy -overHang*tan(_pitchAngle)+_profileHeightZ/2
		rotz 0
		muly -1
		cutplane 1, 0, 1           !Bottom Cut1
		_nCut = _nCut + 1
		del 6
	else
		addy rm_ProfileWidth/2
		rotx 90
		addx -overhang
		addy -overhang*tan(_pitchAngle)+_profileHeightZ
		rotz -_bottomCutAngle
		muly -1
		cutplane 1, 0, 1       !Bottom Cut
		_nCut = _nCut + 1
		del 6
	endif

! --- Top Cut ------------------------------------------------------------------
	addy rm_ProfileWidth/2
	rotx 90
	addx rm_ProjectedLength-_topJointPlus
	addz rm_ProfileWidth/2
	roty -(180-_TopJointAngle)
	cutplane 0, 1, 1
	roty 90
	cutplane 0, 1, 1       !Top Cut
	del 6
	_nCut = _nCut + 2


! --- 3D Body ------------------------------------------------------------------
	addy rm_ProfileWidth/2
	rotx 90

	base
		prism 6, rm_ProfileWidth,
				0, 0,
				rm_ProjectedLength, rm_ProjectedLength*tan(_pitchAngle),
				rm_ProjectedLength, rm_ProjectedLength*tan(_pitchAngle)+rm_ProfileHeight/cos(_pitchAngle),
				-overhang, -overhang*tan(_pitchAngle)+rm_ProfileHeight/cos(_pitchAngle),
				-overhang, -overhang*tan(_pitchAngle),
				0, 0

		vert 0, 0, 0
		vert rm_ProjectedLength,rm_Height,0
		vert -rm_Height,rm_ProjectedLength,0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
	body 1

	for i = 1 to _nCut
		cutend
	next i

	del 2


! ==============================================================================
for i = 1 to nProjectionCut
	cutend
next i

END
