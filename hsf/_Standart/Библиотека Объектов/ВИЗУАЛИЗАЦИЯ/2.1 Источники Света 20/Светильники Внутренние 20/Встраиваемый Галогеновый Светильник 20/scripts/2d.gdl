
call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
	gs_symbtype_2D_m = 1
endif

pen gs_cont_pen

!!---- Hotspot Editing

unID=1

hotspot2 0, 0, unID : unID = unID + 1

!!---- Hotspot Editing

If gs_symbtype_2D_m = 1 Then				! Realistic Symbol

	if ABS(rotAngleX) < EPS then
		for i=1 to 4
			hotspot2 0, 0, unID, A2, 1+128 : unID=unID+1
			hotspot2 -1, 0, unID, A2, 3 : unID=unID+1
			hotspot2 a/2, 0, unID, A2, 2 : unID=unID+1
			rot2 90
		next i
		del 4

		circle2 0, 0, a/2
		circle2 0, 0, a/2-fwid

		fill gs_fill_type
		pen gs_fill_pen

		if gs_fill_pen<>0 then
			POLY2_B 2, 1+2+64, gs_fill_pen, gs_back_pen,
				0, 0, 900,
				a/2, 360, 4000
		endif
	else
		for i=1 to 2
			hotspot2 0, 0, unID, A2, 1+128 : unID=unID+1
			hotspot2 -1, 0, unID, A2, 3 : unID=unID+1
			hotspot2 a/2, 0, unID, A2, 2 : unID=unID+1
			rot2 180
		next i
		del 2

		pen gs_fill_pen
		fill gs_fill_type
		project2{3} 3, 270, 3+32, 1+4, gs_back_pen, 0, 0, 0, parameters	gs_detlevel_3D_m	= 2,
																		gs_resol			= 24,
																		bShowIllumination3D	= 0

		pen gs_cont_pen
		project2{3} 3, 270, 2, 1+4+8, parameters	gs_detlevel_3D_m	= 2,
													gs_resol			= 24,
													bShowIllumination3D	= 0
	endif
ENDIF

If gs_symbtype_2D_m = 2 OR gs_symbtype_2D_m = 3 Then		! Electrical Symbol or RCP Symbol
	call "Lamp_symbol" parameters all \
									bWallLamp = 0,
									bDirect = 0,
									Symbol_unID = unID,
									specSymbolRotation=180
EndIf
