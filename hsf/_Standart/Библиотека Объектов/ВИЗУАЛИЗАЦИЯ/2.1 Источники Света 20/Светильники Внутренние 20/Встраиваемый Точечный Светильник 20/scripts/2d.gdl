
! Recessed Spot

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
	gs_symbtype_2D_m = 1
endif

!!---- Hotspot Editing

unID=1

hotspot2 0, 0, unID : unID=unID+1

If gs_symbtype_2D_m = 1 Then			! Realistic Symbol

	!!---- Hotspot Editing

	if ABS(rotAngleX) < EPS then
		for i=1 to 4
			hotspot2 0, 0, unID, A2, 1+128 : unID=unID+1
			hotspot2 -1, 0, unID, A2, 3 : unID=unID+1
			hotspot2 a/2, 0, unID, A2, 2 : unID=unID+1
			rot2 90
		next i
		del 4
	else
		for i=1 to 2
			hotspot2 0, 0, unID, A2, 1+128 : unID=unID+1
			hotspot2 -1, 0, unID, A2, 3 : unID=unID+1
			hotspot2 a/2, 0, unID, A2, 2 : unID=unID+1
			rot2 180
		next i
		del 2
	endif

	pen gs_cont_pen
	fill gs_fill_type

	mul2 1, ABS(cos(rotAngleX))

	poly2_b 2, 1+2+64,gs_fill_pen,gs_back_pen,
			0,0,901,
			a/2,360,4001

	Circle2 0,0,a/2-sow

	FOR S=0 TO 3
		v=(a/2-sow)*cos(s*90+45)
		w=(a/2-sow)*sin(s*90+45)

		Line2 0,0,v,w
	NEXT S

	if ABS(rotAngleX) > EPS then
		mul2 1, SGN(rotAngleX)
		pen gs_cont_pen

		POLY2_B 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-a/2,	0,							1,
			-a/2,	ZZYZX * ABS(tan(rotAngleX)),	1,
			0,		ZZYZX * ABS(tan(rotAngleX)),	901,
			0,		-180,						4001,
			a/2,	0,							1,
			0,		0,							901,
			0,		180,						4001
	endif
ENDIF

If gs_symbtype_2D_m = 2 OR gs_symbtype_2D_m = 3 Then			! Electrical Symbol or RCP Symbol
	call "Lamp_symbol" parameters all \
									bWallLamp = 0,
									bDirect = 0,
									Symbol_unID = unID,
									specSymbolRotation=180
EndIf
