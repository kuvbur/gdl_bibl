
! Recessed Spot

If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


rotx rotAngleX

!!---- Geometry Hotspot Editing
unID=1

hotspot	0,0,0,unID,ZZYZX,1+256 : unID=unID+1
hotspot	0,0,1,unID,ZZYZX,3 : unID=unID+1
hotspot	0,0,-ZZYZX,unID,ZZYZX,2 : unID=unID+1

for i=1 to 4
	hotspot 0,0,-zzyzx,unID,A2,1+128,A : unID=unID+1
	hotspot -1,0,-zzyzx,unID,A2,3,A : unID=unID+1
	hotspot a/2,0,-zzyzx,unID,A2,2,A : unID=unID+1
	rotz 90
next i
del 4


!!---- Geometry

pen gs_cont_pen
resol gs_resol
material matFrame

addz -zzyzx
EXTRUDE 3, 0, 0, zzyzx, 55,
		0,0,901,
		a/2,360,4001,
		a/2-sow,360,4001

If not(gs_light_switch) Then
	material matGlass

	addz zzyzx/10
	CYLIND zzyzx/8,a/2-sow
	del 1
endif

del 1


!!---- Light Cone ----!!

If gs_light_switch Then

	addz -0.003

	! ======================================================================
	! Non Visible Omni Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
											gs_color_red						= gs_color_red,
											gs_color_green						= gs_color_green,
											gs_color_blue						= gs_color_blue,
											gs_light_intensity						= gs_light_intensity,
											iColorPreset			= iColorPreset,
											iLightGroupType			= LIGHTGROUP_BUILTIN_SPOT,
											c4dDetUseInner			= 1,
											c4dDetInnerAngle		= c4dDetInnerAngle,
											innerConeAngleHalf		= innerConeAngleHalf,
											innerConeRadius			= innerConeRadius,
											c4dDetOuterAngle		= c4dDetOuterAngle,
											outerConeAngleHalf		= outerConeAngleHalf,
											outerConeRadius			= outerConeRadius,
											iC4dDetAreaShape		= AREASHAPE_SPHERE,
											c4dDetAreaX				= 0.03,
											c4dDetAreaY				= 0.03,
											c4dDetAreaZ				= 0.03,
											bGenShadow				= bGenShadow,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 3,
											bDetShowFalloff 		= 1,
											iFalloffType			= FALLOFFTYPE_SPOT,
											c4dDetRadius			= c4dDetRadius,
											c4dVisBrightness		= c4dVisBrightness,
											c4dPhoPhotometric		= c4dPhoPhotometric,
											iC4dPhoUnit				= iC4dPhoUnit,
											photoIntensityLumen		= photoIntensityLumen,
											photoIntensityCandela	= photoIntensityCandela,
											bShowIllumination3D		= bShowIllumination3D,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											penIllumination			= penIllumination,
											iModelType				= iModelType,
											lightUnID				= unID

	addz 0.003 - 9 * ZZYZX/10

	! ======================================================================
	! Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch			= gs_light_switch,
											gs_color_red			= gs_color_red,
											gs_color_green			= gs_color_green,
											gs_color_blue			= gs_color_blue,
											gs_light_intensity		= 400,
											iLightGroupType			= LIGHTGROUP_BUILTIN_VIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_DISC,
											c4dDetAreaX				= A - 2 * sow,
											c4dDetAreaY				= A - 2 * sow,
											bGenShadow				= 1,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											c4dDetRadius			= 0,
											c4dDetFalloffAngle		= c4dDetOuterAngle,
											c4dDetOnlyZ				= 1,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											bShowIllumination3D		= 0

	del 2

EndIf
