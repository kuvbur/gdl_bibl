
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_CUSTOM_1 then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_CUSTOM_1 then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_MODEL then
			gosub "modelSettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 1,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	! Lamp Dimensions and Geometry

	x1		= 2
	x2		= 118
	y1		= 5
	y2		= 190

	outfWdt	= 115
	outfHgt	= 15
	infWdt	= 80
	infHgt	= 19

	xPic	= x1+infWdt+5
	yPic	= 93
	picWdt	= 86
	picHgt	= 40

	ui_style 0, 1
	ui_outfield	`Размеры Светильника`,	x1,			y1,				200,	outfHgt

	y1 = y1 + 24

	ui_style 0, 0
	ui_outfield	`Поворот вокруг X`,			x1,			y1,				outfWdt,	outfHgt
	ui_infield	"rotAngleX",					x2,			y1-3,			infWdt,		infHgt

	ui_infield	"A",							xPic+3,		yPic-infHgt-3,	infWdt,		infHgt		ui_tooltip `Диаметр`
	ui_infield	"ZZYZX",						x1,			yPic+11,		infWdt,		infHgt		ui_tooltip `Высота`
	ui_infield	"sow",							xPic+3,		yPic+picHgt+3,	infWdt,		infHgt		ui_tooltip `Толщина Обода`

	ui_pict		1,								xPic,		yPic,			picWdt,		picHgt, 1

	ui_separator 204, 0,	204, 266

	! Light Dimensions

	x2		= 209
	y2		= 5
	dy		= 48

	outfWdt	= 160
	outfHgt	= 15
	infWdt	= 100
	infHgt	= 19

	xPic	= x2+infWdt+3
	yPic	= 47
	picWdt	= 131
	picHgt	= 182

	ui_style 0, 1
	ui_outfield	`Параметры Освещения`,		x2,		y2,			outfWdt,	outfHgt

	y2 = y2 + 2 * dy

	ui_style 0, 0
	ui_outfield	`Внутренний Угол`,			x2,		y2,			infWdt,		outfHgt
	ui_infield	"c4dDetInnerAngle",		x2,		y2+16,		infWdt,		infHgt

	y2 = y2 + dy

	ui_outfield	`Наружный Угол`,			x2,		y2,			infWdt,		outfHgt
	ui_infield	"c4dDetOuterAngle",		x2,		y2+16,		infWdt,		infHgt

	y2 = y2 + dy

	ui_outfield	`Дистанция Света`,		x2,		y2,			infWdt,		outfHgt
	ui_infield	"c4dDetRadius",			x2,		y2+16,		infWdt,		infHgt

	ui_pict		"ui_lampSpotDim(1)",	xPic,	yPic,		picWdt,		picHgt, 1

return


"modelSettingsPage":

	x1	= 2
	x2	= 180
	cx1	= 175
	cx2	= 180
	y1	= 5
	dy	= 26

	outFldHgt	= 15
	strInfHgt	= 19
	matInfHgt	= 25

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x1,		y1,		cx1,	outFldHgt,	4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x2+cx2-50,		y1-3,	50,	strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	ui_infield{3} "iModelType",				x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, `Каркас`,	1,
		1, `Оттененная`,	2		ui_tooltip `Тип 3D-модели Освещения`

	y1 = y1 + dy

	ui_outfield `Перо Конуса Света`,		x1,			y1,		cx1,	outFldHgt, 4 * (not(bShowIllumination3D) | gs_detlevel_3D_m = 0)
	ui_infield "penIllumination",		x2+cx2-33,	y1-3,	33,		strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	ui_separator 0,	y1-6,	444,	y1-6

	ui_style 0, 1
	ui_outfield `Покрытия`,		x1,		y1,		2 * cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Корпус`,		x1,		y1,		cx1,		outFldHgt
	ui_infield	"matFrame",		x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Стекло`,		x1,		y1,		cx1,		outFldHgt, gs_light_switch * 4
	ui_infield	"matGlass",		x2,		y1-5,	cx2,		matInfHgt

return

