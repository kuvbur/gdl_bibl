
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_BASIC_PARAMS then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_BASIC_PARAMS then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_MODEL then
			gosub "modelSettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	x1	= 2
	x2	= 132
	x3	= 234
	x4	= 364
	cx1	= 120
	cx2	= 80
	y1	= 160
	y2	= 184
	dy	= 24

	outFldHgt	= 15
	lgtInfHgt	= 19

	ui_separator 0,	y1-7,	444,	y1-7

	ui_style 0, 1
	ui_outfield `Размеры`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Диаметр`,			x1,		y1,		cx1,	outFldHgt
	ui_infield	"A",				x2,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy

	ui_outfield `Длина Цепи`,		x1,		y1,		cx1,	outFldHgt
	ui_infield	"ZZYZX",			x2,		y1-3,	cx2,	lgtInfHgt

return


"modelSettingsPage":

	x1	= 2
	x2	= 180
	cx1	= 160
	cx2	= 180
	y1	= 5
	dy	= 26

	outFldHgt	= 15
	strInfHgt	= 19
	matInfHgt	= 25

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x1,		y1,		cx1,	outFldHgt,	4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x2+cx2-50,		y1-3,	50,	strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	y1 = y1 + dy

	ui_outfield `Перо Конуса Света`,		x1,			y1,		cx1,	outFldHgt, 4 * (not(bShowIllumination3D) | gs_detlevel_3D_m = 0)
	ui_infield "penIllumination",		x2+cx2-33,	y1-3,	33,		strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	ui_separator 0,	y1-6,	444,	y1-6

	ui_style 0, 1
	ui_outfield `Покрытия`,			x1,		y1,		2 * cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Основание`,				x1,		y1,		cx1,		outFldHgt
	ui_infield	"matFrame",			x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Свеча`,			x1,		y1,		cx1,		outFldHgt
	ui_infield	"matCandle",		x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

return


