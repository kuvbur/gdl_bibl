
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_CUSTOM_1 then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_CUSTOM_1 then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_MODEL then
			gosub "modelSettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	! Mounting Settings

	x1		= 2
	x2		= 117
	x3		= 217
	x4		= 348
	y1		= 0
	y2		= y1
	dy		= 23

	outfWdt1	= 110
	outfWdt2	= 115
	infWdt1		= 90
	infWdt2		= 95
	outfHgt		= 15
	infHgt		= 19

	ui_style 0, 1
	ui_outfield	`Крепление`,				x1,		y1,		outfWdt1,	outfHgt

	y1 = y1 + dy - 2

	ui_style 0, 0
	ui_infield{3} "iMountType",			x1,		y1-4,	205,		105,
			2, 1, 12, 4,
			195, 95, 190, 90,
			1,	mount_types[1],		1,
			2,	mount_types[2],		2,
			3,	mount_types[3],		3,
			4,	mount_types[4],		4,
			5,	mount_types[5],		5,
			6,	mount_types[6],		6,
			7,	mount_types[7],		7,
			8,	mount_types[8],		8,
			9,	mount_types[9],		9,
			10,	mount_types[10],	10,
			11,	mount_types[11],	11

	y1 = y1 + 108

	dim	stNumSpots[6]
		stNumSpots[1] = "1"
		stNumSpots[2] = "2"
		stNumSpots[3] = "3"
		stNumSpots[4] = "4"
		stNumSpots[5] = "5"
		stNumSpots[6] = "6"

	if (iMountType <> 11) and (iMountType <> 9) then
		ui_outfield	`Количество Ламп`,	x1,		y1,		outfWdt1,	outfHgt
		ui_infield{3} "numSpots",		x2,		y1-4,	infWdt1,		infHgt,
			8, "", 1, 1,
			0, 0, 0, 0,
			1, stNumSpots[1], 1,
			1, stNumSpots[2], 2,
			1, stNumSpots[3], 3,
			1, stNumSpots[4], 4,
			1, stNumSpots[5], 5,
			1, stNumSpots[6], 6

		y1 = y1 + dy
	endif

	if (iMountType >= 7 and iMountType <= 11) then
		ui_outfield	`Поворот Шины 1`,		x1,		y1,		outfWdt1,	outfHgt
		ui_infield	"arm1angle",		x2,		y1-4,	infWdt1,	infHgt

		y1 = y1 + dy
	endif

	if (iMountType = 7 or iMountType = 8 or iMountType = 10 or iMountType = 11) then
		ui_outfield	`Поворот Шины 2`,		x1,		y1,		outfWdt1,	outfHgt
		ui_infield	"arm2angle",		x2,		y1-4,	infWdt1,	infHgt

		y1 = y1 + dy
	endif

	if iMountType = 10 then
		ui_outfield	`Поворот Шины 3`,		x1,		y1,		outfWdt1,	outfHgt
		ui_infield	"arm3angle",		x2,		y1-4,	infWdt1,	infHgt

		y1 = y1 + dy
	endif

	if (iMountType <> 3 and iMountType <> 5 and iMountType <> 10) then
		ui_outfield	`Форма Крепления`,	x1,		y1,		outfWdt1,	outfHgt
		ui_infield{3} "iMountCover",	x2,		y1-4,	infWdt1,	infHgt,
			8, "", 1, 1,
			0, 0, 0, 0,
			1, cover_types[1], 1,
			1, cover_types[2], 2,
			1, cover_types[3], 3

		y1 = y1 + dy
	endif

	if iMountType = 1 or iMountType = 2 or iMountType = 5 or iMountType = 6 or iMountType = 9 or iMountType = 11 then
		ui_outfield `Ориентация`,	x1,		y1,		outfWdt1,	outfHgt
		ui_infield{3} "iOrientation",	x2,		y1-4,	infWdt1,	infHgt,
			8, "", 1, 1,
			0, 0, 0, 0,
			1, lamporient_types[1], 1,
			1, lamporient_types[2], 2

		y1 = y1 + dy
	endif


	! Dimension Settings

	y1 = 250

	ui_outfield	`Длина Светильника`,	x1,		y1,		outfWdt1,	outfHgt
	ui_infield{3} "A",			x2,		y1-4,	infWdt1,	infHgt

	ui_outfield	`Высота Светильника`,	x3,		y1,		outfWdt2,	outfHgt, 4
	ui_infield{3} "B",			x4,		y1-4,	infWdt2,	infHgt


	ui_separator 0, 240,	444, 240

	ui_separator 212, 0,	212, 238


	! Spot Settings

	ui_style 0, 1
	ui_outfield	`Светильники`,					x3,		y2,		outfWdt2,	outfHgt

	y2 = y2 + dy - 2

	ui_style 0, 0
	ui_outfield	`Стиль Светильников`,				x3,		y2,		outfWdt2,	outfHgt
	ui_infield{3} "iSpotStyle",				x4,		y2-4,	infWdt2,	105,
			2, 2, 9, 3,
			95, 95, 80, 90,
			1, spot_types[1], 1,
			2, spot_types[2], 2,
			3, spot_types[3], 3,
			4, spot_types[4], 4,
			5, spot_types[5], 5,
			6, spot_types[6], 6,
			7, spot_types[7], 7,
			8, spot_types[8], 8,
			9, spot_types[9], 9

	y2 = y2 + 108

	ui_outfield	`Направление`,				x3,		y2,		outfWdt2,	outfHgt
	ui_infield{3} "iSpotOrientation",		x4,		y2-4,	infWdt2,	infHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, orientation_types[1], 1,
		1, orientation_types[2], 2,
		1, orientation_types[3], 3

	y2 = y2 + dy

	if (iSpotOrientation = 3) then
		uiAngleEditText = `Направление источников света можно настроить в 3D-окне.`

		ui_outfield	uiAngleEditText,		x3,		y2,		216,	outfHgt * 2

		y2 = y2 + 2 * dy
	else
		if (iSpotOrientation = 2) then
			ui_outfield	`Угол по Горизонтали`,	x3,		y2,		outfWdt2,	outfHgt
			ui_infield	"spotangle_hor",	x4,		y2-4,	infWdt2,	infHgt

			y2 = y2 + dy

			ui_outfield	`Угол по Вертикали`,	x3,		y2,		outfWdt2,	outfHgt
			ui_infield	"spotangle_vert",	x4,		y2-4,	infWdt2,	infHgt

			y2 = y2 + dy
		endif
	endif

return


"modelSettingsPage":

	x1	= 2
	x2	= 180
	cx1	= 175
	cx2	= 180
	y1	= 5
	dy	= 26

	outFldHgt	= 15
	strInfHgt	= 19
	matInfHgt	= 25

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x2, 	y1-4,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1,stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x1,		y1,		cx1,	outFldHgt,	4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x2+cx2-50,		y1-4,	50,	strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x1, 	y1-4,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x1, 	y1-4,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	y1 = y1 + dy

	ui_outfield `Перо Конуса Света`,		x1,			y1,		cx1,	outFldHgt, 4 * (not(bShowIllumination3D) | gs_detlevel_3D_m = 0)
	ui_infield "penIllumination",		x2+cx2-33,	y1-4,	33,		strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	ui_separator 0,	y1-6,	444,	y1-6

	ui_style 0, 1
	ui_outfield `Покрытия`,			x1,		y1,		2 * cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Крепление`,		x1,		y1,		cx1,		outFldHgt
	ui_infield	"matFrame",			x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Светильники`,				x1,		y1,		cx1,		outFldHgt
	ui_infield	"matSpot",			x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Стекло`,			x1,		y1,		cx1,		outFldHgt
	ui_infield	"matGlassOther",	x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

return

