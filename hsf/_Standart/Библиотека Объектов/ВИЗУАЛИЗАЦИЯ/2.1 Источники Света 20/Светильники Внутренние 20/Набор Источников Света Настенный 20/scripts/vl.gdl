
hideparameter all

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m


! ArchiFM

call "FM_types" parameters all

values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0


call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_OMNI,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes = 0,
									bEnableSymbolic2D	= 1


if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

values "gs_resol" range [3, ]



if gs_cont_pen=0 then parameters gs_cont_pen=10
if gs_fill_pen=0 then parameters gs_fill_pen=91

! Light parameters


ratio=(sgn(45)>0)*cos(90-45)+(sgn(45)<0)*cos(90+45)


! --- Synhronized mm and point in marker width ---

Dot_in_mm = 25.4 / 72

if GLOB_MODPAR_NAME = "fsz" then
	fsz_point = fsz / Dot_in_mm
	parameters fsz_point = fsz_point
else
	fsz = fsz_point * Dot_in_mm
	parameters fsz = fsz
endif


! --- STRING VALUES ---

values "strMountType" mount_types
if iMountType = 6 then
	values "strMountCover" cover_types
else
	values "strMountCover" cover_types[1], cover_types[2]
endif
values "strSpotStyle" spot_types
values "strSpotOrientation" orientation_types
values "strOrientation" lamporient_types


! --- INTEGER VALUES ---

values "iMountType" 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11
if iMountType = 6 then
	values "iMountCover" 1, 2, 3
else
	values "iMountCover" 1, 2
endif
values "iSpotStyle" 1, 2, 3, 4, 5, 6, 7, 8, 9
values "iSpotOrientation" 1, 2, 3
values "iOrientation" 1, 2


! --- STRING - INTEGER CONNECTION ---

! --- Spot Styles ---

if GLOB_MODPAR_NAME = "strSpotStyle" then
	iSpotStyle = 1
	for i = 1 to vardim1(spot_types)
		if strSpotStyle = spot_types[i] then iSpotStyle = i
	next i
	parameters iSpotStyle = iSpotStyle
else
	strSpotStyle = spot_types[max(min(iSpotStyle, vardim1(spot_types)), 1)]
	parameters strSpotStyle = strSpotStyle
endif

! --- Mount Types ---

if GLOB_MODPAR_NAME = "strMountType" then
	iMountType = 1
	for i = 1 to vardim1(mount_types)
		if strMountType = mount_types[i] then iMountType = i
	next i
	parameters iMountType = iMountType
else
	strMountType = mount_types[max(min(iMountType, vardim1(mount_types)), 1)]
	parameters strMountType = strMountType
endif

! --- Cover Types ---

if GLOB_MODPAR_NAME = "strMountCover" then
	iMountCover = 1
	for i = 1 to vardim1(cover_types)
		if strMountCover = cover_types[i] then iMountCover = i
	next i
	parameters iMountCover = iMountCover
else
	strMountCover = cover_types[max(min(iMountCover, vardim1(cover_types)), 1)]
	parameters strMountCover = strMountCover
endif

! --- SpotOrientation Types ---

if GLOB_MODPAR_NAME = "strSpotOrientation" then
	iSpotOrientation = 1
	for i = 1 to vardim1(orientation_types)
		if strSpotOrientation = orientation_types[i] then iSpotOrientation = i
	next i
	parameters iSpotOrientation = iSpotOrientation
else
	strSpotOrientation = orientation_types[max(min(iSpotOrientation, vardim1(orientation_types)), 1)]
	parameters strSpotOrientation = strSpotOrientation
endif

! --- Orientation Types ---

if GLOB_MODPAR_NAME = "strOrientation" then
	iOrientation = 1
	for i = 1 to vardim1(lamporient_types)
		if strOrientation = lamporient_types[i] then iOrientation = i
	next i
	parameters iOrientation = iOrientation
else
	strOrientation = lamporient_types[max(min(iOrientation, vardim1(lamporient_types)), 1)]
	parameters strOrientation = strOrientation
endif


! --- 3D Detail Level ---

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif


!--- Set default parameters ---

if GLOB_MODPAR_NAME = "strMountType" or GLOB_MODPAR_NAME = "iMountType" then
	parameters iMountCover = 1
	if iMountType = 10 or iMountType = 11 then
		parameters numSpots = 6
	else
		parameters numSpots = 4
	endif
	if iMountType = 9 or iMountType = 11 then
		parameters arm1angle = 90
	else
		parameters arm1angle = 0
	endif
	if iMountType = 10 then
		parameters arm2angle = 120
	else
		if iMountType = 11 then
			parameters arm2angle = -90
		else
			parameters arm2angle = 0
		endif
	endif
endif


if iMountType = 3 or iMountType = 10 then
	parameters iMountCover = 2
	lock "strMountCover"
	hideparameter "strMountCover"
endif
if iMountType = 5 then
	lock "strMountCover"
	hideparameter "strMountCover"
endif

if iMountType < 7 then
	lock "arm1angle"
	hideparameter "arm1angle"
endif

if iMountType < 7 or iMountType = 9 then
	lock "arm2angle"
	hideparameter "arm2angle"
endif

if iMountType <> 10 then
	lock "arm3angle"
	hideparameter "arm3angle"
endif

if iSpotOrientation = 1 or iSpotOrientation = 2 then
	lock "spotangle_h", "spotangle_v"
	hideparameter "spotangle_h", "spotangle_v"
	values "spotangle_hor" range [0, 360]
	values "spotangle_vert" range [0, 90]
endif
if iSpotOrientation <> 2 then
	lock "spotangle_hor", "spotangle_vert"
	hideparameter "spotangle_hor", "spotangle_vert"
endif
if iSpotOrientation = 3 or iSpotOrientation = 2 then
	for i = 1 to numSpots
		if spotangle_v[i] < 360 and spotangle_v[i] > 225  then
			spotangle_v[i] = 0
			parameters spotangle_v = spotangle_v
		endif
		if spotangle_v[i] > 90 and spotangle_v[i] <= 225 then
			spotangle_v[i] = 90
			parameters spotangle_v = spotangle_v
		endif
	next i
endif

if iMountType = 9 then
	values "numSpots" 4
	lock  "numSpots"
	hideparameter "numSpots"
else
	if iMountType = 10 then
		values "numSpots" 3,6
	else
		if iMountType = 11 then
			values "numSpots" 6
			lock  "numSpots"
			hideparameter "numSpots"
		else
			if iMountType = 5 then
				values "numSpots" 1,2,3,4,5,6
			else
				if iMountType = 4 then
					values "numSpots" 1,2,3,4
				else
					if iMountType = 3 then
						values "numSpots" 3,4,5,6
					else
						values "numSpots" 2,3,4,5,6
					endif
				endif
			endif
		endif
	endif
endif

if GLOB_MODPAR_NAME = "iSpotOrientation" or GLOB_MODPAR_NAME = "strSpotOrientation" or \
   GLOB_MODPAR_NAME = "strMountType" or GLOB_MODPAR_NAME = "iMountType" then
	if iSpotOrientation = 1 then
		for i = 1 to numSpots
			if iMountType = 3 or iMountType = 4 then
				spotangle_h[i] = rnd(5) - rnd(10) - (i-1)*360/numSpots
				spotangle_v[i] = rnd(5) - rnd(10) + 45
			else
				spotangle_h[i] = rnd(5) - rnd(10) + i*360/numSpots + 180/numSpots
				spotangle_v[i] = rnd(5) - rnd(10) + i*90/numSpots
			endif
			parameters spotangle_h[i] = spotangle_h[i], spotangle_v[i] = spotangle_v[i]
		next i
	else
		if iSpotOrientation = 2 then
			spotangle_hor = spotangle_h[1]
			spotangle_vert = spotangle_v[1]
			parameters spotangle_hor = spotangle_h[1], spotangle_vert = spotangle_v[1]
			for i = 2 to numSpots
				spotangle_h[i] = spotangle_hor
				spotangle_v[i] = spotangle_vert
				parameters spotangle_h[i] = spotangle_h[i], spotangle_v[i] = spotangle_v[i]
			next i
		endif
	endif
endif

if GLOB_MODPAR_NAME = "spotangle_h" or GLOB_MODPAR_NAME = "spotangle_v" then
	if iSpotOrientation = 2 then
		spotangle_hor = spotangle_h[1]
		spotangle_vert = spotangle_v[1]
		parameters spotangle_hor = spotangle_h[1], spotangle_vert = spotangle_v[1]
		for i = 2 to numSpots
			spotangle_h[i] = spotangle_h[1]
			spotangle_v[i] = spotangle_v[1]
			parameters spotangle_h[i] = spotangle_h[i], spotangle_v[i] = spotangle_v[i]
		next i
	endif
endif

if GLOB_MODPAR_NAME = "spotangle_hor" or GLOB_MODPAR_NAME = "spotangle_vert" then
	for i = 1 to numSpots
		spotangle_h[i] = spotangle_hor
		spotangle_v[i] = spotangle_vert
		parameters spotangle_h[i] = spotangle_h[i], spotangle_v[i] = spotangle_v[i]
	next i
endif

if iMountType = 3 or iMountType = 4 or iMountType = 7 or iMountType = 8 or iMountType = 10 then
	parameters iOrientation = 1
	lock "strOrientation"
	hideparameter "strOrientation"
endif

if GLOB_MODPAR_NAME="A2" then
	A = A2*2
	parameters A = A
else
	A2 = A/2
	parameters A2 = A2
endif

if GLOB_MODPAR_NAME = "strMountType" or GLOB_MODPAR_NAME = "iMountType" then

	if iMountType = 1 then
		parameters A = 0.55, B = 0.1, A2 = A/2
	endif

	if iMountType = 2 then
		parameters A = 0.6, B = 0.116, A2 = A/2
	endif

	if iMountType = 3 then
		parameters A = 0.2184, B = A/2, A2 = A/2
	endif

	if iMountType = 4 then
		parameters A = 0.3, B = A, A2 = A/2
	endif

	if iMountType = 5 then
		parameters A = 0.5, B = 0.2, A2 = A/2
	endif

	if iMountType = 6 then
		parameters A = 0.5, B = 0.1, A2 = A/2
	endif

	if iMountType = 7 then
		parameters A = 0.6, B = 0.1, A2 = A/2
	endif

	if iMountType = 8 then
		parameters A = 0.6, B = 0.1, A2 = A/2
	endif

	if iMountType = 9 then
		parameters A = 0.5, B = A, A2 = A/2
	endif

	if iMountType = 10 then
		parameters A = 0.6, B = A, A2 = A/2
	endif

	if iMountType = 11 then
		parameters A = 0.5, B = 2*A, A2 = A/2
	endif

endif

if iMountType <> 5 then
	lock "B"
endif

if iMountType = 3 then
	lock "A"
endif

lock "ZZYZX"
hideparameter "ZZYZX"


values "gs_ui_current_page" pageValues
