
If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


!!---- Geometry Hotspot Editing
unID=1

hotspot	0,0,0,unID,ZZYZX,1+256 : unID=unID+1
hotspot	0,0,-1,unID,ZZYZX,3 : unID=unID+1
hotspot	0,0,ZZYZX,unID,ZZYZX,2 : unID=unID+1

hotspot	0,0,ZZYZX,unID,shadeHeight,1+128 : unID=unID+1
hotspot	0,0,ZZYZX+1,unID,shadeHeight,3 : unID=unID+1
hotspot	0,0,ZZYZX-shadeHeight,unID,shadeHeight,2 : unID=unID+1

for i=1 to 4
	hotspot 0,0,zzyzx-shadeHeight,unID,A2,1+128,A : unID=unID+1
	hotspot -1,0,zzyzx-shadeHeight,unID,A2,3,A : unID=unID+1
	hotspot A2,0,zzyzx-shadeHeight,unID,A2,2,A : unID=unID+1

	rotz 90
next i
del 4

!!---- Geometry ----!!

pen gs_cont_pen
material matFrame
resol gs_resol

cylind  0.02, A2          !stay-plate

addz 0.02

If gs_detlevel_3D_m=1 Then resol 3 Else resol 12
cylind zzyzx-shadeHeight-0.001-0.02, 0.02

material matShadeOut
resol gs_resol

addz zzyzx-shadeHeight-0.02

roty 90
mulx 2 * shadeHeight / A
revolve 3, 360, 1,
	0,				A/2,	1,
	0,				0,		901,
	0,				90,		4001
del 2

if not(gs_light_switch) then
	material matGlass
	prism_ 2, -0.001,
		0,		0,		915,
		A/2,	360,	4015
endif

del 2


addz zzyzx-shadeHeight-0.01
resol 36
If gs_light_switch Then

	addz 0.01
	
	! ======================================================================
	! Non Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
												gs_color_red						= gs_color_red,
												gs_color_green						= gs_color_green,
												gs_color_blue						= gs_color_blue,
												gs_light_intensity						= gs_light_intensity,
												iColorPreset			= iColorPreset,
												iLightGroupType			= LIGHTGROUP_BUILTIN_NONVIS_AREA,
												iC4dDetAreaShape		= AREASHAPE_DISC,
												c4dDetAreaX				= A,
												c4dDetAreaY				= A,
												c4dDetAreaZ				= 0.03,
												bGenShadow				= bGenShadow,
												iShadowQuality			= iShadowQuality,
												iShadowQualityGroupType	= 2,
												iC4dGenVisibility		= 1,
												bDetShowFalloff 		= 1,
												iFalloffType			= FALLOFFTYPE_CONE,
												c4dDetRadius			= c4dDetRadius,
												c4dDetFalloffAngle		= c4dDetFalloffAngle,
												falloffAngleTemp		= falloffAngleTemp,
												c4dPhoPhotometric		= c4dPhoPhotometric,
												iC4dPhoUnit				= iC4dPhoUnit,
												photoIntensityLumen		= photoIntensityLumen,
												photoIntensityCandela	= photoIntensityCandela,
												bShowIllumination3D		= bShowIllumination3D,
												gs_resol				= gs_resol,
												gs_cont_pen				= gs_cont_pen,
												penIllumination			= penIllumination,
												iModelType				= iModelType,
												lightUnID				= unID

	! ======================================================================
	! Visible Area Light
	! ======================================================================

	rotz 90

	call	"lightMacro_m" parameters		gs_light_switch		= gs_light_switch,
											gs_color_red		= gs_color_red,
											gs_color_green		= gs_color_green,
											gs_color_blue		= gs_color_blue,
											gs_light_intensity	= 400,
											iLightGroupType		= LIGHTGROUP_BUILTIN_VIS_AREA,
											iC4dDetAreaShape	= AREASHAPE_DISC,
											c4dDetAreaX			= A,
											c4dDetAreaY			= A,
											bGenShadow			= 1,
											iShadowQuality		= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility	= 1,
											bDetShowFalloff 	= 1,
											c4dDetRadius		= 0,
											c4dDetFalloffAngle	= c4dDetFalloffAngle,
											gs_resol			= gs_resol,
											gs_cont_pen			= gs_cont_pen,
											bShowIllumination3D	= 0

	del 2

EndIf
del top

