
If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


!!---- Geometry Hotspot Editing
unID=1

hotspot	0,0,0,unID : unID=unID+1

hotspot	0.40,0,0,unID,zzyzx,1+128 : unID=unID+1
hotspot	0.40,0,-1,unID,zzyzx,3 : unID=unID+1
hotspot	0.40,0,zzyzx,unID,zzyzx,2 : unID=unID+1

for i=1 to 4
	hotspot 0,0,0,unID,B2,1+128,B : unID=unID+1
	hotspot -1,0,0,unID,B2,3,B : unID=unID+1
	hotspot B2,0,0,unID,B2,2,B : unID=unID+1

	rotz 90
next i
del 4

!!---- Geometry ----!!

pen gs_cont_pen
material matFrame
resol gs_resol

cylind  0.02, B2					!base

If gs_detlevel_3D_m=1 Then resol 5 Else resol 12

addz 0.02
cylind zzyzx-0.11, 0.02				!stock

addz zzyzx-0.11
elbow 0.05, 90, 0.02				!arme1

add 0.05, 0, 0.05
roty 90

addz 0.1 - 0.02 * 1.2 * 5/8
rotz 90
cutplane atn(1.2 * 5/8)
del 2

CYLIND 0.1, 0.02					!arme2

cutend

addz 0.1
roty -90
addz -0.04

MULX 1.2
MULY 0.8
addx 0.21
RESOL gs_resol

addz -0.005
cutform 2, 2, 1+2,
	0,		0,		0.4,	0.08,

	0,		0,		901,
	0.25,	360,	4015
del 1

CONE 0.08, 0.25, 0.2, 90, 90		!lampshade

cutend


if not(gs_light_switch) then
	material matGlass
	addz -0.001
	prism_ 2, 0.001,
		0,		0,		915,
		0.25,	360,	4015
	del 1
endif

del 3

if gs_light_switch then
	gosub "C4D_Light"
endif

del 7


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"C4D_Light":
! ==============================================================================

	addx 0.252

	! ======================================================================
	! Non Visible Area Light
	! ======================================================================

	rotz 90 + 180 * (gs_resol % 2)

	call	"lightMacro_m" parameters		gs_light_switch			= gs_light_switch,
											gs_color_red			= gs_color_red,
											gs_color_green			= gs_color_green,
											gs_color_blue			= gs_color_blue,
											gs_light_intensity		= gs_light_intensity,
											iColorPreset			= iColorPreset,
											iLightGroupType			= LIGHTGROUP_BUILTIN_NONVIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_DISC,
											c4dDetAreaX				= 0.4,
											c4dDetAreaY				= 0.6,
											c4dDetAreaZ				= 0.03,
											bGenShadow				= bGenShadow,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											iFalloffType			= FALLOFFTYPE_CONE,
											c4dDetRadius			= c4dDetRadius,
											c4dDetFalloffAngle		= c4dDetFalloffAngle,
											falloffAngleTemp		= falloffAngleTemp,
											c4dPhoPhotometric		= c4dPhoPhotometric,
											iC4dPhoUnit				= iC4dPhoUnit,
											photoIntensityLumen		= photoIntensityLumen,
											photoIntensityCandela	= photoIntensityCandela,
											bShowIllumination3D		= bShowIllumination3D,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											penIllumination			= penIllumination,
											iModelType				= iModelType,
											lightUnID				= unID

	del 1

	! ======================================================================
	! Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch			= gs_light_switch,
											gs_color_red			= gs_color_red,
											gs_color_green			= gs_color_green,
											gs_color_blue			= gs_color_blue,
											gs_light_intensity		= 400,
											iLightGroupType			= LIGHTGROUP_BUILTIN_VIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_DISC,
											c4dDetAreaX				= 0.6,
											c4dDetAreaY				= 0.4,
											bGenShadow				= 1,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											c4dDetRadius			= 0,
											c4dDetFalloffAngle		= c4dDetFalloffAngle,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											bShowIllumination3D		= 0

	del 1
	
return

