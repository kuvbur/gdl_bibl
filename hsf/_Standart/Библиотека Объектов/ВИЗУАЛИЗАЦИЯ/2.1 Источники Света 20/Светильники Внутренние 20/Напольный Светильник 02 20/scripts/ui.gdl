
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_BASIC_PARAMS then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_BASIC_PARAMS then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_FPLAN_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_FPLAN_MODEL then
			gosub "displaySettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	x1	= 2
	x2	= 132
	x3	= 234
	x4	= 364
	cx1	= 120
	cx2	= 80
	y1	= 160
	y2	= 184
	dy	= 24

	outFldHgt	= 15
	lgtInfHgt	= 19

	ui_separator 0,	y1-7,	444,	y1-7

	ui_style 0, 1
	ui_outfield `Размеры`,		x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Общая Длина`,			x1,		y1,		cx1,	outFldHgt, 4
	ui_infield	"A",					x2,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy

	ui_outfield `Диаметр Основания`,	x1,		y1,		cx1,	outFldHgt
	ui_infield	"B",					x2,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy

	ui_outfield `Общая Высота`,			x1,		y1,		cx1,	outFldHgt
	ui_infield	"ZZYZX",				x2,		y1-3,	cx2,	lgtInfHgt


	ui_outfield	`Угол Ослабления Света`,	x3,		y2,		cx1,	outFldHgt
	ui_infield	"c4dDetFalloffAngle",	x4,		y2-3,	cx2,	lgtInfHgt

return


"displaySettingsPage":

	dim	surfaceBlock[2][2]
		surfaceBlock[1][1]	= `Основание`			:	surfaceBlock[1][2] = "matFrame"
		surfaceBlock[2][1]	= `Стекло`			:	surfaceBlock[2][2] = "matGlass"

	dim	flSurfaceBlk[2]
		flSurfaceBlk[1]		= 0
		flSurfaceBlk[2]		= gs_light_switch * 4

	call "ui_lampDisplaySettings_m" parameters all	bEnableModelType	= 1,
													pageStartY			= 3,
													stDetailLevel		= stDetailLevel,
													surfaceBlock		= surfaceBlock,
													flSurfaceBlk		= flSurfaceBlk

return

