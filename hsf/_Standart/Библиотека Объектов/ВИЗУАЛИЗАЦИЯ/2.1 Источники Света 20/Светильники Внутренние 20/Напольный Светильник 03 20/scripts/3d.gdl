
! Floor Lamp 03

If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


!!---- Geometry Hotspot Editing
unID=1

hotspot	0,0,0,unID,zzyzx,1+256 : unID=unID+1
hotspot	0,0,-1,unID,zzyzx,3 : unID=unID+1
hotspot	0,0,zzyzx,unID,zzyzx,2 : unID=unID+1

!!---- Geometry ----!!

pen gs_cont_pen
material matFrame
resol gs_resol

CYLIND     zzyzx/75,  zzyzx/15

add -zzyzx/450, -zzyzx/450, zzyzx/75
BRICK zzyzx/225, zzyzx/225, zzyzx-zzyzx/75

add -lud/6 + zzyzx/450, -2.5 * zzyzx/225, zzyzx - lsh - 2 * zzyzx/75
BRICK lud/3, lud + 6 * zzyzx/225, lsh/3

addy 6 * zzyzx/225
addx 1 * zzyzx/225
rotx 90
addz -lud
addy lsh/3

addz zzyzx/450
cutform 4, 1, 1+2,
	0,		0,		lud - zzyzx/225,	lud - zzyzx/225,

	0 - 0.5 * zzyzx/225,					0 + zzyzx/225,	15,
	lud/3 - 1.5 * zzyzx/225,				0 + zzyzx/225,	15,
	lud/2 + (lud/3 - 3 * zzyzx/225) / 2,	2 * lsh/3,	15,
	-lud/2 + (lud/3 - 1 * zzyzx/225) / 2,	2 * lsh/3,	15

del 1

PRISM 4, lud,
	0,										0,
	lud/3 - 2 * zzyzx/225,					0,
	lud/2 + (lud/3 - 2 * zzyzx/225) / 2,	2 * lsh/3,
	-lud/2 + (lud/3 - 2 * zzyzx/225) / 2,	2 * lsh/3

cutend

rotx -90
add -lud/2 + (lud/3 - 2 * zzyzx/225) / 2 + zzyzx/450, -lud + zzyzx/450,  2 * lsh/3

if not(gs_light_switch) then
	material matGlass
	block lud - zzyzx/225, lud - zzyzx/225, zzyzx/900
endif

!!---- Light Cone ----!!

resol 36
If gs_light_switch Then

	add lud/2-zzyzx/450, lud/2-zzyzx/450, 0
	roty 180

	! ======================================================================
	! Non Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
											gs_color_red						= gs_color_red,
											gs_color_green						= gs_color_green,
											gs_color_blue						= gs_color_blue,
											gs_light_intensity						= gs_light_intensity,
											iColorPreset			= iColorPreset,
											iLightGroupType			= LIGHTGROUP_BUILTIN_NONVIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_RECT,
											c4dDetAreaX				= lud - ZZYZX/225,
											c4dDetAreaY				= lud - ZZYZX/225,
											c4dDetAreaZ				= 0.03,
											bGenShadow				= bGenShadow,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											iFalloffType			= FALLOFFTYPE_PYRAMID,
											c4dDetRadius			= c4dDetRadius,
											c4dDetFalloffAngle		= c4dDetFalloffAngle,
											falloffAngleTemp		= falloffAngleTemp,
											c4dPhoPhotometric		= c4dPhoPhotometric,
											iC4dPhoUnit				= iC4dPhoUnit,
											photoIntensityLumen		= photoIntensityLumen,
											photoIntensityCandela	= photoIntensityCandela,
											bShowIllumination3D		= bShowIllumination3D,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											penIllumination			= penIllumination,
											iModelType				= iModelType,
											lightUnID				= unID

	! ======================================================================
	! Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch		= gs_light_switch,
											gs_color_red		= gs_color_red,
											gs_color_green		= gs_color_green,
											gs_color_blue		= gs_color_blue,
											gs_light_intensity	= 400,
											iLightGroupType		= LIGHTGROUP_BUILTIN_VIS_AREA,
											iC4dDetAreaShape	= AREASHAPE_RECT,
											c4dDetAreaX			= lud - ZZYZX/225,
											c4dDetAreaY			= lud - ZZYZX/225,
											bGenShadow			= 1,
											iShadowQuality		= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility	= 1,
											bDetShowFalloff 	= 1,
											c4dDetRadius		= 0,
											c4dDetFalloffAngle	= c4dDetFalloffAngle,
											gs_resol			= gs_resol,
											gs_cont_pen			= gs_cont_pen,
											bShowIllumination3D	= 0
	del 2

EndIf
del top

End
