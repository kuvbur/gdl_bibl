
hideparameter all

call "FM_types" parameters all

values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_NONVIS_AREA,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes = 1,
									bEnableSymbolic2D	= 0

if gs_cont_pen=0 then parameters gs_cont_pen=10
if gs_fill_pen=0 then parameters gs_fill_pen=91

values "gs_resol" range [3, ]

if gs_detlevel_3D_m = DET3D_SIMPLE then
	lock "gs_resol"
endif

if gs_detlevel_3D_m = DET3D_OFF then
	lock "gs_resol", "gs_shadow"
endif

values "ZZYZX" range (0, )
values "lud" RANGE (0, 2 * zzyzx/15]
values "lud2" RANGE (0, zzyzx/15]
values "lsh" range (0, ZZYZX - 0.1)

A = 2 * zzyzx/15
B = zzyzx/15 + max(zzyzx/75 + lud, zzyzx/15)
parameters A = A, B = B
lock "a","b"

if GLOB_MODPAR_NAME = "lud" then
	lud2 = lud/2
	parameters lud2 = lud2
else
	lud = lud2*2
	parameters lud = lud
endif


if (gs_light_switch) then lock "matGlass"

values "gs_ui_current_page" pageValues

