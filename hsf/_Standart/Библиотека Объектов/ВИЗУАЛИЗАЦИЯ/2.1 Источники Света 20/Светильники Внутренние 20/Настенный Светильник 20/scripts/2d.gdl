
pen gs_cont_pen
fill gs_fill_type

unID=1

if iShadeType = 1 or iShadeType = 11 then
	rot2 -90
	if bMigrationRotZ then
		rot2 90
	endif
endif

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
	gs_symbtype_2D_m = 1
endif

! ==============================================================================
!	Realistic Symbol
! ==============================================================================

if gs_symbtype_2D_m = 1 then

	hotspot2 0,0, unID : unID=unID+1

	! ==============================================================================
	!	Shade 2D
	! ==============================================================================

	if iShadeType = 1 then
		gosub "SHADE_1"
	endif
	if iShadeType = 2 then
		gosub "SHADE_2"
	endif
	if iShadeType = 3 then
		gosub "SHADE_3"
	endif
	if iShadeType = 4 then
		gosub "SHADE_4"
	endif
	if iShadeType = 5 then
		gosub "SHADE_5"
	endif
	if iShadeType = 6 then
		gosub "SHADE_6"
	endif
	if iShadeType = 7 or iShadeType = 8 then
		gosub "SHADE_7_8"
	endif
	if iShadeType = 9 then
		gosub "SHADE_9"
	endif
	if iShadeType = 10 then
		gosub "SHADE_10"
	endif
	if iShadeType = 11 then
		gosub "SHADE_11"
	endif
	if iShadeType = 12 then
		gosub "SHADE_12"
	endif
	if iShadeType = 13 then
		gosub "SHADE_13"
	endif
	if iShadeType = 14 then
		gosub "SHADE_14"
	endif

endif


! ==============================================================================

if iShadeType = 1 or iShadeType = 11 then
	del 1
	if bMigrationRotZ then
		del 1
	endif
endif

! ==============================================================================
!	Electrical Symbol or RCP Symbol
! ==============================================================================

if gs_symbtype_2D_m = 2 or gs_symbtype_2D_m = 3 then
	call "Lamp_symbol" parameters all	bWallLamp		= 1,
										bCeilingLamp	= 0,
										bDirect			= 0,
										Symbol_unID		= unID,
										bMigrationRotZ	= bMigrationRotZ,
										specSymbolRotation	= 180
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
"SHADE_1":
! ==============================================================================

	hotspot2 0,	0.085,unID	: unID=unID+1
	hotspot2 0,	-0.085,unID	: unID=unID+1
	hotspot2 A-A2,	-B/2,	unID	: unID=unID+1
	hotspot2 A-A2,	 B/2,	unID	: unID=unID+1

	hotspot2 0,0,unID,A,1+128 : unID=unID+1
	hotspot2 -1,0,unID,A,3 : unID=unID+1
	hotspot2 A,0,unID,A,2 : unID=unID+1

	add2 A2+0.0275, 0
	poly2_b 2, 1+2+64,gs_fill_pen,gs_back_pen,
			0,0,901,
			A2,360,4001
	del 1

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0.085, 1,
				0.04, 0.085, 1,
				0.04, -0.085, 1,
				0, -0.085, 1

return


! ==============================================================================
"SHADE_2":
! ==============================================================================

	hb=b/3      !Base height
	bd=(hb-1/4")/2

	for i=1 to 2
		hotspot2 0, -hb-0.0236,unID,A2,1+128 : unID=unID+1
		hotspot2 -1,-hb-0.0236,unID,A2,3 : unID=unID+1
		hotspot2 A2,-hb-0.0236,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, 0,	unID,B,1+128: unID=unID+1
	hotspot2 0, 1,	unID,B,3	: unID=unID+1
	hotspot2 0, -B,	unID,B,2 	: unID=unID+1

	hotspot2 a/2-1/2", 0,		unID	: unID=unID+1
	hotspot2 -a/2+1/2", 0,	unID	: unID=unID+1

	rot2 90
	for i = 1 to 2
		poly2_b 13, 1+2+4+64, gs_fill_pen, gs_back_pen,
			 0,a/2-1/2",1,
			-3/16",a/2-1/2",1,
			-1/4",a/2-9/16",1,
			-1/4",a/2-11/16",1,
			-5/16",a/2-3/4",1,
			-1/4"-bd+1/16",a/2-3/4",1,
			-1/4"-bd,a/2-13/16",1,
			-1/4"-bd,a/2-15/16",1,
			-1/4"-bd-1/16",a/2-1",1,
			-hb,a/2-1",1,
			-hb,0,0,
			 0,0,1,
			 0,a/2-1",1
		mul2 1, -1
	next i
	del 2

	line2 -1/4",a/2-11/16",	-1/4", -a/2+11/16"
	line2 -1/4"-bd,a/2-13/16",-1/4"-bd,-a/2+13/16"

	add2 -hb, 0
	mul2 (b-hb)/0.08, 1

	for i = 1 to 2
		poly2_b 16, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.0000,  a/2-0.0290, 1,
			-0.0004,  a/2-0.0290+0.0097, 1,
			-0.0036,  a/2-0.0290+0.0188, 1,
			-0.0100,  a/2-0.0290+0.0260, 1,
			-0.0162,  a/2-0.0290+0.0288, 1,
			-0.0190,  a/2, 1,
			-0.0283,  a/2-0.0290+0.0262, 1,
			-0.0368,  a/2-0.0290+0.0215, 1,
			-0.0444,  a/2-0.0290+0.0155, 1,
			-0.0509,  a/2-0.0290+0.0083, 1,
			-0.0563,  a/2-0.0290, 1,
			-0.0606,  a/2-0.0290-0.0084, 1,
			-0.0640,  a/2-0.0290-0.0175, 1,
			-0.0665,  a/2-0.0290-0.0267, 1,
			-0.0800,  0, 0,
			-0,  0, 1
		mul2 1, -1
	next i
	del 2

	del 2

	del 1

return


! ==============================================================================
"SHADE_3":
! ==============================================================================

	hb=0.035		!Base height
	shadeheight = b - hb

	for i=1 to 2
		hotspot2 0, 0,unID,A2,1+128 : unID=unID+1
		hotspot2 -1,0,unID,A2,3 : unID=unID+1
		hotspot2 A2,0,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, 0,	unID,B,1+128: unID=unID+1
	hotspot2 0, 1,	unID,B,3	: unID=unID+1
	hotspot2 0, -B,	unID,B,2 	: unID=unID+1

	hotspot2 0, -hb,	unID	: unID=unID+1

	rot2 90
	for i = 1 to 2
		poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
			 0,	a/2,1,
			-hb/2,	a/2,	1,
			-hb/2,	a/2-0.01,	1,
			-hb,	a/2-0.01,	1,
			-hb,	0,	0,
			 0, 0, 1

		mul2 1, -1
	next i
	del 2

	line2 -hb/2,	a/2-0.01, -hb/2,	-a/2+0.01

	add2 -hb, 0
	mul2 shadeheight/(0.05833333333333+0.01138026878427), a/0.33
	add2 -0.01138026878427, 0

	for i = 1 to 2
		poly2_b 17, 1+2+4+64, gs_fill_pen, gs_back_pen,
		   0.01138026878427, 0.1373099392565,      1,
		   0, 0.11458292629,    900,
		   0, 26.59884798931,   4001,
			0,         0.14,      1,
			0, 0.11458292629,    900,
					   0, 26.59884798931,   4001,
			-0.01138026878427, 0.1373099392565,      1,
			0.006560974361986, 0.1011676379033,    900,
					   0, 19.31151154783,   4001,
			-0.0223232002213, 0.1293431345516,      1,
			0.04432862837405, 0.06670532744836,    900,
					   0, 22.54929555119,   4001,
			-0.04124789556922, 0.09899494936612,      1,
			0.2370033247881, 0,    900,
					   0, 19.58430938155,   4001,
			-0.05833333333333, 0,      0,
			0.01138026878427, 0,      1
		mul2 1, -1
	next i
	del 6

return


! ==============================================================================
"SHADE_4":
! ==============================================================================

	innersize_factor = 0.95

	for i=1 to 2
		hotspot2 0, 0,unID,A2,1+128 : unID=unID+1
		hotspot2 -1,0,unID,A2,3 : unID=unID+1
		hotspot2 A2,0,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, 0,	unID,B,1+128: unID=unID+1
	hotspot2 0, 1,	unID,B,3	: unID=unID+1
	hotspot2 0, -B,	unID,B,2 	: unID=unID+1


	rot2 90
	mul2 b/0.05833333333333, a/0.28

	for i = 1 to 2
		poly2_b 12, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.01138026878427, innersize_factor*0.1373099392565,      1,
			-0.01138026878427, 0.1373099392565,      1,
			0.006560974361986, 0.1011676379033,    900,
					   0, 19.31151154783,   4001,
			-0.0223232002213, 0.1293431345516,      1,
			0.04432862837405, 0.06670532744836,    900,
					   0, 22.54929555119,   4001,
			-0.04124789556922, 0.09899494936612,      1,
			0.2370033247881, 0,    900,
					   0, 19.58430938155,   4001,
			-0.05833333333333, 0,      0,
			-0.01138026878427, 0,      1
		mul2 1, -1
	next i
	del 4

return


! ==============================================================================
"SHADE_5":
! ==============================================================================

	shader_r = a*1.8
	shader_thk = 0.01
	diffh = shader_r - sqr(shader_r*shader_r - a/2*a/2)

	for i=1 to 2
		hotspot2 0,  -B + diffh,unID,A2,1+128 : unID=unID+1
		hotspot2 -1, -B + diffh,unID,A2,3 : unID=unID+1
		hotspot2 A2, -B + diffh,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, 0,	unID,B,1+128: unID=unID+1
	hotspot2 0, 1,	unID,B,3	: unID=unID+1
	hotspot2 0, -B,	unID,B,2 	: unID=unID+1

	poly2_b 8, 1+2+64,gs_fill_pen,gs_back_pen,
			-A/2, -B + diffh + shader_thk, 1,
			-A/2, -B + diffh, 1,
			0, -b+shader_r,901,
			A/2, -B + diffh,3001,
			A/2, -B + diffh + shader_thk, 1,
			0, -b+shader_r,901,
			-A/2, -B + diffh + shader_thk,3001,
			-A/2, -B + diffh + shader_thk, 1

return


! ==============================================================================
"SHADE_6":
! ==============================================================================

	toruswidth = (b-0.02)*2

	for i=1 to 2
		hotspot2 0,  0,unID,A2,1+128 : unID=unID+1
		hotspot2 -1, 0,unID,A2,3 : unID=unID+1
		hotspot2 A2, 0,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, 0,	unID,B,1+128: unID=unID+1
	hotspot2 0, 1,	unID,B,3	: unID=unID+1
	hotspot2 0, -B,	unID,B,2 	: unID=unID+1

	rot2 -90
	poly2_b 6, 1+2+4+64,gs_fill_pen,gs_back_pen,
		0, a/2, 1,
		0.02, a/2, 1,
		toruswidth/2, -90, 2001,
		toruswidth/2 + 0.02, -a/2 + toruswidth/2, 1,
		toruswidth/2, -90, 2001,
		0, -a/2, 1


	del 1

return


! ==============================================================================
"SHADE_7_8":
! ==============================================================================

	for i=1 to 2
		hotspot2 0,  -B,unID,A2,1+128 : unID=unID+1
		hotspot2 -1, -B,unID,A2,3 : unID=unID+1
		hotspot2 A2, -B,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, 0,	unID,B,1+128: unID=unID+1
	hotspot2 0, 1,	unID,B,3	: unID=unID+1
	hotspot2 0, -B,	unID,B,2 	: unID=unID+1

	poly2_b 5, 1+2+64,gs_fill_pen,gs_back_pen,
		-a/2, 0, 1,
		 a/2, 0, 1,
		 a/2,  -b, 1,
		-a/2,  -b, 1,
		-a/2, 0, 1


return


! ==============================================================================
"SHADE_9":
! ==============================================================================

	for i=1 to 2
		hotspot2 0,  0,unID,A2,1+128 : unID=unID+1
		hotspot2 -1, 0,unID,A2,3 : unID=unID+1
		hotspot2 A2, 0,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, -B,	unID	: unID=unID+1

	poly2_b 4, 1+2+4+64, gs_fill_pen,gs_back_pen,
		-a/2, 0, 1,
		-a/2, -0.02, 0,
		 a/2, -0.02, 1,
		 a/2, 0, 1

	add2 0, -0.02
	mul2 a/0.2, 1
	for i = 1 to 2
		poly2_b 9, 1+2+64, gs_fill_pen,gs_back_pen,
			0.1,-0.01,1,
			0.09,-0.01,1,
			0.09,-0.03,901,
			0.02,45,4001,
			0,0.02,901,
			0,-0.08,3000,
			0, 0, 0,
			0.1, 0, 1,
			0.1, -0.01, 1
		mul2 -1, 1
	next i
	del 4

return


! ==============================================================================
"SHADE_10":
! ==============================================================================

	for i=1 to 2
		hotspot2 0,  0,unID,A2,1+128 : unID=unID+1
		hotspot2 -1, 0,unID,A2,3 : unID=unID+1
		hotspot2 A2, 0,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, 0,	unID,B,1+128: unID=unID+1
	hotspot2 0, 1,	unID,B,3	: unID=unID+1
	hotspot2 0, -B,	unID,B,2 	: unID=unID+1

	mul2 1, b/(a/2)
	poly2_b 3, 1+2+4+64,gs_fill_pen,gs_back_pen,
			a/2, 0, 1,
			0,0,901,
			0,-180,4001

	arc2 0,0,a/4,180,0
	del 1

	circle2 0,-b/8,b/10


return


! ==============================================================================
"SHADE_11":
! ==============================================================================

	poly2_b 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
		0,-b/10,1,
		0.015,-b/10,1,
		0.015,b/10,1,
		0,b/10,1,
		0,-b/10,-1

	poly2_b 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
		a,-b/30,1,
		0.015,-b/30,1,
		0.015,b/30,1,
		a,b/30,1,
		a,-b/30,-1

	poly2_b 2, 1+2+4+64,gs_fill_pen,gs_back_pen,
		a,0,901,
		b/2,360,4001

	circle2 a,0,b/15
	circle2 a,0,b/2-b/16
	line2 a+b/15,0,a+b/2,0
	line2 a-b/15,0,a-b/2,0
	line2 a,b/15,a,b/2
	line2 a,-b/15,a,-b/2

	hotspot2 a,0,		unID	: unID=unID+1
	hotspot2 a+b/2,0,	unID	: unID=unID+1
	hotspot2 a,b/2,		unID	: unID=unID+1
	hotspot2 a,-b/2,	unID	: unID=unID+1
	hotspot2 a-b/2,0,	unID	: unID=unID+1

return


! ==============================================================================
"SHADE_12":
! ==============================================================================

	for i=1 to 2
		hotspot2 0, 0,unID,A2,1+128 : unID=unID+1
		hotspot2 -1,0,unID,A2,3 : unID=unID+1
		hotspot2 A2,0,unID,A2,2 : unID=unID+1
		mul2 -1, 1
	next i
	del 2

	hotspot2 0, -A2,	unID	: unID=unID+1

	diameter2 = a
	diameter1 = 0.8*a

	poly2_b 3, 1+2+4+64,gs_fill_pen,gs_back_pen,
			diameter2/2, 0, 1,
			0,0,901,
			0,-180,4001

	arc2 0,0,diameter1/2,180,0

	add2 0, -0.001
	poly2_b 4, 1+2+4+64,gs_fill_pen,gs_back_pen,
		0.02,0,1,
		0,-0.015,901,
		0,-0.025,3001,
		-0.02,0,3001
	del 1

return


! ==============================================================================
"SHADE_13":
! ==============================================================================

	rot2 -90 * not(bMigrationRotZ)

	hotspot2 B/2,		0,	unID	: unID = unID + 1

	hotspot2 0,		0,	unID, 	A, 1+128	: unID = unID + 1
	hotspot2 0,		-A,	unID, 	A, 2		: unID = unID + 1
	hotspot2 0,		1,	unID, 	A, 3		: unID = unID + 1

	hotspot2 0,		0,	unID, 	A, 1+128	: unID = unID + 1
	hotspot2 0,		A,	unID, 	A, 2		: unID = unID + 1
	hotspot2 0,		-1,	unID, 	A, 3		: unID = unID + 1

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,	-A,		1,
		0,	0,		900,
		0,	180,	4001,
		0,	-A,		-1

	del 1

return


! ==============================================================================
"SHADE_14":
! ==============================================================================

	_halfAngle = 180 - 2 * atn(B/(2*A))
	_topRadius = B / (2 * sin(_halfAngle))

	rot2 -90 * not(bMigrationRotZ)

	hotspot2 0,		0,		unID, A, 1+128	: unID = unID + 1
	hotspot2 A,		0,		unID, A, 2		: unID = unID + 1
	hotspot2 -1,	0,		unID, A, 3		: unID = unID + 1

	hotspot2 0,		0,		unID, B2, 1+128,	B	: unID = unID + 1
	hotspot2 0,		B2,		unID, B2, 2,		B	: unID = unID + 1
	hotspot2 0,		-1,		unID, B2, 3,		B	: unID = unID + 1

	hotspot2 0,		0,		unID, B2, 1+128,	B	: unID = unID + 1
	hotspot2 0,		-B2,	unID, B2, 2,		B	: unID = unID + 1
	hotspot2 0,		1,		unID, B2, 3,		B	: unID = unID + 1

	rot2 -_halfAngle

	add2 (A - _topRadius) * cos(_halfAngle), (A - _topRadius) * sin(_halfAngle)

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		_topRadius,	0,				1,
		0,			0,				900,
		0,			2 * _halfAngle,	4001,
		_topRadius,	0,				-1

	del 3

return



