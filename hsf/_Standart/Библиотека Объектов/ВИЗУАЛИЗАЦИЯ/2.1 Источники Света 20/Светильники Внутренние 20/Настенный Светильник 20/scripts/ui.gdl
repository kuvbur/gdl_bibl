
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_CUSTOM_1 then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_CUSTOM_1 then
			gosub "lampStyleSettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_BASIC_PARAMS then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_BASIC_PARAMS then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_MODEL then
			gosub "modelSettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampStyleSettingsPage":

	ui_infield{3} "iShadeType", 0, 0, 444, 266,
		1, 1, 16, 4,
		105, 130, 101, 105,
		1,	shade_types[1],		1,
		2,	shade_types[2],		2,
		3,	shade_types[3],		3,
		4,	shade_types[4],		4,
		5,	shade_types[5],		5,
		6,	shade_types[6],		6,
		7,	shade_types[7],		7,
		8,	shade_types[8],		8,
		9,	shade_types[9],		9,
		10,	shade_types[10],	10,
		11,	shade_types[11],	11,
		12,	shade_types[12],	12,
		13,	shade_types[13],	13,
		14,	shade_types[14],	14

return


"lampGeometrySettingsPage":

	x1	= 2
	x2	= 132
	x3	= 234
	x4	= 364
	cx1	= 120
	cx2	= 80
	y1	= 160
	y2	= 184
	dy	= 24

	outFldHgt	= 15
	lgtInfHgt	= 19

	ui_separator 0,	y1-7,	444,	y1-7

	ui_style 0, 1
	ui_outfield `Размеры`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Ширина`,	x1,		y1,		cx1,	outFldHgt, (iShadeType = 13) * 4

	if iShadeType = 13 | iShadeType = 14 then
		ui_infield	"B",		x2,		y1-3,	cx2,	lgtInfHgt
	else
		ui_infield	"A",		x2,		y1-3,	cx2,	lgtInfHgt
	endif

	y1 = y1 + dy

	ui_outfield `Длина`,	x1,		y1,		cx1,	outFldHgt, (iShadeType = 9) * 4
	if iShadeType = 13 | iShadeType = 14 then
		ui_infield	"A",		x2,		y1-3,	cx2,	lgtInfHgt
	else
		ui_infield	"B",		x2,		y1-3,	cx2,	lgtInfHgt
	endif

	y1 = y1 + dy

	ui_outfield `Высота`,	x1,		y1,		cx1,	outFldHgt, (iShadeType = 13) * 4
	ui_infield	"ZZYZX",	x2,		y1-3,	cx2,	lgtInfHgt

return


"modelSettingsPage":

	x1	= 2
	x2	= 180
	cx1	= 175
	cx2	= 180
	y1	= 5
	dy	= 26

	outFldHgt	= 15
	strInfHgt	= 19
	matInfHgt	= 25

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x1,		y1,		cx1,	outFldHgt,	4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x2+cx2-50,		y1-3,	50,	strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	y1 = y1 + dy

	ui_outfield `Перо Конуса Света`,		x1,			y1,		cx1,	outFldHgt, 4 * (not(bShowIllumination3D) | gs_detlevel_3D_m = 0)
	ui_infield "penIllumination",		x2+cx2-33,	y1-3,	33,		strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	ui_separator 0,	y1-6,	444,	y1-6

	ui_style 0, 1
	ui_outfield `Покрытия`,			x1,		y1,		2 * cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0

	if (iShadeType <> 6) then
		ui_outfield	`Крепление`,			x1,		y1,		cx1,		outFldHgt
		ui_infield	"matFrame",			x2,		y1-5,	cx2,		matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType < 7) then
		ui_outfield	`Лампа`,				x1,		y1,		cx1,		outFldHgt
		ui_infield	"matLamp",			x2,		y1-5,	cx2,		matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType = 8 | iShadeType = 9 | iShadeType = 10 | iShadeType = 12) then
		ui_outfield	`Стекло`,			x1,		y1,		cx1,		outFldHgt
		ui_infield	"matGlassOther",	x2,		y1-5,	cx2,		matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType = 7 | iShadeType = 8) then
		if iShadeType = 7 then
			stGlassOutfield = `Стекло`
		else
			stGlassOutfield = `Лампа`
		endif

		ui_outfield	stGlassOutfield,	x1,		y1,		cx1,		outFldHgt, 4 * bLight
		ui_infield	"matGlass",			x2,		y1-5,	cx2,		matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType = 13) then
		ui_outfield	`Плафон`,			x1,		y1,		cx1,		outFldHgt
		ui_infield	"matShadeOut",		x2,		y1-5,	cx2,		matInfHgt
	endif

return

