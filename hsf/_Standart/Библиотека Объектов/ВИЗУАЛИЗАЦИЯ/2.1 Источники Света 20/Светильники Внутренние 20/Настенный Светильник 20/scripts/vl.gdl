
hideparameter all

call "FM_types" parameters all

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

values "A" range (0.1,)

if iShadeType = 6 then
	values "B" range (0, A/4 + 0.02)
else
	values "B" range (0,)
endif

values "A2" range (0.05,)
values "gs_resol" range[3,)
values "ZZYZX" range (0.1,)
values "B2" range (0.05,)

! --- 3D Detail Level ---

values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! --- C4D Parameters ---

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_OMNI,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes = 0,
									bEnableSymbolic2D	= 1


if gs_cont_pen<=0 then parameters gs_cont_pen=10
if gs_fill_pen<=0 then parameters gs_fill_pen=91

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif


if iShadeType = 9 then
	if GLOB_MODPAR_NAME="A2" then
		A = A2*2
		parameters A = A
	else
		A2 = A/2
		parameters A2 = A2
	endif
else
	if iShadeType <> 11 then
		if iShadeType = 1 then
			if GLOB_MODPAR_NAME="ZZYZX" then
				A 		= ZZYZX + 0.0275
				A2 		= ZZYZX/2
				B 		= ZZYZX
				parameters A = A, A2 = A2, B = B
			else
				if GLOB_MODPAR_NAME="A2" then
					A 		=	A2*2 + 0.0275
					B 		=	A2*2
					ZZYZX 	=	A2*2
					parameters A = A, B = B, ZZYZX = ZZYZX
				else
					A2 		= (A-0.0275)/2
					B 		= A-0.0275
					ZZYZX 	= A-0.0275
					parameters A2 = A2, B = B, ZZYZX = ZZYZX
				endif
			endif
		else
			if GLOB_MODPAR_NAME="ZZYZX" then
				if iShadeType <> 14 then
					A 	= ZZYZX
					A2 	= ZZYZX/2
					parameters A = A, A2 = A2
				endif
			else
				if  iShadeType = 10 then
					if GLOB_MODPAR_NAME="A2" then
						A 		= A2*2
						ZZYZX 	= ZZYZX
						parameters A = A, ZZYZX = ZZYZX
					else
						A2 		= A/2
						ZZYZX 	= ZZYZX
						parameters A2 = A2, ZZYZX = ZZYZX
					endif
				else
					if GLOB_MODPAR_NAME="A2" then
						A 		= A2*2
						ZZYZX 	= A2*2
						parameters A = A, ZZYZX = ZZYZX
					else
						if iShadeType <> 14 then
							A2 		= A/2
							ZZYZX 	= A
							parameters A2 = A2, ZZYZX = ZZYZX
						else
							A2 		= A/2
							parameters A2 = A2
						endif
					endif
				endif
			endif
		endif
	endif
endif

if iShadeType = 9 then
	lock "B"
endif

if iShadeType = 13 then
	B = 2 * A
	ZZYZX = A
	parameters B = B, ZZYZX = ZZYZX
	lock "B", "ZZYZX"
endif

if GLOB_MODPAR_NAME="B2" then
	if iShadeType = 14 then
		B = B2 * 2
		parameters B = B
	else
		ZZYZX = B2*2
		parameters ZZYZX = ZZYZX
	endif
else
	if iShadeType = 14 then
		B2 = B/2
		parameters B2 = B2
	else
		B2 = ZZYZX/2
		parameters B2 = B2
	endif
endif



! --- Synhronized mm and point in marker width ---

Dot_in_mm = 25.4 / 72

if GLOB_MODPAR_NAME = "fsz" then
	fsz_point = fsz / Dot_in_mm
	parameters fsz_point = fsz_point
else
	fsz = fsz_point * Dot_in_mm
	parameters fsz = fsz
endif

! --- Bottom, Top Level Calculation  ---

if iShadeType = 1 or iShadeType = 2 or iShadeType = 3 or iShadeType = 4 or iShadeType = 5 or iShadeType = 6 or iShadeType = 7 or iShadeType = 8 then
	ac_bottomlevel = -A / 2
	ac_toplevel = A / 2
else
	if iShadeType = 9 or iShadeType = 10 or iShadeType = 12 or iShadeType = 13 or iShadeType = 14 then
		ac_bottomlevel = 0
		ac_toplevel = ZZYZX
	else
		ac_bottomlevel = -0.1
		headRad = SQR((b/2-b/16) ** 2 + b ** 2)
		ac_toplevel = ZZYZX + 0.001 + COS(ATN(0.001 / (b + b / 10))) * headRad - b
	endif
endif
parameters	ac_bottomlevel	= ac_bottomlevel,
			ac_toplevel		= ac_toplevel


! --- Shade Types ---
values "strShadeType" shade_types
values "iShadeType" 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14
if GLOB_MODPAR_NAME = "strShadeType" then
	iShadeType = 1
	for i = 2 to vardim1(shade_types)
		if strShadeType = shade_types[i] then
			iShadeType = i
			i = vardim1(shade_types)	! break
		endif
	next i
	parameters iShadeType = iShadeType
else
	strShadeType = shade_types[max(min(iShadeType, vardim1(shade_types)),1)]
	parameters strShadeType = strShadeType
endif

!--- Set default parameters ---

if GLOB_MODPAR_NAME = "strShadeType" or GLOB_MODPAR_NAME = "iShadeType" then

	if iShadeType = 1 then
		A		= 0.28
		A2		= (A - 0.0275)/2
		B		= A - 0.0275
		ZZYZX	= A - 0.0275
	endif

	if iShadeType = 2 then
		A		= 0.25
		A2		= 0.125
		B		= 0.150
		ZZYZX	 = 0.25
	endif

	if iShadeType = 3 or iShadeType = 4 or iShadeType = 5 or iShadeType = 6 then
		A		= 0.3
		A2		= 0.15
		B		= 0.08
		ZZYZX	= 0.3
	endif

	if iShadeType = 7  or iShadeType = 8 then
		A		= 0.4
		A2		= 0.2
		B		= 0.05
		ZZYZX	= 0.4
	endif

	if iShadeType = 9 then
		A		= 0.2
		A2		= 0.1
		B		= 0.0839
		ZZYZX	= 0.4
	endif

	if iShadeType = 10 then
		A		= 0.3
		A2		= 0.15
		B		= 0.15
		ZZYZX	= 0.4
		
		matGlassOther = 29			! "Glass-Blue"
		parameters matGlassOther = matGlassOther
	endif

	if iShadeType = 11 then
		A		= 0.4
		A2		= 0.2
		B		= 0.54
		ZZYZX	= 0.65
	endif

	if iShadeType = 12 then
		A		= 0.30
		A2		= A/2
		B		= A/2
		ZZYZX	= 0.40
	endif

	if iShadeType = 13 then
		A = 0.20
	endif

	if iShadeType = 14 then
		A		= 0.3
		B		= 0.5
		ZZYZX	= 0.25
	endif

	parameters	A		= A,
				A2		= A2,
				B		= B,
				ZZYZX	= ZZYX
endif

values "gs_ui_current_page" pageValues


! --- Materials

if bLight then
	lock "matGlass"
endif

if 	iShadeType = 1 | iShadeType = 2 | \
	iShadeType = 3 | iShadeType = 4 | iShadeType = 5 then

	hideparameter	"matGlass", "matGlassOther", "matShadeOut"
	lock			"matGlass", "matGlassOther", "matShadeOut"
else
	if iShadeType = 6 then
		hideparameter	"matFrame", "matGlass", "matGlassOther", "matShadeOut"
		lock			"matFrame", "matGlass", "matGlassOther", "matShadeOut"
	else
		hideparameter	"matLamp"
		lock			"matLamp"

		if not(iShadeType = 13) then
			hideparameter	"matShadeOut"
			lock			"matShadeOut"
		endif

		if iShadeType = 7 then
			hideparameter	"matGlassOther"
			lock			"matGlassOther"
		else
			if not(iShadeType = 8) then
				hideparameter	"matGlass"
				lock			"matGlass"

				if iShadeType = 11 | iShadeType = 13 | iShadeType = 14 then
					hideparameter	"matGlassOther"
					lock			"matGlassOther"
				endif
			endif
		endif
	endif
endif


