
If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


unID=1

hotspot	0,0,0,unID : unID=unID+1

hotspot	0.08,0,0,unID : unID=unID+1
hotspot	0,0.08,0,unID : unID=unID+1
hotspot	-0.08,0,0,unID : unID=unID+1
hotspot	0,-0.08,0,unID : unID=unID+1

ROTZ 90

! --- Base ---

pen gs_cont_pen
material matFrame

roty -90
RESOL 1.5*gs_resol
revolve 6,360,1+2,
		0,0.08,1,
		0,0.07,901,
		0.01,0.07,3000,
		0.01,0.07,1,
		-0.015,0,901,
		0.05,0.03,3001

material matComp
revolve 3,360,1+2,
		0.0525,0.031,1,
		-0.01,0,901,
		0.06,0.001,3001

! --- ARM ---

material matFrame

add -0.012,0.003,0

	r1=0.01
	if gs_detlevel_3D_m=1 then
		resol 4
	else
		resol 12
	endif
	armc 0.07,0.005,0.072,0,0.045,90

del 1

add 0.065,0.048,0
rotz -45+alf
addy -R

add 0,R,-0.006

	 prism_ 2,0.012,
		0,0,979,
		0.005,360,4079

del 1

roty 90
	resol GS_RESOL
	 revolve 2,90,1+2+16+32,
		0,R,901,
		0.003,360,4001

del 1

add R,0,-0.006

if gs_detlevel_3D_m=1 then
	resol 4
else
	resol 12
endif
prism_ 2,0.012,
		0,0,979,
		0.005,360,4079

! --- LAMP HEAD ---

rotz 45-alf
add -0.005,-0.045,0.006
	resol gs_resol
	revolve 7, 360, 2,
		0,			0.035,	1,
		0,			0.04,	1,
		0,			0.035,	901,
		0.005,		0.035,	3000,
		0.005,		0.035,	1,
		-0.0075,	0,		901,
		0.025,		0.015,	3001

if not(gs_light_switch) then
	material matGlass

	roty 90
	rotz 180/gs_resol
	circle 0.035
	del 2
endif

material matComp

	 revolve 3, 360, 1+2,
		0.02625,	0.0155,	1,
		-0.005,		0,		901,
		0.03,		0.0005,	3001

! --- LIGHT ---


If gs_light_switch Then

	roty 90
	addz 0.0002

	! ======================================================================
	! Non Visible Omni Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
											gs_color_red						= gs_color_red,
											gs_color_green						= gs_color_green,
											gs_color_blue						= gs_color_blue,
											gs_light_intensity						= gs_light_intensity,
											iColorPreset			= iColorPreset,
											iLightGroupType			= LIGHTGROUP_BUILTIN_NONVIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_DISC,
											c4dDetAreaX				= 0.07,
											c4dDetAreaY				= 0.07,
											c4dDetAreaZ				= 0.03,
											bGenShadow				= bGenShadow,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											iFalloffType			= FALLOFFTYPE_CONE,
											c4dDetRadius			= c4dDetRadius,
											c4dDetFalloffAngle		= c4dDetFalloffAngle,
											falloffAngleTemp		= falloffAngleTemp,
											c4dPhoPhotometric		= c4dPhoPhotometric,
											iC4dPhoUnit				= iC4dPhoUnit,
											photoIntensityLumen		= photoIntensityLumen,
											photoIntensityCandela	= photoIntensityCandela,
											bShowIllumination3D		= bShowIllumination3D,
											gs_cont_pen				= gs_cont_pen,
											penIllumination			= penIllumination,
											iModelType				= iModelType,
											lightUnID				= unID


	! ======================================================================
	! Visible Area Light
	! ======================================================================

	rotz -90

	call	"lightMacro_m" parameters		gs_light_switch			= gs_light_switch,
											gs_color_red			= gs_color_red,
											gs_color_green			= gs_color_green,
											gs_color_blue			= gs_color_blue,
											gs_light_intensity		= 400,
											iLightGroupType			= LIGHTGROUP_BUILTIN_VIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_DISC,
											c4dDetAreaX				= 0.07,
											c4dDetAreaY				= 0.07,
											bGenShadow				= 1,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											c4dDetRadius			= 0,
											c4dDetFalloffAngle		= 0,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											bShowIllumination3D		= 0

	del 3

EndIf

del 8
