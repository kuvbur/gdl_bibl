
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_BASIC_PARAMS then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_BASIC_PARAMS then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_FPLAN_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_FPLAN_MODEL then
			gosub "displaySettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	x1	= 2
	x2	= 132
	x3	= 234
	x4	= 364
	cx1	= 120
	cx2	= 80
	y1	= 160
	dy	= 24

	outFldHgt	= 15
	lgtInfHgt	= 19

	ui_separator 0,	y1-7,	444,	y1-7

	ui_style 0, 1
	ui_outfield `Размеры`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Плафон по X`,	x1,		y1,		cx1,	outFldHgt
	ui_infield	"A",				x2,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy

	ui_outfield	`Плафон по Z`,	x1,		y1,		cx1,	outFldHgt
	ui_infield	"ZZYZX",			x2,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy

	ui_outfield	`Длина Кронштейна`,		x1,		y1,		cx1,	outFldHgt
	ui_infield	"armLength",		x2,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 - 2 * dy

	ui_outfield	`Угол Плафона по X`,	x3,		y1,		cx1,	outFldHgt
	ui_infield	"shadeRotX",		x4,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy

	ui_outfield	`Угол Плафона по Y`,	x3,		y1,		cx1,	outFldHgt
	ui_infield	"shadeRotY",		x4,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy


return


"displaySettingsPage":

	dim	surfaceBlock[2][2]
		surfaceBlock[1][1]	= `Кронштейн`		:	surfaceBlock[1][2] = "matFrame"
		surfaceBlock[2][1]	= `Пластик`	:	surfaceBlock[2][2] = "matPlast"

	dim	flSurfaceBlk[2]
		flSurfaceBlk[1]		= 0
		flSurfaceBlk[2]		= 0

	call "ui_lampDisplaySettings_m" parameters all	bEnableModelType	= 1,
													pageStartY			= 3,
													stDetailLevel		= stDetailLevel,
													surfaceBlock		= surfaceBlock,
													flSurfaceBlk		= flSurfaceBlk

return

