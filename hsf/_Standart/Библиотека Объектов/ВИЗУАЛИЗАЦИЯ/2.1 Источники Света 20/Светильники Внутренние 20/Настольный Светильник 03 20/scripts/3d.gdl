
If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


!!---- Geometry Hotspot Editing
unID=1

hotspot	0,0,0,unID : unID=unID+1
hotspot	0.055,0,0,unID : unID=unID+1
hotspot	0,0.055,0,unID : unID=unID+1
hotspot	-0.055,0,0,unID : unID=unID+1
hotspot	0,-0.055,0,unID : unID=unID+1

addz 0.273
hotspot 0,-0.047,0,unID,ala,6 : unID=unID+1
hotspot 1,-0.047,0,unID,ala,4+128 : unID=unID+1
hotspot 0,-1,0,unID,ala,7 : unID=unID+1
hotspot 0.378*cos(ala),-0.047,0.378*sin(ala),unID,ala,5 : unID=unID+1

hotspot 0,0.047,0,unID,ala,6 : unID=unID+1
hotspot 1,0.047,0,unID,ala,4+128 : unID=unID+1
hotspot 0,-1,0,unID,ala,7 : unID=unID+1
hotspot 0.378*cos(ala),0.047,0.378*sin(ala),unID,ala,5 : unID=unID+1

roty -ala
addx 0.378
roty ala

hotspot 0,-0.042,0,unID,aua,6 : unID=unID+1
hotspot 1,-0.042,0,unID,aua,4+128 : unID=unID+1
hotspot 0,-1,0,unID,aua,7 : unID=unID+1
hotspot 0.506*cos(aua),-0.042,0.506*sin(aua),unID,aua,5 : unID=unID+1

hotspot 0,0.042,0,unID,aua,6 : unID=unID+1
hotspot 1,0.042,0,unID,aua,4+128 : unID=unID+1
hotspot 0,-1,0,unID,aua,7 : unID=unID+1
hotspot 0.506*cos(aua),0.042,0.506*sin(aua),unID,aua,5 : unID=unID+1

del 4

!!---- Geometry Hotspot Editing

pen gs_cont_pen
resol gs_resol
material matFrame

let a=0
let b=0

! Trafozylinder
cylind 0.01,0.055

material matPlast

addz 0.01
cylind 0.07,0.055
addz 0.07
addy -0.055

! Auflagearm senkrecht

material matFrame

addx -0.01
brick 0.02,0.005,0.20
addy 0.105
brick 0.02,0.005,0.20
del 2

! Arm unten

addx 0.0075
addz 0.19
roty -ala
addy 0.005
addx -0.16
addz -0.0425

material matPlast

brick 0.015,0.10,0.10

material matFrame

del 1
addx 0.015
brick 0.52,0.005,0.015
addy 0.005
brick 0.015,0.09,0.015
addy 0.09
brick 0.52,0.005,0.015
del 11

! zweiter Arm
! Drehpunkt

addy -0.055
addz 0.27

! Einstellung des zweiten Drehpunktes durch
! Winkelfunktionen

let n=cos(ala)*0.37
let m=sin(ala)*0.37

addz m
addx n
roty -aua
addy 0.01
addx -0.20
addz -0.035

material matPlast

brick 0.01,0.09,0.08

material matFrame

del 1
addx 0.01
brick 0.705,0.005,0.01
addy 0.005
brick 0.01,0.08,0.01
addy 0.08
brick 0.705,0.005,0.01

del 9

addz m+0.27
addx n

! Einstellung des Drehpunktes Des Leuchtkorpers
! uber die Winkelfunktionen

let o=cos(aua)*0.52
let p=sin(aua)*0.52

addz p
addx o

roty 0

addy 0.095
addx -0.015
addz -0.015
rotx 90

material matPlast

addz 0.001
prism_ 6, 0.078,
	0,		0,		7,
	0.09,	0,		15,
	0.06,	0.03,	15,
	-0.02,	0.03,	15,
	-0.05,	0.06,	15,
	-0.055,	0.055,	15

if not(gs_light_switch) then
	material matGlass

	prism 4, 0.078,
		0,		0,
		0.09,	0,
		0.09,	-0.002,
		0,		-0.002
endif
del 1

addx 0.045
addz 0.04

rotz -90

!!---- Light Cone ----!!

resol 36
If gs_light_switch Then

	roty -90

	! ======================================================================
	! Non Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
											gs_color_red						= gs_color_red,
											gs_color_green						= gs_color_green,
											gs_color_blue						= gs_color_blue,
											gs_light_intensity						= gs_light_intensity,
											iColorPreset			= iColorPreset,
											iLightGroupType			= LIGHTGROUP_BUILTIN_NONVIS_AREA,
											iC4dDetAreaShape		= AREASHAPE_RECT,
											c4dDetAreaX				= 0.078,
											c4dDetAreaY				= 0.09,
											c4dDetAreaZ				= 0.03,
											bGenShadow				= bGenShadow,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											iFalloffType			= FALLOFFTYPE_PYRAMID,
											c4dDetRadius			= c4dDetRadius,
											c4dDetFalloffAngle		= c4dDetFalloffAngle,
											falloffAngleTemp		= falloffAngleTemp,
											c4dPhoPhotometric		= c4dPhoPhotometric,
											iC4dPhoUnit				= iC4dPhoUnit,
											photoIntensityLumen		= photoIntensityLumen,
											photoIntensityCandela	= photoIntensityCandela,
											bShowIllumination3D		= bShowIllumination3D,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											penIllumination			= penIllumination,
											iModelType				= iModelType,
											lightUnID				= unID

	! ======================================================================
	! Visible Area Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch		= gs_light_switch,
											gs_color_red		= gs_color_red,
											gs_color_green		= gs_color_green,
											gs_color_blue		= gs_color_blue,
											gs_light_intensity	= 400,
											iLightGroupType		= LIGHTGROUP_BUILTIN_VIS_AREA,
											iC4dDetAreaShape	= AREASHAPE_RECT,
											c4dDetAreaX			= 0.078,
											c4dDetAreaY			= 0.09,
											bGenShadow			= 1,
											iShadowQuality		= iShadowQuality,
											iShadowQualityGroupType	= 2,
											iC4dGenVisibility	= 1,
											bDetShowFalloff 	= 1,
											c4dDetRadius		= 0,
											c4dDetFalloffAngle	= c4dDetFalloffAngle,
											gs_resol			= gs_resol,
											gs_cont_pen			= gs_cont_pen,
											bShowIllumination3D	= 0

EndIf

del top
