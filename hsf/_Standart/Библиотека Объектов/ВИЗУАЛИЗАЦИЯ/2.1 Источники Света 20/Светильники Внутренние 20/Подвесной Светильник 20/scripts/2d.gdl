

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
	gs_symbtype_2D_m = 1
endif

!--- Hotspot Editing
unID=1

hotspot2 0, 0, unID : unID=unID+1


! ==============================================================================
!	Realistic Symbol
! ==============================================================================

if gs_symbtype_2D_m = 1 then

	pen gs_cont_pen
	fill gs_fill_type

	if iShadeType = 1 or iShadeType = 3 then
		for i=1 to 4
			hotspot2		0,		0,		unID,	D12,	1+128,	width	: unID=unID+1
			hotspot2		-1,		0,		unID,	D12,	3,		width	: unID=unID+1
			hotspot2 diameter1/2,	0,		unID,	D12,	2,		width	: unID=unID+1
			rot2 90
		next i
		del 4
	endif

	if iShadeType = 2 then
		for i=1 to 4
			hotspot2		0,		0,		unID,	D12,	1+128,	bottomwidth	: unID=unID+1
			hotspot2		-1,		0,		unID,	D12,	3,		bottomwidth	: unID=unID+1
			hotspot2 diameter1/2,	0,		unID,	D12,	2,		bottomwidth	: unID=unID+1
			rot2 90
		next i
		del 4
	endif

	if iShadeType = 4 then
		for i=1 to 4
			hotspot2		0,		0,		unID,	D12,	1+128,	diameter	: unID=unID+1
			hotspot2		-1,		0,		unID,	D12,	3,		diameter	: unID=unID+1
			hotspot2 diameter1/2,	0,		unID,	D12,	2,		diameter	: unID=unID+1
			rot2 90
		next i
		del 4
	endif

	if iShadeType = 5 or iShadeType = 6 or iShadeType = 8 or iShadeType = 9 or iShadeType = 11 or \
		iShadeType = 12 or iShadeType = 14 then
		for i=1 to 4
			hotspot2		0,		0,		unID,	D12,	1+128,	diameter1	: unID=unID+1
			hotspot2		-1,		0,		unID,	D12,	3,		diameter1	: unID=unID+1
			hotspot2 diameter1/2,	0,		unID,	D12,	2,		diameter1	: unID=unID+1
			rot2 90
		next i
		del 4
	endif

	if iShadeType = 7 or iShadeType = 9 or iShadeType = 10 or iShadeType = 11 or iShadeType = 12 or \
		iShadeType = 13 then		!!! or iShadeType = 14
		for i=1 to 4
			hotspot2 0,			0 ,	unID,	D2,	1+128,	diameter : unID=unID+1
			hotspot2 -1,		0 ,	unID,	D2,	3,		diameter : unID=unID+1
			hotspot2 diameter/2,0 ,	unID,	D2,	2,		diameter : unID=unID+1
			rot2 90
		next i
		del 4
	endif

	if iShadeType = 8 then
		for i=1 to 4
			hotspot2 diameter/2,	0 ,	unID	: unID=unID+1
			rot2 90
		next i
		del 4
	endif

	if (iShadeType <> 1 and iShadeType <> 2 and iShadeType <> 3) then

		circle2 0, 0, A/2
		circle2 0, 0, 0.05
		circle2 0, 0, 0.005

		poly2_b 2, 2+4+64,      gs_fill_pen,     gs_back_pen,
				0,0,901,
				A/2,360,4001

	else

		rect2 -diameter1/2, -diameter1/2, diameter1/2, diameter1/2
		circle2 0, 0, 0.05
		circle2 0, 0, 0.005

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-A/2, -A/2, 1,
				 A/2, -A/2, 1,
				 A/2,  A/2, 1,
				-A/2,  A/2, 1

	endif

	if iShadeType = 8 or iShadeType = 9 or iShadeType = 11 or iShadeType = 12 then
		if iShadeType <> 11 and iShadeType <> 12 then
			line_type gs_hidden_line_type
		endif
		circle2 0, 0, diameter1/2
	endif

endif



! ==============================================================================
!	Electrical Symbol or RCP Symbol
! ==============================================================================

if gs_symbtype_2D_m = 2 or gs_symbtype_2D_m = 3 then

	call "Lamp_symbol" parameters all \
									bWallLamp = 0,
									bDirect = 0,
									Symbol_unID = unID,
									specSymbolRotation=180

endif



! ==============================================================================
!	Realistic Symbol / New hotspots
! ==============================================================================

if gs_symbtype_2D_m = 1 then

	if (iShadeType <> 1 and iShadeType <> 2 and iShadeType <> 3) then

		if iShadeType = 5 then
			for i=1 to 4
				hotspot2  0, 0,	unID,	D22, 1+128,	diameter2 : unID=unID+1
				hotspot2 -1, 0,	unID,	D22, 3,		diameter2 : unID=unID+1
				hotspot2 D22, 0,unID,	D22, 2,		diameter2 : unID=unID+1
				rot2 90
			next i
			del 4
			circle2 0, 0, diameter2/2
		endif

	else

		hotspot2  -diameter1/2, -diameter1/2,	unID	: unID=unID+1
		hotspot2  diameter1/2, -diameter1/2,	unID	: unID=unID+1
		hotspot2  diameter1/2,  diameter1/2,	unID	: unID=unID+1
		hotspot2  -diameter1/2, diameter1/2,	unID	: unID=unID+1

		if iShadeType = 2 then
			for i=1 to 4
				hotspot2  0, 0,	unID,	D22, 1+128,	topwidth : unID=unID+1
				hotspot2 -1, 0,	unID,	D22, 3,		topwidth : unID=unID+1
				hotspot2 D22, 0,unID,	D22, 2,		topwidth : unID=unID+1
				rot2 90
			next i
			del 4
			rect2 -diameter2/2, -diameter2/2, diameter2/2, diameter2/2
		endif

	endif

endif
