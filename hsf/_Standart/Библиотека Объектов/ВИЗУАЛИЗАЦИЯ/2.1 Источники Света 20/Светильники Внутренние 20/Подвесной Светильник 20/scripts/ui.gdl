
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_CUSTOM_1 then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_CUSTOM_1 then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_MODEL then
			gosub "modelSettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	! Shade Settings

	x1		= 2
	x2		= 122
	y1		= 3
	dy		= 24

	outfWdt	= 115
	outfHgt	= 15
	infWdt	= 95
	infHgt	= 19


	ui_style 0, 1
	ui_outfield	`Плафон`,		x1,		y1,			outfWdt,	outfHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Стиль Плафона`,	x1,		y1,			outfWdt,	outfHgt
	ui_infield{3} "iShadeType",	x2,		y1-3,		infWdt,		105,
		2, 1, 15, 3,
		80, 90, 80, 90,
		15,	shade_types[15],	15,
		1,	shade_types[1],		1,
		2,	shade_types[2],		2,
		3,	shade_types[3],		3,
		4,	shade_types[4],		4,
		5,	shade_types[5],		5,
		6,	shade_types[6],		6,
		7,	shade_types[7],		7,
		8,	shade_types[8],		8,
		9,	shade_types[9],		9,
		10,	shade_types[10],	10,
		11,	shade_types[11],	11,
		12,	shade_types[12],	12,
		13,	shade_types[13],	13,
		14,	shade_types[14],	14

	y1 = y1 + 111

	if iShadeType > 6 & iShadeType < 15 & iShadeType <> 8 then
		ui_outfield	`Диаметр`,			x1,		y1,		outfWdt,	outfHgt
		ui_infield	"diameter",			x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy
	endif

	if	iShadeType = 4 | iShadeType = 5 | iShadeType = 6 | iShadeType = 8 | \
		iShadeType = 9 | iShadeType = 11 | iShadeType = 12 | iShadeType = 14 then

		ui_outfield	`Верхний Диаметр`,		x1,		y1,		outfWdt,	outfHgt
		ui_infield	"diameter1",		x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy
	endif

	if iShadeType = 5 then
		ui_outfield	`Нижний Диаметр`,	x1,		y1,		outfWdt,	outfHgt
		ui_infield	"diameter2",		x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy
	endif

	if iShadeType = 1 | iShadeType = 3 then
		ui_outfield	`Ширина`,			x1,		y1,		outfWdt,	outfHgt
		ui_infield	"width",			x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy
	endif

	if iShadeType = 2 then
		ui_outfield	`Верхняя Ширина`,		x1,		y1,		outfWdt,	outfHgt
		ui_infield	"topwidth",			x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy

		ui_outfield	`Нижняя Ширина`,		x1,		y1,		outfWdt,	outfHgt
		ui_infield	"bottomwidth",		x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy
	endif

	if iShadeType <> 15 then
		ui_outfield	`Высота Плафона`,		x1,		y1,		outfWdt,	outfHgt
		ui_infield	"shadeheight",		x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy

		ui_outfield	`Толщина Плафона`,	x1,		y1,		outfWdt,	outfHgt
		ui_infield	"shadethk",			x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy
	endif

	ui_outfield	`Общая Высота`,		x1,		y1,		outfWdt,	outfHgt
	ui_infield	"ZZYZX",			x2,		y1-3,	infWdt,		infHgt

	ui_separator 222, 0,	222, 266

	! Mounting Settings

	x3		= 227
	x4		= 348
	y2		= 3
	dy		= 24

	outfWdt	= 115
	outfHgt	= 15
	infWdt	= 95
	infHgt	= 19

	ui_style 0, 1
	ui_outfield	`Крепление`,			x3,		y2,		outfWdt,	outfHgt

	y2 = y2 + dy

	ui_style 0, 0
	ui_infield{3} "iMountType",		x4,		y2-3,	infWdt,		105,
		2, 2, 6, 3,
		80, 90, 80, 90,
		5, mount_types[5], 5,
		1, mount_types[1], 1,
		2, mount_types[2], 2,
		3, mount_types[3], 3,
		4, mount_types[4], 4

	y2 = y2 + 111

	ui_outfield	`Тип Подвеса`,		x3,		y2,		infWdt,		outfHgt
	ui_infield{3} "iSuspenderType",	x4,		y2-3,	infWdt,		infHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, suspender_types[1], 1,
		1, suspender_types[2], 2,
		1, suspender_types[3], 3

	y2 = y2 + dy

	ui_outfield	`Диаметр Штока`,	x3,		y2,		outfWdt,	outfHgt, not(iSuspenderType = 2) * 4
	ui_infield	"rodthk",			x4,		y2-3,	infWdt,		infHgt

return


"modelSettingsPage":

	x1	= 2
	x2	= 180
	cx1	= 175
	cx2	= 180
	y1	= 5
	dy	= 26

	outFldHgt	= 15
	strInfHgt	= 19
	matInfHgt	= 25

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1,stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x1,		y1,		cx1,	outFldHgt,	4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x2+cx2-50,		y1-3,	50,	strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	y1 = y1 + dy

	ui_outfield `Перо Конуса Света`,		x1,			y1,		cx1,	outFldHgt, 4 * (not(bShowIllumination3D) | gs_detlevel_3D_m = 0)
	ui_infield "penIllumination",		x2+cx2-33,	y1-3,	33,		strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	ui_separator 0,	y1-6,	444,	y1-6

	ui_style 0, 1
	ui_outfield `Покрытия`,			x1,		y1,		2 * cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Подвес`,			x1,		y1,		cx1,		outFldHgt
	ui_infield	"matFrame",			x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Плафон Снаружи`,	x1,		y1,		cx1,		outFldHgt, (iSHadeType = 15) * 4
	ui_infield	"matShadeOut",		x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Плафон Внутри`,		x1,		y1,		cx1,		outFldHgt, (iSHadeType = 15) * 4
	ui_infield	"matShadeIn",		x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

return


