
hideparameter all

call "FM_types" parameters all

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m


values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

values "gs_resol" range [8, )

if gs_cont_pen=0 then parameters gs_cont_pen=10
if gs_fill_pen=0 then parameters gs_fill_pen=91

!!if iShadeType <> 15 then
	lock "strCustomShade"
	hideparameter "strCustomShade"
!!endif


! ==============================================================================
!	Light Settings
! ==============================================================================

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_OMNI,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes = 0,
									bEnableSymbolic2D	= 1


! --- Synhronized mm and point in marker width ---

Dot_in_mm = 25.4 / 72

if GLOB_MODPAR_NAME = "fsz" then
	fsz_point = fsz / Dot_in_mm
	parameters fsz_point = fsz_point
else
	fsz = fsz_point * Dot_in_mm
	parameters fsz = fsz
endif


parameters ac_bottomlevel = -ZZYZX
parameters ac_toplevel = 0


! ----------- STRING VALUES

values "strShadeType" shade_types[15], shade_types[1], shade_types[2], shade_types[3], shade_types[4], shade_types[5],
		shade_types[6], shade_types[7], shade_types[8], shade_types[9], shade_types[10], shade_types[11], shade_types[12],
		shade_types[13], shade_types[14]
values "strMountType" mount_types[5], mount_types[1], mount_types[2], mount_types[3], mount_types[4]
values "strSuspenderType" suspender_types


! ----------- INTEGER VALUES

values "iShadeType" 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15
values "iMountType" 1, 2, 3, 4, 5
values "iSuspenderType" 1, 2, 3


! ---------- STRING - INTEGER CONNECTION

! --- Shade Types ---

if GLOB_MODPAR_NAME = "strShadeType" then
	iShadeType = 1
	for i = 1 to vardim1(shade_types)
		if strShadeType = shade_types[i] then iShadeType = i
	next i
	parameters iShadeType = iShadeType
else
	strShadeType = shade_types[max(min(iShadeType, vardim1(shade_types)), 1)]
	parameters strShadeType = strShadeType
endif

! --- Mount Types ---

if GLOB_MODPAR_NAME = "strMountType" then
	iMountType = 1
	for i = 1 to vardim1(mount_types)
		if strMountType = mount_types[i] then iMountType = i
	next i
	parameters iMountType = iMountType
else
	strMountType = mount_types[max(min(iMountType, vardim1(mount_types)), 1)]
	parameters strMountType = strMountType
endif

! --- Suspender Types ---

if GLOB_MODPAR_NAME = "strSuspenderType" then
	iSuspenderType = 1
	for i = 1 to vardim1(suspender_types)
		if strSuspenderType = suspender_types[i] then iSuspenderType = i
	next i
	parameters iSuspenderType = iSuspenderType
else
	strSuspenderType = suspender_types[max(min(iSuspenderType, vardim1(suspender_types)), 1)]
	parameters strSuspenderType = strSuspenderType
endif


!--- Set parameter values ---

if iShadeType = 8 then
	values "diameter1" range[0.1, shadeheight*2]
	values "width" range[0.1, shadeheight*2]
	values "bottomwidth" range[0.1, shadeheight*2]
	values "D12" range[0.05, shadeheight]
else
	if iShadeType = 9 then
		values "diameter1" range[0.1, diameter]
		values "width" range[0.1, diameter]
		values "bottomwidth" range[0.1, diameter]
		values "D12" range[0.05, diameter/2]
	else
		if iShadeType = 12 then
			values "diameter1" range[max(0.1,(diameter-shadeheight+eps)), (diameter+shadeheight-eps)]
			values "width" range[max(0.1,(diameter-shadeheight+eps)), (diameter+shadeheight-eps)]
			values "bottomwidth" range[max(0.1,(diameter-shadeheight+eps)), (diameter+shadeheight-eps)]
			values "D12" range[max(0.05,(diameter-shadeheight+eps)/2), (diameter+shadeheight-eps)/2 ]
		else
			values "diameter1" range[0.1,)
			values "width" range[0.1,)
			values "bottomwidth" range[0.1,)
			values "D12" range[0.05, )
		endif
	endif
endif

if iShadeType = 11 then
	values "shadeheight" range [0.1, min( (sqr((diameter/2)**2-0.05**2))*2, zzyzx-0.1)]
else
	if iShadeType = 12 then
		values "shadeheight" range [max(0.1,abs(diameter-diameter1)), zzyzx-0.1]
	else
		if iShadeType <> 15 then
			values "shadeheight" range [0.1, zzyzx-0.1]
		endif
	endif
endif

if iShadeType = 10 then
	values "diameter" range[0.1, shadeheight*2]
	values "D2" range[0.05, shadeheight]
else
	if iShadeType = 11 then
		values "diameter" range [2*sqr((shadeheight/2)**2+0.05**2), )
		values "D2" range [sqr((shadeheight/2)**2+0.05**2), )
	else
		if iShadeType = 12 then
			values "diameter" range[max(0.1,(diameter1-shadeheight+eps)), (diameter1+shadeheight-eps)]
			values "D2" range[max(0.05,(diameter1-shadeheight+eps)/2), (diameter1+shadeheight-eps)/2]
		else
			if iShadeType = 9 then
				values "diameter" range[0.15,)
				values "D2" range[0.075, )
			else
				values "diameter" range[0.1,)
				values "D2" range[0.05, )
			endif
		endif
	endif
endif

values "A2" range[0.05,)
values "ZZYZX" range[shadeheight + 0.2,)
values "D22" range[0.05,)
values "diameter2" range[0.1,)
values "topwidth" range[0.1,)
values "shadethk" range(0,)
values "rodthk" range(0,)

values "gs_ui_current_page" pageValues

parameters B = A
lock "B"
lock "A"

if iShadeType = 1 or iShadeType = 3 or iShadeType = 4 or iShadeType = 6 or\
	 iShadeType = 10 or iShadeType = 13 then
	hideparameter "diameter2"
	lock "diameter2"
endif

if iShadeType = 1 or iShadeType = 3 or iShadeType = 4 or iShadeType = 6 or iShadeType = 8 or \
	iShadeType = 9 or iShadeType = 14 then
	if GLOB_MODPAR_NAME = "diameter1" then
		A  = diameter1
		A2 = diameter1/2
		parameters A = A, A2 = A2
	else
		if GLOB_MODPAR_NAME="D12" then
			A		  = D12*2
			B		  = D12*2
			diameter1 = D12*2
			parameters A = A,B = B, diameter1 = diameter1
		endif
	endif
endif

if iShadeType = 2 then
	if GLOB_MODPAR_NAME="D22" then
		diameter2 = D22*2
		parameters diameter2 = diameter2, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
					A2 = max(diameter1,diameter2)/2, topwidth = diameter2
	else
		if GLOB_MODPAR_NAME="topwidth" then
			D22 = topwidth/2
			parameters D22 = D22, A = max(diameter1,topwidth), B = max(diameter1,topwidth),
						A2 = max(diameter1,topwidth)/2, diameter2 = topwidth
		else
			if GLOB_MODPAR_NAME="bottomwidth" then
				D12 = bottomwidth/2
				parameters D12 = D12, A = max(bottomwidth,diameter2), B = max(bottomwidth,diameter2),
							A2 = max(bottomwidth,diameter2)/2, diameter1 = bottomwidth
			else
				if GLOB_MODPAR_NAME="D12" then
					diameter1 = D12*2
					parameters diameter1 = diameter1, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
								A2 = max(diameter1,diameter2)/2, bottomwidth = diameter1
				endif
			endif
		endif
	endif
endif

if iShadeType = 5 then
	if GLOB_MODPAR_NAME="D22" then
		diameter2 = D22*2
		parameters diameter2 = diameter2, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
					A2 = max(diameter1,diameter2)/2
	else
		if GLOB_MODPAR_NAME="diameter2" then
			D22 = diameter2/2
			parameters D22 = D22, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
						A2 = max(diameter1,diameter2)/2
		else
			if GLOB_MODPAR_NAME="diameter1" then
				D12 = diameter1/2
				parameters D12 = D12, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
							A2 = max(diameter1,diameter2)/2
			else
				if GLOB_MODPAR_NAME="D12" then
					diameter1 = D12*2
					parameters diameter1 = diameter1, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
								A2 = max(diameter1,diameter2)/2
				endif
			endif
		endif
	endif
endif

if iShadeType = 7 then
	hideparameter "diameter1", "diameter2"
	lock "diameter1", "diameter2"
	if GLOB_MODPAR_NAME="A2" then
		A		  = A2*2
		B		  = A2*2
		diameter = A2*2
		parameters A = A, B = B, diameter = diameter
	else
		A2		  = diameter/2
		A		  = diameter
		B		  = diameter
		parameters A2 = A2, B = B, A = A
	endif
endif

if iShadeType = 8 then
	diameter = (shadeheight*shadeheight + (diameter1/2)*(diameter1/2))/shadeheight
	parameters diameter = diameter, A = diameter, B = diameter, A2 = diameter/2, D2 = diameter/2
	hideparameter "diameter", "diameter2"
	lock "diameter", "diameter2"
endif

if iShadeType = 9 then
	parameters A = diameter , B = diameter, A2 = diameter/2
	hideparameter "diameter2"
	lock "diameter2"
endif

if iShadeType = 10 then
	hideparameter "diameter2", "diameter1"
	lock "diameter1", "diameter2"
	if GLOB_MODPAR_NAME="A2" then
		A		  = A2*2
		B		  = A2*2
		diameter = A2*2
		parameters A = A, B = B, diameter = diameter
	else
		A2		  = diameter/2
		A		  = diameter
		B		  = diameter
		parameters A2 = A2, B = B, A = A
	endif
endif

if iShadeType = 11 then
	if GLOB_MODPAR_NAME="diameter" then
		diameter1 = sqr (diameter**2 - shadeheight**2)
		D12 = (sqr(diameter**2 - shadeheight**2))/2
		parameters diameter1 = diameter1, D12 = D12
		lock "shadeheight"
	else
		if GLOB_MODPAR_NAME="D2" then
			diameter1 = sqr ((D2*2)**2 - shadeheight**2)
			D12 = (sqr((D2*2)**2 - shadeheight**2))/2
			A = D2*2
			A2 = D2*2
			diameter = D2*2
			parameters diameter1 = diameter1, D12 = D12, A = A, diameter = diameter
			lock "shadeheight"
		else
			if GLOB_MODPAR_NAME="diameter1" then
				D12 = diameter1/2
				diameter = sqr(diameter1**2 + shadeheight**2)
				A = sqr(diameter1**2 + shadeheight**2)
				parameters diameter = diameter, D12 = D12, A = A, A2 = A/2
				lock "shadeheight"
			else
				if GLOB_MODPAR_NAME="D12" then
					diameter = sqr((D12*2)**2 + shadeheight**2)
					A = sqr((D12*2)**2 + shadeheight**2)
					diameter1 = D12*2
					parameters diameter = diameter, diameter1 = diameter1, A = A, A2 = A/2
					lock "shadeheight"
				else
					if GLOB_MODPAR_NAME="shadeheight" then
						diameter1 = sqr (diameter**2 - shadeheight**2)
						D12 = (sqr(diameter**2 - shadeheight**2))/2
						parameters diameter1 = diameter1, D12 = D12
					endif
				endif
			endif
		endif
	endif
	hideparameter "diameter2"
	lock "diameter2"
endif

if iShadeType = 12 then
	if abs(diameter-diameter1) > eps then
		shaderad = ((shadeheight/2)**2+(diameter/2-diameter1/2)**2) / (2*(diameter/2-diameter1/2))
	else
		!! simple cylind
	endif
	if GLOB_MODPAR_NAME="D12" then
		diameter1 = D12*2
		parameters diameter1 = diameter1
	else
		if GLOB_MODPAR_NAME="diameter1" then
			D12 = diameter1/2
			parameters D12 = D12
		else
			if GLOB_MODPAR_NAME="D2" then
				diameter = D2*2
				parameters diameter = diameter
			else
				if GLOB_MODPAR_NAME="diameter" then
					D2 = diameter/2
					parameters D2 = D2
				endif
			endif
		endif
	endif
	A = max(diameter, diameter1)
	B = max(diameter, diameter1)
	A2 = max(diameter/2, diameter1/2)
	parameters A = A, B = B, A2 = A2

	hideparameter "diameter2"
	lock "diameter2"
endif

if iShadeType = 13 then
	if GLOB_MODPAR_NAME="D2" then
		diameter	= D2*2
		A			= D2*2
		parameters A = A, B = B, diameter = diameter
	else
		D2		  = diameter/2
		A		  = diameter
		B		  = diameter
		parameters D2 = D2, B = B, A = A
	endif
	hideparameter "diameter2", "diameter1"
	lock "diameter2", "diameter1"
endif

if iShadeType = 14 then
	A = max(diameter, diameter1)
	B = max(diameter, diameter1)
	A2 = max(diameter, diameter1)/2
	parameters A = A, B = B, A2 = A2
	hideparameter "diameter2"
	lock "diameter2"
endif

if iShadeType = 15 then
	A = 0.056
	B = 0.056
	A2 = 0.028
	shadeheight = 0.072
	parameters A = A, B = B, A2 = A/2, shadeheight = shadeheight
	hideparameter "PendantLam_sp1", "matShadeOut", "matShadeIn"
	lock "PendantLam_sp1"
endif

if iShadeType < 7 then
	hideparameter "diameter"
	lock "diameter"
endif

if GLOB_MODPAR_NAME="D22" then
	diameter2 = D22*2
	parameters diameter2 = diameter2
else
	D22 = diameter2/2
	parameters D22 = D22
endif

if GLOB_MODPAR_NAME="D2" then
	diameter = D2*2
	parameters diameter = diameter
else
	D2 = diameter/2
	parameters D2 = D2
endif

! --- Parameters for Type1, Type2, Type3 name ---
if iShadeType = 1 or iShadeType = 3 then
	if GLOB_MODPAR_NAME="diameter1" then
		width = diameter1
		parameters width = width
	else
		diameter1 = width
		A = width
		A2 = width/2
		B = width
		parameters diameter1 = diameter1, A = A, A2 = A2, B = B
	endif
	if GLOB_MODPAR_NAME="D12" then
		width = D12*2
		parameters width = width
	else
		D12 = width/2
		parameters diameter1 = diameter1
	endif
endif

if iShadeType = 2 then
	if GLOB_MODPAR_NAME="diameter2" then
		D22 = diameter2/2
		parameters D22 = D22, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
					A2 = max(diameter1,diameter2)/2, topwidth = diameter2
	else
		if GLOB_MODPAR_NAME="diameter1" then
			D12 = diameter1/2
			parameters D12 = D12, A = max(diameter1,diameter2), B = max(diameter1,diameter2),
						A2 = max(diameter1,diameter2)/2, bottomwidth = diameter1
		endif
	endif
endif

if iShadeType = 1 or iShadeType = 2 or iShadeType = 3 then
	if iShadeType = 2 then
		hideparameter "width", "diameter1", "diameter2"
		lock "width", "diameter1", "diameter2"
	else
		hideparameter "diameter1", "bottomwidth", "topwidth"
		lock "diameter1", "bottomwidth", "topwidth"
	endif
else
	hideparameter "width", "bottomwidth", "topwidth"
	lock "width", "bottomwidth", "topwidth"
endif


!--- Set default parameters ---

if GLOB_MODPAR_NAME = "strShadeType" or GLOB_MODPAR_NAME = "iShadeType" then

	if iShadeType = 1 or iShadeType = 3 then
		diameter1 = 0.4
		shadeheight = 0.3
		parameters	diameter1	= diameter1,
					shadeheight	= shadeheight,
					diameter2	= diameter1,
					A			= diameter1,
					B			= diameter1,
					A2			= diameter1/2,
					D12			= diameter1/2,
					width		= diameter1
	endif

	if iShadeType = 2 then
		diameter1 = 0.4
		diameter2 = 0.2
		shadeheight = 0.3
		parameters	diameter1	= diameter1,
					diameter2	= diameter2,
					shadeheight = shadeheight,
					A			= max(diameter1,diameter2),
					B			= max(diameter1,diameter2),
					A2			= max(diameter1,diameter2)/2,
					D12			= diameter1/2,
					D22			= diameter2/2,
					bottomwidth	= diameter1,
					topwidth	= diameter2
	endif

	if iShadeType = 4 then
		diameter1 = 0.5
		shadeheight = 0.3
		parameters	diameter1	= diameter1,
					shadeheight	= shadeheight,
					diameter2	= diameter1,
					A			= diameter1,
					B			= diameter1,
					A2			= diameter1/2,
					D12			= diameter1/2
	endif

		if iShadeType = 6 then
		diameter1 = 0.5
		shadeheight = 0.2
		parameters	diameter1	= diameter1,
					shadeheight	= shadeheight,
					diameter2	= diameter1,
					A			= diameter1,
					B			= diameter1,
					A2			= diameter1/2,
					D12			= diameter1/2
	endif

	if iShadeType = 5 then
		diameter1 = 0.5
		diameter2 = 0.4
		shadeheight = 0.3
		parameters	diameter1	= diameter1,
					diameter2	= diameter2,
					shadeheight	= shadeheight,
					A			= max(diameter1,diameter2),
					B			= max(diameter1,diameter2),
					A2			= max(diameter1,diameter2)/2,
					D12			= diameter1/2,
					D22			= diameter2/2
	endif

	if iShadeType = 7 then
		diameter = 0.5
		shadeheight = 0.25
		parameters	diameter	= diameter,
					shadeheight	= shadeheight,
					A			= diameter,
					B			= diameter,
					A2			= diameter/2,
					D2			= diameter/2
	endif

	if iShadeType = 8 then
		diameter = 0.5
		diameter1 = 0.4
		shadeheight = 0.4
		parameters	diameter    = diameter,
					diameter1	= diameter1,
					shadeheight	= shadeheight,
					A			= diameter,
					B			= diameter,
					A2			= diameter/2,
					D12			= diameter/2
	endif

	if iShadeType = 9 then
		diameter = 0.25
		diameter1 = 0.15
		shadeheight = 0.4
		parameters	diameter	= diameter,
					diameter1	= diameter1,
					shadeheight	= shadeheight,
					A			= diameter,
					B			= diameter,
					A2			= diameter/2,
					D2			= diameter/2,
					D12			= diameter1/2
	endif

	if iShadeType = 10 then
		diameter = 0.2
		shadeheight = 0.4
		parameters	diameter	= diameter,
					shadeheight = shadeheight,
					A			= diameter,
					B			= diameter,
					A2			= diameter/2,
					D2			= diameter/2
	endif

	if iShadeType = 11 then
		diameter = 0.5
		diameter1 = 0.3
		shadeheight = 0.4
		parameters	diameter	= diameter,
					diameter1	= diameter1,
					shadeheight	= shadeheight,
					A			= diameter,
					B			= diameter,
					A2			= diameter/2,
					D2			= diameter/2,
					D12			= diameter1/2
	endif

	if iShadeType = 12 then
		diameter = 0.3
		diameter1 = 0.2
		shadeheight = 0.4
		parameters	diameter	= diameter,
					diameter1	= diameter1,
					shadeheight	= shadeheight,
					A			= max(diameter1,diameter),
					B			= max(diameter1,diameter),
					A2			= max(diameter1,diameter)/2,
					D2			= diameter/2,
					D12			= diameter1/2
	endif

	if iShadeType = 13 then
		diameter = 0.5
		shadeheight = 0.265
		parameters	diameter	= diameter,
					shadeheight	= shadeheight,
					A			= diameter,
					B			= diameter,
					A2			= diameter/2,
					D2			= diameter/2
	endif

	if iShadeType = 14 then
		diameter1 = 0.5
		diameter = 0.3
		shadeheight = 0.3
		parameters	diameter1 	= diameter1,
					diameter	= diameter,
					shadeheight = shadeheight,
					A 			= diameter1,
					B 			= diameter1,
					A2 			= diameter1/2,
					D2 			= diameter/2,
					D12 		= diameter1/2

	endif

	if iShadeType = 15 then
		shadeheight = 0.072
		parameters	shadeheight	= shadeheight
	endif

endif

if iSuspenderType <> 2 then
	hideparameter "rodthk"
	lock "rodthk"
endif

