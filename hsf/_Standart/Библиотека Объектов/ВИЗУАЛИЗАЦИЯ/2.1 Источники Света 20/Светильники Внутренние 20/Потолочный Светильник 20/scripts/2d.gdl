
pen gs_cont_pen
fill gs_fill_type

if (iShadeType = 12) & bMigrationPos then add2 A2, B2

unID=1

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
	gs_symbtype_2D_m = 1
endif

! ==============================================================================
!	Realistic Symbol
! ==============================================================================

if gs_symbtype_2D_m = 1 then
	if ABS(rotAngleX) > 0 then
		pen gs_fill_pen
		fill gs_fill_type
		project2{3} 3, 270, 3+32, 1+4, gs_back_pen, 0, 0, 0, parameters	gs_detlevel_3D_m	= 2,
																		gs_resol			= 24,
																		bShowIllumination3D	= 0

		pen gs_cont_pen
		project2{3} 3, 270, 2, 1+4+8, parameters	gs_detlevel_3D_m	= 2,
													gs_resol			= 24,
													bShowIllumination3D	= 0

		hotspot2 0,	0,	unID : unID = unID + 1
	else
		!!---- Hotspot Editing
		hotspot2 0,0, unID : unID = unID + 1

		if iShadeType <> 11 and iShadeType <> 12 then
			for i=1 to 4
				hotspot2 0,		0,	unID,	A2,	1+128	: unID = unID + 1
				hotspot2 -1,	0,	unID,	A2,	3		: unID = unID + 1
				hotspot2 A2,	0,	unID,	A2,	2		: unID = unID + 1
				rot2 90
			next i
			del 4
		else
			if iShadeType = 11 then
				for i=1 to 2
					hotspot2 0,		0,	unID,	A2,	1+128	: unID = unID + 1
					hotspot2 -1,	0,	unID,	A2,	3		: unID = unID + 1
					hotspot2 A2,	0,	unID,	A2,	2		: unID = unID + 1

					hotspot2 0,		0,	unID,	B2,	1+128 	: unID = unID + 1
					hotspot2 0,		-1,	unID,	B2,	3 		: unID = unID + 1
					hotspot2 0,		B2,	unID,	B2,	2 		: unID = unID + 1
					rot2 180
				next i
				del 2
			else
				hotspot2 0,		0,		unID, A2, 1+128,	lampWidth : unID = unID + 1
				hotspot2 A2,	0,		unID, A2, 2,		lampWidth : unID = unID + 1
				hotspot2 -1,	0,		unID, A2, 3,		lampWidth : unID = unID + 1

				hotspot2 0,		0,		unID, A2, 1+128,	lampWidth : unID = unID + 1
				hotspot2 -A2,	0,		unID, A2, 2,		lampWidth : unID = unID + 1
				hotspot2 1,		0,		unID, A2, 3,		lampWidth : unID = unID + 1

				hotspot2 0,		0,		unID, B2, 1+128,	lampDepth : unID = unID + 1
				hotspot2 0,		B2,		unID, B2, 2,		lampDepth : unID = unID + 1
				hotspot2 0,		-1,		unID, B2, 3,		lampDepth : unID = unID + 1

				hotspot2 0,		0,		unID, B2, 1+128,	lampDepth : unID = unID + 1
				hotspot2 0,		-B2,	unID, B2, 2,		lampDepth : unID = unID + 1
				hotspot2 0,		1,		unID, B2, 3,		lampDepth : unID = unID + 1


				hotspot2 0,			0,			unID, mountW2, 1+128,	mountWidth : unID = unID + 1
				hotspot2 mountW2,	0,			unID, mountW2, 2,		mountWidth : unID = unID + 1
				hotspot2 -1,		0,			unID, mountW2, 3,		mountWidth : unID = unID + 1

				hotspot2 0,			0,			unID, mountW2, 1+128,	mountWidth : unID = unID + 1
				hotspot2 -mountW2,	0,			unID, mountW2, 2,		mountWidth : unID = unID + 1
				hotspot2 1,			0,			unID, mountW2, 3,		mountWidth : unID = unID + 1

				hotspot2 0,			0,			unID, mountD2, 1+128,	mountDepth : unID = unID + 1
				hotspot2 0,			mountD2,	unID, mountD2, 2,		mountDepth : unID = unID + 1
				hotspot2 0,			-1,			unID, mountD2, 3,		mountDepth : unID = unID + 1

				hotspot2 0,			0,			unID, mountD2, 1+128,	mountDepth : unID = unID + 1
				hotspot2 0,			-mountD2,	unID, mountD2, 2,		mountDepth : unID = unID + 1
				hotspot2 0,			1,			unID, mountD2, 3,		mountDepth : unID = unID + 1
			endif
		endif

		!!---- Hotspot Editing

		if (iShadeType <> 5 and iShadeType <> 9 and iShadeType <> 10 and iShadeType <> 11 and iShadeType <> 12) then

			poly2_b 2, 1+2+64,gs_fill_pen,gs_back_pen,
					0,0,901,
					A2,360,4001

		else
			if iShadeType <> 12 then
				poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					-a/2, -b/2, 1,
					a/2, -b/2, 1,
					a/2,  b/2, 1,
					-a/2, b/2, 1
			else
				poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					-lampWidth/2,	-lampDepth/2,	1,
					lampWidth/2,	-lampDepth/2,	1,
					lampWidth/2,	lampDepth/2,	1,
					-lampWidth/2,	lampDepth/2,	1

				poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					-mountWidth/2,	-mountDepth/2,	1,
					mountWidth/2,	-mountDepth/2,	1,
					mountWidth/2,	mountDepth/2,	1,
					-mountWidth/2,	mountDepth/2,	1
			endif
		endif
	endif
endif



! ==============================================================================
!	Electrical Symbol or RCP Symbol
! ==============================================================================

if gs_symbtype_2D_m = 2 or gs_symbtype_2D_m = 3 then

	call "Lamp_symbol" parameters all \
									bWallLamp = 0,
									bCeilingLamp = 1,
									bDirect = 0,
									Symbol_unID = unID,
									specSymbolRotation=180

endif

if (iShadeType = 12) & bMigrationPos then del 1

