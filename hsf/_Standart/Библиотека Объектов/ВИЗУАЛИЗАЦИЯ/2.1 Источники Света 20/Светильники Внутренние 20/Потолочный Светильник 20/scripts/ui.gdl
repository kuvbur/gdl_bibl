
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_CUSTOM_1 then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_CUSTOM_1 then
			gosub "lampStyleSettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_BASIC_PARAMS then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_BASIC_PARAMS then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_MODEL then
			gosub "modelSettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampStyleSettingsPage":

	ui_infield{3} "iShadeType", 0, 0, 444, 266,
		1, 1, 12, 3,
		105, 130, 101, 105,
		1,	shade_types[1],		1,
		2,	shade_types[2],		2,
		3,	shade_types[3],		3,
		4,	shade_types[4],		4,
		5,	shade_types[5],		5,
		6,	shade_types[6],		6,
		7,	shade_types[7],		7,
		8,	shade_types[8],		8,
		9,	shade_types[9],		9,
		10,	shade_types[10],	10,
		11,	shade_types[11],	11,
		12,	shade_types[12],	12

return


"lampGeometrySettingsPage":

	x1	= 2
	x2	= 132
	x3	= 234
	x4	= 364
	cx1	= 120
	cx2	= 80
	y1	= 160
	y2	= 172 + (iShadeType <> 12) * 24
	dy	= 24

	outFldHgt	= 15
	lgtInfHgt	= 19

	ui_separator 0,	y1-7,	444,	y1-7

	ui_style 0, 1
	ui_outfield `Размеры`,		x1,		y1,		cx1,	outFldHgt

	y1 = y1 + 1.5 * dy

	ui_style 0, 0

	ui_outfield	`Поворот Вокруг Х`,	x3,		y2,		cx1,	outFldHgt
	ui_infield	"rotAngleX",			x4,		y2-3,	cx2,	lgtInfHgt

	y2 = y2 + dy

	if iShadeType <> 12 then
		ui_outfield `Ширина 1`,			x1,		y1,		cx1,	outFldHgt
		ui_infield	"A",				x2,		y1-3,	cx2,	lgtInfHgt

		y1 = y1 + dy

		ui_outfield `Ширина 2`,			x1,		y1,		cx1,	outFldHgt
		ui_infield	"B",				x2,		y1-3,	cx2,	lgtInfHgt

		y1 = y1 + dy

		ui_outfield `Высота`,			x1,		y1,		cx1,	outFldHgt
		ui_infield	"ZZYZX",			x2,		y1-3,	cx2,	lgtInfHgt
	else
		ui_outfield	`Ширина Светильника`,		x1,		y1,		cx1,	outFldHgt
		ui_infield	"lampWidth",		x2,		y1-3,	cx2,	lgtInfHgt

		y1 = y1 + dy

		ui_outfield	`Длина Светильника`,		x1,		y1,		cx1,	outFldHgt
		ui_infield	"lampDepth",		x2,		y1-3,	cx2,	lgtInfHgt

		y1 = y1 + dy

		ui_outfield	`Высота Светильника`,		x1,		y1,		cx1,	outFldHgt
		ui_infield	"lampHeight",		x2,		y1-3,	cx2,	lgtInfHgt


		ui_outfield	`Ширина Крепления`,	x3,		y2,		cx1,	outFldHgt
		ui_infield	"mountWidth",		x4,		y2-3,	cx2,	lgtInfHgt

		y2 = y2 + dy

		ui_outfield	`Длина Крепления`,	x3,		y2,		cx1,	outFldHgt
		ui_infield	"mountDepth",		x4,		y2-3,	cx2,	lgtInfHgt

		y2 = y2 + dy

		ui_outfield	`Высота Крепления`,	x3,		y2,		cx1,	outFldHgt
		ui_infield	"mountHeight",		x4,		y2-3,	cx2,	lgtInfHgt
	endif

return


"modelSettingsPage":

	x1	= 2
	x2	= 180
	cx1	= 175
	cx2	= 180
	y1	= 5
	dy	= 26

	outFldHgt	= 15
	strInfHgt	= 19
	matInfHgt	= 25

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x1,		y1,		cx1,	outFldHgt,	4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x2+cx2-50,		y1-3,	50,	strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	y1 = y1 + dy

	ui_outfield `Перо Конуса Света`,		x1,			y1,		cx1,	outFldHgt, 4 * (not(bShowIllumination3D) | gs_detlevel_3D_m = 0)
	ui_infield "penIllumination",		x2+cx2-33,	y1-3,	33,		strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	ui_separator 0,	y1-6,	444,	y1-6

	ui_style 0, 1
	ui_outfield `Покрытия`,			x1,		y1,		2 * cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	if not(iShadeType = 6 | iShadeType = 7) then
		ui_outfield	`Крепление`,			x1,		y1,		cx1,	outFldHgt
		ui_infield	"matFrame",			x2,		y1-5,	cx2,	matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType < 7 | iShadeType = 8) then
		ui_outfield	`Лампа`,				x1,		y1,		cx1,	outFldHgt
		ui_infield	"matLamp",			x2,		y1-5,	cx2,	matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType = 10 | iShadeType = 11) then
		ui_outfield	`Стекло`,			x1,		y1,		cx1,	outFldHgt
		ui_infield	"matGlassOther",	x2,		y1-5,	cx2,	matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType = 9 | iShadeType = 10 | iShadeType = 12) then
		if iShadeType = 10 then
			stGlassOutfield = `Лампа`
		else
			stGlassOutfield = `Стекло`
		endif

		ui_outfield	stGlassOutfield,	x1,		y1,		cx1,	outFldHgt, 4 * bLight
		ui_infield	"matGlass",			x2,		y1-5,	cx2,	matInfHgt

		y1 = y1 + dy
	endif

	if (iShadeType = 7) then
		ui_outfield	`Плафон Снаружи`,	x1,		y1,		cx1,	outFldHgt
		ui_infield	"matShadeOut",		x2,		y1-5,	cx2,	matInfHgt

		y1 = y1 + dy

		ui_outfield	`Плафон Внутри`,		x1,		y1,		cx1,	outFldHgt
		ui_infield	"matShadeIn",		x2,		y1-5,	cx2,	matInfHgt
	endif

return

