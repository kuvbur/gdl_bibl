
hideparameter all

call "FM_types" parameters all

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

values "A" range (0.1,)
values "B" range (0.1,)

if iShadeType = 12 then
	values "A2" range [0.025,)
	values "B2" range [0.025,)
else
	values "A2" range (0.05,)
	values "B2" range (0.05,)
endif

values "rotAngleX"		range (-90, 90)
values "lampWidth"		range [0.05, )
values "lampDepth"		range [0.05, )
values "lampHeight"		range [0.005, )
values "mountWidth"		range [0.05, )
values "mountDepth"		range [0.05, )
values "mountHeight"	range [0.005, )

if iShadeType = 4 or iShadeType = 5 then
	values "gs_resol" range[8,)
else
	values "gs_resol" range[3,)
endif

if iShadeType = 6 then
	values "ZZYZX" range (0, A/4 + 0.02)
else
	values "ZZYZX" range (0, )
endif


! --- 3D Detail Level ---

values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! --- C4D Parameters ---

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_OMNI,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes = 0,
									bEnableSymbolic2D	= 1


if gs_cont_pen<=0 then parameters gs_cont_pen=10
if gs_fill_pen<=0 then parameters gs_fill_pen=91

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif


if iShadeType = 11 then
	if GLOB_MODPAR_NAME="A2" then
		A = A2*2
		parameters A =A
	else
		A2 = A/2
		parameters A2 = A2
	endif
else
	if iShadeType = 1 then
		if GLOB_MODPAR_NAME = "A2" then
			A 		=	A2*2
			B 		=	A2*2
			ZZYZX 	=	A2*2 + 0.0125
			parameters A = A, B = B, ZZYZX = ZZYZX
		else
			if GLOB_MODPAR_NAME = "B" then
				A 		=	B
				A2 		=	B/2
				ZZYZX 	=	B + 0.0125
				parameters A = A, A2 = A2, ZZYZX = ZZYZX
			else
				A2 		= A/2
				B 		= A
				ZZYZX 	= A + 0.0125
				parameters A2 = A2, B = B, ZZYZX = ZZYZX
			endif
		endif
		lock "ZZYZX"
	else
		if iShadeType <> 12 then
			if GLOB_MODPAR_NAME="B" then
				A  = B
				A2 = B/2
				parameters A = A, A2 = A2
			else
				if GLOB_MODPAR_NAME="A2" then
					A = A2*2
					B = A2*2
					parameters A = A, B = B
				else
					A2 = A/2
					B  = A
					parameters A2 = A2, B = B
				endif
			endif
		endif
	endif
endif

if iShadeType = 11 then
	lock "ZZYZX"
endif

if iShadeType <> 12 then
	if GLOB_MODPAR_NAME="B2" then
		B = B2*2
		parameters B =B
	else
		B2 = B/2
		parameters B2 = B2
	endif

	hideparameter "shade12Dim"
else
	if GLOB_MODPAR_NAME = "A2" | GLOB_MODPAR_NAME = "B2" then
		lampWidth = 2 * A2
		lampDepth = 2 * B2

		parameters	lampWidth = lampWidth,
					lampDepth = lampDepth
	else
		A2 = lampWidth / 2
		B2 = lampDepth / 2

		parameters	A2 = A2,
					B2 = B2
	endif

	if GLOB_MODPAR_NAME = "mountW2" | GLOB_MODPAR_NAME = "mountD2" then
		mountWidth = 2 * mountW2
		mountDepth = 2 * mountD2

		parameters	mountWidth = mountWidth,
					mountDepth = mountDepth
	else
		mountW2 = mountWidth / 2
		mountD2 = mountDepth / 2

		parameters	mountW2 = mountW2,
					mountD2 = mountD2
	endif

	if GLOB_MODPAR_NAME = "ZZYZX" then
		mountHeight = ZZYZX - lampHeight
		parameters mountHeight = mountHeight
	else
		ZZYZX = lampHeight + mountHeight
		parameters ZZYZX = ZZYZX
	endif

	A		= max(lampWidth, mountWidth)
	B		= max(lampDepth, mountDepth)

	parameters	A		= A,
				B		= B

	lock "A", "B"
endif




! --- Synhronized mm and point in marker width ---

Dot_in_mm = 25.4 / 72

if GLOB_MODPAR_NAME = "fsz" then
	fsz_point = fsz / Dot_in_mm
	parameters fsz_point = fsz_point
else
	fsz = fsz_point * Dot_in_mm
	parameters fsz = fsz
endif


parameters ac_bottomlevel = -ZZYZX
parameters ac_toplevel = 0

if iShadeType <> 99 then
	lock "strCustomShade"
	hideparameter "strCustomShade"
endif


! --- Shade Types ---
values "strShadeType" shade_types
values "iShadeType" 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12
if GLOB_MODPAR_NAME = "strShadeType" then
	iShadeType = 1
	for i = 2 to vardim1(shade_types)
		if strShadeType = shade_types[i] then
			iShadeType = i
			i = vardim1(shade_types)	! break
		endif
	next i
	parameters iShadeType = iShadeType
else
	strShadeType = shade_types[max(min(iShadeType, vardim1(shade_types)), 1)]
	parameters strShadeType = strShadeType
endif

!--- Set default parameters ---

if GLOB_MODPAR_NAME = "strShadeType" or GLOB_MODPAR_NAME = "iShadeType" then

	if iShadeType = 1 then
		parameters A = 0.24, B = 0.24, A2 = 0.12, ZZYZX = 0.24+0.0275
	endif

	if iShadeType = 2 then
		parameters A = 0.25, B = 0.25, A2 = 0.125, ZZYZX = 0.149
	endif

	if iShadeType = 3 or iShadeType = 4 or iShadeType = 5 or iShadeType = 6 then
		parameters A = 0.3, B = 0.3, A2 = 0.15, ZZYZX = 0.08
	endif

	if iShadeType = 7 then
		parameters A = 0.25, B = 0.25, A2 = 0.125, ZZYZX = 0.25
	endif

	if iShadeType = 8 then
		parameters A = 0.2, B = 0.2, A2 = 0.1, ZZYZX = 0.4
	endif

	if iShadeType = 9  or iShadeType = 10 then
		parameters A = 0.4, B = 0.4, A2 = 0.2, ZZYZX = 0.05
	endif

	if iShadeType = 11 then
		parameters A = 0.2, B = 0.4, A2 = 0.1, ZZYZX = 0.0839
	endif

	if iShadeType = 12 then
		lampWidth	= 1
		lampDepth	= 1
		lampHeight	= 0.05
		mountWidth	= 0.95
		mountDepth	= 0.95
		mountHeight	= 0.05

		parameters	lampWidth	= lampWidth,
					lampDepth	= lampDepth,
					lampHeight	= lampHeight,
					mountWidth	= mountWidth,
					mountDepth	= mountDepth,
					mountHeight	= mountHeight
	endif
endif

values "gs_ui_current_page" pageValues

! --- Materials

if bLight then
	lock "matGlass"
endif

if	iShadeType = 1 | iShadeType = 2 | iShadeType = 3 | \
	iShadeType = 4 | iShadeType = 5 | iShadeType = 8 then

	hideparameter	"matGlass", "matGlassOther", "matShadeOut", "matShadeIn"
	lock			"matGlass", "matGlassOther", "matShadeOut", "matShadeIn"
else
	if iShadeType = 6 then
		hideparameter	"matFrame", "matGlass", "matGlassOther", "matShadeOut", "matShadeIn"
		lock			"matFrame", "matGlass", "matGlassOther", "matShadeOut", "matShadeIn"
	else
		hideparameter	"matLamp"
		lock			"matLamp"

		if iShadeType = 7 then
			hideparameter	"matFrame", "matGlass", "matGlassOther"
			lock			"matFrame", "matGlass", "matGlassOther"
		else
			hideparameter	"matShadeOut", "matShadeIn"
			lock			"matShadeOut", "matShadeIn"

			if iShadeType = 9 | iShadeType = 12 then
				hideparameter	"matGlassOther"
				lock			"matGlassOther"
			else
				if iShadeType = 11 then
					hideparameter	"matGlass"
					lock			"matGlass"
				endif
			endif
		endif
	endif
endif

