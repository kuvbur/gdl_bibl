
call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
	gs_symbtype_2D_m = 1
endif

unID = 1
hotspot2 a/2, 0, unID : unID = unID+1
CALL "Resize_A_B_ZZYZX" Parameters All

pen gs_cont_pen
if gs_symbtype_2D_m = 1 then			! Realistic Symbol
	RECT2 0,0,a,b3

	line2 thick, 0, thick, b3
	line2 a-thick, 0, a-thick, b3
	line2 thick, b3, thick, b3+b2
	line2 kk+thick, b3, kk+thick, b3+b2
	line2 a-thick, b3, a-thick, b3+b2
	line2 a-thick-kk, b3, a-thick-kk, b3+b2
	add2 0, b3+b2
	RECT2 0,0,a,b1
	line2 thick, 0, thick, b1
	line2 a-thick, 0, a-thick, b1
	line2 thick, lvt, a-thick, lvt
	line2 thick, b1-lvt, a-thick, b1-lvt
	del 1

	add2 thick, lvt
	for ix=1 to n
		add2 (a-2*thick)/(n+1), 0
		line2 0, b3+b2, 0, b1+b2+b3-2*lvt
	next ix
	del n
	del 1

	fill gs_fill_type
!	pen gs_fill_pen
	if gs_fill_pen<>0 then
		POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
			0, 0, 0,
			a, 0, 0,
			a, b3, 0,
			0, b3, 0,
			0, 0, -1

		add2 thick, b3
		POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
			0, 0, 0,
			kk, 0, 0,
			kk, b2, 0,
			0, b2, 0,
			0, 0, -1
		del 1
		add2 a-thick-kk, b3
		POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
			0, 0, 0,
			kk, 0, 0,
			kk, b2, 0,
			0, b2, 0,
			0, 0, -1
		del 1
	endif

	add2 0, b3+b2
	POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
		0, 0, 0,
		a, 0, 0,
		a, b1, 0,
		0, b1, 0,
		0, 0, -1
	del 1

ENDIF


if LibraryLangCode <> "JPN" then
	If show_txt and (gs_symbtype_2D_m = 2 OR gs_symbtype_2D_m = 3) Then		! Electrical Symbol or RCP Symbol
		nTrans = 0

			hotspot2 txt_pos_x,0,UnID, txt_pos_y, 1+128 : unID=unID+1
			hotspot2 txt_pos_x,-1,UnID, txt_pos_y, 3 : unID=unID+1
			hotspot2 txt_pos_x,txt_pos_y,UnID, txt_pos_y, 2 : unID=unID+1

			hotspot2 0,txt_pos_y,UnID, txt_pos_x, 1+128 : unID=unID+1
			hotspot2 -1,txt_pos_y,UnID, txt_pos_x, 3 : unID=unID+1
			hotspot2 txt_pos_x,txt_pos_y,UnID, txt_pos_x, 2 : unID=unID+1

			define style textStyle fontType, 10/GLOB_SCALE*fsz, 5,
								gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline + 128 * gs_text_style_strike
			style textStyle

			add2 txt_pos_x,txt_pos_y
			nTrans = nTrans + 1

			! --- Oriented View ----------------------------------------------------

			angleViewRot = 0
			rrr = REQUEST ("View_Rotangle", "", angleViewRot)
			totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

			! Text Rotation - Readable / Always Horizontal / Align with Symbol
			! ----------------------------------------------------------------------
			if typeTextRotation_m = 2 then								! Readable
				if (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps)) then
					rot2 180
					nTrans = nTrans + 1
				endif
			else
				if typeTextRotation_m = 1 then							! Horizontal
					rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED)) ! 1/-1
					nTrans = nTrans + 1
				endif
			endif
			! ----------------------------------------------------------------------

			text2 0,0,txt
			del nTrans
	EndIf

	If gs_symbtype_2D_m = 2 Then			! Electrical Symbol
		if gs_cont_pen=0 then gs_cont_pen=1

		pen gs_cont_pen

		rect2 0,0,a,b
		line2 0,b/2,a,b/2
	endif

	If gs_symbtype_2D_m = 3 Then			! RCP Symbol
		if gs_cont_pen=0 then gs_cont_pen=1

		pen gs_cont_pen

		rect2 0,0,a,b
		line2 0,b/2,a,b/2
	endif

	fill gs_fill_type
	pen gs_fill_pen
	add2 0, b/2
	POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
		0, 0, 0,
		a, 0, 0,
		a, b/2, 0,
		0, b/2, 0,
		0, 0, -1
	del 1
else
	if gs_symbtype_2D_m = 2 or gs_symbtype_2D_m = 3 then
		add2 a/2, 0
		call "Lamp_symbol" parameters all specSymbolRotation=0
		del 1
	endif
endif

