
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_CUSTOM_1 then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_CUSTOM_1 then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_MODEL then
			gosub "modelSettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames,
										iSymbolType		= 4 * gs_symbtype_2D_m - 5


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	! Mounting Settings

	x1		= 2
	x2		= 122
	y1		= 8
	dy		= 24

	outfWdt	= 115
	outfHgt	= 15
	infWdt	= 95
	infHgt	= 19


	ui_style 0, 1
	ui_outfield	`Крепление`,		x1,		y1,			outfWdt,	outfHgt

	ui_style 0, 0
	ui_infield{3} "iMountType",	x2,		y1-3,		infWdt,		infHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, mount_types[1], 1,
		1, mount_types[2], 2,
		1, mount_types[3], 3

	y1 = y1 + dy

	if iMountType = 1 then
		ui_outfield	`Стиль Светильника`,	x1,		y1,			outfWdt,	outfHgt
		ui_infield{3} "iLampType",	x2,		y1-3,		infWdt,		105,
			2, 2, 6, 2,
			80, 90, 80, 90,
			1, lamp_types[1], 1,
			2, lamp_types[2], 2,
			3, lamp_types[3], 3,
			4, lamp_types[4], 4,
			5, lamp_types[5], 5,
			6, lamp_types[6], 6
	endif

	if iMountType = 2 then
		ui_outfield	`Стиль Светильника`,	x1,		y1,			outfWdt,	outfHgt
		ui_infield{3} "iLampType",	x2,		y1-3,		infWdt,		105,
			2, 3, 2, 1,
			80, 90, 80, 90,
			1, lamp_types[1], 1,
			2, lamp_types[2], 2
	endif

	if iMountType <> 3 then y1 = y1 + 111

	ui_outfield	`Длина`,			x1,		y1,		outfWdt,	outfHgt
	ui_infield	"A",				x2,		y1-3,	infWdt,		infHgt

	y1 = y1 + dy

	ui_outfield	`Ширина`,			x1,		y1,		outfWdt,	outfHgt
	ui_infield	"B",				x2,		y1-3,	infWdt,		infHgt

	y1 = y1 + dy

	if iMountType = 1 then
		ui_outfield	`Высота Корпуса`,	x1,		y1,		outfWdt,	outfHgt
		ui_infield	"lh",				x2,		y1-3,	infWdt,		infHgt

		y1 = y1 + dy
	endif

	ui_outfield	`Общая Высота`,		x1,		y1,		outfWdt,	outfHgt
	ui_infield	"ZZYZX",			x2,		y1-3,	infWdt,		infHgt

	y1 = y1 + dy

	if iLampType = 5 then
		ui_outfield	`Толщина Плафона`,	x1,		y1,		outfWdt,	outfHgt
		ui_infield	"kx",					x2,		y1-3,	infWdt,		infHgt
	endif

	y1 = y1 + 0.5 * dy

	if iMountType <> 1 then
		ui_outfield	`Поворот вокруг X`,	x1,		y1,		outfWdt,	outfHgt
		ui_infield	"rotAngleX",			x2,		y1-3,	infWdt,		infHgt
	endif

	ui_separator 222, 0,	222, 266

	! Fixture Settings

	x3		= 227
	x4		= 348
	y2		= 8
	dy		= 24

	outfWdt	= 115
	outfHgt	= 15
	infWdt	= 95
	infHgt	= 19

	ui_style 0, 1
	ui_outfield	`Корпус`,			x3,		y2,			outfWdt,	outfHgt

	y2 = y2 + dy

	ui_style 0, 0
	ui_outfield	`Рассеиватель`,	x3,		y2,			outfWdt,	outfHgt
	ui_infield{3} "scrtype_m",		x4,		y2-3,		infWdt,		105,
		2, 1, 4, 1,
		80, 90, 80, 90,
		1, fixture_types[1], 0,
		2, fixture_types[2], 1,
		3, fixture_types[3], 2,
		4, fixture_types[4], 3

	y2 = y2 + 111

	ui_outfield	`Пластин Вдоль`,	x3,		y2,		outfWdt,	outfHgt, (scrtype_m = 3) * 4
	ui_infield	"sm_n",					x4,		y2-3,	infWdt,		infHgt

	y2 = y2 + dy

	ui_outfield	`Пластин Поперек`,		x3,		y2,		outfWdt,	outfHgt, not(scrtype_m = 2) * 4
	ui_infield	"sm_m",					x4,		y2-3,	infWdt,		infHgt

	y2 = y2 + dy

	ui_outfield	`Высота Пластин`,		x3,		y2,		outfWdt,	outfHgt, (iMountType = 1) * 4
	ui_infield	"grating_h",			x4,		y2-3,	infWdt,		infHgt

	y2 = y2 + dy

	ui_outfield	`Количество Ламп`,		x3,		y2,		outfWdt,	outfHgt
	ui_infield	"nTubes",				x4,		y2-3,	infWdt,		infHgt

return


"modelSettingsPage":

	x1	= 2
	x2	= 180
	cx1	= 175
	cx2	= 180
	y1	= 5
	dy	= 26

	outFldHgt	= 15
	strInfHgt	= 19
	matInfHgt	= 25

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1,stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x1,		y1,		cx1,	outFldHgt,	4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x2+cx2-50,		y1-3,	50,	strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x1, 	y1-3,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	ui_infield{3} "iModelType",				x2, 	y1-3,	cx2,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, `Каркас`,	1,
		1, `Оттененная`,	2		ui_tooltip `Тип 3D-модели Освещения`

	y1 = y1 + dy

	ui_outfield `Перо Конуса Света`,		x1,			y1,		cx1,	outFldHgt, 4 * (not(bShowIllumination3D) | gs_detlevel_3D_m = 0)
	ui_infield "penIllumination",		x2+cx2-33,	y1-3,	33,		strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	ui_separator 0,	y1-6,	444,	y1-6

	ui_style 0, 1
	ui_outfield `Покрытия`,		x1,		y1,		2 * cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Корпус`,		x1,		y1,		cx1,		outFldHgt
	ui_infield	"matFrame",		x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Рассеиватель`,		x1,		y1,		cx1,		outFldHgt
	ui_infield	"matScreen",	x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

	ui_outfield	`Стекло`,		x1,		y1,		cx1,		outFldHgt, not(scrtype_m = 3) * 4
	ui_infield	"matGlass",		x2,		y1-5,	cx2,		matInfHgt

	y1 = y1 + dy

return


