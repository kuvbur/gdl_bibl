
hideparameter all

call "FM_types" parameters all

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

values "rotAngleX" range (-90, 90)

values "ZZYZX" range [0.06,]

values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_NONVIS_AREA,
									bShow3D				= gs_detlevel_3D_m,
									bFixFalloffAngle	= 1,
									bEnable3DModelTypes = 1,
									bEnableSymbolic2D	= 1


if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif


values "fh" range [0, )
values "lh" range (0, )
values "ZZYZX" range [lh, )
values "kx" range (0, b*0.7]
values "grating_h" range (0, )
values "sm_n" range [0, )
values "sm_m" range [0, )

if iLampType <> 5 then hideparameter "kx"

if gs_cont_pen=0 then parameters gs_cont_pen=10
if gs_fill_pen=0 then parameters gs_fill_pen=91

values "gs_resol" range [3, ]

if (scrtype_m=0 or scrtype_m=1) then lock "sm_m"

if scrtype_m=3 then
	lock "sm_n", "sm_m"
else
	lock "matGlass"
endif

if iMountType = 1 then
	if GLOB_MODPAR_NAME = "ZZYZX" or GLOB_MODPAR_NAME = "iMountType" or GLOB_MODPAR_NAME = "strMountType" then
		fh = ZZYZX - lh
		parameters fh = ZZYZX - lh
	else
		ZZYZX = fh + lh
		parameters ZZYZX = fh + lh
	endif
endif
if iMountType = 2 then
	parameters lh = ZZYZX
endif

values "A" range (0,)
values "B" range (0,)


! --- Synhronized mm and point in marker width ---

Dot_in_mm = 25.4 / 72

if GLOB_MODPAR_NAME = "fsz" then
	fsz_point = fsz / Dot_in_mm
	parameters fsz_point = fsz_point
else
	fsz = fsz_point * Dot_in_mm
	parameters fsz = fsz
endif

if GLOB_MODPAR_NAME = "strMountType" or GLOB_MODPAR_NAME = "iMountType" then
	txt_pos_x = A / 2
	txt_pos_y = B/2
	parameters txt_pos_x = txt_pos_x, txt_pos_y = txt_pos_y
endif


! --- VALUES ---

values "strMountType" mount_types
values "scrtype" fixture_types

values "iMountType" 1, 2, 3
values "scrtype_m" 0, 1, 2, 3

if iMountType = 1 then
	values "strLampType" lamp_types
	values "iLampType" 1, 2, 3, 4, 5, 6
endif

if iMountType = 2 then
	values "strLampType" lamp_types[1], lamp_types[2]
	values "iLampType" 1, 2
endif

if iMountType = 3 then
	lock "strLampType", "iLampType"
endif


values "nTubes" range [1, int((b-2*frameWidth)/0.03)-1]

if iMountType = 1 then
	lock "grating_h"
	if iLampType = 2 then
		parameters lh = b/2
		lock "lh"
	endif
endif

if iMountType <> 1 then
	lock "lh"
	hideparameter "lh"
else
	hideparameter	"rotAngleX"
	lock			"rotAngleX"
endif

! --- STRING - INTEGER CONNECTION ---

! --- Lamp Types ---

if GLOB_MODPAR_NAME = "strLampType" then
	iLampType = 1
	for i = 1 to vardim1(lamp_types)
		if strLampType = lamp_types[i] then iLampType = i
	next i
	parameters iLampType = iLampType
else
	strLampType = lamp_types[max(min(iLampType, vardim1(lamp_types)), 1)]
	parameters strLampType = strLampType
endif

! --- Mount Types ---

if GLOB_MODPAR_NAME = "strMountType" then
	iMountType = 1
	for i = 1 to vardim1(mount_types)
		if strMountType = mount_types[i] then iMountType = i
	next i
	parameters iMountType = iMountType
else
	strMountType = mount_types[max(min(iMountType, vardim1(mount_types)), 1)]
	parameters strMountType = strMountType
endif

! --- Fixture Types ---

if GLOB_MODPAR_NAME = "scrtype" then
	scrtype_m = 0
	for i = 1 to vardim1(fixture_types)
		if scrtype = fixture_types[i] then scrtype_m = i-1
	next i
	parameters scrtype_m = scrtype_m
else
	scrtype = fixture_types[max(min(scrtype_m+1, vardim1(fixture_types)), 1)]
	parameters scrtype = scrtype
endif

!--- Set default parameters ---

if GLOB_MODPAR_NAME = "strLampType" or GLOB_MODPAR_NAME = "iLampType" or \
	GLOB_MODPAR_NAME = "strMountType" or GLOB_MODPAR_NAME = "iMountType" then

	if iMountType = 1 then
		parameters A = 1.5, ZZYZX  = 1, B = 0.3, lh = 0.17
	endif
	if iMountType = 2 then
		parameters A = 0.6, ZZYZX  = 0.1, B = 0.6
	endif
	if iMountType = 3 then
		parameters A = 0.6, ZZYZX  = 0.1, B = 0.6
	endif
endif

if iMountType = 3 then			! Recessed
	bottomlevel = -0.01
	toplevel =	ZZYZX-0.01
else							! Ceiling and Suspended
	bottomlevel = -ZZYZX
	toplevel = 0
endif

parameters ac_bottomlevel = bottomlevel
parameters ac_toplevel = toplevel


values "gs_ui_current_page" pageValues
