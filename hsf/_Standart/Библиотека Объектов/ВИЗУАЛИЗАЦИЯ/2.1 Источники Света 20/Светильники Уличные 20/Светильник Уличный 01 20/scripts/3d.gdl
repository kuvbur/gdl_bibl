

if gs_detlevel_3D_m=0 then End
if gs_detlevel_3D_m=1 then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


!!---- Geometry Hotspot Editing
unID = 1

hotspot	A - bd/2,	0,	0,		unID,	ZZYZX,	1+128	: unID = unID + 1
hotspot	A - bd/2,	0,	-1,		unID,	ZZYZX,	3		: unID = unID + 1
hotspot	A - bd/2,	0,	ZZYZX,	unID,	ZZYZX,	2		: unID = unID + 1

hotspot 0,	0,	0,	unID	: unID = unID + 1


!!---- Geometry ----!!

pen gs_cont_pen
resol gs_resol
material matPost


_postHeight = zzyzx - 2.5 * lsl * sin(linc) - (lsl + td/2) * cos(linc)

CONE _postHeight,	bd/2,	td/2,	90,	90

addz _postHeight

ELBOW lsl, 90 - linc, td/2

addx lsl
roty 90 - linc
addx -lsl

CYLIND 1.5 * lsl,	td/2

add td/2, -bd/2, 1.5 * lsl
roty -90

PRISM 4, td,
	0,		0,
	lsl,	bd/8,
	lsl,	7/8 * bd,
	0,		bd

addz -td

material matLamp

PRISM 4, td,
	0,		0,
	lsl,	bd/8,
	lsl,	7/8 * bd,
	0,		bd

del 7


!!---- Light

resol 36
if gs_light_switch then

	addz _postHeight
	addx lsl
	roty 90 - linc
	addx -lsl
	addz 2 * lsl
	addx td
	roty -90

	lightUnID = 1000

	! ======================================================================
	! Non Visible Omni Light
	! ======================================================================

	call "lightMacro_m" parameters	gs_light_switch			= gs_light_switch,
									gs_color_red			= gs_color_red,
									gs_color_green			= gs_color_green,
									gs_color_blue			= gs_color_blue,
									gs_light_intensity		= gs_light_intensity,
									iColorPreset			= iColorPreset,
									iLightGroupType			= LIGHTGROUP_BUILTIN_OMNI,
									iC4dDetAreaShape		= AREASHAPE_SPHERE,
									c4dDetAreaX				= 0.03,
									c4dDetAreaY				= 0.03,
									c4dDetAreaZ				= 0.03,
									bGenShadow				= bGenShadow,
									iShadowQuality			= iShadowQuality,
									iShadowQualityGroupType	= 2,
									iC4dGenVisibility		= 1,
									bDetShowFalloff 		= 1,
									iFalloffType			= FALLOFFTYPE_PYRAMID,
									c4dDetRadius			= c4dDetRadius,
									c4dDetFalloffAngle		= 60,
									c4dPhoPhotometric		= c4dPhoPhotometric,
									iC4dPhoUnit				= iC4dPhoUnit,
									photoIntensityLumen		= photoIntensityLumen,
									photoIntensityCandela	= photoIntensityCandela,
									bShowIllumination3D		= 0,
									gs_resol				= gs_resol,
									gs_cont_pen				= gs_cont_pen,
									penIllumination			= penIllumination,
									iModelType				= iModelType,
									lightUnID				= lightUnID

	addz -td/2

	call "lightMacro_m" parameters	iLightGroupType		= LIGHTGROUP_BUILTIN_FALLOFF,
									iFalloffType		= FALLOFFTYPE_PYRAMID,
									c4dDetAreaX			= lsl,
									c4dDetAreaY			= 0.75 * bd,
									c4dDetRadius		= c4dDetRadius,
									c4dDetFalloffAngle	= 60,
									bFixFalloffAngle	= 1,
									c4dPhoPhotometric	= c4dPhoPhotometric,
									bShowIllumination3D	= bShowIllumination3D,
									penIllumination		= penIllumination,
									iModelType			= iModelType,
									gs_resol			= gs_resol,
									lightUnID			= unID + 1000

endif


