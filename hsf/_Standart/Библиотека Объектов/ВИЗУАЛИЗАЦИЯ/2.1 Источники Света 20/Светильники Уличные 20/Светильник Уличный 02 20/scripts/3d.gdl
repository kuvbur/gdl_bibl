
! Street Lamp 02

If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


!!---- Geometry Hotspot Editing
unID=1
hotspot 0,0,0,unID : unID=unID+1

For j=1 to iLampType
	hotspot	0.22+pw,0,0,unID,ZZYZX,1+128 : unID=unID+1
	hotspot	0.22+pw,0,-1,unID,ZZYZX,3 : unID=unID+1
	hotspot	0.22+pw,0,ZZYZX,unID,ZZYZX,2 : unID=unID+1

	hotspot	0.22+pw,0,0,unID,ZZYZX,1+128 : unID=unID+1
	hotspot	0.22+pw,0,-1,unID,ZZYZX,3 : unID=unID+1
	hotspot	0.22+pw,0,ZZYZX,unID,ZZYZX,2 : unID=unID+1

	add 0.42+pw, 0, zzyzx-0.20-B2
	for i=1 to 4
		hotspot 0,0,0,unID,B2,1+128 : unID=unID+1
		hotspot -1,0,0,unID,B2,3 : unID=unID+1
		hotspot B2,0,0,unID,B2,2 : unID=unID+1
		rotz 90
	next i
	del 5
	rotz 360/iLampType
Next j
del iLampType


!!---- Geometry ----!!

pen gs_cont_pen
resol gs_resol
material matPost

roty -90
revolve	4, 360, 1+2,
		0.00, pw, 2,
		0.64, pw, 2,
		0.70, pw/2, 2,
		zzyzx-0.35-pw/2, pw/2, 2
del 1

addz zzyzx-0.80-pw/2

for j=1 to iLampType
	addx pw+0.02
	cylind 0.60,pw/2
	addz 0.60
	elbow 0.20, 180, pw/2
	del 2

	addz 0.15
	for i=1 to 2
		prism_	7, 0.06,
				pw/2*cos(30), pw/4, 79,
				0, 0, 979,
				pw/2*cos(30), -pw/4, 3079,
				pw+0.02-pw/2*cos(30), -pw/4, 79,
				pw+0.02, 0, 979,
				pw+0.02-pw/2*cos(30), pw/4, 3079,
				pw/2*cos(30), pw/4, -1
		addz 0.15
	next i
	del 3

	rotz 360/iLampType
next j
del iLampType+1


material matLamp

for j=1 to iLampType
	add pw+0.42, 0, zzyzx-pw/2-0.20-B2
	sphere B2
	del 1
	rotz 360/iLampType
next j
del iLampType

material matLamp

!!---- Light

resol 36
If gs_light_switch Then

	For k=1 to iLampType
		add pw+0.42, 0, zzyzx-pw/2-0.20-B2
		roty 90

		lightUnID = k * 1000
	
		! ======================================================================
		! Non Visible Omni Light
		! ======================================================================
	
		call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
													gs_color_red						= gs_color_red,
													gs_color_green						= gs_color_green,
													gs_color_blue						= gs_color_blue,
													gs_light_intensity						= gs_light_intensity,
													iColorPreset			= iColorPreset,
													iLightGroupType			= LIGHTGROUP_BUILTIN_OMNI,
													iC4dDetAreaShape		= AREASHAPE_SPHERE,
													c4dDetAreaX				= 0.03,
													c4dDetAreaY				= 0.03,
													c4dDetAreaZ				= 0.03,
													bGenShadow				= bGenShadow,
													iShadowQuality			= iShadowQuality,
													iShadowQualityGroupType	= 2,
													iC4dGenVisibility		= 1,
													bDetShowFalloff 		= 1,
													iFalloffType			= FALLOFFTYPE_OMNI,
													c4dDetRadius			= c4dDetRadius,
													c4dPhoPhotometric		= c4dPhoPhotometric,
													iC4dPhoUnit				= iC4dPhoUnit,
													photoIntensityLumen		= photoIntensityLumen,
													photoIntensityCandela	= photoIntensityCandela,
													bShowIllumination3D		= 0,
													gs_resol				= gs_resol,
													gs_cont_pen				= gs_cont_pen,
													penIllumination			= penIllumination,
													lightUnID				= lightUnID

		del 2
		rotz 360/iLampType
	Next k
	del iLampType

	addz zzyzx-pw/2-0.20-B2

	call "lightMacro_m" parameters		iLightGroupType		= LIGHTGROUP_BUILTIN_FALLOFF,
										iFalloffType		= FALLOFFTYPE_OMNI,
										c4dDetAreaX			= 0.1,
										c4dDetAreaY			= 0.1,
										c4dDetRadius		= c4dDetRadius,
										c4dDetFalloffAngle	= 90,
										c4dPhoPhotometric	= c4dPhoPhotometric,
										bShowIllumination3D	= bShowIllumination3D,
										penIllumination		= penIllumination,
										iModelType			= 1,
										gs_resol			= gs_resol,
										lightUnID			= unID + 1000

	del 1
EndIf

