
If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


!!---- Geometry Hotspot Editing
unID=1

hotspot	0, 0, 0,    unID, ZZYZX, 1+256 : unID=unID+1
hotspot	0, 0, -1,   unID, ZZYZX, 3     : unID=unID+1
hotspot	0, 0, ZZYZX,unID, ZZYZX, 2     : unID=unID+1

For i=1 To iLampType
	hotspot	 0,     0, ZZYZX-gsd2, unID, arm_len, 1+128 : unID=unID+1
	hotspot	-1,     0, ZZYZX-gsd2, unID, arm_len, 3     : unID=unID+1
	hotspot	arm_len,0, ZZYZX-gsd2, unID, arm_len, 2     : unID=unID+1

	add arm_len, 0, ZZYZX-gsd2
	For j=1 To 4
		hotspot 0,   0, 0, unID, gsd2, 1+128, gsd : unID=unID+1
		hotspot -1,  0, 0, unID, gsd2, 3,     gsd : unID=unID+1
		hotspot gsd2,0, 0, unID, gsd2, 2,     gsd : unID=unID+1
		rotz 90
	Next j
	del 5

	rotz 360/iLampType
Next i
del iLampType


!!---- Geometry ----!!

pen gs_cont_pen
resol gs_resol
material matPost

roty -90
revolve	4, 360, 1+2,
		0.00, pw, 2,
		0.15*zzyzx, pw, 2,
		0.15*zzyzx, pw/2, 2,
		zzyzx, pw/2, 2
del 1

addz zzyzx-gsd/2-gsd/8
For i=1 To iLampType
	material matPost

	prism_	6, gsd/4,
			pw/2 * cos(30),			pw/4,	79,
			0,						0,		979,
			pw/2 * cos(30),			-pw/4,	3079,
			arm_len - 0.9 * gsd2,	-pw/4,	15,
			arm_len - 0.9 * gsd2,	pw/4,	15,
			pw/2 * cos(30),			pw/4,	-1

	material matLamp
	add arm_len, 0, gsd/8
	sphere gsd/2
	del 1

	rotz 360/iLampType
Next i
del iLampType+1


!!---- Light

resol 36
If gs_light_switch Then

	For k=1 to iLampType
		add arm_len, 0, zzyzx-gsd/2

		lightUnID = k * 1000

		! ======================================================================
		! Non Visible Omni Light
		! ======================================================================

		call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
													gs_color_red						= gs_color_red,
													gs_color_green						= gs_color_green,
													gs_color_blue						= gs_color_blue,
													gs_light_intensity						= gs_light_intensity,
													iColorPreset			= iColorPreset,
													iLightGroupType			= LIGHTGROUP_BUILTIN_OMNI,
													iC4dDetAreaShape		= AREASHAPE_SPHERE,
													c4dDetAreaX				= 0.03,
													c4dDetAreaY				= 0.03,
													c4dDetAreaZ				= 0.03,
													bGenShadow				= bGenShadow,
													iShadowQuality			= iShadowQuality,
													iShadowQualityGroupType	= 2,
													iC4dGenVisibility		= 1,
													bDetShowFalloff 		= 1,
													iFalloffType			= FALLOFFTYPE_OMNI,
													c4dDetRadius			= c4dDetRadius,
													c4dPhoPhotometric		= c4dPhoPhotometric,
													iC4dPhoUnit				= iC4dPhoUnit,
													photoIntensityLumen		= photoIntensityLumen,
													photoIntensityCandela	= photoIntensityCandela,
													bShowIllumination3D		= 0,
													gs_resol				= gs_resol,
													gs_cont_pen				= gs_cont_pen,
													penIllumination			= penIllumination,
													lightUnID				= lightUnID


		del 1

		rotz 360/iLampType
	Next k
	del iLampType

	addz zzyzx-gsd/2

	call "lightMacro_m" parameters		iLightGroupType		= LIGHTGROUP_BUILTIN_FALLOFF,
										iFalloffType		= FALLOFFTYPE_OMNI,
										c4dDetAreaX			= 0.1,
										c4dDetAreaY			= 0.1,
										c4dDetRadius		= c4dDetRadius,
										c4dDetFalloffAngle	= 90,
										c4dPhoPhotometric	= c4dPhoPhotometric,
										bShowIllumination3D	= bShowIllumination3D,
										penIllumination		= penIllumination,
										iModelType			= 1,
										gs_resol			= gs_resol,
										lightUnID			= unID + 1000

	del 1
EndIf
