
ui_dialog `Параметры Светильника и Освещения`
ui_current_page gs_ui_current_page


for i = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[i][1] = TABID_BASIC_PARAMS then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_BASIC_PARAMS then
			gosub "lampGeometrySettingsPage"
		endif
   endif

	if uiUsedPageIDs[i][1] = TABID_FPLAN_MODEL then
		ui_page uiUsedPageIDs[i][1], uiUsedPageIDs[i][2], uiUsedPageNames[i][1], uiUsedPageNames[i][2]
		if gs_ui_current_page = TABID_FPLAN_MODEL then
			gosub "displaySettingsPage"
		endif
	endif
next i

! ==============================================================================
! Call Light Macro's TabPages
! ==============================================================================

call	"lightMacro_m" parameters all	bIsLamp			= 1,
										bSpot			= 0,
										uiUsedPageIDs	= uiUsedPageIDs,
										uiUsedPageNames	= uiUsedPageNames


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! UI Page Subroutines
! ==============================================================================


"lampGeometrySettingsPage":

	x1	= 2
	x2	= 132
	x3	= 234
	x4	= 364
	cx1	= 120
	cx2	= 80
	y1	= 160
	dy	= 24

	outFldHgt	= 15
	lgtInfHgt	= 19

	ui_separator 0,	y1-7,	444,	y1-7

	ui_style 0, 1
	ui_outfield `Размеры`,			x1,		y1,		cx1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield	`Тип Светильника`,				x1,		y1,		cx1,	outFldHgt
	ui_infield{3} "iLampType",				x2,		y1-3,	cx2,	lgtInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, stTypeValues[1], 1,
		1, stTypeValues[2], 2,
		1, stTypeValues[3], 3,
		1, stTypeValues[4], 4

	y1 = y1 + dy

	ui_outfield	`Диаметр Столба`,			x1,		y1,		cx1,	outFldHgt
	ui_infield	"pw",						x2,		y1-3,	cx2,	lgtInfHgt

	y1 = y1 + dy

	ui_outfield	`Высота Светильника`,				x1,		y1,		cx1,	outFldHgt
	ui_infield	"ZZYZX",					x2,		y1-3,	cx2,	lgtInfHgt

	y2	= 184

	ui_outfield	`Диаметр Плафона`,	x3,		y2,		cx1,	outFldHgt
	ui_infield	"gsd",						x4,		y2-3,	cx2,	lgtInfHgt

	y2 = y2 + dy

	ui_outfield	`Расстояние Плафона от Центра`,	x3,		y2,		180,	outFldHgt

	y2 = y2 + dy

	ui_infield	"arm_len",					x4,		y2-3,	cx2,	lgtInfHgt

return


"displaySettingsPage":

	dim	surfaceBlock[2][2]
		surfaceBlock[1][1] = `Стойка`	:	surfaceBlock[1][2] = "matPost"
		surfaceBlock[2][1] = `Лампа`	:	surfaceBlock[2][2] = "matLamp"

	dim	flSurfaceBlk[2]
		flSurfaceBlk[1]		= 0
		flSurfaceBlk[2]		= 0

	call "ui_lampDisplaySettings_m" parameters all	bEnableModelType	= 0,
													pageStartY			= 3,
													stDetailLevel		= stDetailLevel,
													surfaceBlock		= surfaceBlock,
													flSurfaceBlk		= flSurfaceBlk

return

