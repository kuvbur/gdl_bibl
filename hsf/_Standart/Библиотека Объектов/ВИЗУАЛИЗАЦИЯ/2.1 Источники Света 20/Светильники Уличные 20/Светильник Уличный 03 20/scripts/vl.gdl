
hideparameter all

call "FM_types" parameters all

values "zzyzx" range [0.55, )
values "pw"    range (0, )

values "stLampType" stTypeValues
values "iLampType" 1, 2, 3, 4

if GLOB_MODPAR_NAME = "stLampType" then
	iLampType = 1
	for i = 2 to vardim1(stTypeValues)
		if stLampType = stTypeValues[i] then
			iLampType	= i
			i			= vardim1(stTypeValues)		! break
		endif
	next i
	parameters iLampType = iLampType
else
	iLampType = max(1, min(iLampType, vardim1(stTypeValues)))
	stLampType = stTypeValues[iLampType]
	parameters stLampType = stLampType
endif

If iLampType=3 then										! 3 Glass Sphere 
		values "gsd"     range [0.1, 2*(arm_len*cos(30))]
		values "gsd2"    range [0.1/2, arm_len*cos(30)]
		values "arm_len" range [(gsd/2)/cos(30),)			
else
	If iLampType=4 then									! 4 Glass Sphere 
		values "gsd"     range [0.1, 2*(arm_len*cos(45))]
		values "gsd2"    range [0.1/2, arm_len*cos(45)]
		values "arm_len" range [(gsd/2)/cos(45),)	
	else
		values "gsd"     range [0.1, arm_len*2-pw]
		values "gsd2"    range [0.05, (arm_len*2-pw)/2]
		values "arm_len" range [pw/2+gsd/2,)			! 1 or 2 Glass Sphere 
	endif
endif

values "gs_detlevel_3D" stDetailLevel
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetailLevel[1] then gs_detlevel_3D_m = DET3D_OFF
	if gs_detlevel_3D = stDetailLevel[3] then gs_detlevel_3D_m = DET3D_DETAILED

	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetailLevel[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetailLevel[1]
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetailLevel[3]

	parameters gs_detlevel_3D = gs_detlevel_3D
endif

call "lightMacro_m" parameters all	iLightGroupType		= LIGHTGROUP_BUILTIN_OMNI,
									bShow3D				= gs_detlevel_3D_m,
									bEnable3DModelTypes = 0


if gs_cont_pen=0 then parameters gs_cont_pen=10
if gs_fill_pen=0 then parameters gs_fill_pen=91

values "gs_resol" range [6, ]

if gs_detlevel_3D_m = DET3D_SIMPLE then
	lock "gs_resol"
endif

if gs_detlevel_3D_m = DET3D_OFF then
	lock "gs_resol", "gs_shadow"
endif

If iLampType=1 Then parameters A=arm_len+gsd, B=gsd
If iLampType=2 Then parameters A=2*arm_len+gsd, B=gsd
If iLampType=3 Then parameters A=arm_len+gsd/2+(arm_len+gsd/2)*cos(60), B=arm_len*sin(60)*2+gsd
If iLampType=4 Then parameters A=2*arm_len+gsd, B=2*arm_len+gsd
lock "A", "B"

If GLOB_MODPAR_NAME="gsd2" Then
	gsd = 2*gsd2
	parameters gsd = gsd
else
	gsd2 = gsd/2
	parameters gsd2 = gsd2
endif

values "gs_ui_current_page" pageValues


! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	rrr = REQUEST ("Name_of_macro", "", LPName)
	call "Onorm_Light" parameters all sLibpartName = strsub(LPName,0,strlen(LPName)-4)
else
	hideparameter "gs_onorm_Title"
endif
