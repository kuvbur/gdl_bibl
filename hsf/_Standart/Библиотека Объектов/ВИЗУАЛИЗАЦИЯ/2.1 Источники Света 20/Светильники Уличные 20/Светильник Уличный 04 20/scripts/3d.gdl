
If gs_detlevel_3D_m=0 Then End
If gs_detlevel_3D_m=1 Then gs_resol=6

if GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 then gs_resol = 12

if not(gs_shadow) then shadow off


! --- Geometry Hotspot Editing ---
unID=1

hotspot	0,0,0,unID=unID+1

hotspot	0, 0, 0,     unID, ZZYZX, 1+256 : unID=unID+1
hotspot	0, 0, -1,    unID, ZZYZX, 3     : unID=unID+1
hotspot	0, 0, ZZYZX, unID, ZZYZX, 2     : unID=unID+1

addz zzyzx-a/6
for i=1 to 4
	hotspot 0,  0, 0, unID, A2, 1+128, A : unID=unID+1
	hotspot -1, 0, 0, unID, A2, 3,     A : unID=unID+1
	hotspot A2, 0, 0, unID, A2, 2,     A : unID=unID+1
	rotz 90
next i
del 5


!--------------------------------

pen gs_cont_pen

resol gs_resol


material matPost
cylind a,pw/2+0.01	!! Post Base

addz a
! --- Post ---
if zzyzx - a*2 > EPS then
	cylind zzyzx-a/2-a-a/2, pw/2
endif

addz zzyzx-a/2-a-a/2
cylind a/2,pw/2
del 1
addz zzyzx-a/2-a
rotz 45
material matComp
addz 0.01


for n=1 to 4
	prism_	5,(a/2)-(a/6)-0.02,
			a/4,-0.005,15,
			a*(2/6),-0.005,15,
			a*(2/6),0.005,15,
			a/4,0.005,15,
			a/4,-0.005,-1
	rotz 90
next n
del 5

addz a*(2/6)
for n=1 to 4
	prism_	 5,(a/6)-0.01,
			 a/4,-0.005,15,
			 a*(2/6),-0.005,15,
			 a*(2/6),0.005,15,
			 a/4,0.005,15,
			 a/4,-0.005,-1
	rotz 90
next n
del 6

material matLamp

RESOL 2*gs_resol

addz 0.02
prism_ 2,(a/2)-0.04,
		0,0,908,
		a/4,360,4008
del 1

material matPost
! head bottom
cylind 0.01,a*0.35
addz -0.05
material matComp
for t=1 to 2
	prism_     14,      0.01,
			  pw/2+0.05,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
			   -(pw/2+0.05),     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
				pw/2+0.05,     0.0,     -1,
				pw/2,     0.0,      13,
				0.0,     0.0,    913,
				0.0,     180.0,   4013,
			   -pw/2,     0.0,      13,
				0.0,     0.0,    913,
				0.0,     180.0,   4013,
				pw/2,     0.0,     -1

	addz -0.05
next t
del 3
! head top
material matPost


del 2
addz zzyzx-0.01
cylind 0.01,a*(5/12)
addz -a/6
RESOL 2*gs_resol

prism_ 14, 0.01,
		a/2,     0.0,      79,
		0.0,     0.0,    979,
		0.0,     180.0,   4079,
	   -a/2,     0.0,      79,
		0.0,     0.0,    979,
		0.0,     180.0,   4079,
		a/2,     0.0,     -1,
		a/4,     0.0,      13,
		0.0,     0.0,    913,
		0.0,     180.0,   4013,
	   -a/4,     0.0,      13,
		0.0,     0.0,    913,
		0.0,     180.0,   4013,
		a/2,     0.0,     -1
del 2


MATERIAL matLamp

resol 36
If gs_light_switch Then

	addz zzyzx-(a/8)-0.01

	lightUnID = 1000

	! ======================================================================
	! Non Visible Omni Light
	! ======================================================================

	call	"lightMacro_m" parameters		gs_light_switch						= gs_light_switch,
											gs_color_red						= gs_color_red,
											gs_color_green						= gs_color_green,
											gs_color_blue						= gs_color_blue,
											gs_light_intensity						= gs_light_intensity,
											iColorPreset			= iColorPreset,
											iLightGroupType			= LIGHTGROUP_BUILTIN_OMNI,
											iC4dDetAreaShape		= AREASHAPE_SPHERE,
											c4dDetAreaX				= 0.03,
											c4dDetAreaY				= 0.03,
											c4dDetAreaZ				= 0.03,
											bGenShadow				= bGenShadow,
											iShadowQuality			= iShadowQuality,
											iShadowQualityGroupType	= 3,
											iC4dGenVisibility		= 1,
											bDetShowFalloff 		= 1,
											iFalloffType			= FALLOFFTYPE_OMNI,
											c4dDetRadius			= c4dDetRadius,
											c4dPhoPhotometric		= c4dPhoPhotometric,
											iC4dPhoUnit				= iC4dPhoUnit,
											photoIntensityLumen		= photoIntensityLumen,
											photoIntensityCandela	= photoIntensityCandela,
											bShowIllumination3D		= bShowIllumination3D,
											gs_resol				= gs_resol,
											gs_cont_pen				= gs_cont_pen,
											penIllumination			= penIllumination,
											lightUnID				= lightUnID

		del 1

EndIf

