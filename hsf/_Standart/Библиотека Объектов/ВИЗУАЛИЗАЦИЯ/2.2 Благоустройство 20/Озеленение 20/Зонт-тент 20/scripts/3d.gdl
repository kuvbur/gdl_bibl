
if iDetlevel3D = DETLEVEL3D_OFF then END

if not(gs_shadow) then SHADOW OFF

PEN gs_cont_pen

if iDetlevel3D = DETLEVEL3D_SIMPLE then
	resol 6
else
	resol gs_resol
endif

material matFrame
gosub "holderElement"

if bopened then
	armangle	= atn(0.5/HalfA)
else
	armangle	= 100-atn(0.5/HalfA)
endif

armlenght	= sqr(HalfA^2 + 0.4^2)
rodlenght	= cos(armangle)*armlenght/iParasolType


UnID = 1
hotspot 0,0,0,UnID : UnID = UnID + 1
hotspot 0,0,zzyzx,UnID : UnID = UnID + 1

addz zzyzx

cutpolya 2,2,0,
	0,0,900,
	0.02,360,4000

for i = 1 to 8
	gosub "hotspotEditing"

	gosub "parasol"

	material matFrame
	if not(bopened) then addy 0.02
	rotx 90 + armangle

	upperedge = 13
	gosub "wandElement"

	del 1
	if not(bopened) then del 1


	addz -0.4
	rotx 90 - armangle

	upperedge = 15
	gosub "wandElement"

	del 2

	rotz 45
next i

cutend
del i

end

"hotspotEditing":
	if iParasolType = PARASOLTYPE_GARDEN then
		zPos = 0.02-sin(armangle)*armlenght
	else
		zPos = -0.2-sin(armangle)*armlenght
	endif

	if bopened & i mod 2 = 1 then
		hotspot 0,		0,	zPos, UnID, HalfA, 1+128, A : UnID = UnID + 1
		hotspot HalfA,	0,	zPos, UnID, HalfA, 2, A : UnID = UnID + 1
		hotspot -HalfA,	0,	zPos, UnID, HalfA, 3, A : UnID = UnID + 1
	endif
return

"parasol":
	if iParasolType = PARASOLTYPE_GARDEN then
		material matGardenShade
		plane_ 3,
			0,0,0.02,1,
			rodlenght,0,0.02-sin(armangle)*armlenght,1,
			sin(45)*rodlenght,cos(45)*rodlenght,0.02-sin(armangle)*armlenght,1
	else
		if i mod 2 = 1 then
			material matBeachShade1
		else
			material matBeachShade2
		endif
	
		plane_ 3,
			0,0,0.02,1,
			rodlenght,0,0.02-sin(armangle)*armlenght/2,0,
			sin(45)*rodlenght,cos(45)*rodlenght,0.02-sin(armangle)*armlenght/2,1
	
		plane_ 4,
			rodlenght,0,0.02-sin(armangle)*armlenght/2,1,
			rodlenght*2,0,-0.2-sin(armangle)*armlenght,0,
			sin(45)*rodlenght*2,cos(45)*rodlenght*2,-0.2-sin(armangle)*armlenght,1,
			sin(45)*rodlenght,cos(45)*rodlenght,0.02-sin(armangle)*armlenght/2,0

		if iDetlevel3D = DETLEVEL3D_SIMPLE  | not(bopened) then
			plane_ 4,
				rodlenght*2,0,-0.2-sin(armangle)*armlenght,1,
				rodlenght*2,0,-0.4-sin(armangle)*armlenght,1,
				sin(45)*rodlenght*2,cos(45)*rodlenght*2,-0.4-sin(armangle)*armlenght,1,
				sin(45)*rodlenght*2,cos(45)*rodlenght*2,-0.2-sin(armangle)*armlenght,0
		endif

		if iDetlevel3D = DETLEVEL3D_DETAILED & bopened then
			add rodlenght*2,0,-0.2-sin(armangle)*armlenght
			rotz 45/2
			roty 90

			_segW = cos(45)*rodlenght*2.165
			poly_ 7,
				0, 0, 1,
				0, 0.2, 900,
				0.1, 90, 4001,
				0.2, _segW-0.2, 1,
				0, _segW-0.2, 900,
				0.1, 90, 4001,
				0, _segW, 1

			del 3
		endif
	endif
return

"wandElement":
	if iDetlevel3D = DETLEVEL3D_DETAILED  & bopened then
		if iParasolType = PARASOLTYPE_GARDEN then
			if upperedge = 13 then
				wandElementLengh = armlenght-0.05
			else
				wandElementLengh = 0.2/cos(90-armangle)
			endif
		else
			if upperedge = 13 then
				wandElementLengh = armlenght/2-0.02
	
				addz wandElementLengh
				if bopened then
					rotx atn(0.22/wandElementLengh)
				else
					rotx 3
				endif
				prism_ 5, wandElementLengh,
					0, 0, 15,
					0.01, 0, 15,
					0.01, 0.01, upperedge,
					-0.01, 0.01, upperedge,
					-0.01, 0, 15
				del 2
			else
				wandElementLengh = 0.2/cos(90-armangle)
			endif
		endif
	
			prism_ 5, wandElementLengh,
				0, 0, 15,
				0.01, 0, 15,
				0.01, 0.01, upperedge,
				-0.01, 0.01, upperedge,
				-0.01, 0, 15
	endif
return

"holderElement":
	if iParasolType = PARASOLTYPE_GARDEN then
		if 	iDetlevel3D = DETLEVEL3D_DETAILED 	|\
			(iDetlevel3D = DETLEVEL3D_SIMPLE	&\
			not(bopened))						then
            
			_holderHeight = 0.4
		else
			_holderHeight = 0.02
		endif

		put	0,	0,	-1,	1,
			0,	0,	zzyzx-_holderHeight,	1,
			0,	0,	zzyzx+0.1,	1
		
		for i = 1 to gs_resol
			_xPos = sin(i*(45)/gs_resol) + i*HalfA/gs_resol
			_zPos = cos(i*(45)/gs_resol) + zzyzx - 0.9
		
			put		_xPos,	0,	_zPos,	1
		next i
	
		put	_xPos-0.5,	0,	_zPos-0.5,	1
		put	_xPos-0.5,	0,	_zPos-0.5,	1
		put	_xPos-0.5,	0,	0,	1
	
		tube 2, (nsp/4), 3,
			0, 0, 901,
			0.02, 360, 4001,
		
			get (nsp)

		if iDetlevel3D = DETLEVEL3D_DETAILED then
			add _xPos,	0,	_zPos
			rotx 90
			addz -0.025
			cylind 0.05, 0.042
			del 3

			add _xPos-0.5,	0,	_zPos-0.525
			cylind 0.1, 0.025
			
			rotx 90
			add -0.03, 0.02, -0.025/2
			rotz 75

			prism_ 9, 0.025,
				0, 0, 15,
				0.02, 0.02, 900,
				0.02, 270, 4015,
				0, -0.06, 900,
				0.06, 75, 4015,
				-0.01, -0.07, 900,
				0.06, -70, 4015,
				0.02, 0.02, 900,
				0.02, 30, 4015

			del 4
		endif

		addx _xPos-0.5
		cylind zzyzx, 0.02

	
		for rimi = 1 to 4
			prism_ 4, 0.05,
				0, 0, 15,
				0.6, 0, 15,
				0.6, 0.6, 15,
				0, 0.6, 15
			rotz 90
		next rimi
	
		del rimi
	else
		cylind zzyzx+0.015, 0.02
	endif

return

