
! Hotspots
if iViewType2D = TOP_VIEW | iViewType2D = TOP_VIEW_SYMB then
	unID = 1

	HOTSPOT2 0,0,unID 							: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, -1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, temp_BB, unID, temp_BB, 2, B	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, 1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, -temp_BB, unID, temp_BB, 2, B	: unID=unID+1

else
	unID=1

	HOTSPOT2 0,0,unID 								: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+128					: unID=unID+1
	HOTSPOT2 0, -1, unID, B, 3+128					: unID=unID+1
	HOTSPOT2 0, B, unID, B, 2						: unID=unID+1

endif

if txt_show then
	call "plantName_2D" parameters txt = txt,
			fontType = fontType,
			fontSizeFixed_mm = fontSizeFixed_mm,
			fontSizeFixed_pt = fontSizeFixed_pt,
			txt_pen = txt_pen,
			bBold = bBold,
			bItalic = bItalic,
			bUnderLined = bUnderLined,
			txt_pos_x = txt_pos_x,
			txt_pos_y = txt_pos_y,
			typeTextRotation_m = typeTextRotation_m,
			gs_readable_angle = gs_readable_angle
endif

if iViewType2D = TOP_VIEW_SYMB then
	call "treeTopViews" parameters A = A,
		B = B,
		iTopViewSymb = iTopViewSymb,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type = gs_fill_type,
		gs_fill_pen = gs_fill_pen,
		gs_back_pen = gs_back_pen
else
	if iViewType2D = SIDE_VIEW_SYMB then
		call "HouseSideView" parameters A = A, 
			B = B,
			iSideViewSymb = iSideViewSymb,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type = gs_fill_type,
			gs_fill_pen = gs_fill_pen,
			gs_back_pen = gs_back_pen
	else
		if iViewType2D = TOP_VIEW then
			add2 -a/2, -b/2
			iPicName = 4			! daisy top

			if iTreeType = TYPE_DAISY then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 4		! daisy top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 1		! daisy gray top
			endif
			
			if iTreeType = TYPE_DIFFEN then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 16		! diffenbachia top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 13		! diffenbachia gray top
			endif
			
			if iTreeType = TYPE_SABAL then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 27		! sabal palmetto top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 25		! sabal palmetto gray top
			endif
			
			if iTreeType = TYPE_YUCCA then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 39		! yucca top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 37		! yucca gray top
			endif			
		else
			add2 -a/2, 0

			iPicName = 8				! daisy1
			if iTreeType = TYPE_DAISY then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 8	! daisy1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 12	! daisy2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 6	! daisy1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 10	! daisy2 gray
			endif
			
			if iTreeType = TYPE_DIFFEN then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 20	! diffenbachia1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 24	! diffenbachia2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 18	! diffenbachia1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 22	! diffenbachia2 gray
			endif
			
			if iTreeType = TYPE_SABAL then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 32	! sabal palmetto1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 36	! sabal palmetto2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 30	! sabal palmetto1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 34	! sabal palmetto2 gray
			endif
			
			if iTreeType = TYPE_YUCCA then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 44	! yucca1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 48	! yucca2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 42	! yucca1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 46	! yucca2 gray
			endif
		endif

		if bCustomPic2D then
			_Picname = gs_picture_name_2D
			if gs_picture_name_2D = "" then
				if iViewType2D = TOP_VIEW then
					iPicName = 4							! daisy top
				else
					iPicName = 8							! daisy1
				endif
				_PicName = iPicName
			endif
		else
			_Picname = iPicName
		endif

		fill selection_fill_type
		add2 A/2, B/2
		if iViewType2D = TOP_VIEW then
			mul2 1, B/A
			poly2_B 2, 2+4+64, 1, 0,
					0, 0, 900,
					A/2, 360, 4000
			del 1
		else
			poly2_B 4, 2+4, 1, 0,
					-A/2, -B/2, 0,
					-A/2, B/2, 0,
					A/2, B/2, 0,
					A/2, -B/2, 0
		endif
		del 1

		picture2{2}  _Picname,a,b,1	

		del 1

	endif
endif

end
