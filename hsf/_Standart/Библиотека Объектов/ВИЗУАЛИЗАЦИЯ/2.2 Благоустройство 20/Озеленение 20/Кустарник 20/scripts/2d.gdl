
! Hotspots
if iViewType2D = TOP_VIEW | iViewType2D = TOP_VIEW_SYMB then
	unID = 1

	HOTSPOT2 0,0,unID 							: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, -1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, temp_BB, unID, temp_BB, 2, B	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, 1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, -temp_BB, unID, temp_BB, 2, B	: unID=unID+1
else
	unID=1

	HOTSPOT2 0,0,unID 								: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+128					: unID=unID+1
	HOTSPOT2 0, -1, unID, B, 3+128					: unID=unID+1
	HOTSPOT2 0, B, unID, B, 2						: unID=unID+1

endif


if txt_show then
	call "plantName_2D" parameters txt = txt,
			fontType = fontType,
			fontSizeFixed_mm = fontSizeFixed_mm,
			fontSizeFixed_pt = fontSizeFixed_pt,
			txt_pen = txt_pen,
			bBold = bBold,
			bItalic = bItalic,
			bUnderLined = bUnderLined,
			txt_pos_x = txt_pos_x,
			txt_pos_y = txt_pos_y,
			typeTextRotation_m = typeTextRotation_m,
			gs_readable_angle = gs_readable_angle
endif
	
if iViewType2D = TOP_VIEW_SYMB then
	call "treeTopViews" parameters A = A,
		B = B,
		iTopViewSymb = iTopViewSymb,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type = gs_fill_type,
		gs_fill_pen = gs_fill_pen,
		gs_back_pen = gs_back_pen
else
	if iViewType2D = SIDE_VIEW_SYMB then
		add2 -a/2, 0
		call "shrubSideViews" parameters A = A, 
			B = B,
			iSideViewSymb = iSideViewSymb,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type = gs_fill_type,
			gs_fill_pen = gs_fill_pen,
			gs_back_pen = gs_back_pen
		del 1
	else
		if iViewType2D = TOP_VIEW then
			add2 -a/2, -b/2

			iPicName = 43		!bush general top
			if iTreeType = TYPE_GEN then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 43		!bush general top
				if iTopViewPic2D = PIC_WINTER then iPicname = 44		!bush general winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 45		!bush general fall top
				if iTopViewPic2D = PIC_FOGGY then iPicname = 46			!bush general top grey
			endif

			if iTreeType = TYPE_HAZE then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 1		!bush hazel top
				if iTopViewPic2D = PIC_WINTER then iPicname = 2		!bush hazel winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 3		!bush hazel fall top
				if iTopViewPic2D = PIC_FOGGY then iPicname = 4		!bush hazel grey top
			endif
			
			if iTreeType = TYPE_ELD then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 17		!bushes elderberry top
				if iTopViewPic2D = PIC_WINTER then iPicname = 18		!bushes elderberry winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 19		!bushes elderberry top fall
				if iTopViewPic2D = PIC_FOGGY then iPicname = 20			!bushes elderberry top gray
			endif
			
			if iTreeType = TYPE_ROSE then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 66		!bush rose summer top2
				if iTopViewPic2D = PIC_WINTER then iPicname = 67		!bush rose winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 59		!bush rose fall top
				if iTopViewPic2D = PIC_SPRING then iPicname = 63		!bush rose spring top
				if iTopViewPic2D = PIC_FOGGY then iPicname = 61			!bush rose gray top
			endif
		else
			add2 -a/2, 0
			
			iPicName = 51		!bush general
			if iTreeType = TYPE_GEN then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 51		!bush general
				if iSideViewPic2D = PIC_WINTER then iPicname = 52		!bush general winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 53		!bush general fall
				if iSideViewPic2D = PIC_FOGGY then iPicname = 54			!bush general grey
			endif

			if iTreeType = TYPE_HAZE then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 9		!bush hazel
				if iSideViewPic2D = PIC_WINTER then iPicname = 10		!bush hazel winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 11		!bush hazel fall
				if iSideViewPic2D = PIC_FOGGY then iPicname = 12			!bush hazel grey
			endif
			
			if iTreeType = TYPE_ELD then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 25		!bushes elderberry
				if iSideViewPic2D = PIC_WINTER then iPicname = 26		!bushes elderberry winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 27		!bushes elderberry fall
				if iSideViewPic2D = PIC_FOGGY then iPicname = 28			!bushes elderberry grey
			endif
			
			if iTreeType = TYPE_ROSE then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 33		!bush rose summer
				if iSideViewPic2D = PIC_WINTER then iPicname = 34		!bush rose winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 35		!bush rose fall
				if iSideViewPic2D = PIC_SPRING then iPicname = 36		!bush rose spring
				if iSideViewPic2D = PIC_FOGGY then iPicname = 37			!bush rose grey
			endif
		endif

		if bCustomPic2D then
			_Picname = gs_picture_name_2D
			if gs_picture_name_2D = "" then
				if iViewType2D = TOP_VIEW then
					iPicName = 43		!bush general top
				else
					iPicName = 51		!bush general
				endif
				_PicName = iPicName
			endif
		else
			_Picname = iPicName
		endif

		fill selection_fill_type
		add2 A/2, B/2
		if iViewType2D = TOP_VIEW then
			mul2 1, B/A
			poly2_B 2, 2+4+64, 1, 0,
					0, 0, 900,
					A/2, 360, 4000
			del 1
		else
			poly2_B 4, 2+4, 1, 0,
					-A/2, -B/2, 0,
					-A/2, B/2, 0,
					A/2, B/2, 0,
					A/2, -B/2, 0
		endif
		del 1

		picture2{2}  _Picname,a,b,1	

		del 1

	endif
endif

end
