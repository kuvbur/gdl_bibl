
if iViewType2D = SIDE_VIEW | iViewType2D = SIDE_VIEW_SYMB | not(bShowasBitmap) then end

if iViewType3D = SIDE_VIEW then
	iPicName = 51											!bush general

	if iTreeType = TYPE_GEN then
		if iPic3D = PIC_SUMMER then iPicName = 51			!bush general
		if iPic3D = PIC_WINTER then iPicName = 52			!bush general winter
		if iPic3D = PIC_AUTUMN then iPicName = 53			!bush general fall
		if iPic3D = PIC_FOGGY then iPicName = 54				!bush general grey
	endif

	if iTreeType = TYPE_HAZE then
		if iPic3D = PIC_SUMMER then iPicName = 9			!bush hazel 
		if iPic3D = PIC_WINTER then iPicName = 10			!bush hazel winter
		if iPic3D = PIC_AUTUMN then iPicName = 11			!bush hazel fall
		if iPic3D = PIC_FOGGY then iPicName = 12				!bush hazel grey
	endif
		
	if iTreeType = TYPE_ELD then
		if iPic3D = PIC_SUMMER then iPicName = 25			!bushes elderberry
		if iPic3D = PIC_WINTER then iPicName = 26			!bushes elderberry winter
		if iPic3D = PIC_AUTUMN then iPicName = 27			!bushes elderberry fall
		if iPic3D = PIC_FOGGY then iPicName = 28				!bushes elderberry gray
	endif
	
	if iTreeType = TYPE_ROSE then
		if iPic3D = PIC_SUMMER then iPicName = 33			!bush rose summer
		if iPic3D = PIC_WINTER then iPicName = 34			!bush rose winter
		if iPic3D = PIC_AUTUMN then iPicName = 35			!bush rose fall
		if iPic3D = PIC_SPRING then iPicName = 36			!bush rose spring
		if iPic3D = PIC_FOGGY then iPicName = 37				!bush rose grey
	endif
else
	iPicName = 43											!bush general top
	
	if iTreeType = TYPE_GEN then
		if iPic3D = PIC_SUMMER then iPicName = 43			!bush general top
		if iPic3D = PIC_WINTER then iPicName = 44			!bush general winter top
		if iPic3D = PIC_AUTUMN then iPicName = 45			!bush general fall top
		if iPic3D = PIC_FOGGY then iPicName = 46				!bush general top grey
	endif

	if iTreeType =  TYPE_HAZE then
		if iPic3D = PIC_SUMMER then iPicName = 1			!bush hazel top
		if iPic3D = PIC_WINTER then iPicName = 2			!bush hazel winter top
		if iPic3D = PIC_AUTUMN then iPicName = 3			!bush hazel fall top
		if iPic3D = PIC_FOGGY then iPicName = 4				!bush hazel grey top
	endif
	
	if iTreeType = TYPE_ELD then
		if iPic3D = PIC_SUMMER then iPicName = 17			!bushes elderberry top
		if iPic3D = PIC_WINTER then iPicName = 18			!bushes elderberry winter top
		if iPic3D = PIC_AUTUMN then iPicName = 19			!bushes elderberry top fall
		if iPic3D = PIC_FOGGY then iPicName = 20				!bushes elderberry top gray
	endif
	
	if iTreeType = TYPE_ROSE then
		if iPic3D = PIC_SUMMER then iPicName = 66			!bush rose summer top2
		if iPic3D = PIC_WINTER then iPicName = 67			!bush rose winter top
		if iPic3D = PIC_AUTUMN then iPicName = 59			!bush rose fall top
		if iPic3D = PIC_SPRING then iPicName = 63			!bush rose spring top
		if iPic3D = PIC_FOGGY then iPicName = 61				!bush rose gray top
	endif
endif

if bCustomPic3D then _PicName = gs_picture_name

if gs_shadow then
	shadow on
else
	shadow off
endif

pen gs_cont_pen

nTrans = 0
if iViewType3D = TOP_VIEW then
	addz elevation
	nTrans = nTrans + 1

	unID = 1

	HOTSPOT 0, 0, 0, unID 							: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT -1, 0, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT temp_AA, 0, 0, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT 1, 0, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT -temp_AA, 0, 0, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT 0, -1, 0, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT 0, temp_BB, 0, unID, temp_BB, 2, B		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT 0, 1, 0, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT 0, -temp_BB, 0, unID, temp_BB, 2, B		: unID=unID+1

	_PicSize = B

	add -a/2, -b/2, 0
	nTrans = nTrans + 1

	rotx 270
	nTrans = nTrans + 1			
else
	dx = GLOB_EYEPOS_X - GLOB_TARGPOS_X
	dy = GLOB_EYEPOS_Y - GLOB_TARGPOS_Y
	
	rotx 90
	nTrans = nTrans + 1
	
	if SYMB_MIRRORED then
		roty SYMB_ROTANGLE
	else
		roty -SYMB_ROTANGLE
	endif
	nTrans = nTrans + 1
	
	if abs(GLOB_EYEPOS_X - GLOB_TARGPOS_X)<EPS then
		if GLOB_EYEPOS_Y>GLOB_TARGPOS_Y then
			roty 180
			nTrans = nTrans + 1
		endif
	else
	
		alf = atn(abs(dy/dx))
		
		if SYMB_MIRRORED then
			if dx<0 and dy>0 then 
				roty -(180-alf)
				nTrans = nTrans + 1
			endif
			if dx<0 and dy<0 then 
				roty -(180+alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy<0 then 
				roty -(-alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy>0 then 
				roty -(alf)
				nTrans = nTrans + 1
			endif
		else
			if dx<0 and dy>0 then 
				roty (180-alf)
				nTrans = nTrans + 1
			endif
			if dx<0 and dy<0 then 
				roty (180+alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy<0 then 
				roty (-alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy>0 then 
				roty (alf)
				nTrans = nTrans + 1
			endif
		endif
		
		roty 90
		nTrans = nTrans + 1
	endif

	rotx 270
	nTrans = nTrans + 1			

	unID = 1

	HOTSPOT 0, 0, 0, unID								: unID=unID+1

	HOTSPOT 0, 0, ZZYZX/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT -1, 0, ZZYZX/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT temp_AA, 0, ZZYZX/2, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT 0, 0, ZZYZX/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT 1, 0, ZZYZX/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT -temp_AA, 0, ZZYZX/2, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT 0, 0, 0, unID, ZZYZX, 1+128					: unID=unID+1
	HOTSPOT 0, 0, -1, unID, ZZYZX, 3+128				: unID=unID+1
	HOTSPOT 0, 0, ZZYZX, unID, ZZYZX, 2					: unID=unID+1

	addx -a/2
	nTrans = nTrans + 1

	_PicSize = ZZYZX
endif

if bCustomPic3D then
	_Picname = gs_picture_name
	if gs_picture_name = "" then
		if iViewType3D = TOP_VIEW then
			iPicName = 43							!bush general top
		else
			iPicName = 51							!bush general
		endif
		_PicName = iPicName
	endif
else
	_Picname = iPicName
endif

valueOpacity = (1 - iOpacity/100) 
define empty_fill "emptyFill"

DIM	tmp_C4D_ChCol_Color[3]
	tmp_C4D_ChCol_Color[1] = 0.878401
	tmp_C4D_ChCol_Color[2] = 0.513481
	tmp_C4D_ChCol_Color[3] = 0.412253

DIM	tmp_C4D_ChSpe_Color[3]
	tmp_C4D_ChSpe_Color[1] = 0.878401
	tmp_C4D_ChSpe_Color[2] = 0.513481
	tmp_C4D_ChSpe_Color[3] = 0.412253

DEFINE TEXTURE "TextureTree3D" _PicName,   A,   _PicSize,    1,   0
DEFINE MATERIAL "TreeMat3D" 20,
	0, 0, 0,

	0.58, 0.85, 0.04, valueOpacity,
	22,
	0,
	0.878401, 0.513481, 0.412253,

	0, 0, 0,
	0,

	ind(fill, "emptyFill"), 1,
	ind(texture, "TextureTree3D"),
	ADDITIONAL_DATA	C4D_ChSize_HorSize			= A,
					C4D_ChSize_VerSize			= _PicSize,
		
					C4D_ChCol_State				= 1,
					C4D_ChCol_Color				= tmp_C4D_ChCol_Color,	!surface RGB
					C4D_ChCol_Texture			= "1",
					C4D_ShImage_File_1			= _PicName,
		
					C4D_ChDif_State				= 1,
					C4D_ChDif_Brightness		= 0.85,					!diffuseReflection
					C4D_ChDif_Specular			= 0,
		
					C4D_ChTra_State				= (valueOpacity > EPS),
					C4D_ChTra_Brightness		= (valueOpacity ^ (1 / 2.94)),
					C4D_ChTra_Refraction		= 1.0,					!use 1.0 to avoid displacement (1.52 for glass)
		
					C4D_ChRef_State				= 1,
					C4D_ChRef_Brightness		= 0.0244,				!ACShinessConverter (shining) * specular
		
					C4D_ChSpe_State				= 1,
					C4D_ChSpe_Color				= tmp_C4D_ChSpe_Color,
					C4D_ChSpe_Height			= 0.04,					!specular
					C4D_ChSpe_Width				= 0.4025,				! (1 - (shining/100) ^ (1/2.94))
		
					C4D_ChAlp_State				= 1,
					C4D_ChAlp_Texture			= "2",
					C4D_ShImage_File_2			= _PicName,
					C4D_ShImage_AlphaOnly_2		= 1.0

material "TreeMat3D"


vert	EPS, 0, EPS					! 1
vert	A-EPS, 0, EPS				! 2
vert	A-EPS, 0, _PicSize-2*EPS	! 3
vert	EPS, 0, _PicSize-2*EPS		! 4

edge 1, 2, 0, 0, 0
edge 2, 3, 0, 0, 0
edge 3, 4, 0, 0, 0
edge 4, 1, 0, 0, 0

pgon{2} 4, 0, 0, 6,
	-EPS, 0, 0,
	1, 0, 0,
	-EPS, 0, 1,
	-EPS, 1, 0,
	1, 2, 3, 4

body 1

del nTrans
end

