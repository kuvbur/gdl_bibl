
! Hotspots
if iViewType2D = TOP_VIEW | iViewType2D = TOP_VIEW_SYMB then
	unID = 1

	HOTSPOT2 0,0,unID 							: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, -1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, temp_BB, unID, temp_BB, 2, B	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, 1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, -temp_BB, unID, temp_BB, 2, B	: unID=unID+1

else
	unID=1

	HOTSPOT2 0,0,unID 								: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+128					: unID=unID+1
	HOTSPOT2 0, -1, unID, B, 3+128					: unID=unID+1
	HOTSPOT2 0, B, unID, B, 2						: unID=unID+1

endif

if txt_show then
	call "plantName_2D" parameters txt = txt,
			fontType = fontType,
			fontSizeFixed_mm = fontSizeFixed_mm,
			fontSizeFixed_pt = fontSizeFixed_pt,
			txt_pen = txt_pen,
			bBold = bBold,
			bItalic = bItalic,
			bUnderLined = bUnderLined,
			txt_pos_x = txt_pos_x,
			txt_pos_y = txt_pos_y,
			typeTextRotation_m = typeTextRotation_m,
			gs_readable_angle = gs_readable_angle
endif
	
if iViewType2D = TOP_VIEW_SYMB then
	call "treeTopViews" parameters A = A,
		B = B,
		iTopViewSymb = iTopViewSymb,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type = gs_fill_type,
		gs_fill_pen = gs_fill_pen,
		gs_back_pen = gs_back_pen
else
	if iViewType2D = SIDE_VIEW_SYMB then
		call "treeSideViews" parameters A = A, 
			B = B,
			iSideViewSymb = iSideViewSymb,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type = gs_fill_type,
			gs_fill_pen = gs_fill_pen,
			gs_back_pen = gs_back_pen
	else
		if iViewType2D = TOP_VIEW then
			add2 -a/2, -b/2

			iPicName = 1		!deciduous trees maple top
			if iTreeType = TYPE_MAPLE then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 1		!deciduous trees maple top
				if iTopViewPic2D = PIC_WINTER then iPicname = 2		!deciduous trees maple winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 3		!deciduous trees maple fall top	
				if iTopViewPic2D = PIC_FOGGY then iPicname =  4		!deciduous trees maple grey top	
			endif
			
			if iTreeType = TYPE_BIRCH then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 13	!deciduous trees birch top
				if iTopViewPic2D = PIC_WINTER then iPicname = 14	!deciduous trees birch top winter	
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 15	!deciduous trees birch top fall	
				if iTopViewPic2D = PIC_FOGGY then iPicname = 16		!deciduous trees birch grey top	
			endif
			
			if iTreeType = TYPE_BEECH then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 29	!deciduous trees beech top
				if iTopViewPic2D = PIC_WINTER then iPicname = 30	!deciduous trees beech winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 31	!deciduous trees beech fall top
				if iTopViewPic2D = PIC_FOGGY then iPicname = 32		!deciduous trees beech grey top
			endif
			
			if iTreeType = TYPE_CHERRY then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 86	!cherry top
				if iTopViewPic2D = PIC_WINTER then iPicname = 88	!cherry winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 73	!cherry fall top
				if iTopViewPic2D = PIC_SPRING then iPicname = 81	!cherry spring top
				if iTopViewPic2D = PIC_FOGGY then iPicname = 77		!cherry gray top
			endif
			
			if iTreeType = TYPE_LINDEN then
				if iTopViewPic2D = PIC_SUMMER then iPicname = 47	!deciduous trees linden top 2
				if iTopViewPic2D = PIC_WINTER then iPicname = 48	!deciduous trees linden winter top
				if iTopViewPic2D = PIC_AUTUMN then iPicname = 49	!deciduous trees linden fall top
				if iTopViewPic2D = PIC_FOGGY then iPicname = 50		!deciduous trees linden gray top
			endif
						
		else
			add2 -a/2, 0

			iPicName = 9		!deciduous trees maple
			if iTreeType = TYPE_MAPLE then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 9		!deciduous trees maple
				if iSideViewPic2D = PIC_WINTER then iPicname = 10		!deciduous trees maple winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 11		!deciduous trees maple fall
				if iSideViewPic2D = PIC_FOGGY then iPicname = 12		!deciduous trees maple grey
			endif
			
			if iTreeType = TYPE_BIRCH then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 21		!deciduous trees birch
				if iSideViewPic2D = PIC_WINTER then iPicname = 22		!deciduous trees birch winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 23		!deciduous trees birch fall
				if iSideViewPic2D = PIC_FOGGY then iPicname = 24			!deciduous trees birch grey
			endif
			
			if iTreeType = TYPE_BEECH then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 37		!deciduous trees beech
				if iSideViewPic2D = PIC_WINTER then iPicname = 38		!deciduous trees beech winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 39		!deciduous trees beech fall
				if iSideViewPic2D = PIC_FOGGY then iPicname = 40			!deciduous trees beech grey
			endif
			
			if iTreeType = TYPE_CHERRY then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 92		!cherry
				if iSideViewPic2D = PIC_WINTER then iPicname = 91		!cherry winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 76		!cherry fall
				if iSideViewPic2D = PIC_SPRING then iPicname = 84		!cherry spring
				if iSideViewPic2D = PIC_FOGGY then iPicname = 80			!cherry gray
			endif
			
			if iTreeType = TYPE_LINDEN then
				if iSideViewPic2D = PIC_SUMMER then iPicname = 61		!deciduous trees linden
				if iSideViewPic2D = PIC_WINTER then iPicname = 62		!deciduous trees linden winter
				if iSideViewPic2D = PIC_AUTUMN then iPicname = 63		!deciduous trees linden fall
				if iSideViewPic2D = PIC_FOGGY then iPicname = 64			!deciduous trees linden grey
			endif
		endif

		if bCustomPic2D then
			_Picname = gs_picture_name_2D
			if gs_picture_name_2D = "" then
				if iViewType2D = TOP_VIEW then
					iPicName = 1							! deciduous trees maple top
				else
					iPicName = 9							! deciduous trees maple
				endif
				_PicName = iPicName
			endif
		else
			_Picname = iPicName
		endif

		fill selection_fill_type
		add2 A/2, B/2
		if iViewType2D = TOP_VIEW then
			mul2 1, B/A
			poly2_B 2, 2+4+64, 1, 0,
					0, 0, 900,
					A/2, 360, 4000
			del 1
		else
			poly2_B 4, 2+4, 1, 0,
					-A/2, -B/2, 0,
					-A/2, B/2, 0,
					A/2, B/2, 0,
					A/2, -B/2, 0
		endif
		del 1

		picture2{2}  _Picname,a,b,1	

		del 1
	endif
endif

end

