
if iViewType2D = SIDE_VIEW | iViewType2D = SIDE_VIEW_SYMB | not(bShowasBitmap) then end

if iViewType3D = SIDE_VIEW then
	iPicName = 9											!deciduous trees maple
	if iTreeType = TYPE_MAPLE then
		if iPic3D = PIC_SUMMER then iPicName = 9			!deciduous trees maple
		if iPic3D = PIC_WINTER then iPicName = 10			!deciduous trees maple winter
		if iPic3D = PIC_AUTUMN then iPicName = 11			!deciduous trees maple fall
		if iPic3D = PIC_FOGGY then iPicName = 12				!deciduous trees maple grey
	endif
	
	if iTreeType = TYPE_BIRCH then
		if iPic3D = PIC_SUMMER then iPicName = 21			!deciduous trees birch
		if iPic3D = PIC_WINTER then iPicName = 22			!deciduous trees birch winter
		if iPic3D = PIC_AUTUMN then iPicName = 23			!deciduous trees birch fall
		if iPic3D = PIC_FOGGY then iPicName = 24				!deciduous trees birch grey
	endif
	
	if iTreeType = TYPE_BEECH then
		if iPic3D = PIC_SUMMER then iPicName = 37			!deciduous trees beech
		if iPic3D = PIC_WINTER then iPicName = 38			!deciduous trees beech winter
		if iPic3D = PIC_AUTUMN then iPicName = 39			!deciduous trees beech fall
		if iPic3D = PIC_FOGGY then iPicName = 40				!deciduous trees beech grey
	endif
	
	if iTreeType = TYPE_CHERRY then
		if iPic3D = PIC_SUMMER then iPicName = 92			!cherry
		if iPic3D = PIC_WINTER then iPicName = 91			!cherry winter
		if iPic3D = PIC_AUTUMN then iPicName = 76			!cherry fall
		if iPic3D = PIC_SPRING then iPicName = 84			!cherry spring
		if iPic3D = PIC_FOGGY then iPicName = 80				!cherry gray
	endif
	
	if iTreeType = TYPE_LINDEN then
		if iPic3D = PIC_SUMMER then iPicName = 61			!deciduous trees linden
		if iPic3D = PIC_WINTER then iPicName = 62			!deciduous trees linden winter
		if iPic3D = PIC_AUTUMN then iPicName = 63			!deciduous trees linden fall
		if iPic3D = PIC_FOGGY then iPicName = 64				!deciduous trees linden gray
	endif
else
	iPicName = 1											!decidupus trees maple top
	if iTreeType = TYPE_MAPLE then
		if iPic3D = PIC_SUMMER then iPicName = 1			!deciduous trees maple top
		if iPic3D = PIC_WINTER then iPicName = 2			!deciduous trees maple winter top
		if iPic3D = PIC_AUTUMN then iPicName = 3			!deciduous trees maple fall top
		if iPic3D = PIC_FOGGY then iPicName = 4				!deciduous trees maple grey top
	endif
	
	if iTreeType = TYPE_BIRCH then
		if iPic3D = PIC_SUMMER then iPicName = 13			!deciduous trees birch top
		if iPic3D = PIC_WINTER then iPicName = 14			!deciduous trees birch top winter
		if iPic3D = PIC_AUTUMN then iPicName = 15			!deciduous trees birch top fall
		if iPic3D = PIC_FOGGY then iPicName = 16				!deciduous trees birch grey top
	endif
	
	if iTreeType = TYPE_BEECH then
		if iPic3D = PIC_SUMMER then iPicName = 29			!deciduous trees beech top
		if iPic3D = PIC_WINTER then iPicName = 30			!deciduous trees beech winter top
		if iPic3D = PIC_AUTUMN then iPicName = 31			!deciduous trees beech fall top
		if iPic3D = PIC_FOGGY then iPicName = 32				!deciduous trees beech grey top
	endif
	
	if iTreeType = TYPE_CHERRY then
		if iPic3D = PIC_SUMMER then iPicName = 86			!deciduous trees cherry top
		if iPic3D = PIC_WINTER then iPicName = 88			!deciduous trees cherry winter top
		if iPic3D = PIC_AUTUMN then iPicName = 73			!deciduous trees cherry fall top
		if iPic3D = PIC_SPRING then iPicName = 81			!deciduous trees cherry spring top
		if iPic3D = PIC_FOGGY then iPicName = 77				!deciduous trees cherry gray top
	endif
	
	if iTreeType = TYPE_LINDEN then
		if iPic3D = PIC_SUMMER then iPicName = 47			!deciduous trees linden top 2
		if iPic3D = PIC_WINTER then iPicName = 48			!deciduous trees linden winter top
		if iPic3D = PIC_AUTUMN then iPicName = 49			!deciduous trees linden fall top
		if iPic3D = PIC_FOGGY then iPicName = 50				!deciduous trees linden gray top
	endif
endif

if bCustomPic3D then _PicName = gs_picture_name

if gs_shadow then
	shadow on
else
	shadow off
endif

pen gs_cont_pen

nTrans = 0
if iViewType3D = TOP_VIEW then
	addz elevation
	nTrans = nTrans + 1

	unID = 1

	HOTSPOT 0, 0, 0, unID 							: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT -1, 0, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT temp_AA, 0, 0, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT 1, 0, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT -temp_AA, 0, 0, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT 0, -1, 0, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT 0, temp_BB, 0, unID, temp_BB, 2, B		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT 0, 1, 0, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT 0, -temp_BB, 0, unID, temp_BB, 2, B		: unID=unID+1

	_PicSize = B

	add -a/2, -b/2, 0
	nTrans = nTrans + 1

	rotx 270
	nTrans = nTrans + 1			
else
	dx = GLOB_EYEPOS_X - GLOB_TARGPOS_X
	dy = GLOB_EYEPOS_Y - GLOB_TARGPOS_Y
	
	rotx 90
	nTrans = nTrans + 1
	
	if SYMB_MIRRORED then
		roty SYMB_ROTANGLE
	else
		roty -SYMB_ROTANGLE
	endif
	nTrans = nTrans + 1
	
	if abs(GLOB_EYEPOS_X - GLOB_TARGPOS_X)<EPS then
		if GLOB_EYEPOS_Y>GLOB_TARGPOS_Y then
			roty 180
			nTrans = nTrans + 1
		endif
	else
	
		alf = atn(abs(dy/dx))
		
		if SYMB_MIRRORED then
			if dx<0 and dy>0 then 
				roty -(180-alf)
				nTrans = nTrans + 1
			endif
			if dx<0 and dy<0 then 
				roty -(180+alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy<0 then 
				roty -(-alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy>0 then 
				roty -(alf)
				nTrans = nTrans + 1
			endif
		else
			if dx<0 and dy>0 then 
				roty (180-alf)
				nTrans = nTrans + 1
			endif
			if dx<0 and dy<0 then 
				roty (180+alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy<0 then 
				roty (-alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy>0 then 
				roty (alf)
				nTrans = nTrans + 1
			endif
		endif
		
		roty 90
		nTrans = nTrans + 1
	endif

	rotx 270
	nTrans = nTrans + 1			

	unID = 1

	HOTSPOT 0, 0, 0, unID								: unID=unID+1

	HOTSPOT 0, 0, ZZYZX/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT -1, 0, ZZYZX/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT temp_AA, 0, ZZYZX/2, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT 0, 0, ZZYZX/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT 1, 0, ZZYZX/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT -temp_AA, 0, ZZYZX/2, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT 0, 0, 0, unID, ZZYZX, 1+128					: unID=unID+1
	HOTSPOT 0, 0, -1, unID, ZZYZX, 3+128				: unID=unID+1
	HOTSPOT 0, 0, ZZYZX, unID, ZZYZX, 2					: unID=unID+1

	addx -a/2
	nTrans = nTrans + 1

	_PicSize = ZZYZX
endif

if bCustomPic3D then
	_Picname = gs_picture_name
	if gs_picture_name = "" then
		if iViewType3D = TOP_VIEW then
			iPicName = 1							!deciduous trees maple top
		else
			iPicName = 9							!deciduous trees maple
		endif
		_PicName = iPicName
	endif
else
	_Picname = iPicName
endif

valueOpacity = (1 - iOpacity/100)
define empty_fill "emptyFill"

DIM	tmp_C4D_ChCol_Color[3]
	tmp_C4D_ChCol_Color[1] = 0.878401
	tmp_C4D_ChCol_Color[2] = 0.513481
	tmp_C4D_ChCol_Color[3] = 0.412253

DIM	tmp_C4D_ChSpe_Color[3]
	tmp_C4D_ChSpe_Color[1] = 0.878401
	tmp_C4D_ChSpe_Color[2] = 0.513481
	tmp_C4D_ChSpe_Color[3] = 0.412253

DEFINE TEXTURE "TextureTree3D" _PicName,   A,   _PicSize,    1,   0
DEFINE MATERIAL "TreeMat3D" 20,
	0, 0, 0,

	0.58, 0.85, 0.04, valueOpacity,
	22,
	0,
	0.878401, 0.513481, 0.412253,

	0, 0, 0,
	0,

	ind(fill, "emptyFill"), 1,
	ind(texture, "TextureTree3D"),
	ADDITIONAL_DATA	C4D_ChSize_HorSize			= A,
					C4D_ChSize_VerSize			= _PicSize,
		
					C4D_ChCol_State				= 1,
					C4D_ChCol_Color				= tmp_C4D_ChCol_Color,	!surface RGB
					C4D_ChCol_Texture			= "1",
					C4D_ShImage_File_1			= _PicName,
		
					C4D_ChDif_State				= 1,
					C4D_ChDif_Brightness		= 0.85,					!diffuseReflection
					C4D_ChDif_Specular			= 0,
		
					C4D_ChTra_State				= (valueOpacity > EPS),
					C4D_ChTra_Brightness		= (valueOpacity ^ (1 / 2.94)),
					C4D_ChTra_Refraction		= 1.0,					!use 1.0 to avoid displacement (1.52 for glass)
		
					C4D_ChRef_State				= 1,
					C4D_ChRef_Brightness		= 0.0244,				!ACShinessConverter (shining) * specular
		
					C4D_ChSpe_State				= 1,
					C4D_ChSpe_Color				= tmp_C4D_ChSpe_Color,
					C4D_ChSpe_Height			= 0.04,					!specular
					C4D_ChSpe_Width				= 0.4025,				! (1 - (shining/100) ^ (1/2.94))
		
					C4D_ChAlp_State				= 1,
					C4D_ChAlp_Texture			= "2",
					C4D_ShImage_File_2			= _PicName,
					C4D_ShImage_AlphaOnly_2		= 1.0

material "TreeMat3D"


vert	EPS, 0, EPS					! 1
vert	A-EPS, 0, EPS				! 2
vert	A-EPS, 0, _PicSize-2*EPS	! 3
vert	EPS, 0, _PicSize-2*EPS		! 4

edge 1, 2, 0, 0, 0
edge 2, 3, 0, 0, 0
edge 3, 4, 0, 0, 0
edge 4, 1, 0, 0, 0

pgon{2} 4, 0, 0, 6,
	-EPS, 0, 0,
	1, 0, 0,
	-EPS, 0, 1,
	-EPS, 1, 0,
	1, 2, 3, 4

body 1

del nTrans
end

