
TABID_ROOT			= -1
TABID_TREE_SHAPES	= 1
TABID_TREE_DIM		= 2
TABID_TREE_CROWN	= 3

stTabTitlesTreeShapes	= `Формы Деревьев`
stTabTitlesDim			= `Размеры и Ствол`
stTabTitlesCrown		= `Крона`

uiTabIconsTreeShapes	= "uiTab_Style_flat(1)"
uiTabIconsDim			= "uiTab_TreeDimTrunk_flat(1)"
uiTabIconsCrown			= "uiTab_TreeCrown_flat(1)"

file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_TreeDimTrunk_flat(1)"
file_dependence "uiTab_TreeCrown_flat(1)"

rowy = 19

ui_current_page gs_ui_current_page

if TABID_TREE_SHAPES > 0 then
	ui_page TABID_TREE_SHAPES, TABID_ROOT, stTabTitlesTreeShapes, uiTabIconsTreeShapes
	gosub "TreeShapesPage"
endif

if TABID_TREE_DIM > 0 then
	ui_page TABID_TREE_DIM, TABID_ROOT, stTabTitlesDim, uiTabIconsDim
	gosub "DimTrunkPage"
endif

if TABID_TREE_CROWN > 0 then
	ui_page TABID_TREE_CROWN, TABID_ROOT, stTabTitlesCrown, uiTabIconsCrown
	gosub "CrownPage"
endif

call "plant_macro" parameters all	iPlantType = TYPE_DEC,
									bCalledAsTabPage = 1,
									b2DTabShowSurfaces	= 1,
									iViewType2D = TOP_VIEW_SYMB


!================================================================================
end ! end ! end !end ! end ! end ! end ! end ! end end ! end ! end ! end ! end !
!================================================================================


"TreeShapesPage":
	! ===== Page 1 : Tree Shapes ===== !

	pageStartY = 20
	pageStartX = 2
	
	ui_style 0, 1
	ui_outfield `Сброс Формы Дерева к:`, pageStartX, pageStartY, 200, 15
	ui_style 0, 0
	
	
	buttonHeight = 87
	buttonWidth = 87
	gap = 1
	
	buttonX = pageStartX
	buttonY = pageStartY + 17
	
	ui_pict_button UI_FUNCTION, "", 1, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_1*10		! tree1.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 2, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_2*10		! tree2.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 3, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_3*10		! tree3.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 4, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_4*10		! tree4.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 5, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_5*10		! tree5.png
	buttonX = buttonX + buttonWidth + gap
	
	buttonX = pageStartX
	buttonY = pageStartY + 17 + buttonHeight + gap
	
	ui_pict_button UI_FUNCTION, "", 6, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_6*10		! tree6.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 7, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_7*10		! tree7.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 8, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_8*10		! tree8.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 9, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_9*10		! tree9.png
	buttonX = buttonX + buttonWidth + gap
	
	ui_pict_button UI_FUNCTION, "", 10, buttonX, buttonY, buttonWidth, buttonHeight, TREE_FORM_10*10		! tree10.png
	buttonX = buttonX + buttonWidth + gap
return


"DimTrunkPage":
	! ===== Page 2 : Dimensions and Trunk ===== !

	pageStartY = 5

	x=295									! picture
	y=50

	x1=2
	x2=145
	y0=pageStartY + 4
	dy=24

	heightOutf = 15

	descx = 157
	contx = 95

	ui_style 0, 0

	if bBranches then
		ui_pict 51, x, y, 114, pageStartY + 145, 1		! ui_Fronded_tree2
	else
		ui_pict 50, x, y, 114, pageStartY + 145, 1		! ui_Fronded_tree1
	endif

	ui_outfield `Ширина`, x1, y0, descx, heightOutf 
	ui_infield "a", x1+descx+1, y0 - 4, contx, rowy
	y0=y0+dy

	ui_outfield `Длина`, x1, y0, descx, heightOutf 
	ui_infield "b", x1+descx+1, y0 - 4, contx, rowy
	y0=y0+dy

	ui_outfield `Высота`, x1, y0, descx, heightOutf 
	ui_infield "zzyzx", x1+descx+1, y0 - 4, contx, rowy
	y0=y0+dy*1.5

	ui_infield{3} "bBranches",	x1, y0 - 4, descx+contx, rowy,
			7,"",2,1,
			0,0,0,0,
			1,`Показ Веток`, 0,
			2,`Показ Веток`, 1
	y0=y0+dy

	ui_outfield `Разрешение Ствола`, x1, y0, descx, heightOutf 
	ui_infield "resBranch", x1+descx+1, y0 - 4, contx, rowy
	y0=y0+dy

	ui_outfield `Высота Ствола`, x1, y0, descx, heightOutf 
	ui_infield "hTrunk", x1+descx+1, y0 - 4, contx, rowy
	y0=y0+dy

	ui_outfield `Диаметр Ствола в 3D`, x1, y0, descx, heightOutf 
	ui_infield "diaTrunk", x1+descx+1, y0 - 4, contx, rowy
	!y0=y0+1.5*dy
	y0=y0+dy

	ui_outfield `Показ в 3D`, x1, y0, descx, heightOutf 
	ui_infield{3} "iDetlevel3D", x1+descx+1, y0, contx, rowy,
			8, "", 2, 1, 0, 0, 0, 0,
			1, stDetlevel3D[3], DETLEVEL3D_DETAILED,
			2, stDetlevel3D[1], DETLEVEL3D_OFF
	y0=y0+dy

	ui_infield{3} "gs_shadow",	x1, y0 - 4, descx+contx, rowy,
			7,"",2,1,
			0,0,0,0,
			1,`Отбрасывание Теней`, 0,
			2,`Отбрасывание Теней`, 1

return


"CrownPage":
	! ===== Page 3 : Crown ===== !

	pageStartY = 5

	x=330											! picture
	y=80

	x1=30
	x2=200
	x3=2
	x4=200
	x6=90

	y0=pageStartY 
	dy=20

	heightOutf = 15

	ui_style 0, 0

	if autoCrownType then
		ui_pict 52, x-32, y, 140, pageStartY + 120, 3			! ui_Fronded_tree_auto
	else
		if iCrownType = CROWN_DETAILED then
			ui_pict 53, x, y, 80, pageStartY + 95, 1			! ui_Fronded_tree_det
		endif
		if iCrownType = CROWN_SIMPLIFIED then
			ui_pict 54, x, y, 80, pageStartY + 95, 1			! ui_Fronded_tree_sema
		endif
		if iCrownType = CROWN_ELLIPSOID then
			ui_pict 55, x, y, 80, pageStartY + 95, 1			! ui_Fronded_tree_einf
		endif
	endif

	ui_pict 56, x3, y0+18, 18, pageStartY + 37, 1				! ui_Fronded_tree_absrt

	ui_infield{3} "autoCrownType",	x3 + 30, y0 - 4, 258, rowy,
			7,"",2,1,
			0,0,0,0,
			1,`Автоматический Тип Кроны`, 0,
			2,`Автоматический Тип Кроны`, 1
	dy = 24
	y0=y0+dy

	ui_outfield `Диапазон Детальной Кроны`, x3+30, y0, 154, heightOutf, 4*not(autoCrownType)
	ui_infield "rangeDetCrown", x4, y0 - 4, x6, rowy
	y0=y0+dy

	ui_outfield `Диапазон Простой Кроны`, x3+30, y0, 154, heightOutf, 4*not(autoCrownType)
	ui_infield "rangeSimpCrown", x4, y0, x6, rowy
	y0=y0+dy

	ui_outfield `Эллипс при превышении Большего Диапазона`, x3+30, y0, 258, heightOutf, 4*not(autoCrownType)
	y0=y0+dy

	ui_outfield `Тип`, x3, y0, x2-x3-1, heightOutf, 4*(autoCrownType)

	ui_infield{3} "iCrownType", x2, y0 - 4, x6, rowy,
			8, "", 3, 1, 0, 0, 0, 0,
			1, stCrownType[1], CROWN_DETAILED,
			2, stCrownType[2], CROWN_SIMPLIFIED,
			3, stCrownType[3], CROWN_ELLIPSOID
	y0=y0+dy+6

	ui_separator 0, y0-10, 295, y0-10
	dy = 22
	
	ui_outfield `Плотность в %`, x3, y0, x2-x3-1, heightOutf
	ui_infield "crownDensity", x2, y0 - 4, x6, rowy
	y0=y0+dy

	ui_outfield `Разрешение Кроны`, x3, y0, x2-x3-1, heightOutf
	ui_infield "resCrown", x2, y0 - 4, x6, rowy
	y0=y0+dy

	ui_outfield `Диаметр Сферы Кроны`, x3, y0, x2-x3-1, heightOutf
	ui_infield "diaCrownSphere", x2, y0 - 4, x6, rowy
	y0=y0+dy

	ui_outfield `Разрешение Листьев`, x3, y0, x2-x3-1, heightOutf
	ui_infield "resLeaves", x2, y0 - 4, x6, rowy
	y0=y0+dy

	ui_outfield `Диаметр Листьев`, x3, y0, x2-x3-1, heightOutf
	ui_infield "diaLeaves", x2, y0 - 4, x6, rowy

	if _bSwitchEllipsInSettings then
		ui_pict 57, 410, y0, 12, 12, 1 ui_tooltip `Для дальнейшего редактирования в диалоге отображен простой 3D-вид.`	! ui_warningsign.png
	endif
return






