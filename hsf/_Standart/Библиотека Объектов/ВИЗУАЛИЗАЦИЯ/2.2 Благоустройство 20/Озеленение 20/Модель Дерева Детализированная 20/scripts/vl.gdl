
call "FM_types" parameters all

values "iTreeForm"	TREE_FORM_1,
					TREE_FORM_2,
					TREE_FORM_3,
					TREE_FORM_4,
					TREE_FORM_5,
					TREE_FORM_6,
					TREE_FORM_7,
					TREE_FORM_8,
					TREE_FORM_9,
					TREE_FORM_10


if GLOB_UI_BUTTON_ID > 9 | GLOB_MODPAR_NAME = "iTreeForm" then

	if GLOB_UI_BUTTON_ID > 9 then iTreeForm = GLOB_UI_BUTTON_ID/10

	A = sizeDefaultA[iTreeForm]
	temp_AA = A/2
	B = sizeDefaultB[iTreeForm]
	temp_BB = A/2
	Zzyzx = sizeDefaultZzyzx[iTreeForm]

	hTrunk			= heightDefaultTrunk[iTreeForm]
	diaTrunk		= diaDefaultTrunk[iTreeForm]
	diaCrownSphere	= diaDefaultCrownSphere[iTreeForm]
	diaLeaves		= diaDefaultLeaves[iTreeForm]
	resBranch		= _resolBranch[iTreeForm]
	crownDensity	= _crownDensity[iTreeForm]
	resCrown		= _resolCrown[iTreeForm]
	resLeaves		= _resolLeaves[iTreeForm]

	parameters	A = A,
				temp_AA = temp_AA,
				B = B,
				temp_BB = temp_BB,
				Zzyzx = Zzyzx,
				hTrunk = hTrunk,
				diaTrunk = diaTrunk,
				diaCrownSphere = diaCrownSphere,
				diaLeaves = diaLeaves,
				resBranch = resBranch,
				crownDensity = crownDensity,
				resCrown = resCrown,
				resLeaves = resLeaves,
				iTreeForm = iTreeForm
endif


values{2} "iCrownType"	CROWN_DETAILED, stCrownType[1],
						CROWN_SIMPLIFIED, stCrownType[2],
						CROWN_ELLIPSOID, stCrownType[3]
						

values{2} "iDetlevel3D"	DETLEVEL3D_DETAILED,	stDetlevel3D[3],
						DETLEVEL3D_OFF,			stDetlevel3D[1]

values "zzyzx" range (0,)
values "hTrunk" range [0, zzyzx)
values "diaTrunk" range (0, )
values "resBranch" range [3, )
values "crownDensity" range (0, )
values "resCrown" range [3, )
values "diaCrownSphere" range (0, )
values "resLeaves" range [3, )
values "diaLeaves" range (0, )
values "rangeDetCrown" range (0, rangeSimpCrown)
values "rangeSimpCrown" range (rangeDetCrown, )

if autoCrownType = 0 then
	lock "rangeDetCrown"
	lock "rangeSimpCrown"
	if iCrownType = CROWN_SIMPLIFIED or iCrownType = CROWN_ELLIPSOID then
	  lock "resLeaves"
	  lock "diaLeaves"
	endif
else
	lock "iCrownType"
endif

values "gs_ui_current_page" 1, 2, 3, 11, 13, 14

call "Fronded_tree_ui_macro-10" parameters all

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	rrr = REQUEST ("Name_of_macro", "", LPName)
	call "Onorm_Plant" parameters all sLibpartName = strsub(LPName,0,strlen(LPName)-4)
else
	hideparameter "gs_onorm_Title"
endif
