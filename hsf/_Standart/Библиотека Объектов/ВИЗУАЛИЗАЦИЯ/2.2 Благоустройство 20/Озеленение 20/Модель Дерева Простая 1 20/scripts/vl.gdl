
call "FM_types" parameters all

values{2} "iDetlevel3D"	DETLEVEL3D_DETAILED,	stDetlevel3D[1],
						DETLEVEL3D_SIMPLE,		stDetlevel3D[2],
						DETLEVEL3D_OFF,			stDetlevel3D[3]

values{2} "iTreeForm"	TREE_FORM_1,			stTreeForms[1],
						TREE_FORM_2,			stTreeForms[2]


if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=91

IF iDetlevel3D = DETLEVEL3D_SIMPLE then
  lock "numNodeLeaf","bRandomNumNodeLeaf","angleRotLeaf",
	   "diaLeaves","numLeaves","spreadLeaves","lengthBranch","angleMaxBranch"
ENDIF

values "gs_resol" range [3, )
if iTreeForm = TREE_FORM_1 then
	values "zzyzx" range [lengthBranch+hTrunk,)
else
	values "zzyzx" range [hTrunk+0.1,)
endif
values "hTrunk" range (0,)
values "diaTrunk" range (0,)
values "numNodeLeaf" range [3,5]
values "crownDensity" range (0, )

if GLOB_MODPAR_NAME = "crownDensity" then
	numLeaves	= crownDensity * 10
	parameters numLeaves = numLeaves
else
	crownDensity = numLeaves / 10
	parameters crownDensity = crownDensity
endif

values "gs_ui_current_page" 1, 11, 13, 14

call "Fronded_tree_ui_macro-10" parameters all


if iTreeForm = TREE_FORM_1 then
	hideparameter	"angleMinBranch",
					"numDebranch"
	lock	"angleMinBranch",
			"numDebranch"

else
	hideparameter	"iDetlevel3D",
					"diaTrunk",
					"numNodeLeaf",
					"bRandomNumNodeLeaf",
					"angleRotLeaf",
					"diaLeaves",
					"spreadLeaves"
	lock	"iDetlevel3D",
			"diaTrunk",
			"numNodeLeaf",
			"bRandomNumNodeLeaf",
			"angleRotLeaf",
			"diaLeaves",
			"spreadLeaves"

endif

if iTreeForm = TREE_FORM_1 then
	AC_show2DHotspotsIn3D	= 1
else
	AC_show2DHotspotsIn3D	= 0
	hideparameter "zzyzx"
endif
parameters AC_show2DHotspotsIn3D	= AC_show2DHotspotsIn3D

if GLOB_MODPAR_NAME = "iTreeForm" then
	if iTreeForm = TREE_FORM_1 then
		A = 4
		temp_AA = 2
		B = 4
		temp_BB = 2
		ZZYZX = 8
	else
		A = 3
		temp_AA = 1.5
		B = 3
		temp_BB = 1.5
		lengthBranch = 1.5
		hTrunk	= 2
		parameters	lengthBranch = lengthBranch,
					hTrunk		= hTrunk
		ZZYZX = 3.5
	endif
	parameters 	A = A,
				B = B,
				temp_AA = temp_AA,
				temp_BB = temp_BB,
				ZZYZX = ZZYZX
endif

if iTreeForm = TREE_FORM_2 then
	if GLOB_MODPAR_NAME = "hTrunk" | GLOB_MODPAR_NAME = "lengthBranch" then
		zzyzx	= hTrunk + lengthBranch
		parameters zzyzx = zzyzx
	else
		lengthBranch	= zzyzx - hTrunk
		parameters lengthBranch = lengthBranch
	endif
endif

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	rrr = REQUEST ("Name_of_macro", "", LPName)
	call "Onorm_Plant" parameters all sLibpartName = strsub(LPName,0,strlen(LPName)-4)
else
	hideparameter "gs_onorm_Title"
endif
