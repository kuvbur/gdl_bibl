
if iDetlevel3D = DETLEVEL3D_OFF then end

IF gs_shadow=0 THEN SHADOW OFF


pen gs_cont_pen
material gs_leave_mat


if iHedgeType = HEDGE_LINEAR then
	GOSUB "Linear"
else
	if iHedgeType = HEDGE_BENDED then
		gosub "Bended"
	else
		gosub "End"
	endif
endif

!================================================================================
end ! end ! end !end ! end ! end ! end ! end ! end end ! end ! end ! end ! end !
!================================================================================


!================================================================================
! Linear Hedge Element
!================================================================================
"Linear":


fls=2*a*zzyzx
fld=a*b
flg=fls+fld


for y=1 to nul	! Number of Leaves

	flz=rnd(flg)

	if flz>=fld then
		kox=rnd(a)
		koy=int(rnd(1)+0.5)*b
		koz=rnd(zzyzx-dl/3)

		add kox,koy,koz
		rotx 90
	else
		kox=rnd(a)
		koy=dl/3+rnd(b-2/3*dl)

		add kox,koy,zzyzx
		rotx 0
	endif

	rotz rnd(180)
	rotx rol

	gosub "Leaves"

	del 4
next y

return

!================================================================================
! Bended Hedge Element
!================================================================================
"Bended":

if breit<0 then breit=0
if rad<=0 then rad=1

if wink >=360 then wink=360
if wink<=0 then
	wink=-wink
	muly -1
endif


flsi=wink/360*2*rad*pi*zzyzx
flsa=wink/360*2*(rad+breit)*pi*zzyzx
fld=wink/360*(rad+breit)^2*pi-wink/360*rad^2*pi

flg=flsi+flsa+fld

for y=1 to nul       ! ***********  Schleife Blatter

	flz=rnd(flg)

	rotz rnd(wink)

	if flz>=fld then              ! ********************* Seitenflachen oder Deckflache
		koz=rnd(zzyzx-dl/3)
		if  flz>=fld+flsi then      ! ***************** Seitenflache AUSSEN
			addx rad+breit
			addz koz
		else                            ! ******************* Seitenflache INNEN
			addx rad
			addz koz
		endif                  ! ********************* Seitenflachen Ende
		roty 90
	else                            ! ******************** Deckflache
		addx rad+dl/3+rnd(breit-2/3*dl)
		addz zzyzx
		rotx 0
	endif                    !*****************    Flachen Ende

	rotz rnd(180)
	rotx rol

	gosub "Leaves"

	del 6
next y

if wink<=0 then del 1

return

!================================================================================
! End Hedge Element
!================================================================================
"End":

for y=1 to nul

	koy=dl/3+rnd(b-dl*2/3)
	koz=rnd(zzyzx-dl/3)

	add 0,koy,koz
	rotz 90
	rotx 90
	rotz rnd(180)
	rotx rol

	gosub "Leaves"

	del 5
next y

return


!================================================================================
! Leaves
!================================================================================
"Leaves":

mulx dl/0.2
muly dl/0.2

if rlp then lpn = int(rnd(2.99))+3	! Random node number 3-5

if lpn = 5 then
	poly 5,
		0,0.1,
		0.0951,0.0309,
		0.0588,-0.0809,
		-0.0588,-0.0809,
		-0.0951,0.0309
else
	if lpn = 4 then
		poly 4,
			0,0.1,
			0.1,0,
			0,-0.1,
			-0.1,0
	else
		poly 3,
			0,0.1,
			0.0866,-0.05,
			-0.0866,-0.05
	endif
endif

del 2

return




