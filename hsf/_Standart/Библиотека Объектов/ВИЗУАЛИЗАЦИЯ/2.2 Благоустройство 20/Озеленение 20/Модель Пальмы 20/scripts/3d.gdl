
if iDetlevel3D = DETLEVEL3D_OFF then end
if not(gs_shadow) then SHADOW OFF

hotspot 0,0,zzyzx
pen gs_cont_pen

MATERIAL gs_trunk_mat

if iPalmTreeType = PALMTYPE_GENERAL then
	CONE ZZYZX, diaLowerTrunk/2, diaUpperTrunk/2, 90, 90
	
	ADDZ zzyzx-0.3
	
	! === extra bark ===============================================================
	if iDetlevel3D = DETLEVEL3D_DETAILED then		! Detailed

		ADDZ -1.15
		nBarkLevels = 12
		hBarkLevel = 1.15 / nBarkLevels
		for zz = 0 to nBarkLevels-1
			for z=1 to 8
				tempVal = ((diaLowerTrunk-diaUpperTrunk)/ZZYZX*(1.35-zz*hBarkLevel)+diaUpperTrunk)/2
				addx tempVal
				mulx 2*tempVal/0.43
				muly 2*tempVal/0.43
				call "DPLS" parameters all
				del 3
				rotz 360/8
			next z
			del 8
	
			ROTZ 22.5
			ADDZ hBarkLevel
		next zz
	else				! Simple
		if sel then
			lnum = 8
		endif
	endif
	
	! === leaves ===============================================================
	ADDZ 0.05
	FOR Z=1 TO lnum/2
		ADDX diaUpperTrunk*1.13/2
		MATERIAL stlmat
		MULX (0.8+rnd(0.4))*(A-diaUpperTrunk*1.13)*0.5/3.4706
		MULY (a-diaUpperTrunk*1.13)*0.5/3.4706
		if iDetlevel3D = DETLEVEL3D_DETAILED then
			MULZ 0.9+rnd(0.2)
		else
			MULZ 1
		endif
		call "CPLS" parameters all
		MATERIAL gs_leave_mat
		gosub "call_leafMacro"
		DEL 4
	
		ROTZ 360/lnum
		ADDZ 0.25
		ADDX diaUpperTrunk*1.15/2
		MATERIAL stlmat
		MULX (A-diaUpperTrunk*1.15)*0.8*0.5/3.4706
		MULY (a-diaUpperTrunk*1.15)*0.8*0.5/3.4706
		call "CPLS" parameters all
		MATERIAL gs_leave_mat
		gosub "call_leafMacro"
		DEL 5
		ROTZ 720/lnum
	NEXT Z
	
	
	ADDZ 0.3
	SHADOW OFF
	MATERIAL stlmat
	CYLIND 0.001, diaUpperTrunk/2
else											! Fan Palm
	CONE ZZYZX, diaLowerTrunk/2, diaUpperTrunk/2, 90, 90
	ROTZ -15

	ADDZ ZZYZX-0.35
	! === extra bark ===============================================================
	if iDetlevel3D = DETLEVEL3D_DETAILED then
		ADDZ -1.15
		nBarkLevels = 12
		hBarkLevel = 1.15 / nBarkLevels
		for zz = 0 to nBarkLevels-1
			for z=1 to 8
				tempVal = ((diaLowerTrunk-diaUpperTrunk)/ZZYZX*(1.35-hBarkLevel*zz)+diaUpperTrunk)/2
				addx tempVal
				mulx 2*tempVal/0.43
				muly 2*tempVal/0.43
				CALL "DPLS" parameters all
				del 3
				rotz 360/8
			next z
			del 8
	
			ROTZ 22.5
			ADDZ hBarkLevel
		next zz
		del 1
	endif
	
	! === leaves ===============================================================
	nLeafLayers = 5
	dim nLeavesArray[]
		nLeavesArray[1] = 4
		nLeavesArray[2] = 8
		nLeavesArray[3] = 8
		nLeavesArray[4] = 2
		nLeavesArray[5] = 2
	dim addxLeavesArray[]
		addxLeavesArray[1] = 0.6139
		addxLeavesArray[2] = 0.5946
		addxLeavesArray[3] = 0.4614
		addxLeavesArray[4] = 0.2453
		addxLeavesArray[5] = 0.0768
	dim addzLeavesArray[]
		addzLeavesArray[1] = 0.1054
		addzLeavesArray[2] = 0.3742
		addzLeavesArray[3] = 0.6082
		addzLeavesArray[4] = 0.7658
		addzLeavesArray[5] = 0.7939
	dim rotzLeavesArray[]
		rotzLeavesArray[1] = 33.3889
		rotzLeavesArray[2] = 8.4353
		rotzLeavesArray[3] = -16.5643
		rotzLeavesArray[4] = -41.5638
		rotzLeavesArray[5] = -63.7919
	
	ADDZ 0.08
	for i = 1 to nLeafLayers
		FOR Z=1 TO nLeavesArray[i]
			MATERIAL stlmat
			ADDX (diaLowerTrunk/ZZYZX/5-diaUpperTrunk/ZZYZX/5+diaUpperTrunk)/3**0.5
			MULX (A-(diaLowerTrunk/ZZYZX/5-diaUpperTrunk/ZZYZX/5+diaUpperTrunk)*2/3**0.5)/3.6258
			MULY (a-(diaLowerTrunk/ZZYZX/5-diaUpperTrunk/ZZYZX/5+diaUpperTrunk)*2/3**0.5)/3.6258
			if i = 1 then call "FPS-25" parameters all
			if i = 2 then call "FPS0" parameters all
			if i = 3 then call "FPS+25" parameters all
			if i = 4 then call "FPS+50" parameters all
			if i = 5 then call "FPS+70" parameters all
	
			MATERIAL gs_leave_mat
			ADDX addxLeavesArray[i]
			ADDZ addzLeavesArray[i]
			ROTY rotzLeavesArray[i]

			call "FPL100" parameters all
	
			DEL 6
			ROTZ 360/nLeavesArray[i]
		NEXT Z
		if iDetlevel3D <> DETLEVEL3D_DETAILED then
			SHADOW OFF
		endif
		if i < 4 then
			ROTZ 15
		else
			ROTZ 90
		endif
		if i < 3 then
			ADDZ 0.1
		endif
	next i
	
	del 1
	
	
	ADDZ 0.07
	SHADOW OFF
	MATERIAL stlmat
endif


!================================================================================
end ! end ! end !end ! end ! end ! end ! end ! end end ! end ! end ! end ! end !
!================================================================================


"call_leafMacro":
if iDetlevel3D = DETLEVEL3D_DETAILED then	! Detailed
	call "CPL20" parameters all
else
	if not(sel) then
		call "CPL100C" parameters all
	else
		call "CPL100S" parameters all
	endif
endif
return

