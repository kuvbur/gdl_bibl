
if iViewType2D = SIDE_VIEW | iViewType2D = SIDE_VIEW_SYMB | not(bShowasBitmap) then end

if iViewType3D = SIDE_VIEW then
	iPicName = 20	!coconut palm
	if iTreeType = TYPE_COCONUT then
		if iPic3D = PIC_COLOR 	then iPicName = 20	!coconut palm
		if iPic3D = PIC_COLOR2 then iPicName = 24	!coconut palm2
		if iPic3D = PIC_FOGGY 	then iPicName = 15	!coconut palm gray 
		if iPic3D = PIC_FOGGY2 	then iPicName = 22	!coconut palm2 gray
	endif
	
	if iTreeType = TYPE_DATE then
		if iPic3D = PIC_COLOR 	then iPicName = 8	!canary date palm1
		if iPic3D = PIC_COLOR2 then iPicName = 12	!canary date palm2
		if iPic3D = PIC_FOGGY 	then iPicName = 6	!canary date palm1 gray 
		if iPic3D = PIC_FOGGY2 	then iPicName = 9	!canary date palm2  gray
	endif
	
	if iTreeType = TYPE_COMMON then
		if iPic3D = PIC_COLOR 	then iPicName = 32	!common palm1
		if iPic3D = PIC_COLOR2 then iPicName = 36	!common palm2
		if iPic3D = PIC_FOGGY 	then iPicName = 30	!common palm1 gray 
		if iPic3D = PIC_FOGGY2 	then iPicName = 34	!common palm2 gray
	endif

	if iTreeType = TYPE_SABAL then
		if iPic3D = PIC_COLOR 	then iPicName = 56	!sabal palmetto1
		if iPic3D = PIC_COLOR2 then iPicName = 60	!sabal palmetto2
		if iPic3D = PIC_FOGGY 	then iPicName = 54	!sabal palmetto1 gray 
		if iPic3D = PIC_FOGGY2 	then iPicName = 58	!sabal palmetto2 gray
	endif

	if iTreeType = TYPE_PONYTAIL then
		if iPic3D = PIC_COLOR 	then iPicName = 44	!ponytail palm1
		if iPic3D = PIC_COLOR2 then iPicName = 48	!ponytail palm2
		if iPic3D = PIC_FOGGY 	then iPicName = 42	!ponytail palm1 gray 
		if iPic3D = PIC_FOGGY2 	then iPicName = 46	!ponytail palm2 gray
	endif
else
	iPicName = 16	!coconut palm top
	if iTreeType = TYPE_COCONUT then
		if iPic3D = PIC_COLOR 	then iPicName = 16	!coconut palm top
		if iPic3D = PIC_FOGGY 	then iPicName = 13	!coconut palm gray top 
	endif
	
	if iTreeType = TYPE_DATE then
		if iPic3D = PIC_COLOR 	then iPicName = 3	!canary date palm top
		if iPic3D = PIC_FOGGY 	then iPicName = 1	!canary date palm gray top 
	endif
	
	if iTreeType = TYPE_COMMON then
		if iPic3D = PIC_COLOR 	then iPicName = 27	!common palm top
		if iPic3D = PIC_FOGGY 	then iPicName = 25	!common palm gray top 
	endif

	if iTreeType = TYPE_SABAL then
		if iPic3D = PIC_COLOR 	then iPicName = 51	!sabal palmetto top
		if iPic3D = PIC_FOGGY 	then iPicName = 49	!sabal palmetto gray top
	endif

	if iTreeType = TYPE_PONYTAIL then
		if iPic3D = PIC_COLOR 	then iPicName = 39	!ponytail palm top
		if iPic3D = PIC_FOGGY 	then iPicName = 37	!ponytail palm gray top 
	endif
endif

if bCustomPic3D then _PicName = gs_picture_name

if gs_shadow then
	shadow on
else
	shadow off
endif

pen gs_cont_pen

nTrans = 0
if iViewType3D = TOP_VIEW then
	addz elevation
	nTrans = nTrans + 1

	unID = 1

	HOTSPOT 0, 0, 0, unID 							: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT -1, 0, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT temp_AA, 0, 0, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT 1, 0, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT -temp_AA, 0, 0, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT 0, -1, 0, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT 0, temp_BB, 0, unID, temp_BB, 2, B		: unID=unID+1

	HOTSPOT 0, 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT 0, 1, 0, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT 0, -temp_BB, 0, unID, temp_BB, 2, B		: unID=unID+1

	_PicSize = B

	add -a/2, -b/2, 0
	nTrans = nTrans + 1

	rotx 270
	nTrans = nTrans + 1			
else
	dx = GLOB_EYEPOS_X - GLOB_TARGPOS_X
	dy = GLOB_EYEPOS_Y - GLOB_TARGPOS_Y
	
	rotx 90
	nTrans = nTrans + 1
	
	if SYMB_MIRRORED then
		roty SYMB_ROTANGLE
	else
		roty -SYMB_ROTANGLE
	endif
	nTrans = nTrans + 1
	
	if abs(GLOB_EYEPOS_X - GLOB_TARGPOS_X)<EPS then
		if GLOB_EYEPOS_Y>GLOB_TARGPOS_Y then
			roty 180
			nTrans = nTrans + 1
		endif
	else
	
		alf = atn(abs(dy/dx))
		
		if SYMB_MIRRORED then
			if dx<0 and dy>0 then 
				roty -(180-alf)
				nTrans = nTrans + 1
			endif
			if dx<0 and dy<0 then 
				roty -(180+alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy<0 then 
				roty -(-alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy>0 then 
				roty -(alf)
				nTrans = nTrans + 1
			endif
		else
			if dx<0 and dy>0 then 
				roty (180-alf)
				nTrans = nTrans + 1
			endif
			if dx<0 and dy<0 then 
				roty (180+alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy<0 then 
				roty (-alf)
				nTrans = nTrans + 1
			endif
			if dx>0 and dy>0 then 
				roty (alf)
				nTrans = nTrans + 1
			endif
		endif
		
		roty 90
		nTrans = nTrans + 1
	endif

	rotx 270
	nTrans = nTrans + 1			

	unID = 1

	HOTSPOT 0, 0, 0, unID								: unID=unID+1

	HOTSPOT 0, 0, ZZYZX/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT -1, 0, ZZYZX/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT temp_AA, 0, ZZYZX/2, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT 0, 0, ZZYZX/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT 1, 0, ZZYZX/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT -temp_AA, 0, ZZYZX/2, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT 0, 0, 0, unID, ZZYZX, 1+128					: unID=unID+1
	HOTSPOT 0, 0, -1, unID, ZZYZX, 3+128				: unID=unID+1
	HOTSPOT 0, 0, ZZYZX, unID, ZZYZX, 2					: unID=unID+1

	addx -a/2
	nTrans = nTrans + 1

	_PicSize = ZZYZX
endif

if bCustomPic3D then
	_Picname = gs_picture_name
	if gs_picture_name = "" then
		if iViewType3D = TOP_VIEW then
			iPicName = 16							! coconut palm top
		else
			iPicName = 20							! coconut palm
		endif
		_PicName = iPicName
	endif
else
	_Picname = iPicName
endif

valueOpacity = (1 - iOpacity/100)
define empty_fill "emptyFill"

DIM	tmp_C4D_ChCol_Color[3]
	tmp_C4D_ChCol_Color[1] = 0.878401
	tmp_C4D_ChCol_Color[2] = 0.513481
	tmp_C4D_ChCol_Color[3] = 0.412253

DIM	tmp_C4D_ChSpe_Color[3]
	tmp_C4D_ChSpe_Color[1] = 0.878401
	tmp_C4D_ChSpe_Color[2] = 0.513481
	tmp_C4D_ChSpe_Color[3] = 0.412253

DEFINE TEXTURE "TextureTree3D" _PicName,   A,   _PicSize,    1,   0
DEFINE MATERIAL "TreeMat3D" 20,
	0, 0, 0,

	0.58, 0.85, 0.04, valueOpacity,
	22,
	0,
	0.878401, 0.513481, 0.412253,

	0, 0, 0,
	0,

	ind(fill, "emptyFill"), 1,
	ind(texture, "TextureTree3D"),
	ADDITIONAL_DATA	C4D_ChSize_HorSize			= A,
					C4D_ChSize_VerSize			= _PicSize,
		
					C4D_ChCol_State				= 1,
					C4D_ChCol_Color				= tmp_C4D_ChCol_Color,	!surface RGB
					C4D_ChCol_Texture			= "1",
					C4D_ShImage_File_1			= _PicName,
		
					C4D_ChDif_State				= 1,
					C4D_ChDif_Brightness		= 0.85,					!diffuseReflection
					C4D_ChDif_Specular			= 0,
		
					C4D_ChTra_State				= (valueOpacity > EPS),
					C4D_ChTra_Brightness		= (valueOpacity ^ (1 / 2.94)),
					C4D_ChTra_Refraction		= 1.0,					!use 1.0 to avoid displacement (1.52 for glass)
		
					C4D_ChRef_State				= 1,
					C4D_ChRef_Brightness		= 0.0244,				!ACShinessConverter (shining) * specular
		
					C4D_ChSpe_State				= 1,
					C4D_ChSpe_Color				= tmp_C4D_ChSpe_Color,
					C4D_ChSpe_Height			= 0.04,					!specular
					C4D_ChSpe_Width				= 0.4025,				! (1 - (shining/100) ^ (1/2.94))
		
					C4D_ChAlp_State				= 1,
					C4D_ChAlp_Texture			= "2",
					C4D_ShImage_File_2			= _PicName,
					C4D_ShImage_AlphaOnly_2		= 1.0

material "TreeMat3D"


vert	EPS, 0, EPS					! 1
vert	A-EPS, 0, EPS				! 2
vert	A-EPS, 0, _PicSize-2*EPS	! 3
vert	EPS, 0, _PicSize-2*EPS		! 4

edge 1, 2, 0, 0, 0
edge 2, 3, 0, 0, 0
edge 3, 4, 0, 0, 0
edge 4, 1, 0, 0, 0

pgon{2} 4, 0, 0, 6,
	-EPS, 0, 0,
	1, 0, 0,
	-EPS, 0, 1,
	-EPS, 1, 0,
	1, 2, 3, 4

body 1

del nTrans
end
