
! Hotspots
if iViewType2D = TOP_VIEW | iViewType2D = TOP_VIEW_SYMB then
	unID = 1

	HOTSPOT2 0,0,unID 							: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, -1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, temp_BB, unID, temp_BB, 2, B	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, 1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, -temp_BB, unID, temp_BB, 2, B	: unID=unID+1

else
	unID=1

	HOTSPOT2 0,0,unID 								: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+128					: unID=unID+1
	HOTSPOT2 0, -1, unID, B, 3+128					: unID=unID+1
	HOTSPOT2 0, B, unID, B, 2						: unID=unID+1

endif


if txt_show then
	call "plantName_2D" parameters txt = txt,
			fontType = fontType,
			fontSizeFixed_mm = fontSizeFixed_mm,
			fontSizeFixed_pt = fontSizeFixed_pt,
			txt_pen = txt_pen,
			bBold = bBold,
			bItalic = bItalic,
			bUnderLined = bUnderLined,
			txt_pos_x = txt_pos_x,
			txt_pos_y = txt_pos_y,
			typeTextRotation_m = typeTextRotation_m,
			gs_readable_angle = gs_readable_angle
endif
	
if iViewType2D = TOP_VIEW_SYMB then
	call "treeTopViews" parameters A = A,
		B = B,
		iTopViewSymb = iTopViewSymb,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type = gs_fill_type,
		gs_fill_pen = gs_fill_pen,
		gs_back_pen = gs_back_pen
else
	if iViewType2D = SIDE_VIEW_SYMB then
		call "PineSideView" parameters A = A, 
			B = B,
			iSideViewSymb = iSideViewSymb,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type = gs_fill_type,
			gs_fill_pen = gs_fill_pen,
			gs_back_pen = gs_back_pen
	else
		if iViewType2D = TOP_VIEW then
			add2 -a/2, -b/2
	
			iPicName = 61		! pine - snow no snow top
			if iTreeType = TYPE_GENERAL then
				if iTopViewPic2D = PIC_SIMPLE then 		iPicName = 61		! pine - snow no snow top
				if iTopViewPic2D = PIC_SNOWY then 		iPicName = 65		! pine - snow top
				if iTopViewPic2D = PIC_SIMP_FOGGY then 	iPicName = 57		! pine - snow no snow gray top
				if iTopViewPic2D = PIC_SNOW_FOGGY then 	iPicName = 55		! pine - snow gray top
			endif
			
			if iTreeType = TYPE_SCOTS then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 43		! scots pine top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 41		! scots pine gray top
			endif
			
			if iTreeType = TYPE_ABIS then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 53		! pine abies top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 31		! pine abies gray top
			endif
			
			if iTreeType = TYPE_BONSAI then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 4		! bonsai top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 2		! bonsai top gray
			endif
		else
			add2 -a/2, 0
			iPicname = 64		! pine - snow no snow
			if iTreeType = TYPE_GENERAL then
				if iSideViewPic2D = PIC_SIMPLE then 	iPicname = 64		! pine - snow no snow
				if iSideViewPic2D = PIC_SNOWY then 		iPicname = 70		! pine - snow w snow
				if iSideViewPic2D = PIC_SIMP_FOGGY then 	iPicname = 60	 	! pine - snow no snow gray
				if iSideViewPic2D = PIC_SNOW_FOGGY then 	iPicname = 68		! pine - snow w snow gray
			endif
			
			if iTreeType = TYPE_SCOTS then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 48		! scots pine1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 52		! scots pine2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 46		! scots pine1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 50		! scots pine2 gray
			endif
			
			if iTreeType = TYPE_ABIS then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 36		! pine abies1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 40		! pine abies2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 54		! pine abies1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 38		! pine abies2 gray
			endif
			
			if iTreeType = TYPE_BONSAI then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 8		! pine - bonsai1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 12		! pine - bonsai2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 6		! pine - bonsai1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 10		! pine - bonsai2 gray
			endif
		endif

		if bCustomPic2D then
			_Picname = gs_picture_name_2D
			if gs_picture_name_2D = "" then
				if iViewType2D = TOP_VIEW then
					iPicName = 61							! pine - snow no snow top
				else
					iPicName = 64							! pine - snow no snow
				endif
				_PicName = iPicName
			endif
		else
			_Picname = iPicName
		endif

		fill selection_fill_type
		add2 A/2, B/2
		if iViewType2D = TOP_VIEW then
			mul2 1, B/A
			poly2_B 2, 2+4+64, 1, 0,
					0, 0, 900,
					A/2, 360, 4000
			del 1
		else
			poly2_B 4, 2+4, 1, 0,
					-A/2, -B/2, 0,
					-A/2, B/2, 0,
					A/2, B/2, 0,
					A/2, -B/2, 0
		endif
		del 1

		picture2{2}  _Picname,a,b,1	

		del 1
	endif
endif

end


