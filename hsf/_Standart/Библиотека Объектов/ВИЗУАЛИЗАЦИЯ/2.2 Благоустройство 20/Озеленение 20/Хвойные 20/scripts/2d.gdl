
! Hotspots
if iViewType2D = TOP_VIEW | iViewType2D = TOP_VIEW_SYMB then
	unID = 1

	HOTSPOT2 0,0,unID 							: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, 0, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, 0, unID, temp_AA, 2, A	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, -1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, temp_BB, unID, temp_BB, 2, B	: unID=unID+1

	HOTSPOT2 0, 0, unID, temp_BB, 1+128, B		: unID=unID+1
	HOTSPOT2 0, 1, unID, temp_BB, 3+128, B		: unID=unID+1
	HOTSPOT2 0, -temp_BB, unID, temp_BB, 2, B	: unID=unID+1

else
	unID=1

	HOTSPOT2 0,0,unID 								: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 -1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A		: unID=unID+1
	HOTSPOT2 1, B/2, unID, temp_AA, 3+128, A		: unID=unID+1
	HOTSPOT2 -temp_AA, B/2, unID, temp_AA, 2, A		: unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+128					: unID=unID+1
	HOTSPOT2 0, -1, unID, B, 3+128					: unID=unID+1
	HOTSPOT2 0, B, unID, B, 2						: unID=unID+1

endif


if txt_show then
	call "plantName_2D" parameters txt = txt,
			fontType = fontType,
			fontSizeFixed_mm = fontSizeFixed_mm,
			fontSizeFixed_pt = fontSizeFixed_pt,
			txt_pen = txt_pen,
			bBold = bBold,
			bItalic = bItalic,
			bUnderLined = bUnderLined,
			txt_pos_x = txt_pos_x,
			txt_pos_y = txt_pos_y,
			typeTextRotation_m = typeTextRotation_m,
			gs_readable_angle = gs_readable_angle
endif

if iViewType2D = TOP_VIEW_SYMB then
	call "treeTopViews" parameters A = A,
		B = B,
		iTopViewSymb = iTopViewSymb,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type = gs_fill_type,
		gs_fill_pen = gs_fill_pen,
		gs_back_pen = gs_back_pen
else
	if iViewType2D = SIDE_VIEW_SYMB then
		call "EvergrSideView" parameters A = A, 
			B = B,
			iSideViewSymb = iSideViewSymb,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type = gs_fill_type,
			gs_fill_pen = gs_fill_pen,
			gs_back_pen = gs_back_pen
	else
		if iViewType2D = TOP_VIEW then
			add2 -a/2, -b/2
			iPicName = 41			! evergreen ash top

			if iTreeType = TYPE_GEN1 then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 41		! evergreen ash top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 37		! evergreen ash gray top
			endif
			
			if iTreeType = TYPE_GEN2 then
				if iTopViewPic2D = PIC_SUMMER then 		iPicname = 57		! evergreen holly top
				if iTopViewPic2D = PIC_AUTUMN then 		iPicname = 49		! evergreen holly berries top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 53		! evergreen holly gray top
			endif
			
			if iTreeType = TYPE_GEN3 then
				if iTopViewPic2D = PIC_SUMMER then 		iPicname = 29		! evergreen - olive no olives top
				if iTopViewPic2D = PIC_AUTUMN then 		iPicname = 33		! evergreen - olive top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 25		! evergreen - olive gray top
			endif
			
			if iTreeType = TYPE_THUJA then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 15		! evergreen thuja top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 13		! evergreen thuja gray top
			endif
			
			if iTreeType = TYPE_JUNIPER then
				if iTopViewPic2D = PIC_COLOR then		iPicname = 1		! juniper summer top
				if iTopViewPic2D = PIC_FOGGY then 		iPicname = 2		! juniper gray top
			endif
		else
			add2 -a/2, 0

			iPicName = 44				! evergreen ash
			if iTreeType = TYPE_GEN1 then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 44	! evergreen ash
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 48	! evergreen ash2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 40	! evergreen ash grey
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 46	! evergreen ash2 gray
			endif
			
			if iTreeType = TYPE_GEN2 then
				if iSideViewPic2D = PIC_SUMMER then 	iPicname = 60	! evergreen holly
				if iSideViewPic2D = PIC_AUTUMN then 	iPicname = 52	! evergreen holly berries
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 56	! evergreen holly gray
			endif
			
			if iTreeType = TYPE_GEN3 then
				if iSideViewPic2D = PIC_SUMMER then 	iPicname = 32	! evergreen - olive no olives
				if iSideViewPic2D = PIC_AUTUMN then 	iPicname = 36	! evergreen - olive
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 28	! evergreen - olive gray
			endif
			
			if iTreeType = TYPE_THUJA then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 20	! evergreen thuja1
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 24	! evergreen thuja2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 17	! evergreen thuja1 gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 21	! evergreen thuja2 gray
			endif
			
			if iTreeType = TYPE_JUNIPER then
				if iSideViewPic2D = PIC_COLOR then		iPicname = 5	! juniper summer
				if iSideViewPic2D = PIC_COLOR2 then		iPicname = 9	! juniper2
				if iSideViewPic2D = PIC_FOGGY then 		iPicname = 6	! juniper gray
				if iSideViewPic2D = PIC_FOGGY2 then 		iPicname = 10	! juniper2 gray
			endif
		endif

		if bCustomPic2D then
			_Picname = gs_picture_name_2D
			if gs_picture_name_2D = "" then
				if iViewType2D = TOP_VIEW then
					iPicName = 41							! evergreen ash top
				else
					iPicName = 44							! evergreen ash
				endif
				_PicName = iPicName
			endif
		else
			_Picname = iPicName
		endif

		fill selection_fill_type
		add2 A/2, B/2
		if iViewType2D = TOP_VIEW then
			mul2 1, B/A
			poly2_B 2, 2+4+64, 1, 0,
					0, 0, 900,
					A/2, 360, 4000
			del 1
		else
			poly2_B 4, 2+4, 1, 0,
					-A/2, -B/2, 0,
					-A/2, B/2, 0,
					A/2, B/2, 0,
					A/2, -B/2, 0
		endif
		del 1

		picture2{2}  _Picname,a,b,1	

		del 1

	endif
endif

end
