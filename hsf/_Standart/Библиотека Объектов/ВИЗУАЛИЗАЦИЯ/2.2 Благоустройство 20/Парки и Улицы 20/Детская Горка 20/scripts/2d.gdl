
pen gs_cont_pen
fill gs_fill_type

! -----------------------------------------------------------------------------
! Hotspots
! -----------------------------------------------------------------------------
unID = 1
unID_2 = 50


hotspot2 -1, 0, unID_2, A, 3	: unID_2 = unID_2 + 1
hotspot2 0, 0, unID, A, 1+256	: unID = unID + 1
hotspot2 A, 0, unID, A, 2		: unID = unID + 1

hotspot2 -1, B, unID_2, A, 3	: unID_2 = unID_2 + 1
hotspot2 A, B, unID, A, 2		: unID = unID + 1
hotspot2 0, B, unID, A, 1+256	: unID = unID + 1

hotspot2 A / 2, B / 2, unID : unID = unID + 1

! -----------------------------------------------------------------------------
! Geometry
! -----------------------------------------------------------------------------

gosub iSlideType

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================
! -----------------------------------------------------------------------------
1: ! Plastic
! -----------------------------------------------------------------------------
	
	add2 0, sideOffset
	
	add2 A / 2, ladderDistance - platformDepth / 2 + beamThk / 2
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-slideSideWidth / 2, -platformDepth / 2, 1,
		-slideSideWidth / 2, platformDepth / 2, 1,
		slideSideWidth / 2, platformDepth / 2, 1,
		slideSideWidth / 2, -platformDepth / 2, 1,
		-slideSideWidth / 2, -platformDepth / 2, -1

	del 1

	add2 A / 2, ladderDistance + beamThk / 2
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-slideSideWidth / 2, 0, 1,
		slideSideWidth / 2, 0, 1,
		slideSideWidth / 2, B - ladderDistance - beamThk / 2 - sideOffset, 1,
		-slideSideWidth / 2, B - ladderDistance - beamThk / 2 - sideOffset, 1,
		-slideSideWidth / 2, 0, -1

	line2 -slideBottomWidth / 2, 0, -slideBottomWidth / 2, B - ladderDistance - beamThk / 2 - sideOffset
	line2 slideBottomWidth / 2, 0, slideBottomWidth / 2, B - ladderDistance - beamThk / 2 - sideOffset

	del 1

	add2 A / 2, -sideOffset
	for i = 1 to 2

		add2 ladderWidth / 2, 0
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			plateThk, 0, 1,
			0, 0, 1,
			0, ladderDistance + beamThk * 2.5 + sideOffset, 1,
			plateThk, ladderDistance + beamThk * 2.5 + sideOffset, 1,
			plateThk, 0, -1

		add2 plateThk, sideOffset + ladderDistance - platformDepth / 2
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			plateThk, -sidePanelRad, 1,
			0, -sidePanelRad, 1,
			0, sidePanelRad, 1,
			plateThk, sidePanelRad, 1,
			plateThk, -sidePanelRad, -1
			
		del 2
		mul2 -1, 1

	next i
	del 3

	add2 A / 2, 0
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-ladderWidth / 2, 0, 1,
		ladderWidth / 2, 0, 1,
		ladderWidth / 2, ladderDistance - platformDepth + beamThk / 2, 1,
		-ladderWidth / 2, ladderDistance - platformDepth + beamThk / 2, 1,
		-ladderWidth / 2, 0, -1

	for i = 1 to nRisers
		
		add2 0, tan(ladderAngle) * (i * riserHeight)
		line2 -ladderWidth * 0.4, 0, ladderWidth * 0.4, 0
		del 1

	next i

	del 1


	del 1

return

! -----------------------------------------------------------------------------
2: ! Metal
! -----------------------------------------------------------------------------

	add2 A / 2, ladderDistance - platformDepth / 2 + beamThk / 2
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-slideSideWidth / 2, -platformDepth / 2, 1,
		-slideSideWidth / 2, platformDepth / 2, 1,
		slideSideWidth / 2, platformDepth / 2, 1,
		slideSideWidth / 2, -platformDepth / 2, 1,
		-slideSideWidth / 2, -platformDepth / 2, -1

	del 1

	add2 A / 2, ladderDistance + beamThk / 2
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-slideSideWidth / 2, 0, 1,
		slideSideWidth / 2, 0, 1,
		slideSideWidth / 2, B - ladderDistance - beamThk / 2, 1,
		-slideSideWidth / 2, B - ladderDistance - beamThk / 2, 1,
		-slideSideWidth / 2, 0, -1

	line2 -slideBottomWidth / 2, 0, -slideBottomWidth / 2, B - ladderDistance - beamThk / 2
	line2 slideBottomWidth / 2, 0, slideBottomWidth / 2, B - ladderDistance - beamThk / 2

	del 1

	add2 A / 2, 0
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-ladderWidth / 2, 0, 1,
		ladderWidth / 2, 0, 1,
		ladderWidth / 2, ladderDistance - platformDepth + beamThk / 2, 1,
		-ladderWidth / 2, ladderDistance - platformDepth + beamThk / 2, 1,
		-ladderWidth / 2, 0, -1

		for i = 1 to nRisers
		
			add2 0, tan(ladderAngle) * (i * riserHeight)
			line2 -ladderWidth * 0.4, 0, ladderWidth * 0.4, 0
			del 1

		next i

	del 1

	add2 A / 2, ladderDistance + beamThk / 2
	for i = 1 to 2

		add2 slideSideWidth / 2, 0
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			plateThk, 0, 1,
			plateThk, -sidePlateWidth, 1,
			0, -sidePlateWidth, 1,
			0, 0, -1

		add2 plateThk, 0
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			beamThk, 0, 1,
			beamThk, -beamThk, 1,
			0, -beamThk, 1,
			0, 0, -1
		
		del 2
		mul2 -1, 1

	next i
	del 3

return

