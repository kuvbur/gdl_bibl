
resol gs_resol

! -----------------------------------------------------------------------------
! Hotspots
! -----------------------------------------------------------------------------
unID = 1
unID_2 = 50

hotspot -1, 0, 0, unID_2, A, 3	: unID_2 = unID_2 + 1
hotspot 0, 0, 0, unID, A, 1+256	: unID = unID + 1
hotspot A, 0, 0, unID, A, 2		: unID = unID + 1

hotspot -1, B, 0, unID_2, A, 3	: unID_2 = unID_2 + 1
hotspot A, B, 0, unID, A, 2		: unID = unID + 1
hotspot 0, B, 0, unID, A, 1+256	: unID = unID + 1

hotspot -1, 0, ZZYZX, unID_2, A, 3	: unID_2 = unID_2 + 1
hotspot 0, 0, ZZYZX, unID, A, 1+256	: unID = unID + 1
hotspot A, 0, ZZYZX, unID, A, 2		: unID = unID + 1

hotspot -1, B, ZZYZX, unID_2, A, 3	: unID_2 = unID_2 + 1
hotspot A, B, ZZYZX, unID, A, 2		: unID = unID + 1
hotspot 0, B, ZZYZX, unID, A, 1+256	: unID = unID + 1


! -----------------------------------------------------------------------------
! Geometry
! -----------------------------------------------------------------------------
gosub iSlideType

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================

! -----------------------------------------------------------------------------
1: ! Plastic
! -----------------------------------------------------------------------------

	addy sideOffset

	add A / 2, -plateThk, 0
	rotx -90 - ladderAngle

	for i = 1 to nRisers

		if i = 1 then
			edgeStatStart = 15
		else
			edgeStatStart = 10
		endif

		if i = nRisers then
			edgeStatEnd = 15
		else
			edgeStatEnd = 10
		endif
		
		material matPlasticLadder
		addy -ladderPanelHeight * (i)
		prism_ 11, plateThk,
			-ladderWidth / 2, 0, edgeStatStart,
			ladderWidth / 2, 0, 15,
			ladderWidth / 2, -ladderPanelHeight, edgeStatEnd,
			-ladderWidth / 2, -ladderPanelHeight, 15,
			-ladderWidth / 2, 0, -1,

			-ladderWidth / 3, -ladderPanelHeight * 0.5, 15,
			ladderWidth / 3, -ladderPanelHeight * 0.5, 15,
			ladderWidth / 3, -ladderPanelHeight * 0.3, 79,
			0, -ladderPanelHeight * 0.8, 979,
			-ladderWidth / 3, -ladderPanelHeight * 0.3, 3079,
			-ladderWidth / 3, -ladderPanelHeight * 0.5, -1

		add 0, 0.01, -plateThk / 2
		material matPlatform
		prism_ 7, plateThk * 2,
		   -ladderWidth / 3, -ladderPanelHeight * 0.4, 79,
			0, -ladderPanelHeight * 1.3, 979,
			ladderWidth / 3, -ladderPanelHeight * 0.4, 3079,
			ladderWidth / 3, -ladderPanelHeight * 0.3, 79,
			0, -ladderPanelHeight * 0.8, 979,
			-ladderWidth / 3, -ladderPanelHeight * 0.3, 3079,
			-ladderWidth / 3, -ladderPanelHeight * 0.4, -1

		del 2

	next i
	del 2

	material matFrame
	addx  A / 2
	rotz -90
	rotx 90

	for i = 1 to 2

		addz ladderWidth / 2

		prism_ 7, plateThk,
			sideOffset, 0, 15,
			-(tan(ladderAngle) * slideHeight - sideOffset), slideHeight, 79,
			-ladderDistance + platformDepth / 2, ZZYZX - topHeight / 2, 1079,
			-ladderDistance, slideHeight - topHeight / 3, 1079,
			-sideOffset * nRisers / 2.5, slideHeight - topHeight / 2, 1079,
			-sideOffset * nRisers / 4, 0, 79,
			sideOffset, 0, -1
			
		del 1
		mulz -1
		
	next i
	del 5

	material matFrame
	add A / 2, ladderDistance - platFormDepth / 2, ZZYZX - sidePanelRad
	roty 90
	
	add -0.2, -0.2, 0.2
	cutpolya 2, 1, 0,
		0, 0, 979,
		0.1, 360, 4079
	del 1
		
	for i = 1 to 2

		addz ladderWidth / 2 + plateThk

		prism_ 2, plateThk,
			0, 0, 979,
			sidePanelRad, 360, 4079

		del 1
		mulz -1

	next i
	del 4
	cutend

	material matPlatform
	add A / 2, ladderDistance - platformDepth / 2 + beamThk / 2, slideHeight
	prism_ 5, platformThk,
		-slideSideWidth / 2, -platformDepth / 2, 15,
		-slideSideWidth / 2, platformDepth / 2, 15,
		slideSideWidth / 2, platformDepth / 2, 15,
		slideSideWidth / 2, -platformDepth / 2, 15,
		-slideSideWidth / 2, -platformDepth / 2, -1

	del 1

	material matPlasticLadder
	add A / 2, ladderDistance + beamThk / 2, slideHeight
	rotx 90
	prism_ 9, plateThk,
		-slideSideWidth / 2, 0, 15,
		-slideSideWidth / 2, sidePanelRad * 1.3, 15,
		slideSideWidth / 2, sidePanelRad * 1.3, 15,
		slideSideWidth / 2, 0, 15,
		slideBottomWidth / 2, 0, 15,
		slideBottomWidth / 2, sidePanelRad * 0.7, 79,
		-slideBottomWidth / 2, sidePanelRad * 0.7, 1079,
		-slideBottomWidth / 2, 0, 15,
		-slideSideWidth / 2, 0, 15

	del 2

	material matPlasticSlide
	add A / 2, ladderDistance + beamThk / 2,  slideHeight
	gosub "slidePath"
	tube 14, NSP / 4, 1+2+4+16,
		0, 0, 1,
		slideSideWidth / 2 - slideRoundRad, 0, 1,
		slideSideWidth / 2, slideRoundRad, 1001,
		slideSideWidth / 2, slideSideHeight - slideRoundRad, 1,
		slideBottomWidth / 2, slideSideHeight - slideRoundRad, 1001,
		slideBottomWidth / 2, slideBottomThk + slideRoundRad, 1,
		slideBottomWidth / 2 - slideRoundRad, slideBottomThk, 1001,
		-slideBottomWidth / 2 + slideRoundRad, slideBottomThk, 1,
		-slideBottomWidth / 2, slideBottomThk + slideRoundRad, 1001,
		-slideBottomWidth / 2, slideSideHeight - slideRoundRad, 1,
		-slideSideWidth / 2, slideSideHeight - slideRoundRad, 1001,
		-slideSideWidth / 2, slideRoundRad, 1,
		-slideSideWidth / 2 + slideRoundRad, 0, 1001,
		0, 0, -1,
		get(NSP)

	del 1

	add A / 2, ladderDistance + beamThk / 2 + (slideHeight - slideBottomHeight) / tan(35) + slideBottomLength, slideBottomHeight
	rotx 90
	mulz -1
	hprism_ matPlasticSlide, matPlasticSlide, matPlasticSlide, matPlasticSlide,
			14, 0.05, 0, 0.01, 1,
			0, 0, 78,
			slideSideWidth / 2 - slideRoundRad, 0, 78,
			slideSideWidth / 2, slideRoundRad, 1078,
			slideSideWidth / 2, slideSideHeight - slideRoundRad, 78,
			slideBottomWidth / 2, slideSideHeight - slideRoundRad, 1078,
			slideBottomWidth / 2, slideBottomThk + slideRoundRad, 78,
			slideBottomWidth / 2 - slideRoundRad, slideBottomThk, 1078,
			-slideBottomWidth / 2 + slideRoundRad, slideBottomThk, 78,
			-slideBottomWidth / 2, slideBottomThk + slideRoundRad, 1078,
			-slideBottomWidth / 2, slideSideHeight - slideRoundRad, 78,
			-slideSideWidth / 2, slideSideHeight - slideRoundRad, 1078,
			-slideSideWidth / 2, slideRoundRad, 78,
			-slideSideWidth / 2 + slideRoundRad, 0, 1078,
			0, 0, -1

	del 3

	material matMetal
	add A / 2, ladderDistance + beamThk / 2 + (slideHeight - slideBottomHeight) / tan(35) + slideBottomLength * 0.7, 0
		
	for i = 1 to 2
	
		addx slideBottomWidth / 2
		prism_ 2, slideBottomHeight * 0.7,
			0, 0, 979,
			riserRad, 360, 4079

		addz slideBottomHeight * 0.7
		prism_ 2, slideBottomHeight * 0.3,
			0, 0, 979,
			riserRad * 1.5, 360, 4079

		del 2
		mulx -1
		
	next i

	del 3

	del 1

return

! -----------------------------------------------------------------------------
2: ! Metal
! -----------------------------------------------------------------------------
	material matWood
	addz beamThk / 2
	cutplane{2} 180, 2
	del 1
	addx A / 2
	rotx -ladderAngle
	for i = 1 to 2

		addx -ladderWidth / 2
		prism_ 5, ladderLength,
			0, -beamThk / 2, 15,
			0, beamThk / 2, 15,
			beamThk / 2, beamThk / 2, 15,
			beamThk / 2, -beamThk / 2, 15,
			0, -beamThk / 2, 15

		del 1
		mulx -1

	next i
	del 4
	cutend

	material matMetal
	add A / 2, tan(ladderAngle) * beamThk / 2, 0

	for i = 1 to 2

		addx -ladderWidth / 2 + beamThk / 4
		prism_ 2, beamThk / 2,
			0, 0, 979,
			riserRad * 1.5, 360, 4079
		del 1	
		mulx -1

	next i
	del 3

	material matMetal
	addx beamThk * 1.5 + plateThk
	roty 90
	for i = 1 to nRisers

		add -(i * riserHeight), tan(ladderAngle) * (i * riserHeight), 0
		prism_ 2, riserLength,
			0, 0, 979,
			riserRad, 360, 4079
		del 1

	next i
	del 2

	material matWood
	add A / 2, ladderDistance, 0
	for i = 1 to 2
		
		addx -A / 2

		prism_ 5, ZZYZX,
			0, -beamThk / 2, 15,
			0, beamThk / 2, 15,
			beamThk, beamThk / 2, 15,
			beamThk, -beamThk / 2, 15,
			0, -beamThk / 2, -1

		del 1
		mulx -1

	next i
	del 3

	material matPlastic
	add A / 2, ladderDistance, slideHeight
	roty 90
	for i = 1 to 2

		addz -ladderWidth / 2 - plateThk
		prism_ 6, plateThk,
			0, beamThk / 2, 15,
			0, -sidePlateWidth + plateRoundRad, 79,
			-plateRoundRad * cos(ladderAngle), -sidePlateWidth + plateRoundRad * sin(ladderAngle), 1079,
			-sidePlateHeight + plateRoundRad * cos(ladderAngle), -sidePlateWidth + (sidePlateHeight - plateRoundRad) * tan(ladderAngle), 79,
			-sidePlateHeight, beamThk / 2, 1079,
			0, beamThk / 2, -1

		del 1
		mulz -1

	next i
	del 4

	material matMetal
	add A / 2, ladderDistance + beamThk / 2, slideHeight + sideplateHeight
	rotx -90
	prism_ 8, plateThk,
		-frontPlateWidth / 2, 0, 15,
		-frontPlateWidth / 2, frontPlateHeight, 15,
		-frontPlateWidth / 2 + beamThk, frontPlateHeight, 79,
		0, frontPlateHeight + frontPlateRad, 979,
		frontPlateWidth / 2 - beamThk, frontPlateHeight, 3079,
		frontPlateWidth / 2, frontPlateHeight, 15,
		frontPlateWidth / 2, 0, 15,
		-frontPlateWidth / 2, 0, -1

	del 2

	material matPlatform
	add A / 2, ladderDistance - platformDepth / 2 + beamThk / 2, slideHeight
	prism_ 9, platformThk,
		-platformWidth / 2, -platformDepth / 2, 15,
		-platformWidth / 2, 0, 15,
		-slideSideWidth / 2, 0, 15,
		-slideSideWidth / 2, platformDepth / 2, 15,
		slideSideWidth / 2, platformDepth / 2, 15,
		slideSideWidth / 2, 0, 15,
		platformWidth / 2, 0, 15,
		platformWidth / 2, -platformDepth / 2, 15,
		-platformWidth / 2, -platformDepth / 2, -1

	del 1

	material matMetalSlide
	add A / 2, ladderDistance + beamThk / 2,  slideHeight
	gosub "slidePath"
	tube 9, NSP / 4, 1+2+4+16,
		slideSideWidth / 2, 0, 0,
		slideSideWidth / 2, slideSideHeight - slideRoundRad, 1,
		slideBottomWidth / 2, slideSideHeight - slideRoundRad, 1001,
		slideBottomWidth / 2, slideBottomThk, 0,
		-slideBottomWidth / 2, slideBottomThk, 0,
		-slideBottomWidth / 2, slideSideHeight - slideRoundRad, 1,
		-slideSideWidth / 2, slideSideHeight - slideRoundRad, 1001,
		-slideSideWidth / 2, 0, 0,
		slideSideWidth / 2, 0, -1,
		get(NSP)

	del 1

	add A / 2, ladderDistance + beamThk / 2 + (slideHeight - slideBottomHeight) / tan(35) + slideBottomLength, slideBottomHeight
	rotx 90
	mulz -1
	hprism_ matMetalSlide, matMetalSlide, matMetalSlide, matMetalSlide,
			15, 0.05, 0, 0.01, 1,
			slideSideWidth / 2, -slideBottomHeight, 79,
			slideSideWidth / 2, 0, 78,
			slideSideWidth / 2, slideSideHeight - slideRoundRad, 78,
			slideBottomWidth / 2, slideSideHeight - slideRoundRad, 1078,
			slideBottomWidth / 2, slideBottomThk, 14,
			-slideBottomWidth / 2, slideBottomThk, 14,
			-slideBottomWidth / 2, slideSideHeight - slideRoundRad, 78,
			-slideSideWidth / 2, slideSideHeight - slideRoundRad, 1078,
			-slideSideWidth / 2, 0, 79,
			-slideSideWidth / 2, -slideBottomHeight, 78,
			-slideBottomWidth / 2, -slideBottomHeight, 78,
			-slideBottomWidth / 2, 0, 78,
			slideBottomWidth / 2, 0, 78,
			slideBottomWidth / 2, -slideBottomHeight, 78,
			slideSideWidth / 2, slideBottomHeight, -1

	material matPlatform
	prism_ 5, 0.03,
		-slideBottomWidth / 2, -slideBottomHeight, 15,
		slideBottomWidth / 2, -slideBottomHeight, 15,
		slideBottomWidth / 2, 0, 15,
		-slideBottomWidth / 2, 0, 15,
		-slideBottomWidth / 2, -slideBottomHeight, -1

	del 3

return

! -----------------------------------------------------------------------------
"slidePath":
! -----------------------------------------------------------------------------

	put	0, -1, 0, 0,
		0, 0, 0, 0,
		0, slideTopLength, 0, 0

	alpha = 180
	delta = 35 / gs_resol * 2
	for j = 1 to gs_resol / 2 + 2

		put 0, slideTopLength + slideBendRad1 * sin(alpha), -slideBendRad1 - slideBendRad1 * cos(alpha), 0
		alpha = alpha - delta

	next j

	alpha = -55
	delta = 35 / gs_resol * 2 
	for j = 1 to gs_resol / 2

		put	0, (slideHeight - slideBottomHeight) / tan(35) - slideBendRad2 * cos(alpha), -(slideHeight - slideBottomHeight) + slideBendRad2  + slideBendRad2 * sin(alpha), 0
		alpha = alpha - delta

	next j

	put	0, (slideHeight - slideBottomHeight) / tan(35) + slideBottomLength, -(slideHeight - slideBottomHeight), 0,
		0, 10, -(slideHeight - slideBottomHeight), 0


return


