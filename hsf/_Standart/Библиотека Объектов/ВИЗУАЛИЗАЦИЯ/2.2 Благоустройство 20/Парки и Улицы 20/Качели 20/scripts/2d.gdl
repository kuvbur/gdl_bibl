
pen gs_cont_pen
fill gs_fill_type

! -----------------------------------------------------------------------------
! Hotspots
! -----------------------------------------------------------------------------
unID = 1

hotspot2 0, -B / 2, unID : unID = unID + 1
hotspot2 A, -B / 2, unID : unID = unID + 1
hotspot2 A, B / 2, unID : unID = unID + 1
hotspot2 0, B / 2, unID : unID = unID + 1

! -----------------------------------------------------------------------------
! Geometry
! -----------------------------------------------------------------------------

for k = 1 to nSeats

	add2 aSeatPositions[k], 0
	if bBabySeats[k] then
		gosub "drawBabySeat"
	else
		gosub "drawSeat"
	endif
	del 1

next k

transf2 = 0
bDoublePlate = 0
add2 legOffset, 0

for k = 1 to nLegs
	
	transf1 = 0

	if k = 1 then
		gosub "drawRotLeg"
	else 
		if k = nLegs then
			add2 beamThk, 0
			mul2 -1, 1
			transf1 = transf1 + 2
			gosub "drawRotLeg"
		else
			add2 beamThk, 0
			transf2 = transf2 + 1
			gosub "drawLegs"
		endif
	endif

	del transf1

	if k < nLegs then
		crossBeamLength = aCrossBeamSegments[k]
		gosub "drawCrossBeam"
		add2 aCrossBeamSegments[k], 0
		transf2 = transf2 + 1
	endif
next k

del transf2 + 1

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================

! -----------------------------------------------------------------------------
"drawLegs": ! Legs with plate
! -----------------------------------------------------------------------------
	for i = 1 to 2

		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-beamThk / 2, 0, 1,
			-beamThk / 2, B / 2, 1,
			beamThk / 2, B / 2, 1,
			beamThk / 2, 0, 1,
			-beamThk / 2, 0, 1

		mul2 1, -1

	next i

	del 2
return

! -----------------------------------------------------------------------------
"drawRotLeg": ! Legs with plate
! -----------------------------------------------------------------------------
	for i = 1 to 2

		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-beamThk / 2, 0, 1,
			-beamThk / 2 - legOffset, B / 2, 1,
			beamThk / 2 - legOffset, B / 2, 1,
			beamThk / 2, 0, 1,
			-beamThk / 2, 0, 1

		mul2 1, -1

	next i

	del 2
return


! -----------------------------------------------------------------------------
"drawCrossBeam": ! Cross Beam
! -----------------------------------------------------------------------------
	add2 beamThk / 2, 0

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, -crossBeamRad, 1,
		0, crossBeamRad, 1,
		crossBeamLength, crossBeamRad, 1,
		crossBeamLength, -crossBeamRad, 1,
		0, -crossBeamRad, -1

	del 1
return

! -----------------------------------------------------------------------------
"drawSeat": ! Simple seat
! -----------------------------------------------------------------------------

	poly2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-seatWidth / 2 + seatRad, -seatDepth / 2, 1,
		-seatWidth / 2 + seatRad, 0, 901,
		seatRad, -180, 4001,
		seatWidth / 2 - seatRad, seatDepth / 2, 1,
		 seatWidth / 2 - seatRad, 0, 901,
		seatRad, -180, 4001,
		-seatWidth / 2 + seatRad, -seatDepth / 2, -1

return

! -----------------------------------------------------------------------------
"drawBabySeat": ! Baby seat
! -----------------------------------------------------------------------------
	poly2_b 13, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-babySeatWidth / 2 + babySeatRad, -babySeatDepth / 2, 1,
		-babySeatWidth / 2 + babySeatRad, -babySeatDepth / 2 + babySeatRad, 901,
		babySeatRad, -90, 4001,
		-babySeatWidth / 2, babySeatDepth / 2 - babySeatRad, 1,
		-babySeatWidth / 2 + babySeatRad, babySeatDepth / 2 - babySeatRad, 901,
		babySeatRad, -90, 4001,
		babySeatWidth / 2 - babySeatRad, babySeatDepth / 2, 1,
		babySeatWidth / 2 - babySeatRad, babySeatDepth / 2 - babySeatRad, 901,
		babySeatRad, -90, 4001,
		babySeatWidth / 2, -babySeatDepth / 2 + babySeatRad, 01,
		babySeatWidth / 2 - babySeatRad, -babySeatDepth / 2 + babySeatRad, 901,
		babySeatRad, -90, 4001,
		-babySeatWidth / 2 + babySeatRad, -babySeatDepth / 2, -1
		
return

