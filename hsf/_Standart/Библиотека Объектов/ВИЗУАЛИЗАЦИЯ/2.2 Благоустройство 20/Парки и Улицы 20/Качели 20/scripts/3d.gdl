

resol gs_resol

! -----------------------------------------------------------------------------
! Hotspots
! -----------------------------------------------------------------------------
unID = 1

hotspot 0, -B / 2, 0, unID : unID = unID + 1
hotspot A, -B / 2, 0, unID : unID = unID + 1
hotspot A, B / 2, 0, unID : unID = unID + 1
hotspot 0, B / 2, 0, unID : unID = unID + 1

hotspot 0, -B / 2, ZZYZX, unID : unID = unID + 1
hotspot A, -B / 2, ZZYZX, unID : unID = unID + 1
hotspot A, B / 2, ZZYZX, unID : unID = unID + 1
hotspot 0, B / 2, ZZYZX, unID : unID = unID + 1

! -----------------------------------------------------------------------------
! Geometry
! -----------------------------------------------------------------------------

cutplane{2} 180, 4
transf2 = 0
bDoublePlate = 0
add legOffset, 0, ZZYZX - crossBeamoffset

for k = 1 to nLegs
	
	transf1 = 0

	if k = 1 then
		roty legTilt
		transf1 = transf1 + 1
		bDoublePlate = 0
	else 
		if k = nLegs then
			addx beamThk
			roty -legTilt
			mulx -1
			transf1 = transf1 + 3
			bDoublePlate = 0
		else
			addx beamThk
			transf2 = transf2 + 1
			bDoublePlate = 1
		endif
	endif

	gosub "drawLegs"
	del transf1

	if k < nLegs then
		roty 90
		addz beamThk / 2
		crossBeamLength = aCrossBeamSegments[k]
		gosub "drawCrossBeam"
		del 2
		addx aCrossBeamSegments[k]
		transf2 = transf2 + 1
	endif

next k

del transf2 + 1
cutend

for k = 1 to nSeats

	add aSeatPositions[k], 0, seatHeight
	if bBabySeats[k] then
		gosub "drawBabySeat"
	else
		gosub "drawSeat"
	endif
	del 1

next k

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================
! -----------------------------------------------------------------------------
"drawLegs": ! Legs with plate
! -----------------------------------------------------------------------------

	addz crossBeamOffset

	roty 90
	rotz 45
	cutpolya 5, 1, 0,
		-beamThk / 2, -beamThk / 2, 15,
		beamThk / 2, -beamThk / 2, 15,
		beamThk / 2, beamThk / 2, 15,
		-beamThk / 2, beamThk / 2, 15,
		-beamThk / 2, -beamThk / 2, -1

	del 2

	for i = 1 to 2

		if i = 1 then
			cutplane{2} 90, 4
			rotx -180 + legAngle
		else
			cutplane{2} -90, 4
			rotx 180 - legAngle
		endif

		material matWood
		prism_ 5, beamLength,
			-beamThk / 2, -beamThk / 2, 15,
			beamThk / 2, -beamThk / 2, 15,
			beamThk / 2, beamThk / 2, 15,
			-beamThk / 2, beamThk / 2, 15,
			-beamThk / 2, -beamThk / 2, -1

		addz beamLength
		material matMetal
		prism_ 13, spikeLength,
			-spikeThk / 2, spikeGroove, 15,
			-spikeGroove, spikeGroove, 15,
			-spikeGroove, spikeThk / 2, 15,
			spikeGroove, spikeThk / 2, 15,
			spikeGroove, spikeGroove, 15,
			spikeThk / 2, spikeGroove, 15,
			spikeThk / 2, -spikeGroove, 15,
			spikeGroove, -spikeGroove, 15,
			spikeGroove, -spikeThk / 2, 15,
			-spikeGroove, -spikeThk / 2, 15,
			-spikeGroove, -spikeGroove, 15,
			-spikeThk / 2, -spikeGroove, 15,
			-spikeThk / 2, spikeGroove, -1

		del 2
		cutend

	next i
	cutend

	addz -plateTrim
	cutplane{2} 0, 4
	del 1

	roty 90
	addz beamThk / 2
	material matMetal

	prism_ 5, plateThk,
		0, -beamThk / 2 / cos(legAngle), 15,
		plateHeight, -beamThk / 2 / cos(legAngle) - tan(legAngle) * plateHeight, 15,
		plateHeight, beamThk / 2 / cos(legAngle) + tan(legAngle) * plateHeight, 15,
		0, beamThk / 2 / cos(legAngle), 15,
		0, -beamThk / 2 / cos(legAngle), -1

	del 2

	if bDoublePlate then
		roty 90
		mulz -1
		addz beamThk / 2
		material matMetal
	
		prism_ 5, plateThk,
			0, -beamThk / 2 / cos(legAngle), 15,
			plateHeight, -beamThk / 2 / cos(legAngle) - tan(legAngle) * plateHeight, 15,
			plateHeight, beamThk / 2 / cos(legAngle) + tan(legAngle) * plateHeight, 15,
			0, beamThk / 2 / cos(legAngle), 15,
			0, -beamThk / 2 / cos(legAngle), -1
	
		del 3
	endif

	cutend
	del 1
return

! -----------------------------------------------------------------------------
"drawCrossBeam": ! Cross Beam
! -----------------------------------------------------------------------------
	material matMetal
	prism_ 2, crossBeamLength,
		0, 0, 979,
		crossBeamRad, 360, 4079

return

! -----------------------------------------------------------------------------
"drawSeat": ! Simple seat
! -----------------------------------------------------------------------------
	material matSeat
	mulz -1

	bprism_ matSeat, matSeat, matSeat,
		7, seatThk, seatBend,
		-seatWidth / 2 + seatRad, -seatDepth / 2, 79,
		-seatWidth / 2 + seatRad, 0, 979,
		seatRad, -180, 4079,
		seatWidth / 2 - seatRad, seatDepth / 2, 79,
		 seatWidth / 2 - seatRad, 0, 979,
		seatRad, -180, 4079,
		-seatWidth / 2 + seatRad, -seatDepth / 2, -1

	del 1

	for i = 1 to 2

		material matRope
		tube 2, 4, 1+2+16+32,
			0, 0, 901,
			ropeRad, 360, 4001,

			-seatWidth / 2 + ropeRad, 0, -1, 0,
			-seatWidth / 2 + ropeRad, 0, seatThk, 0,
			-connectionWidth / 2, 0, ZZYZX - seatHeight - crossBeamOffset - crossBeamRad * 2, 0,
			-connectionWidth / 2, 0, ZZYZX, 0

		add -connectionWidth / 2, 0, ZZYZX - seatHeight - crossBeamOffset
		mulz -1

		prism_ 2, crossBeamRad * 2,
			0, 0, 979,
			ropeRad * 2, 360, 4079

		del 2

		mulx -1

	next i

	del 2
return

! -----------------------------------------------------------------------------
"drawBabySeat": ! Baby seat
! -----------------------------------------------------------------------------
	material matSeat

	prism_ 26, babySeatHeight / 2,
		-babySeatWidth / 2 + babySeatRad, -babySeatDepth / 2, 79,
		-babySeatWidth / 2 + babySeatRad, -babySeatDepth / 2 + babySeatRad, 979,
		babySeatRad, -90, 4079,
		-babySeatWidth / 2, babySeatDepth / 2 - babySeatRad, 79,
		-babySeatWidth / 2 + babySeatRad, babySeatDepth / 2 - babySeatRad, 979,
		babySeatRad, -90, 4079,
		babySeatWidth / 2 - babySeatRad, babySeatDepth / 2, 79,
		babySeatWidth / 2 - babySeatRad, babySeatDepth / 2 - babySeatRad, 979,
		babySeatRad, -90, 4079,
		babySeatWidth / 2, -babySeatDepth / 2 + babySeatRad, 79,
		babySeatWidth / 2 - babySeatRad, -babySeatDepth / 2 + babySeatRad, 979,
		babySeatRad, -90, 4079,
		-babySeatWidth / 2 + babySeatRad, -babySeatDepth / 2, -1,

		-babySeatWidth / 2 + babySeatRad + babySeatThk, -babySeatDepth / 2 + babySeatThk, 79,
		-babySeatWidth / 2 + babySeatRad + babySeatThk, -babySeatDepth / 2 + babySeatRad + babySeatThk, 979,
		babySeatRad, -90, 4079,
		-babySeatWidth / 2 + babySeatThk, babySeatDepth / 2 - babySeatRad - babySeatThk, 79,
		-babySeatWidth / 2 + babySeatRad + babySeatThk, babySeatDepth / 2 - babySeatRad - babySeatThk, 979,
		babySeatRad , -90, 4079,
		babySeatWidth / 2 - babySeatRad - babySeatThk, babySeatDepth / 2 - babySeatThk, 79,
		babySeatWidth / 2 - babySeatRad - babySeatThk, babySeatDepth / 2 - babySeatRad - babySeatThk, 979,
		babySeatRad, -90, 4079,
		babySeatWidth / 2 - babySeatThk, -babySeatDepth / 2 + babySeatRad + babySeatThk, 79,
		babySeatWidth / 2 - babySeatRad - babySeatThk, -babySeatDepth / 2 + babySeatRad + babySeatThk, 979,
		babySeatRad, -90, 4079,
		-babySeatWidth / 2 + babySeatRad + babySeatThk, -babySeatDepth / 2 + babySeatThk, -1

	rotx -90
	roty -90

	addz -babySeatWidth / 6

	prism_ 7, babySeatWidth / 3,
		-babySeatDepth / 2, 0, 79,
		0, 0, 979,
		babySeatDepth / 2, -180, 4079,
		babySeatDepth / 2 - babySeatThk, 0, 79,
		0, 0, 979,
		babySeatDepth / 2 - babySeatThk, 180, 4079,
		-babySeatDepth / 2, 0, -1

	del 3

	rotx -90
	addz -babySeatDepth / 2 + babySeatRad

	prism_ 17, babySeatDepth - 2 * babySeatRad,
		babySeatWidth / 2, 0, 79,
		babySeatWidth / 2, babySeatDepth / 2 - babySeatRad, 79,
		babySeatWidth / 2 - babySeatRad, babySeatDepth / 2 - babySeatRad, 979,
		babySeatRad, 90, 4079,
		-babySeatWidth / 2 + babySeatRad, babySeatDepth / 2, 79,
		-babySeatWidth / 2 + babySeatRad, babySeatDepth / 2 - babySeatRad, 979,
		babySeatRad, 90, 4079,
		-babySeatWidth / 2, 0, 79,
		-babySeatWidth / 2 + babySeatThk, 0, 79,
		-babySeatWidth / 2 + babySeatThk, babySeatDepth / 2 - babySeatRad - babySeatThk, 79,
		-babySeatWidth / 2 + babySeatThk + babySeatRad, babySeatDepth / 2 - babySeatRad - babySeatThk, 979,
		babySeatRad, -90, 4079,
		babySeatWidth / 2 - babySeatRad - babySeatThk, babySeatDepth / 2 - babySeatThk, 79,
		babySeatWidth / 2 - babySeatRad - babySeatThk, babySeatDepth / 2 - babySeatThk - babySeatRad, 979,
		babySeatRad, -90, 4079,
		babySeatWidth / 2 - babySeatThk, 0, 79,
		babySeatWidth / 2, 0, -1

	del 2

	for i = 1 to 2

		material matRope
		tube 2, 4, 1+2+16+32,
			0, 0, 901,
			ropeRad, 360, 4001,

			-babySeatWidth / 2, 0, -1, 0,
			-babySeatWidth / 2, 0, babySeatHeight / 2, 0,
			-connectionWidth / 2, 0, ZZYZX - seatHeight - crossBeamOffset - crossBeamRad * 2, 0,
			-connectionWidth / 2, 0, ZZYZX, 0

		add -connectionWidth / 2, 0, ZZYZX - seatHeight - crossBeamOffset
		mulz -1

		prism_ 2, crossBeamRad * 2,
			0, 0, 979,
			ropeRad * 2, 360, 4079

		del 2

		add -babySeatWidth / 2 + babySeatThk / 2, 0, babySeatHeight / 2 + 0.01
		mulz -1
		material matMetal
		prism_ 5, babySeatHeight / 6,
			-ropeRad * 0.75, babySeatDepth / 4 - babySeatRad, 15,
			ropeRad * 0.75, babySeatDepth / 4 - babySeatRad, 15,
			ropeRad * 0.75, -babySeatDepth / 4 + babySeatRad, 15,
			-ropeRad * 0.75, -babySeatDepth / 4 + babySeatRad, 15,
			-ropeRad * 0.75, babySeatDepth / 4 - babySeatRad, -1

		del 2

		mulx -1

	next i

	del 2

return

