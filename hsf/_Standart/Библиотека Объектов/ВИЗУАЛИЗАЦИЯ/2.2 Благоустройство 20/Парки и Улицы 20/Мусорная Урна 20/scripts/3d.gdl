
! Trash Holder


if gs_detlevel_3D_m = DLEV3D_OFF then end

IF gs_shadow=0 THEN SHADOW OFF

pen gs_cont_pen


! --- Hotspots ---

unID=1

HOTSPOT	0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
HOTSPOT	0, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
HOTSPOT	0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

FOR NN=1 TO 4
	HOTSPOT 0, 0, 0, unID, temp_RR, 1+128, A:	unID=unID+1
	HOTSPOT -1, 0, 0, unID, temp_RR, 3+128, A: unID=unID+1
	HOTSPOT temp_RR, 0, 0, unID, temp_RR, 2, A: unID=unID+1

	HOTSPOT 0, 0, zzyzx, unID, temp_RR, 1+128, A:	unID=unID+1
	HOTSPOT -1, 0, zzyzx, unID, temp_RR, 3+128, A: unID=unID+1
	HOTSPOT temp_RR,  0, zzyzx, unID, temp_RR, 2, A: unID=unID+1

	ROTz 90
NEXT NN
DEL 4

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

	! --- Feedback of 3D Hotspot Editing ---
	RESOL gs_resol
	prism_ 7, zzyzx,
			A/2,     0.0,      79,
			0.0,     0.0,    979,
			0.0,     180.0,   4079,
		   -A/2,     0.0,      79,
			0.0,     0.0,    979,
			0.0,     180.0,   4079,
			A/2,     0.0,     -1

ELSE
	! --- 3D MODEL ---

	mul  a/0.4614, b/0.4624, zzyzx/0.80

	ADDZ        0.1
	for i = 1 to 2
		cprism_ rmat,rmat,rmat,
			8, 0.05,
			0.2,  0,   79,
			0,    0,   900,
			  0, 359, 4079,
			0.2,  0,   -1,
			0.19, 0,   79,
			0,    0,   900,
			  0, 359, 4079,
			0.19, 0,   -1
		ADDZ        0.55
	next i
	DEL          2+1

	for i = 1 to 21
		cPRISM_ wmat,wmat,wmat,
				 5,     0.8,
			   -0.041597,     0.197217,     15,
				0.008229,     0.201388,     15,
				0.005726,     0.231284,     15,
			   -0.0441,     0.227113,     15,
			   -0.041597,     0.197217,     -1
		rotz 360/21
	next i
	del 21
ENDIF
