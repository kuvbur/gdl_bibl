
!!Tree Protection Vert.gsm

if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 12

IF gs_shadow=0 THEN SHADOW OFF


PEN gs_cont_pen

!!! ********************** Hotspots

	unID=1

		HOTSPOT	0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
		HOTSPOT	0, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
		HOTSPOT	0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

	FOR NN=1 TO 4
		HOTSPOT 0, 0, 0,unID, temp_RR, 1+128, A:	unID=unID+1
		HOTSPOT -1, 0, 0, unID, temp_RR, 3+128, A: unID=unID+1
		HOTSPOT temp_RR, 0, 0,unID, temp_RR, 2, A: unID=unID+1

		HOTSPOT 0, 0, ZZYZX, unID, radin, 1+128:	unID=unID+1
		HOTSPOT -1, 0, ZZYZX, unID, radin, 3+128: unID=unID+1
		HOTSPOT radin,  0, ZZYZX,unID, radin, 2: unID=unID+1

	ROTz 90
	NEXT NN
	DEL 4

!!! ********************** 3D MODEL

RESOL gs_resol

roty -90

alfa=60

material rmat
addx 0.10
revolve 5, 360, 0,
		0.00, a/2-0.02, 1,
		0.02, a/2-0.02, 1,
		0.02, a/2-0.04, 1,
		0.00, a/2-0.04, 1,
		0.00, a/2-0.02, 1

addx (a/2-radin)*tan(alfa)
revolve 5, 360, 0,
		0.00, radin+0.02, 1,
		0.02, radin+0.02, 1,
		0.02, radin, 1,
		0.00, radin, 1,
		0.00, radin+0.02, 1

del 2

addx zzyzx-0.10
revolve 5, 360, 0,
		0.00, radin+0.02, 1,
		0.02, radin+0.02, 1,
		0.02, radin, 1,
		0.00, radin, 1,
		0.00, radin+0.02, 1
del 1

gamma=(90-alfa)/2

material bmat
for i=1 to 2*nBars
	addz -0.01
	if int(i/2)=i/2 then
		prism_  9, 0.02,
				0, a/2-0.02, 15,
				0, a/2, 15,
				0.12+0.02*tan(gamma), a/2, 15,
				0.10+(a/2-radin)*tan(alfa)+0.02*tan(gamma), radin+0.04, 15,
				0.10+(a/2-radin)*tan(alfa)+0.12, radin+0.04, 15,
				0.10+(a/2-radin)*tan(alfa)+0.12, radin+0.02, 15,
				0.10+(a/2-radin)*tan(alfa), radin+0.02, 15,
				0.12, a/2-0.02, 15,
				0, a/2-0.02, -1
	else
		prism_  9, 0.02,
				0, a/2-0.02, 15,
				0, a/2, 15,
				0.12+0.02*tan(gamma), a/2, 15,
				0.10+(a/2-radin)*tan(alfa)+0.02*tan(gamma), radin+0.04, 15,
				zzyzx, radin+0.04, 15,
				zzyzx, radin+0.02, 15,
				0.10+(a/2-radin)*tan(alfa), radin+0.02, 15,
				0.12, a/2-0.02, 15,
				0, a/2-0.02, -1
	endif
	del 1
	rotx 360/(2*nBars)
next i
del nBars+1

DEL top
