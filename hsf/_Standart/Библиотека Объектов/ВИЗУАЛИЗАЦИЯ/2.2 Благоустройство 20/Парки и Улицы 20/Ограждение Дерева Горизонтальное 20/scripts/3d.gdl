
!!Tree Protection Horiz.gsm

if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 12

IF gs_shadow=0 THEN SHADOW OFF

!!! ********************** Hotspots

	unID=1

	HOTSPOT 0,0,0, unID :	unID=unID+1

	FOR NN=1 TO 4
		HOTSPOT 0, 0, 0, unID, temp_RR, 1+128, A:	unID=unID+1
		HOTSPOT -1, 0, 0, unID, temp_RR, 3+128, A: unID=unID+1
		HOTSPOT temp_RR, 0, 0, unID, temp_RR, 2, A: unID=unID+1

		HOTSPOT 0, 0, 0, unID, temp_radin, 1+128, radin:	unID=unID+1
		HOTSPOT -1, 0, 0, unID, temp_radin, 3+128, radin: unID=unID+1
		HOTSPOT temp_radin, 0, 0, unID, temp_radin, 2, radin: unID=unID+1

	ROTz 90
	NEXT NN
	DEL 4

!!! ************************* Feedback of 3D Hotspot Editing
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN

		RESOL gs_resol
		prism_ 14, zzyzx,
				A/2,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
			   -A/2,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
				A/2,     0.0,     -1,
				radin/2,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
			   -radin/2,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
				radin/2,     0.0,     -1

ELSE
!!! ********************** 3D MODEL

	material mat1
	RESOL gs_resol

	rad_1=a/2

	for i=1 to 2
		cutpoly 4,
				0.01,  rad_1+0.10,
			   -0.01,  rad_1+0.10,
			   -0.01, -(rad_1+0.10),
				0.01, -(rad_1+0.10)
		rotz 90
	next i
	del 2

	rad_2=rad_1
	for i=1 to nRings
		prism_ 14, zzyzx,
				rad_2,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
			   -rad_2,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
				rad_2,     0.0,     -1,
				rad_2-0.02,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
			   -(rad_2-0.02),     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
				rad_2-0.02,     0.0,     -1

		rad_2=rad_2-(rad_1-radin/2-0.02)/(nRings-1)
	next i

	for i=1 to 2
		cutend
	next i

	for i=1 to 4
		prism_  5, zzyzx,
				0.01,  rad_1, 15,
			   -0.01,  rad_1, 15,
			   -0.01,  radin/2, 15,
				0.01,  radin/2, 15,
				0.01,  rad_1, -1

		rotz 90
	next i
	del 2
	DEL 2
ENDIF
