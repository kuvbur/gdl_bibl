
!! Frame Vine.gsm

pen gs_cont_pen
fill gs_fill_type

add2 -a/2, brace_oh+post_y+beam_x/2-b/2		!!Drag to Origo

if iFrameVineStyle = FRAMEVINESTYLE_SINGLE or iFrameVineStyle = FRAMEVINESTYLE_SINGLESUPP then
	n=1
else
	n=2
endif

_frame_fill = 1+4								! 5 : contour only
											! 7 : contour + fill

if iFloorPlanDisplayType = DISPLAY_PROJECTED then
	if (GLOB_CSTORY_ELEV+GLOB_CUTPLANES_INFO [1]) >= ZZYZX then
		LINE_TYPE contour_line_type
		PEN gs_cont_pen
		_frame_fill = 1+2+4+64
		gosub "beams"
		gosub "post"
		gosub "breaces"
	else
		LINE_TYPE contour_line_type
		PEN cut_pen
		gosub "post"
	endif
endif

if iFloorPlanDisplayType = DISPLAY_OVERHEAD_PROJ then
	if (GLOB_CSTORY_ELEV+GLOB_CUTPLANES_INFO [1]) >= ZZYZX then
		LINE_TYPE contour_line_type
		PEN gs_cont_pen
		_frame_fill = 1+2+4+64
		gosub "beams"
		gosub "post"
		gosub "breaces"
	else
		LINE_TYPE overhead_Line_Type
		PEN gs_cont_pen
		gosub "beams"

		LINE_TYPE contour_line_type
		PEN cut_pen
		gosub "post"

		LINE_TYPE overhead_Line_Type
		PEN gs_cont_pen
		gosub "breaces"
	endif
endif

if iFloorPlanDisplayType = DISPLAY_SYMBOLIC then
	LINE_TYPE overhead_Line_Type
	PEN gs_cont_pen
	gosub "beams"

	LINE_TYPE contour_line_type
	PEN cut_pen
	gosub "post"

	LINE_TYPE overhead_Line_Type
	PEN gs_cont_pen
	gosub "breaces"
endif

if iFloorPlanDisplayType = DISPLAY_OUTLINES then
	LINE_TYPE contour_line_type
	PEN gs_cont_pen
	_frame_fill = 1+2+4+64
	gosub "beams"
	gosub "post"
	gosub "breaces"
endif

if iFloorPlanDisplayType = DISPLAY_OVERHEAD_ALL then
	LINE_TYPE overhead_Line_Type
	PEN gs_cont_pen
	gosub "beams"
	gosub "post"
	gosub "breaces"
endif

del 1

end
!===================================================================================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
!===================================================================================================================================

! -----------------------------------------------------------------------------
"beams":
! -----------------------------------------------------------------------------

hotspot2 0,0
hotspot2 a,0
if iFrameVineStyle = FRAMEVINESTYLE_DOUBLE then
	hotspot2 0,b-2*brace_oh-2*post_y-beam_x
	hotspot2 a,b-2*brace_oh-2*post_y-beam_x
endif

!! Beam
for i=1 to n
	poly2_b 5, _frame_fill, gs_fill_pen,  gs_back_pen,
			0, -beam_x/2, 1,
			a, -beam_x/2, 1,
			a,  beam_x/2, 1,
			0,  beam_x/2, 1,
			0, -beam_x/2, 1
	add2 0,b-2*brace_oh-2*post_y-beam_x
next i
del n

IF post_num > 1 Then
inner_num=ceil(((a-beam_ohl-beam_ohr-2*post_x-braces_x)/(post_num-1))/maxspace)
Else
	inner_num=ceil(((a-beam_ohl-beam_ohr-2*post_x-braces_x)/(1))/maxspace)
Endif
IF post_num > 1 Then
real_space=((a-beam_ohl-beam_ohr-2*post_x-braces_x)/(post_num-1))/inner_num
Else
	real_space=((a-beam_ohl-beam_ohr-2*post_x-braces_x)/(1))/inner_num
Endif

if iFrameVineStyle = FRAMEVINESTYLE_SINGLESUPP then
	add2 beam_ohl+post_x,b-brace_oh-post_y-beam_x/2
	poly2_b 5, _frame_fill, fp, gs_fill_pen,
			0, -beam_x/2, 1,
			a-beam_ohl-beam_ohr-2*post_x, -beam_x/2, 1,
			a-beam_ohl-beam_ohr-2*post_x, 0, 1,
			0, 0, 1,
			0, -beam_x/2, 1
	del 1
endif

return


! -----------------------------------------------------------------------------
"post":
! -----------------------------------------------------------------------------
add2 beam_ohl,0
add2 post_x/2,beam_x/2+post_y/2

!! Post
for h=1 to n
	for k=1 to post_num
	HOTSPOT2 post_x/2+braces_x/2,-post_y/2-beam_x/2
		for j=1 to 2
			for i=1 to 2
				poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
					   -post_x/2, -post_y/2, 1,
					   -post_x/2,  post_y/2, 1,
						post_x/2,  post_y/2, 1,
						post_x/2, -post_y/2, 1,
					   -post_x/2, -post_y/2, 1
				add2 0,-(post_y+beam_x)
			next i
			del 2
			add2 post_x+braces_x,0
		next j
		del 2
		IF post_num > 1 Then
		add2 (a-beam_ohl-beam_ohr-2*post_x-braces_x)/(post_num-1),0
		Else
			add2 (a-beam_ohl-beam_ohr-2*post_x-braces_x)/(1),0
		Endif
	next k
	del post_num
	add2 0,b-2*brace_oh-2*post_y-beam_x
next h
del n+2

return


! -----------------------------------------------------------------------------
"breaces":
! -----------------------------------------------------------------------------
add2 beam_ohl+post_x+braces_x/2,-brace_oh-beam_x/2-post_y

IF post_num > 1 Then
	xx = post_num-1
Else
	xx = 1
Endif

for j=1 to xx
	if j=xx then
		real_num=inner_num+1
	else
		real_num=inner_num
	endif
	for i=1 to real_num
		poly2_b 5, _frame_fill, gs_fill_pen, gs_back_pen,
				braces_x/2, 0, 1,
				braces_x/2, b, 1,
			   -braces_x/2, b, 1,
			   -braces_x/2, 0, 1,
				braces_x/2, 0, 1
		hotspot2 0,0
		hotspot2 0,b
		add2 real_space,0
	next i
	del real_num
	IF post_num > 1 Then
	add2 (a-beam_ohl-beam_ohr-2*post_x-braces_x)/(post_num-1),0
	Else
		add2 (a-beam_ohl-beam_ohr-2*post_x-braces_x)/(1),0
	Endif
next j
del post_num-1

return
