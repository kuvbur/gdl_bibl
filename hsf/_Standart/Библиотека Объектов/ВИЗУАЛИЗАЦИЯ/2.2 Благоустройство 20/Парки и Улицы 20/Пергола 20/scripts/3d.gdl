
!! Frame Vine.gsm

pen gs_cont_pen
if gs_detlevel_3D_m = DLEV3D_OFF then end

IF gs_shadow=0 THEN SHADOW OFF


material gs_frame_mat

if iFrameVineStyle = FRAMEVINESTYLE_SINGLE or iFrameVineStyle = FRAMEVINESTYLE_SINGLESUPP then
	n=1
else
	n=2
endif

ADD -a/2,brace_oh+post_y+beam_x/2-b/2,0		!!Drag to Origo

addz zzyzx-braces_y-beam_y
for i=1 to n
	prism_  5, beam_y,
			0, -beam_x/2, 15,
			a, -beam_x/2, 15,
			a, beam_x/2, 15,
			0, beam_x/2, 15,
			0, -beam_x/2, -1

	gosub 2000
	addy b-2*brace_oh-2*post_y-beam_x
next i
del n+1

addx beam_ohl
add post_x/2,beam_x/2+post_y/2,0

if post_num=1 then
	inner_pnum=post_num
else
	inner_pnum=post_num-1
endif

for h=1 to n
	for k=1 to post_num
		for j=1 to 2
			for i=1 to 2
				MATERIAL gs_post_mat
				prism_  5, zzyzx,
					   -post_x/2, -post_y/2, 15,
					   -post_x/2,  post_y/2, 15,
						post_x/2,  post_y/2, 15,
						post_x/2, -post_y/2, 15,
					   -post_x/2, -post_y/2, -1

				gosub 1000
				addy -(post_y+beam_x)
			next i
			del 2
			addx post_x+braces_x
		next j
		del 2
		addx (a-beam_ohl-beam_ohr-2*post_x-braces_x)/inner_pnum
	next k
	del post_num
	addy b-2*brace_oh-2*post_y-beam_x
next h
del n+2

inner_num=ceil(((a-beam_ohl-beam_ohr-2*post_x-braces_x)/(inner_pnum))/maxspace)
real_space=((a-beam_ohl-beam_ohr-2*post_x-braces_x)/(inner_pnum))/inner_num

addz zzyzx-braces_y
add beam_ohl+post_x+braces_x/2,-brace_oh-beam_x/2-post_y,0

for j=1 to inner_pnum
	if j=post_num-1 then
		real_num=inner_num+1
	else
		real_num=inner_num
	endif
	for i=1 to real_num
		material gs_frame_mat
		prism_  5, braces_y,
				braces_x/2, 0, 15,
				braces_x/2, b, 15,
			   -braces_x/2, b, 15,
			   -braces_x/2, 0, 15,
				braces_x/2, 0, -1
		gosub 3000
		addx real_space
	next i
	del real_num
	addx (a-beam_ohl-beam_ohr-2*post_x-braces_x)/(inner_pnum)
next j
del inner_pnum
del 2

if iFrameVineStyle = FRAMEVINESTYLE_SINGLESUPP then
	add beam_ohl+post_x,b-brace_oh-post_y-beam_x/2,zzyzx-braces_y-beam_y
	prism_  5, beam_y,
			0, -beam_x/2, 15,
			a-beam_ohl-beam_ohr-2*post_x, -beam_x/2, 15,
			a-beam_ohl-beam_ohr-2*post_x, 0, 15,
			0, 0, 15,
			0, -beam_x/2, -1

	del 1
endif
DEL 1

END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

3000:
rotz 90
gosub 5000
del 1
return


5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

