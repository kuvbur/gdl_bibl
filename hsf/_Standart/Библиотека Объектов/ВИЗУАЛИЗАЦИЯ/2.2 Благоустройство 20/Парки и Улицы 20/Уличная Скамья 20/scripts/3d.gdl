

if gs_detlevel_3D_m = DLEV3D_OFF then end

if gs_shadow=0 then shadow OFF

pen gs_cont_pen

if iBenchStyle = 1 then
	gosub "style1_hotspots"
	gosub "style1_3d"
else
	if iBenchStyle = 2 then
		gosub "style2_hotspots"
		gosub "style2_3d"
	else
		if iBenchStyle = 3 then
			gosub "style3_hotspots"
			gosub "style3_3d"
		else !if iBenchStyle = 4 then
			gosub "style4_hotspots"
			gosub "style4_3d"
		endif
	endif
endif

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
! style1_hotspots
! ------------------------------------------------------------------------------
"style1_hotspots":	

	! --- 3D Hotspots
	unID = 1
	
	hotspot	A/2,	0,		ZZYZX, unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		ZZYZX, unID, B,	2+128	: unID = unID + 1
	hotspot	A/2,	+1,		ZZYZX, unID, B,	3		: unID = unID + 1
	
	hotspot	A/2,	0,		0, unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		0, unID, B,	2+128	: unID = unID + 1
	hotspot	A/2,	+1,		0, unID, B,	3		: unID = unID + 1
	
	hotspot	A/2,	0,		seatHeight, unID, B,	1+128	: unID = unID + 1
	hotspot	A/2,	-B,		seatHeight, unID, B,	2		: unID = unID + 1
	hotspot	A/2,	+1,		seatHeight, unID, B,	3		: unID = unID + 1
	
	hotspot	0,	-B,		0,			unID, seatHeight,	1+128	: unID = unID + 1
	hotspot	0,	-B,		seatHeight, unID, seatHeight,	2		: unID = unID + 1
	hotspot	0,	-B,		-1,			unID, seatHeight,	3		: unID = unID + 1
	
	hotspot	A,	-B,		0,			unID, seatHeight,	1+128	: unID = unID + 1
	hotspot	A,	-B,		seatHeight, unID, seatHeight,	2		: unID = unID + 1
	hotspot	A,	-B,		-1,			unID, seatHeight,	3		: unID = unID + 1
	
	add 0, - 0.25926 * B, _seatBearingHeight + 0.83333 * (ZZYZX - _seatBearingHeight)
	hotspot	0,		0,		0, unID, A,	1+256	: unID = unID + 1
	hotspot	A,		0,		0, unID, A,	2		: unID = unID + 1
	hotspot	-1,		0,		0, unID, A,	3		: unID = unID + 1
	del 1

	addx A / 2
	hotspot	-0.348958 * A,	0,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	-0.348958 * A,	0,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	-0.348958 * A,	0,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	hotspot	0.348958 * A,	0,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	0.348958 * A,	0,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	0.348958 * A,	0,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	del 1
	! --- End of 3D Hotspots

return

! ------------------------------------------------------------------------------
! style1_3d
! ------------------------------------------------------------------------------
"style1_3d":	

	! --- Lower part

	material gs_stand_mat
	rotx 90
	roty -90

	addz -0.182278 * A
	
	prism_ 9, 0.031278 * A,
		0.370370 * B,	0,								15,
		0.555556 * B,	0,								15,
		0.555556 * B,	_seatBearingHeight - 0.10,		15,
		0.925926 * B,	_seatBearingHeight - 0.10,		15,
		0.925926 * B,	_seatBearingHeight,				15,
		0.10,			_seatBearingHeight,				8,
		0,				_seatBearingHeight,				13,
		0,				_seatBearingHeight - 0.10,		15,
		0.370370 * B,	_seatBearingHeight - 0.10,		15

	addz -0.666667 * A

	prism_ 9, 0.031278 * A,
		0.370370 * B,	0,								15,
		0.555556 * B,	0,								15,
		0.555556 * B,	_seatBearingHeight - 0.10,		15,
		0.925926 * B,	_seatBearingHeight - 0.10,		15,
		0.925926 * B,	_seatBearingHeight,				15,
		0.10,			_seatBearingHeight,				8,
		0,				_seatBearingHeight,				13,
		0,				_seatBearingHeight - 0.10,		15,
		0.370370 * B,	_seatBearingHeight - 0.10,		15
	
	del 4
	! --- End of lower part


	! --- Upper part
	addx 0.5 * A
	addz _seatBearingHeight

	! ------ Bearing
	material gs_stand_mat
	rotx 90
	roty -90
	addz 0.317722 * A
	
	prism_ 4,0.031278 * A,
		0.10,	0,								15,
		0.10,	(ZZYZX - _seatBearingHeight),	15,
		0,		(ZZYZX - _seatBearingHeight),	15,
		0,		0,								8
	
	addz -0.666667 * A

	prism_ 4,0.031278 * A,
		0.10,	0,								15,
		0.10,	(ZZYZX - _seatBearingHeight),	15,
		0,		(ZZYZX - _seatBearingHeight),	15,
		0,		0,								8
	
	del 4
	! ------ End of bearing
	
	! ------ Boards
	material gs_seat_mat
	add -0.5 * A,	-0.165, 0.5 * (ZZYZX - _seatBearingHeight)
	block A, 0.045, 0.333333 * (ZZYZX - _seatBearingHeight)
	del 1

	add -0.5 * A, -B, 0
	block A, 0.246914 * B, 0.045
	addy 0.283951 * B
	block A, 0.246914 * B, 0.045
	addy 0.283951 * B
	block A, 0.246914 * B, 0.045
	del 3
	! ------ End of boards
	
	del 1
	! --- End of upper part
	
	del 3

return

! ------------------------------------------------------------------------------
! style2_hotspots
! ------------------------------------------------------------------------------
"style2_hotspots":	

	! --- 3D Hotspots
	unID = 1
	
	hotspot	0.022222 * A,	0,				0, unID		: unID = unID + 1
	hotspot	0.022222 * A,	-0.96828 * B,	0, unID		: unID = unID + 1
	hotspot	0.977778 * A,	0,				0, unID		: unID = unID + 1
	hotspot	0.977778 * A,	-0.96828 * B,	0, unID		: unID = unID + 1
	
	hotspot	A/2,	0,		ZZYZX, unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		ZZYZX, unID, B,	2+128	: unID = unID + 1
	hotspot	A/2,	+1,		ZZYZX, unID, B,	3		: unID = unID + 1
	
	hotspot	A/2,	0,		0, unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		0, unID, B,	2+128	: unID = unID + 1
	hotspot	A/2,	+1,		0, unID, B,	3		: unID = unID + 1
	
	hotspot	A/2,	0,		seatHeight, unID, B,	1+128	: unID = unID + 1
	hotspot	A/2,	-B,		seatHeight, unID, B,	2		: unID = unID + 1
	hotspot	A/2,	+1,		seatHeight, unID, B,	3		: unID = unID + 1
	
	hotspot	0,	-B,		0,			unID, seatHeight,	1+128	: unID = unID + 1
	hotspot	0,	-B,		seatHeight, unID, seatHeight,	2		: unID = unID + 1
	hotspot	0,	-B,		-1,			unID, seatHeight,	3		: unID = unID + 1
	
	hotspot	A,	-B,		0,			unID, seatHeight,	1+128	: unID = unID + 1
	hotspot	A,	-B,		seatHeight, unID, seatHeight,	2		: unID = unID + 1
	hotspot	A,	-B,		-1,			unID, seatHeight,	3		: unID = unID + 1
	
	hotspot	0,		-0.080855 * B, ZZYZX, unID, A,	1+256	: unID = unID + 1
	hotspot	A,		-0.080855 * B, ZZYZX, unID, A,	2		: unID = unID + 1
	hotspot	-1,		-0.080855 * B, ZZYZX, unID, A,	3		: unID = unID + 1

	hotspot	0.022222 * A,  0,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	0.022222 * A,  0,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	0.022222 * A,  0,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	hotspot	0.977778 * A,  0,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	0.977778 * A,  0,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	0.977778 * A,  0,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	! --- End of 3D Hotspots

return

! ------------------------------------------------------------------------------
! style2_3d
! ------------------------------------------------------------------------------
"style2_3d":	

	add 0.5 * A, 0, 0
	
	! --- Lower part
	
	! ------ Bearing
	material gs_frame_mat
	rotx 90
	roty -90
	addz 0.461111 * A
	
	prism_  9+1,     0.016667 * A,
		0.123131 * B,	_seatBearingHeight,				15,
		0.123131 * B,	0.929412 * _seatBearingHeight,	15,
		0,				0,								15,
		0.053592 * B,	0,								15,
		0.176723 * B,	0.929412 * _seatBearingHeight,	15,
		0.915509 * B,	0.929412 * _seatBearingHeight,	15,
		0.915509 * B,	0,								15,
		0.968280 * B,	0,								15,
		0.968280 * B,	_seatBearingHeight,				15,
		0.176723 * B,	_seatBearingHeight,				8
	
	addz -0.938889 * A
	
	prism_  9+1,     0.016667 * A,
		0.123131 * B,	_seatBearingHeight,				15,
		0.123131 * B,	0.929412 * _seatBearingHeight,	15,
		0,				0,								15,
		0.053592 * B,	0,								15,
		0.176723 * B,	0.929412 * _seatBearingHeight,	15,
		0.915509 * B,	0.929412 * _seatBearingHeight,	15,
		0.915509 * B,	0,								15,
		0.968280 * B,	0,								15,
		0.968280 * B,	_seatBearingHeight,				15,
		0.176723 * B,	_seatBearingHeight,				8
	
	del 4
	! --- End of lower part

	! --- Upper part

	! ------ Bearing
	addz _seatBearingHeight

	rotx 90
	roty -90
	addz 0.461 * A
	
	prism_  4,  0.016667 * A,
		0.123131 * B,	0,											10,
		0.176723 * B,	0,											15,
		0.037882 * B,	0.990211 * (ZZYZX - _seatBearingHeight),	15,
		-0.014079 * B,	0.978575 * (ZZYZX - _seatBearingHeight),	15
	
	addz -0.938889 * A
	
	prism_  4,	0.016667 * A,
		0.123131 * B,	0,											10,
		0.176723 * B,	0,											15,
		0.037882 * B,	0.990211 * (ZZYZX - _seatBearingHeight),	15,
		-0.014079 * B,	0.978575 * (ZZYZX - _seatBearingHeight),	15
	
	del 4
	! ------ End of bearing
	
	! ------ Boards
	rotx 90
	roty -90
	addz -0.5 * A
	material gs_seat_mat
	
	prism_ 5, A,
		0.037882 * B,	0.990211 * (ZZYZX - _seatBearingHeight),	15,
		0.080855 * B,	0.683737 * (ZZYZX - _seatBearingHeight),	15,
		0.124155 * B,	0.693437 * (ZZYZX - _seatBearingHeight),	15,
		0.081184 * B,	1.000000 * (ZZYZX - _seatBearingHeight),	15,
		0.037882 * B,	0.990211 * (ZZYZX - _seatBearingHeight),	-1
	prism_ 5, A,
		0.086994 * B,	0.639956 * (ZZYZX - _seatBearingHeight),	15,
		0.129964 * B,	0.333482 * (ZZYZX - _seatBearingHeight),	15,
		0.173266 * B,	0.343179 * (ZZYZX - _seatBearingHeight),	15,
		0.130294 * B,	0.649656 * (ZZYZX - _seatBearingHeight),	15,
		0.086994 * B,	0.639956 * (ZZYZX - _seatBearingHeight),	-1
	
	del 3
	
	add -0.5 * A, -B, 0
	block A, 0.246262 * B, 0.025
	addy 0.281442 * B
	block A, 0.246262 * B, 0.025
	addy 0.281442 * B
	block A, 0.246262 * B, 0.025
	del 3
	! ------ End of boards
	
	! --- End of upper part
	
	del 2

return

! ------------------------------------------------------------------------------
! style3_hotspots
! ------------------------------------------------------------------------------
"style3_hotspots":
	
	! --- 3D Hotspots
	unID = 1
	
	hotspot	A/2,	0,		ZZYZX, unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		ZZYZX, unID, B,	2+128	: unID = unID + 1
	hotspot	A/2,	+1,		ZZYZX, unID, B,	3		: unID = unID + 1
	
	hotspot	A/2,	0,		0, unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		0, unID, B,	2+128	: unID = unID + 1
	hotspot	A/2,	+1,		0, unID, B,	3		: unID = unID + 1
	
	addz _seatBearingHeight
	hotspot	A/2,	0,		0.0874751 * (ZZYZX - _seatBearingHeight), unID, B,	1+128	: unID = unID + 1
	hotspot	A/2,	-B,		0.0874751 * (ZZYZX - _seatBearingHeight), unID, B,	2		: unID = unID + 1
	hotspot	A/2,	+1,		0.0874751 * (ZZYZX - _seatBearingHeight), unID, B,	3		: unID = unID + 1
	del 1
	
	hotspot	0,	-0.6101 / 0.710 * B,		0,			unID, seatHeight,	1+128	: unID = unID + 1
	hotspot	0,	-0.6101 / 0.710 * B,		seatHeight, unID, seatHeight,	2		: unID = unID + 1
	hotspot	0,	-0.6101 / 0.710 * B,		-1,			unID, seatHeight,	3		: unID = unID + 1
	
	hotspot	A,	-0.6101 / 0.710 * B,		0,			unID, seatHeight,	1+128	: unID = unID + 1
	hotspot	A,	-0.6101 / 0.710 * B,		seatHeight, unID, seatHeight,	2		: unID = unID + 1
	hotspot	A,	-0.6101 / 0.710 * B,		-1,			unID, seatHeight,	3		: unID = unID + 1
	
	hotspot	0,		0,		ZZYZX, unID, A,	1+256	: unID = unID + 1
	hotspot	A,		0,		ZZYZX, unID, A,	2		: unID = unID + 1
	hotspot	-1,		0,		ZZYZX, unID, A,	3		: unID = unID + 1
	
	hotspot	A / 2,  0,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	A / 2,  0,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	A / 2,  0,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	
	! --- End of 3D Hotspots
	
return
	
! ------------------------------------------------------------------------------
! style3_3d
! ------------------------------------------------------------------------------
"style3_3d":	

	material gs_frame_mat
	addy - B
	muly B / 0.71
	
	! --- Lower part
	mulz _seatBearingHeight/0.337
	
	rotz 90
	rotx 90
	
	for i=1 to 2
	
		prism_		29,			0.05,
			0.199203,	0.336903,	79,
			0.199203,	0.235684,	79,
			0.131073,	0.194943,	79,
			0.073418,	0.140233,	79,
			0.029014,	0.074193,	79,
			0,			0,			79,
			0.051907,	0,			79,
			0.084928,	0.071534,	79,
			0.134667,	0.132502,	79,
			0.197969,	0.179041,	79,
			0.270822,	0.208199,	79,
			0.348606,	0.218129,	79,
			0.426391,	0.208199,	79,
			0.499244,	0.179040,	79,
			0.562546,	0.132502,	79,
			0.612285,	0.071533,	79,
			0.645305,	0,			79,
			0.697212,	0,			79,
			0.668198,	0.074193,	79,
			0.623794,	0.140233,	79,
			0.566139,	0.194943,	79,
			0.498009,	0.235684,	79,
			0.498009,	0.336903,	74,
			0.448208,	0.336903,	79,
			0.448208,	0.254136,	79,
			0.382194,	0.266648,	79,
			0.315018,	0.266648,	79,
			0.249004,	0.254136,	79,
			0.249004,	0.336903,	74

		addz A - 0.05
	next i
	del 2
	del 3
	! --- End of lower part
	
	! --- Upper part
	addz _seatBearingHeight
	mulz (ZZYZX - _seatBearingHeight)/0.503
	rotz 90
	rotx 90

	! ------ Bearing
	
	for i=1 to 2
		prism_		29,			0.05,
			0.202154,	0.079427,	79,
			0.130342,	0.102293,	79,
			0.099869,	0.103205,	79,
			0.080734,	0.100786,	79,
			0.021022,	0.070251,	79,
			0.002922,	0.044035,	79,
			0.019103,	0.015835,	79,
			0.049441,	0.013748,	79,
			0.107973,	0.042575,	79,
			0.199203,	0.013748,	79,
			0.199203,	0,			74,
			0.249004,	0,			79,
			0.448208,	0,			74,
			0.498009,	0,			79,
			0.498009,	0.063841,	79,
			0.689946,	0.445299,	79,
			0.710000,	0.468961,	79,
			0.708072,	0.486417,	79,
			0.692260,	0.499218,	79,
			0.672592,	0.503097,	79,
			0.656394,	0.484477,	79,
			0.636402,	0.462036,	79,
			0.495227,	0.178029,	79,
			0.459752,	0.135063,	79,
			0.409797,	0.101565,	79,
			0.360567,	0.081902,	79,
			0.297581,	0.069523,	79,
			0.240387,	0.070979,	79,
			0.202154,	0.079427,	-1
	
	
		addz A - 0.05
	next i
	del 2
	! ------ End of bearing
	
	! ------ Boards
	material gs_seat_mat
	
	addz 0.05
	prism_		7,		A - 0.10,
		0.052468,	0.032904,	79,
		0.081797,	0.047349,	15,
		0.062087,	0.087840,	15,
		0.021971,	0.068082,	79,
		0.005565,	0.043535,	15,
		0.042762,	0.018382,	15,
		0.052468,	0.032904,	-1
	
	prism_		9,		A - 0.10,
		0.116328,	0.054296,	15,
		0.104109,	0.056434,	79,
		0.097204,	0.055899,	79,
		0.090297,	0.051624,	15,
		0.066806,	0.090020,	15,
		0.082951,	0.100013,	79,
		0.106264,	0.101817,	79,
		0.124007,	0.098713,	15,
		0.116328,	0.054296,	-1
	
	prism_		5,		A - 0.10,
		0.119608,	0.055557,	15,
		0.176040,	0.036855,	15,
		0.190066,	0.079674,	15,
		0.133634,	0.098376,	15,
		0.119608,	0.055557,	-1
	
	prism_		5,		A - 0.10,
		0.192784,	0.029776,	15,
		0.251778,	0.022665,	15,
		0.257111,	0.067429,	15,
		0.198116,	0.074539,	15,
		0.192784,	0.029776,	-1
	
	prism_		5,		A - 0.10,
		0.271166,	0.019497,	15,
		0.330205,	0.026225,	15,
		0.325159,	0.071022,	15,
		0.266120,	0.064294,	15,
		0.271166,	0.019497,	-1
	
	prism_		5,		A - 0.10,
		0.349797,	0.027626,	15,
		0.405715,	0.047832,	15,
		0.390561,	0.090261,	15,
		0.334644,	0.070055,	15,
		0.349797,	0.027626,	-1
	
	prism_		5,		A - 0.10,
		0.424462,	0.053729,	15,
		0.474261,	0.086328,	15,
		0.449812,	0.124114,	15,
		0.400014,	0.091515,	15,
		0.424462,	0.053729,	-1
	
	prism_		5,		A - 0.10,
		0.533490,	0.148972,	15,
		0.495708,	0.102846,	15,
		0.461116,	0.131514,	15,
		0.498898,	0.177640,	15,
		0.533490,	0.148972,	-1
	
	prism_		5,		A - 0.10,
		0.544689,	0.168949,	15,
		0.571261,	0.222404,	15,
		0.531172,	0.242566,	15,
		0.504600,	0.189111,	15,
		0.544689,	0.168949,	-1
	
	prism_		5,		A - 0.10,
		0.578336,	0.236639,	15,
		0.604908,	0.290095,	15,
		0.564819,	0.310257,	15,
		0.538247,	0.256801,	15,
		0.578336,	0.236639,	-1
	
	prism_		5,		A - 0.10,
		0.638556,	0.357785,	15,
		0.611984,	0.304330,	15,
		0.571895,	0.324492,	15,
		0.598467,	0.377947,	15,
		0.638556,	0.357785,	-1
	
	prism_		5,		A - 0.10,
		0.645631,	0.372020,	15,
		0.672203,	0.425475,	15,
		0.632114,	0.445637,	15,
		0.605542,	0.392182,	15,
		0.645631,	0.372020,	-1
	
	prism_      5,		A - 0.10,
		0.673473,	0.430830,	15,
		0.707731,	0.471745,	15,
		0.673449,	0.500787,	15,
		0.639191,	0.459872,	15,
		0.673473,	0.430830,	-1
	del 1
	! ------ End of boards
	
	del 4
	! --- End of upper part
	
	del 2

return

! ------------------------------------------------------------------------------
! style4_hotspots
! ------------------------------------------------------------------------------
"style4_hotspots":	

	! --- 3D Hotspots
	unID = 1
	
	hotspot	0,	-B,		0, unID, A,	1+256	: unID = unID + 1
	hotspot	A,	-B,		0, unID, A,	2		: unID = unID + 1
	hotspot	-1,	-B,		0, unID, A,	3		: unID = unID + 1
	
	hotspot	0,	0,		0, unID, A,	1+256	: unID = unID + 1
	hotspot	A,	0,		0, unID, A,	2		: unID = unID + 1
	hotspot	-1,	0,		0, unID, A,	3		: unID = unID + 1
	
	hotspot	0,	0,		0, unID, B,	1+256	: unID = unID + 1
	hotspot	0,	-B,		0, unID, B,	2		: unID = unID + 1
	hotspot	0,	+1,		0, unID, B,	3		: unID = unID + 1
	
	hotspot	A,	0,		0, unID, B,	1+256	: unID = unID + 1
	hotspot	A,	-B,		0, unID, B,	2		: unID = unID + 1
	hotspot	A,	+1,		0, unID, B,	3		: unID = unID + 1
	
	hotspot	A/2,	0,		0,	 unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		0,	 unID, B,	2+128	: unID = unID + 1
	hotspot	A/2,	+1,		0,	 unID, B,	3		: unID = unID + 1
	
	hotspot	A/2,	0,		ZZYZX, unID, B,	1+256	: unID = unID + 1
	hotspot	A/2,	-B,		ZZYZX, unID, B,	2		: unID = unID + 1
	hotspot	A/2,	+1,		ZZYZX, unID, B,	3		: unID = unID + 1
	
	hotspot	0,	-B,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	0,	-B,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	0,	-B,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	hotspot	A,	-B,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	A,	-B,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	A,	-B,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	hotspot	0,	0,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	0,	0,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	0,	0,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	hotspot	A,	0,		0,			unID, ZZYZX,	1+128	: unID = unID + 1
	hotspot	A,	0,		ZZYZX,		unID, ZZYZX,	2		: unID = unID + 1
	hotspot	A,	0,		-1,			unID, ZZYZX,	3		: unID = unID + 1
	
	! --- End of 3D Hotspots

return

! ------------------------------------------------------------------------------
! style4_3d
! ------------------------------------------------------------------------------
"style4_3d":
	
	addy -B
	
	! --- Bearing

	material gs_stand_mat
	for i=1 to 2
		prism_ 4, 0.05,
			0.015,	0.015,		15,
			0.135,	0.015,		15,
			0.135,	B - 0.015,	15,
			0.015,	B - 0.015,	15
	
		addz 0.05
		prism_ 4, ZZYZX - 0.09,
			0,		0,	11,
			0.15,	0,	11,
			0.15,	B,	11,
			0,		B,	11
	
		addz ZZYZX - 0.09
		prism_ 8, 0.04,
			0,		0,			14,
			0.15,	0,			14,
			0.15,	0.025,		15,
			0.085,	0.025,		15,
			0.085,	B - 0.025,	15,
			0.15,	B - 0.025,	14,
			0.15,	B,			14,
			0,		B,			14
	
		lin_ 0.15, 0.025, 0, 0.15, B - 0.025, 0
		del 2
	
		addx a
		mulx -1
	next i
	del 4
	! --- End of bearing
	
	! --- Boards
	
	material gs_seat_mat
	addy 0.04
	addz ZZYZX - 0.04
	for i=1 to 4
		prism_  4, 0.04,
			0.10,	 	0,				15,
			A - 0.10,	0,				15,
			A - 0.10,	(B - 0.11)/4,	15,
			0.10,		(B - 0.11)/4,	15

		addy (B - 0.11)/4+0.01
	next i
	del 6
	! --- End of boards
	
	del 1

return

