
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 				= -1
_idxTab = 1
TABID_StylePage					= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1

pageStartY = 0
pageStartX = 0

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Скамьи`
ui_current_page gs_ui_current_page

ui_page TABID_StylePage, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionsPage"

ui_page TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionsPage":
! ------------------------------------------------------------------------------

	pageStartY = 5
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	x1 = 0
	x2 = 123
	x3 = 231
	x4 = 354
	dy = 24

	_yCurr = pageStartY

	ui_outfield `Высота Сидения`, x1, _yCurr, x2 - 3, _outfHeight
	ui_infield "seatHeight", x2, _yCurr-_yOffset, 90, _infHeight  ui_tooltip `Высота Сидения`

	_yCurr = _yCurr + dy

	ui_infield{4} "iBenchStyle", 0, 24, 444, 241,
		1, 1, !"OutdoorBench_Style.png"
		4, 1,
		105, 125, 90, 90,
		1, "", 4,
		2, "", 1,
		3, "", 2,
		4, "", 3 ui_tooltip `Стиль Скамьи`



return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------

	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

	paramNameReprArray 	[_idx] = "gs_2D_representation"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_cont_pen"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_fill_type"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_fill_pen"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_back_pen"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
	
	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_3D_representation"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"

	if gs_detlevel_3D_m <> DLEV3D_OFF then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_shadow"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
	endif

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_material"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""

	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_seat_mat"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

	if iBenchStyle = 2 or iBenchStyle = 3 then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_frame_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
	endif

	if iBenchStyle = 1 or iBenchStyle = 4 then
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_stand_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
	endif


	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray


return

