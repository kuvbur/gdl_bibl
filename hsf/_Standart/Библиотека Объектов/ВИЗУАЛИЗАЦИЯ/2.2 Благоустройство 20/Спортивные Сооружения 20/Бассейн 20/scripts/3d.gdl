
!!Pool.gsm

pen gs_cont_pen

if gs_detlevel_3D_m  = DETLEV_OFF then end
if gs_detlevel_3D_m  = DETLEV_SIMPLE then gs_resol = 6

if not(gs_shadow) then shadow off

addz zzyzx

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!pool!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
RESOL 3*gs_resol
if iPoolShape = POOL_RECT then
	addz -pldp-vb
	material smat
	prism_  4, vb,
			-a/2-vb, -b/2-vb, 11,
			a/2+vb, -b/2-vb, 11,
			a/2+vb, b/2+vb, 11,
			-a/2-vb, b/2+vb, 11
	addz vb
	prism_  10, pldp-pdt,
			-a/2-vb, -b/2-vb, 14,
			a/2+vb, -b/2-vb, 14,
			a/2+vb, b/2+vb, 14,
			-a/2-vb, b/2+vb, 14,
			-a/2-vb, -b/2-vb, -1,

			-a/2, -b/2, 15,
			a/2, -b/2, 15,
			a/2, b/2, 15,
			-a/2, b/2, 15,
			-a/2, -b/2, -1
	if wh>0 then
		material wmat
		prism_  4, wh,
				-a/2, -b/2, 15,
				a/2, -b/2, 15,
				a/2, b/2, 15,
				-a/2, b/2, 15
	endif
	del 2                       !!!!addz vb, addz -zzyzx-vb!!!!

	addz -pdt
	material pdmat
	prism_  10, pdt,
			-a/2-vb-pdn, -b/2-vb-pdn, 15,
			a/2+vb+pdn, -b/2-vb-pdn, 15,
			a/2+vb+pdn, b/2+vb+pdn, 15,
			-a/2-vb-pdn, b/2+vb+pdn, 15,
			-a/2-vb-pdn, -b/2-vb-pdn, -1,

			-a/2, -b/2, 15,
			a/2, -b/2, 15,
			a/2, b/2, 15,
			-a/2, b/2, 15,
			-a/2, -b/2, -1
	del 1                       !!!!addz -pdt!!!!

	if ladder=1 then
		add -a/2+0.5, -b/2-vb/2, 0
		gosub 10
		del 1
	endif                       !!!!ladder=1!!!!


endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if ABS(a-b)<EPS and iPoolShape <> POOL_RECT then		!!a=b
	addz -pldp-vb
	material smat
	prism_  2, vb,
			0, 0, 975,
			a/2+vb, 360, 4075
	addz vb
	prism_  3, pldp-pdt,
			0, 0, 978,
			a/2+vb, 360, 4079,
			a/2, 360, 4079
	if wh>0 then
		material wmat
		prism_  2, wh,
				0, 0, 979,
				a/2, 360, 4079
	endif
	del 2                       !!!!addz vb, addz -zzyzx-vb!!!!

	addz -pdt
	material pdmat
	prism_  3, pdt,
			0, 0, 979,
			a/2+vb+pdn, 360, 4079,
			a/2, 360, 4079
	del 1                       !!!!addz -pdt!!!!

	if ladder=1 then
		add -0.25, -b/2-vb/2, 0
		gosub 10
		del 1
	endif                       !!!!ladder=1!!!!

endif                       !!!!a=b and iPoolShape <> POOL_RECT!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if ABS(a-b)>EPS and iPoolShape <> POOL_RECT then		!!a<>b
	if a>b then
		addz -pldp-vb
		material smat
		prism_  8, vb,
				-as/2, -b/2-vb, 75,
				as/2, -b/2-vb, 75,
				as/2, 0, 975,
				0, 180, 4075,
				-as/2, b/2+vb, 75,
				-as/2, 0, 975,
				0, 180, 4075,
				-as/2, -b/2-vb, -1
		addz vb
		prism_  16, pldp-pdt,
				-as/2, -b/2-vb, 78,
				as/2, -b/2-vb, 78,
				as/2, 0, 978,
				0, 180, 4078,
				-as/2, b/2+vb, 78,
				-as/2, 0, 978,
				0, 180, 4078,
				-as/2, -b/2-vb, -1,

				-as/2, -b/2, 79,
				as/2, -b/2, 79,
				as/2, 0, 979,
				0, 180, 4079,
				-as/2, b/2, 79,
				-as/2, 0, 979,
				0, 180, 4079,
				-as/2, -b/2, -1
		if wh>0 then
			material wmat
			prism_  8, wh,
					-as/2, -b/2, 79,
					as/2, -b/2, 79,
					as/2, 0, 979,
					0, 180, 4079,
					-as/2, b/2, 79,
					-as/2, 0, 979,
					0, 180, 4079,
					-as/2, -b/2, -1
		endif
		del 2                       !!!!addz vb, addz -zzyzx-vb!!!!

		addz -pdt
		material pdmat
		prism_  16, pdt,
				-as/2, -b/2-vb-pdn, 79,
				as/2, -b/2-vb-pdn, 79,
				as/2, 0, 979,
				0, 180, 4079,
				-as/2, b/2+vb+pdn, 79,
				-as/2, 0, 979,
				0, 180, 4079,
				-as/2, -b/2-vb-pdn, -1,

				-as/2, -b/2, 79,
				as/2, -b/2, 79,
				as/2, 0, 979,
				0, 180, 4079,
				-as/2, b/2, 79,
				-as/2, 0, 979,
				0, 180, 4079,
				-as/2, -b/2, -1

		del 1                       !!!!addz -pdt!!!!

		if ladder=1 then
			add -0.25, -b/2-vb/2, 0
			gosub 10
			del 1
		endif                       !!!!ladder=1!!!!

	endif                       !!!!a>b!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if b>a then
		addz -pldp-vb
		material smat
		prism_  8, vb,
				-a/2-vb, as/2, 75,
				-a/2-vb, -as/2, 75,
				0, -as/2, 975,
				0, 180, 4075,
				a/2+vb, as/2, 75,
				0, as/2, 975,
				0, 180, 4075,
				-a/2-vb, as/2, -1
		addz vb
		prism_  16, pldp-pdt,
				-a/2-vb, as/2, 78,
				-a/2-vb, -as/2, 78,
				0, -as/2, 978,
				0, 180, 4078,
				a/2+vb, as/2, 78,
				0, as/2, 978,
				0, 180, 4078,
				-a/2-vb, as/2, -1,

				-a/2, as/2, 79,
				-a/2, -as/2, 79,
				0, -as/2, 979,
				0, 180, 4079,
				a/2, as/2, 79,
				0, as/2, 979,
				0, 180, 4079,
				-a/2, as/2, -1
		if wh>0 then
			material wmat
			prism_  8, wh,
				-a/2, as/2, 79,
				-a/2, -as/2, 79,
				0, -as/2, 979,
				0, 180, 4079,
				a/2, as/2, 79,
				0, as/2, 979,
				0, 180, 4079,
				-a/2, as/2, -1
		endif
		del 2                       !!!!addz vb, addz -zzyzx-vb!!!!

		addz -pdt
		material pdmat
		prism_  16, pdt,
				-a/2-vb-pdn, as/2, 79,
				-a/2-vb-pdn, -as/2, 79,
				0, -as/2, 979,
				0, 180, 4079,
				a/2+vb+pdn, as/2, 79,
				0, as/2, 979,
				0, 180, 4079,
				-a/2-vb-pdn, as/2, -1,

				-a/2, as/2, 79,
				-a/2, -as/2, 79,
				0, -as/2, 979,
				0, 180, 4079,
				a/2, as/2, 79,
				0, as/2, 979,
				0, 180, 4079,
				-a/2, as/2, -1

		del 1                       !!!!addz -pdt!!!!


		if ladder=1 then
			add a/2+vb/2, -0.25, 0
			rotz 90
			gosub 10
			del 2
		endif                       !!!!ladder=1!!!!
	endif                   !!!!b>a!!!!
endif

del 1                       !!!!zzyzx!!!!
end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!end!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

10:
RESOL gs_resol
material lmat
roty 90
addy 0.3757
addx 0.15
addz 0.02
cylind 0.46, 0.015
del 1
addx 0.25
addz 0.02
cylind 0.46, 0.015
del 1
del 4
for n=1 to 2
	cylind 0.02, 0.04
	addz 0.02
	sweep   2, 14, 0, 1, 1+2+4+16+32,
			0, 0, 901,
			0.02, 360, 4001,

			0, 0, 0,
			0, 0, 0.490624,
			0, 0.013559, 0.546034,
			0, 0.051171, 0.588922,
			0, 0.104337, 0.609597,
			0, 0.161042, 0.603387,
			0, 0.208473, 0.571695,
			0, 0.235911, 0.521682,
			0, 0.375695, 0.0,
			0, 0.375695, -0.62,
			0, 0.364977, -0.66,
			0, 0.335695, -0.689282,
			0, 0.295695, -0.7,
			0, 0.133262, -0.7
	del 1
	addx 0.5
next n
del 2

return
