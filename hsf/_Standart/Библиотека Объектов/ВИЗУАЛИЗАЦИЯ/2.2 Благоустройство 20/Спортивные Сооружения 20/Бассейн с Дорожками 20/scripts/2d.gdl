
! Swimming Pool.gsm

drawindex 10

pen gs_cont_pen
fill gs_fill_type


hotspot2 0, 0,1

if penDeckFill<>0 then     !! Pool Deck Fill
	x1=1+2+4+64
else
	x1=1
endif

if gs_fill_pen<>0 then     !! Pool Deck Fill
	x2=1+2+4+64
else
	x2=1
endif

hotspot2 -a/2, -b/2,2
hotspot2 -a/2, b/2,3
hotspot2 a/2, b/2,4
hotspot2 a/2, -b/2,5
hotspot2 -a/2-vb-poolDeckNosing, -b/2-vb-poolDeckNosing,6
hotspot2 -a/2-vb-poolDeckNosing, b/2+vb+poolDeckNosing,7
hotspot2 a/2+vb+poolDeckNosing, b/2+vb+poolDeckNosing,8
hotspot2 a/2+vb+poolDeckNosing, -b/2-vb-poolDeckNosing,9



poly2_b 10, x1, penDeckFill, gs_back_pen,
		-a/2-vb-poolDeckNosing, -b/2-vb-poolDeckNosing, 1,
		a/2+vb+poolDeckNosing, -b/2-vb-poolDeckNosing, 1,
		a/2+vb+poolDeckNosing, b/2+vb+poolDeckNosing, 1,
		-a/2-vb-poolDeckNosing, b/2+vb+poolDeckNosing, 1,
		-a/2-vb-poolDeckNosing, -b/2-vb-poolDeckNosing, -1,

		-a/2, -b/2, 1,
		a/2, -b/2, 1,
		a/2, b/2, 1,
		-a/2, b/2, 1,
		-a/2, -b/2, -1

poly2_b 5, x2, gs_fill_pen, gs_back_pen,
		-a/2, -b/2, 15,
		a/2, -b/2, 15,
		a/2, b/2, 15,
		-a/2, b/2, 15,
		-a/2, -b/2, -1


!! Ladder !!
if bLadder = 1 then

	drawindex 20

	poly2_b 4, 1+2+4+64, penLadderFill, gs_back_pen,
			-A/2, -B/2+0.5, 1,
			-A/2+0.29, -B/2+0.5, 1,
			-A/2+0.29, -B/2+1, 1,
			-A/2, -B/2+1, 1

	poly2_b 4, 1+2+4+64, penLadderFill, gs_back_pen,
			-A/2, B/2-0.5, 1,
			-A/2+0.29, B/2-0.5, 1,
			-A/2+0.29, B/2-1, 1,
			-A/2, B/2-1, 1

	poly2_b 4, 1+2+4+64, penLadderFill, gs_back_pen,
			A/2, -B/2+0.5, 1,
			A/2-0.29, -B/2+0.5, 1,
			A/2-0.29, -B/2+1, 1,
			A/2, -B/2+1, 1

	poly2_b 4, 1+2+4+64, penLadderFill, gs_back_pen,
			A/2, B/2-0.5, 1,
			A/2-0.29, B/2-0.5, 1,
			A/2-0.29, B/2-1, 1,
			A/2, B/2-1, 1

endif
!! End of ladder !!


!! Starting platforms !!
for hi = 1 to 2
	for hj = 1 to nTrack
		add2 -A/2+trackWidth/2, -B/2
		poly2_b 4, 1, gs_fill_pen, gs_back_pen,
			-spfx/2, 0, 1,
			-spfx/2, -spfy, 1,
			spfx/2, -spfy, 1,
			spfx/2, 0, 1
		del 1
		add2 trackWidth, 0
	next hj
	del nTrack
mul2 1, -1
next hi
del 2
!! End of starting platforms !!


!! Lane markings !!
	for hj = 1 to nTrack
		add2 -A/2+trackWidth/2, 0
		line2 0, -B/2+2.5, 0, B/2-2.5
		line2 -0.5, -B/2+2.5, 0.5, -B/2+2.5
		line2 -0.5,  B/2-2.5, 0.5,  B/2-2.5
		del 1
		add2 trackWidth, 0
	next hj
	del nTrack
!! End of lane markings !!


!! Lane ropes !!
	line_type ltLaneRope
	for hj = 1 to nTrack-1
		line2 -A/2+trackWidth, -B/2, -A/2+trackWidth, B/2
		add2 trackWidth, 0
	next hj
	del nTrack-1
!! End of lane ropes !!


end


10:		! Ladder
if penLadderFill<>0 then
	add2 0.02, 0.3757
	poly2_b 4, 1+2+4+64, penLadderFill, gs_back_pen,
			0, -0.015/2, 1,
			0, 0.015/2, 1,
			0.46, 0.015/2, 1,
			0.46, -0.015/2, 1
	del 1

	for x=1 to 2
		poly2_b 2, 1+2+4+64, penLadderFill, gs_back_pen,
				0, 0, 901,
				0.04, 360, 4001
		poly2_b 8, 1+2+4+64, penLadderFill, gs_back_pen,
				0.02, 0, 1,
				0.02, 0.3757, 1,
				0, 0.3757, 901,
				0, 180, 4001,
				-0.02, 0, 1,
				0, 0, 901,
				0, 180, 4001,
				0.02, 0, -1
		add2 0.5, 0
	next x
	del 2
else
	add2 0.02, 0.3757
	line2 0, -0.015/2,0.46, -0.015/2
	line2 0, 0.015/2,0.46, 0.015/2
	del 1
	for x=1 to 2
		poly2_b 2, 1, penLadderFill, gs_back_pen,
				0, 0, 901,
				0.04, 360, 4001
		poly2_b 8, 1, penLadderFill, gs_back_pen,
				0.02, 0, 1,
				0.02, 0.3757, 1,
				0, 0.3757, 901,
				0, 180, 4001,
				-0.02, 0, 1,
				0, 0, 901,
				0, 180, 4001,
				0.02, 0, -1
		add2 0.5, 0
	next x
	del 2

endif

return

