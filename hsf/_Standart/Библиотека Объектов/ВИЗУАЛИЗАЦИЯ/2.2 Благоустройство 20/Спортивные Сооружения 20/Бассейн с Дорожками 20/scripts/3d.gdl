
! Swimming Pool.gsm

pen gs_cont_pen

if gs_detlevel_3D_m = 0 then end
if gs_detlevel_3D_m = 1 then iResol = 6

if bShadow = 0 then shadow off
if bShadow = 1 then shadow on

addz zzyzx

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!pool!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

resol iResol

addz -poolDepth-vb
material matStructure
prism_  4, vb,
		-a/2-vb, -b/2-vb, 11,
		a/2+vb, -b/2-vb, 11,
		a/2+vb, b/2+vb, 11,
		-a/2-vb, b/2+vb, 11
addz vb
prism_  10, poolDepth-pdt,
		-a/2-vb, -b/2-vb, 14,
		a/2+vb, -b/2-vb, 14,
		a/2+vb, b/2+vb, 14,
		-a/2-vb, b/2+vb, 14,
		-a/2-vb, -b/2-vb, -1,

		-a/2, -b/2, 15,
		a/2, -b/2, 15,
		a/2, b/2, 15,
		-a/2, b/2, 15,
		-a/2, -b/2, -1
if waterHeight>0 then
	material matWater
	prism_  4, waterHeight,
			-a/2, -b/2, 15,
			a/2, -b/2, 15,
			a/2, b/2, 15,
			-a/2, b/2, 15
endif
del 2                       !!!!addz vb, addz -zzyzx-vb!!!!

addz -pdt
material matPoolDeck
prism_  10, pdt,
		-a/2-vb-poolDeckNosing, -b/2-vb-poolDeckNosing, 15,
		a/2+vb+poolDeckNosing, -b/2-vb-poolDeckNosing, 15,
		a/2+vb+poolDeckNosing, b/2+vb+poolDeckNosing, 15,
		-a/2-vb-poolDeckNosing, b/2+vb+poolDeckNosing, 15,
		-a/2-vb-poolDeckNosing, -b/2-vb-poolDeckNosing, -1,

		-a/2, -b/2, 15,
		a/2, -b/2, 15,
		a/2, b/2, 15,
		-a/2, b/2, 15,
		-a/2, -b/2, -1
del 1                       !!!!addz -pdt!!!!


!! Ladder !!
if bLadder = 1 & gs_detlevel_3D_m = 2 then
	add -a/2-vb/2, -b/2+1, 0
	rotz -90
	gosub 10
	del 2

	add -a/2-vb/2, b/2-0.5, 0
	rotz -90
	gosub 10
	del 2

	add a/2+vb/2, b/2-1, 0
	rotz 90
	gosub 10
	del 2

	add a/2+vb/2, -b/2+0.5, 0
	rotz 90
	gosub 10
	del 2
endif
!! End of ladder !!


!! Starting platforms !!
for hi = 1 to 2
	for hj = 1 to nTrack
		add -A/2+trackWidth/2, -B/2-spfy/2, 0
		gosub 20
		del 1
		addx trackWidth
	next hj
	del nTrack
muly -1
next hi
del 2

if gs_detlevel_3D_m = 2 & bStPlatformNum = 1 then	! Numbering
	material matLaneMarking
	for hi = 1 to 2
		for hj = 1 to nTrack
			add -A/2+trackWidth/2, -B/2-spfy/2, 0
			gosub 21
			del 1
			addx trackWidth
		next hj
		del nTrack
	addy B+spfy
	next hi
	del 2
endif
!! End of starting platforms !!


!! Lane markings !!
if gs_detlevel_3D_m = 2 then
	for hj = 1 to nTrack
		add -A/2+trackWidth/2, 0, -zzyzx+vb
		gosub 30
		del 1
		addx trackWidth
	next hj
	del nTrack
endif
!! End of lane markings !!


!! Lane ropes !!
if waterHeight > 0 then
	material matLaneRope
	for hj = 1 to nTrack-1
		add -A/2+trackWidth, -B/2, -poolDepth+waterHeight
		rotx -90
		cylind B, 0.05
		del 2
		addx trackWidth
	next hj
	del nTrack-1
endif
!! End of lane ropes !!


del 1                       !!!!zzyzx!!!!
end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!end!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

10:
material matLadder
roty 90
addy 0.3757
addx 0.15
addz 0.02
cylind 0.46, 0.015
del 1
addx 0.25
addz 0.02
cylind 0.46, 0.015
del 1
del 4
for n=1 to 2
	cylind 0.02, 0.04
	addz 0.02
	sweep   2, 14, 0, 1, 1+2+4+16+32,
			0, 0, 901,
			0.02, 360, 4001,

			0, 0, 0,
			0, 0, 0.490624,
			0, 0.013559, 0.546034,
			0, 0.051171, 0.588922,
			0, 0.104337, 0.609597,
			0, 0.161042, 0.603387,
			0, 0.208473, 0.571695,
			0, 0.235911, 0.521682,
			0, 0.375695, 0.0,
			0, 0.375695, -0.62,
			0, 0.364977, -0.66,
			0, 0.335695, -0.689282,
			0, 0.295695, -0.7,
			0, 0.133262, -0.7
	del 1
	addx 0.5
next n
del 2

return


20:		! Starting platform

if gs_detlevel_3D_m = 1 then
	material matStartingPlatform

	prism_ 5, spfz,
		-spfx/2, -spfy/2, 15,
		 spfx/2, -spfy/2, 15,
		 spfx/2,  spfy/2, 15,
		-spfx/2,  spfy/2, 15,
		-spfx/2, -spfy/2, -1
else
	material matStartingPlatform

	prism_ 5, spfz-spfh,
		-spfx/2+0.125, -spfy/2+0.125, 15,
		 spfx/2-0.125, -spfy/2+0.125, 15,
		 spfx/2-0.125,  spfy/2-0.125, 15,
		-spfx/2+0.125,  spfy/2-0.125, 15,
		-spfx/2+0.125, -spfy/2+0.125, -1

	addz spfz-spfh
	sprism_ matStartingPlatform, matStartingPlatform, matStartingPlatform,
		5, -spfx/2, 0, spfx/2, 0, spfh, spfa,
		-spfx/2, -spfy/2, 15,
		 spfx/2, -spfy/2, 15,
		 spfx/2,  spfy/2, 15,
		-spfx/2,  spfy/2, 15,
		-spfx/2, -spfy/2, -1
	del 1

	material matLadder

	add -spfx/2+0.05, spfy/2-0.02, 0.1
	cylind spfz-0.22, 0.02
	del 1

	add spfx/2-0.05, spfy/2-0.02, 0.1
	cylind spfz-0.22, 0.02
	del 1

	cutpolya 2, 1, 0,
		-spfx/2+0.05, spfy/2-0.02, 907+64,
		0.02, 360, 4007+64

	cutpolya 2, 1, 0,
		spfx/2-0.05, spfy/2-0.02, 907+64,
		0.02, 360, 4007+64

	add spfx/2-0.05, spfy/2-0.02, 0.15
	roty -90
	cylind spfx-0.1, 0.0175
	del 2

	cutend
	cutend

	addz 0.15
	roty 90
	cutpolya 2, 1, 0,
		0, spfy/2-0.02, 907+64,
		0.0175, 360, 4007+64
	del 2

	add 0, spfy/2-0.125, 0.15
	rotx -90
	cylind 0.105, 0.0175
	del 2

	cutend
endif

return


21:		! Starting platform numbering

numw = 0.025
numx = 0.1
numy = 0.15
numh = numy/2 - numw/2

for hk=1 to 4
	add -numx/2, -0.125, 0.27-numy/2
	rotx 90
!	text 0.001, 0, nTrack+1-hj

	gosub 40+nTrack+1-hj

	del 2
	rotz 90
next hk
del 4

return


30:		! Lane markings

material matLaneMarking

prism_ 13, 0.001,
	-0.5, -B/2+2.5, 15,
	 0.5, -B/2+2.5, 15,
	 0.5, -B/2+2.75, 15,
	 0.125, -B/2+2.75, 15,
	 0.125, B/2-2.75, 15,
	 0.5, B/2-2.75, 15,
	 0.5, B/2-2.5, 15,
	-0.5, B/2-2.5, 15,
	-0.5, B/2-2.75, 15,
	-0.125, B/2-2.75, 15,
	-0.125, -B/2+2.75, 15,
	-0.5, -B/2+2.75, 15,
	-0.5, -B/2+2.5, -1

for hk=1 to 2
addy -B/2+0.001
rotx 90
prism_ 13, 0.001,
	-0.125, 0, 15,
	 0.125, 0, 15,
	 0.125, poolDepth-0.5-0.125, 15,
	 0.25, poolDepth-0.5-0.125, 15,
	 0.25, poolDepth-0.5+0.125, 15,
	 0.125, poolDepth-0.5+0.125, 15,
	 0.125, poolDepth, 15,
	-0.125, poolDepth, 15,
	-0.125, poolDepth-0.5+0.125, 15,
	-0.25, poolDepth-0.5+0.125, 15,
	-0.25, poolDepth-0.5-0.125, 15,
	-0.125, poolDepth-0.5-0.125, 15,
	-0.125, 0, -1
del 2
muly -1
next hk
del 2

return


41:		! Staring platform number 1

prism_ 5, 0.001,
	numx/2-numw/2, 0, 15,
	numx/2+numw/2, 0, 15,
	numx/2+numw/2, numy, 15,
	numx/2-numw/2, numy, 15,
	numx/2-numw/2, 0, -1

return


42:		! Staring platform number 2

prism_ 13, 0.001,
	0, 0, 15,
	numx, 0, 15,
	numx, numw, 15,
	numw, numw, 15,
	numw, numh, 15,
	numx, numh, 15,
	numx, numy, 15,
	0, numy, 15,
	0, numy-numw, 15,
	numx-numw, numy-numw, 15,
	numx-numw, numh+numw, 15,
	0, numh+numw, 15,
	0, 0, -1

return


43:		! Staring platform number 3

prism_ 14, 0.001,
	0, 0, 15,
	numx, 0, 15,
	numx, numw, 15,
	numx, numy, 15,
	0, numy, 15,
	0, numy-numw, 15,
	numx-numw, numy-numw, 15,
	numx-numw, numh+numw, 15,
	0, numh+numw, 15,
	0, numh, 15,
	numx-numw, numh, 15,
	numx-numw, numw, 15,
	0, numw, 15,
	0, 0, -1

return


44:		! Staring platform number 4

prism_ 11, 0.001,
	numx-numw, 0, 15,
	numx, 0, 15,
	numx, numy, 15,
	numx-numw, numy, 15,
	numx-numw, numh+numw, 15,
	numw, numh+numw, 15,
	numw, numy, 15,
	0, numy, 15,
	0, numh, 15,
	numx-numw, numh, 15,
	numx-numw, 0, -1

return


45:		! Staring platform number 5

prism_ 13, 0.001,
	0, 0, 15,
	numx, 0, 15,
	numx, numh+numw, 15,
	numw, numh+numw, 15,
	numw, numy-numw, 15,
	numx, numy-numw, 15,
	numx, numy, 15,
	0, numy, 15,
	0, numh, 15,
	numx-numw, numh, 15,
	numx-numw, numw, 15,
	0, numw, 15,
	0, 0, -1

return


46:		! Staring platform number 6

prism_ 9+5, 0.001,
	0, 0, 15,
	numx, 0, 15,
	numx, numh+numw, 15,
	numw, numh+numw, 15,
	numw, numy-numw, 15,
	numx, numy-numw, 15,
	numx, numy, 15,
	0, numy, 15,
	0, 0, -1,

	numw, numw, 15,
	numx-numw, numw, 15,
	numx-numw, numh, 15,
	numw, numh, 15,
	numw, numw, -1

return


47:		! Staring platform number 7

prism_ 7, 0.001,
	numx-numw, 0, 15,
	numx, 0, 15,
	numx, numy, 15,
	0, numy, 15,
	0, numy-numw, 15,
	numx-numw, numy-numw, 15,
	numx-numw, 0, -1

return


48:		! Staring platform number 8

prism_ 5+5+5, 0.001,
	0, 0, 15,
	numx, 0, 15,
	numx, numy, 15,
	0, numy, 15,
	0, 0, -1,

	numw, numw, 15,
	numx-numw, numw, 15,
	numx-numw, numh, 15,
	numw, numh, 15,
	numw, numw, -1,

	numw, numh+numw, 15,
	numx-numw, numh+numw, 15,
	numx-numw, numy-numw, 15,
	numw, numy-numw, 15,
	numw, numh+numw, -1

return


49:		! Staring platform number 9

prism_ 9+5, 0.001,
	0, 0, 15,
	numx, 0, 15,
	numx, numy, 15,
	0, numy, 15,
	0, numh, 15,
	numx-numw, numh, 15,
	numx-numw, numw, 15,
	0, numw, 15,
	0, 0, -1,

	numw, numh+numw, 15,
	numx-numw, numh+numw, 15,
	numx-numw, numy-numw, 15,
	numw, numy-numw, 15,
	numw, numh+numw, -1

return


50:		! Staring platform number 10

addx numw
prism_ 5, 0.001,
	-2*numw, 0, 15,
	-numw, 0, 15,
	-numw, numy, 15,
	-2*numw, numy, 15,
	-2*numw, 0, -1

prism_ 5+5, 0.001,
	0, 0, 15,
	numx, 0, 15,
	numx, numy, 15,
	0, numy, 15,
	0, 0, -1,

	numw, numw, 15,
	numx-numw, numw, 15,
	numx-numw, numy-numw, 15,
	numw, numy-numw, 15,
	numw, numw, -1
del 1

return

