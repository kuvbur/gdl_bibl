
! Bowling Alley

pen gs_cont_pen
fill gs_fill_type

hotspot2 0, 0
hotspot2 a, 0
hotspot2 0, b/2
hotspot2 a, b/2
hotspot2 a, b
hotspot2 0, b

hotspot2 walk_way_w_b, walk_way_w_s
hotspot2 walk_way_w_b, b-walk_way_w_s
hotspot2 walk_way_w_b+pinsetter_area_w, walk_way_w_s
hotspot2 walk_way_w_b+pinsetter_area_w, b-walk_way_w_s
hotspot2 walk_way_w_b+pinsetter_area_w+lane_length, walk_way_w_s
hotspot2 walk_way_w_b+pinsetter_area_w+lane_length, b-walk_way_w_s
hotspot2 a-settee_area_length, walk_way_w_s
hotspot2 a-settee_area_length, b-walk_way_w_s



! --- Walk Way Area ---
poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
	0, 0, 1,
	walk_way_w_b+pinsetter_area_w+lane_length+approachLength+settee_area_length, 0, 1,
	walk_way_w_b+pinsetter_area_w+lane_length+approachLength+settee_area_length, lane_area_w+walk_way_w_s*2, 1,
	0, lane_area_w+walk_way_w_s*2, 1


! --- Approach ---
add2 walk_way_w_b+pinsetter_area_w+lane_length, walk_way_w_s
poly2_ 4, 1+4,
	lane_border_ovhg, 0, 1,
	approachLength, 0, 0,
	approachLength, lane_area_w, 1,
	lane_border_ovhg, lane_area_w, 0
del 1


! --- Curtain Wall ---
if bCurtainWall then
	add2 walk_way_w_b+pinsetter_area_w+curtain_wall_pos-curtain_wall_thk, 0

	hotspot2 curtain_wall_thk, 0
	hotspot2 curtain_wall_thk, b

	line_type ltDashedLine
	poly2_ 4, 1+4,
		0, 0, 1,
		curtain_wall_thk, 0, 1,
		curtain_wall_thk, lane_area_w+walk_way_w_s*2, 1,
		0, lane_area_w+walk_way_w_s*2, 1
	line_type 1

	del 1
endif


! --- Lane Border Side ---
add2 walk_way_w_b+pinsetter_area_w, walk_way_w_s
	for lb=1 to side_lane_border_number
	poly2_ 4, 1+4,
		0, 0, 1,
		lane_length+lane_border_ovhg, 0, 1,
		lane_length+lane_border_ovhg, lane_border_w_s, 1,
		0, lane_border_w_s, 1
	add2 0, ball_ch_w*4+lane_w*2+lane_border_w_m+lane_border_w_s
	next lb
	del side_lane_border_number
del 1


! --- Lane Border Middle ---
add2 walk_way_w_b+pinsetter_area_w, walk_way_w_s+lane_border_w_s+ball_ch_w*2+lane_w

for lb=1 to middle_lane_border_number
	if bBallReturnSystem then gosub 1000

	if bControlPanel then
		add2 lane_length+approachLength+cont_pan_pos, lane_border_w_m/2
		for cpd=1 to 2
			gosub 7000
		mul2 1, -1
		next cpd
		del 2
		del 1
	endif

	poly2_ 4, 1+4,
		0, 0, 1,
		lane_length+lane_border_ovhg, 0, 1,
		lane_length+lane_border_ovhg, lane_border_w_m, 1,
		0, lane_border_w_m, 1

	add2 0, lane_border_w_m/2
	for lbb=1 to 2
		add2 0, lane_border_w_m/2
		hotspot2 lane_length, (lane_w+ball_ch_w*2)/2

		poly2_ 4, 1+4,
			0, 0, 0,
			lane_length, 0, 1,
			lane_length, lane_w+ball_ch_w*2, 0,
			0, lane_w+ball_ch_w*2, 1

		poly2_ 4, 1+4,
			0, ball_ch_w, 1,
			lane_length, ball_ch_w, 0,
			lane_length, lane_w+ball_ch_w, 1,
			0, lane_w+ball_ch_w, 0

		line2	lane_length+approachLength, (brs_w/2+r_rail*2)*bBallReturnSystem-lane_border_w_m/2+(brs_w/2+r_rail*2)*bBallReturnSystem,
				lane_length+approachLength, lane_w+ball_ch_w*2+lane_border_w_s/2+(lane_border_w_s/2)*((lb=middle_lane_border_number)*(lbb=1) or (lb=1)*(lbb=2))

		if bPins then
			add2 pin_pos, ball_ch_w+lane_w/2
			rot2 90
			gosub "Bowling Pin Group"
			del 2
		endif

		del 1
	mul2 1, -1
	next lbb
	del 2+1

add2 0, ball_ch_w*4+lane_w*2+lane_border_w_m+lane_border_w_s
next lb
del middle_lane_border_number

del 1


! --- Pinsetter ---
if bPinsetter then
	add2 walk_way_w_b, walk_way_w_s
	line_type ltDashedLine
	poly2_ 4, 1+4,
		0, 0, 1,
		pinsetter_area_w+pins_ovhg, 0, 1,
		pinsetter_area_w+pins_ovhg, lane_area_w, 1,
		0, lane_area_w, 1
	line_type 1
	del 1
endif



end







1000:

! --- Ball Return System ---

add2 lane_length+approachLength-brs_pos, -brs_w/2+lane_border_w_m/2

hotspot2 0, brs_w/2
hotspot2 brs_length, brs_w/2

poly2_ 4, 1+4,
	0, 0, 1,
	brs_r, 0, 1,
	brs_r, brs_w, 1,
	0, brs_w, 1

poly2_ 10, 1+4,
	brs_r-rail_ovhg, -r_rail*2, 1,
	brs_length-brs_w/2-r_rail*2, -r_rail*2, 1,
	brs_length*2, 0, 800,
	brs_length-brs_w/2-r_rail*2, brs_w+r_rail*2, 1001,
	brs_r-rail_ovhg, brs_w+r_rail*2, 1,

	brs_r-rail_ovhg, brs_w, 1,
	brs_length-brs_w/2-r_rail*2, brs_w, 1,
	brs_length*2, 0, 800,
	brs_length-brs_w/2-r_rail*2, 0, 1001,
	brs_r-rail_ovhg, 0, 1

if bBalls then
	add2 brs_r+ball_r*3/2, (ball_r*3/2)/2+brs_w/2
	for bb=1 to 6
		gosub 8000
		add2 ball_r*5/2, 0
		if abs(int(bb/2)-bb/2)>eps then
			add2 0, -ball_r*3/2
		else
			add2 0, ball_r*3/2
		endif
	next bb
	del 12
	del 1
endif

del 1

return



"Bowling Pin":

	circle2 0, 0, 2 1/2"

return



"Bowling Pin Group":

	for pn=1 to 4
		for pnn=1 to pn
			gosub "Bowling Pin"
			add2 cos(60)*pin_dist*2, 0
		next pnn
		del pn
	add2 -cos(60)*pin_dist, sin(60)*pin_dist
	next pn
	del 4

return



7000:
! --- Control Panel ---

add2 0, 15"
rot2 135

hotspot2 0, 0

	! LCD
	poly2_ 4, 1+4,
		-cont_pan_w/2, -cont_pan_h/2, 1,
		cont_pan_w/2, -cont_pan_h/2, 1,
		cont_pan_w/2, cont_pan_h/2, 1,
		-cont_pan_w/2, cont_pan_h/2, 1

	! Screen
	poly2_ 4, 1+4,
		-screen2_w/2, -screen2_h/2, 1,
		screen2_w/2, -screen2_h/2, 1,
		screen2_w/2, screen2_h/2, 1,
		-screen2_w/2, screen2_h/2, 1

del 2

return



8000:
! --- Ball ---

circle2 0, 0, ball_r

return
