
! Bowling Alley

pen gs_cont_pen
resol iResol

if gs_detlevel_3D_m=0 then end

if gs_detlevel_3D_m=1 then
	bPins=0
	bBalls=0
	resol 4
endif

IF bShadow=0 THEN SHADOW OFF

hotspot 0, 0, -lane_bed_elev
hotspot a, 0, -lane_bed_elev+settee_area_elev
hotspot a, b, -lane_bed_elev+settee_area_elev
hotspot 0, b, -lane_bed_elev

hotspot walk_way_w_b, walk_way_w_s, -lane_bed_elev
hotspot walk_way_w_b, b-walk_way_w_s, -lane_bed_elev
hotspot walk_way_w_b+pinsetter_area_w, walk_way_w_s, 0
hotspot walk_way_w_b+pinsetter_area_w, b-walk_way_w_s, 0
hotspot walk_way_w_b+pinsetter_area_w+lane_length, walk_way_w_s, 0
hotspot walk_way_w_b+pinsetter_area_w+lane_length, b-walk_way_w_s, 0
hotspot a-settee_area_length, walk_way_w_s, 0
hotspot a-settee_area_length, b-walk_way_w_s, 0



!!!!!!!!!!!!!!!!!!Ball Channel Cut
add 0, walk_way_w_s+lane_border_w_s, 0
rotz 90
rotx 90

for bt=1 to nLane
	if bTargetDisplay and bCurtainWall then
		add ball_ch_w+lane_w/2, curtain_wall_hole_h+target_display_elev, walk_way_w_b+pinsetter_area_w+curtain_wall_pos
		gosub 3000
		del 1
	endif

	if bPins then
		add ball_ch_w+lane_w/2, 0, walk_way_w_b+pinsetter_area_w+pin_pos
		rotx -90
		gosub "Bowling Pin Group"
		del 2
	endif

	if bPlayerDisplay then
		add ball_ch_w+lane_w/2, 0, walk_way_w_b+pinsetter_area_w+lane_length+player_display_pos
		gosub 6000
		del 1
	endif

	for btt=1 to 2
		material matBallChannel
		cutpolya 6, 2, 0,
			0, 1, 15,
			0, 0, 79,
			ball_ch_w/2, -ball_ch_h+channel_r, 900,
			ball_ch_w, 0, 3000,
			ball_ch_w, -eps, 15,
			ball_ch_w, 1, 15
	addx lane_w+ball_ch_w
	next btt
	del 2
	if abs(int(bt/2)-bt/2)>eps then
		addx lane_w+ball_ch_w*2+lane_border_w_m
	else
		addx lane_w+ball_ch_w*2+lane_border_w_s
	endif
next bt
del nLane

del 3



!!!!!!!!!!!!!!!!!!Lane area
add walk_way_w_b+pinsetter_area_w, walk_way_w_s, 0
material matLane
prism_ 4, -lane_bed_elev,
	0, 0, 15,
	lane_length, 0, 15,
	lane_length, lane_area_w, 15,
	0, lane_area_w, 15
del 1



for bt=1 to nLane
	for btt=1 to 2
		cutend
	next btt
next bt



!!!!!!!!!!!!!!!!!!Approach
add walk_way_w_b+pinsetter_area_w+lane_length, walk_way_w_s, 0
material matApproach
prism_ 4, -lane_bed_elev,
	0, 0, 15,
	approachLength, 0, 15,
	approachLength, lane_area_w, 15,
	0, lane_area_w, 15
del 1



!!!!!!!!!!!!!!!!!!Settee Area
add walk_way_w_b+pinsetter_area_w, 0, settee_area_elev-lane_bed_elev
material matSetteeArea
prism_ 8, -settee_area_elev,
	0, 0, 15,
	lane_length+approachLength+settee_area_length, 0, 15,
	lane_length+approachLength+settee_area_length, lane_area_w+walk_way_w_s*2, 15,
	0, lane_area_w+walk_way_w_s*2, 15,
	0, lane_area_w+walk_way_w_s, 15,
	lane_length+approachLength, lane_area_w+walk_way_w_s, 15,
	lane_length+approachLength, walk_way_w_s, 15,
	0, walk_way_w_s, 15
del 1



!!!!!!!!!!!!!!!!!!Walk Way Area
add 0, 0, -lane_bed_elev
material matWalkWay
prism_ 4, -slab_thk,
	0, 0, 15,
	walk_way_w_b+pinsetter_area_w+lane_length+approachLength+settee_area_length, 0, 15,
	walk_way_w_b+pinsetter_area_w+lane_length+approachLength+settee_area_length, lane_area_w+walk_way_w_s*2, 15,
	0, lane_area_w+walk_way_w_s*2, 15
del 1



!!!!!!!!!!!!!!!!!!Curtain Wall
if bCurtainWall then
	hotspot walk_way_w_b+pinsetter_area_w+curtain_wall_pos, 0, roomHeight
	hotspot walk_way_w_b+pinsetter_area_w+curtain_wall_pos, b, roomHeight

	add walk_way_w_b+pinsetter_area_w+curtain_wall_pos-curtain_wall_thk, 0, -lane_bed_elev+settee_area_elev

	rotz 90
	rotx 90

	material matCurtainWall
	prism_ 8, curtain_wall_thk,
		0, 0, 15,
		walk_way_w_s, 0, 15,
		walk_way_w_s, curtain_wall_hole_h+lane_bed_elev-settee_area_elev, 15,
		lane_area_w+walk_way_w_s, curtain_wall_hole_h+lane_bed_elev-settee_area_elev, 15,
		lane_area_w+walk_way_w_s, 0, 15,
		lane_area_w+walk_way_w_s*2, 0, 15,
		lane_area_w+walk_way_w_s*2, roomHeight+lane_bed_elev-settee_area_elev, 15,
		0, roomHeight+lane_bed_elev-settee_area_elev, 15

	del 3
endif



!!!!!!!!!!!!!!!!!!Lane Border Side
add walk_way_w_b+pinsetter_area_w, walk_way_w_s, 0

for lb=1 to side_lane_border_number
material matLaneBorder
prism_ 4, lane_border_h,
	0, 0, 15,
	lane_length+lane_border_ovhg, 0, 15,
	lane_length+lane_border_ovhg, lane_border_w_s, 15,
	0, lane_border_w_s, 15
addy ball_ch_w*4+lane_w*2+lane_border_w_m+lane_border_w_s
next lb
del side_lane_border_number

del 1



!!!!!!!!!!!!!!!!!!Lane Border Middle
add walk_way_w_b+pinsetter_area_w, walk_way_w_s+lane_border_w_s+ball_ch_w*2+lane_w, 0

for lb=1 to middle_lane_border_number
	if bBallReturnSystem then gosub 1000

	if bControlPanel then
		add lane_length+approachLength+cont_pan_pos, lane_border_w_m/2, 0
		for cpd=1 to 2
			gosub 7000
		muly -1
		next cpd
		del 2
		del 1
	endif

	material matLaneBorder
	prism_ 4, lane_border_h,
		0, 0, 15,
		lane_length+lane_border_ovhg, 0, 15,
		lane_length+lane_border_ovhg, lane_border_w_m, 15,
		0, lane_border_w_m, 15
addy ball_ch_w*4+lane_w*2+lane_border_w_m+lane_border_w_s
next lb
del middle_lane_border_number

del 1



!!!!!!!!!!!!!!!!!!Pinsetter
if bPinsetter then
	add walk_way_w_b, walk_way_w_s, -lane_bed_elev
	rotx 90
	material matPinsetter
	prism_ 6, -lane_area_w,
		0, 0, 15,
		pinsetter_area_w, 0, 15,
		pinsetter_area_w, lane_bed_elev+curtain_wall_hole_h, 15,
		pinsetter_area_w+pins_ovhg, lane_bed_elev+curtain_wall_hole_h, 15,
		pinsetter_area_w+pins_ovhg, pins_h, 15,
		0, pins_h, 15
	del 2
endif



end









1000:
!!!!!!!!!!!!!!!!!!Ball Return System
resol iResol*4

add lane_length+approachLength-brs_pos, brs_w/2+lane_border_w_m/2, 0

	hotspot 0, -brs_w/2, 0
	hotspot brs_length, -brs_w/2, rail_h

	rotx 90
	material matBallReturnSystem
	prism_ 6, brs_w,
		brs_r, 0, 15,
		eps, 0, 15,
		0, 0, 79,
		0, brs_r, 800,
		brs_r-sqr(brs_r**2-brs_h**2), brs_h, 1015,
		brs_r, 0, 15
	del 1



	ress=180*iResol/360
	ress=INT(ress)
	teta=180/ress
	alpha=0

	FOR e=1 TO ress STEP 1
	PUT -r*COS(alpha)+r,r*SIN(alpha)+rail_length,0,0
	alpha=alpha+teta
	NEXT e

	add brs_r-rail_ovhg, r_rail, rail_h
	rotz -90
	material matBallReturnSystemRail
	tube 2,6+ress,2+16+32,
		0,0,901,
		r_rail,360,4001,

		0,-1,0,0,
		0,0,0,0,
		0,rail_length,0,0,
		GET (ress*4),
		r*2,rail_length,0,0,
		r*2,0,0,0,
		r*2,-1,0,0
	del 2



	add 0, 0, ball_tray_h
	material matBallReturnSystem
	prism_ 5, -ball_tray_thk,
		brs_r, r_rail, 15,
		brs_length-brs_w/2-r_rail*2, r_rail, 79,
		brs_length*2, 0, 800,
		brs_length-brs_w/2-r_rail*2, -brs_w-r_rail, 1079,
		brs_r, -brs_w-r_rail, 15
	del 1

	addx brs_pos-r_rail*2
	material matBallReturnSystem
	prism_ 4, ball_tray_h-ball_tray_thk,
		0, r_rail, 15,
		r_rail*2, r_rail, 15,
		r_rail*2, -brs_w-r_rail, 15,
		0, -brs_w-r_rail, 15
	del 1

	resol iResol

	if bBalls then
		add brs_r+ball_r*3/2, (ball_r*3/2)/2-brs_w/2, ball_tray_h
		for bb=1 to 6
			if bb=1 or bb=4 then ballmat=matBall1
			if bb=2 or bb=5 then ballmat=matBall2
			if bb=3 or bb=6 then ballmat=matBall3
			gosub 8000
			addx ball_r*5/2
			if abs(int(bb/2)-bb/2)>eps then
				addy -ball_r*3/2
			else
				addy ball_r*3/2
			endif
		next bb
		del 12
		del 1
	endif

del 1

return



3000:
!!!!!!!!!!!!!!!!!!Target Display

material matTargetDisplay
prism_ 3, target_display_thk,
	0, 0, 15,
	target_display_w/2, target_display_h, 15,
	-target_display_w/2, target_display_h, 15

return



"Bowling Pin":

addz 15"
roty 90
material matPin
revolve 19, 360, 1,
		0'-0.0000", 0'-0.0000",      1,
		0'-1.3558", 0'-0.0000",    900,
		0'-0.0000", -288'-2.6890",   4001,
		0'-1.3049", 0'-1.3548",      1,
		0'-1.3049", -0'-8.5873",    900,
		0'-0.0000", -49'-10.0034",   4001,
		0'-3.9098", 0'-1.0075",      1,
		0'-4.6135", 0'-5.1415",    900,
		0'-0.0000", 127'-10.4580",   4001,
		0'-6.6667", 0'-1.4851",      1,
		1'-0.5307", -0'-10.0200",    900,
		0'-0.0000", -39'-1.6797",   4001,
		0'-9.1716", 0'-2.4487",      1,
		0'-10.2524", -0'-6.7928",    900,
		0'-0.0000", -88'-0.8453",   4001,
		1'-1.4612", 0'-1.9409",      1,
		0'-11.1112", -0'-5.3166",    900,
		0'-0.0000", -41'-8.2542",   4001,
		1'-3.0000", 0'-1.2463",      1
del 2

return



"Bowling Pin Group":

for pn=1 to 4
	for pnn=1 to pn
		gosub "Bowling Pin"
		add cos(60)*pin_dist*2, 0, 0
	next pnn
	del pn
add -cos(60)*pin_dist, sin(60)*pin_dist, 0
next pn
del 4

return



6000:
!!!!!!!!!!!!!!!!!!Player's Display

add 0, playerDisplayElev, 0
rotx player_display_angle

	hotspot 0, 0, 0

	!!!LCD
	material matPlayerDisplayFrame
	prism_ 4, -player_display_thk,
		-player_display_w/2, 0, 15,
		player_display_w/2, 0, 15,
		player_display_w/2, player_display_h, 15,
		-player_display_w/2, player_display_h, 15

	!!!Screen
	add 0, (player_display_h-screen_h)/2, 0
	material matPlayerDisplayScreen
	prism_ 4, screen_thk,
		-screen_w/2, 0, 15,
		screen_w/2, 0, 15,
		screen_w/2, screen_h, 15,
		-screen_w/2, screen_h, 15
	del 1

	!!!Hanger
	add 0, 0, -player_display_thk-backboard_thk
		cutplane
	del 1

	add 0, player_display_h/2, -player_display_thk-backboard_thk
	rotx 90-player_display_angle
	addz bef_2
	material matPlayerDisplayHanger
	prism_ 4, -roomHeight+playerDisplayElev,
		-hanger_w/2, -hanger_w/2, 15,
		hanger_w/2, -hanger_w/2, 15,
		hanger_w/2, hanger_w/2, 15,
		-hanger_w/2, hanger_w/2, 15
	del 3

	cutend

del 2

return



7000:
!!!!!!!!!!!!!!!!!!Control Panel

add 0, 15", -lane_bed_elev+settee_area_elev+cont_pan_elev
rotz 135

	hotspot 0, 0, 0

	rotx cont_pan_angle

	!!!LCD
	material matControlPanelFrame
	prism_ 4, -cont_pan_thk,
		-cont_pan_w/2, -cont_pan_h/2, 15,
		cont_pan_w/2, -cont_pan_h/2, 15,
		cont_pan_w/2, cont_pan_h/2, 15,
		-cont_pan_w/2, cont_pan_h/2, 15

	!!!Screen
	material matControlPanelScreen
	prism_ 4, screen2_thk,
		-screen2_w/2, -screen2_h/2, 15,
		screen2_w/2, -screen2_h/2, 15,
		screen2_w/2, screen2_h/2, 15,
		-screen2_w/2, screen2_h/2, 15

	del 1



	!!!Stander
	rotx cont_pan_angle
	add 0, 0, -cont_pan_thk
		cutplane
	del 2

	add 0, 0, 1-cont_pan_thk
	material matControlPanelStander
	prism_ 4, -cont_pan_elev+cont_pan_thk-1,
		-stander_w/2, -stander_w/2, 15,
		stander_w/2, -stander_w/2, 15,
		stander_w/2, stander_w/2, 15,
		-stander_w/2, stander_w/2, 15
	del 1

	cutend

del 2

return



8000:
!!!!!!!!!!!!!!!!!!Ball

addz ball_r
material ballmat
sphere ball_r
del 1

return
