
!=============================================================================
! Hotspot editing
!=============================================================================

unID = 1

if iCourtSize = COURT_STANDARD then
	hotspot2 0,					0,				unID	: unID = unID + 3
	hotspot2 0,					playFieldWidth,	unID	: unID = unID + 3
	hotspot2 playFieldLength,	playFieldWidth,	unID	: unID = unID + 3
	hotspot2 playFieldLength,	0,				unID	: unID = unID + 3
else
	hotspot2 0,					0,	unID, playFieldLength, 1+256	: unID = unID + 1
	hotspot2 playFieldLength,	0,	unID, playFieldLength, 2		: unID = unID + 1
	hotspot2 -1,				0,	unID, playFieldLength, 3		: unID = unID + 1

	hotspot2 0,					playFieldWidth,	unID, playFieldLength, 1+256	: unID = unID + 1
	hotspot2 playFieldLength,	playFieldWidth,	unID, playFieldLength, 2		: unID = unID + 1
	hotspot2 -1,				playFieldWidth,	unID, playFieldLength, 3		: unID = unID + 1

	hotspot2 0, 0,				unID,	playFieldWidth, 1+256	: unID = unID + 1
	hotspot2 0, playFieldWidth,	unID,	playFieldWidth, 2		: unID = unID + 1
	hotspot2 0, -1,				unID,	playFieldWidth, 3		: unID = unID + 1

	hotspot2 playFieldLength, 0,				unID,	playFieldWidth, 1+256	: unID = unID + 1
	hotspot2 playFieldLength, playFieldWidth,	unID,	playFieldWidth, 2		: unID = unID + 1
	hotspot2 playFieldLength, -1,				unID,	playFieldWidth, 3		: unID = unID + 1
endif

if bRoutArea then
	add2 -rOutWidthEnd, -rOutWidthSide

	hotspot2 A/2, rOutWidthSide,		unID, rOutWidthSide, 1+128	: unID = unID + 1
	hotspot2 A/2, 0,					unID, rOutWidthSide, 2		: unID = unID + 1
	hotspot2 A/2, rOutWidthSide + 1,	unID, rOutWidthSide, 3		: unID = unID + 1

	hotspot2 0, rOutWidthSide,		unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot2 0, 0,					unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot2 0, rOutWidthSide + 1,	unID, rOutWidthSide, 3,		B	: unID = unID + 1

	hotspot2 A, rOutWidthSide,		unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot2 A, 0,					unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot2 A, rOutWidthSide + 1,	unID, rOutWidthSide, 3,		B	: unID = unID + 1


	hotspot2 A/2, rOutWidthSide + playFieldWidth,		unID, rOutWidthSide, 1+128	: unID = unID + 1
	hotspot2 A/2, 2 * rOutWidthSide + playFieldWidth,	unID, rOutWidthSide, 2		: unID = unID + 1
	hotspot2 A/2, rOutWidthSide,						unID, rOutWidthSide, 3		: unID = unID + 1

	hotspot2 0, rOutWidthSide + playFieldWidth,		unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot2 0, 2 * rOutWidthSide + playFieldWidth,	unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot2 0, rOutWidthSide,						unID, rOutWidthSide, 3,		B	: unID = unID + 1

	hotspot2 A, rOutWidthSide + playFieldWidth,		unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot2 A, 2 * rOutWidthSide + playFieldWidth,	unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot2 A, rOutWidthSide,						unID, rOutWidthSide, 3,		B	: unID = unID + 1


	hotspot2 rOutWidthEnd + playFieldLength,	rOutWidthSide + playFieldWidth/2, unID, rOutWidthEnd, 1+128	: unID = unID + 1
	hotspot2 2*rOutWidthEnd + playFieldLength,	rOutWidthSide + playFieldWidth/2, unID, rOutWidthEnd, 2		: unID = unID + 1
	hotspot2 playFieldLength-1,					rOutWidthSide + playFieldWidth/2, unID, rOutWidthEnd, 3		: unID = unID + 1

	hotspot2 rOutWidthEnd + playFieldLength,	0, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot2 2*rOutWidthEnd + playFieldLength,	0, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot2 playFieldLength-1,					0, unID, rOutWidthEnd, 3,		A	: unID = unID + 1

	hotspot2 rOutWidthEnd + playFieldLength,	B, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot2 2*rOutWidthEnd + playFieldLength,	B, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot2 playFieldLength-1,					B, unID, rOutWidthEnd, 3,		A	: unID = unID + 1


	hotspot2 rOutWidthEnd,		rOutWidthSide + playFieldWidth/2, unID, rOutWidthEnd, 1+128	: unID = unID + 1
	hotspot2 0,					rOutWidthSide + playFieldWidth/2, unID, rOutWidthEnd, 2		: unID = unID + 1
	hotspot2 rOutWidthEnd+1,	rOutWidthSide + playFieldWidth/2, unID, rOutWidthEnd, 3		: unID = unID + 1

	hotspot2 rOutWidthEnd,		0, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot2 0,					0, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot2 rOutWidthEnd+1,	0, unID, rOutWidthEnd, 3,		A	: unID = unID + 1

	hotspot2 rOutWidthEnd,		B, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot2 0,					B, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot2 rOutWidthEnd+1,	B, unID, rOutWidthEnd, 3,		A	: unID = unID + 1

	del 1
endif

add2 playFieldLength/2, playFieldWidth/2

!=============================================================================
! Runout Area
!=============================================================================

drawindex 10
pen line_cont_pen
fill gs_fill_type

if bRoutArea then
	poly2_b 10, 1+2+4+64, runout_fill_pen, gs_back_pen,
		-A/2, -B/2, 1,
		 A/2, -B/2, 1,
		 A/2,  B/2, 1,
		-A/2,  B/2, 1,
		-A/2, -B/2, -1,

		-A/2 + rOutWidthEnd, -B/2 + rOutWidthSide, 1,
		 A/2 - rOutWidthEnd, -B/2 + rOutWidthSide, 1,
		 A/2 - rOutWidthEnd,  B/2 - rOutWidthSide, 1,
		-A/2 + rOutWidthEnd,  B/2 - rOutWidthSide, 1,
		-A/2 + rOutWidthEnd, -B/2 + rOutWidthSide, -1
endif


!=============================================================================
! Lines
!=============================================================================

pen gs_cont_pen
drawindex 20
poly2_b 4, 1+2+4+64, line_fill_pen, gs_back_pen,
	-playFieldLength/2, -playFieldWidth/2,  1,
	 playFieldLength/2, -playFieldWidth/2,  1,
	 playFieldLength/2,  playFieldWidth/2,  1,
	-playFieldLength/2,  playFieldWidth/2,  1


!=============================================================================
! Court
!=============================================================================

fill gs_fill_type
drawindex 30

mul2 playFieldLength/44', playFieldWidth/20'

for i = 1 to 2
	for j = 1 to 2
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			22'-lineWidth,	10'-lineWidth,	1,
			19'+8",			10'-lineWidth,	1,
			19'+8",			8'-7",			1,
			22'-lineWidth,	8'-7",			1

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			19'+8"-lineWidth,	10'-lineWidth,	1,
			6'+7",				10'-lineWidth,	1,
			6'+7",				8'-7",			1,
			19'+8"-lineWidth,	8'-7",			1

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			6'+7"-lineWidth,	10'-lineWidth,	1,
			0,					10'-lineWidth,	0,
			0,					8'-7",			1,
			6'+7"-lineWidth,	8'-7",			1

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			22'-lineWidth,		8'-7"-lineWidth,	1,
			19'+8",				8'-7"-lineWidth,	1,
			19'+8",				lineWidth/2,		1,
			22'-lineWidth,		lineWidth/2,		1

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			19'+8"-lineWidth,	8'-7"-lineWidth,	1,
			6'+7",				8'-7"-lineWidth,	1,
			6'+7",				lineWidth/2,		1,
			19'+8"-lineWidth,	lineWidth/2,		1

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			6'+7"-lineWidth,	8'-7"-lineWidth,	1,
			0,					8'-7"-lineWidth,	0,
			0,					0,					0,
			6'+7"-lineWidth,	0,					1
		mul2 1, -1
	next j
	mul2 -1, 1
next i

del 4


!=============================================================================
! Net
!=============================================================================

if bNet then
	line2 0, 10'-12", 0, -10'-12"
endif





