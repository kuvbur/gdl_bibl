
if gs_detlevel_3D_m = DETLEVEL_OFF then end
if not(gs_shadow) then shadow off

!=============================================================================
! Hotspot editing
!=============================================================================

unID = 1

if iCourtSize = COURT_STANDARD then
	hotspot 0,					0,				0,	unID	: unID = unID + 3
	hotspot 0,					playFieldWidth,	0,	unID	: unID = unID + 3
	hotspot playFieldLength,	playFieldWidth,	0,	unID	: unID = unID + 3
	hotspot playFieldLength,	0,				0,	unID	: unID = unID + 3
else
	hotspot 0,					0,	0,	unID, playFieldLength, 1+256	: unID = unID + 1
	hotspot playFieldLength,	0,	0,	unID, playFieldLength, 2		: unID = unID + 1
	hotspot -1,					0,	0,	unID, playFieldLength, 3		: unID = unID + 1

	hotspot 0,					playFieldWidth,	0, unID, playFieldLength, 1+256	: unID = unID + 1
	hotspot playFieldLength,	playFieldWidth,	0, unID, playFieldLength, 2		: unID = unID + 1
	hotspot -1,					playFieldWidth,	0, unID, playFieldLength, 3		: unID = unID + 1

	hotspot 0, 0,				0, unID,	playFieldWidth, 1+256	: unID = unID + 1
	hotspot 0, playFieldWidth,	0, unID,	playFieldWidth, 2		: unID = unID + 1
	hotspot 0, -1,				0, unID,	playFieldWidth, 3		: unID = unID + 1

	hotspot playFieldLength, 0,					0, unID,	playFieldWidth, 1+256	: unID = unID + 1
	hotspot playFieldLength, playFieldWidth,	0, unID,	playFieldWidth, 2		: unID = unID + 1
	hotspot playFieldLength, -1,				0, unID,	playFieldWidth, 3		: unID = unID + 1
endif

if bRoutArea then
	add -rOutWidthEnd, -rOutWidthSide, 0

	hotspot A/2, rOutWidthSide,		0, unID, rOutWidthSide, 1+128	: unID = unID + 1
	hotspot A/2, 0,					0, unID, rOutWidthSide, 2		: unID = unID + 1
	hotspot A/2, rOutWidthSide + 1,	0, unID, rOutWidthSide, 3		: unID = unID + 1

	hotspot 0, rOutWidthSide,		0, unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot 0, 0,					0, unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot 0, rOutWidthSide + 1,	0, unID, rOutWidthSide, 3,		B	: unID = unID + 1

	hotspot A, rOutWidthSide,		0, unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot A, 0,					0, unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot A, rOutWidthSide + 1,	0, unID, rOutWidthSide, 3,		B	: unID = unID + 1


	hotspot A/2, rOutWidthSide + playFieldWidth,		0, unID, rOutWidthSide, 1+128	: unID = unID + 1
	hotspot A/2, 2 * rOutWidthSide + playFieldWidth,	0, unID, rOutWidthSide, 2		: unID = unID + 1
	hotspot A/2, rOutWidthSide,							0, unID, rOutWidthSide, 3		: unID = unID + 1

	hotspot 0, rOutWidthSide + playFieldWidth,		0, unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot 0, 2 * rOutWidthSide + playFieldWidth,	0, unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot 0, rOutWidthSide,						0, unID, rOutWidthSide, 3,		B	: unID = unID + 1

	hotspot A, rOutWidthSide + playFieldWidth,		0, unID, rOutWidthSide, 1+128,	B	: unID = unID + 1
	hotspot A, 2 * rOutWidthSide + playFieldWidth,	0, unID, rOutWidthSide, 2,		B	: unID = unID + 1
	hotspot A, rOutWidthSide,						0, unID, rOutWidthSide, 3,		B	: unID = unID + 1


	hotspot rOutWidthEnd + playFieldLength,		rOutWidthSide + playFieldWidth/2, 0, unID, rOutWidthEnd, 1+128	: unID = unID + 1
	hotspot 2*rOutWidthEnd + playFieldLength,	rOutWidthSide + playFieldWidth/2, 0, unID, rOutWidthEnd, 2		: unID = unID + 1
	hotspot playFieldLength-1,					rOutWidthSide + playFieldWidth/2, 0, unID, rOutWidthEnd, 3		: unID = unID + 1

	hotspot rOutWidthEnd + playFieldLength,		0, 0, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot 2*rOutWidthEnd + playFieldLength,	0, 0, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot playFieldLength-1,					0, 0, unID, rOutWidthEnd, 3,		A	: unID = unID + 1

	hotspot rOutWidthEnd + playFieldLength,		B, 0, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot 2*rOutWidthEnd + playFieldLength,	B, 0, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot playFieldLength-1,					B, 0, unID, rOutWidthEnd, 3,		A	: unID = unID + 1


	hotspot rOutWidthEnd,	rOutWidthSide + playFieldWidth/2, 0, unID, rOutWidthEnd, 1+128	: unID = unID + 1
	hotspot 0,				rOutWidthSide + playFieldWidth/2, 0, unID, rOutWidthEnd, 2		: unID = unID + 1
	hotspot rOutWidthEnd+1,	rOutWidthSide + playFieldWidth/2, 0, unID, rOutWidthEnd, 3		: unID = unID + 1

	hotspot rOutWidthEnd,	0, 0, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot 0,				0, 0, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot rOutWidthEnd+1,	0, 0, unID, rOutWidthEnd, 3,		A	: unID = unID + 1

	hotspot rOutWidthEnd,	B, 0, unID, rOutWidthEnd, 1+128,	A	: unID = unID + 1
	hotspot 0,				B, 0, unID, rOutWidthEnd, 2,		A	: unID = unID + 1
	hotspot rOutWidthEnd+1,	B, 0, unID, rOutWidthEnd, 3,		A	: unID = unID + 1

	del 1
endif


addz -0.3
add playFieldLength/2, playFieldWidth/2, 0



!=============================================================================
! Run Out Field
!=============================================================================

pen gs_cont_pen
material runout_mat

if bRoutArea then
	prism_ 10,     0.3,
		-A/2, -B/2, 15,
		 A/2, -B/2, 15,
		 A/2,  B/2, 15,
		-A/2,  B/2, 15,
		-A/2, -B/2, -1,

		-A/2+rOutWidthEnd, -B/2+rOutWidthSide, 15,
		 A/2-rOutWidthEnd, -B/2+rOutWidthSide, 15,
		 A/2-rOutWidthEnd,  B/2-rOutWidthSide, 15,
		-A/2+rOutWidthEnd,  B/2-rOutWidthSide, 15,
		-A/2+rOutWidthEnd, -B/2+rOutWidthSide, -1
endif

mul playFieldLength/44', playFieldWidth/20', 1


!=============================================================================
! PlayField
!=============================================================================

material court_mat

for i = 1 to 2
	for j = 1 to 2
		prism_ 4,     0.3,
			22'-lineWidth,	10'-lineWidth,	15,
			19'+8",			10'-lineWidth,	15,
			19'+8",			8'-7",			15,
			22'-lineWidth,	8'-7",			15

		prism_ 4,     0.3,
			19'+8"-lineWidth,	10'-lineWidth,	15,
			6'+6",				10'-lineWidth,	15,
			6'+6",				8'-7",			15,
			19'+8"-lineWidth,	8'-7",			15

		prism_ 4,     0.3,
			6'+6"-lineWidth,	10'-lineWidth,	15,
			0,					10'-lineWidth,	8,
			0,					8'-7",			15,
			6'+6"-lineWidth,	8'-7",			15

		prism_ 4,     0.3,
			22'-lineWidth,	8'-7"-lineWidth,	15,
			19'+8",			8'-7"-lineWidth,	15,
			19'+8",			lineWidth/2,		15,
			22'-lineWidth,	lineWidth/2,		15

		prism_ 4,     0.3,
			19'+8"-lineWidth,	8'-7"-lineWidth,	15,
			6'+6",				8'-7"-lineWidth,	15,
			6'+6",				lineWidth/2,		15,
			19'+8"-lineWidth,	lineWidth/2,		15

		prism_ 4,     0.3,
			6'+6"-lineWidth,	8'-7"-lineWidth,	15,
			0,					8'-7"-lineWidth,	8,
			0,					0,					8,
			6'+6"-lineWidth,	0,					15

		muly -1
	next j

	mulx -1
next i

del 5


!=============================================================================
! PlayField Lines
!=============================================================================

material line_mat

x_sign = 1

for i = 1 to 2

	y_sign = 1

	for j = 1 to 2

	PUT		x_sign*(22'-lineWidth),		y_sign*(10'-lineWidth),		15,
			x_sign*(19'+8"), 			y_sign*(10'-lineWidth),		15,
			x_sign*(19'+8"), 			y_sign*(8'-7"),				15,
			x_sign*(22'-lineWidth), 	y_sign*(8'-7"),				15,
			x_sign*(22'-lineWidth), 	y_sign*(10'-lineWidth),		-1,

			x_sign*(19'+8"-lineWidth), 	y_sign*(10'-lineWidth),		15,
			x_sign*(6'+6"), 			y_sign*(10'-lineWidth),		15,
			x_sign*(6'+6"), 			y_sign*(8'-7"),				15,
			x_sign*(19'+8"-lineWidth), 	y_sign*(8'-7"),				15,
			x_sign*(19'+8"-lineWidth), 	y_sign*(10'-lineWidth),		-1,

			x_sign*(6'+6"-lineWidth), 	y_sign*(10'-lineWidth),		15,
			0, 							y_sign*(10'-lineWidth),		8,
			0, 							y_sign*(8'-7"),				15,
			x_sign*(6'+6"-lineWidth), 	y_sign*(8'-7"),				15,
			x_sign*(6'+6"-lineWidth), 	y_sign*(10'-lineWidth),		-1,

			x_sign*(22'-lineWidth), 	y_sign*(8'-7"-lineWidth),	15,
			x_sign*(19'+8"), 			y_sign*(8'-7"-lineWidth),	15,
			x_sign*(19'+8"), 			y_sign*(lineWidth/2),		15,
			x_sign*(22'-lineWidth), 	y_sign*(lineWidth/2),		15,
			x_sign*(22'-lineWidth), 	y_sign*(8'-7"-lineWidth),	-1,

			x_sign*(19'+8"-lineWidth), 	y_sign*(8'-7"-lineWidth),	15,
			x_sign*(6'+6"), 			y_sign*(8'-7"-lineWidth),	15,
			x_sign*(6'+6"), 			y_sign*(lineWidth/2),		15,
			x_sign*(19'+8"-lineWidth), 	y_sign*(lineWidth/2),		15,
			x_sign*(19'+8"-lineWidth), 	y_sign*(8'-7"-lineWidth),	-1,

			x_sign*(6'+6"-lineWidth), 	y_sign*(8'-7"-lineWidth),	15,
			0, 							y_sign*(8'-7"-lineWidth),	8,
			0, 							0,							8,
			x_sign*(6'+6"-lineWidth), 	0,							13,
			x_sign*(6'+6"-lineWidth), 	y_sign*(8'-7"-lineWidth),	-1

		y_sign =  -1
	next j

	x_sign =  -1
next i

prism_ 5+nsp/3, 0.3,
	-22', -10',  15,
	 22', -10',  15,
	 22',  10',  15,
	-22',  10',  15,
	-22', -10',  -1,

	get(nsp)


del 1

del 1


!=============================================================================
! Net
!=============================================================================


if bNet then
	addz 0.3
	material post_mat

	for i = 1 to 2
		!Base
		prism_ 12, 0.025,
			-0.3, 	playFieldWidth/2+0.05, 	15,
			-0.3, 	playFieldWidth/2+0.075, 	15,
			-0.05, 	playFieldWidth/2+0.075, 	15,
			-0.05, 	playFieldWidth/2+ 1.1, 	15,
			-0.3, 	playFieldWidth/2+ 1.1, 	15,
			-0.3, 	playFieldWidth/2+ 1.23, 	15,

			0.3, 	playFieldWidth/2+ 1.23, 	15,
			0.3, 	playFieldWidth/2+ 1.1, 	15,
			0.05, 	playFieldWidth/2+ 1.1, 	15,
			0.05, 	playFieldWidth/2+0.075, 	15,
			0.3, 	playFieldWidth/2+0.075, 	15,
			0.3, 	playFieldWidth/2+0.05, 	15

		!Post
		addz 0.025
		addy playFieldWidth/2+12"
		cylind netheight, 0.03
		del 2
		muly -1
	next i
	del 2

	material net_mat
	roty -90
	poly_ 4,
		netheight-0.76, 	 playFieldWidth/2+12"-0.03, 1,
		netheight, 			 playFieldWidth/2+12"-0.03, 1,
		netheight, 			-playFieldWidth/2-12"+0.03, 1,
		netheight-0.76, 	-playFieldWidth/2-12"+0.03, 1

	del 2
endif

del 1






