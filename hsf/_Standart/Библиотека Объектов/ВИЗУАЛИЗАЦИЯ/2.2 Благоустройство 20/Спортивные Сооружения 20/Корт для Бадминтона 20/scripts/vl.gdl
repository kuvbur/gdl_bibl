
call "FM_types" parameters all

!=============================================================================
! Court type and Sizes
!=============================================================================

dim stCourtSize[2]
	stCourtSize[1] = `Стандарт`
	stCourtSize[2] = `Специальный`

values "strCourtSize" stCourtSize
values "iCourtSize" COURT_STANDARD, COURT_CUSTOM

if GLOB_MODPAR_NAME = "strCourtSize" then
	iCourtSize = COURT_STANDARD
	if strCourtSize = stCourtSize[2] then iCourtSize = COURT_CUSTOM
	parameters iCourtSize = iCourtSize
else
	strCourtSize = stCourtSize[1]
	if iCourtSize = COURT_CUSTOM then strCourtSize = stCourtSize[2]
	parameters strCourtSize = strCourtSize
endif

values "netheight" range [0.76, )

if not(bNet) then
	hideparameter "netHeight"
	lock "netHeight"
endif
if iCourtSize = COURT_STANDARD then
	netHeight = 1.55
	parameters netHeight = netHeight
	lock "netHeight"

	playFieldLength	= 13.4
	playFieldWidth	= 6.1
	parameters playFieldLength = playFieldLength, playFieldWidth = playFieldWidth
	lock "playFieldLength", "playFieldWidth"
else
	values "playFieldLength" range [2,)
	values "playFieldWidth" range [1,)
endif

if bRoutArea then
	values "A" range (playFieldLength, )
	values "B" range (playFieldWidth, )

	values "rOutWidthEnd" range (0, )
	values "rOutWidthSide" range (0, )

	if GLOB_MODPAR_NAME = "A" or GLOB_MODPAR_NAME = "B" then
		rOutWidthEnd = (A - playFieldLength) / 2
		parameters rOutWidthEnd = rOutWidthEnd

		rOutWidthSide = (B - playFieldWidth) / 2
		parameters rOutWidthSide = rOutWidthSide
	else
		A = 2 * rOutWidthEnd + playFieldLength
		parameters A = A

		B = 2 * rOutWidthSide + playFieldWidth
		parameters B = B
	endif
else
	if GLOB_MODPAR_NAME = "A" then
		playFieldLength = A
		parameters playFieldLength = playFieldLength
	else
		A = playFieldLength
		parameters A = A
	endif
	if GLOB_MODPAR_NAME = "B" then
		playFieldWidth = B
		parameters playFieldWidth = playFieldWidth
	else
		B = playFieldWidth
		parameters B = B
	endif

	lock "rOutWidthEnd", "rOutWidthSide"
	hideparameter "rOutWidthEnd", "rOutWidthSide"

	if iCourtSize = COURT_STANDARD then
		lock "A", "B"
	endif
endif

parameters ZZYZX = netHeight


!=============================================================================
! 3D Detail levels
!=============================================================================

dim stDetlevelValues[2]
	stDetlevelValues[1] = `Детальный`
	stDetlevelValues[2] = `Откл.`

values "gs_detlevel_3D"  	stDetlevelValues
values "gs_detlevel_3D_m"  DETLEVEL_DETAILED, DETLEVEL_OFF

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DETLEVEL_DETAILED
	if gs_detlevel_3D = stDetlevelValues[2] then gs_detlevel_3D_m = DETLEVEL_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetlevelValues[1]
	if gs_detlevel_3D_m = DETLEVEL_OFF then gs_detlevel_3D = stDetlevelValues[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif


!=============================================================================
! Pen and Material Limitations
!=============================================================================

if gs_cont_pen<=0 then parameters gs_cont_pen=4
if line_cont_pen<=0 then parameters line_cont_pen=4

if gs_fill_pen<=0 then parameters gs_fill_pen=91
if line_fill_pen<=0 then parameters line_fill_pen=91
if runout_fill_pen<=0 then parameters runout_fill_pen=91

if not(bNet) then
	hideparameter "post_mat", "net_mat"
	lock "post_mat", "net_mat"
endif
if not(bRoutArea) then
	hideparameter "runout_mat"
	lock "runout_mat"
endif

