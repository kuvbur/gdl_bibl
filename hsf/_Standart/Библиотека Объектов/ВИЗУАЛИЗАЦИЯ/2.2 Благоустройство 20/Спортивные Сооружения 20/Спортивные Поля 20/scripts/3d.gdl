
if gs_detlevel_3D_m=0 then end

unID = 1
resol gs_resol

!!!!!!!************************ TRACKS

GROUP "basic_plane"
	GOSUB "place_basic_plane"
ENDGROUP



DIM _courtAccessories[]

!!!!!!!************************ Long Jump + Triple Jump
IF ltj = 1 THEN
	_partName = "longjump"
	_courtAccessories[VARDIM1(_courtAccessories) + 1] = _partName
	GROUP _partName
		ROTz 90
		ADD 7', -200',0
		GOSUB "place_longjump"	
		DEL 2
	ENDGROUP
ENDIF

!!!!!!!************************ High Jump
IF hj = 1 THEN
	_partName = "highjump"
	_courtAccessories[VARDIM1(_courtAccessories) + 1] = _partName
	GROUP _partName
		ROTz 90
		ADD bb-40',-aa-50',0
		GOSUB "place_highjump"
		DEL 2
	ENDGROUP
ENDIF

!!!!!!!************************ Pole Vault
IF pv =1 THEN
	_partName = "polevault"
	_courtAccessories[VARDIM1(_courtAccessories) + 1] = _partName
	GROUP _partName
		ROTz 90
		ADD 36',46',0
		ROTz 90
		GOSUB "place_polevault"
		DEL 3
	ENDGROUP
ENDIF

!!!!!!!************************ Hammer Throw
IF ht = 1 THEN
	_partName = "hammerthrow"
	_courtAccessories[VARDIM1(_courtAccessories) + 1] = _partName
	GROUP _partName
		ADDz 0.001
		ROTz 180
		ADD 21',-30',0
		GOSUB "place_hammerthrow"
		DEL 3
	ENDGROUP
ENDIF

!!!!!!!************************ Discus Throw
IF dt = 1 THEN
	_partName = "discusthrow"
	_courtAccessories[VARDIM1(_courtAccessories) + 1] = _partName
	GROUP _partName
		ADDz 0.001
		ROTz 180
		ADD -aa-16',-bb,0
		GOSUB "place_discusthrow"
		DEL 3
	ENDGROUP
ENDIF

!!!!!!!************************ Shot Put
IF sp = 1 THEN
	_partName = "shotput"
	_courtAccessories[VARDIM1(_courtAccessories) + 1] = _partName
	GROUP _partName
		ADDz 0.001
		ROTz 180
		ADD -aa-21',-25',0
		GOSUB "place_shotput"
		DEL 3
	ENDGROUP
ENDIF

!!!!!!!************************ Javelin Throw
IF jt = 1 THEN
	_partName = "javelinthrow"
	_courtAccessories[VARDIM1(_courtAccessories) + 1] = _partName
	GROUP _partName
		ROTz 180
		ADD -aa-150',-bb*0.5-25',0
		GOSUB "place_javelinthrow"
		DEL 2
	ENDGROUP
ENDIF




!Subtract court accessories from the court basic plane
_resultBasicPlaneGroup = "basic_plane"
FOR i = 1 TO VARDIM1(_courtAccessories)
	_afterSubstraction = SUBGROUP (_resultBasicPlaneGroup, _courtAccessories[i])
	KILLGROUP _resultBasicPlaneGroup 
	_resultBasicPlaneGroup = _afterSubstraction 
NEXT i


!Place all parts of the court
PLACEGROUP _resultBasicPlaneGroup 
FOR i = 1 TO VARDIM1(_courtAccessories)
	PLACEGROUP _courtAccessories[i]
NEXT i



!!!!!!!************************
!!!!!!!************************   END
!!!!!!!************************
END


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! "place_basic_plane"
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_basic_plane":

IF tracks THEN
	pen track_pen
	material track_mat
	pen track_pen
	aa=aa-20
	r=(bb+10)/2
	PRISM_ 18,0.001,
		0,0-1.22*8,15,
		aa,0-1.22*8,15,
		aa,r,900,
		0,180,4000,
		aa,r*2+1.22*8,15,
		0,r*2+1.22*8,15,
		0,r,900,
		0,180,4000,
		0,0+1.22*8,-1,

		0,0,15,
		aa,0,15,
		aa,r,900,
		0,180,4000,
		aa,2*r,15,
		0,2*r,15,
		0,r,900,
		0,180,4000,
		0,0,-1

	FOR i=0 TO 8
		material line_mat
		ADDz 0.002

		POLY_ 18,
			0,-i*4',15,
			  aa,-i*4',79,
			  aa,r,900,
			0,180,4015,
			  aa,r*2+i*4',15,
			0,r*2+i*4',79,
			0,r,900,
			0,180,4015,
			0,-i*4',-1,

			0,-i*4'+0.5',15,
			  aa,-i*4'+0.5',79,
			  aa,r,900,
			0,180,4015,
			  aa,r*2+i*4'-0.5',15,
			0,r*2+i*4'-0.5',79,
			0,r,900,
			0,180,4015,
			0,-i*4'+0.5',-1
		DEL 1
	NEXT i

	ADDx -100'
	FOR i=1 TO 9
		ADDz 0.0015

		POLY_ 5,
			0, 0, 15,
			150, 0, 15,
			150, 0.05, 15,
			0, 0.05, 15,
			0, 0, -1
		DEL 1
		ADDy -4'
	NEXT i

	ADD -3,4',0.002
	POLY_ 10,
		0, 0, 15,
		3, 0, 15,
		3, 32'+0.05, 15,
		0, 32'+0.05, 15,
		0, 0, -1,

		0.05, 0.05, 15,
		3-0.05, 0.05, 15,
		3-0.05, 32', 15,
		0.05, 32', 15,
		0.05, 0.05, -1

	ADDx 153
	POLY_ 5,
		0, 0, 15,
		0, 32'+0.05,15,
		0.05, 32'+0.05,15,
		0.05, 0,  15,
		0, 0, -1

	DEL 12

	ADD -100'-3,-32',0
	material track_mat
	block 153,32',0.001

	DEL 1

	IF apa THEN
		material outfield_mat

		PRISM_ 14,0.0001,
			0,0,15,
			aa,0,15,
			aa,r,900,
			0,180,4000,
			aa,2*r,15,
			0,2*r,15,
			0,r,900,
			0,180,4000,
			-30',0,-1,

			-30',15',15,
			aa+20-30',15',15,
			aa+20-30',15'+bb,15,
			-30',15'+bb,15,
			-30',15',-1

		material field_mat
		pen field_pen
		ADD -30',15',0
		block aa+20,bb,0.001
		DEL 1
	ENDIF
ENDIF


RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! "place_longjump"
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_longjump":
	HOTSPOT ltx, 0, 0,	unID, lty, 1+128  	: unID = unID + 1
	HOTSPOT ltx, -1, 0,	unID, lty, 3		: unID = unID + 1
	HOTSPOT ltx, lty,0, unID, lty, 2		: unID = unID + 1

	HOTSPOT 0, lty, 0,	unID, ltx, 1+128   	: unID = unID + 1
	HOTSPOT -1, lty, 0,	unID, ltx, 3  		: unID = unID + 1
	HOTSPOT ltx, lty, 0,unID, ltx, 2  		: unID = unID + 1

	ADD ltx,lty,0

	!!!!!!!!!!!!RUNWAY!!!!!!!!!!!!
	material track_mat
	pen track_pen

	IF apa THEN
		PRISM_ 5, 0.039',
			   0', 0', 15,
			   0', 147.63', 15,
			   4.1', 147.63', 15,
			   4.1', 0', 15,
			   0', 0', -1
	ENDIF

	!border!
	material line_mat

	ADD -0.16', 0, 0.039'

	POLY_ 5,
	   0, 0, 15,
	   0, 158.46', 15,
	   0.16', 158.46', 15,
	   0.16', 0, 15,
	   0, 0, -1

	ADDX 0.16' + 4.1'

	POLY_ 5,
	   0, 0, 15,
	   0, 158.46', 15,
	   0.16', 158.46', 15,
	   0.16', 0, 15,
	   0, 0, -1

	!!!!!!!!TAKEOF BOARD+JUMP INDICATOR!!!!!!!
	material line_mat
	pen line_pen
	DEL 2
	ADDY 147.63'
	ADDX 0.049'
	ADDZ 0.019'+0.032'

	material line_mat
	pen line_pen

	POLY_ 5,
	   0, 0,15,
	   0, 0.65', 15,
	   4', 0.65', 15,
	   4', 0, 15,
	   0, 0, -1

	ADDY 0.65'
	ADDZ 0.018'-0.032'

	MATERIAL mat4
	PRISM_ 5, 0.018',
	   0, 0, 15,
	   0, 0.32', 15,
	   4', 0.32', 15,
	   4', 0, 15,
	   0, 0, -1

	DEL 2

	material track_mat
	pen track_pen
	ADDX -0.049'
	PRISM_ 5, 0.032',
	   0, 0, 15,
	   0, 0.98', 15,
	   0.049', 0.98', 15,
	   0.049', 0, 15,
	   0, 0, -1

	ADDX 4.051'
	PRISM_ 5, 0.032',
	   0, 0, 15,
	   0, 0.98', 15,
	   0.049', 0.98', 15,
	   0.049', 0, 15,
	   0, 0, -1
	DEL 1
	ADDY 0.98'
	material track_mat
	pen track_pen
	IF apa THEN
		PRISM_ 5, 0.032',
		   0, 0, 15,
		   0, 9.84', 15,
		   4.1', 9.84', 15,
		   4.1', 0, 15,
		   0, 0, -1
	ENDIF
	!!!!!!!!LANDING AREA!!!!!!!
	material line_mat
	pen landing_pen
	ADDZ -0.65'+0.98'
	ADDY 9.84'
	ADDX -2.87'

	POLY_ 10,
		  0, 0, 15,
		  0, 22.96', 15,
		  9.84', 22.96', 15,
		  9.84', 0, 15,
		  0, 0, -1,
		  0.065', 0.065', 15,
		  0.065', 22.9', 15,
		  9.77', 22.9', 15,
		  9.77', 0.065', 15,
		  0.065', 0.065', -1

	MATERIAL sand

	POLY_ 5,
		  0.065', 0.065', 15,
		  0.065', 22.9', 15,
		  9.77', 22.9', 15,
		  9.77', 0.065', 15,
		  0.065', 0.065', -1

	!!!!!!!!!TRIPLE JUMP!!!!!!
	DEL 8

	ADDX 4.1'
	ROTZ 180
	ADDX 0.049'
	ADDZ 0.065'+0.032'
	material line_mat
	pen line_pen

	POLY_ 5,
	   0, 0, 15,
	   0, 0.65', 15,
	   4', 0.65', 15,
	   4', 0, 15,
	   0, 0, -1

	ADDY 0.65'
	ADDZ 0.019'-0.032'
	MATERIAL mat4

	PRISM_ 5, 0.019',
		   0, 0, 15,
		   0, 0.32', 15,
		   4',0.32', 15,
		   4', 0, 15,
		   0, 0, -1
	DEL 2

	material line_mat
	pen line_pen

	ADDX -0.049'
	ADDz 0.032'

	POLY_ 5,
	   0, 0, 15,
	   0, 0.98', 15,
	   0.049', 0.98', 15,
	   0.049', 0, 15,
	   0, 0, -1

	ADDX 4.051'

	POLY_ 5,
	   0, 0, 15,
	   0, 0.98', 15,
	   0.049', 0.98', 15,
	   0.049', 0, 15,
	   0, 0, -1

	DEL 1+1
	ADDY 0.98'
	material track_mat
	pen track_pen
	IF apa THEN
		PRISM_ 5, 0.032',
		   0, 0, 15,
		   0, 42.65', 15,
		   4.1', 42.65', 15,
		   4.1', 0, 15,
		   0, 0, -1
	ENDIF
	!!!!!!!!!LANDING AREA!!!!!!!

	material line_mat
	pen landing_pen
	ADDZ -0.65'
	ADDY 42.65'

	ADDX -2.87'
	ADDz 0.98'

	POLY_ 10,
		0, 0, 15,
		0, 22.96', 15,
		9.84', 22.96', 15,
		9.84', 0, 15,
		0, 0, -1,
		0.065', 0.065', 15,
		0.065', 22.9', 15,
		9.77', 22.9', 15,
		9.77', 0.065', 15,
		0.065', 0.065', -1

	DEL 1

	MATERIAL sand
	PRISM_ 5, 0.98',
		  0.065', 0.065', 15,
		  0.065', 22.9', 15,
		  9.77', 22.9', 15,
		  9.77', 0.065', 15,
		  0.065', 0.065', -1

	DEL 8

	ROTZ 180
	!border!

	material line_mat
	pen track_pen
	ADDX -0.16'
	ADDz 0.039'

	POLY_ 5,
	   0, 0, 15,
	   0, 43.63', 15,
	   0.16', 43.63', 15,
	   0.16', 0, 15,
	   0, 0, 15

	ADDX 0.16' + 4.1'

	POLY_ 5,
		0, 0, 15,
		0, 43.63', 15,
		0.16', 43.63', 15,
		0.16', 0, 15,
		0, 0, -1

	DEL 4+2
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! place_highjump
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_highjump":

	HOTSPOT hjx, 0, 0,		unID , hjy, 1+128   : unID = unID + 1
	HOTSPOT hjx, -1, 0, 	unID , hjy, 3  		: unID = unID + 1
	HOTSPOT hjx, hjy,0, 	unID , hjy, 2  		: unID = unID + 1

	HOTSPOT 0, hjy, 0,	unID , hjx, 1+128   : unID = unID + 1
	HOTSPOT -1, hjy,0, 	unID , hjx, 3  		: unID = unID + 1
	HOTSPOT hjx, hjy,0,	unID , hjx, 2  		: unID = unID + 1

	ADD hjx,hjy,0
	ROTz 210

	!!!----landing unit
	MATERIAL landing_mat
	pen landing_pen
	PRISM_ 9, 2.13',
			5.74', 0, 15,
			5.74', -1.3', 15,
			8.72', -1.3', 15,
			8.72', -13', 15,
			-8.72', -13', 15,
			-8.72', -1.3', 15,
			-5.74', -1.3', 15,
			-5.74', 0, 15,
			5.74', 0, -1

	!!!----concrete platforms
	MATERIAL block_mat
	PRISM_ 5, 0.01,
			5.74', -1.31', 15,
			8.72', -1.31', 15,
			8.72', 1.67', 15,
			5.74', 1.67', 15,
			5.74', -1.31', -1

	PRISM_ 5, 0.01,
			-5.74', -1.31', 15,
			-8.72', -1.31', 15,
			-8.72', 1.67', 15,
			-5.74', 1.67', 15,
			-5.74', -1.31', -1

	!!!----runway
	material track_mat
	pen track_pen
	IF apa THEN
	PRISM_ 6, 0.0012,
			8.72', 0, 15,
			52.5'*cos(15), 52.5'*sin(15), 15,
			0, -8.72'*sin(15), 900 + 15,
			-52.5'*cos(15), 52.5'*sin(15), 3000 + 13,
			-8.72', 0, 15,
			8.72', 0, -1
	ENDIF
	material line_mat
	ADDz 0.0012

	POLY_ 12,
		8.72' + 0.05*sin(15), -0.05*cos(15), 15,
		52.66'*cos(15), 15.95*sin(15), 15,
		0, -8.72'*sin(15), 900 + 15,
		-52.66'*cos(15), 15.95*sin(15), 3000 + 13,
		-8.72' - 0.05*sin(15), -0.05*cos(15), 15,
		8.72' + 0.05*sin(15), -0.05*cos(15), -1,
		8.72', 0, 15,
		52.5'*cos(15), 52.5'*sin(15), 15,
		0, -8.72'*sin(15), 900 + 15,
		-52.5'*cos(15), 52.5'*sin(15), 3000 + 13,
		-8.72', 0, 15,
		8.72', 0, -1

	DEL 1


	!!!----bars

	resol gs_res_2

	ADDx 7.2342'
	MATERIAL bars_mat
	pen landing_pen
	CYLIND 8.86', 0.03
	ADDx -14.46'
	CYLIND 8.86', 0.03
	ADDz 7.55'
	ROTy 90
	CYLIND 14.46', 0.015
	DEL 4+2

	resol gs_resol

RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! place_polevault
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_polevault":

	ROTz -90
	HOTSPOT pvx, 0, 0,	unID , pvy, 1+128 	: unID = unID + 1
	HOTSPOT pvx, -1,0, 	unID , pvy, 3 		: unID = unID + 1
	HOTSPOT pvx, pvy,0,	unID , pvy, 2 		: unID = unID + 1

	HOTSPOT 0, pvy,0, 	unID , pvx, 1+128  	: unID = unID + 1
	HOTSPOT -1, pvy,0, 	unID , pvx, 3 		: unID = unID + 1
	HOTSPOT pvx, pvy,0,	unID , pvx, 2		: unID = unID + 1
	DEL 1

	ADD pvy,-pvx,0

	!!!----pole vault box
	pen track_pen

	MASS track_mat, track_mat, track_mat,
		5, 2, 128, -0.2,
		0.67', 3.54', 0, 15,
		-0.67', 3.54', 0, 15,
		-1', 0, 0, 15,
		1', 0,  0, 15,
		0.67', 3.54', 0, -1,

		0.246',2.8', -0.66', 15,
		-0.246',2.8', -0.66', -1

	!!!----runway
	material track_mat
	IF apa THEN
			PRISM_ 10, 0.005,
				-2', 4.85', 15,
				2', 4.85', 15,
				2', -145', 15,
				-2', -145', 15,
				-2', 4.85', -1,
				0.67', 3.54', 15,
				-0.67', 3.54', 15,
				-1', 0, 15,
				1', 0, 15,
				0.67', 3.54', -1
	ENDIF
	material line_mat

	ADDz 0.0015

	POLY_ 5,
		-2', 4.85', 15,
		-2.1', 4.85', 15,
		-2.1', -145', 15,
		-2', -145', 15,
		-2', 4.85', -1

	POLY_ 5,
		2', 4.85', 15,
		2.1', 4.85', 15,
		2.1', -145', 15,
		2', -145', 15,
		2', 4.85', -1

	ADD -2.43',-147.1',0

	POLY_ 5,
		 4.85',2', 15,
		 4.85',2.1', 15,
		 0,2.1', 15,
		 0,2', 15,
		 4.85',2', -1

	DEL 1+1

	ADDy 4.85'

	!!!----landing units
	MATERIAL landing_mat
	pen landing_pen
		PRISM_ 5,2.625',
			-8.2', 0, 15,
			8.2', 0, 15,
			8.2', 13', 15,
			-8.2', 13', 15,
			-8.2', 0, -1

		PRISM_ 5, 2.625',
			1.64', 0, 15,
			8.2', 0, 15,
			8.2', -3.3', 15,
			1.64', -3.3', 15,
			1.64', 0, -1

		PRISM_ 5, 2.625',
			-1.64', 0, 15,
			-8.2', 0, 15,
			-8.2', -3.3', 15,
			-1.64', -3.3', 15,
			-1.64', 0, -1

	!!!----concrete platforms
		MATERIAL block_mat
		PRISM_ 5, 0.01,
				8.2', 2.625', 15,
				11.5', 2.625', 15,
				11.5', -5.413', 15,
				8.2', -5.413', 15,
				8.2', 2.625', -1

		PRISM_ 5, 0.01,
				-8.2', 2.625', 15,
				-11.5', 2.625', 15,
				-11.5', -5.413', 15,
				-8.2', -5.413', 15,
				-8.2', 2.625', -1

	!!!----bars

	resol gs_res_2

	DEL 1
	ADDy 3.937'
	ADDx 10'
		MATERIAL bars_mat
		CYLIND 21.3255', 0.1'
	ADDx -20'
		CYLIND 21.3255', 0.1'
	ADDz 16.4'
	ROTy 90
		CYLIND 20', 0.015
	DEL 5+1

	resol gs_resol

RETURN


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! place_hammerthrow
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_hammerthrow":
	!!!----cage

	resol gs_res_2

	HOTSPOT htx, 0, 0,	unID , hty, 1+128  	: unID = unID + 1
	HOTSPOT htx, -1,0, 	unID , hty, 3  		: unID = unID + 1
	HOTSPOT htx, hty,0,	unID , hty, 2  		: unID = unID + 1

	HOTSPOT 0, hty,0, 	unID , htx, 1+128   : unID = unID + 1
	HOTSPOT -1, hty,0, 	unID , htx, 3  		: unID = unID + 1
	HOTSPOT htx, hty,0,	unID , htx, 2  		: unID = unID + 1

	ADD htx,hty,0
	ROTz 110
	ADD  -4.49', -7.94', 0
	MATERIAL bars_mat

	CYLIND 22.96', 0.098'
	ROTz 0
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 8.98', 0.0098', 22.3'
	DEL 2

	ADD-10.74', -1.48', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz -46
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 8.98', 0.0098', 22.3'
	DEL 2

	ADD 4.49', -7.94', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz 46
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 8.98', 0.0098', 22.3'
	DEL 2

	ADD 10.74', -1.48', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz 82
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 9.18', 0.0098', 22.3'
	DEL 2

	ADD 12.07', 7.69', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz 104
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 8.98', 0.0098', 22.3'
	DEL 2

	ADD 9.84', 16.4', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	DEL 1

	ADD -9.84', 16.4', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz -104
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 8.98', 0.0098', 22.3'
	DEL 2

	ADD -12.07', 7.69', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz -82
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 9.18', 0.0098', 22.3'
	DEL 2

	!!!----base
	MATERIAL base_mat
	pen base_pen
	CYLIND 0.2', 3.48'
	ADDz -0.32'
	material line_mat
	pen line_pen
	PRISM_ 4, 0.2',
			0, 0, 900 + 13,
			3.50', 360, 4000 + 13,
			0, 0, 900 + 13,
			3.48', 360, 4000 + 1
	DEL 1
	material line_mat
	pen line_pen
	PRISM_ 4, 0.3',
			0, 0, 900 + 13,
			4.81', 360, 4000 + 13,
			0, 0, 900 + 13,
			3.50', 360, 4000 + 1

	!!!----markings
	material line_mat
	pen line_pen
	ADD 3.48', -0.08', 0
	BLOCK 2.46', 0.3', 0.001

	DEL 1
	ADD -5.94', -0.08', 0
	BLOCK 2.46', 0.3', 0.001
	DEL 1
	ROTz 20
	BLOCK 0.3', 295.27', 0.01
	ROTz -40
	BLOCK 0.3', 295.27', 0.005
	DEL 2

	resol min(64, gs_resol*2)

	FOR i = 0 TO 5

		ADDz 0.01

		POLY_ 	7,
			-sin(20)*(295.27' - i*16.4'), cos(20)*(295.27' - i*16.4')-0.3', 13,
			0, 0, 900 + 13,
			sin(20)*(295.27' - i*16.4'), cos(20)*(295.27' - i*16.4'), 3000 + 13,
			sin(20)*(295.27' - i*16.4' - 0.16'), cos(20)*(295.27' - i*16.4' - 0.16'), 13,
			0, 0, 900 + 13,
			-sin(20)*(295.27' - i*16.4' - 0.16'), cos(20)*(295.27' - i*16.4' - 0.16'), 3000 + 13-0.3',
			-sin(20)*(295.27' - i*16.4'), cos(20)*(295.27' - i*16.4')-0.3', -1

		DEL 1

	NEXT i

	resol gs_resol
	DEL 2

RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! place_discusthrow
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_discusthrow":

	!!!----cage
	HOTSPOT dtx, 0,0, 	unID , dty, 1+128   : unID = unID + 1
	HOTSPOT dtx, -1,0, 	unID , dty, 3  		: unID = unID + 1
	HOTSPOT dtx, dty,0,	unID , dty, 2  		: unID = unID + 1

	HOTSPOT 0, dty,0, 	unID , dtx, 1+128   : unID = unID + 1
	HOTSPOT -1, dty,0, 	unID , dtx, 3  		: unID = unID + 1
	HOTSPOT dtx, dty,0,	unID , dtx, 2  		: unID = unID + 1

	ADD dtx,dty,0
	ROTz 290

	resol gs_res_2

	ADD 0, -9.84', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz 32
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 10.4', 0.009', 22.30'
	DEL 2

	ADD 8.82', -4.34', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz 84.5
	MATERIAL cage_mat
	 pen cage_pen
	BLOCK 10.4', 0.009', 22.30'
	DEL 2

	ADD -9.84', 6', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz -84.5
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 10.4', 0.009', 22.30'
	DEL 2

	ADD -9.84', 16.4', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz -90
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 10.4', 0.009', 22.30'
	DEL 2

	ADD 9.84', 16.4', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	DEL 1

	ADD -8.82', -4.34', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz -32
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 10.4', 0.009', 22.30'
	DEL 2

	ADD 9.84', 6', 0
	MATERIAL bars_mat
	CYLIND 22.96', 0.098'
	ROTz 90
	MATERIAL cage_mat
	pen cage_pen
	BLOCK 10.4', 0.009', 22.30'
	DEL 2



	!!!----base
	MATERIAL base_mat
	pen base_pen
	CYLIND 0.3', 4.1'
	ADDz -0.32'
	material line_mat
	pen line_pen
	PRISM_ 4, 0.39',
			0, 0, 900 + 13,
			4.12', 360, 4000 + 13,
			0, 0, 900 + 13,
			4.1', 360, 4000 + 1
	DEL 1
	material line_mat
	pen line_pen
	PRISM_ 4, 0.3',
			0, 0, 900 + 13,
			4.77', 360, 4000 + 13,
			0, 0, 900 + 13,
			4.12', 360, 4000 + 1

	resol gs_resol

	!!!----markings
	material line_mat
	pen line_pen
	ADD 4.1', -0.082', 0
	BLOCK 2.46', 0.3', 0.003'
	material line_mat
	pen line_pen
	ADD -10.66', -0.082', 0
	BLOCK 2.46', 0.3', 0.003'
	ADDx 6.47'
	ROTz 20
	BLOCK 0.3', 262.59', 0.0029'
	ROTz -40
	BLOCK 0.3', 262.66', 0.0029'
	DEL 5

	resol min(64, gs_resol*2)

	FOR i = 0 TO 5

		ADDz 0.004'

		POLY_ 	7,
			-sin(20)*(262.46' - i*16.4'), cos(20)*(262.46' - i*16.4')-0.3', 13,
			0, 0, 900 + 13,
			sin(20)*(262.46' - i*16.4'), cos(20)*(262.46' - i*16.4'), 3000 + 13,
			sin(20)*(262.46' - i*16.4' - 0.016'), cos(20)*(262.46' - i*16.4' - 0.016'), 13,
			0, 0, 900 + 13,
			-sin(20)*(262.46' - i*16.4' - 0.016'), cos(20)*(262.46' - i*16.4' - 0.016')-0.3', 3000 + 13,
			-sin(20)*(262.46' - i*16.4'), cos(20)*(262.46' - i*16.4')-0.3', -1

		DEL 1

	NEXT i

	resol gs_resol
	DEL 2
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! place_shotput
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_shotput":

	!!!----base
	HOTSPOT spx, 0,0, 	unID , spy, 1+128   : unID = unID + 1
	HOTSPOT spx, -1,0, 	unID , spy, 3  		: unID = unID + 1
	HOTSPOT spx, spy,0,	unID , spy, 2  		: unID = unID + 1

	HOTSPOT 0, spy,0, 	unID , spx, 1+128   : unID = unID + 1
	HOTSPOT -1, spy,0, 	unID , spx, 3  		: unID = unID + 1
	HOTSPOT spx, spy,0,	unID , spx, 2  		: unID = unID + 1

	ADD spx,spy,0
	ROTz 250
	!resol gs_res_2

	MATERIAL base_mat
	pen base_pen

	ADDz -0.001'
	CYLIND 0.0032', 3.484'

	resol gs_resol
	MATERIAL mat4
	PRISM_ 	4, 0.065',
			0, 0, 900 + 13,
			3.503', 360, 4000 + 13,
			0, 0, 900 + 13,
			3.484', 360, 4000 + 1
	PRISM_ 	6, 0.1,
			3.484'*cos(60), 3.484'*sin(60), 13,
			0, 0, 900 + 13,
			-3.484'*cos(60), 3.484'*sin(60), 3000 + 13,
			-3.484'*cos(60), 3.484'*sin(60) + 0.984', 13,
			3.484'*cos(60), 3.484'*sin(60) + 0.984', 13,
			3.484'*cos(60), 3.484'*sin(60), -1

	material line_mat
	pen line_pen
	PRISM_ 	4, 0.005',
			0, 0, 900 + 13,
			4.816', 360, 4000 + 13,
			0, 0, 900 + 13,
			3.503', 360, 4000 + 1
	del 1

	!!!----markings
	material line_mat
	pen line_pen
	ADD 3.484', -0.082', 0
	BLOCK 2.460', 0.3', 0.005'
	material line_mat
	pen line_pen
	DEL 1
	ADD -5.944', -0.082', 0
	BLOCK 2.460', 0.3', 0.005'
	DEL 1
	ROTz 20
	BLOCK 0.3', 82.020', 0.005'
	ROTz -40
	BLOCK 0.3', 82.020', 0.005'
	DEL 2

	FOR i = 0 TO 5

		ADDz 0.0048'

		POLY_ 	7,
			-sin(20)*(65.616' - i*0.5), cos(20)*(65.616' - i*0.5), 13,
			0, 0, 900 + 13,
			sin(20)*(65.616' - i*0.5), cos(20)*(65.616' - i*0.5), 3000 + 13,
			sin(20)*(65.616' - i*0.5 - 0.164'), cos(20)*(65.616' - i*0.5 - 0.164'), 13,
			0, 0, 900 + 13,
			-sin(20)*(65.616' - i*0.5 - 0.164'), cos(20)*(65.616' - i*0.5 - 0.164'), 3000 + 13,
			-sin(20)*(65.616' - i*0.5), cos(20)*(65.616' - i*0.5), -1

		DEL 1

	NEXT i
	DEL 2

RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! place_javelinthrow
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
"place_javelinthrow":

	HOTSPOT jtx, 0,0, 	unID , jty, 1+128 	: unID = unID + 1
	HOTSPOT jtx, -1,0, 	unID , jty, 3 		: unID = unID + 1
	HOTSPOT jtx, jty,0,	unID , jty, 2 		: unID = unID + 1

	HOTSPOT 0, jty,0, 	unID ,jtx, 1+128  	: unID = unID + 1
	HOTSPOT -1, jty,0, 	unID , jtx, 3 		: unID = unID + 1
	HOTSPOT jtx, jty,0,	unID , jtx, 2 		: unID = unID + 1

	ADD jtx,jty,0

	!!!!!!!!!LANCER DU JAVELOT
	material track_mat
	pen track_pen
			TOLER 0.001
	IF apa THEN
			PRISM_ 6,0.05',
				   0,0,15,
				   0,13.22',15,
				   114.829',13.22',15,
				   88.528',2,900,
					114.829',0,3000 + 15,
				   0,0,-1

			PRISM_ 6,0.06',
					115.026'+2*tan(28.95),13.22',15,
					115.026'+2*tan(28.95),0,15,
					114.829',0,15,
					88.528',6.561',900+15,
					114.829',13.22',3000+15,
					115.026'+2*tan(28.95),13.22',-1
	else
			PRISM_ 12,0.036',
					115.026'+2*tan(28.95),13.22',15,
					115.026'+2*tan(28.95),0,15,
					114.829',0,15,
					88.528',6.561',900+15,
					114.829',13.22',3000+15,
					115.026'+2*tan(28.95),13.22',-1,

					115'+2*tan(28.95),13.20',15,
					115'+2*tan(28.95),0,15,
					114.9',0,15,
					88.528',6.561',900+15,
					114.9',13.20',3000+15,
					115'+2*tan(28.95),13.20',-1

	ENDIF

	BLOCK 0.229',13.22',0.036'
	ADDX 114.829'
	ADDY 13.22'
	material line_mat
	pen line_pen
	BLOCK 0.229',4.921',0.036'
	DEL 1
	ADDY -1.5

	BLOCK 0.229',4.921',0.036'
	DEL 2
	ADDY -0.164'
	material line_mat
	pen track_pen
	BLOCK 114.829',0.164',0.036'

	ADDY 0.164'+13.22'
	BLOCK 114.829',0.164',0.036'

	DEL 2

	material line_mat
	pen line_pen
	ADDY 6.561'-0.082'
	ADDX 88.528'+0.011'
	ROTZ 28.95*0.5
	ADDX 8+0.1
			BLOCK 301.509',0.164',0.036'
	DEL 2
	ROTZ -28.95*0.5
	ADDX 26.246'+0.328'
			BLOCK 301.509',0.164',0.036'
	DEL 2
	ROTZ -90

	resol min(64, gs_resol*2)

	FOR i = 0 TO 10

		ADDz 0.0048'

		POLY_ 	7,
			-sin(28.95*0.5)*(328.083' - i*5), cos(28.95*0.5)*(328.083' - i*5), 13,
			0, 0, 900 + 13,
			sin(28.95*0.5)*(328.083' - i*5), cos(28.95*0.5)*(328.083' - i*5), 3000 + 13,
			sin(28.95*0.5)*(328.083' - i*5 - 0.164'), cos(28.95*0.5)*(328.083' - i*5 - 0.164'), 13,
			0, 0, 900 + 13,
			-sin(28.95*0.5)*(328.083' - i*5 - 0.164'), cos(28.95*0.5)*(328.083' - i*5 - 0.164'), 3000 + 13,
			-sin(28.95*0.5)*(328.083' - i*5), cos(28.95*0.5)*(328.083' - i*5), -1

		DEL 1
	NEXT i
	DEL 3+1

	resol gs_resol

RETURN



