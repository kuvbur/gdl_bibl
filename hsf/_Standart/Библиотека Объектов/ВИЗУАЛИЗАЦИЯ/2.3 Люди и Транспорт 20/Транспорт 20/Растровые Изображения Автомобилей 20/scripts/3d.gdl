
if gs_detlevel_3D_m = 0 then end

DEFINE MATERIAL "pictmat" 0,
		1,1,1,
		0.5, 0.8, 0, 0,
		0,
		0,
		0,0,0,
		0,0,0,
		0

MATERIAL "pictmat"
SHADOW ON

IF not(gs_shadow) THEN SHADOW OFF

pen gs_cont_pen

! --- rotation to camera ---

rotx 90
rotz gs_rot

rotToEye = -SYMB_ROTANGLE

dx= GLOB_EYEPOS_X - GLOB_TARGPOS_X

if abs(dx)<EPS then
	rotToEye = rotToEye + 90
else
	rotToEye = rotToEye + atn((GLOB_EYEPOS_Y - GLOB_TARGPOS_Y)/dx)
endif
if dx<0 then
	rotToEye = 180+rotToEye
endif
if SYMB_MIRRORED then
	rotToEye = -rotToEye
endif

roty rotToEye + 90

! --- vehicle length ---

if gs_vehicle_type_m <> 6 then
	halfLength = A/2
else
	halfLength = B/2
endif

! --- hotspots ---

hotspot  0,          0,0
hotspot -halfLength, 0,0
hotspot  halfLength, 0,0
hotspot -halfLength, zzyzx,0
hotspot  halfLength, zzyzx,0

! --- car picture ---

addx -halfLength

if gs_vehicle_type_m = 0 then
	if gs_picture_name <> "" then
		picture gs_picture_name, A, zzyzx, 1
	else
		picture 1,               A, zzyzx, 1
	endif
else
	if gs_vehicle_type_m = 6 then
		picture 6,                 B, zzyzx, 1
	else
		picture gs_vehicle_type_m, A, zzyzx, 1
	endif
endif
