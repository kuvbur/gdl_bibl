
call "FM_types" parameters all

dim stVehicleTypes[9]
	stVehicleTypes[1] = `Тип 1`
	stVehicleTypes[2] = `Тип 2`
	stVehicleTypes[3] = `Тип 3`
	stVehicleTypes[4] = `Тип 4`
	stVehicleTypes[5] = `Тип 5`
	stVehicleTypes[6] = `Тип 6`
	stVehicleTypes[7] = `Тип 7`
	stVehicleTypes[8] = `Тип 8`
	stVehicleTypes[9] = `Специальные`

values "gs_vehicle_type" stVehicleTypes
values "gs_rot" range[-45,45]
values "gs_detlevel_3D"  `Детальный`,`Откл.`

if gs_cont_pen<=0 then parameters gs_cont_pen=4
if gs_fill_pen<=0 then parameters gs_fill_pen=91

if GLOB_MODPAR_NAME = "gs_vehicle_type" then
	gs_vehicle_type_m = 0	! custom
	for iType = 1 to 8
		if stVehicleTypes[iType] = gs_vehicle_type then
			gs_vehicle_type_m = iType
		endif
	next iType
	parameters gs_vehicle_type_m = gs_vehicle_type_m
else
	if gs_vehicle_type_m = 0 then	! custom
		gs_vehicle_type = stVehicleTypes[9]
	else
		gs_vehicle_type = stVehicleTypes[gs_vehicle_type_m]
	endif
	parameters gs_vehicle_type = gs_vehicle_type
endif

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 0
	if gs_detlevel_3D = `Детальный` then
		gs_detlevel_3D_m = 2
	endif
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	if gs_detlevel_3D_m = 0 then
		gs_detlevel_3D = `Откл.`
	else
		gs_detlevel_3D = `Детальный`
	endif
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_vehicle_type_m > 0 then
	dim aSizes[8]
	dim bSizes[8]
	dim zSizes[8]
	aSizes[1] = 4.545 : bSizes[1] = 1.82 : zSizes[1] = 1.696
	aSizes[2] = 4.041 : bSizes[2] = 1.80 : zSizes[2] = 1.458
	aSizes[3] = 5.043 : bSizes[3] = 1.90 : zSizes[3] = 1.481
	aSizes[4] = 4.660 : bSizes[4] = 1.95 : zSizes[4] = 1.954
	aSizes[5] = 4.419 : bSizes[5] = 1.80 : zSizes[5] = 1.544
	aSizes[6] = 4.100 : bSizes[6] = 1.80 : zSizes[6] = 1.402
	aSizes[7] = 4.813 : bSizes[7] = 1.82 : zSizes[7] = 1.450
	aSizes[8] = 5.118 : bSizes[8] = 1.92 : zSizes[8] = 1.676

	parameters A     = aSizes[gs_vehicle_type_m]
	parameters B     = bSizes[gs_vehicle_type_m]
	parameters zzyzx = zSizes[gs_vehicle_type_m]

	lock "a"
	lock "b"
	lock "zzyzx"
	hideparameter "gs_picture_name"
endif

! =============================================================================
! UI
! =============================================================================

values "gs_ui_current_page" 1

