
if (abs(SYMB_ROTANGLE)<EPS) = SYMB_MIRRORED then
	jamb1=WIDO_RIGHT_JAMB
	jamb2=WIDO_LEFT_JAMB
else
	jamb2=WIDO_RIGHT_JAMB
	jamb1=WIDO_LEFT_JAMB
endif



pen gs_pen_2D

lod2D = 0										! Don't draw anything

gs_panelLineType = 1
if iTypeDemolitionFill = RENOVATION_FILL_DEMOLISHED then		! Demolition
	line_type gs_Demolition_linetype
	gs_panelLineType = gs_Demolition_linetype
else
	line_type 1
endif



a0=a
a_original=a

a=a + WIDO_RIGHT_JAMB*gs_lich + WIDO_LEFT_JAMB*gs_lich

add2 (-WIDO_LEFT_JAMB*gs_lich+WIDO_RIGHT_JAMB*gs_lich) / 2, 0


! =============================================================================
! GS Window Opening Functions
! =============================================================================

call "GS Terrace Door Functions",
	parameters all	gs_stCavityClosure		= gs_stCavityClosure,
					gs_wcont_line_out		= gs_wcont_line_out,
					gs_wcont_line_in		= gs_wcont_line_in,
					gs_wallhole_fill		= 0,
	returned_parameters thkSkinTurnInLeft,	! Thickness of turned plaster skins at the Inside Face
						thkSkinTurnInRight,
						thkSkinTurnOutLeft,	! Thickness of turned plaster skins at the Outside Face
						thkSkinTurnOutRight,
						penSkinContourIn,	! Skin Contour Pen Opp. Side
						penSkinContourOut,	! Skin Contour Pen Ref. Side
						ltSkinContourIn,	! Skin Contour Line Type Opp. Side
						ltSkinContourOut,	! Skin Contour Line Type Ref. Side

						addPrmX, addPrmY,
						iLoD2D,
						gs_opLinePen,
						gs_iSwingType,
						gs_swingLineType,
						bShowAddHotspots,
						bShowUnitHotspots

add2 addPrmX, addPrmY

if iLoD2D = SYMBW_OFF then end
if iLoD2D = SYMBW_S_1LINE then				lod2D = DET2D_FS_SIMPLE_ONE 		! Simple - One Line --> INT 200
if iLoD2D = SYMBW_S_CENTERLINE then			lod2D = DET2D_FS_SIMPLE_ONE 		! Simple - Centerline --> SWE 200
if iLoD2D = SYMBW_S_2LINE_ACC then			lod2D = DET2D_FS_SIMPLE_TWO 		! Simple - Two Lines
if iLoD2D = SYMBW_S_2LINE then				lod2D = DET2D_FS_SIMPLE_CHI			! Chinese Simple
if iLoD2D = SYMBW_M_2LINE_GLASS then		lod2D = DET2D_FS_MIDDLE 			! Middle - without acc
if iLoD2D = SYMBW_M_2LINE_NOGLASS then		lod2D = DET2D_FS_SIMPLE_TWO 		! Middle - with acc --> INT 100
if iLoD2D = SYMBW_M_FRAMESASH then			lod2D = DET2D_FS_DETAILED			! NLD 100
if iLoD2D = SYMBW_D_SGLASS then				lod2D = DET2D_FS_DETAILED 			! Detailed --> INT 50, DAN 20
if iLoD2D = SYMBW_D_REBATEONGLASS then		lod2D = DET2D_FS_DETAILED 			! Detailed --> NLD 50
if iLoD2D = SYMBW_D_SGLASS then				lod2D = DET2D_FS_DETAILED 			! Detailed --> INT 50, DAN 20
if iLoD2D = SYMBW_D_2GLASS then				lod2D = DET2D_FS_DETAILED 			! Detailed --> NOR 50
if iLoD2D = SYMBW_D_2LINE_HS then			lod2D = DET2D_FS_DETAILED_CHI		! Chinese Detailed

! =============================================================================
! Demolition / New construction
! =============================================================================

if iTypeDemolitionFill = RENOVATION_FILL_DEMOLISHED then gs_swingLineType = gs_Demolition_linetype		! Demolition
if iTypeDemolitionFill = RENOVATION_FILL_NEW & gs_bShowNewOnCC then end		! New


if iTypeDemolitionFill = RENOVATION_FILL_DEMOLISHED then
	gs_wallhole_fill = gs_Demolition_fill
	gs_wallhole_pen_fg = gs_Demolition_pen_fg
	gs_wallhole_pen_bg = gs_Demolition_pen_bg
endif

if	iTypeDemolitionFill = RENOVATION_FILL_NEW |\
	iTypeDemolitionFill = RENOVATION_FILL_NEW_FULL then 

	gs_wallhole_fill = gs_New_fill
	gs_wallhole_pen_fg = gs_New_pen_fg
	gs_wallhole_pen_bg = gs_New_pen_bg
endif


bListView = (GLOB_CONTEXT = 6 | GLOB_CONTEXT = 46)

mul2 1,-1
add2 0,WIDO_FRAME_THICKNESS

!******************************* Hotspots on Unit Size

if bShowUnitHotspots then
	unID = 100
	hotspot2 -A/2 + gs_tolerance,  0,            unID
	hotspot2  A/2 - gs_tolerance,  0,            unID + 1
	hotspot2 -A/2 + gs_tolerance, -gs_frame_thk, unID + 2
	hotspot2  A/2 - gs_tolerance, -gs_frame_thk, unID + 3
endif

!******************************* Hotspots on Unit Size END


!******************************* Hotspots on Sidelight

if lod2D > DET2D_FS_SIMPLE_ONE then
	if not(symm) then
		hotspot2  -A/2,       0, 1300, bf1, 1+128
		hotspot2  -A,         0, 1301, bf1, 3
		hotspot2  -A/2 + bf1, 0, 1302, bf1, 2
	else
		if bShowAddHotspots then hotspot2  -A/2 + bf1, 0, 1302
	endif
	if bShowAddHotspots then hotspot2  -A/2 + bf1, -gs_frame_thk, 1303
endif



!******************************* Hotspots on Sidelight END

if lod2D <> DET2D_FS_SIMPLE_CHI & lod2D <> DET2D_FS_DETAILED_CHI then					!!!Not Chinese

	!******************** Sonnenschutz ***********************************

	sym2=1

	if t_sonn=1 then

		add2 addPrmX, addPrmY

		if not(WIDO_REVEAL_SIDE) then
			add2 0, -gs_frame_thk
			mul2 1, -1
		endif

		if gs_lich=1 then
			a1=a0
			add2 -(WIDO_RIGHT_JAMB-WIDO_LEFT_JAMB)/2,0
		else
			a1=	a-(WIDO_RIGHT_JAMB+WIDO_LEFT_JAMB)
			add2 -(WIDO_RIGHT_JAMB-WIDO_LEFT_JAMB)/2,0
		endif

		add2 0, WIDO_SILL+0.02

			call "schiebejalousie_3fl_m" parameters a		= a1,
										lod2D				= lod2D,
										sym2				= sym2,
										ltyp				= ltyp_lam,
										loc_type			= loc_type,
										rab_s				= rab_s,
										ras_s				= ras_s,
										gs_sc_pen			= gs_sash_pen_cont,
										iLayoutType			= 1,
										offladen			= offladen,
										bedec				= bedec,
										sc					= sck,
										bShowAddHotspots	= bShowAddHotspots


		del 2
		if not(WIDO_REVEAL_SIDE) then del 2

		del 1

	endif



	!****************** field width values when symmetric

	if symm then bf1=a-(a+vbm-vbr+vb+2*vbl)/3


	!********** modification of "Versetzt in Breite"

	vb_original=vb

	vbm=(stb_m-vb*2)/2
	vbl=stb_l-vb
	vbr=stb_r-vb

	vb_l=stb_l-vb
	vb_r=stb_r-vb

	vb=gs_frame_width-vb

	!********** main fields

	aleft=bf1-vbl-vbm
	aright=a-bf1-vbr-vbm


	!**********************************
	!**********************************
	!************************** Fenster


	if spb<=0.001 or sps<=0.001 then sv=0

	if lod2D = DET2D_FS_SIMPLE_ONE then		goto 1100
	if lod2D = DET2D_FS_SIMPLE_TWO | lod2D = DET2D_FS_MIDDLE then		goto 1050
	if lod2D = DET2D_FS_DETAILED then		goto 1020


	!********************* scale 1:500

	1500:

	end

	!********************* scale 1:200<

	1200:

	end



	!********************* scale 1:200

	1100:

	pen gs_frame_pen_cont

	if (abs(SYMB_ROTANGLE)<EPS) = SYMB_MIRRORED then
		j1=WIDO_RIGHT_JAMB
		j2=WIDO_LEFT_JAMB
	else
		j1=WIDO_LEFT_JAMB
		j2=WIDO_RIGHT_JAMB
	endif

	if not(WIDO_REVEAL_SIDE) then
		add2 0, -gs_frame_thk
	endif


	line2 -a/2+j1, 0, a/2-j2, 0


	line_type gs_swingLineType
	aop= -a/2+stb_r
	hop=0
	ro=(-a/2+stb_r-(a/2-stb_r-(a-bf1)))*(-0.5)
	opp=gs_ow22
	gosub 4022
	if gs_ow22 > EPS then gosub 3331

	line_type gs_swingLineType
	aop=a/2-stb_r-(a-bf1)
	hop=0
	ro=(-a/2+stb_r-(a/2-stb_r-(a-bf1)))*(-0.5)
	opp=gs_ow22
	gosub 4002
	if gs_ow22 > EPS then gosub 3332

	line_type gs_swingLineType

	hop=0
	opp=gs_ow21

	if loc_op2=3 or loc_op2=5 then
		aop=-a/2+bf1+stb_m/2
		ro=a/2-br_a2-(-a/2+bf1+stb_m/2)
		gosub 4011
		if gs_ow21 > EPS then gosub 3331
	endif

	if loc_op2=2 or loc_op2=4 then
		aop=a/2-stb_r
		ro=a-bf1-stb_m/2-stb_r
		gosub 4001
		if gs_ow21 > EPS then gosub 3332
	endif

	line_type SYMB_LINETYPE


	if not(WIDO_REVEAL_SIDE) then del 1



	gosub 2000


	end


	!********************* scale 1:100


	1050:


	gosub 2000

	pen gs_frame_pen_cont
	fill gs_fillFr

	poly2_b 4, 2+4+64,gs_penFr_fg,gs_penFr_bg,
		-a/2,0,0,
		-a/2,-gs_frame_thk,0,
		a/2,-gs_frame_thk,0,
		a/2,0,0

	line2 -a/2,0,-a/2,-gs_frame_thk
	line2 a/2,0,a/2,-gs_frame_thk

	line2 -a/2,0,a/2,0
	line2 -a/2,-gs_frame_thk,a/2,-gs_frame_thk

	line2  a/2-br_a2,0,a/2-br_a2,-gs_frame_thk
	line2 -a/2+bf1+stb_m/2, 0, -a/2+bf1+stb_m/2, -gs_frame_thk

	if bShowAddHotspots then
		for i=1 to 2
			hotspot2 -a/2+br_a1,       0, 1330 + (i - 1) * 2	! left frame
			hotspot2  a/2-br_a2,       0, 1331 + (i - 1) * 2	! right frame

			hotspot2 -a/2+bf1-stb_m/2, 0, 1310 + (i - 1) * 2	! middle frame
			hotspot2 -a/2+bf1+stb_m/2, 0, 1311 + (i - 1) * 2	! middle frame

			add2 0, -gs_frame_thk
		next i
		del 2
	endif


	line_type gs_swingLineType
	aop=-a/2+br_a1
	hop=-gs_frame_thk + gs_frame_thk / 2 * (lod2D = DET2D_FS_MIDDLE)
	ro=(-a/2+br_a1-(-a/2+bf1-stb_m/2))*(-0.5)
	opp=gs_ow22
	gosub 4022
	if gs_ow22 > EPS then gosub 3331

	line_type gs_swingLineType
	aop=-a/2+bf1-stb_m/2
	hop=-gs_frame_thk + gs_frame_thk / 2 * (lod2D = DET2D_FS_MIDDLE)
	ro=(-a/2+br_a1-(-a/2+bf1-stb_m/2))*(-0.5)
	opp=gs_ow22
	gosub 4002
	if gs_ow22 > EPS then gosub 3332


	line_type SYMB_LINETYPE

	line2 -a/2+br_a1,0,-a/2+br_a1,-gs_frame_thk
	line2 -a/2+bf1-stb_m/2, 0, -a/2+bf1-stb_m/2, -gs_frame_thk

	ax=(bf1-stb_l-stb_m/2-4*rab)/2

	line2 -a/2+br_a1+rab+ax, 0, -a/2+br_a1+rab+ax, -gs_frame_thk
	line2 -a/2+br_a1+rab+ax+rab, 0, -a/2+br_a1+rab+ax+rab, -gs_frame_thk
	line2 -a/2+br_a1+rab+ax+rab+rab, 0, -a/2+br_a1+rab+ax+rab+rab, -gs_frame_thk

	if lod2D = DET2D_FS_MIDDLE then
		if abs(gs_ow22) < EPS | bMainWindowFixIn2D then line2 -a/2+br_a1,-gs_frame_thk/2, -a/2+br_a1+rab+ax, -gs_frame_thk/2					! Window Glass symbol
		if abs(gs_ow22) < EPS | bMainWindowFixIn2D then line2 -a/2+br_a1+rab+ax+rab+rab, -gs_frame_thk/2, -a/2+bf1-stb_m/2, -gs_frame_thk/2		! Window Glass symbol
		if abs(gs_ow21) < EPS | bMainDoorFixIn2D then line2 -a/2+bf1+stb_m/2,-gs_frame_thk/2, a/2-br_a2,-gs_frame_thk/2						! Door Glass symbol
	endif

	line_type gs_swingLineType

	if loc_op2=3 or loc_op2=5 then
		aop=-a/2+bf1+stb_m/2
		hop=-gs_frame_thk + gs_frame_thk / 2 * (lod2D = DET2D_FS_MIDDLE)
		ro=a/2-br_a2-(-a/2+bf1+stb_m/2)
		opp=gs_ow21
		gosub 4011
		if gs_ow21 > EPS then gosub 3331
	endif

	if loc_op2=2 or loc_op2=4 then
		aop=a/2-stb_r
		hop=-gs_frame_thk + gs_frame_thk / 2 * (lod2D = DET2D_FS_MIDDLE)
		ro=a-bf1-stb_m/2-stb_r
		opp=gs_ow21
		gosub 4001
		if gs_ow21 > EPS then gosub 3332
	endif

	line_type SYMB_LINETYPE


	end



	!********************* scale 1:20/50


	1020:


	gosub 2000

	pen gs_frame_pen_cont

	if bListView then
		line2 -a/2,0,-a/2,-gs_frame_thk
		line2  a/2,0, a/2,-gs_frame_thk
	endif

	line2 -a/2,0,a/2,0

	if bShowAddHotspots then
		hotspot2 -a/2+stb_l,       0,   1330	! left frame additional point (outer)
		hotspot2  a/2-stb_r,       0,   1331	! right frame additional point (outer)
		hotspot2 -a/2+bf1-stb_m/2, 0,   1310	! middle frame left corner (outer)
		hotspot2 -a/2+bf1+stb_m/2, 0,   1311	! middle frame right corner (outer)
	endif

	fill gs_fillFr
	poly2_b 6, 2+4+64,gs_penFr_fg,gs_penFr_bg,
		-a/2,-gs_frame_thk,0,
		-a/2,0,0,
		-a/2+stb_l,0,0,
		-a/2+stb_l,-vt,0,
		-a/2+vbl,-vt,0,
		-a/2+vbl,-gs_frame_thk,0

	poly2_b 8, 2+4+64,gs_penFr_fg,gs_penFr_bg,
		-a/2+bf1-stb_m/2-vbl+stb_l, -gs_frame_thk,0,
		-a/2+bf1-stb_m/2-vbl+stb_l, -vt,0,
		-a/2+bf1-stb_m/2, -vt,0,
		-a/2+bf1-stb_m/2, 0,0,
		-a/2+bf1+stb_m/2, 0,0,
		-a/2+bf1+stb_m/2, -vt,0,
		-a/2+bf1+stb_m/2+vbr-stb_r, -vt,0,
		-a/2+bf1+stb_m/2+vbr-stb_r, -gs_frame_thk,0

	fill gs_fillShCover
	poly2_b 4, 2+4+64,gs_penShCov_fg,gs_penShCov_bg,
		-a/2+stb_l,0,0,
		-a/2+stb_l,-vt,0,
		-a/2+bf1-stb_m/2, -vt,0,
		-a/2+bf1-stb_m/2, 0,0

	line2 -a/2,-gs_frame_thk,-a/2+vbl,-gs_frame_thk
	line2 -a/2+stb_l,0,-a/2+stb_l,-vt
	line2 -a/2+bf1-stb_m/2, 0, -a/2+bf1-stb_m/2, -vt

	fill gs_fillFr
	poly2_b 6, 2+4+64,gs_penFr_fg,gs_penFr_bg,
		a/2,-gs_frame_thk,0,
		a/2,0,0,
		a/2-stb_r,0,0,
		a/2-stb_r,-vt,0,
		a/2-vbr,-vt,0,
		a/2-vbr,-gs_frame_thk,0

	fill gs_fillShCover
	poly2_b 4, 2+4+64,gs_penShCov_fg,gs_penShCov_bg,
		a/2-stb_r,0,0,
		a/2-stb_r,-vt,0,
		-a/2+bf1+stb_m/2, -vt,0,
		-a/2+bf1+stb_m/2, 0,0

	line2 a/2,-gs_frame_thk,a/2-vbr,-gs_frame_thk
	line2 a/2-stb_r,0,a/2-stb_r,-vt
	line2 -a/2+bf1+stb_m/2, 0, -a/2+bf1+stb_m/2, -vt

	!*** left sashes

	ax=(bf1-vbl-vbm/2-4*rab)/2

	pen gs_sash_pen_cont

	fill gs_fillSash
	poly2_b 4, 1+2+4+64,gs_penSash_fg,gs_penSash_bg,
		-a/2+vbl,-vt,1,
		-a/2+vbl,-vt-ras,1,
		-a/2+vbl+rab,-vt-ras,1,
		-a/2+vbl+rab,-vt,1

	poly2_b 4, 1+2+4+64,gs_penSash_fg,gs_penSash_bg,
		-a/2+vbl+rab+ax,-vt,1,
		-a/2+vbl+rab+ax,-vt-ras,1,
		-a/2+vbl+rab*2+ax,-vt-ras,1,
		-a/2+vbl+rab*2+ax,-vt,1


	poly2_b 4, 1+2+4+64,gs_penSash_fg,gs_penSash_bg,
		-a/2+bf1-vbm-rab,-vt,1,
		-a/2+bf1-vbm-rab,-vt-ras,1,
		-a/2+bf1-vbm,-vt-ras,1,
		-a/2+bf1-vbm,-vt,1

	poly2_b 4, 1+2+4+64,gs_penSash_fg,gs_penSash_bg,
		-a/2+vbl+rab*2+ax,-vt,1,
		-a/2+vbl+rab*2+ax,-vt-ras,1,
		-a/2+vbl+rab*3+ax,-vt-ras,1,
		-a/2+vbl+rab*3+ax,-vt,1

	fill gs_fillShCover
	poly2_b 4, 1+2+4+64,gs_penShCov_fg,gs_penShCov_bg,
		-a/2+vbl+rab,-vt,1,
		-a/2+vbl+rab,-vt-ras,1,
		-a/2+vbl+rab+ax, -vt-ras,1,
		-a/2+vbl+rab+ax, -vt,1

	poly2_b 4, 1+2+4+64,gs_penShCov_fg,gs_penShCov_bg,
		-a/2+vbl+rab*3+ax,-vt,1,
		-a/2+vbl+rab*3+ax,-vt-ras,1,
		-a/2+bf1-vbm-rab, -vt-ras,1,
		-a/2+bf1-vbm-rab, -vt,1


!	rect2 -a/2+vbl,-vt, -a/2+bf1-vbm,-vt-ras
	line2 -a/2+vbl+rab,-vt,-a/2+vbl+rab,-vt-ras
	line2 -a/2+bf1-vbm-rab, -vt, -a/2+bf1-vbm-rab, -vt-ras

	line2 -a/2+vbl+rab+ax, -vt, -a/2+vbl+rab+ax, -vt-ras
	line2 -a/2+vbl+rab*2+ax, -vt, -a/2+vbl+rab*2+ax, -vt-ras
	line2 -a/2+vbl+rab*3+ax, -vt, -a/2+vbl+rab*3+ax, -vt-ras

	line2 -a/2+vbl+rab+ax, -vt-ras/2, -a/2+vbl+rab, -vt-ras/2
	line2 -a/2+vbl+rab+ax+2*rab, -vt-ras/2, -a/2+bf1-vbm-rab, -vt-ras/2

	if bShowAddHotspots then	! Sash hotspots
		hotspot2 -a/2+vbl,     -vt,     2000
		hotspot2 -a/2+vbl,     -vt-ras, 2002
		hotspot2 -a/2+bf1-vbm, -vt,     2001
		hotspot2 -a/2+bf1-vbm, -vt-ras, 2003
	endif

	line_type gs_swingLineType
	aop=-a/2+vbl
	hop=-vt-ras
	ro=(-a/2+vbl-(-a/2+bf1-vbm))*(-0.5)
	opp=gs_ow22
	gosub 4022
	if gs_ow22 > EPS then gosub 3331

	line_type gs_swingLineType
	aop=-a/2+bf1-vbm
	hop=-vt-ras
	ro=(-a/2+vbl-(-a/2+bf1-vbm))*(-0.5)
	opp=gs_ow22
	gosub 4002
	if gs_ow22 > EPS then gosub 3332

	line_type gs_panelLineType

	!*** right sash (door)

	pen gs_sash_pen_cont


	!*** door NOT fix

	fill gs_fillSash
	poly2_b 4, 1+2+4+64,gs_penSash_fg,gs_penSash_bg,
		a/2-vbr,-vt,1,
		a/2-vbr,-vt-ras,1,
		a/2-vbr-rab,-vt-ras,1,
		a/2-vbr-rab,-vt,1

	poly2_b 4, 1+2+4+64,gs_penSash_fg,gs_penSash_bg,
		-a/2+bf1+vbm+rab,-vt,1,
		-a/2+bf1+vbm+rab,-vt-ras,1,
		-a/2+bf1+vbm,-vt-ras,1,
		-a/2+bf1+vbm,-vt,1

	fill gs_fillShCover
	poly2_b 4, 1+2+4+64,gs_penShCov_fg,gs_penShCov_bg,
		-a/2+bf1+vbm+rab,-vt,1,
		-a/2+bf1+vbm+rab,-vt-ras,1,
		a/2-vbr-rab, -vt-ras,1,
		a/2-vbr-rab, -vt,1


!	rect2 a/2-vbr,-vt, -a/2+bf1+vbm,-vt-ras
	line2  a/2-vbr-rab,-vt, a/2-vbr-rab,-vt-ras
	line2 -a/2+bf1+vbm+rab, -vt,  -a/2+bf1+vbm+rab, -vt-ras
	line2 -a/2+bf1+vbm+rab, -vt-ras/2, a/2-vbr-rab, -vt-ras/2

	if bShowAddHotspots then	! Leaf hotspots
		hotspot2 -a/2+bf1+vbm, -vt,     300
		hotspot2  a/2-vbr,     -vt,     301
		hotspot2 -a/2+bf1+vbm, -vt-ras, 302
		hotspot2  a/2-vbr,     -vt-ras, 303
	endif


	if not(bMainFieldFix) then
		pen gs_frame_pen_cont
		line2 -a/2+bf1-vbm, -gs_frame_thk, -a/2+bf1+vbm, -gs_frame_thk
	else
		if bMainFieldFix then
			line2 -a/2+bf1-stb_m/2, -gs_frame_thk, -a/2+bf1+vbm, -gs_frame_thk
		else
			line2 -a/2+bf1-vbm, -gs_frame_thk, -a/2+bf1+stb_m/2, -gs_frame_thk
		endif
	endif

	line_type gs_swingLineType

	if loc_op2=2 or loc_op2=4 then
		aop=a/2-stb_r+vb_original
		hop=-vt-ras
		ro=a-bf1-stb_m/2-stb_r+2*vb_original
		opp=gs_ow21
		gosub 4001
		if gs_ow21 > EPS then gosub 3332
	endif

	if loc_op2=3 or loc_op2=5 then
		aop=-a/2+bf1+vbm
		hop=-vt-ras
		ro=a/2-vbr-(-a/2+bf1+vbm)
		opp=gs_ow21
		gosub 4011
		if gs_ow21 > EPS then gosub 3331
	endif

	line_type SYMB_LINETYPE

else						!!!Chinese

	! ==============================================================================
	! Symbol at Middle
	! ==============================================================================

	pen gs_frame_pen_cont

	xxx = (bf1- 1.5*gs_frame_width + 2 *gs_vb )/2
	add2 -A/2 + (gs_frame_width - gs_vb + xxx/2)  -  (gs_frame_width - gs_vb)/2,  0

		!!! Window

		gs_optype_m = SASH_FIXED
		if iWindowOpeningType = SASH_SIDE_HUNG | iWindowOpeningType = SASH_TILT_TURN then
			gs_optype_m = SASH_SIDE_HUNG_M
		endif
		if iWindowOpeningType = SASH_SIDE_HUNG_M | iWindowOpeningType = SASH_TILT_TURN_M then
			gs_optype_m = SASH_SIDE_HUNG
		endif

		call "BasicWindowPanelStraight",
			parameters	A							= xxx + (gs_frame_width - gs_vb),
						gs_optype_m					= gs_optype_m,
						gs_frame_thk				= gs_frame_thk,
						gs_frame_width_left			= 0,
						gs_frame_width_right		= 0.001,
						gs_frame_right_endline		= 0,
						gs_frame_left_endline		= 0,
						gs_frame_right_endline_comp	= 0,
						gs_frame_left_endline_comp	= 2,
						gs_frame_cut_width			= gs_vb,
						gs_frame_cut_depth			= 0,
						gs_bFrameLeft				= 1,
						gs_bFrameRight				= 0,
						gs_frame_ovhg_left_out		= gs_frame_ovhg_left_out,
						gs_frame_ovhg_right_out		= 0,
						gs_frame_ovhg_left_in		= gs_frame_ovhg_left_in,
						gs_frame_ovhg_right_in		= 0,
						gs_sash_thk					= gs_sash_thk,
						gs_sash_width				= gs_sash_width,
						iLoD2D						= iLoD2D,
						gs_pen_2D 					= gs_pen_2D,
						gs_frame_pen_cont			= gs_frame_pen_cont,
						gs_fillFr					= gs_fillFr,
						gs_penFr_fg					= gs_penFr_fg,
						gs_penFr_bg					= gs_penFr_bg,
						gs_fillSh					= gs_fillSash,
						gs_penSh_fg					= gs_penSash_fg,
						gs_penSh_bg					= gs_penSash_bg,
						gs_fillShCover				= gs_fillShCover,
						gs_penShCov_fg       		= gs_penShCov_fg,
						gs_penShCov_bg      		= gs_penShCov_bg,
						gs_glass_thk				= gs_glass_thk,
						!!! for CHI symbol
						ac_cw_function				= ac_cw_function,
						ac_corner_window			= ac_corner_window,
						ac_corner_angle				= ac_corner_angle,
						ac_diff_con_wall_thk		= ac_diff_con_wall_thk,
						ac_con_wall_thk				= ac_con_wall_thk,
						gs_open_2D	 				= gs_ow22,
						gs_swingLineType 			= gs_swingLineType,
						sh_UnID						= 0,
						gs_opLinePen				= gs_opLinePen
	del 1

	add2 -A/2 + bf1 -  (gs_frame_width/2 - gs_vb + xxx/2)  +  (gs_frame_width/2 - gs_vb)/2,  0

	gs_optype_m = SASH_FIXED
		if iWindowOpeningType = SASH_SIDE_HUNG | iWindowOpeningType = SASH_TILT_TURN then
			gs_optype_m = SASH_SIDE_HUNG
		endif
		if iWindowOpeningType = SASH_SIDE_HUNG_M | iWindowOpeningType = SASH_TILT_TURN_M then
			gs_optype_m = SASH_SIDE_HUNG_M
		endif

		call "BasicWindowPanelStraight",
			parameters	A							= xxx + (gs_frame_width/2 - gs_vb),
						gs_optype_m					= gs_optype_m,
						gs_frame_thk				= gs_frame_thk,
						gs_frame_width_left			= 0.001,
						gs_frame_width_right		= gs_frame_width/2 - gs_vb,
						gs_frame_right_endline		= 0,
						gs_frame_left_endline		= 0,
						gs_frame_right_endline_comp	= 0,
						gs_frame_left_endline_comp	= 0,
						gs_frame_cut_width			= gs_vb,
						gs_frame_cut_depth			= 0,
						gs_bFrameLeft				= 1,
						gs_bFrameRight				= 0,
						gs_frame_ovhg_left_out		= 0,
						gs_frame_ovhg_right_out		= 0,
						gs_frame_ovhg_left_in		= 0,
						gs_frame_ovhg_right_in		= 0,
						gs_sash_thk					= gs_sash_thk,
						gs_sash_width				= gs_sash_width,
						iLoD2D						= iLoD2D,
						gs_pen_2D 					= gs_pen_2D,
						gs_frame_pen_cont			= gs_frame_pen_cont,
						gs_fillFr					= gs_fillFr,
						gs_penFr_fg					= gs_penFr_fg,
						gs_penFr_bg					= gs_penFr_bg,
						gs_fillSh					= gs_fillSash,
						gs_penSh_fg					= gs_penSash_fg,
						gs_penSh_bg					= gs_penSash_bg,
						gs_fillShCover				= gs_fillShCover,
						gs_penShCov_fg       		= gs_penShCov_fg,
						gs_penShCov_bg      		= gs_penShCov_bg,
						gs_glass_thk				= gs_glass_thk,
						!!! for CHI symbol
						ac_cw_function				= ac_cw_function,
						ac_corner_window			= ac_corner_window,
						ac_corner_angle				= ac_corner_angle,
						ac_diff_con_wall_thk		= ac_diff_con_wall_thk,
						ac_con_wall_thk				= ac_con_wall_thk,
						gs_open_2D	 				= gs_ow22,
						gs_swingLineType			= gs_swingLineType,
						sh_UnID						= 2000,
						gs_opLinePen				= gs_opLinePen
	del 1

	add2 A/2-bf1/4, SymbolMid_shiftDy


	!!! Door

	gs_optype_m = SASH_FIXED
	if iDoorOpeningType = SASH_SIDE_HUNG | iDoorOpeningType = SASH_TILT_TURN then gs_optype_m = SASH_SIDE_HUNG
	if iDoorOpeningType = SASH_SIDE_HUNG_M | iDoorOpeningType = SASH_TILT_TURN_M then gs_optype_m = SASH_SIDE_HUNG_M

		call "BasicWindowPanelStraight",
			parameters	A							= A - bf1,
						gs_optype_m					= gs_optype_m,
						gs_frame_thk				= gs_frame_thk,
						gs_frame_width_left			= gs_frame_width/2 - gs_vb,
						gs_frame_width_right		= gs_frame_width - gs_vb,
						gs_frame_right_endline		= 0,
						gs_frame_left_endline		= 0,
						gs_frame_right_endline_comp	= 2,
						gs_frame_left_endline_comp	= 0,
						gs_frame_cut_width			= gs_vb,
						gs_frame_cut_depth			= 0,
						gs_bFrameLeft				= 1,
						gs_bFrameRight				= 1,
						gs_frame_ovhg_left_out		= 0,
						gs_frame_ovhg_right_out		= gs_frame_ovhg_right_out,
						gs_frame_ovhg_left_in		= 0,
						gs_frame_ovhg_right_in		= gs_frame_ovhg_right_in,
						gs_sash_thk					= gs_sash_thk,
						gs_sash_width				= gs_sash_width,
						iLoD2D						= iLoD2D,
						gs_pen_2D 					= gs_pen_2D,
						gs_frame_pen_cont			= gs_frame_pen_cont,
						gs_fillFr					= gs_fillFr,
						gs_penFr_fg					= gs_penFr_fg,
						gs_penFr_bg					= gs_penFr_bg,
						gs_fillSh					= gs_fillSash,
						gs_penSh_fg					= gs_penSash_fg,
						gs_penSh_bg					= gs_penSash_bg,
						gs_fillShCover				= gs_fillShCover,
						gs_penShCov_fg       		= gs_penShCov_fg,
						gs_penShCov_bg      		= gs_penShCov_bg,
						gs_glass_thk				= gs_glass_thk,
						!!! for CHI symbol
						ac_cw_function				= ac_cw_function,
						ac_corner_window			= ac_corner_window,
						ac_corner_angle				= ac_corner_angle,
						ac_diff_con_wall_thk		= ac_diff_con_wall_thk,
						ac_con_wall_thk				= ac_con_wall_thk,
						gs_open_2D	 				= gs_ow21,
						gs_swingLineType			= gs_swingLineType,
						sh_UnID						= 1000,
						gs_opLinePen				= gs_opLinePen

endif

end


2000:


!************************** wall edge lines, sill, board **************************
!**********************************************************************************
mul2 1,-1
add2 0,WIDO_FRAME_THICKNESS

add2 -addPrmX, -addPrmY

call "terracedoor_wall_lines" parameters all nWindows = 2,
		loc_op				= loc_op,
		lod2D				= lod2D,
		gs_opening_dir_m	= 1,
		gs_wcont_line_m		= iWallContourLineW,
		gs_wcont_line_d_m	= iWallContourLineD,
		gs_wallhole_fill	= gs_wallhole_fill * gs_bFills,
		gs_wallhole_pen_fg	= gs_wallhole_pen_fg * gs_bFills,
		gs_wallhole_pen_bg	= gs_wallhole_pen_bg * gs_bFills,
		thkSkinTurnInLeft	= thkSkinTurnInLeft,
		thkSkinTurnInRight	= thkSkinTurnInRight,
		thkSkinTurnOutLeft	= thkSkinTurnOutLeft,
		thkSkinTurnOutRight	= thkSkinTurnOutRight,
		penSkinContourIn	= penSkinContourIn,
		penSkinContourOut	= penSkinContourOut,
		ltSkinContourIn		= ltSkinContourIn,
		ltSkinContourOut	= ltSkinContourOut

del 1

del 2

return



3331:

pen gs_sash_pen_cont

! *************************** 2D offnungslinien Flugel 1

add2 aop,hop


pen gs_opLinePen

if opp > EPS then
	if gs_iSwingType = 1 then
		arc2 0,0,ro,-opp,0
	else
		line2 ro, 0, cos(opp)*ro, -sin(opp)*ro
	endif
endif

line_type SYMB_LINETYPE
line2 0,0,cos(opp)*ro, -sin(opp)*ro
del 1
pen gs_sash_pen_cont
return


3332:

pen gs_sash_pen_cont

! *************************** 2D offnungslinien Flugel 2

add2 aop,hop


pen gs_opLinePen

if opp > EPS then
	if gs_iSwingType = 1 then
		arc2 0,0,ro,180,180+opp
	else
		line2 -ro, 0, -ro * cos(opp), -ro * sin (opp)
	endif
endif

line_type gs_panelLineType
line2 0,0,-cos(opp)*ro, -sin(opp)*ro
del 1
pen gs_sash_pen_cont
return



4001:

! ***** Hotspot editing - LEFT

add2 aop,hop

pan=ro

unID_Opening = 1000

!*** base hotspot
hotspot2 -pan, 0,unID_Opening,gs_ow21,4+128
!*** moving hotspot
hotspot2 -(cos(gs_ow21))*pan, -(sin(gs_ow21))*pan,unID_Opening + 1,gs_ow21,5
!*** center of angle
hotspot2 0, 0,unID_Opening + 2,gs_ow21,6 + 512

del 1

! ***** end of hotspot editing

return


4011:

! ***** Hotspot editing - LEFT

add2 aop,hop

pan=ro

unID_Opening = 1000

!*** base hotspot
hotspot2  pan, 0,unID_Opening,gs_ow21,4+128
!*** moving hotspot
hotspot2  (cos(gs_ow21))*pan, -(sin(gs_ow21))*pan,unID_Opening + 1,gs_ow21,5
!*** center of angle
hotspot2 0, 0,unID_Opening + 2,gs_ow21,6

del 1

! ***** end of hotspot editing

return


4002:

! ***** Hotspot editing - RIGHT

add2 aop,hop

pan=ro

unID_Opening = 1010

!*** base hotspot
hotspot2 -pan, 0,unID_Opening,gs_ow22,4+128
!*** moving hotspot
hotspot2  -(cos(gs_ow22))*pan, -(sin(gs_ow22))*pan,unID_Opening + 1,gs_ow22,5
!*** center of angle
hotspot2 0, 0,unID_Opening + 2,gs_ow22,6 + 512

del 1

! ***** end of hotspot editing

return


4022:

! ***** Hotspot editing - RIGHT

add2 aop,hop

pan=ro

unID_Opening = 1020

!*** base hotspot
hotspot2  pan, 0,unID_Opening,gs_ow22,4+128
!*** moving hotspot
hotspot2   (cos(gs_ow22))*pan, -(sin(gs_ow22))*pan,unID_Opening + 1,gs_ow22,5
!*** center of angle
hotspot2 0, 0,unID_Opening + 2,gs_ow22,6

del 1

! ***** end of hotspot editing

return



