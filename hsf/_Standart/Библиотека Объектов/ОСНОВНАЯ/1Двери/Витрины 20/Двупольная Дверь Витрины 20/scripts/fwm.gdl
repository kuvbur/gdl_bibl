
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "2739250E-C9C5-4555-9665-26F863EC54CC"
	_startID	= "D8302F73-785F-428A-9DF5-7E08C7063801"
	_endID		= "E8747767-10D4-4258-BB2B-C33B4E81A28B"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "E8747767-10D4-4258-BB2B-C33B4E81A28B"
	_endID		= "2DAF11DB-56BC-49E0-B61F-B17DE46BA54D"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "2DAF11DB-56BC-49E0-B61F-B17DE46BA54D"
	_endID		= "D3BC8E3F-6E38-45F1-B0A0-3173E9A87C38"
gosub "deleteUnusedListParams_FWM"

	_startID	= "D3BC8E3F-6E38-45F1-B0A0-3173E9A87C38"
	_endID		= "6F1CE1CF-B54A-426C-8023-41FBB3B29A41"
gosub "DoorPanelType_FWM"

	_startID	= "6F1CE1CF-B54A-426C-8023-41FBB3B29A41"
	_endID		= "9E119AAB-6F4A-4595-98D1-3D052FC25B45"
gosub "RenameMinSpaceParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 1,
													bMigrateKickBoard2		= 1,
													bMigrateCasingType		= 0,
													bMigrateBottomCasing	= 0,
													bMigrateCasingCover		= 0,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		iTypeDemolitionFill = 1				! RENOVATION_FILL_EXIST
		parameters iTypeDemolitionFill = iTypeDemolitionFill

		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

! ==============================================================================
! DoorPanelType
! ==============================================================================
"DoorPanelType_FWM":
	if actualGUID = _startID then
		_gs_door_typ_m = 0		! Empty
		bHas = DELETED_PAR_VALUE("gs_door_typ_m", _gs_door_typ_m)
	
		iDoorPanelType = _gs_door_typ_m
		parameters iDoorPanelType = iDoorPanelType
	
		actualGUID = _endID
	endif
return

! ==============================================================================
! RenameMinSpaceParams
! ==============================================================================
"RenameMinSpaceParams_FWM":
	if actualGUID = _startID then
	    bHas =  DELETED_PAR_VALUE("ms_openingSWidth1", _ms_openingSWidth1) &\
	            DELETED_PAR_VALUE("ms_openingSWidth2", _ms_openingSWidth2) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth1", _ms_oppositeSWidth1) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth2", _ms_oppositeSWidth2)
	            
	    if bHas then
	        msOpeningSideWidth2 = _ms_openingSWidth1
	        msOpeningSideWidth1 = _ms_openingSWidth2
	        msOppositeSideWidth2 = _ms_oppositeSWidth1
	        msOppositeSideWidth1 = _ms_oppositeSWidth2
	        
	        parameters  msOpeningSideWidth2 = msOpeningSideWidth2,
	                    msOpeningSideWidth1 = msOpeningSideWidth1,
	                    msOppositeSideWidth2 = msOppositeSideWidth2,
	                    msOppositeSideWidth1 = msOppositeSideWidth1
		endif
	    
	    actualGUID = _endID
	endif
return

