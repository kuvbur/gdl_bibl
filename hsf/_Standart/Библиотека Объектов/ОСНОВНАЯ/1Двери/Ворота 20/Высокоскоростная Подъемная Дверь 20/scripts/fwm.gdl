
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "273F0BD4-AC23-4515-BD94-9E18CB971DC2"
	_startID	= "78C25C0C-AD69-4C95-8897-CFBC0B436037"
	_endID		= "D14C820D-5239-4613-806C-34C240701782"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "D14C820D-5239-4613-806C-34C240701782"
	_endID		= "77A98575-9B6B-4152-8FDB-030EA9A9351D"
gosub "deleteUnusedListParams_FWM"

	_startID	= "77A98575-9B6B-4152-8FDB-030EA9A9351D"
	_endID		= "F0CA3758-B089-4C94-9059-134220C35AE4"
gosub "DoorPanelType_FWM"

	_startID	= "F0CA3758-B089-4C94-9059-134220C35AE4"
	_endID		= "BB3985EB-145C-47A9-B66A-11595E816E4D"
gosub "RenameMinSpaceParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		iTypeDemolitionFill = 1				! RENOVATION_FILL_EXIST
		parameters iTypeDemolitionFill = iTypeDemolitionFill

		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

! ==============================================================================
! DoorPanelType
! ==============================================================================
"DoorPanelType_FWM":
	if actualGUID = _startID then
		! Empty Step migrate forward every case
	
		actualGUID = _endID
	endif
return

! ==============================================================================
! RenameMinSpaceParams
! ==============================================================================
"RenameMinSpaceParams_FWM":
	if actualGUID = _startID then
	    bHas =  DELETED_PAR_VALUE("ms_openingSWidth1", _ms_openingSWidth1) &\
	            DELETED_PAR_VALUE("ms_openingSWidth2", _ms_openingSWidth2) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth1", _ms_oppositeSWidth1) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth2", _ms_oppositeSWidth2)
	            
	    if bHas then
	        msOpeningSideWidth2 = _ms_openingSWidth1
	        msOpeningSideWidth1 = _ms_openingSWidth2
	        msOppositeSideWidth2 = _ms_oppositeSWidth1
	        msOppositeSideWidth1 = _ms_oppositeSWidth2
	        
	        parameters  msOpeningSideWidth2 = msOpeningSideWidth2,
	                    msOpeningSideWidth1 = msOpeningSideWidth1,
	                    msOppositeSideWidth2 = msOppositeSideWidth2,
	                    msOppositeSideWidth1 = msOppositeSideWidth1
		endif
	    
	    actualGUID = _endID
	endif
return

