


! ================= 2D Representation following project scale =============================
! =========================================================================================

if iTypeDemolitionFill = RENOVATION_FILL_DEMOLISHED then		! Demolition
	line_type gs_Demolition_linetype
else
	line_type 1
endif


! =============================================================================
! GS Door Opening Functions
! =============================================================================

call "GS Door Opening Functions",
	parameters all 	isEmptyOpening			= isEmptyOpening,
					iWindowShape			= iWindowShape,
					gs_wcont_line_out		= gs_wcont_line_out,
					gs_wcont_line_in		= gs_wcont_line_in,
	returned_parameters	iLoD2D,
						bSimpleLeafin50,
						bShowAddHotspots


lod2D	= DET2D_FS_OFF

if iLoD2D = SYMBD_S_1LINE then						lod2D	= DET2D_FS_SIMPLE			! Simple --> INT 200, Simple USA
if iLoD2D = SYMBD_S_1LINECL then					lod2D	= DET2D_FS_SIMPLE			! Simple --> NLD 200
if iLoD2D = SYMBD_M_1LINELEAF_2LINE then			lod2D	= DET2D_FS_MIDDLE			! Middle --> INT 100
if iLoD2D = SYMBD_M_2LINELEAFCL_GLASS then			lod2D	= DET2D_FS_MIDDLE			! Middle --> NLD 100
if iLoD2D = SYMBD_M_1LINELEAF_1LINE then			lod2D	= DET2D_FS_MIDDLE			! Middle USA
if iLoD2D = SYMBD_D_2LINELEAFOP_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> INT 50
if iLoD2D = SYMBD_D_1LINELEAF_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> SWE 50
if iLoD2D = SYMBD_D_2LINELEAFCL_REBATEONGLASS then	lod2D	= DET2D_FS_DETAILED			! Detailed --> NLD 50
if iLoD2D = SYMBD_D_2LEAFOPGLASS_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> INT 50 Storefronts
if iLoD2D = SYMBD_D_2LINELEAFOP_SASH then			lod2D	= DET2D_FS_DETAILED			! Detailed USA
if iLoD2D = SYMBD_S_2LINE then						lod2D	= DET2D_FS_SIMPLE_CHI		! Chinese Simple
if iLoD2D = SYMBD_D_2LINELEAFOP_2LINE then			lod2D 	= DET2D_FS_DETAILED_CHI		! Chinese Detailed

if lod2D = DET2D_FS_OFF then end


if iTypeDemolitionFill = RENOVATION_FILL_NEW & gs_bShowNewOnCC then end		! New

if bSimpleLeafin50 & lod2D = DET2D_FS_DETAILED then lod2D = DET2D_FS_MIDDLE		!simple leaf in detailed symbol

! =============================================================================
! Minimal Space for Doors
! =============================================================================
reqBMinSpace = 0
success = LIBRARYGLOBAL ("LibraryGlobals13", "showBMinSpace", reqBMinSpace)
if success > 0 and reqBMinSpace then
	if ms_oppositeSDepth > EPS or ms_openingSDepth > EPS then
		drawindex 10
		call "dw_minimalspace" parameters gs_leaf_optype_m = 31,
								returned_parameters reqBMinSpace,
													reqMSFillType,
													reqMSFillPen,
													reqMSFillBackPen,
													reqMSContLineType,
													reqMSContLinePen
		pen reqMSContLinePen
		line_type reqMSContLineType
		fill reqMSFillType
		unID = 10400

		if lod2D > DET2D_FS_SIMPLE & lod2D <> DET2D_FS_SIMPLE_CHI & lod2D <> DET2D_FS_DETAILED_CHI then
			_bSimpleOrCHI = 0
		else
			_bSimpleOrCHI = 1
		endif

		if not(_bSimpleOrCHI) then
			put	(a/2-2")*cos(-95/2),						(a/2-2")*sin(-95/2), 0
		endif

		if ms_oppositeSDepth > EPS then
			put	-a/2*cos(180-95/2), 						-a/2*sin(180-95/2), 1

			if msOppositeSideWidth1 > EPS then
				put	-a/2*cos(180-95/2) + msOppositeSideWidth1, 	-a/2*sin(180-95/2), 1,
					-a/2*cos(180-95/2) + msOppositeSideWidth1, 	-a/2*sin(180-95/2) - ms_oppositeSDepth, 1
			else
				put	-a/2*cos(180-95/2),						 	-a/2*sin(180-95/2) - ms_oppositeSDepth, 1
			endif

			if msOppositeSideWidth2 > EPS then
				put	a/2*cos(180-95/2) - msOppositeSideWidth2, 	-a/2*sin(180-95/2)- ms_oppositeSDepth, 1,
					a/2*cos(180-95/2) - msOppositeSideWidth2, 	-a/2*sin(180-95/2), 1
			else
				put	a/2*cos(180-95/2),						 	-a/2*sin(180-95/2)- ms_oppositeSDepth, 1
			endif

			put		a/2*cos(180-95/2), 							-a/2*sin(180-95/2), 0
		endif

		if not(_bSimpleOrCHI) then
			put -(a/2-2")*cos(-95/2),						(a/2-2")*sin(-95/2), 0
		endif

		put		0, 			0, 900,
				0, -		95, 4000 + _bSimpleOrCHI

		if ms_openingSDepth > EPS then
			if not(_bSimpleOrCHI) then
				put	a/2*cos(180-95/2),  						a/2*sin(180-95/2), 1
			endif

			if msOpeningSideWidth2 > EPS then
				put	a/2*cos(180-95/2) - msOpeningSideWidth2,  	a/2*sin(180-95/2), 1,
					a/2*cos(180-95/2) - msOpeningSideWidth2,  	a/2*sin(180-95/2) + ms_openingSDepth, 1
			else
				put	a/2*cos(180-95/2),						  	a/2*sin(180-95/2) + ms_openingSDepth, 1
			endif

			if msOpeningSideWidth2 > EPS then
				put	-a/2*cos(180-95/2) + msOpeningSideWidth1, 	a/2*sin(180-95/2)+ ms_openingSDepth, 1,
					-a/2*cos(180-95/2) + msOpeningSideWidth1,  	a/2*sin(180-95/2), 1
			else
				put	-a/2*cos(180-95/2),						 	a/2*sin(180-95/2)+ ms_openingSDepth, 1
			endif

			put		-a/2*cos(180-95/2),  						a/2*sin(180-95/2), 0
		endif

		if not(_bSimpleOrCHI) then
			put	(a/2-2")*cos(-95/2),						-(a/2-2")*sin(-95/2), 0
		endif

		put		0, 			0, 900,
				0, 			-95, 4000


		poly2_b nsp/3, 1+2+4+64, reqMSFillPen, reqMSFillBackPen,
				get(nsp)


		hotspot2 -a/2*cos(180-95/2) + msOppositeSideWidth1, -a/2*sin(180-95/2) - ms_oppositeSDepth, unID : unID = unID +1
		hotspot2 a/2*cos(180-95/2)- msOppositeSideWidth2, -a/2*sin(180-95/2)- ms_oppositeSDepth, unID : unID = unID +1
		hotspot2 a/2*cos(180-95/2) - msOpeningSideWidth2,  	a/2*sin(180-95/2) + ms_openingSDepth, unID : unID = unID +1
		hotspot2 -a/2*cos(180-95/2) + msOpeningSideWidth1, 	a/2*sin(180-95/2)+ ms_openingSDepth, unID : unID = unID +1
	endif
endif

gs_fillFr	= gs_fillFr * gs_bFills
gs_wallhole_fill = gs_wallhole_fill * gs_bFills
gs_fillLeaf = gs_fillLeaf * gs_bFills

fill gs_wallhole_fill

if iTypeDemolitionFill = RENOVATION_FILL_DEMOLISHED then		! Demolition
	line_type gs_Demolition_linetype
else
	line_type 1
endif


!************************************ 1:100 / 1:200

if lod2D < DET2D_FS_DETAILED | lod2D = DET2D_FS_SIMPLE_CHI | lod2D = DET2D_FS_DETAILED_CHI then

	pen gs_leaf_pen_2D
	line2    0.05882789619416, 0.05931641843489, 0.6662752128482, 0.6667306632274
	line2    -0.05937218334979, 0.05931641843489, -0.6668029642982, 0.6667471993833
	line2    0.05937218334979, -0.05931641843489, 0.6668029642982, -0.6667471993833
	line2    -0.05921786760435, -0.05896031159569, -0.6666486485527, -0.666391092544

	for i=1 to 2

		if lod2D = DET2D_FS_SIMPLE | lod2D = DET2D_FS_SIMPLE_CHI | lod2D = DET2D_FS_DETAILED_CHI then

			pen gs_frame_pen_cont
			arc2 0, 0, a/2, 180-95/2, 180+95/2

			if bShowAddHotspots & (lod2D = DET2D_FS_MIDDLE | lod2D = DET2D_FS_DETAILED) then
				hotspot2 a/2*cos(180-95/2),  a/2*sin(180-95/2), 2000 + (i - 1)
				hotspot2 a/2*cos(180-95/2), -a/2*sin(180-95/2), 2002 + (i - 1)
			endif
		else

			pen gs_frame_pen_cont
			fill gs_FillSdl
			poly2_b 5, 1+2+4+32, gs_penSdl_fg, gs_penSdl_bg,
				(a/2-2")*cos(-95/2),(a/2-2")*sin(-95/2),1,
				0,0,901,
				0,95,4001,
				(a/2)*cos(95/2),(a/2)*sin(95/2),1,
				0,-95,4001

			if bShowAddHotspots & lod2D = DET2D_FS_MIDDLE then
				hotspot2 a/2*cos(180-95/2),  a/2*sin(180-95/2), 2000 + (i - 1)
				hotspot2 a/2*cos(180-95/2), -a/2*sin(180-95/2), 2002 + (i - 1)
			endif
		endif

		hotspot2 a/2*cos(180-95/2),  a/2*sin(180-95/2), 2004 + (i - 1)
		hotspot2 a/2*cos(180-95/2), -a/2*sin(180-95/2), 2006 + (i - 1)

		mul2 -1,1
	next i
	del 2

endif


!************************************ 1:50

if lod2D >= DET2D_FS_DETAILED & lod2D <> DET2D_FS_SIMPLE_CHI & lod2D <> DET2D_FS_DETAILED_CHI then

	for i=1 to 2
		fill gs_FillSdl
		pen gs_frame_pen_cont
		poly2_b 5, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
			(a/2-2")*cos(-95/2),(a/2-2")*sin(-95/2),1,
			0,0,901,
			0,95,4001,
			(a/2)*cos(95/2),(a/2)*sin(95/2),1,
			0,-95,4001

		fill gs_fillFr
		poly2_b 5, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			(a/2-2")*cos(-95/2),(a/2-2")*sin(-95/2),1,
			0,0,901,
			(a/2-2")*cos(-95/2+5),(a/2-2")*sin(-95/2+5),3001,
			(a/2)*cos(-95/2+5),(a/2)*sin(-95/2+5),1,
			(a/2)*cos(-95/2),(a/2)*sin(-95/2),3001

		poly2_b 5, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			(a/2-2")*cos(-95/2),-(a/2-2")*sin(-95/2),1,
			0,0,901,
			(a/2-2")*cos(-95/2+5),-(a/2-2")*sin(-95/2+5),3001,
			(a/2)*cos(-95/2+5),-(a/2)*sin(-95/2+5),1,
			(a/2)*cos(-95/2),-(a/2)*sin(-95/2),3001

		poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			(a/2), gs_frame_thk/2, 1,
			(a/2-2"), gs_frame_thk/2, 1,
			(a/2-2"), -gs_frame_thk/2, 1,
			(a/2), -gs_frame_thk/2, 1

		mul2 -1,1
	next i


	fill gs_fillLeaf
	pen gs_leaf_pen_2D
	rot2 45

	for i = 1 to 4
		rot2 90
		poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
			-1", 1.5", 1,
			1", 1.5", 1,
			1", 0.47*A, 1,
			-1", 0.47*A, 1
	next i
	del 5

	if bShowAddHotspots then
		hotspot2  a/2*cos(180-95/2),  a/2*sin(180-95/2), 2000
		hotspot2  a/2*cos(180-95/2), -a/2*sin(180-95/2), 2002
		hotspot2 -a/2*cos(180-95/2),  a/2*sin(180-95/2), 2001
		hotspot2 -a/2*cos(180-95/2), -a/2*sin(180-95/2), 2003
	endif
endif








