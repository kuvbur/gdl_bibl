
!!! D Door Revolving !!!


! =============================================================================
! GS Door Opening Functions
! =============================================================================

call "GS Door Opening Functions",
	parameters all	iWindowShape			= iWindowShape


! =============================================================================
! Section detail level settings
! =============================================================================

lodSE = LODSE_20
if GLOB_SCALE > 21  then lodSE = LODSE_50
if GLOB_SCALE > 51  then lodSE = LODSE_100
if GLOB_SCALE > 101 then lodSE = LODSE_200
if GLOB_SCALE > 201 then lodSE = LODSE_500


if iDWDet3D = DETLEVEL_DW3D_OFF then end
if iDWDet3D = DETLEVEL_DW3D_SIMPLE then res = 16


! ==============================================================================
! Unit hotspots
! ==============================================================================

if (GLOB_CONTEXT = 4 & lodSE <= LODSE_100) | GLOB_CONTEXT = 6 then
	hotspot -A/2, 0, -gs_frame_thk/2, 100
	hotspot  A/2, 0, -gs_frame_thk/2, 101
	hotspot -A/2, 0, gs_frame_thk/2, 102
	hotspot  A/2, 0, gs_frame_thk/2, 103

	hotspot -A/2, b, -gs_frame_thk/2, 104
	hotspot  A/2, b, -gs_frame_thk/2, 105
	hotspot -A/2, b, gs_frame_thk/2, 106
	hotspot  A/2, b, gs_frame_thk/2, 107
endif


pen gs_frame_pen
rotx -90
material gs_frame_mat

resol res
if gs_shadow=0 then shadow off


mulz b/(84 1/2")
roty -90
rotx 42.5
gosub 100
del 8
rotx 180
gosub 100
del 8
roty 90



base

if iDWDet3D <> DETLEVEL_DW3D_SIMPLE then cylind 7',1 1/2"

!!	BODY -1

addz 7'


!*** top cover

if gs_top_d-0.0001>a then

	rotz -42.5

	cutpolya 5, 1, 0,
		-a/2-1, -WIDO_SILL, 15,
		-a/2-1, -WIDO_SILL+WALL_THICKNESS, 15,
		-a/2, -WIDO_SILL+WALL_THICKNESS, 15,
		-a/2, -WIDO_SILL, 15,
		-a/2-1, -WIDO_SILL, -1

	cutpolya 5, 1, 0,
		 a/2+1, -WIDO_SILL, 15,
		 a/2+1, -WIDO_SILL+WALL_THICKNESS, 15,
		 a/2, -WIDO_SILL+WALL_THICKNESS, 15,
		 a/2, -WIDO_SILL, 15,
		 a/2+1, -WIDO_SILL, -1

	del 1

endif

roty -90
rotx -42.5
revolve 2,360,1+2+16+32,
	0,gs_top_d/2,0,
	1/2",gs_top_d/2,0
del 2

if gs_top_d-0.0001>a then
	cutend
	cutend
endif



!!	BODY -1

vert 0, 0, 0
vert 0, 1, 0
vert 1, 0, 0
vert 0, 0, 1
coor 5, -1, -2, -3, -4
body 1


del 2
rotx gs_open_3D+2.5
gosub 50
del 8
rotx 90
gosub 50
del 8
rotx 90
gosub 50
del 8
rotx 90
gosub 50

del top
end


!door
50:
material gs_frame_mat
addy 1 1/2"
addz -1"
if gs_SashText_typ_num = MITERED_JOINT then
	base
	prism_ 4,2",
		0, 0, 10,
		1', 2", 15,
		1', (0.45*A)-2", 10,
		0, 0.45*A, 15
	prism_ 4,2",
		7', 0, 10,
		6'10", 2", 15,
		6'10", (0.45*A)-2", 10,
		7', 0.45*A, 15
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1

	base
	prism_ 4,2",
		0, 0, 10,
		1', 2", 15,
		6'10", 2", 10,
		7', 0, 15
	prism_ 4,2",
		0, 0.45*A, 10,
		1', (0.45*A)-2", 15,
		6'10", (0.45*A)-2", 10,
		7', 0.45*A, 15
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1
endif
if gs_SashText_typ_num = BUTT_JOINT then
	base
	prism_ 6,2",
		0,0,15,
		1', 0, 8,
		1',2",15,
		1', (0.45*A)-2", 10,
		1', 0.45*A, 13,
		0, 0.45*A, 15
	prism_ 6,2",
		7',0,15,
		6'10", 0,8,
		6'10",2",15,
		6'10", (0.45*A)-2", 10,
		6'10", 0.45*A, 13,
		7', 0.45*A, 15
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1

	base
	prism_ 4,2",
		1', 0, 10,
		1',2",15,
		6'10", 2", 10,
		6'10", 0, 15
	prism_ 4,2",
		1', 0.45*A, 10,
		1', (0.45*A)-2", 15,
		6'10", (0.45*A)-2", 10,
		6'10", 0.45*A, 15
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1
endif

body -1

addx 1'
addy 2"
addz 1"
if gs_glass_mat=0 then 10
material gs_glass_mat
pen gs_glass_pen
!glass
	brick GlassHeight, GlassWidth, 0.01
	body -1

10: addx 3'/b*(84 1/2")
addz -1 1/2"
pen gs_frame_pen
material gs_handle_mat

	base
	if iDWDet3D <> DETLEVEL_DW3D_SIMPLE then block HandleHeight, GlassWidth, 1/2"
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1

	body -1
addz 2 1/2"
	base
	if iDWDet3D <> DETLEVEL_DW3D_SIMPLE then block HandleHeight, GlassWidth, 1/2"
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1
	body -1

return


!round walls
100:
	base
	revolve 5,5,63,
		0,(a/2-2"),0,
		0,(a/2),0,
		7',(a/2),0,
		7',(a/2-2"),0,
		0,(a/2-2"),0
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1


	body -1

rotx 5

	revolve 5,90-5,63,
		0,(a/2-2"),0,
		0,(a/2),0,
		4",(a/2),0,
		4",(a/2-2"),0,
		0,(a/2-2"),0
	body -1

addx 4"
if gs_glass_mat=0 then 20
!glass
pen gs_glass_pen
material gs_glass_mat
	revolve 2,42.5-midFrameAng,16+32,
		0,(a/2-1"),0,
		6'4",(a/2-1"),0
	body -1

20: addx 6'-4"
pen gs_frame_pen
material gs_frame_mat
	revolve 5,90-5,63,
		0,(a/2-2"),0,
		0,(a/2),0,
		4",(a/2),0,
		4",(a/2-2"),0,
		0,(a/2-2"),0
	body -1

!!rotx 42.5-midFrameAng
rotx 42.5

addx -6'-4"
!middle bar

addz -gs_frame_thk/2
	base
	prism_ 4, gs_frame_thk,
		0,(a/2-2"), 15,
		0,(a/2), 15,
		6'4",(a/2), 15,
		6'4",(a/2-2"), 15

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1

	body -1

del 1

rotx -midFrameAng

rotx midFrameAng*2
if gs_glass_mat=0 then 30
pen gs_glass_pen
material gs_glass_mat
!glass
	revolve 2,42.5-midFrameAng,16+32,
		0,(a/2-1"),0,
		6'4",(a/2-1"),0
	body -1

30: rotx 42.5-midFrameAng
addx -4"
pen gs_frame_pen
material gs_frame_mat
	base
	revolve 5,5,63,
		0,(a/2-2"),0,
		0,(a/2),0,
		7',(a/2),0,
		7',(a/2-2"),0,
		0,(a/2-2"),0
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body 1

	body -1
del 1

return
