
! =============================================================================
! Tabpage Structure Definition
! =============================================================================

! call all available page fix IDs
	call "ui_tabIDlistHierarchy_m" parameters  iUITreeStandardType = 12,		!INT_STANDARD_D_OPENING_UI
					returned_parameters iUiID_Root,
										iUiID_DoorSettOpening,	iParID_DoorSettOpening,
										iUiID_DoorSettOpModAttr,iParID_DoorSettOpModAttr,
								
										! Wall Opening group
										iUiID_WallOpening,		iParID_WallOpening,
										iUiID_Reveal,			iParID_Reveal,
										iUiID_Closure,			iParID_Closure,
										iUiID_MasonryArch,		iParID_MasonryArch,
										iUiID_WallOpModelAttr,	iParID_WallOpModelAttr,
										
										! Fixtures and Fittings group
										iUiID_FixtureFitting,	iParID_FixtureFitting,
										iUiID_Sill,				iParID_Sill,
										iUiID_Board,			iParID_Board,
										iUiID_CasingOut,		iParID_CasingOut,
										iUiID_CasingIn,			iParID_CasingIn,
										iUiID_Shutter,			iParID_Shutter,
										iUiID_FixFittingModAttr, iParID_FixFittingModAttr,
							
										iUiID_FloorPlanSection,	iParID_FloorPlanSection,
										iUiID_MinSpace,			iParID_MinSpace,
										iUiID_Descriptions,		iParID_Descriptions
							

dim uiUsedPageIDs[][2]
dim uiUsedPageNames[][2]

idxPage = 1

! --------------------------------------------------------------------------------
! Door Settings and Opening group
! --------------------------------------------------------------------------------
uiUsedPageIDs[idxPage][1]	= iUiID_DoorSettOpening			: uiUsedPageIDs[idxPage][2]		= iParID_DoorSettOpening
uiUsedPageNames[idxPage][1]	= `Параметры Двери и Открывания`	: uiUsedPageNames[idxPage][2]	= "uiTab_DoorSettings_flat(1)"
idxPage = idxPage + 1

! override regular iParID_DoorSettOpModAttr with iUiID_Root, different hierarchy applied in this door
iParID_DoorSettOpModAttr = iUiID_Root
uiUsedPageIDs[idxPage][1]	= iUiID_DoorSettOpModAttr	: uiUsedPageIDs[idxPage][2]		= iParID_DoorSettOpModAttr
uiUsedPageNames[idxPage][1]	= `Реквизиты Модели`		: uiUsedPageNames[idxPage][2]	= "uiTab_3DRepr_flat(1)"
idxPage = idxPage + 1

! --------------------------------------------------------------------------------
! Wall Opening
! --------------------------------------------------------------------------------
! override regular iParID_Reveal with iUiID_Root, different hierarchy applied in this door
iParID_Reveal = iUiID_Root
uiUsedPageIDs[idxPage][1]	= iUiID_Reveal				: uiUsedPageIDs[idxPage][2]		= iParID_Reveal
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

! override regular iParID_Closure with iUiID_Root, different hierarchy applied in this door
iParID_Closure = iUiID_Root
uiUsedPageIDs[idxPage][1]	= iUiID_Closure				: uiUsedPageIDs[idxPage][2]		= iParID_Closure
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

! --------------------------------------------------------------------------------
! Additional independent pages
! --------------------------------------------------------------------------------
uiUsedPageIDs[idxPage][1]	= iUiID_FloorPlanSection	: uiUsedPageIDs[idxPage][2]		= iParID_FloorPlanSection
uiUsedPageNames[idxPage][1]	= `План Этажа и Разрез`	: uiUsedPageNames[idxPage][2]	= "uiTab_2DRepr_flat(1)"
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_MinSpace			: uiUsedPageIDs[idxPage][2]		= iParID_MinSpace
uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_Descriptions		: uiUsedPageIDs[idxPage][2]		= iParID_Descriptions
uiUsedPageNames[idxPage][1] = `Характеристики`			: uiUsedPageNames[idxPage][2]	= "uiTab_Descriptions_flat(1)"
idxPage = idxPage + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Вращающейся Двери`
ui_current_page gs_ui_current_page

for iTabs = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[iTabs][1] = iUiID_DoorSettOpening then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_DoorSettOpening then
			gosub "pageDoorSettOpening"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_DoorSettOpModAttr then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_DoorSettOpModAttr then
			gosub "pageModelAttributes"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_Descriptions then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Descriptions then
			gosub "pageCallListing"
		endif
	endif
next iTabs


! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_DoorSettings_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! --------------------------------------------------------------------------------
! GS Door Opening Functions
! --------------------------------------------------------------------------------
call "GS Door Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					isEmptyOpening			= 0,
					uiUsedPageIDs			= uiUsedPageIDs,
					uiUsedPageNames			= uiUsedPageNames


! =============================================================================
end
! =============================================================================



! --------------------------------------------------------------------------------
! Door Settings and Opening
! --------------------------------------------------------------------------------
"pageDoorSettOpening":

	pageStartY 	= 6
	y0 			= pageStartY 
	dy			= 23
	
	x1			= 5
	x2			= 165
	x3 			= 225
	
	pict_x		= 165
	pict_y		= 120
	
	heightOutf	= 15
	heightInf	= 19

	widthOutf = 125
	widthInf = 106

	y0 = pageStartY + 7

	ui_pict 1, 196, y0, 133, 149, 1					! RotatingSettOpening.png

	ui_infield "gs_top_d", 330, pageStartY, 88, 19 ui_tooltip `Диаметр Вверху`

	y0 = y0 + 110
	ui_infield "gs_frame_thk", 330, y0, 88, 19 ui_tooltip `Толщина Коробки`

	y0 = pageStartY + 144

	ui_outfield `Угол Открывания в 3D`, 5, y0, 125, heightOutf
	ui_infield "gs_open_3D", 132, y0-4, 50, heightInf

	ui_separator 1, pageStartY + 168, 443, pageStartY + 168

	y0 = pageStartY + 178

	ui_style 0,1
	ui_outfield `2D-детализация`, x1, y0, x2-2, heightOutf

	y0 = y0 + 23

	ui_style 0,0

	ui_outfield `3D-детализация`, x1,y0, widthOutf, heightOutf	
	ui_infield{3} "iDWDet3D",x1 + widthOutf + 2, y0-4, widthOutf,heightInf,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stDetail[1],	DETLEVEL_DW3D_SCALESENS,
			"",	stDetail[2],	DETLEVEL_DW3D_DETAILED,
			"",	stDetail[3],	DETLEVEL_DW3D_SIMPLE,
			"",	stDetail[4],	DETLEVEL_DW3D_OFF,
			"",	stDetail[5],	DETLEVEL_DW3D_CHI_SIMPLE,
			"",	stDetail[6],	DETLEVEL_DW3D_CHI_DETAILED

	y0 = y0 + 23

	ui_outfield `2D-детализация`, x1,y0, widthOutf, heightOutf	
	ui_infield{3} "iDWDet2D", x1 + widthOutf + 2, y0-4, widthOutf, heightInf,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"",	stDetail2D[1],	DETLEVEL_DW2D_MVO,
			"",	stDetail2D[2],	DETLEVEL_DW2D_SCSENS,
			"",	stDetail2D[3],	DETLEVEL_DW2D_50,
			"",	stDetail2D[4],	DETLEVEL_DW2D_100,
			"",	stDetail2D[5],	DETLEVEL_DW2D_200,
			"",	stDetail2D[6],	DETLEVEL_DW2D_OFF,
			"",	stDetail2D[7],	DETLEVEL_DW2D_CHISMPL,
			"",	stDetail2D[8],	DETLEVEL_DW2D_CHIDET,
			"",	stDetail2D[9],	DETLEVEL_DW2D_20

	y0 = y0 + 23

return
	

! --------------------------------------------------------------------------------
! Model Attributes
! --------------------------------------------------------------------------------
"pageModelAttributes":

	pageStartY = 1

	itemID = 1
	listFieldID = iUiID_DoorSettOpModAttr

	ui_listfield listFieldID, 0, pageStartY, 444, 264 
	ui_listitem itemID, listFieldID, "iDWDet3D",			0,	"ui_list_detailLevel3D_flat(1)"	: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "gs_SashText_typ_num",	0,	""							: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "gs_frame_pen",		0,	"ui_list_pencolor_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "gs_glass_pen",		0,	"ui_list_pencolor_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "gs_frame_mat", 		0,	"ui_list_surface_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "gs_glass_mat",		0,	"ui_list_surface_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "gs_handle_mat",		0,	"ui_list_surface_flat(1)"		: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "res",					0,	"ui_list_resol_flat(1)"			: itemID = itemID + 1
	ui_listitem itemID, listFieldID, "gs_shadow",			0,	"ui_list_shadow_flat(1)"			: itemID = itemID + 1

return
	
	
! --------------------------------------------------------------------------------
! Descriptions
! --------------------------------------------------------------------------------
"pageCallListing":
	call "ui_Description_m" parameters all	iDWObjectType				= 4,		! REVOLVING_OBJECT
											gs_show_UKListParams		= gs_show_UKListParams,
											listFieldID 				= iUiID_Descriptions,
											gs_bOpeningToZone			= gs_bOpeningToZone,
											gs_list_cost				= gs_list_cost,
											gs_list_manufacturer		= gs_list_manufacturer,
											gs_list_note				= gs_list_note,
											gs_list_location			= gs_list_location,
											gs_list_location_as			= gs_list_location_as,
											gs_list_accessories			= gs_list_accessories,
											gs_list_glazing				= gs_list_glazing,
											gs_list_headdet				= gs_list_headdet,
											gs_list_jambdet				= gs_list_jambdet,
											gs_list_silldet				= gs_list_silldet,
											gs_list_hwsetno				= gs_list_hwsetno,
											gs_list_hwkeyside			= gs_list_hwkeyside,
											gs_list_optype				= gs_list_optype,
											gs_list_finish				= gs_list_finish,
											gs_list_elevation			= gs_list_elevation,
											gs_list_hings				= gs_list_hings,
											gs_list_lockset				= gs_list_lockset,
											gs_list_glassarea_tmp		= gs_list_glassarea_tmp,
											gs_list_glassarea_override	= gs_list_glassarea_override,
											gs_list_glassarea			= gs_list_glassarea,
											gs_list_glassperimeter		= gs_list_glassperimeter,
											gs_list_heattransfer		= gs_list_heattransfer,
											gs_list_firerating			= gs_list_firerating,
											gs_list_acousticrating		= gs_list_acousticrating,
											gs_list_type				= gs_list_type,
											FM_Type						= FM_Type,
											iFMType						= iFMType,
											FM_InventoryNumber			= FM_InventoryNumber,
											FM_SerialNumber				= FM_SerialNumber,
											FM_ProductionYear			= FM_ProductionYear,
											FM_ObjectWeight				= FM_ObjectWeight,
											FM_ObjectWeightUnit			= FM_ObjectWeightUnit,
											gs_list_orientationDef_m	= gs_list_orientationDef_m,
											gs_list_NBSClause			= gs_list_NBSClause,
											gs_list_doorcloser			= gs_list_doorcloser,
											gs_list_visionpanel_type	= gs_list_visionpanel_type,
											gs_list_elevationtype		= gs_list_elevationtype,
											gs_list_frametype			= gs_list_frametype,
											gs_list_holddevice			= gs_list_holddevice,
											gs_list_firesignage			= gs_list_firesignage,
											gs_list_comment				= gs_list_comment,
											ifc_optypestr				= ifc_optypestr,
											ifc_optype					= ifc_optype

return

