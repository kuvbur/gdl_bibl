

! =============================================================================
! GS Door Openeing Functions
! =============================================================================

call "GS Door Opening Functions",
	parameters all 	isEmptyOpening			= isEmptyOpening,
					iWindowShape			= iWindowShape,
					bEnableListing		= 1


if gs_pen_2D=0 then parameters gs_pen_2D=3
if gs_frame_pen=0 then parameters gs_frame_pen=2
if gs_glass_pen=0 then parameters gs_glass_pen=2


if a<=0 then parameters a=0.01
values "b"	range (0,)

values{2} "gs_SashText_typ_num" 	MITERED_JOINT, `Угловое Соединение`,
									BUTT_JOINT, `Соединение Встык`


! =============================================================================
! Orientation
! =============================================================================

values{2} "gs_list_orientationDef_m"	0, `Автоматически`,
										1, `Специальный`

if gs_list_orientationDef_m = 0 then
	ac_OpeningSide = ""
	parameters ac_OpeningSide = ac_OpeningSide

	lock "ac_OpeningSide"
	hideparameter "ac_OpeningSide"
endif


! =============================================================================
! IFC Parameters
! =============================================================================

ifc_optype = 10		! DOOR_OP_REVOLVING
parameters ifc_optype = ifc_optype
call "IFC_optype_door" parameters ifc_optype = ifc_optype

lock "ifc_optypestr"

ifc_LiningDepth = 0
parameters ifc_LiningDepth = ifc_LiningDepth

ifc_LiningThickness = 0
parameters ifc_LiningThickness = ifc_LiningThickness

dim ifc_DoorPanel[][4]
	ifc_DoorPanel[1][1] = 0
	ifc_DoorPanel[1][2] = 0
	ifc_DoorPanel[1][3] = 5		! REVOLVING
	ifc_DoorPanel[1][4] = 2		! MIDDLE

parameters ifc_DoorPanel = ifc_DoorPanel


! =============================================================================
! FM Types
! =============================================================================

call "FM_types" parameters all


! ===== General Door Parameters =====

!values "gs_detlevel_3D"		stDetail[2], stDetail[3], stDetail[4]

! =============================================================================
! Minimal Space parameter cotrol
! =============================================================================

	call "dw_minimalspace" parameters A = A,
									B = B,
									ac_wallhole_width = ac_wallhole_width,
									EgressMSWidth = ac_egress_width,
									msOpeningSideWidth2 = msOpeningSideWidth2,
									msOpeningSideWidth1 = msOpeningSideWidth1,
									ms_openingSDepth = ms_openingSDepth,
									msOppositeSideWidth2 = msOppositeSideWidth2,
									msOppositeSideWidth1 = msOppositeSideWidth1,
									ms_oppositeSDepth = ms_oppositeSDepth,
									ac_unit_width = ac_unit_width,
									gs_opside_list = gs_opside_list,
									gs_opside_list_m = gs_opside_list_m,
									gs_leaf_optype_m = 31


! =============================================================================
! 3D Detail Level
! =============================================================================


if gs_3DDetEnable =0 then

    values{2} "iDWDet3D" 	DETLEVEL_DW3D_DETAILED,		`Детальный`,
						    DETLEVEL_DW3D_SIMPLE,		`Простой`,
						    DETLEVEL_DW3D_OFF,			`Откл.`

else
	dim n3DScaleValueMask[]
	dim n3DScaleValueMask_m[]

	i=1
	if bittest( gs_3DDetEnable, 0) then                                              ! Scale Sensitive in Value list, 1
				n3DScaleValueMask[i] = stDetail[1]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_SCALESENS
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 1) then                                              ! Detailed in Value list,2
				n3DScaleValueMask[i] = stDetail[2]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_DETAILED
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 2) then                                              ! Simple in Value list,4
				n3DScaleValueMask[i] = stDetail[3]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_SIMPLE
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 3) then                                              ! Off in Value list,8
				n3DScaleValueMask[i] = stDetail[4]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_OFF
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 4) then                                              ! Chinese Simple in Value list,16
				n3DScaleValueMask[i] = stDetail[5]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_CHI_SIMPLE
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 5) then                                              ! Chinese Detailed in Value list,32
				n3DScaleValueMask[i] = stDetail[6]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_CHI_DETAILED
				i = i + 1
	endif

	values{2} "iDWDet3D" n3DScaleValueMask_m, n3DScaleValueMask
endif

if iDWDet3D = DETLEVEL_DW3D_SIMPLE then lock "res"


!**************** Swedish list parameter control **********************
if gs_SWE_listParams_contr > 0 then
	call "SWE_ListParameters_control" parameters gs_SWE_listParams_contr = gs_SWE_listParams_contr,
			swe_list_d1				= swe_list_d1
endif

! =============================================================================
! Japanese list parameter control
! =============================================================================

if gs_JPN_listParams_contr then
	call "ListValuesJPN" parameters all gs_JPNObjectType = 1,		!COMMPARS_INTDOORS
			gs_is_p2	= 0,
			gs_is_p6	= 0
endif


! =============================================================================
! Total Glazed Area Listing Parameters
! =============================================================================
if NOT(gs_list_glassarea_override) then
	if GLOB_MODPAR_NAME = "gs_list_glassarea" then
		gs_list_glassarea_tmp = gs_list_glassarea
		parameters gs_list_glassarea_tmp = gs_list_glassarea_tmp
	else
		gs_list_glassarea = gs_list_glassarea_tmp
		parameters gs_list_glassarea = gs_list_glassarea
	endif

	gs_list_glassarea = (GlassWidth * GlassHeight - GlassWidth * HandleHeight) * 2
	parameters gs_list_glassarea = gs_list_glassarea
	lock "gs_list_glassarea"
else
	if GLOB_MODPAR_NAME = "gs_list_glassarea" then
		gs_list_glassarea_tmp = gs_list_glassarea
		parameters gs_list_glassarea_tmp = gs_list_glassarea_tmp
	else
		gs_list_glassarea = gs_list_glassarea_tmp
		parameters gs_list_glassarea = gs_list_glassarea
	endif
endif

! =============================================================================
! Zone Recognitions for Doors - Parapet wall Inset value calculation
! Frame buggy > develop WIDO_FRAME_THICKNESS
!		GLOB_ELEVATION		- UNconditional - Zone`s always flowing
!		WIDO_REVEAL_SIDE	- conditional
!		REVEAL (WIDO_SILL)	- conditional
!
! ParapetWallInset Values:	flowing: zero or more from the outside (reveal side)
!		-EPS < >= WALL_THICKNESS
! =============================================================================

ZONE_RECOGNITION_NONE 		= 0
ZONE_RECOGNITION_FULL 		= 1
ZONE_RECOGNITION_OUTSIDE 	= 2
ZONE_RECOGNITION_INSIDE 	= 3
ZONE_RECOGNITION_NOREVEAL	= 4
ZONE_RECOGNITION_REVOLVING	= 5

if WIDO_REVEAL_SIDE then
	pwi = ZONE_RECOGNITION_INSIDE
else
	if WIDO_SILL > gs_frame_thk/2 + EPS then
		pwi = ZONE_RECOGNITION_REVOLVING
	else
		pwi = ZONE_RECOGNITION_FULL
	endif
endif
parameters pwi  = pwi

