
! Contact person: <SzL, NJ>


! =============================================================================
! GS Door Opening Functions
! =============================================================================

call "GS Door Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					gs_lowerArchHeight		= gs_lowerArchHeight,
					nArchResolution			= gs_res,
					bParalellInCurvedWalls	= bParalellInCurvedWalls


! =============================================================================


shadow on
pen WALL_VIEW_PEN
resol gs_res

LowArcoffsx = A/2 - (RadArch - sqr(RadArch ^2 - gs_lowerArchHeight ^2))

if abs(WIDO_ORIG_DIST)<EPS then

	if WIDO_REVEAL_SIDE then mulz -1

	addz -WIDO_SILL + WALL_THICKNESS/2

	! --- Hotspot Editing in 3D ---
	unID=1

	hotspot 0, 0, 0, 											unID, gs_shoulder_height, 1+128 : unID = unID + 1
	hotspot 0, -1, 0,				 							unID, gs_shoulder_height, 3 : unID = unID + 1
	hotspot 0, gs_shoulder_height, 0, 							unID, gs_shoulder_height, 2 : unID = unID + 1

	hotspot 0, gs_shoulder_height, 0, 							unID, gs_lowerArchHeight, 1+128 : unID = unID + 1
	hotspot 0, 0, 0,				 							unID, gs_lowerArchHeight, 3 : unID = unID + 1
	hotspot 0, gs_shoulder_height + gs_lowerArchHeight, 0, 		unID, gs_lowerArchHeight, 2 : unID = unID + 1

	del 1

	if WIDO_REVEAL_SIDE then del 1
endif

! =============================================================================
! Preview in Door Settings Dialog and in Listing
! =============================================================================

if GLOB_PREVIEW_MODE # 0 | GLOB_FEEDBACK_MODE then
	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
		put A/2 + 0.2,	-0.2,		8,
			A/2 + 0.2,	B + 0.2,	8,
			-A/2 - 0.2,	B + 0.2,	8,
			-A/2 - 0.2,	-0.2,		8,
			A/2 + 0.2,	-0.2,		-1
	endif

	if GLOB_PREVIEW_MODE = 2 then
		isPrism = 0
		isExtrude = 1
		gosub "putparameters"

		extrude NSP/3, 0, 0, WALL_THICKNESS, 16+32, get(NSP)
	else
		isPrism = 1
		isExtrude = 0
		gosub "putparameters"

		cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, NSP/3, WALL_THICKNESS, get(NSP)
	endif
	del 1
	if GLOB_PREVIEW_MODE # 0 then end
endif


! =============================================================================
! Wallhole
! =============================================================================

offset = 0
if abs(WIDO_ORIG_DIST) > EPS then
	offset = WIDO_ORIG_DIST-(WIDO_ORIG_DIST^2-(A/2)^2)*0.5
endif

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================

"putparameters":

	flag1 = 1+2+4+8+16
	flag2 = 64+1+2+4+8+16
	if isPrism then
		flag1 = 31
		flag2 = 64+31
	endif
	if isExtrude then
		flag1 = 0
		flag2 = 1
	endif

	put 			A/2,				0, 					    				flag1,
					A/2,				gs_shoulder_height,						flag1,
					LowArcoffsx - EPS,	gs_shoulder_height,						flag1,
					LowArcoffsx,		gs_shoulder_height,						flag2,
					A/2 - RadArch,		gs_shoulder_height + gs_lowerArchHeight, 900, 
					0,					B,										3000 + flag1,
					-EPS,				B,										flag2,
					-A/2 + RadArch,		gs_shoulder_height + gs_lowerArchHeight, 900,
					-LowArcoffsx,		gs_shoulder_height,						3000 + flag2,
					-LowArcoffsx + EPS,	gs_shoulder_height,						flag1,
					-A/2,				gs_shoulder_height,						flag1,
					-A/2,				0,										flag1,
					A/2,				0,										-1

return
