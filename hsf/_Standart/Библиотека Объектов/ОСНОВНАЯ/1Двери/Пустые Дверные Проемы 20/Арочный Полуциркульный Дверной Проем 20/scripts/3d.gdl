
! Contact person: <SzL, NJ>

! =============================================================================

A		 = max(0.01, A)
openingArchHeight = min(openingArchHeight, A/2)
openingArchHeight = max(openingArchHeight, 0.01)

H	= B - openingArchHeight
R2	= ((A/2)^2 + openingArchHeight^2) / (2 * openingArchHeight)


! =============================================================================
! GS Door Opening Functions
! =============================================================================

call "GS Door Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					nArchResolution			= gs_res,
					bParalellInCurvedWalls	= bParalellInCurvedWalls


! =============================================================================
! Preview in Door Settings Dialog and in Listing
! =============================================================================

pen WALL_VIEW_PEN
resol gs_res

if (GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3) | GLOB_FEEDBACK_MODE then
	put	 A/2+0.2,	-0.2,	8,
		 A/2+0.2,	 B+0.2,	8,
		-A/2-0.2,	 B+0.2,	8,
		-A/2-0.2,	-0.2,	8,
		 A/2+0.2,	-0.2,	-1

	if B - R2 > H - EPS then
		! Half circle
		put	-A/2,		0,		15,
			 A/2,		0,		15,
			 A/2,		H,		15,
			 A/2,		H,		79,
			 0,			1,		800,
			-A/2,		H,		1079,
			-A/2,		0,		-1
	else
		put	-A/2,		0,		15,
			 A/2,		0,		15,
			 A/2,		H,		15,
			 A/2,		H,		79,
			 0,			B - R2,	979,
			-A/2,		H,		3079,
			-A/2,		0,		-1
	endif

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE,
		NSP/3, WALL_THICKNESS,
		get(NSP)

	del 1

	end
endif

if (GLOB_PREVIEW_MODE = 2) then		! IS but not surface
	if B - R2 > H - EPS then
		! Half circle
		put	-A/2,		0,		0,
			 A/2,		0,		0,
			 A/2,		H,		0,
			 A/2,		H,		1,
			 0,			1,		800,
			-A/2,		H,		1001,
			-A/2,		0,		-1
	else
		put	-A/2,		0,		0,
			 A/2,		0,		0,
			 A/2,		H,		0,
			 A/2,		H,		1,
			 0,			B - R2,	901,
			-A/2,		H,		3001,
			-A/2,		0,		-1
	endif
	extrude NSP/3, 0, 0, WALL_THICKNESS, 16+32,
		get(NSP)

	end
endif


! =============================================================================
! Hotspot editing
! =============================================================================

if abs(WIDO_ORIG_DIST) < EPS then
	del top
	if WIDO_REVEAL_SIDE then
		addz WIDO_FRAME_THICKNESS
	else
		mulz -1
	endif

	unID = 1

	hotspot  0,	 B,						WIDO_SILL,unID
	hotspot  A/2,B-openingArchHeight,	WIDO_SILL,unID+1
	hotspot -A/2,B-openingArchHeight,	WIDO_SILL,unID+2
	unID = unID + 3

	hotspot  0,	 B,						WIDO_SILL - WALL_THICKNESS,unID
	hotspot  A/2,B-openingArchHeight,	WIDO_SILL - WALL_THICKNESS,unID+1
	hotspot -A/2,B-openingArchHeight,	WIDO_SILL - WALL_THICKNESS,unID+2
	unID = unID + 3

	del 1
endif
