
! Contact person: <SzL, NJ>

! =============================================================================
! GS Door Opening Functions
! =============================================================================

call "GS Door Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					nArchResolution			= gs_res,
					bParalellInCurvedWalls	= bParalellInCurvedWalls


! =============================================================================
! Preview in Door Settings Dialog and in Listing
! =============================================================================

pen WALL_VIEW_PEN
resol gs_res

if (GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3) | GLOB_FEEDBACK_MODE then
	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE,
		 15, WALL_THICKNESS,
		 A/2+0.2,	-0.2,	8,
		 A/2+0.2,	 B+0.2,	8,
		-A/2-0.2,	 B+0.2,	8,
		-A/2-0.2,	-0.2,	8,
		 A/2+0.2,	-0.2,	-1,

		 A/2,	B-openingArchHeight,		79,
		 A/2-R,	B-openingArchHeight,		900,
		 0,		asn(openingArchHeight/R),	4079,
		 0,		B+EPS,						79,
		 R-A/2,	B-openingArchHeight,		900,
		 0,		asn(openingArchHeight/R),	4079,
		-A/2,	B-openingArchHeight,		79,
		-A/2,	0,							15,
		 A/2,	0,							15,
		 A/2,	B-openingArchHeight,		-1

	del 1

	end
endif

if (GLOB_PREVIEW_MODE = 2) then		! IS but not surface
	extrude 10, 0, 0, WALL_THICKNESS, 16+32,
		 A/2,	B-openingArchHeight,		1,
		 A/2-R,	B-openingArchHeight,		900,
		 0,		asn(openingArchHeight/R),	4001,
		 0,		B+EPS,						1,
		 R-A/2,	B-openingArchHeight,		900,
		 0,		asn(openingArchHeight/R),	4001,
		-A/2,	B-openingArchHeight,		1,
		-A/2,	0,							0,
		 A/2,	0,							0,
		 A/2,	B-openingArchHeight,		-1
	end
endif


! =============================================================================
! Hotspots
! =============================================================================

if abs(WIDO_ORIG_DIST) < EPS then
	del top
	if WIDO_REVEAL_SIDE then
		addz WIDO_FRAME_THICKNESS
	else
		mulz -1
	endif
	addz WIDO_SILL

	unID = 1

	hotspot  0,	 B,						0,unID
	hotspot  A/2,B-openingArchHeight,	0,unID+1
	hotspot -A/2,B-openingArchHeight,	0,unID+2
	unID = unID + 3

	hotspot  0,	 B,						-WALL_THICKNESS,unID
	hotspot  A/2,B-openingArchHeight,	-WALL_THICKNESS,unID+1
	hotspot -A/2,B-openingArchHeight,	-WALL_THICKNESS,unID+2
	unID = unID + 3

	del 2
endif
