
! Contact person: <SzL, NJ>

! =============================================================================
! GS Door Opening Functions
! =============================================================================

call "GS Door Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					nArchResolution			= gs_res,
					bParalellInCurvedWalls	= bParalellInCurvedWalls


! =============================================================================

shadow on
pen WALL_VIEW_PEN
resol gs_res

CUT_FINITE = 0
CUT_SEMIINFINITE = 16
CUT_INFINITE = 24


! =============================================================================
! Preview in Door Settings Dialog and in Listing
! =============================================================================

if GLOB_PREVIEW_MODE # 0 | GLOB_FEEDBACK_MODE then
	if WIDO_REVEAL_SIDE then
		addz (WIDO_SILL-WALL_THICKNESS+WIDO_FRAME_THICKNESS)
	else
		addz -WIDO_SILL
	endif

	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
		put A/2 + 0.2,	-0.2,		8,
			A/2 + 0.2,	B + 0.2,	8,
			-A/2 - 0.2,	B + 0.2,	8,
			-A/2 - 0.2,	-0.2,		8,
			A/2 + 0.2,	-0.2,		-1
	endif

	if GLOB_PREVIEW_MODE = 2 then
		isPrism		= 0
		isExtrude	= 1
		gosub "putparameters"

		extrude nsp/3, 0, 0, WALL_THICKNESS, 16+32, get(nsp)
	else
		isPrism		= 1
		isExtrude	= 0
		gosub "putparameters"

		cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, nsp/3, WALL_THICKNESS, get(nsp)
	endif
	del 1
	if GLOB_PREVIEW_MODE # 0 then end
endif

! --- Hotspot editing in 3D ---------------------------------------------------

unID=1

if WIDO_REVEAL_SIDE then
	addz (WIDO_SILL-WALL_THICKNESS+WIDO_FRAME_THICKNESS)
else
	addz -WIDO_SILL
endif

hotspot -a/2, 0,                  0, unID, gs_shoulder_height, 1+128	:unID=unID+1
hotspot -a/2, gs_shoulder_height, 0, unID, gs_shoulder_height, 2	    :unID=unID+1
hotspot -a/2, -1,                 0, unID, gs_shoulder_height, 3		:unID=unID+1

hotspot a/2, 0,                  0, unID, gs_shoulder_height, 1+128	:unID=unID+1
hotspot a/2, gs_shoulder_height, 0, unID, gs_shoulder_height, 2	    :unID=unID+1
hotspot a/2, -1,                 0, unID, gs_shoulder_height, 3		:unID=unID+1

hotspot -a/2, 0,                  WALL_THICKNESS, unID, gs_shoulder_height, 1+128	:unID=unID+1
hotspot -a/2, gs_shoulder_height, WALL_THICKNESS, unID, gs_shoulder_height, 2	    :unID=unID+1
hotspot -a/2, -1,                 WALL_THICKNESS, unID, gs_shoulder_height, 3		:unID=unID+1

hotspot a/2, 0,                  WALL_THICKNESS, unID, gs_shoulder_height, 1+128	:unID=unID+1
hotspot a/2, gs_shoulder_height, WALL_THICKNESS, unID, gs_shoulder_height, 2	    :unID=unID+1
hotspot a/2, -1,                 WALL_THICKNESS, unID, gs_shoulder_height, 3		:unID=unID+1

del 1

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================

"putparameters":

	flag1 = 1+2+4+8+16
	flag2 = 64+1+2+4+8+16
	if isPrism then
		flag1 = 15
		flag2 = 64+15
	endif
	if isExtrude then
		flag1 = 0
		flag2 = 1
	endif
	sqr3 = sqr(3)
	xtangent = abs((sqr3/2) * openingArchHeight - 0.75 * A)
	ytangent = (3/2) * openingArchHeight - (sqr3/4) * A
	put		A/2, 0, flag1,
			A/2, gs_shoulder_height, flag2,
			-A/2 + 2 * xtangent, gs_shoulder_height, 900,
			0, 60, 4000 + flag2,
			0, B, flag1,
			-xtangent, gs_shoulder_height + ytangent, flag2,
			A/2 - 2 * xtangent, gs_shoulder_height, 900,
			0, 60, 4000 + flag2,
			-A/2, 0, flag1,
			A/2, 0, -1
return
