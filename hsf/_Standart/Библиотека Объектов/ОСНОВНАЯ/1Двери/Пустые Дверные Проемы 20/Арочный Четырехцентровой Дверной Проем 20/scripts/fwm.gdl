
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_startID	= "C6FB587C-EE21-4CAC-8440-6B9BFB8560F6"
	_endID		= "217E780A-D51D-4BE5-BBE3-298C70B0977C"
gosub "repairshoulder_FWM"

	_autoID		= "C6FB587C-EE21-4CAC-8440-6B9BFB8560F6"
	_startID	= "217E780A-D51D-4BE5-BBE3-298C70B0977C"
	_endID		= "AFBD3BA2-6823-4ACC-978C-CAAC90687A51"
gosub "NewDemolitionSymbol_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! try to repair shoulder height
! ==============================================================================
"repairshoulder_FWM":

	if actualGUID = _startID then
		bSuccess  =	STORED_PAR_VALUE ("B", B) &\
					STORED_PAR_VALUE ("openingArchHeight", openingArchHeight) &\
					STORED_PAR_VALUE ("gs_shoulder_height", gs_shoulder_height)

		if bSuccess then
			if	A > B * 4 * sin(60) or \			!keep broken geometry, can't repair
				B < archhmin or \
				openingArchHeight < archhmin or \
				(B - archhmin > 0 and gs_shoulder_height > B - archhmin) then

					bSuccess = 0
			else
				if gs_shoulder_height < 0 then		!try repair
					if B >= archhmin then			!repair
						gs_shoulder_height = 0
						openingArchHeight = B
						parameters openingArchHeight = openingArchHeight,
							gs_shoulder_height = gs_shoulder_height
					else							!keep broken
						bSuccess = 0
					endif
				endif
			endif
		endif

		if bSuccess then actualGUID = _endID else actualGUID = ""
	endif
return


! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGUID = _startID | actualGUID = _autoID then
		actualGUID = _endID
	endif
return

