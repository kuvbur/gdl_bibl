
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "D4498F35-A763-4A49-A54B-2107B01E39A6"
	_startID	= "9CD912CA-6574-4B57-956D-3D7690BC4BED"
	_endID		= "C761554C-F7E2-48E5-B31F-FD33DCAF132B"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "C761554C-F7E2-48E5-B31F-FD33DCAF132B"
	_endID		= "753C9B9E-4E5D-4B09-90F9-ADE9BE35A0E1"
gosub "B406_PocketDoorNominalSizes_FWM"

	_startID	= "753C9B9E-4E5D-4B09-90F9-ADE9BE35A0E1"
	_endID		= "DB1CCE08-4728-449C-9F18-06D5157D1942"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "DB1CCE08-4728-449C-9F18-06D5157D1942"
	_endID		= "19E55AB1-584F-4DFB-BA36-94F1F4A2F320"
gosub "deleteUnusedListParams_FWM"

	_startID	= "19E55AB1-584F-4DFB-BA36-94F1F4A2F320"
	_endID		= "E48F5135-F3C9-496D-B67A-0F5EB7D81CFD"
gosub "DoorPanelType_FWM"

	_startID	= "E48F5135-F3C9-496D-B67A-0F5EB7D81CFD"
	_endID		= "2AA06BEA-A6E6-406D-87B3-B093A999E1F1"
gosub "RenameMinSpaceParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 1,
													bMigrateDoorHandle		= 1,
													bMigrateKickBoard		= 1,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 0,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return


! ==============================================================================
! B406 - Enable Editing Leaf Sizes in Pocket Doors
! ==============================================================================
"B406_PocketDoorNominalSizes_FWM":
	if actualGuid = _startID  then
		bGetValues =	STORED_PAR_VALUE ("gs_unit_nominal",		_bUnitNominal)			& \
						STORED_PAR_VALUE ("ac_unit_width",			_currUnitWidth)			& \
						STORED_PAR_VALUE ("ac_wallhole_width",		_currWallholeWidth)		& \
						STORED_PAR_VALUE ("ac_egress_width",		_currEgressWidth)		& \
						STORED_PAR_VALUE ("ac_egress_height",		_currEgressHeight)		& \
						STORED_PAR_VALUE ("gs_tolerance_right",		_toleranceRight)		& \
						STORED_PAR_VALUE ("gs_tolerance_left",		_toleranceLeft)			& \
						STORED_PAR_VALUE ("gs_uniform_framewidth",	_bUniformFrameWidth)	& \
						STORED_PAR_VALUE ("gs_frame_width",			_frameWidthUniform)		& \
						STORED_PAR_VALUE ("gs_frame_width_right",	_frameWidthRight)		& \
						STORED_PAR_VALUE ("gs_frame_width_left",	_frameWidthLeft)		& \
						STORED_PAR_VALUE ("gs_pocket_door_frame",	_bDoorFrame)			& \
						STORED_PAR_VALUE ("gs_pocket_frame_thk",	_pocketFrameThickness)

		if _bUnitNominal then
			_posHole = 0.005

			if _bUniformFrameWidth then
				_frameWidthRight	= _frameWidthUniform
				_frameWidthLeft		= _frameWidthUniform
			endif

			if _bDoorFrame then
				_wallholeDiffSum	= _frameWidthRight + _frameWidthLeft + _toleranceRight + _toleranceLeft - _pocketFrameThickness - _posHole
			else
				_wallholeDiffSum	= -_pocketFrameThickness - _posHole
			endif

			ac_wallhole_width	= _currUnitWidth/2 + _wallholeDiffSum
			ac_egress_width		= ac_wallhole_width - _frameWidthRight - _frameWidthLeft - _toleranceRight - _toleranceLeft
			ac_leaf_width		= ac_egress_width
			ac_leaf_height		= _currEgressHeight
		else
			ac_wallhole_width	= _currWallholeWidth
			ac_egress_width		= _currEgressWidth
			ac_leaf_width		= _currEgressWidth
			ac_leaf_height		= _currEgressHeight
		endif

		parameters	ac_wallhole_width	= ac_wallhole_width,
					ac_egress_width		= ac_egress_width,
					ac_leaf_width		= ac_leaf_width,
					ac_leaf_height		= ac_leaf_height

		actualGuid = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

! ==============================================================================
! DoorPanelType
! ==============================================================================
"DoorPanelType_FWM":
	if actualGUID = _startID then
		_gs_door_typ_m = 0		! Empty
		bHas = DELETED_PAR_VALUE("gs_door_typ_m", _gs_door_typ_m)
	
		iDoorPanelType = _gs_door_typ_m
		parameters iDoorPanelType = iDoorPanelType
	
		actualGUID = _endID
	endif
return

! ==============================================================================
! RenameMinSpaceParams
! ==============================================================================
"RenameMinSpaceParams_FWM":
	if actualGUID = _startID then
	    bHas =  DELETED_PAR_VALUE("ms_openingSWidth1", _ms_openingSWidth1) &\
	            DELETED_PAR_VALUE("ms_openingSWidth2", _ms_openingSWidth2) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth1", _ms_oppositeSWidth1) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth2", _ms_oppositeSWidth2)
	            
	    if bHas then
	        msOpeningSideWidth2 = _ms_openingSWidth1
	        msOpeningSideWidth1 = _ms_openingSWidth2
	        msOppositeSideWidth2 = _ms_oppositeSWidth1
	        msOppositeSideWidth1 = _ms_oppositeSWidth2
	        
	        parameters  msOpeningSideWidth2 = msOpeningSideWidth2,
	                    msOpeningSideWidth1 = msOpeningSideWidth1,
	                    msOppositeSideWidth2 = msOppositeSideWidth2,
	                    msOppositeSideWidth1 = msOppositeSideWidth1
		endif
	    
	    actualGUID = _endID
	endif
return

