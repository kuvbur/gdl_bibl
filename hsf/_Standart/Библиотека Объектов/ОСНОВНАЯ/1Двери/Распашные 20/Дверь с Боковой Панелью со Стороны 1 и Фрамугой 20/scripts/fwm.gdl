
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "EAC29F67-F25F-4512-9707-E2AE77EF291D"
	_startID	= "F3B7AF3E-2968-449F-8464-D7894B429AED"
	_endID		= "DB75E21A-608C-4630-8713-CBB8AE8333F3"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "DB75E21A-608C-4630-8713-CBB8AE8333F3"
	_endID		= "6EC5E028-255C-4A1B-BCAB-CB0640D952DC"
gosub "NewSillTypes_FWM"

	_startID	= "6EC5E028-255C-4A1B-BCAB-CB0640D952DC"
	_endID		= "B86A85CA-DEE3-4E04-AF2F-06806B5A5349"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "B86A85CA-DEE3-4E04-AF2F-06806B5A5349"
	_endID		= "A2A1D523-8C3F-4750-B43A-FE4580840C5E"
gosub "EnablePanicBarHandle_FWM"

	_startID	= "A2A1D523-8C3F-4750-B43A-FE4580840C5E"
	_endID		= "758309C5-663F-4F92-89A0-8038A84E34B7"
gosub "MigrateNewSurfaces_FWM"

	_startID	= "758309C5-663F-4F92-89A0-8038A84E34B7"
	_endID		= "7E0317BB-445B-4EA7-A80C-838D9949F75F"
gosub "AddFrenchPanel_FWM"

	_startID	= "7E0317BB-445B-4EA7-A80C-838D9949F75F"
	_endID		= "DFAE7CAD-4A71-485A-8F36-A809C086D181"
gosub "deleteUnusedListParams_FWM"

	_startID	= "DFAE7CAD-4A71-485A-8F36-A809C086D181"
	_endID		= "3662A120-1C79-4A05-AF28-5268572583DB"
gosub "DoorPanelType_FWM"

	_startID	= "3662A120-1C79-4A05-AF28-5268572583DB"
	_endID		= "0CD5567B-B987-4DF1-9E6A-238AA400F121"
gosub "RenameMinSpaceParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 1,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 1,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 0,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! EnablePanicBarHandle
! ==============================================================================
"EnablePanicBarHandle_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! MigrateNewSurfaces
! ==============================================================================
"MigrateNewSurfaces_FWM":
	if actualGUID = _startID then
		call "Migration_DoorWindow" parameters all	iMigrationStep				= 2,
													bMigrateTransomSurface		= 1,
													bMigrateSidelight1Surface	= 1,
													bMigrateSidelight2Surface	= 0,
								returned_parameters	bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGUID = ""
		endif
	endif
return

! ==============================================================================
! AddFrenchPanel
! ==============================================================================
"AddFrenchPanel_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

! ==============================================================================
! DoorPanelType
! ==============================================================================
"DoorPanelType_FWM":
	if actualGUID = _startID then
		_gs_door_typ_m = 0		! Empty
		bHas = DELETED_PAR_VALUE("gs_door_typ_m", _gs_door_typ_m)
	
		iDoorPanelType = _gs_door_typ_m
		parameters iDoorPanelType = iDoorPanelType
	
		actualGUID = _endID
	endif
return

! ==============================================================================
! RenameMinSpaceParams
! ==============================================================================
"RenameMinSpaceParams_FWM":
	if actualGUID = _startID then
	    bHas =  DELETED_PAR_VALUE("ms_openingSWidth1", _ms_openingSWidth1) &\
	            DELETED_PAR_VALUE("ms_openingSWidth2", _ms_openingSWidth2) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth1", _ms_oppositeSWidth1) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth2", _ms_oppositeSWidth2)
	            
	    if bHas then
	        msOpeningSideWidth2 = _ms_openingSWidth1
	        msOpeningSideWidth1 = _ms_openingSWidth2
	        msOppositeSideWidth2 = _ms_oppositeSWidth1
	        msOppositeSideWidth1 = _ms_oppositeSWidth2
	        
	        parameters  msOpeningSideWidth2 = msOpeningSideWidth2,
	                    msOpeningSideWidth1 = msOpeningSideWidth1,
	                    msOppositeSideWidth2 = msOppositeSideWidth2,
	                    msOppositeSideWidth1 = msOppositeSideWidth1
		endif
	    
	    actualGUID = _endID
	endif
return

