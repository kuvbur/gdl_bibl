
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "7EBF10FF-1717-4221-AF60-24B99BAD3617"
	_startID	= "65357FF1-4B0E-49A9-8B0F-61836F477408"
	_endID		= "D606A080-7D3E-492E-A4B2-F2887E63462A"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "D606A080-7D3E-492E-A4B2-F2887E63462A"
	_endID		= "AA0E01FB-C60A-43B8-A8E1-917020F921F2"
gosub "NewSillTypes_FWM"

	_startID	= "AA0E01FB-C60A-43B8-A8E1-917020F921F2"
	_endID		= "722941DD-B7DC-43B5-BC07-5283FD98E56C"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "722941DD-B7DC-43B5-BC07-5283FD98E56C"
	_endID		= "72F270AB-536B-4349-8DFE-1407243537CA"
gosub "EnablePanicBarHandle_FWM"

	_startID	= "72F270AB-536B-4349-8DFE-1407243537CA"
	_endID		= "8AF393FC-32BC-4790-87BE-472D9D317767"
gosub "MigrateNewSurfaces_FWM"

	_startID	= "8AF393FC-32BC-4790-87BE-472D9D317767"
	_endID		= "88CF4DC4-EB4F-4452-AE5A-A6796693F17B"
gosub "AddFrenchPanel_FWM"

	_startID	= "88CF4DC4-EB4F-4452-AE5A-A6796693F17B"
	_endID		= "06A771CE-5FBB-4AAE-B54E-8B5F7ECA81F2"
gosub "deleteUnusedListParams_FWM"

	_startID	= "06A771CE-5FBB-4AAE-B54E-8B5F7ECA81F2"
	_endID		= "4C367110-5FF1-426A-9E99-E4C9E3BF1C0F"
gosub "DoorPanelType_FWM"

	_startID	= "4C367110-5FF1-426A-9E99-E4C9E3BF1C0F"
	_endID		= "7F8D8476-FD91-4E27-80D3-FAC14EC2B36A"
gosub "RenameMinSpaceParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 1,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 1,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 0,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! EnablePanicBarHandle
! ==============================================================================
"EnablePanicBarHandle_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! MigrateNewSurfaces
! ==============================================================================
"MigrateNewSurfaces_FWM":
	if actualGUID = _startID then
		call "Migration_DoorWindow" parameters all	iMigrationStep				= 2,
													bMigrateTransomSurface		= 1,
													bMigrateSidelight1Surface	= 1,
													bMigrateSidelight2Surface	= 1,
								returned_parameters	bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGUID = ""
		endif
	endif
return

! ==============================================================================
! AddFrenchPanel
! ==============================================================================
"AddFrenchPanel_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

! ==============================================================================
! DoorPanelType
! ==============================================================================
"DoorPanelType_FWM":
	if actualGUID = _startID then
		_gs_door_typ_m = 0		! Empty
		bHas = DELETED_PAR_VALUE("gs_door_typ_m", _gs_door_typ_m)
	
		iDoorPanelType = _gs_door_typ_m
		parameters iDoorPanelType = iDoorPanelType
	
		actualGUID = _endID
	endif
return

! ==============================================================================
! RenameMinSpaceParams
! ==============================================================================
"RenameMinSpaceParams_FWM":
	if actualGUID = _startID then
	    bHas =  DELETED_PAR_VALUE("ms_openingSWidth1", _ms_openingSWidth1) &\
	            DELETED_PAR_VALUE("ms_openingSWidth2", _ms_openingSWidth2) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth1", _ms_oppositeSWidth1) &\
	            DELETED_PAR_VALUE("ms_oppositeSWidth2", _ms_oppositeSWidth2)
	            
	    if bHas then
	        msOpeningSideWidth2 = _ms_openingSWidth1
	        msOpeningSideWidth1 = _ms_openingSWidth2
	        msOppositeSideWidth2 = _ms_oppositeSWidth1
	        msOppositeSideWidth1 = _ms_oppositeSWidth2
	        
	        parameters  msOpeningSideWidth2 = msOpeningSideWidth2,
	                    msOpeningSideWidth1 = msOpeningSideWidth1,
	                    msOppositeSideWidth2 = msOppositeSideWidth2,
	                    msOppositeSideWidth1 = msOppositeSideWidth1
		endif
	    
	    actualGUID = _endID
	endif
return

