
targetGUID = TO_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

gosub "RenameMinSpaceParams_BWM"

gosub "DoorPanelType_BWM"

gosub "deleteUnusedListParams_BWM"

gosub "AddFrenchPanel_BWM"

gosub "MigrateNewSurfaces_BWM"

gosub "EnablePanicBarHandle_BWM"

gosub "HandleToSecondLeaf_BWM"

gosub "NewDemolitionSymbol_BWM"

gosub "NewSillTypes_BWM"

gosub "B406_NewFuctionsCommon_BWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid targetGuid

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! RenameMinSpaceParams
! ==============================================================================
"RenameMinSpaceParams_BWM":
	if targetGUID # "" then
	    newparameter "ms_openingSWidth1", "Length"
		newparameter "ms_openingSWidth2", "Length"
	    newparameter "ms_oppositeSWidth1", "Length"
		newparameter "ms_oppositeSWidth2", "Length"
		
		ms_openingSWidth1 = msOpeningSideWidth2
		ms_openingSWidth2 = msOpeningSideWidth1
	    ms_oppositeSWidth1 = msOppositeSideWidth2
	    ms_oppositeSWidth2 = msOppositeSideWidth1
		
		parameters 	ms_openingSWidth1 = ms_openingSWidth1,
					ms_openingSWidth2 = ms_openingSWidth2,
	                ms_oppositeSWidth1 = ms_oppositeSWidth1,
	                ms_oppositeSWidth2 = ms_oppositeSWidth2
	endif
return

! ==============================================================================
! DoorPanelType
! ==============================================================================
"DoorPanelType_BWM":
	if targetGUID # "" then
		newparameter "gs_door_typ_m", "Integer"
	
		gs_door_typ_m = iDoorPanelType
		parameters gs_door_typ_m = gs_door_typ_m
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_BWM":
	if targetGUID # "" then
		! Empty Step migrate backward every case
	endif
return

! ==============================================================================
! AddFrenchPanel
! ==============================================================================
"AddFrenchPanel_BWM":
	if targetGUID # "" then
		if (iMullionStyle_09 = 17) then	! MULLION_FRENCH_PANEL
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! MigrateNewSurfaces
! ==============================================================================
"MigrateNewSurfaces_BWM":
	if targetGUID # "" then
		call "Migration_DoorWindow" parameters all	iMigrationStep				= 2,
													bMigrateTransomSurface		= 0,
													bMigrateSidelight1Surface	= 0,
													bMigrateSidelight2Surface	= 1,
								returned_parameters	bMigrationSuccess
	
		if not(bMigrationSuccess) then
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! EnablePanicBarHandle
! ==============================================================================
"EnablePanicBarHandle_BWM":
	if targetGUID # "" then
		if (gs_handle_type_m = 23) | (gs_handle_type_m = 24) then	! Panic Bar 1, Panic Bar 2
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! HandleToSecondLeaf
! ==============================================================================
"HandleToSecondLeaf_BWM":
	if targetGUID # "" then
		LEAF_2L_DOUBLE_EGRESS = 7
		if	(gs_handle_uniform & (gs_handle_type_m > 0) & (gs_leaf_optype_m <> LEAF_2L_DOUBLE_EGRESS)) |\
			(not(gs_handle_uniform) & (gs_handle_type2_m > 0) & (gs_leaf_optype_m <> LEAF_2L_DOUBLE_EGRESS)) |\
			(not(gs_handle_uniform) & (gs_handle_type_m <> gs_handle_type2_m) & (gs_leaf_optype_m = LEAF_2L_DOUBLE_EGRESS)) then
	
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_BWM":
	if targetGUID # "" then
		if (iTypeDemolitionFill = 4) then		! RENOVATION_FILL_NEW_FULL
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_BWM":
	if targetGUID # "" then
		if gs_sill_outside & (iSillType = 1	|\		! SILL_TIMBER
							  iSillType = 4	|\		! SILL_CERAMIC
							  iSillType = 5	|\		! SILL_ETHERNIT
							  iSillType = 6	|\		! SILL_CONCRETE_TUB
							  iSillType = 7	|\		! SILL_ALUMINIUM
							  iSillType = 10 |\		! SILL_SOLID_22
							  iSillType = 11 ) then	! SILL_METAL_1
			targetGUID = ""
		endif

		if	(gs_sill_outside_sl_right | gs_sill_outside_sl_left) &\
			(iSillType = 4	|\		! SILL_CERAMIC
			 iSillType = 5	|\		! SILL_ETHERNIT
			 iSillType = 6	|\		! SILL_CONCRETE_TUB
			 iSillType = 7	|\		! SILL_ALUMINIUM
			 iSillType = 10 |\		! SILL_SOLID_22
			 iSillType = 11 ) then	! SILL_METAL_1

			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_BWM":
	if targetGUID # "" then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 1,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 1,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 0,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if not(bMigrationSuccess) then
			targetGUID = ""
		endif
	endif
return

