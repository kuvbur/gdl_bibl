
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "CA124692-C1CA-436E-AECB-584928660147"
	_startID	= "113DFFD1-6BA3-449A-929D-1DED27017998"
	_endID		= "AA8E36EB-05C4-487C-B759-D146E1938E61"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "AA8E36EB-05C4-487C-B759-D146E1938E61"
	_endID		= "4A3B4752-DC4B-48B0-A034-E7D5B1E28A62"
gosub "NewOplines_FWM"

	_startID	= "4A3B4752-DC4B-48B0-A034-E7D5B1E28A62"
	_endID		= "FDE76DAD-CCCC-437C-BA00-EB615AC09F5F"
gosub "NewSillTypes_FWM"

	_startID	= "FDE76DAD-CCCC-437C-BA00-EB615AC09F5F"
	_endID		= "07A29C31-6335-4C98-A952-03BD649E094B"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "07A29C31-6335-4C98-A952-03BD649E094B"
	_endID		= "71E5C21B-1FCF-437B-99F4-B1A443E4900A"
gosub "New_Corner_Column_Surface_FWM"

	_startID	= "71E5C21B-1FCF-437B-99F4-B1A443E4900A"
	_endID		= "90C8B06D-001C-4D29-A4A9-B7C96A28F281"
gosub "deleteUnusedListParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 0,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 1,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! NewOplines
! ==============================================================================
"NewOplines_FWM":
	gs_openLength_2D = 0
	parameters	gs_openLength_2D = gs_openLength_2D
	actualGUID = _endID
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! New_Corner_Column_Surface
! ==============================================================================
"New_Corner_Column_Surface_FWM":
	if actualGUID = _startID then
		matCornerColumn = gs_frame_mat
		parameters matCornerColumn = matCornerColumn
		
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

