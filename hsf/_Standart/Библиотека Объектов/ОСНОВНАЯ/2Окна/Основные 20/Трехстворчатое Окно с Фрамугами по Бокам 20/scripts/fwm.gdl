
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "79FABDC3-153D-446E-8A71-621F6C1BA26B"
	_startID	= "F36DA274-1B7B-4E49-970B-B1F474795DBF"
	_endID		= "C787F485-F642-4603-83A8-08F04E3CDEAC"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "C787F485-F642-4603-83A8-08F04E3CDEAC"
	_endID		= "34B1D767-88CE-4A6E-B608-53F5AFEAE902"
gosub "NewOplines_FWM"

	_startID	= "34B1D767-88CE-4A6E-B608-53F5AFEAE902"
	_endID		= "8F46165D-54D9-4029-99D8-2FDAA6993647"
gosub "NewSillTypes_FWM"

	_startID	= "8F46165D-54D9-4029-99D8-2FDAA6993647"
	_endID		= "D369269F-2EBA-4516-BA39-720789EDAA8D"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "D369269F-2EBA-4516-BA39-720789EDAA8D"
	_endID		= "825F0F8A-4913-4841-8E47-BEFE2FEEB017"
gosub "New_Corner_Column_Surface_FWM"

	_startID	= "825F0F8A-4913-4841-8E47-BEFE2FEEB017"
	_endID		= "576003E5-E9EA-41ED-88DB-80DFEBAAEF8C"
gosub "deleteUnusedListParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 0,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 1,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! NewOplines
! ==============================================================================
"NewOplines_FWM":
	gs_openLength_2D = 0
	parameters	gs_openLength_2D = gs_openLength_2D
	actualGUID = _endID
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! New_Corner_Column_Surface
! ==============================================================================
"New_Corner_Column_Surface_FWM":
	if actualGUID = _startID then
		matCornerColumn = gs_frame_mat
		parameters matCornerColumn = matCornerColumn
		
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

