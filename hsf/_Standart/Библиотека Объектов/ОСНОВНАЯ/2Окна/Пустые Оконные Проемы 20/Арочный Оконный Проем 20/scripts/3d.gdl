
! Contact person: <SzL, NJ>

! =============================================================================

lod3D = 0										! Don't draw anything
if iDWDet3D = DETLEVEL_DW3D_SIMPLE then lod3D = 1	! Simple
if iDWDet3D = DETLEVEL_DW3D_DETAILED then lod3D = 2	! Detailed
if iDWDet3D = DETLEVEL_DW3D_CHI_SIMPLE | iDWDet3D = DETLEVEL_DW3D_CHI_DETAILED then
	if (GLOB_CONTEXT = 4 | GLOB_CONTEXT = 24 | GLOB_CONTEXT = 6) then
		lod3D = 5								! Chinese
	else
		lod3D = 2
	endif
endif

if not(gs_shadow) then shadow off

if h < B - A/2 then h = B - A/2
if h > B then h = B - 0.02

! =============================================================================
! GS Window Opening Functions
! =============================================================================

call "GS Window Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					nArchResolution			= gs_res,
					gs_IsCurved				= 0,
					bParalellInCurvedWalls	= bParalellInCurvedWalls,
					lod3D					= lod3D,
					ac_wallhole_height		= B,
					ac_reveal_height		= B


! =============================================================================
! Preview in Object Settings Dialog
! =============================================================================

pen WALL_VIEW_PEN
resol gs_res

if GLOB_PREVIEW_MODE # 0 | GLOB_FEEDBACK_MODE then

	bFullArc = (abs(A/2 - B + gs_shoulder_height) < EPS)

	openingArchHeight = b-gs_shoulder_height
	rr = (openingArchHeight^2 + (a/2)^2) / 2 / openingArchHeight

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
		put  A/2 + 0.2,	-0.2,		8,
			 A/2 + 0.2,	 B + 0.2,	8,
			-A/2 - 0.2,	 B + 0.2,	8,
			-A/2 - 0.2,	-0.2,		8,
			 A/2 + 0.2,	-0.2,		-1
	endif

	if GLOB_PREVIEW_MODE = 2 then
		put -A/2,				 	0,						0,
			A/2,		 			0,						0,
			A/2,		 			gs_shoulder_height-eps,	bFullArc,
			A/2,		 			gs_shoulder_height,		1,
			1/tan(90+acs(a/2/rr)), 	1, 						800,
			0, 						b, 						1001,
			-1, 					0, 						800,
			-A/2, 					gs_shoulder_height, 	1000 + bFullArc,
			-A/2,					0,						-1

		extrude nsp/3, 0, 0, WALL_THICKNESS, 16+32, get(nsp)
	else
		put -A/2,				 	0,						15,
			A/2,		 			0,						15,
			A/2,		 			gs_shoulder_height-eps,	13 + not(bFullArc) * 2,
			A/2,		 			gs_shoulder_height,		13,
			1/tan(90+acs(a/2/rr)), 	1, 						800,
			0, 						b, 						1079,
			-1, 					0, 						800,
			-A/2, 					gs_shoulder_height, 	1013 + not(bFullArc) * 2,
			-A/2,					0,						-1

		cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, nsp/3, WALL_THICKNESS, get(nsp)
	endif
	del 1
	if GLOB_PREVIEW_MODE # 0 then end
endif


! =============================================================================
! Hotspot editing
! =============================================================================

curvedWall	= abs(WIDO_ORIG_DIST) > EPS

if not(curvedWall) then
	del top
	if WIDO_REVEAL_SIDE then
		addz WIDO_FRAME_THICKNESS
	else
		mulz -1
	endif
	addz WIDO_SILL

	unID = 1

	hotspot A/2,  0,					0, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot A/2, -1,					0, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot A/2,  gs_shoulder_height,	0, unID, gs_shoulder_height, 2:		unID=unID+1

	hotspot -A/2,  0,					0, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot -A/2, -1,					0, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot -A/2,  gs_shoulder_height,	0, unID, gs_shoulder_height, 2:		unID=unID+1

	hotspot A/2,  0,					-WALL_THICKNESS, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot A/2, -1,					-WALL_THICKNESS, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot A/2,  gs_shoulder_height,	-WALL_THICKNESS, unID, gs_shoulder_height, 2:		unID=unID+1

	hotspot -A/2,  0,					-WALL_THICKNESS, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot -A/2, -1,					-WALL_THICKNESS, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot -A/2,  gs_shoulder_height,	-WALL_THICKNESS, unID, gs_shoulder_height, 2:		unID=unID+1

	del 2
endif
