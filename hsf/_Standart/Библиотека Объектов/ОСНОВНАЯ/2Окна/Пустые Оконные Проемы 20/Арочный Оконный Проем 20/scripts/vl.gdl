
! Contact person: <SzL, NJ>

! =============================================================================
! GS Window Opening Functions
! =============================================================================

call "GS Window Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					bParalellInCurvedWalls	= bParalellInCurvedWalls,
					ac_reveal_height		= B


! =============================================================================
! Orientation
! =============================================================================

ac_OpeningSide = ""
parameters ac_OpeningSide = ac_OpeningSide


! =============================================================================
! IFC Parameters
! =============================================================================

ifc_optype = 10		! WIN_OP_USER_DEFINED
parameters ifc_optype = ifc_optype
call "IFC_optype_window" parameters ifc_optype = ifc_optype

lock "ifc_optypestr"


if GLOB_MODPAR_NAME = "openingArchHeight" | GLOB_MODPAR_NAME = "B" then
	gs_shoulder_height = B - openingArchHeight
	parameters gs_shoulder_height = B - openingArchHeight
endif

if GLOB_MODPAR_NAME = "gs_shoulder_height" then
	openingArchHeight = B - gs_shoulder_height
	parameters openingArchHeight = B - gs_shoulder_height
endif

values "A"	 				range (0, )
values "B"	 				range (gs_shoulder_height, )
values "gs_shoulder_height" range [B - A/2,)
values "openingArchHeight" 	range (0, A/2]

values{2} "iTypeCurvedWallCut" 	CUT_PARALLEL, `Параллельно`,
								CUT_RADIAL, `Радиально`
values "gs_res" range[3,)
values "gs_frame_thk" range[0,]

if gs_pen_2D <= 0 then parameters gs_pen_2D = 3

! Wall Contour Left/Right --------------------------------------------------------------

gs_stack_right = not(gs_stack_right_contour)
parameters gs_stack_right = not(gs_stack_right_contour)

gs_stack_left = not(gs_stack_left_contour)
parameters gs_stack_left = not(gs_stack_left_contour)


! =============================================================================
! 3D Detail Level
! =============================================================================

if gs_3DDetEnable =0 then

    values{2} "iDWDet3D" 	DETLEVEL_DW3D_DETAILED,		`Детальный`,
						    DETLEVEL_DW3D_SIMPLE,		`Простой`,
						    DETLEVEL_DW3D_OFF,			`Откл.`

else
	dim n3DScaleValueMask[]
	dim n3DScaleValueMask_m[]

	i=1
	if bittest( gs_3DDetEnable, 0) then                                              ! Scale Sensitive in Value list, 1
				n3DScaleValueMask[i] = stDetail[1]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_SCALESENS
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 1) then                                              ! Detailed in Value list,2
				n3DScaleValueMask[i] = stDetail[2]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_DETAILED
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 2) then                                              ! Simple in Value list,4
				n3DScaleValueMask[i] = stDetail[3]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_SIMPLE
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 3) then                                              ! Off in Value list,8
				n3DScaleValueMask[i] = stDetail[4]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_OFF
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 4) then                                              ! Chinese Simple in Value list,16
				n3DScaleValueMask[i] = stDetail[5]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_CHI_SIMPLE
				i = i + 1
	endif
	if bittest( gs_3DDetEnable, 5) then                                              ! Chinese Detailed in Value list,32
				n3DScaleValueMask[i] = stDetail[6]
				n3DScaleValueMask_m[i] = DETLEVEL_DW3D_CHI_DETAILED
				i = i + 1
	endif

	values{2} "iDWDet3D" n3DScaleValueMask_m, n3DScaleValueMask
endif


!**************** Swedish list parameter control **********************
if gs_SWE_listParams_contr > 0 then
	call "SWE_ListParameters_control" parameters gs_SWE_listParams_contr = gs_SWE_listParams_contr,
			swe_list_f1				= swe_list_f1
endif
