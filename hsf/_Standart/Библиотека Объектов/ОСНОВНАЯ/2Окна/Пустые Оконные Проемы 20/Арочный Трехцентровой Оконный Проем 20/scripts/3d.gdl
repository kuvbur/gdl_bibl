
! Contact person: <SzL, NJ>

! =============================================================================

lod3D = 0										! Don't draw anything
if iDWDet3D = DETLEVEL_DW3D_SIMPLE then lod3D = 1	! Simple
if iDWDet3D = DETLEVEL_DW3D_DETAILED then lod3D = 2	! Detailed
if iDWDet3D = DETLEVEL_DW3D_CHI_SIMPLE | iDWDet3D = DETLEVEL_DW3D_CHI_DETAILED then
	if (GLOB_CONTEXT = 4 | GLOB_CONTEXT = 24 | GLOB_CONTEXT = 6) then
		lod3D = 5								! Chinese
	else
		lod3D = 2
	endif
endif

if not(gs_shadow) then shadow off


! =============================================================================
! GS Window Opening Functions
! =============================================================================

call "GS Window Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					smallRadiusEllips		= shr,
					nArchResolution			= gs_res,
					gs_IsCurved				= 0,
					bParalellInCurvedWalls	= bParalellInCurvedWalls,
					lod3D					= lod3D


! =============================================================================
! Preview in Object Settings Dialog
! =============================================================================

pen WALL_VIEW_PEN
resol gs_res

h1 = B - h

if GLOB_PREVIEW_MODE # 0 | GLOB_FEEDBACK_MODE then

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	! Half round shape
	if A/2 - h1 <= EPS | h1 <= 0 then
		h = B - A/2
	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
			put  A/2 + 0.2,	-0.2,		8,
				 A/2 + 0.2,	 B + 0.2,	8,
				-A/2 - 0.2,	 B + 0.2,	8,
				-A/2 - 0.2,	-0.2,		8,
				 A/2 + 0.2,	-0.2,		-1
		endif

	if GLOB_PREVIEW_MODE = 2 then
			put	A/2,	0,	0,
				A/2,	h,	1,
				-A/2,	h,	1001,
				-A/2,	0,	0,
				A/2,	0,	-1

			extrude nsp/3, 0, 0, WALL_THICKNESS, 16+32, get(nsp)
		else
			put	A/2,	0,	15,
				A/2,	h,	79,
				-A/2,	h,	1079,
				-A/2,	0,	15,
				A/2,	0,	-1

		  cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, nsp/3, WALL_THICKNESS, get(nsp)
		endif
		goto 10
	endif

	! Two round corner shape
	if h1 - r1 <= EPS | r1 <= 0 then
		r1 = h1
	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
			put  A/2 + 0.2,	-0.2,		8,
				 A/2 + 0.2,	 B + 0.2,	8,
				-A/2 - 0.2,	 B + 0.2,	8,
				-A/2 - 0.2,	-0.2,		8,
				 A/2 + 0.2,	-0.2,		-1
		endif

	if GLOB_PREVIEW_MODE = 2 then
			put	A/2,	0,	0,
				A/2,	h,	1,
				A/2-h1,	b,	1001,
				-A/2+h1,b,	1,
				-A/2,	h,	1001,
				-A/2,	0,	0,
				A/2,	0,	-1

			extrude nsp/3, 0, 0, WALL_THICKNESS, 16+32, get(nsp)
		else
			put	A/2,	0,	15,
				A/2,	h,	79,
				A/2-h1,	b,	1079,
				-A/2+h1,b,	79,
				-A/2,	h,	1079,
				-A/2,	0,	15,
				A/2,	0,	-1

			  cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, nsp/3, WALL_THICKNESS, get(nsp)
		endif
		goto 10
	endif

	! Ellipse shape
	m = A/2 - r1
	n = h1 - r1
	c = sqr(m^2+n^2)/2
	f = ((2*c^2)/n)-n
	r2= f + h1
	alpha = 2 * asn(c/(f+n))
	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
		put  A/2 + 0.2,	-0.2,		8,
			 A/2 + 0.2,	 B + 0.2,	8,
			-A/2 - 0.2,	 B + 0.2,	8,
			-A/2 - 0.2,	-0.2,		8,
			 A/2 + 0.2,	-0.2,		-1
	endif

	if GLOB_PREVIEW_MODE = 2 then
		put A/2,			0,					0,
			A/2,			h,					1,
			r2*sin(alpha),	r1*cos(alpha)+h,	1001,
			-r2*sin(alpha),	r1*cos(alpha)+h,	1001,
			-A/2,			h,					1001,
			-A/2,			0,					0,
			A/2,			0,					-1

		extrude nsp/3, 0, 0, WALL_THICKNESS, 16+32, get(nsp)
	else
		put A/2,			0,					15,
			A/2,			h,					79,
			 r2*sin(alpha),r1*cos(alpha)+h,1079,
			-r2*sin(alpha),r1*cos(alpha)+h,1079,
			-A/2,			h,					1079,
			-A/2,			0,					15,
			A/2,			0,					-1

		cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, nsp/3, WALL_THICKNESS, get(nsp)
	endif

	10:
	del 1
	if GLOB_PREVIEW_MODE # 0 then end
endif


! =============================================================================
! Hotspot editing
! =============================================================================

curvedWall	= abs(WIDO_ORIG_DIST) > EPS

if not(curvedWall) | (curvedWall & iTypeCurvedWallCut = CUT_PARALLEL) then

	del top
	if WIDO_REVEAL_SIDE then
		addz WIDO_FRAME_THICKNESS
	else
		mulz -1
	endif
	addz WIDO_SILL

	unID = 1

	hotspot A/2,  0,					0, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot A/2, -1,					0, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot A/2,  gs_shoulder_height,	0, unID, gs_shoulder_height, 2:		unID=unID+1

	hotspot 0,B,0,unID:unID=unID+1

	hotspot -A/2,  0,					0, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot -A/2, -1,					0, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot -A/2,  gs_shoulder_height,	0, unID, gs_shoulder_height, 2:		unID=unID+1

	hotspot A/2,  0,					-WALL_THICKNESS, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot A/2, -1,					-WALL_THICKNESS, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot A/2,  gs_shoulder_height,	-WALL_THICKNESS, unID, gs_shoulder_height, 2:		unID=unID+1

	hotspot 0,B,-WALL_THICKNESS,unID:unID=unID+1

	hotspot -A/2,  0,					-WALL_THICKNESS, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot -A/2, -1,					-WALL_THICKNESS, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot -A/2,  gs_shoulder_height,	-WALL_THICKNESS, unID, gs_shoulder_height, 2:		unID=unID+1

	del 2
endif
