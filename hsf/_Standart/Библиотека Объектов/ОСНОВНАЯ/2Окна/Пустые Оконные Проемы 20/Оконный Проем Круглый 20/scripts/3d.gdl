
! Contact person: <SzL, NJ>

! =============================================================================
! GS Window Opening Functions
! =============================================================================

lod3D = 1

call "GS Window Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					nArchResolution			= gs_res,
					gs_IsCurved				= 0,
					bParalellInCurvedWalls	= bParalellInCurvedWalls,
					lod3D					= lod3D


! =============================================================================
! Preview in Object Settings Dialog
! =============================================================================

pen WALL_VIEW_PEN
resol gs_res

if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 | GLOB_FEEDBACK_MODE then
	addy b/2
	muly b/a
	cutpolya 2,1,0,
		0,0,979,
		A/2,360,4079
	del 2

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, 5, WALL_THICKNESS,
		 A/2+0.2,-0.2,8,
		 A/2+0.2, B+0.2,8,
		-A/2-0.2, B+0.2,8,
		-A/2-0.2,-0.2,8,
		 A/2+0.2,-0.2,-1

	cutend

	del 1
	end
endif

if (GLOB_PREVIEW_MODE = 2) then		! IS but not surface
	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif
	addy b/2
	muly b/a
	extrude 2, 0, 0, WALL_THICKNESS, 16+32,
		0,		0,		901,
		A/2,	360,	4001
	del 3
	end
endif


! =============================================================================
! Hotspots
! =============================================================================

curvedWall	= abs(WIDO_ORIG_DIST) > EPS

if not(curvedWall) then
	del top
	if WIDO_REVEAL_SIDE then
		addz WIDO_FRAME_THICKNESS
	else
		mulz -1
	endif
	addz WIDO_SILL

	unID = 1

	hotspot  0,	 0,	 0,unID:unID=unID+1
	hotspot  A/2,B/2,0,unID:unID=unID+1
	hotspot  0,	 B,	 0,unID:unID=unID+1
	hotspot -A/2,B/2,0,unID:unID=unID+1

	hotspot  0,	 0,		-WALL_THICKNESS,unID:unID=unID+1
	hotspot  A/2,B/2,	-WALL_THICKNESS,unID:unID=unID+1
	hotspot  0,	 B,		-WALL_THICKNESS,unID:unID=unID+1
	hotspot -A/2,B/2,	-WALL_THICKNESS,unID:unID=unID+1

	del 2
endif
