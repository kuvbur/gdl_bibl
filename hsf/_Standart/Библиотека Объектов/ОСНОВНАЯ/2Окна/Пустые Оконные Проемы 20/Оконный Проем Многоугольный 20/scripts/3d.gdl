
! Contact person: <SzL, NJ>

! =============================================================================
! GS Window Opening Functions
! =============================================================================

call "GS Window Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					gs_IsCurved				= 0,
					bParalellInCurvedWalls	= bParalellInCurvedWalls

UnID = 1
hotspot a/2, 0, 0, UnID : UnID = UnID + 1
hotspot a/2, ac_wallhole_height, 0, UnID : UnID = UnID + 1
hotspot -a/2, 0, 0, UnID : UnID = UnID + 1
hotspot -a/2, ac_wallhole_height, 0, UnID : UnID = UnID + 1

hotspot a/2, 0, -WALL_THICKNESS, UnID : UnID = UnID + 1
hotspot a/2, ac_wallhole_height, -WALL_THICKNESS, UnID : UnID = UnID + 1
hotspot -a/2, 0, -WALL_THICKNESS, UnID : UnID = UnID + 1
hotspot -a/2, ac_wallhole_height, -WALL_THICKNESS, UnID : UnID = UnID + 1


! =============================================================================
! Preview in Object Settings Dialog
! =============================================================================

pen WALL_VIEW_PEN

if fra(gs_numOfSides/2) <> 0 then
	temp = 90
else
	temp = 0
endif


if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 | GLOB_FEEDBACK_MODE then
	resol gs_numOfSides

	addy b/2
	muly b/a
	rotz temp
	cutpolya 2,1,0,
		0,0,915,
		a/2,360,4015
	del 3

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, 5, WALL_THICKNESS,
		 a/2+0.2,-0.2,8,
		 a/2+0.2,b+0.2,8,
		-a/2-0.2,b+0.2,8,
		-a/2-0.2,-0.2,8,
		 a/2+0.2,-0.2,-1

	cutend

	del 1
	end
endif

if GLOB_PREVIEW_MODE = 2 then
	resol gs_numOfSides

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif
	addy b/2
	muly b/a
	rotz temp
	extrude 2, 0, 0, WALL_THICKNESS, 16+32,
		0,		0,		900,
		A/2,	360,	4000
	del 4

	end
endif
