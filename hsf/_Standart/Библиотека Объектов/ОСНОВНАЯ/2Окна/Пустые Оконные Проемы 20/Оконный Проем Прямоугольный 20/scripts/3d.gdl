
! Contact person: <SzL, NJ>

! =============================================================================

lod3D = 0										! Don't draw anything
if iDWDet3D = DETLEVEL_DW3D_SIMPLE then lod3D = 1	! Simple
if iDWDet3D = DETLEVEL_DW3D_DETAILED then lod3D = 2	! Detailed
if iDWDet3D = DETLEVEL_DW3D_CHI_SIMPLE | iDWDet3D = DETLEVEL_DW3D_CHI_DETAILED then
	if (GLOB_CONTEXT = 4 | GLOB_CONTEXT = 24 | GLOB_CONTEXT = 6) then
		lod3D = 5								! Chinese
	else
		lod3D = 2
	endif
endif

if not(gs_shadow) then shadow off


! =============================================================================
! GS Corner Window Opening Functions
! =============================================================================

call "GS Corner Window Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					gs_IsCurved				= 0,
					bParalellInCurvedWalls	= bParalellInCurvedWalls,
					lod3D					= lod3D,
					ac_wallhole_width		= A,
					ac_wallhole_height		= B


! =============================================================================
! Preview in Object Settings Dialog
! =============================================================================

pen WALL_VIEW_PEN

if GLOB_PREVIEW_MODE # 0 | GLOB_FEEDBACK_MODE then

	if B >= gs_shoulder_height then	temp_b = B
	if gs_shoulder_height > B then	temp_b = gs_shoulder_height

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
		cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, 10, WALL_THICKNESS,
			A/2+0.2,	-0.2,		8,
			A/2+0.2,	temp_b+0.2,	8,
			-A/2-0.2,	temp_b+0.2,	8,
			-A/2-0.2,	-0.2,		8,
			A/2+0.2,	-0.2,		-1,
			-A/2,		0,			15,
			-A/2,		B,			15,
			A/2,		B,			15,
			A/2,		0,			15,
			-A/2,		0,			-1
	else
		extrude 5, 0, 0, WALL_THICKNESS, 16+32,
			-A/2,	0,	0,
			-A/2,	B,	0,
			A/2,	B,	0,
			A/2,	0,	0,
			-A/2,	0,	-1
	endif
	del 1
	end
endif

