
! Contact person: <SzL, NJ>

! =============================================================================

lod3D = 0										! Don't draw anything
if iDWDet3D = DETLEVEL_DW3D_SIMPLE then lod3D = 1	! Simple
if iDWDet3D = DETLEVEL_DW3D_DETAILED then lod3D = 2	! Detailed
if iDWDet3D = DETLEVEL_DW3D_CHI_SIMPLE | iDWDet3D = DETLEVEL_DW3D_CHI_DETAILED then
	if (GLOB_CONTEXT = 4 | GLOB_CONTEXT = 24 | GLOB_CONTEXT = 6) then
		lod3D = 5								! Chinese
	else
		lod3D = 2
	endif
endif

if not(gs_shadow) then shadow off


! =============================================================================
! GS Corner Window Opening Functions
! =============================================================================

call "GS Corner Window Opening Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					gs_IsCurved				= 0,
					bParalellInCurvedWalls	= bParalellInCurvedWalls,
					lod3D					= lod3D,
					ac_wallhole_width		= A,
					ac_wallhole_height		= B


! =============================================================================
! Preview in Object Settings Dialog
! =============================================================================

pen WALL_VIEW_PEN

if GLOB_PREVIEW_MODE # 0 | GLOB_FEEDBACK_MODE then

	if bCornerWindow then
		bRightCornerFunction	= 1
		bRightCornerFunction	= (ac_cw_function & ac_corner_window)
		rightCornerAngle		= ac_corner_angle
		rightConnWallThk		= ac_con_wall_thk
	endif

	if bRightCornerFunction & abs(rightCornerAngle) > EPS then
		beta = atn ((rightConnWallThk * sin(rightCornerAngle)) / (WALL_THICKNESS + rightConnWallThk * cos(rightCornerAngle)))
		rx = 1 / tan(rightCornerAngle / 2)

		if rightCornerAngle > 180 then
			overSizeRight = max (0, WALL_THICKNESS / tan(360 - rightCornerAngle + beta) + WIDO_SILL / tan(rightCornerAngle / 2))
		else
			overSizeRight = (WIDO_FRAME_THICKNESS + WIDO_SILL) * rx + max (0, WALL_THICKNESS * tan(rightCornerAngle - beta - 90))
		endif
	else
		overSizeRight = 0
	endif

	if B >= gs_shoulder_height then	temp_b = B
	if gs_shoulder_height > B then	temp_b = gs_shoulder_height

	if not(WIDO_REVEAL_SIDE) then
		addz -WIDO_SILL
	else
		addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS
	endif

	if GLOB_PREVIEW_MODE = 1 | GLOB_PREVIEW_MODE = 3 then
		put	 A/2 + 0.2,	-0.2,			8,
			 A/2 + 0.2,	 temp_b + 0.2,	8,
			-A/2 - 0.2,	 temp_b + 0.2,	8,
			-A/2 - 0.2,	-0.2,			8,
			 A/2 + 0.2,	-0.2,			-1
	endif

	if GLOB_PREVIEW_MODE = 2 then
		put	-A/2,0,					0,
			-A/2,gs_shoulder_height,0,
			 A/2 + overSizeRight,	B,					0,
			 A/2 + overSizeRight,	0,					0,
			-A/2,0,					-1

		extrude nsp/3, 0, 0, WALL_THICKNESS, 16+32, get(nsp)
	else
		put	-A/2,0,					15,
			-A/2,gs_shoulder_height,15,
			 A/2 + overSizeRight,	B,					8 + not((bRightCornerFunction | bCornerWindow) & GLOB_FEEDBACK_MODE) * 7,
			 A/2 + overSizeRight,	0,					15,
			-A/2,0,					-1

		cprism_ WALL_MAT_A, WALL_MAT_B, WALL_MAT_EDGE, nsp/3, WALL_THICKNESS, get(nsp)
	endif

	del 1
	if GLOB_PREVIEW_MODE # 0 then end
endif


! =============================================================================
! Hotspot editing
! =============================================================================

curvedWall	= abs(WIDO_ORIG_DIST) > EPS

if not(curvedWall) | (curvedWall & iTypeCurvedWallCut = CUT_PARALLEL) then
	del top
	if WIDO_REVEAL_SIDE then
		addz WIDO_FRAME_THICKNESS
	else
		mulz -1
	endif
	addz WIDO_SILL

	unID = 1

	hotspot -A/2,  0,					0, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot -A/2, -1,					0, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot -A/2,  gs_shoulder_height,	0, unID, gs_shoulder_height, 2:		unID=unID+1

	hotspot -A/2,  0,					-WALL_THICKNESS, unID, gs_shoulder_height, 1+128:	unID=unID+1
	hotspot -A/2, -1,					-WALL_THICKNESS, unID, gs_shoulder_height, 3:		unID=unID+1
	hotspot -A/2,  gs_shoulder_height,	-WALL_THICKNESS, unID, gs_shoulder_height, 2:		unID=unID+1

	del 2
endif
