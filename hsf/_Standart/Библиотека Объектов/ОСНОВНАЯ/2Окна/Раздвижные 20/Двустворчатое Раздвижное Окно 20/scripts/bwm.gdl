
targetGUID = TO_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

gosub "deleteUnusedListParams_BWM"

gosub "New_Corner_Column_Surface_BWM"

gosub "NewDemolitionSymbol_BWM"

gosub "NewSillTypes_BWM"

gosub "newNorFunctions_BWM"

gosub "B406_NewFuctionsCommon_BWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid targetGuid

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_BWM":
	if targetGUID # "" then
		! Empty Step migrate backward every case
	endif
return

! ==============================================================================
! New_Corner_Column_Surface
! ==============================================================================
"New_Corner_Column_Surface_BWM":
	if targetGUID # "" then
		if matCornerColumn <> gs_frame_mat then
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! newNorFunctions
! ==============================================================================
"newNorFunctions_BWM":
	if targetGUID # "" then
		! TODO B-407 - 2D sliding width is set (not available in AC19) and symbol is not simple detail - ask design

		! constants
		OPENINGTYPE_MAIN_SINGLE		= 1
		OPENINGTYPE_MAIN_DOUBLE		= 2
	
		PANES_UNIFORM	= 1
		PANES_CUSTOM	= 2
	
		MULLION_HVGRID			= 2
		MULLION_EDITABLEGRID	= 3
		MULLION_DIVIDED			= 6
		MULLION_CUSTOM			= 4
		MULLION_SOLID_PANEL		= 15
		! ----------------------------------------------------------------------
		! terminate migration if
		! any new function is used (handle, panes config)
		! ----------------------------------------------------------------------
		if 	(iPanesSizeConfig <> PANES_UNIFORM) |\
			(gs_handle_type_m <> 0) then
	
				targetGUID = ""
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! Editable Grid/Divided sash type is selected (changed hotspot IDs)
		! ----------------------------------------------------------------------

		if (gs_sash_uniform & ((iMullionStyle_00 = MULLION_EDITABLEGRID) | (iMullionStyle_00 = MULLION_DIVIDED)))  |\
			(not(gs_sash_uniform) & ((iMullionStyle_01 = MULLION_EDITABLEGRID) | (iMullionStyle_01 = MULLION_DIVIDED))) then
	
				targetGUID = ""
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! Ledge is ON, and it's not Custom or Solid panel
		! ----------------------------------------------------------------------

		if ((gs_sash_uniform & ((iMullionStyle_00 <> MULLION_CUSTOM) & (iMullionStyle_00 <> MULLION_SOLID_PANEL)))  |\
			(not(gs_sash_uniform) & ((iMullionStyle_01 <> MULLION_CUSTOM) & (iMullionStyle_01 <> MULLION_SOLID_PANEL)))) & (bSashLedge = 1) then
	
				targetGUID = ""
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! the 2 Sashes are not set equally
		! ----------------------------------------------------------------------

		if targetGUID <> "" then
			_bSameSashType = ((not(gs_sash_uniform) & (iMullionStyle_01 = iMullionStyle_03)) | gs_sash_uniform)
			_actSashType = iMullionStyle_01

			if not(_bSameSashType) |\
				(_bSameSashType & (_actSashType = MULLION_HVGRID) & ((vgn_01 <> vgn_03) | (hgn_01 <> hgn_03))) then
				
				targetGUID = ""
			endif
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! the 2 Ventilation Frames over Glazing are not set equally
		! ----------------------------------------------------------------------
		if targetGUID <> "" then
		! iVentilationProfileType
		VENTPROFILE_OVER_FRAME		= 2
		VENTPROFILE_OVER_GLAZING	= 3

			if bVentilationProfile & (iVentilationProfileType = VENTPROFILE_OVER_GLAZING) & \
				(isVentilationProfileInSash_01 <> isVentilationProfileInSash_03) then
				targetGUID = ""
			else
				newparameter "isVentilationProfileInSash_05", "Boolean"
				isVentilationProfileInSash_05 = isVentilationProfileInSash_01
				parameters isVentilationProfileInSash_05 = isVentilationProfileInSash_05
			endif
		endif

		! ----------------------------------------------------------------------
		! set AC18 opening type parameter if Double Sliding
		! else terminate migration
		! ----------------------------------------------------------------------
		if targetGUID <> "" then
			if iOpeningTypeMain = OPENINGTYPE_MAIN_DOUBLE then
				newparameter "gs_optype_m_01", "Integer"
				gs_optype_m_01 = 23
				parameters gs_optype_m_01 = gs_optype_m_01
			else
				targetGUID = ""
			endif
		endif		
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_BWM":
	if targetGUID # "" then
		if (iTypeDemolitionFill = 3 | iTypeDemolitionFill = 4) then		! RENOVATION_FILL_NEW, RENOVATION_FILL_NEW_FULL
			iRevealType = gs_reveal_type_int - 1
			if not((iRevealType = REVEAL_EDGE) | (iRevealType = REVEAL_NO)) then
				targetGUID = ""
			else
				if (iTypeDemolitionFill = 4) then		! RENOVATION_FILL_NEW_FULL
					targetGUID = ""
				endif
			endif
		endif
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_BWM":
	if targetGUID # "" then
		if gs_sill_outside & (iSillType = 17) then		! SILL_DUTCH
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_BWM":
	if targetGUID # "" then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 0,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 1,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if not(bMigrationSuccess) then
			targetGUID = ""
		endif
	endif
return

