
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "819923E0-B524-47F0-969A-FB69565F68C5"
	_startID	= "6483E4E5-00EA-4DBB-996B-3CB9E144A94C"
	_endID		= "73EA333E-7A88-4B96-8355-E7146193D73E"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "73EA333E-7A88-4B96-8355-E7146193D73E"
	_endID		= "45DA3FDB-51EC-4946-928A-323388A91186"
gosub "newNorFunctions_FWM"

	_startID	= "45DA3FDB-51EC-4946-928A-323388A91186"
	_endID		= "B6AF08CF-B6ED-4F72-BC9C-A940973A7611"
gosub "NewSillTypes_FWM"

	_startID	= "B6AF08CF-B6ED-4F72-BC9C-A940973A7611"
	_endID		= "808C6E6D-CF84-4173-80C1-58B9D1B5A043"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "808C6E6D-CF84-4173-80C1-58B9D1B5A043"
	_endID		= "653D5AE8-E160-4376-BC74-92547B8E989B"
gosub "New_Corner_Column_Surface_FWM"

	_startID	= "653D5AE8-E160-4376-BC74-92547B8E989B"
	_endID		= "D640AF4C-A0C7-46E5-9FBE-4D27DD968EF8"
gosub "deleteUnusedListParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 0,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 1,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! newNorFunctions
! ==============================================================================
"newNorFunctions_FWM":
	if actualGUID = _startID then
		_bMigrationSuccess = 1

		! ----------------------------------------------------------------------
		! Editable Grid/Divided changed hotspot IDs, cancel migration
		! new sashtype parameters for both sashes
		! ----------------------------------------------------------------------
		! iMullionStyle
		MULLION_HVGRID			= 2
		MULLION_EDITABLEGRID	= 3
		MULLION_CUSTOM			= 4
		MULLION_DIVIDED			= 6

		_bHasParam = STORED_PAR_VALUE("iMullionStyle_01", _tempSashType)
		if _bHasParam then
			if _tempSashType = MULLION_EDITABLEGRID | _tempSashType = MULLION_DIVIDED then
				! terminate migration if Editable Grid/Divided is selected
				_bMigrationSuccess = 0
			else
				! set Uniform Sashes ON
					gs_sash_uniform = 1
				iMullionStyle_00 = _tempSashType
				parameters gs_sash_uniform = gs_sash_uniform,
							iMullionStyle_00 = iMullionStyle_00
			endif
		endif

		if _bMigrationSuccess = 1 then
		! ----------------------------------------------------------------------
		! set Sash2 parameters equal to Sash 1 parameters
		! ----------------------------------------------------------------------
			if _tempSashType = MULLION_HVGRID then
				_bHasParam = STORED_PAR_VALUE("vgn_01", _tempVgn_01)
				if _bHasParam then
					vgn_03 = _tempVgn_01
					parameters vgn_03 = vgn_03
				endif
		
				_bHasParam = STORED_PAR_VALUE("hgn_01", _tempHgn_01)
				if _bHasParam then
					hgn_03 = _tempHgn_01
					parameters hgn_03 = hgn_03
				endif
		
				_bHasParam = STORED_PAR_VALUE("vgn2_01", _tempVgn2_01)
				if _bHasParam then
					vgn2_03 = _tempVgn2_01
					parameters vgn2_03 = vgn2_03
				endif
		
				_bHasParam = STORED_PAR_VALUE("hgn2_01", _tempHgn2_01)
				if _bHasParam then
					hgn2_03 = _tempHgn2_01
					parameters hgn2_03 = hgn2_03
				endif
			endif
	
			if _tempSashType = MULLION_CUSTOM then
				_bHasParam = DELETED_PAR_VALUE("gs_bMirr_cust_panel2_01", _tempCustSashMirror)
				if _bHasParam then
					gs_bMirr_cust_panel_03 = _tempCustSashMirror
					parameters gs_bMirr_cust_panel_03 = gs_bMirr_cust_panel_03
				endif
		
				_bHasParam = STORED_PAR_VALUE("gs_cust_panel2_01", _tempCustPanel2)
				if _bHasParam & (_tempCustPanel2 <> "") then
					gs_cust_panel_03 = _tempCustPanel2
					parameters gs_cust_panel_03 = gs_cust_panel_03
				endif
			endif

		! ----------------------------------------------------------------------
		! new opening length parameter for second sash
		! ----------------------------------------------------------------------
			_bHasParam = STORED_PAR_VALUE("openingLength2_01", _tempOpeningLength2)
			if _bHasParam then
				openingLength_03 = _tempOpeningLength2
				parameters openingLength_03 = openingLength_03
			endif

		! ----------------------------------------------------------------------
		! Sash 1 vent profile parameter change, new parameter for Sash 2
		! ----------------------------------------------------------------------
			_bHasParam = DELETED_PAR_VALUE("isVentilationProfileInSash_05", _tempVentProfile)
			if _bHasParam then
				isVentilationProfileInSash_01 = _tempVentProfile
				isVentilationProfileInSash_03 = _tempVentProfile
	
				parameters isVentilationProfileInSash_01 = isVentilationProfileInSash_01,
							isVentilationProfileInSash_03 = isVentilationProfileInSash_03
			endif
	
		! ----------------------------------------------------------------------
		! New opening types
		! ----------------------------------------------------------------------
			OPENINGTYPE_MAIN_SINGLE		= 1
			OPENINGTYPE_MAIN_DOUBLE		= 2
	
			iOpeningTypeMain = OPENINGTYPE_MAIN_DOUBLE
			parameters iOpeningTypeMain = iOpeningTypeMain
	
		! ----------------------------------------------------------------------
		! New panel size configuration option
		! ----------------------------------------------------------------------
			PANES_UNIFORM	= 1
			PANES_CUSTOM	= 2
	
			iPanesSizeConfig = PANES_UNIFORM
			parameters iPanesSizeConfig = iPanesSizeConfig
	
			gs_sash_set_to_edit = 1
			parameters gs_sash_set_to_edit = gs_sash_set_to_edit
	
		! ----------------------------------------------------------------------
		! set actualGUID
		! ----------------------------------------------------------------------
			actualGUID = _endID
		else
			actualGUID = ""
		endif
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! New_Corner_Column_Surface
! ==============================================================================
"New_Corner_Column_Surface_FWM":
	if actualGUID = _startID then
		matCornerColumn = gs_frame_mat
		parameters matCornerColumn = matCornerColumn
		
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

