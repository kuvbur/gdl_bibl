
targetGUID = TO_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

gosub "deleteUnusedListParams_BWM"

gosub "New_Corner_Column_Surface_BWM"

gosub "NewDemolitionSymbol_BWM"

gosub "NewSillTypes_BWM"

gosub "newNorFunctions_BWM"

gosub "B406_NewFuctionsCommon_BWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid targetGuid

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_BWM":
	if targetGUID # "" then
		! Empty Step migrate backward every case
	endif
return

! ==============================================================================
! New_Corner_Column_Surface
! ==============================================================================
"New_Corner_Column_Surface_BWM":
	if targetGUID # "" then
		if matCornerColumn <> gs_frame_mat then
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! newNorFunctions
! ==============================================================================
"newNorFunctions_BWM":
	if targetGUID # "" then
	
		! ----------------------------------------------------------------------
		! terminate migration if
		! any new opening type is selected
		! ----------------------------------------------------------------------
		OPENINGTYPE_MAIN_2SASHES	= 1
		OPENINGTYPE_MAIN_3SASHES	= 2
		OPENINGTYPE_MAIN_1SIDE		= 3
		OPENINGTYPE_MAIN_1MIDDLE	= 4
	
		if iOpeningTypeMain = OPENINGTYPE_MAIN_1SIDE | iOpeningTypeMain = OPENINGTYPE_MAIN_1MIDDLE then
			targetGUID = ""
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! Editable Grid sash type is selected in any sash (changed hotspot IDs)
		! ----------------------------------------------------------------------
		MULLION_HVGRID			= 2
		MULLION_EDITABLEGRID	= 3
		MULLION_CUSTOM			= 4
		MULLION_SOLID_PANEL		= 15
		_bHasEditableSash = not(gs_sash_uniform) &\
							((iMullionStyle_01 = MULLION_EDITABLEGRID) |\
							(iMullionStyle_03 = MULLION_EDITABLEGRID) |\
							(iMullionStyle_04 = MULLION_EDITABLEGRID) |\
							((iUTrans <> 0) & (iMullionStyle_05 = MULLION_EDITABLEGRID)) |\
							((iUTrans = 3) & (iMullionStyle_07 = MULLION_EDITABLEGRID)))


		if (gs_sash_uniform & (iMullionStyle_00 = MULLION_EDITABLEGRID)) |\
			_bHasEditableSash then

			targetGUID = ""
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! Handle is used
		! ----------------------------------------------------------------------
		if (gs_handle_type_m <> 0) then
			targetGUID = ""
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! panes are not uniform and set to different styles
		! ----------------------------------------------------------------------
		_bHasDifferentSash = (iMullionStyle_03 <> iMullionStyle_01) |\
								(iMullionStyle_04 <> iMullionStyle_01) |\
								((iUTrans <> 0) & (iMullionStyle_05 <> iMullionStyle_01)) |\
								((iUTrans = 3) & (iMullionStyle_07 <> iMullionStyle_01))

		if not(gs_sash_uniform) & _bHasDifferentSash then
			targetGUID = ""
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! Ledge is ON, and it's not Custom or Solid panel
		! ----------------------------------------------------------------------
		if ((not(_bHasDifferentSash) & (iMullionStyle_01 <> MULLION_CUSTOM) & (iMullionStyle_01 <> MULLION_SOLID_PANEL)) |\
			(gs_sash_uniform & (iMullionStyle_00 <> MULLION_CUSTOM) & (iMullionStyle_00 <> MULLION_SOLID_PANEL))) & bSashLedge then
			
			targetGUID = ""
		endif
							
		! ----------------------------------------------------------------------
		! terminate migration if
		! panes are uniform but side sashes (3 and 4) have different grid number
		! or sash styles are the same but side sashes grid num is different,
		! ----------------------------------------------------------------------
		if targetGUID <> "" then
			if (not(_bHasDifferentSash) & (iMullionStyle_01 = MULLION_HVGRID)) |\
				 (gs_sash_uniform & (iMullionStyle_00 = MULLION_HVGRID)) then
	
				if (vgn_03 <> vgn_04) | (hgn_03 <> hgn_04) then
					targetGUID = ""
				else
					! set grid numbers
					newparameter "vgn", "Integer"
					vgn = vgn_01
		
					newparameter "hgn", "Integer"
					hgn = hgn_01
		
					newparameter "vgn2", "Integer"
					vgn2 = vgn_03
		
					newparameter "hgn2", "Integer"
					hgn2 = hgn_03
		
					newparameter "vgn3", "Integer"
					vgn3 = vgn_05
		
					newparameter "hgn3", "Integer"
					hgn3 = hgn_05
		
					newparameter "vgn4", "Integer"
					vgn4 = vgn_07
		
					newparameter "hgn4", "Integer"
					hgn4 = hgn_07
		
					parameters vgn = vgn,
								vgn2 = vgn2,
								vgn3 = vgn3,
								vgn4 = vgn4,
								hgn = hgn,
								hgn2 = hgn2,
								hgn3 = hgn3,
								hgn4 = hgn4
				endif
			endif
		endif

		! ----------------------------------------------------------------------
		! terminate migration if
		! panes are uniform but side sashes (3 and 4) have different custom sash
		! or sash styles are the same but side sashes custom sash is different
		! ----------------------------------------------------------------------
		if targetGUID <> "" then
			if (not(_bHasDifferentSash) & (iMullionStyle_01 = MULLION_CUSTOM)) |\
				 (gs_sash_uniform & (iMullionStyle_00 = MULLION_CUSTOM)) then
	
				if (gs_cust_panel_03 <> gs_cust_panel_04) then
					targetGUID = ""
				else
					! set custom panel parameters
					newparameter "gs_cust_panel", "String"
					gs_cust_panel = gs_cust_panel_01
		
					newparameter "gs_cust_panel2", "String"
					gs_cust_panel2 = gs_cust_panel_03
		
					newparameter "gs_cust_panel3", "String"
					gs_cust_panel3 = gs_cust_panel_05
		
					newparameter "gs_cust_panel4", "String"
					gs_cust_panel4 = gs_cust_panel_07
		
					parameters gs_cust_panel = gs_cust_panel,
								gs_cust_panel2 = gs_cust_panel2,
								gs_cust_panel3 = gs_cust_panel3,
								gs_cust_panel4 = gs_cust_panel4
				endif
			endif
		endif


		! ----------------------------------------------------------------------
		! set old parameters according to new parameters
		! ----------------------------------------------------------------------
		if targetGUID <> "" then
			! set opening types
			newparameter "gs_optype3_m", "Integer"
			gs_optype3_m = iOpeningTypeTransMain

			newparameter "gs_optype4_m", "Integer"
			gs_optype4_m = iOpeningTypeTransSide

			parameters gs_optype3_m = gs_optype3_m,
						gs_optype4_m = gs_optype4_m

			! set panes configuration
			newparameter "gs_iPanesConfig", "Integer"
			gs_iPanesConfig = iPanesSizeConfig
			parameters gs_iPanesConfig = gs_iPanesConfig

		endif
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_BWM":
	if targetGUID # "" then
		if (iTypeDemolitionFill = 3 | iTypeDemolitionFill = 4) then		! RENOVATION_FILL_NEW, RENOVATION_FILL_NEW_FULL
			iRevealType = gs_reveal_type_int - 1
			if not((iRevealType = REVEAL_EDGE) | (iRevealType = REVEAL_NO)) then
				targetGUID = ""
			else
				if (iTypeDemolitionFill = 4) then		! RENOVATION_FILL_NEW_FULL
					targetGUID = ""
				endif
			endif
		endif
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_BWM":
	if targetGUID # "" then
		if gs_sill_outside & (iSillType = 17) then		! SILL_DUTCH
			targetGUID = ""
		endif
	endif
return

! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_BWM":
	if targetGUID # "" then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 0,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 1,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if not(bMigrationSuccess) then
			targetGUID = ""
		endif
	endif
return

