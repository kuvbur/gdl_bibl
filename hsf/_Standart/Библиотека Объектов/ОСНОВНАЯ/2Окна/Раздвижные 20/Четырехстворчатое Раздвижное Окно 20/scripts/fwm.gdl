
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "E8A2652E-44B7-47B6-BD34-813386BE62BE"		! 4-Sash Sliding Window (INT)
	_startID	= "520805E3-F8B7-4CE3-9CA1-FD11B60A897B"
	_startID2	= "0BE7F011-53B6-469F-B4E3-B80CD69AE441"		! 4-Sash Sliding Window 2 (BRA)
	_endID		= "0F227181-AF4C-469C-8BE7-0C7553DA7242"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "0F227181-AF4C-469C-8BE7-0C7553DA7242"
	_endID		= "D223D269-D628-40B8-A41A-191D07074403"
gosub "newNorFunctions_FWM"

	_startID	= "D223D269-D628-40B8-A41A-191D07074403"
	_endID		= "425FCAFF-67BF-4849-931A-3E90A69E3AFF"
gosub "NewSillTypes_FWM"

	_startID	= "425FCAFF-67BF-4849-931A-3E90A69E3AFF"
	_endID		= "BF43D06C-041C-479E-86BC-CA7B1DF34F6A"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "BF43D06C-041C-479E-86BC-CA7B1DF34F6A"
	_endID		= "D37A1B66-339C-4B56-9818-8C606A6D5A1A"
gosub "New_Corner_Column_Surface_FWM"

	_startID	= "D37A1B66-339C-4B56-9818-8C606A6D5A1A"
	_endID		= "BAE83B20-85AC-46E2-9A2E-BBE97F561EEA"
gosub "deleteUnusedListParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID | actualGuid = _startID2 | actualGuid = _autoID then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 0,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 1,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! newNorFunctions
! ==============================================================================
"newNorFunctions_FWM":
	if actualGUID = _startID then
		_bMigrationSuccess = 1
		
		! ----------------------------------------------------------------------
		! Editable Grid changed hotspot IDs, cancel migration
		! new sashtype parameters for all sashes
		! ----------------------------------------------------------------------
		_bHasParam = STORED_PAR_VALUE("iMullionStyle_00", _tempSashType)
		if _bHasParam then
			MULLION_HVGRID			= 2
			MULLION_EDITABLEGRID	= 3
			MULLION_CUSTOM			= 4

			if _tempSashType = MULLION_EDITABLEGRID then
				! ----------------------------------------------------------------------
				! terminate migration if Editable Grid is selected
				! ----------------------------------------------------------------------
				_bMigrationSuccess = 0
			else
				! ----------------------------------------------------------------------
				! set Uniform Sashes ON
				! ----------------------------------------------------------------------
				gs_sash_uniform = 1
				parameters gs_sash_uniform = gs_sash_uniform

				! ----------------------------------------------------------------------
				! set new sash parameters according to MullionStyle_00
				! ----------------------------------------------------------------------
				if _tempSashType = MULLION_CUSTOM then
					_bHasParamCustom = DELETED_PAR_VALUE("gs_cust_panel", _tempCustPanelMain)
					if _bHasParamCustom & (_tempCustPanelMain <> "") then
						gs_cust_panel_01 = _tempCustPanelMain
						gs_cust_panel_02 = _tempCustPanelMain
						parameters gs_cust_panel_01 = gs_cust_panel_01,
								gs_cust_panel_02 = gs_cust_panel_02
					endif
	
					_bHasParamCustom2 = DELETED_PAR_VALUE("gs_cust_panel2", _tempCustPanelSide)
					if _bHasParamCustom2 & (_tempCustPanelSide <> "") then
						gs_cust_panel_03 = _tempCustPanelSide
						gs_cust_panel_04 = _tempCustPanelSide
						parameters gs_cust_panel_03 = gs_cust_panel_03,
									gs_cust_panel_04 = gs_cust_panel_04
					endif
	
					_bHasParamCustom3 = DELETED_PAR_VALUE("gs_cust_panel3", _tempCustPanelUpperMain)
					if _bHasParamCustom3 & (_tempCustPanelUpperMain <> "") then
						gs_cust_panel_05 = _tempCustPanelUpperMain
						gs_cust_panel_06 = _tempCustPanelUpperMain
						parameters gs_cust_panel_05 = gs_cust_panel_05,
									gs_cust_panel_06 = gs_cust_panel_06
					endif
	
					_bHasParamCustom4 = DELETED_PAR_VALUE("gs_cust_panel4", _tempCustPanelUpperSide)
					if _bHasParamCustom4 & (_tempCustPanelUpperSide <> "") then
						gs_cust_panel_07 = _tempCustPanelUpperSide
						gs_cust_panel_08 = _tempCustPanelUpperSide
						parameters gs_cust_panel_07 = gs_cust_panel_07,
									 gs_cust_panel_08 = gs_cust_panel_08
					endif
				endif

				if _tempSashType = MULLION_HVGRID then
					! ----------------------------------------------------------------------
					! set grid number parameters
					! ----------------------------------------------------------------------
					_bHasGridPar1 = DELETED_PAR_VALUE("vgn", _tempVgn)	
					if _bHasGridPar1 then
						vgn_01 = _tempVgn
						vgn_02 = _tempVgn
						parameters vgn_01 = vgn_01,
									vgn_02 = vgn_02
					endif

					_bHasGridPar2 = DELETED_PAR_VALUE("hgn", _tempHgn)	
					if _bHasGridPar2 then
						hgn_01 = _tempHgn
						hgn_02 = _tempHgn
						parameters hgn_01 = hgn_01,
									hgn_02 = hgn_02
					endif

					_bHasGridPar3 = DELETED_PAR_VALUE("vgn2", _tempVgnSide)	
					if _bHasGridPar3 then
						vgn_03 = _tempVgnSide
						vgn_04 = _tempVgnSide
						parameters vgn_03 = vgn_03,
									vgn_04 = vgn_04
					endif

					_bHasGridPar4 = DELETED_PAR_VALUE("hgn2", _tempHgnSide)	
					if _bHasGridPar4 then
						hgn_03 = _tempHgnSide
						hgn_04 = _tempHgnSide
						parameters hgn_03 = hgn_03,
									hgn_04 = hgn_04
					endif

					_bHasGridPar5 = DELETED_PAR_VALUE("vgn3", _tempVgnUpper)	
					if _bHasGridPar5 then
						vgn_05 = _tempVgnUpper
						vgn_06 = _tempVgnUpper
						parameters vgn_05 = vgn_05,
									vgn_06 = vgn_06
					endif

					_bHasGridPar6 = DELETED_PAR_VALUE("hgn3", _tempHgnUpper)	
					if _bHasGridPar6 then
						hgn_05 = _tempHgnUpper
						hgn_06 = _tempHgnUpper
						parameters hgn_05 = hgn_05,
									hgn_06 = hgn_06
					endif

					_bHasGridPar7 = DELETED_PAR_VALUE("vgn4", _tempVgnUpperSide)	
					if _bHasGridPar7 then
						vgn_07 = _tempVgnUpperSide
						vgn_08 = _tempVgnUpperSide
						parameters vgn_07 = vgn_07,
									vgn_08 = vgn_08
					endif

					_bHasGridPar8 = DELETED_PAR_VALUE("hgn4", _tempHgnUpperSide)	
					if _bHasGridPar8 then
						hgn_07 = _tempHgnUpperSide
						hgn_08 = _tempHgnUpperSide
						parameters hgn_07 = hgn_07,
									hgn_08 = hgn_08
					endif
				endif
			endif
		endif

		if _bMigrationSuccess = 1 then
			! ----------------------------------------------------------------------
			! set opening types
			! ----------------------------------------------------------------------
			OPENINGTYPE_MAIN_SAMERAIL	= 1
			OPENINGTYPE_MAIN_DIFFRAIL	= 2
			OPENINGTYPE_MAIN_4SASHES	= 3

			_bHasParam1 = DELETED_PAR_VALUE("gs_optype_m", _tempMainOpType)
			_bHasParam2 = DELETED_PAR_VALUE("gs_optype2_m", _tempMainSideOpType)
			if _bHasParam1 & _bHasParam2 then
				if _tempMainSideOpType = 2 then
					! side sashes are sliding too
					iOpeningTypeMain = OPENINGTYPE_MAIN_4SASHES
				else
					! side sashes are fixed
					if LibraryLangCode = "BRA" then
						iOpeningTypeMain = OPENINGTYPE_MAIN_SAMERAIL
					else
						iOpeningTypeMain = OPENINGTYPE_MAIN_DIFFRAIL
					endif
				endif
				parameters iOpeningTypeMain = iOpeningTypeMain			
			endif

			_bHasParam = DELETED_PAR_VALUE("gs_optype3_m", _tempTransomMainOpType)
			if _bHasParam then
				iOpeningTypeTransMain = _tempTransomMainOpType
				parameters iOpeningTypeTransMain = iOpeningTypeTransMain			
			endif

			_bHasParam = DELETED_PAR_VALUE("gs_optype4_m", _tempTransomSideOpType)
			if _bHasParam then
				iOpeningTypeTransSide = _tempTransomSideOpType
				parameters iOpeningTypeTransSide = iOpeningTypeTransSide			
			endif

			gs_sash_set_to_edit = 1
			parameters gs_sash_set_to_edit = gs_sash_set_to_edit

			! ----------------------------------------------------------------------
			! set new sash parameters according to their AC19 pair
			! ----------------------------------------------------------------------
			_bHasParam = DELETED_PAR_VALUE("gs_iPanesConfig", _tempPanesConfig)
			if _bHasParam then
				PANES_UNIFORM	= 1
				PANES_CUSTOM	= 2

				if _tempPanesConfig = 1 then
					iPanesSizeConfig = PANES_UNIFORM
				else
					iPanesSizeConfig = PANES_CUSTOM
				endif
				parameters iPanesSizeConfig = iPanesSizeConfig
			endif

			! ----------------------------------------------------------------------
			! sahs calculation: sahswidth means full sash, not measured to axes
			! ----------------------------------------------------------------------
			_bHasParam1 = STORED_PAR_VALUE("sashWidth1", _tempSashSize1)
			_bHasParam2 = STORED_PAR_VALUE("sashWidth2", _tempSashSize2)
			_bHasParam3 = STORED_PAR_VALUE("sashWidth3", _tempSashSize3)
			_bHasParam4	= STORED_PAR_VALUE("gs_sash_width", _tempSashWidth)

			if _bHasParam1 & _bHasParam2 & _bHasParam3 & _bHasParam4 then
				sashWidth1 = _tempSashSize1 + _tempSashWidth
				sashWidth2 = _tempSashSize2 + _tempSashWidth
				sashWidth3 = _tempSashSize3 + _tempSashWidth

				parameters sashWidth1 = sashWidth1,
							sashWidth2 = sashWidth2,
							sashWidth3 = sashWidth3
			endif

			! ----------------------------------------------------------------------
			! set actualGUID
			! ----------------------------------------------------------------------
			actualGUID = _endID
		else
			actualGUID = ""
		endif		
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! New_Corner_Column_Surface
! ==============================================================================
"New_Corner_Column_Surface_FWM":
	if actualGUID = _startID then
		matCornerColumn = gs_frame_mat
		parameters matCornerColumn = matCornerColumn
		
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

