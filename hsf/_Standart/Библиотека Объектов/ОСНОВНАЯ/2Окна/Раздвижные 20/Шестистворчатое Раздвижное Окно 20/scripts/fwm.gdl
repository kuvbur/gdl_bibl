
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_startID	= "399E1DD8-696A-4957-AC8A-90545FD4C8A6"
	_endID		= "DF6C0DDF-05CD-40EE-A946-505A3515B1B0"
gosub "B406_NewFuctionsCommon_FWM"

	_startID	= "DF6C0DDF-05CD-40EE-A946-505A3515B1B0"
	_endID		= "05EB1B07-0074-4343-80B3-147372405620"
gosub "newNorFunctions_FWM"

	_startID	= "05EB1B07-0074-4343-80B3-147372405620"
	_endID		= "B60C5BAC-09B7-4B29-96F7-262B3BC40809"
gosub "NewSillTypes_FWM"

	_startID	= "B60C5BAC-09B7-4B29-96F7-262B3BC40809"
	_endID		= "54FC1DD3-5F51-4E0D-BBAA-A38386CDA42A"
gosub "NewDemolitionSymbol_FWM"

	_startID	= "54FC1DD3-5F51-4E0D-BBAA-A38386CDA42A"
	_endID		= "9B1519B1-2ACC-4CD4-8E01-1508052940C1"
gosub "New_Corner_Column_Surface_FWM"

	_startID	= "9B1519B1-2ACC-4CD4-8E01-1508052940C1"
	_endID		= "054D0D37-FD48-4DE4-9AE2-103024924707"
gosub "deleteUnusedListParams_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! B406_NewFuctionsCommon
! ==============================================================================
"B406_NewFuctionsCommon_FWM":
	if actualGuid = _startID  then
		bMigrationSuccess = 1
		call "Migration_DoorWindow" parameters all	iMigrationStep			= 1,
													bMigrateDoorLeaf		= 0,
													bMigrateDoorHandle		= 0,
													bMigrateKickBoard		= 0,
													bMigrateKickBoard2		= 0,
													bMigrateCasingType		= 1,
													bMigrateBottomCasing	= 1,
													bMigrateCasingCover		= 1,
								returned_parameters bMigrationSuccess
	
		if bMigrationSuccess then
			actualGUID = _endID
		else
			actualGuid = ""
		endif
	endif
return

! ==============================================================================
! newNorFunctions
! ==============================================================================
"newNorFunctions_FWM":
	if actualGUID = _startID then
		_bMigrationSuccess = 1
		
		! ----------------------------------------------------------------------
		! Editable Grid changed hotspot IDs, cancel migration
		! new sashtype parameters for all sashes
		! ----------------------------------------------------------------------
		_bHasParam = STORED_PAR_VALUE("iMullionStyle_00", _tempSashType)
		if _bHasParam then
			MULLION_HVGRID			= 2
			MULLION_EDITABLEGRID	= 3
			MULLION_CUSTOM			= 4

			if _tempSashType = MULLION_EDITABLEGRID then
				! ----------------------------------------------------------------------
				! terminate migration if Editable Grid is selected
				! ----------------------------------------------------------------------
				_bMigrationSuccess = 0
			else
				! ----------------------------------------------------------------------
				! set Uniform Sashes ON
				! ----------------------------------------------------------------------
				gs_sash_uniform = 1
				parameters gs_sash_uniform = gs_sash_uniform

				! ----------------------------------------------------------------------
				! set new sash parameters according to MullionStyle_00
				! ----------------------------------------------------------------------
				if _tempSashType = MULLION_CUSTOM then
					_bHasParamCustom = DELETED_PAR_VALUE("gs_cust_panel", _tempCustPanelMain)
					if _bHasParamCustom & (_tempCustPanelMain <> "") then
						gs_cust_panel_01 = _tempCustPanelMain
						gs_cust_panel_02 = _tempCustPanelMain
						parameters gs_cust_panel_01 = gs_cust_panel_01,
									gs_cust_panel_02 = gs_cust_panel_02
					endif
	
					_bHasParamCustom2 = DELETED_PAR_VALUE("gs_cust_panel2", _tempCustPanelSide)
					if _bHasParamCustom2 & (_tempCustPanelSide <> "") then
						gs_cust_panel_03 = _tempCustPanelSide
						gs_cust_panel_04 = _tempCustPanelSide
						parameters gs_cust_panel_03 = gs_cust_panel_03,
									gs_cust_panel_04 = gs_cust_panel_04
					endif
	
					_bHasParamCustom2 = DELETED_PAR_VALUE("gs_cust_panel3", _tempCustPanelSide2)
					if _bHasParamCustom2 & (_tempCustPanelSide2 <> "") then
						gs_cust_panel_05 = _tempCustPanelSide2
						gs_cust_panel_06 = _tempCustPanelSide2
						parameters gs_cust_panel_05 = gs_cust_panel_05,
									gs_cust_panel_06 = gs_cust_panel_06
					endif

				endif

				if _tempSashType = MULLION_HVGRID then
					! ----------------------------------------------------------------------
					! set grid number parameters
					! ----------------------------------------------------------------------
					_bHasGridPar1 = DELETED_PAR_VALUE("vgn", _tempVgn)	
					if _bHasGridPar1 then
						vgn_01 = _tempVgn
						vgn_02 = _tempVgn
						parameters vgn_01 = vgn_01,
									vgn_02 = vgn_02
					endif

					_bHasGridPar2 = DELETED_PAR_VALUE("hgn", _tempHgn)	
					if _bHasGridPar2 then
						hgn_01 = _tempHgn
						hgn_02 = _tempHgn
						parameters hgn_01 = hgn_01,
									hgn_02 = hgn_02
					endif

					_bHasGridPar3 = DELETED_PAR_VALUE("vgn2", _tempVgnSide)	
					if _bHasGridPar3 then
						vgn_03 = _tempVgnSide
						vgn_04 = _tempVgnSide
						parameters vgn_03 = vgn_03,
									vgn_04 = vgn_04
					endif

					_bHasGridPar4 = DELETED_PAR_VALUE("hgn2", _tempHgnSide)	
					if _bHasGridPar4 then
						hgn_03 = _tempHgnSide
						hgn_04 = _tempHgnSide
						parameters hgn_03 = hgn_03,
									hgn_04 = hgn_04
					endif

					_bHasGridPar5 = DELETED_PAR_VALUE("vgn3", _tempVgnSide2)	
					if _bHasGridPar5 then
						vgn_05 = _tempVgnSide
						vgn_06 = _tempVgnSide
						parameters vgn_05 = vgn_05,
									vgn_06 = vgn_06
					endif

					_bHasGridPar6 = DELETED_PAR_VALUE("hgn3", _tempHgnSide2)	
					if _bHasGridPar6 then
						hgn_05 = _tempHgnSide2
						hgn_06 = _tempHgnSide2
						parameters hgn_05 = hgn_05,
									hgn_06 = hgn_06
					endif
				endif
			endif
		endif

		if _bMigrationSuccess = 1 then
			gs_sash_set_to_edit = 1
			parameters gs_sash_set_to_edit = gs_sash_set_to_edit

			! ----------------------------------------------------------------------
			! set actualGUID
			! ----------------------------------------------------------------------
			actualGUID = _endID
		else
			actualGUID = ""
		endif
	endif
return

! ==============================================================================
! NewSillTypes
! ==============================================================================
"NewSillTypes_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! NewDemolitionSymbol
! ==============================================================================
"NewDemolitionSymbol_FWM":
	if actualGuid = _startID then
		actualGUID = _endID
	endif
return

! ==============================================================================
! New_Corner_Column_Surface
! ==============================================================================
"New_Corner_Column_Surface_FWM":
	if actualGUID = _startID then
		matCornerColumn = gs_frame_mat
		parameters matCornerColumn = matCornerColumn
		
		actualGUID = _endID
	endif
return

! ==============================================================================
! deleteUnusedListParams
! ==============================================================================
"deleteUnusedListParams_FWM":
	if actualGUID = _startID then
		_customList1 = ""
		_customList2 = ""
		_customList3 = ""
		_customList4 = ""
		_customList5 = ""
	
		bDeleted =	DELETED_PAR_VALUE ("gs_list_custom1", _customList1)	& \
					DELETED_PAR_VALUE ("gs_list_custom2", _customList2)	& \
					DELETED_PAR_VALUE ("gs_list_custom3", _customList3)	& \
					DELETED_PAR_VALUE ("gs_list_custom4", _customList4)	& \
					DELETED_PAR_VALUE ("gs_list_custom5", _customList5)
	
		if	_customList1 <> ""	| \
			_customList2 <> ""	| \
			_customList3 <> ""	| \
			_customList4 <> ""	| \
			_customList5 <> ""	then
	
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
return

