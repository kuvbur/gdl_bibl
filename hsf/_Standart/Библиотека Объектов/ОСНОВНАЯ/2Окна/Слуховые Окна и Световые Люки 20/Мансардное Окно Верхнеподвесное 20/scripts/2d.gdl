

! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
	returned_parameters	iLoD2D,
		gs_opLinePen, gs_openedSashPen, gs_iSwingType, gs_swingLineType, gs_opline_style_m

if iLoD2D = SYMBS_OFF | iLoD2D = SYMBS_S_1 then end



pen gs_cont_pen

B2 = B * cos(ac_roofang)
halfA = A / 2
halfB2 = B2 / 2


idx = 1

hotspot2  0,	 0, idx: idx=idx+1
hotspot2 -halfA, 0, idx: idx=idx+1
hotspot2  halfA, 0,	idx: idx=idx+1
hotspot2 -halfA, B2,idx: idx=idx+1
hotspot2  halfA, B2,idx: idx=idx+1

if iLoD2D < SYMBS_D_1 then	! Simple
	if abs(B2) > EPS then
		fill gs_fill_type
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			 halfA,	0,	1,
			-halfA,	0,	1,
			-halfA,	B2,	1,
			 halfA,	B2,	1

		add2 -halfA,B2
		mul2 1,-1

		pen gs_opLinePen
		call "OpeningLines" parameters A = A, B = B2,
			gs_opline_style_m	= gs_opline_style_m,
			iOpeningType		= 8,	! SASH_TOP_HUNG
			opLineType2d		= gs_swingLineType
		del 2
	endif
else	! Detailed

	w  = 0.12 * sin(ac_roofang)
	f1 = 0.06 * cos(ac_roofang)
	f2 = 0.06 * cos(ac_roofang)

	if abs(B2 - w) > EPS then
		line2 -halfA,B2-w,			halfA,B2-w
		rect2 -halfA+0.06,B2-f2-w,	halfA-0.06,f1-w

		fill gs_fill_type
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			 halfA,-w,	1,
			-halfA,-w,	1,
			-halfA,	B2,	1,
			 halfA,	B2,	1
	endif

	add2 -halfA+0.06,B2-f2-w
	mul2 1,-1
	pen gs_opLinePen
	call "OpeningLines" parameters A = A-0.12, B = B2-f1-f2,
		gs_opline_style_m	= gs_opline_style_m,
		iOpeningType		= 8,	! SASH_TOP_HUNG
		opLineType2d		= gs_swingLineType
	del 2
endif
