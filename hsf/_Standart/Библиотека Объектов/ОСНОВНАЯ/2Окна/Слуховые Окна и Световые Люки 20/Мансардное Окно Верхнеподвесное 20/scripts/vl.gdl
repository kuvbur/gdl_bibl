
! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
					iSkylightType			= 13


if (GLOB_MODPAR_NAME = "gs_a_half" | GLOB_MODPAR_NAME = "B") & gs_a_half > -EPS then
	A = gs_a_half * 2
	parameters A = A
else
	gs_a_half = A / 2
	parameters gs_a_half = gs_a_half
endif


call "OpeningOptions" parameters all bSkylight = 1, bEnableSecondaryOpLineTypes = 0

if gs_cont_pen <= 0 then parameters gs_cont_pen = 2


ss = ""
rrr = request("Linear_dimension", "", ss)
bMetric = 0
if strstr (ss, "m") > 0 then bMetric = 1


if bMetric then
	values "ac_roofang_slope_mode" `°`,`%`
else
	values "ac_roofang_slope_mode" `°`,`%`, `на 12'`,`на 12"`
endif

!! Pitch Unit setting !!

LENGTH_12_FEET = 12'
LENGTH_12_INCHES = 12"

if ac_roofang_slope_mode = `°` then
	gs_roofang_perc	= tan(gs_roofang_deg) * 100
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `%` then
	gs_roofang_deg  = atn(gs_roofang_perc / 100)
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12'` then
	slope = gs_roofang_12f / LENGTH_12_FEET
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12i	= slope * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12"` then
	slope = gs_roofang_12i / LENGTH_12_INCHES
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12f	= slope * LENGTH_12_FEET

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
endif

parameters gs_roofang_deg	= gs_roofang_deg
parameters gs_roofang_perc	= gs_roofang_perc
parameters gs_roofang_12f	= gs_roofang_12f
parameters gs_roofang_12i	= gs_roofang_12i


! FM Types --------------------------------------------------------------------
call "FM_types" parameters all

values "A" range [0.04, )
values "B" range [0.1, )

! ==============================================================================
! Convert Display Option settings of old library parts
! ==============================================================================

parameters AC_special_2D_symbol = 0
hideparameter	"AC_special_2D_symbol",
				"AC_plane_definition",
				"AC_symb_display_option",
				"AC_symb_show_projection_to"


! ==============================================================================
! 2D Display Options
! ==============================================================================

parameters AC_use_symb_display_option = 1

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "gs_use_3D_view_attributes"
	lock "gs_use_3D_view_attributes"
endif

if gs_use_3D_view_attributes then
	hideparameter "AC_uncut_pen"
	lock "AC_uncut_pen"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "AC_cut_linetype", "AC_uncut_pen"
	lock "AC_cut_linetype", "AC_uncut_pen"
endif

if AC_symb_display_option = 1 | AC_symb_display_option = 3 | AC_symb_display_option = 4 then
	hideparameter "AC_overhead_linetype", "AC_overhead_pen"
	lock "AC_overhead_linetype", "AC_overhead_pen"
endif

if AC_symb_display_option = 4 then
	hideparameter "AC_cut_linetype"
	lock "AC_cut_linetype"
endif

if AC_symb_display_option = 5 then
	hideparameter "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock "gs_fill_type", "gs_fill_pen", "gs_back_pen"
endif

! ==============================================================================

if AC_symb_display_option <> 3 then
	hideparameter "gs_detlevel_2D"
	lock "gs_detlevel_2D"
endif

! ==============================================================================

if gs_opening_angle > 180 then
	parameters gs_opening_angle = 0
else
	if gs_opening_angle > 45 then parameters gs_opening_angle = 45
endif
if gs_opening_angle < 0 then parameters gs_opening_angle = 0


! Roof Hole Cut ================================================================

call "SkylightRect_RoofCutting_macro" parameters A = A, B = B,
	AC_Custom_3D_Hole	= AC_Custom_3D_Hole


! Debugger functions ===========================================================

if not(ac_skylight_debug) then
	hideparameter "ac_skylight_function"
	if ac_skylight_function then
		hideparameter	"ac_roofang",
						"ac_roofthk",
						"gs_roof_material",
						"gs_roofang_deg",
						"gs_roofang_perc",
						"gs_roofang_12f",
						"gs_roofang_12i",
						"ac_roofang_slope_mode"
	endif
endif


! =============================================================================
! IFC Parameters
! =============================================================================

ifc_optype = 1		! WIN_OP_SINGLE_PANEL
parameters ifc_optype = ifc_optype

call "IFC_optype_window" parameters ifc_optype = ifc_optype

lock "ifc_optypestr"

ifc_LiningDepth = 0.10
parameters ifc_LiningDepth = ifc_LiningDepth

ifc_LiningThickness = 0.04
parameters ifc_LiningThickness = ifc_LiningThickness

ifc_MullionThickness	= 0
ifc_FirstMullionOffset	= 0
ifc_SecondMullionOffset	= 0

parameters	ifc_MullionThickness	= ifc_MullionThickness,
			ifc_FirstMullionOffset	= ifc_FirstMullionOffset,
			ifc_SecondMullionOffset	= ifc_SecondMullionOffset

ifc_TransomThickness	= 0
ifc_FirstTransomOffset	= 0
ifc_SecondTransomOffset	= 0

parameters	ifc_TransomThickness	= ifc_TransomThickness,
			ifc_FirstTransomOffset	= ifc_FirstTransomOffset,
			ifc_SecondTransomOffset	= ifc_SecondTransomOffset

dim ifc_WindowPanel[][4]
	ifc_WindowPanel[1][1] = 0.042
	ifc_WindowPanel[1][2] = 0.036
	ifc_WindowPanel[1][3] = 5	! TOPHUNG
	ifc_WindowPanel[1][4] = 2	! MIDDLE
