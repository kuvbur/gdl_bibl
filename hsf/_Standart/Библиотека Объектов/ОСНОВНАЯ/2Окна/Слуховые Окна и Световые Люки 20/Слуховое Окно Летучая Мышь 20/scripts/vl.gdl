
! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
					iSkylightType			= 1

ss = ""
rrr = request("Linear_dimension", "", ss)
bMetric = 0
if strstr (ss, "m") > 0 then bMetric = 1

parameters gs_marker_text_dist = 0.05
parameters ac_show2DHotspotsIn3D = 0

if bMetric then
	values "ac_roofang_slope_mode" `°`,`%`
else
	values "ac_roofang_slope_mode" `°`,`%`, `на 12'`,`на 12"`
endif

!! Pitch Unit setting !!

if ac_roofang_slope_mode = `°` then
	gs_roofang_perc	= tan(gs_roofang_deg) * 100
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `%` then
	gs_roofang_deg  = atn(gs_roofang_perc / 100)
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12'` then
	slope = gs_roofang_12f / LENGTH_12_FEET
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12i	= slope * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12"` then
	slope = gs_roofang_12i / LENGTH_12_INCHES
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12f	= slope * LENGTH_12_FEET

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
endif

parameters gs_roofang_deg	= gs_roofang_deg
parameters gs_roofang_perc	= gs_roofang_perc
parameters gs_roofang_12f	= gs_roofang_12f
parameters gs_roofang_12i	= gs_roofang_12i


!! shed roof Pitch Unit setting !!

if ac_roofang_slope_mode = `°` then
	gs_shedroofang_perc	= TAN (gs_shedroofang_deg) * 100
	gs_shedroofang_12f	= (gs_shedroofang_perc / 100) * LENGTH_12_FEET
	gs_shedroofang_12i	= (gs_shedroofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_shedroofang_perc"
	hideparameter "gs_shedroofang_12f"
	hideparameter "gs_shedroofang_12i"
endif

if ac_roofang_slope_mode = `%` then
	gs_shedroofang_deg  = ATN (gs_shedroofang_perc / 100)
	gs_shedroofang_12f	= (gs_shedroofang_perc / 100) * LENGTH_12_FEET
	gs_shedroofang_12i	= (gs_shedroofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_shedroofang_deg"
	hideparameter "gs_shedroofang_12f"
	hideparameter "gs_shedroofang_12i"
endif

if ac_roofang_slope_mode = `на 12'` then
	slope = gs_shedroofang_12f / LENGTH_12_FEET
	gs_shedroofang_deg	= ATN(slope)
	gs_shedroofang_perc	= slope * 100
	gs_shedroofang_12i	= slope * LENGTH_12_INCHES

	hideparameter "gs_shedroofang_deg"
	hideparameter "gs_shedroofang_perc"
	hideparameter "gs_shedroofang_12i"
endif

if ac_roofang_slope_mode = `на 12"` then
	slope = gs_shedroofang_12i / LENGTH_12_INCHES
	gs_shedroofang_deg	= ATN(slope)
	gs_shedroofang_perc	= slope * 100
	gs_shedroofang_12f	= slope * LENGTH_12_FEET

	hideparameter "gs_shedroofang_deg"
	hideparameter "gs_shedroofang_perc"
	hideparameter "gs_shedroofang_12f"
endif

if gs_shedroofang_deg	< 0 then gs_shedroofang_deg = 0
if gs_shedroofang_deg	> ac_roofang-1 then gs_shedroofang_deg = ac_roofang-1
if gs_shedroofang_perc	< 0 then gs_shedroofang_perc = 0
if gs_shedroofang_perc	> tan(ac_roofang-1) * 100 then gs_shedroofang_perc = tan(ac_roofang-1) * 100
if gs_shedroofang_12f	< 0 then gs_shedroofang_12f = 0
if gs_shedroofang_12f	> ((ac_roofang-1) / 100) * LENGTH_12_FEET then gs_shedroofang_12f = ((ac_roofang-1) / 100) * LENGTH_12_FEET
if gs_shedroofang_12i	< 0 then gs_shedroofang_12i = 0
if gs_shedroofang_12i	> ((ac_roofang-1) / 100) * LENGTH_12_INCHES then gs_shedroofang_12i = ((ac_roofang-1) / 100) * LENGTH_12_INCHES


parameters gs_shedroofang_deg	= gs_shedroofang_deg
parameters gs_shedroofang_perc	= gs_shedroofang_perc
parameters gs_shedroofang_12f	= gs_shedroofang_12f
parameters gs_shedroofang_12i	= gs_shedroofang_12i




!!! FM Types
call "FM_types" parameters all

if useroofmat then
	lock "gs_lower_mat","gs_upper_mat", "gs_innside_mat"
	hideparameter "gs_lower_mat","gs_upper_mat", "gs_innside_mat"
endif

if ac_skylight_debug=0 then
	hideparameter "ac_skylight_function"
	if ac_skylight_function then
		hideparameter	"ac_roofang",
						"ac_roofthk",
						"gs_roof_material",
						"gs_roofang_deg",
						"gs_roofang_perc",
						"gs_roofang_12f",
						"gs_roofang_12i"
	endif
endif


!!if ac_roofthk < 0.025+0.05*sin(ng) then parameters ac_roofthk = 0.025+0.05*sin(ng)

values "ac_roofthk" range (0.025+0.05*sin(ng),)

values "A" range [1.8,)

values "gs_frame_thk" range (0,(dr1-0.01+rr-a/4)*cos(ng)+0.025+0.05*sin(ng))

lock "B"

parameters gs_fontsize= abs(gs_fontsize)
values "gs_fontsize" range (0,]


! ==============================================================================
! Convert Display Option settings of old library parts
! ==============================================================================

parameters AC_special_2D_symbol = 0
hideparameter	"AC_special_2D_symbol",
				"AC_plane_definition",
				"AC_symb_display_option",
				"AC_symb_show_projection_to"


! ==============================================================================
! 2D Display Options
! ==============================================================================

parameters AC_use_symb_display_option = 1

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "gs_use_3D_view_attributes"
	lock "gs_use_3D_view_attributes"
endif

if gs_use_3D_view_attributes then
	hideparameter "AC_uncut_pen"
	lock "AC_uncut_pen"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "AC_cut_linetype", "AC_uncut_pen"
	lock "AC_cut_linetype", "AC_uncut_pen"
endif

if AC_symb_display_option = 1 | AC_symb_display_option = 3 | AC_symb_display_option = 4 then
	hideparameter "AC_overhead_linetype", "AC_overhead_pen"
	lock "AC_overhead_linetype", "AC_overhead_pen"
endif

if AC_symb_display_option = 4 then
	hideparameter "AC_cut_linetype"
	lock "AC_cut_linetype"
endif

if AC_symb_display_option = 5 then
	hideparameter "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock "gs_fill_type", "gs_fill_pen", "gs_back_pen"
endif

! ==============================================================================

if not(AC_Custom_3D_Hole) then
	hideparameter "gs_hole_edge_upper_depth", "gs_hole_edge_lower_depth", "gs_hole_outside_depth"
	lock "gs_hole_edge_upper_depth", "gs_hole_edge_lower_depth", "gs_hole_outside_depth"
endif
