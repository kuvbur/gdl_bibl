

! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
	returned_parameters	iLoD2D,
		gs_opLinePen, gs_openedSashPen, gs_iSwingType, gs_swingLineType, gs_opline_style_m

if iLoD2D = SYMBS_OFF | iLoD2D = SYMBS_S_1 then end


! Horizontal and low Roof
if abs(ac_roofang) < EPS | ac_roofang > 90+EPS then
	rect2 -A/2, 0, A/2, B*COS(ac_roofang)

	end
endif


! ==============================================================================
! Symbolic 2D
! ==============================================================================

pen gs_cont_pen
fill gs_fill_type

add2 -a/2,0

xBreite = a
! (b wird ignoriert.)

if abs(dSprossen) < eps then
  bSprosseVert = 0
endif


tanDach = tan(ac_roofang)   !### Wir wollen mit dem Tangens rechnen.

cosDachGaube = cos(wDachGaube)
sinDachGaube = sin(wDachGaube)
tanDachGaube = sinDachGaube / cosDachGaube


xBreiteHalb = xBreite / 2
xLkFenster = (xBreite - xFensterbreite) / 2
xRkFenster = xLkFenster + xFensterbreite
xRkRahmenLinks = xLkFenster + dRahmen
xLkRahmenRechts = xLkFenster + xFensterbreite - dRahmen
xLkSprosse = xLkFenster + (xFensterbreite - dSprossen) / 2
xRkSprosse = xLkSprosse + dSprossen
xLkWandRechts = xBreite - dWandGaube

zFrontFirst = zHoeheTraufe + xBreiteHalb * tanDachGaube
zFrontWand = zHoeheTraufe + tanDachGaube * dWandGaube



yProfil = dLeibung + dProfil
yGlas = dLeibung + dProfil / 2
yEcke = dUeberstandGaubeVorn + zHoeheTraufe / tanDach
ySpitze = dUeberstandGaubeVorn + zFrontFirst / tanDach




!### Gauben-Vorderwand:



rect2 0, 0,   xBreite, dWandGaube

!### Fensteroffnung:

line2 xLkFenster, 0,   xLkFenster, dWandGaube
line2 xRkFenster, 0,   xRkFenster, dWandGaube



if dRahmen > 0 then
	if iLoD2D = SYMBS_S_2 then
		line2 xLkFenster, dLeibung,   xRkFenster, dLeibung
	endif

	if iLoD2D >= SYMBS_M_1 then
		!### Fensterrahmen:

		line2 xLkFenster, dLeibung,   xRkFenster, dLeibung
		line2 xLkFenster, yProfil,   xRkFenster, yProfil

		if iLoD2D >= SYMBS_M_1 then
			line2 xRkRahmenLinks, dLeibung,   xRkRahmenLinks, yProfil
			line2 xLkRahmenRechts, dLeibung,   xLkRahmenRechts, yProfil
		endif

		if bSprosseVert then

			!### Fenster mit Mittelsprosse:

			if iLoD2D = SYMBS_D_1 | iLoD2D = SYMBS_D_2 then
			line2 xRkRahmenLinks, yGlas, xLkSprosse, yGlas
				line2 xRkSprosse, yGlas, xLkRahmenRechts, yGlas
			endif
			if iLoD2D >= SYMBS_M_1 then
				line2 xLkSprosse, dLeibung,   xLkSprosse, yProfil
				line2 xRkSprosse, dLeibung,   xRkSprosse, yProfil
			endif
		else

		!### Fenster ohne Mittelsprosse:

			if iLoD2D = SYMBS_D_1 | iLoD2D = SYMBS_D_2 then
				line2 xRkRahmenLinks, yGlas, xLkRahmenRechts, yGlas
			endif
		endif
	endif
endif


! Wande und Dach der Gaube treffen auf das Hauptdach:



line_type iLineType

if gs_back_pen<>0 then
	mask=1+2+4+64
else
	mask=1+4
endif

poly2_b 6, mask, gs_fill_pen, gs_back_pen,
	  0, 0, 1,
	  0, yEcke, 1,
	  xBreiteHalb, ySpitze, 1,
	  xBreite, yEcke, 1,
	  xBreite, 0, 1,
	  0, 0, -1


! Fixpunkte:

hotspot2 xBreiteHalb, 0
hotspot2 0, 0
hotspot2 xBreite, 0

hotspot2 0, yEcke
hotspot2 xBreiteHalb, ySpitze
hotspot2 xBreite, yEcke
