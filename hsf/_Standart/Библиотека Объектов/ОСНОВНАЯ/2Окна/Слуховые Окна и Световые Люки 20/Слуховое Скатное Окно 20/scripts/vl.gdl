
! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
					iSkylightType			= 2

if (GLOB_MODPAR_NAME = "gs_a_half" | GLOB_MODPAR_NAME = "B") & gs_a_half > -EPS then
	A = gs_a_half * 2
	parameters A = A
else
	gs_a_half = A / 2
	parameters gs_a_half = gs_a_half
endif



ss = ""
rrr = request("Linear_dimension", "", ss)
bMetric = 0
if strstr (ss, "m") > 0 then bMetric = 1

parameters gs_marker_text_dist = dWandGaube

if bMetric then
	values "ac_roofang_slope_mode" `°`,`%`
else
	values "ac_roofang_slope_mode" `°`,`%`, `на 12'`,`на 12"`
endif

!! Pitch Unit setting !!

if ac_roofang_slope_mode = `°` then
	gs_roofang_perc	= tan(gs_roofang_deg) * 100
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `%` then
	gs_roofang_deg  = atn(gs_roofang_perc / 100)
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12'` then
	slope = gs_roofang_12f / LENGTH_12_FEET
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12i	= slope * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12"` then
	slope = gs_roofang_12i / LENGTH_12_INCHES
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12f	= slope * LENGTH_12_FEET

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
endif

parameters gs_roofang_deg	= gs_roofang_deg
parameters gs_roofang_perc	= gs_roofang_perc
parameters gs_roofang_12f	= gs_roofang_12f
parameters gs_roofang_12i	= gs_roofang_12i


!! dormer roof Pitch Unit setting !!

if ac_roofang_slope_mode = `°` then
	gs_dormroofang_perc	= TAN (gs_dormroofang_deg) * 100
	gs_dormroofang_12f	= (gs_dormroofang_perc / 100) * LENGTH_12_FEET
	gs_dormroofang_12i	= (gs_dormroofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_dormroofang_perc"
	hideparameter "gs_dormroofang_12f"
	hideparameter "gs_dormroofang_12i"
endif

if ac_roofang_slope_mode = `%` then
	gs_dormroofang_deg  = ATN (gs_dormroofang_perc / 100)
	gs_dormroofang_12f	= (gs_dormroofang_perc / 100) * LENGTH_12_FEET
	gs_dormroofang_12i	= (gs_dormroofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_dormroofang_deg"
	hideparameter "gs_dormroofang_12f"
	hideparameter "gs_dormroofang_12i"
endif

if ac_roofang_slope_mode = `на 12'` then
	slope = gs_dormroofang_12f / LENGTH_12_FEET
	gs_dormroofang_deg	= ATN(slope)
	gs_dormroofang_perc	= slope * 100
	gs_dormroofang_12i	= slope * LENGTH_12_INCHES

	hideparameter "gs_dormroofang_deg"
	hideparameter "gs_dormroofang_perc"
	hideparameter "gs_dormroofang_12i"
endif

if ac_roofang_slope_mode = `на 12"` then
	slope = gs_dormroofang_12i / LENGTH_12_INCHES
	gs_dormroofang_deg	= ATN(slope)
	gs_dormroofang_perc	= slope * 100
	gs_dormroofang_12f	= slope * LENGTH_12_FEET

	hideparameter "gs_dormroofang_deg"
	hideparameter "gs_dormroofang_perc"
	hideparameter "gs_dormroofang_12f"
endif

if gs_dormroofang_deg	< 0 then gs_dormroofang_deg = 0
if gs_dormroofang_deg	> 85 then gs_dormroofang_deg = 85
if gs_dormroofang_perc	< 0 then gs_dormroofang_perc = 0
if gs_dormroofang_perc	> tan(85) * 100 then gs_dormroofang_perc = tan(85) * 100
if gs_dormroofang_12f	< 0 then gs_dormroofang_12f = 0
if gs_dormroofang_12f	> ((85) / 100) * LENGTH_12_FEET then gs_dormroofang_12f = ((85) / 100) * LENGTH_12_FEET
if gs_dormroofang_12i	< 0 then gs_dormroofang_12i = 0
if gs_dormroofang_12i	> ((85) / 100) * LENGTH_12_INCHES then gs_dormroofang_12i = ((85) / 100) * LENGTH_12_INCHES


parameters gs_dormroofang_deg	= gs_dormroofang_deg
parameters gs_dormroofang_perc	= gs_dormroofang_perc
parameters gs_dormroofang_12f	= gs_dormroofang_12f
parameters gs_dormroofang_12i	= gs_dormroofang_12i


values "dUeberstandDachGaube" range[0,]
values "sizeRoofFrontOverhang" range[0,]

minBorder = dWandGaube

values "xFensterbreite" range ( 2*dRahmen + bSprosseVert * dSprossen, a - 2*minBorder]

if iSkylightWinShape = WINSHAPE_ROUND then
	values "zOkFenster" range ( (nSprossenHor>0) * (zSprosseN+dSprossen/2) + (nSprossenHor=0) * (dRahmen + zUkFenster),\
								zHoeheTraufe + (a/2) * tan(gs_dormroofang_deg) - minBorder - hStich]
else
	values "zOkFenster" range ( dRahmen + (nSprossenHor>0) * (zSprosseN+dSprossen/2) + (nSprossenHor=0) * (dRahmen + zUkFenster),\
								zHoeheTraufe + minBorder * tan((gs_dormroofang_deg - 90)/2) + ((a-xFensterbreite)/2-minBorder)* tan(gs_dormroofang_deg)]
endif


! FM Types --------------------------------------------------------------------
call "FM_types" parameters all

if useroofmat then
	lock "matDachLinks","matDachRechts","matDachunterseite","bLinkMaterials", "matWandInnen", "matWandInnen2"
	hideparameter "matDachLinks","matDachRechts","matDachunterseite","bLinkMaterials", "matWandInnen", "matWandInnen2"
else
	if bLinkMaterials then
		if GLOB_MODPAR_NAME = "matDachRechts" then
			matDachLinks = matDachRechts
			parameters matDachLinks = matDachLinks
		else
			matDachRechts = matDachLinks
			parameters matDachRechts = matDachRechts
		endif
	endif
endif

tanDach = tan(ac_roofang)

yEcke = dUeberstandGaubeVorn + zHoeheTraufe / tanDach
ttt=yEcke/cos(ac_roofang)
parameters b=ttt

values{2} "iSkylightWinShape" 	WINSHAPE_RECTANGULAR, `Прямоугольное`,
								WINSHAPE_TRIANGULAR, `Треугольное`,
								WINSHAPE_ROUND, `Круглое`

if iSkylightWinShape = WINSHAPE_RECTANGULAR then
	lock "hStich"
	hideparameter "hStich"
endif

if not(ac_skylight_debug) then
	hideparameter "ac_skylight_function"
	if ac_skylight_function then
		hideparameter	"ac_roofang",
						"ac_roofthk",
						"gs_roof_material",
						"gs_roofang_deg",
						"gs_roofang_perc",
						"gs_roofang_12f",
						"gs_roofang_12i"
	endif
endif


! ==============================================================================
! Convert Display Option settings of old library parts
! ==============================================================================

parameters AC_special_2D_symbol = 0
hideparameter	"AC_special_2D_symbol",
				"AC_plane_definition",
				"AC_symb_display_option",
				"AC_symb_show_projection_to"


! ==============================================================================
! 2D Display Options
! ==============================================================================

parameters AC_use_symb_display_option = 1

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "gs_use_3D_view_attributes"
	lock "gs_use_3D_view_attributes"
endif

if gs_use_3D_view_attributes then
	hideparameter "AC_uncut_pen"
	lock "AC_uncut_pen"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "AC_cut_linetype", "AC_uncut_pen"
	lock "AC_cut_linetype", "AC_uncut_pen"
endif

if AC_symb_display_option = 1 | AC_symb_display_option = 3 | AC_symb_display_option = 4 then
	hideparameter "AC_overhead_linetype", "AC_overhead_pen"
	lock "AC_overhead_linetype", "AC_overhead_pen"
endif

if AC_symb_display_option = 4 then
	hideparameter "AC_cut_linetype"
	lock "AC_cut_linetype"
endif

if AC_symb_display_option = 5 then
	hideparameter "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock "gs_fill_type", "gs_fill_pen", "gs_back_pen"
endif

! ==============================================================================

if not(AC_Custom_3D_Hole) then
	hideparameter "gs_hole_edge_upper_depth", "gs_hole_edge_lower_depth", "gs_hole_outside_depth"
	lock "gs_hole_edge_upper_depth", "gs_hole_edge_lower_depth", "gs_hole_outside_depth"
endif


