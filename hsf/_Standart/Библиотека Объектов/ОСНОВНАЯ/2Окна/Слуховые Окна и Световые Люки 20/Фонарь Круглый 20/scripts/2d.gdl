

! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
	returned_parameters	iLoD2D,
		gs_opLinePen, gs_openedSashPen, gs_iSwingType, gs_swingLineType, gs_opline_style_m

if iLoD2D = SYMBS_OFF | iLoD2D = SYMBS_S_1 then end


! ==============================================================================
! Symbolic 2D
! ==============================================================================

pen gs_cont_pen

if abs(cos(ac_roofang)) < EPS then
	if GLOB_PREVIEW_MODE = 1 then
		ac_roofang = 0
	else
		end
	endif
endif
mul2 1,cos(ac_roofang)

if gs_back_pen<>0 then
	mask=1+2+4+64
else
	mask=1+4
endif

if iFlangeShape = FLANGESHAPE_RECTANGULAR then
	rect2 -a/2,0,a/2,a
	circle2 0,a/2,a/2-gs_flange_width
	hotspot2 0,0, 1
	hotspot2 0,a/2, 2
	hotspot2 0,a,3
	hotspot2 a/2,a/2,4
	hotspot2 -a/2,a/2,5
	hotspot2 -a/2,0,6
	hotspot2 a/2,0,7
	hotspot2 -a/2,a,8
	hotspot2 a/2,a,9

	fill gs_fill_type
	poly2_b 5,mask,gs_fill_pen,gs_back_pen,
			A/2,0,1,
		   -A/2,0,1,
		   -A/2,a,1,
			A/2,a,1,
			A/2,0,-1
endif

if iFlangeShape = FLANGESHAPE_CIRCULAR then
	circle2 0,a/2,a/2
	circle2 0,a/2,a/2-gs_flange_width
	hotspot2 0,0, 1
	hotspot2 0,a/2, 2
	hotspot2 0,a, 3
	hotspot2 a/2,a/2, 4
	hotspot2 -a/2,a/2, 5

	fill gs_fill_type
	poly2_b 2, 2+64,gs_fill_pen,gs_back_pen,
			0,a/2,901,
			a/2,360,4001

	if ac_skylight_function then
		fill gs_roof_fill_type
		poly2_b{2} 7, 1+2+4+8+64,gs_roof_fill_fpen,gs_roof_fill_bpen,
				0,0, SYMB_ROTANGLE/360/2*pi,
				A/2,0,1,
			   -A/2,0,1,
			   -A/2,a,1,
				A/2,a,1,
				A/2,0,-1,

				0,A/2,901,
				A/2, 360, 4001
	endif

endif

