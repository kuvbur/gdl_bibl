

! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
	returned_parameters	iLoD3D,
						lod3D_Casing,
						bOplines3d, opLineType3dIn, opLineType3dOut, gs_opline_style_m, gs_opLinePen, iOldResol


if ac_edge_lower_type = 0 then alphaL = ac_roofang
if ac_edge_lower_type = 1 then alphaL = 0
if ac_edge_lower_type = 2 then alphaL = -90 + ac_roofang
if ac_edge_lower_type = 3 then alphaL = 90 - ac_edge_lower_angle

if ac_edge_upper_type = 0 then alphaU = ac_roofang
if ac_edge_upper_type = 1 then alphaU = 0
if ac_edge_upper_type = 2 then alphaU = 90 - ac_roofang
if ac_edge_upper_type = 3 then alphaU = -90 + ac_edge_upper_angle

if abs(ac_roofang) > EPS then
	top_oversize = ac_roofthk * tan(alphaU)
	bottom_oversize = ac_roofthk * tan(alphaL)
else
	top_oversize = 0
	bottom_oversize = 0
endif

! ==============================================================================

rotx ac_roofang

! ==============================================================================
! Wallhole
! ==============================================================================

bSectionElevationContext = ((GLOB_VIEW_TYPE = 4) | (GLOB_VIEW_TYPE = 5) | (GLOB_PREVIEW_MODE = 2))
ShowUpContLines = 1	! not(bSectionElevationContext)

resWeight = (2*atn(min(1,(iOldResol/4)))) / 90
gs_resol = max(12,4*int(resWeight * gs_resol/4))
resol gs_resol

if SHELLBASE_VIEW_PEN > 0 then
	pen SHELLBASE_VIEW_PEN
else
	pen 1
endif

if SHELLBASE_MAT_OPPOSITE > 0 then
	material SHELLBASE_MAT_OPPOSITE
else
	material 1
endif

unID = 200

if AC_Custom_3D_Hole then
	addy B/2
	hotspot	0, 0,  0,						unID, gs_hole_outside_depth, 1+128 : unID=unID+1
	hotspot	0, 0, -1,						unID, gs_hole_outside_depth, 3     : unID=unID+1
	hotspot	0, 0,  gs_hole_outside_depth,	unID, gs_hole_outside_depth, 2     : unID=unID+1
	del 1

	if (GLOB_VIEW_TYPE = 3 & GLOB_FEEDBACK_MODE = 1) then
		model wire
		prism_ 2, gs_hole_outside_depth,
				0,B/2,995,
				A/2 - gs_flange_width,360,4095
		model solid
	endif
	wallniche 2, 1, 2,
		0,0,1,	gs_hole_outside_depth,
		0,B/2,995,
		A/2 - gs_flange_width,360,4095
else
	wallniche 2, 1, 2+16,
		0,0,-1,	0,
		0,B/2,995,
		A/2 - gs_flange_width,360,4095
endif


if SHELLBASE_MAT_REFERENCE > 0 then
	material SHELLBASE_MAT_REFERENCE
else
	material 1
endif

if AC_Custom_3D_Hole then
	addy B/2
	hotspot	0, 0,  0,						unID, gs_hole_inside_depth, 1+128 : unID=unID+1
	hotspot	0, 0, 1,						unID, gs_hole_inside_depth, 3     : unID=unID+1
	hotspot	0, 0,  -gs_hole_inside_depth,	unID, gs_hole_inside_depth, 2     : unID=unID+1
	del 1

	mulz -1
	if (GLOB_VIEW_TYPE = 3 & GLOB_FEEDBACK_MODE = 1) then
		model wire
		prism_ 2, gs_hole_inside_depth,
				0,B/2,995,
				A/2 - gs_flange_width,360,4095
		model solid
	endif
	wallniche 2, 1, 2,
		0,0,1,	gs_hole_inside_depth,
		0,B/2,995,
		A/2 - gs_flange_width,360,4095
	del 1
else
	wallniche 2, 1, 2+16,
		0,0,1,	0,
		0,	B/2, 979 + 16 * ShowUpContLines,
		A/2 - gs_flange_width,360,4079 + 16 * ShowUpContLines
endif

! ==============================================================================


pen gs_cont_pen
material gs_frame_mat
resol gs_resol



if iFlangeShape = FLANGESHAPE_RECTANGULAR then
	hotspot 0,a/2,0,1
	hotspot -a/2,0,0,2
	hotspot a/2,0,0,3
	hotspot -a/2,a,0,4
	hotspot a/2,a,0,5

	hotspot -a/2,0,-ac_roofthk,6
	hotspot a/2,0,-ac_roofthk,7
	hotspot -a/2,a,-ac_roofthk,8
	hotspot a/2,a,-ac_roofthk,9
else
	hotspot 0,a/2,0,1
	hotspot 0,0,0,2
	hotspot 0,a,0,3
	hotspot a/2,a/2,0,4
	hotspot -a/2,a/2,0,5

	hotspot 0,0,-ac_roofthk,6
	hotspot 0,a,-ac_roofthk,7
	hotspot a/2,a/2,-ac_roofthk,8
	hotspot -a/2,a/2,-ac_roofthk,9
endif

if iLoD3D = SYMBS_3D_OFF then							end
if iLoD3D = SYMBS_3D_S_1 then							gosub "singlePlane"
if iLoD3D = SYMBS_3D_S_2 then							gosub "windowPoligons"
if iLoD3D = SYMBS_3D_M_1 then							gosub "fullDetail"
if iLoD3D = SYMBS_3D_D_1 | iLoD3D = SYMBS_3D_D_2 then	gosub "fullDetail"

del 1

end


! ==============================================================================
! Subroutine
! ==============================================================================

"fullDetail":
	for n=1 to 2
		if iFlangeShape = FLANGESHAPE_RECTANGULAR then
			if n=2 then
				if gs_bFillBody then
					prism_ 12,0.01,
						-a/2,0-bottom_oversize,15,
						a/2,0-bottom_oversize,15,
						a/2,a+top_oversize,15,
						-a/2,a+top_oversize,15,
						-a/2,0-bottom_oversize,-1,
						a/2-gs_flange_width,a/2,13,
						0,a/2,913,
						0.0,180,4013,
						-a/2+gs_flange_width,a/2,13,
						0,a/2,913,
						0.0,180,4013,
						a/2-gs_flange_width,a/2,-1
				endif
			else
				prism_ 12,0.01,
					-a/2,0,15,
					a/2,0,15,
					a/2,a,15,
					-a/2,a,15,
					-a/2,0,-1,
					a/2-gs_flange_width,a/2,13,
					0,a/2,913,
					0.0,180,4013,
					-a/2+gs_flange_width,a/2,13,
						0,a/2,913,
					0.0,180,4013,
					a/2-gs_flange_width,a/2,-1
			endif
		else
				prism_ 14,0.01,
					a/2,a/2,79,
					0,a/2,979,
					0.0,180,4079,
					-a/2,a/2,79,
					0,a/2,979,
					0.0,180,4079,
					a/2-gs_flange_width,a/2,-1 ,
					a/2-gs_flange_width,a/2,13,
					0,a/2,913,
					0.0,180,4013,
					-a/2+gs_flange_width,a/2,13,
					0,a/2,913,
					0.0,180,4013,
					a/2-gs_flange_width,a/2,-1
		endif
		addz -ac_roofthk-0.01
	next n

	body -1
	del 2

	addz -ac_roofthk-0.01

	if gs_bFillBody then
		prism_	14,0.01+ac_roofthk+0.01+ac_curb_height,
				a/2-gs_flange_width,a/2,79,
				0,a/2,979,
				0.0,180,4079,
				-a/2+gs_flange_width,a/2,79,
				0,a/2,979,
				0.0,180,4097,
				a/2-gs_flange_width,a/2,-1,
				a/2-gs_flange_width-0.01,a/2,13,
				0,a/2,913,
				0.0,180,4013,
				-a/2+gs_flange_width+0.01,a/2,13,
				0,a/2,913,
				0.0,180,4013,
				a/2-gs_flange_width-0.01,a/2,-1
	endif

	body -1

	addz 0.01+ac_roofthk+0.01+ac_curb_height
	addy a/2
	roty -90

	material gs_glass_mat

	revolve 3,360,0,
			gs_dome_height,0,1,
			0,1,801,
			0,a/2-gs_flange_width-0.01,1001

	body -1

	del 4
return

"windowPoligons":
	for n=1 to 2
		if iFlangeShape = FLANGESHAPE_RECTANGULAR then
			if n=2 then
				if gs_bFillBody then
					addz 0.008
					poly_ 12,
						-a/2,0-bottom_oversize,1,
						a/2,0-bottom_oversize,1,
						a/2,a+top_oversize,1,
						-a/2,a+top_oversize,1,
						-a/2,0-bottom_oversize,-1,
						a/2-gs_flange_width-0.002,a/2,1,
						0,a/2,901,
						0.0,180,4001,
						-a/2+gs_flange_width+0.002,a/2,1,
						0,a/2,901,
						0.0,180,4001,
						a/2-gs_flange_width-0.002,a/2,-1
					del 1
				endif
			else
				addz 0.002
				poly_ 12,
					-a/2,0,1,
					a/2,0,1,
					a/2,a,1,
					-a/2,a,1,
					-a/2,0,-1,
					a/2-gs_flange_width,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					-a/2+gs_flange_width,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					a/2-gs_flange_width,a/2,-1
				del 1
			endif
		else
			if n=2 then
				addz 0.008
				poly_ 14,
					a/2,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					-a/2,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					a/2-gs_flange_width,a/2,-1,
					a/2-gs_flange_width-0.002,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					-a/2+gs_flange_width+0.002,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					a/2-gs_flange_width-0.002,a/2,-1
				del 1
			else
				addz 0.002
				poly_ 14,
					a/2,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					-a/2,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					a/2-gs_flange_width,a/2,-1,
					a/2-gs_flange_width,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					-a/2+gs_flange_width,a/2,1,
					0,a/2,901,
					0.0,180,4001,
					a/2-gs_flange_width,a/2,-1
				del 1
			endif
		endif
		addz -ac_roofthk-0.01
	next n

	body -1
	del 2

	if gs_bFillBody then
		addz -ac_roofthk-0.002
		extrude	7, 0, 0, 0.01+ac_roofthk+0.002+ac_curb_height, 16+32,
				a/2-gs_flange_width-0.002,a/2,1,
				0,a/2,901,
				0.0,180,4001,
				-a/2+gs_flange_width+0.002,a/2,1,
				0,a/2,901,
				0.0,180,4001,
				a/2-gs_flange_width-0.002,a/2,-1
		del 1
	endif

	body -1

	addz ac_curb_height + 0.01
	addy a/2
	roty -90

	material gs_glass_mat

	revolve 3,360,0,
			gs_dome_height,0,1,
			0,1,801,
			0,a/2-gs_flange_width-0.002,1001

	body -1

	del 3
return

"singlePlane":
	add 0, A/2, 0
	mulz (gs_dome_height+ac_curb_height+0.01)/gs_dome_height
	roty -90

	material gs_glass_mat
	body -1

	revolve 3,360,0,
			gs_dome_height,0,1,
			0,1,801,
			0,A/2 - gs_flange_width,1001

	body -1

	del 3
return
