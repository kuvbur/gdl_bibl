
! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
					iSkylightType			= 5

ss = ""
rrr = request("Linear_dimension", "", ss)
bMetric = 0
if strstr (ss, "m") > 0 then bMetric = 1


if bMetric then
	values "ac_roofang_slope_mode" `°`,`%`
else
	values "ac_roofang_slope_mode" `°`,`%`, `на 12'`,`на 12"`
endif

!! Pitch Unit setting !!

LENGTH_12_FEET = 12'
LENGTH_12_INCHES = 12"

if ac_roofang_slope_mode = `°` then
	gs_roofang_perc	= tan(gs_roofang_deg) * 100
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `%` then
	gs_roofang_deg  = atn(gs_roofang_perc / 100)
	gs_roofang_12f	= (gs_roofang_perc / 100) * LENGTH_12_FEET
	gs_roofang_12i	= (gs_roofang_perc / 100) * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_12f"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12'` then
	slope = gs_roofang_12f / LENGTH_12_FEET
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12i	= slope * LENGTH_12_INCHES

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12i"
endif

if ac_roofang_slope_mode = `на 12"` then
	slope = gs_roofang_12i / LENGTH_12_INCHES
	gs_roofang_deg	= atn(slope)
	gs_roofang_perc	= slope * 100
	gs_roofang_12f	= slope * LENGTH_12_FEET

	hideparameter "gs_roofang_deg"
	hideparameter "gs_roofang_perc"
	hideparameter "gs_roofang_12f"
endif

parameters gs_roofang_deg	= gs_roofang_deg
parameters gs_roofang_perc	= gs_roofang_perc
parameters gs_roofang_12f	= gs_roofang_12f
parameters gs_roofang_12i	= gs_roofang_12i


! ==============================================================================
! Cutting Body
! ==============================================================================

values "gs_hole_inside_depth"	range[0,]
values "gs_hole_outside_depth"	range[0,]

if not(AC_Custom_3D_Hole) then
	lock "gs_hole_inside_depth", "gs_hole_outside_depth"
	hideparameter "gs_hole_inside_depth", "gs_hole_outside_depth"
endif

! FM Types --------------------------------------------------------------------
call "FM_types" parameters all

values "gs_edge_num" range [3, )
values "gs_radius" range [0.01, )
values "gs_dome_height" range [0.01, )
values "gs_curb_thk" range (0, A/4]

if gs_cont_pen<=0 then parameters gs_cont_pen=2

parameters zzyzx=gs_dome_height + ac_curb_height + gs_frame_thk
lock "zzyzx"

if A < 0.01 then
	A = 0.01
	parameters A = A
endif

if not(ac_skylight_debug) then
	hideparameter "ac_skylight_function"
	if ac_skylight_function then
		hideparameter	"ac_roofang",
						"ac_roofthk",
						"gs_roof_material",
						"gs_roofang_deg",
						"gs_roofang_perc",
						"gs_roofang_12f",
						"gs_roofang_12i",
						"ac_roofang_slope_mode"
	endif
endif


! ==============================================================================
! Convert Display Option settings of old library parts
! ==============================================================================

parameters AC_special_2D_symbol = 0
hideparameter	"AC_special_2D_symbol",
				"AC_plane_definition",
				"AC_symb_display_option",
				"AC_symb_show_projection_to"


! ==============================================================================
! Edge
! ==============================================================================

parameters AC_edge_lower_type = 1
parameters AC_edge_upper_type = 1
parameters gs_edge_lower_type = stEdgeTypes[2]
parameters gs_edge_upper_type = stEdgeTypes[2]
lock "AC_edge_lower_type", "AC_edge_upper_type", "gs_edge_lower_type", "gs_edge_upper_type"


! ==============================================================================
! 2D Display Options
! ==============================================================================

parameters AC_use_symb_display_option = 1

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "gs_use_3D_view_attributes"
	lock "gs_use_3D_view_attributes"
endif

if gs_use_3D_view_attributes then
	hideparameter "AC_uncut_pen"
	lock "AC_uncut_pen"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "AC_cut_linetype", "AC_uncut_pen"
	lock "AC_cut_linetype", "AC_uncut_pen"
endif

if AC_symb_display_option = 1 | AC_symb_display_option = 3 | AC_symb_display_option = 4 then
	hideparameter "AC_overhead_linetype", "AC_overhead_pen"
	lock "AC_overhead_linetype", "AC_overhead_pen"
endif

if AC_symb_display_option = 4 then
	hideparameter "AC_cut_linetype"
	lock "AC_cut_linetype"
endif

if AC_symb_display_option = 5 then
	hideparameter "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock "gs_fill_type", "gs_fill_pen", "gs_back_pen"
endif

