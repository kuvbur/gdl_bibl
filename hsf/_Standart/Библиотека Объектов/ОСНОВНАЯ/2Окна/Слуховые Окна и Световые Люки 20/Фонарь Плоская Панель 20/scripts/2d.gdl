

! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
	returned_parameters	iLoD2D,
		gs_opLinePen, gs_openedSashPen, gs_iSwingType, gs_swingLineType, gs_opline_style_m

if iLoD2D = SYMBS_OFF | iLoD2D = SYMBS_S_1 then end

pen gs_cont_pen

if abs(cos(ac_roofang)) < EPS then
	if (GLOB_PREVIEW_MODE = 1) then
		ac_roofang = 0
	else
		end
	endif
endif
mul2 1,cos(ac_roofang)

unID=1
hotspot2 0, 0, unID : unID=unID+1
hotspot2 -a/2, 0, unID : unID=unID+1
hotspot2 a/2, 0, unID : unID=unID+1
hotspot2 -a/2, b/2, unID : unID=unID+1
hotspot2 a/2, b/2, unID : unID=unID+1
hotspot2 -a/2, b, unID : unID=unID+1
hotspot2 a/2, b, unID : unID=unID+1
hotspot2 0, b, unID : unID=unID+1

if gs_back_pen<>0 then
	mask=1+2+4+64
else
	mask=1+4
endif

drawindex 10

pen gs_cont_pen
fill gs_fill_type
poly2_b 5, mask, gs_fill_pen, gs_back_pen,
		-a/2, 0, 1,
		a/2, 0, 1,
		a/2, b, 1,
		-a/2, b, 1,
		-a/2, 0, -1

if iLoD2D = SYMBS_D_1 | iLoD2D = SYMBS_D_2 then				!Detailed
	poly2_b 5, mask, gs_fill_pen, gs_back_pen,
			-a/2+grill_thk, grill_thk, 1,
			a/2-grill_thk, grill_thk, 1,
			a/2-grill_thk, b-grill_thk, 1,
			-a/2+grill_thk, b-grill_thk, 1,
			-a/2+grill_thk, grill_thk, -1

	add2 -a/2, 0

	drawindex 20
	for i=1 to h
		add2  0,b/(h+1)
		poly2_b	5, mask, gs_fill_pen, gs_back_pen,
				grill_thk, -grill_thk/2, 1,
				grill_thk, grill_thk/2, 1,
				a-grill_thk, grill_thk/2, 1,
				a-grill_thk,-grill_thk/2, 1,
				grill_thk, -grill_thk/2, -1

	next i
	del h

	drawindex 30

	fill gs_fill_type
	for i=1 to v
		add2 a/(v+1), 0
		poly2_b	5, mask, gs_fill_pen, gs_back_pen,
				-grill_thk/2, grill_thk, 1,
				 grill_thk/2, grill_thk, 1,
				 grill_thk/2, b-grill_thk, 1,
				-grill_thk/2, b-grill_thk, 1,
				-grill_thk/2, grill_thk, -1
	next i
	del v+1
else
	add2 -a/2, 0
	for i=1 to v
		add2 a/(v+1), 0
		line2   0, 0, 0, b
	next i
	del v

	for i=1 to h
		add2  0,b/(h+1)
		line2   0, 0, a, 0
	next i
	del h+1
endif
