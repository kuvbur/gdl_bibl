

! =============================================================================
! GS Skylight Functions
! =============================================================================

call "GS Skylight Functions",
	parameters all	bEnableLining			= bEnableLining,
					bEnableEdgeTypesUpper	= bEnableEdgeTypesUpper,
					bEnableEdgeTypesLower	= bEnableEdgeTypesLower,
	returned_parameters	iLoD3D,
						lod3D_Casing,
						bOplines3d, opLineType3dIn, opLineType3dOut, gs_opline_style_m, gs_opLinePen


pen gs_cont_pen

rotx ac_roofang

! ==============================================================================
! Wallhole
! ==============================================================================

call "SkylightRect_RoofCutting_macro" parameters A = A, B = B,
	AC_edge_upper_type			= AC_edge_upper_type,
	AC_edge_upper_angle			= AC_edge_upper_angle,
	AC_edge_lower_type			= AC_edge_lower_type,
	AC_edge_lower_angle			= AC_edge_lower_angle,
	AC_Custom_3D_Hole			= AC_Custom_3D_Hole,
	gs_hole_edge_upper_depth	= gs_hole_edge_upper_depth,
	gs_hole_edge_lower_depth	= gs_hole_edge_lower_depth,
	gs_hole_outside_depth		= gs_hole_outside_depth,
	ac_upper_mat				= ac_upper_mat,
	gs_lower_bott_mat			= ac_lower_mat,
	gs_lower_top_mat			= ac_lower_mat,
	ac_roofang					= ac_roofang,
	bUpperCut					= 1,
	ShowUpContLines				= 1


! ==============================================================================

unID=1

! ==============================================================================
! Hotspots
! ==============================================================================

![#COMPATIBILITY] - START
idxVerticalHotspots		= 50
idxHorizontalHotspotsNew= 100
idxSkylight				= 7

if gs_a_half < EPS then gs_a_half = A / 2
![#COMPATIBILITY] - END

![#COMPATIBILITY] - START
hotspot -A/2, 0,0, 1
hotspot  A/2, 0,0, 4
![#COMPATIBILITY] - END

idx = idxVerticalHotspots

hotspot -A/2, 0,0,idx,B,1+256:	idx=idx+1	! (50)
hotspot -A/2,-1,0,idx,B,3:		idx=idx+1	! (51)
hotspot -A/2, B,0,idx,B,2:		idx=idx+1	! (52)

hotspot A/2, 0,0,idx,B,1+256:	idx=idx+1	! (53)
hotspot A/2,-1,0,idx,B,3:		idx=idx+1	! (54)
hotspot A/2, B,0,idx,B,2:		idx=idx+1	! (55)

idx = idxHorizontalHotspotsNew

hotspot 0,			0,0,idx,						gs_a_half,1+128,A:	idx=idx+1	! (100)
hotspot 1,			0,0,idx,						gs_a_half,3,	A:	idx=idx+1	! (101)
hotspot -gs_a_half,	0,0,6,		gs_a_half,2,	A:	idx=idx+1	! (6)	[#COMPATIBILITY]

hotspot  0,			0,0,idx,	gs_a_half,1+128,A:	idx=idx+1	! (103)
hotspot -1,			0,0,idx,	gs_a_half,3,	A:	idx=idx+1	! (104)
hotspot  gs_a_half,	0,0,3,		gs_a_half,2,	A:	idx=idx+1	! (3)	[#COMPATIBILITY]

hotspot 0,			B,0,idx,	gs_a_half,1+128,A:	idx=idx+1	! (106)
hotspot 1,			B,0,idx,	gs_a_half,3,	A:	idx=idx+1	! (107)
hotspot -gs_a_half,	B,0,idx,	gs_a_half,2,	A:	idx=idx+1	! (108)

hotspot  0,			B,0,idx,	gs_a_half,1+128,A:	idx=idx+1	! (109)
hotspot -1,			B,0,idx,	gs_a_half,3,	A:	idx=idx+1	! (110)
hotspot  gs_a_half,	B,0,idx,	gs_a_half,2,	A:	idx=idx+1	! (111)



mullcurb_thk = MAX(curb_thk, grill_thk)


if iLoD3D <> SYMBS_3D_S_1 then
	gosub "Curb"
	addz ac_curb_height
endif

if iLoD3D = SYMBS_3D_D_1 | iLoD3D = SYMBS_3D_D_2 then gosub "Frame"

singPlane_w = A - 2 * mullcurb_thk
singPlane_h = B - 2 * mullcurb_thk

if iLoD3D = SYMBS_3D_M_1 then
	addy mullcurb_thk
		gosub "SinglePlane"
	del 1
endif

singPlane_w = A
singPlane_h = B

if iLoD3D = SYMBS_3D_S_2 then
	gosub "SinglePlane"
endif

if iLoD3D <> SYMBS_3D_S_1 then del 1

if iLoD3D = SYMBS_3D_S_1 then
	gosub "SinglePlane"
endif

end


! ==============================================================================
"Curb":

unID = idxSkylight

!---------------------!
! Curb Height Editing !
!---------------------!

hotspot -a/2,0,0,unID,ac_curb_height,1+128 : unID=unID+1
hotspot -a/2,0,-1,unID,ac_curb_height,3 : unID=unID+1
hotspot -a/2,0,ac_curb_height,unID,ac_curb_height,2 : unID=unID+1

hotspot a/2,0,0,unID,ac_curb_height,1+128 : unID=unID+1
hotspot a/2,0,-1,unID,ac_curb_height,3 : unID=unID+1
hotspot a/2,0,ac_curb_height,unID,ac_curb_height,2 : unID=unID+1

hotspot -a/2,b,0,unID,ac_curb_height,1+128 : unID=unID+1
hotspot -a/2,b,-1,unID,ac_curb_height,3 : unID=unID+1
hotspot -a/2,b,ac_curb_height,unID,ac_curb_height,2 : unID=unID+1

hotspot a/2,b,0,unID,ac_curb_height,1+128 : unID=unID+1
hotspot a/2,b,-1,unID,ac_curb_height,3 : unID=unID+1
hotspot a/2,b,ac_curb_height,unID,ac_curb_height,2 : unID=unID+1


material gs_curb_mat

if abs(ac_curb_height) > EPS then
	if iLoD3D = SYMBS_3D_D_1 | iLoD3D = SYMBS_3D_D_2 then
		if abs(curb_thk) > EPS then
			prism_  10, ac_curb_height,
				-a/2, 0, 15,
				 a/2, 0, 15,
				 a/2, b, 15,
				-a/2, b, 15,
				-a/2, 0, -1,
				-a/2+curb_thk, curb_thk, 15,
				 a/2-curb_thk, curb_thk, 15,
				 a/2-curb_thk, b-curb_thk, 15,
				-a/2+curb_thk, b-curb_thk, 15,
				-a/2+curb_thk, curb_thk, -1
		else
			extrude 4, 0,0, ac_curb_height, 4+16+32+128,
				-A/2, 0, 0,
				 A/2, 0, 0,
				 A/2, B, 0,
				-A/2, B, 0
		endif
	endif
	
	if iLoD3D = SYMBS_3D_M_1 then
		if abs(mullcurb_thk) > EPS then
			prism_  10, ac_curb_height,
				-a/2, 0, 15,
				 a/2, 0, 15,
				 a/2, b, 15,
				-a/2, b, 15,
				-a/2, 0, -1,
				-a/2+mullcurb_thk, mullcurb_thk, 15,
				 a/2-mullcurb_thk, mullcurb_thk, 15,
				 a/2-mullcurb_thk, b-mullcurb_thk, 15,
				-a/2+mullcurb_thk, b-mullcurb_thk, 15,
				-a/2+mullcurb_thk, mullcurb_thk, -1
		else
			extrude 4, 0,0, ac_curb_height, 4+16+32+128,
				-A/2, 0, 0,
				 A/2, 0, 0,
				 A/2, B, 0,
				-A/2, B, 0
		endif
	endif
	
	if iLoD3D = SYMBS_3D_S_2 then
		extrude 4, 0,0,ac_curb_height, 4+16+32+128,
			-A/2, 0, 1,
			 A/2, 0, 1,
			 A/2, B, 1,
			-A/2, B, 1
	endif
endif

return


! ==============================================================================
"Frame":

material gs_frame_mat
if abs(grill_thk) > EPS then
	prism_  10, grill_thk,
		-a/2-grill_thk/2, -grill_thk/2, 11,
		 a/2+grill_thk/2, -grill_thk/2, 11,
		 a/2+grill_thk/2, b+grill_thk/2, 11,
		-a/2-grill_thk/2, b+grill_thk/2, 11,
		-a/2-grill_thk/2, -grill_thk/2, -1,
		-a/2+2*grill_thk, 2*grill_thk, 15,
		 a/2-2*grill_thk, 2*grill_thk, 15,
		 a/2-2*grill_thk, b-2*grill_thk, 15,
		-a/2+2*grill_thk, b-2*grill_thk, 15,
		-a/2+2*grill_thk, 2*grill_thk, -1
	addz grill_thk
	prism_  10, grill_thk,
			-a/2-grill_thk/2, -grill_thk/2, 14,
			 a/2+grill_thk/2, -grill_thk/2, 14,
			 a/2+grill_thk/2, b+grill_thk/2, 14,
			-a/2-grill_thk/2, b+grill_thk/2, 14,
			-a/2-grill_thk/2, -grill_thk/2, -1,
			-a/2, 0, 8,
			 a/2, 0, 8,
			 a/2, b, 8,
			-a/2, b, 8,
			-a/2, 0, -1
	del 1
endif
add -a/2,0,grill_thk

for j=1 to h+1
	for i=1 to v+1
		if abs(grill_thk) > EPS then
			material gs_frame_mat
			prism_  10, grill_thk,
				0, 0, 8,
				a/(v+1), 0, 8,
				a/(v+1), b/(h+1), 8,
				0, b/(h+1), 8,
				0, 0, -1,
				grill_thk/2, grill_thk/2, 15,
				a/(v+1)-grill_thk/2, grill_thk/2, 15,
				a/(v+1)-grill_thk/2, b/(h+1)-grill_thk/2, 15,
				grill_thk/2, b/(h+1)-grill_thk/2, 15,
				grill_thk/2, grill_thk/2, -1
		endif
		addz 2*grill_thk/5
		material gs_glass_mat
		prism_  4, 2*grill_thk/5,
				grill_thk/2, grill_thk/2, 15,
				a/(v+1)-grill_thk/2, grill_thk/2, 15,
				a/(v+1)-grill_thk/2, b/(h+1)-grill_thk/2, 15,
				grill_thk/2, b/(h+1)-grill_thk/2, 15
		del 1
		addx a/(v+1)
		next i
	del v+1
	addy b/(h+1)
	next j

del (h+1)

del 2


return


! ==============================================================================
"SinglePlane":

material gs_glass_mat

addx -singPlane_w/2
rect singPlane_w, singPlane_h
del 1


if iLoD3D = SYMBS_3D_M_1 then
	for j=1 to h+1
		lin_ -singPlane_w/2, 0, 0, singPlane_w/2, 0, 0

		addy singPlane_h/(h+1)
	next j
	del (h+1)


	addx -singPlane_w/2

	for i=1 to v+1
		lin_ 0, 0, 0, 0, singPlane_h, 0

		addx singPlane_w/(v+1)
	next i
	del v+1

	del 1
endif

return



