
! Contact person: <SzL, NJ>

! =============================================================================
! Calculations
! =============================================================================

topHalfAng = (90 + topAng)/2

shoulderHeight_tol_mod = gs_tolerance_left / tan(topHalfAng)
gs_shoulderHeight = gs_shoulderHeight - gs_tolerance_left - shoulderHeight_tol_mod

orig_ac_unit_height = ac_unit_height
ac_unit_height = ac_unit_height - (gs_tolerance_left / cos(topAng) - gs_tolerance_left)



!!Angle and Coordinate Calculation for Pentagon Shape!!

! Trim oversize handling
tr_ov_out = 0
tr_ov_in  = gs_trim_width_in

palpha=atn((ac_unit_height-gs_shoulderHeight)/(ac_unit_width/2))

pbeta=(90+palpha)/2

dyfw1=gs_frame_width/tan(pbeta)      !dy at frame width
dyfw2=gs_frame_width/cos(palpha)

dyShoulderTWIn		= gs_tw_right_in/tan(pbeta)
dyShoulderTWOut		= gs_tw_right_out/tan(pbeta)

dyShoulderWidthIn	= gs_trim_width_in/tan(pbeta)
dyShoulderWidthOut	= gs_trim_width_out/tan(pbeta)

dyShoulderOffsIn	= gs_trim_offs_in/tan(pbeta)
dyShoulderOffsOut	= gs_trim_offs_out/tan(pbeta)


dyTopTWIn		= gs_tw_right_in/cos(palpha)
dyTopTWOut		= gs_tw_right_out/cos(palpha)

dyTopWidthIn	= gs_trim_width_in/cos(palpha)
dyTopWidthOut	= gs_trim_width_out/cos(palpha)

dyTopOffsIn		= gs_trim_offs_in/cos(palpha)
dyTopOffsOut	= gs_trim_offs_out/cos(palpha)


! =============================================================================

lod3D = 0										! Don't draw anything
if (GLOB_VIEW_TYPE = 9 & GLOB_PREVIEW_MODE = 0) then	! in case of Surface IS Calculation
	lod3D = 2	! Detailed
else
	if iDWDet3D = DETLEVEL_DW3D_SIMPLE then lod3D = 1	! Simple
	if iDWDet3D = DETLEVEL_DW3D_DETAILED then lod3D = 2	! Detailed
	if iDWDet3D = DETLEVEL_DW3D_CHI_SIMPLE | iDWDet3D = DETLEVEL_DW3D_CHI_DETAILED then
		if (GLOB_CONTEXT = 4 | GLOB_CONTEXT = 24 | GLOB_CONTEXT = 6) then
			lod3D = 5								! Chinese
		else
			lod3D = 2
		endif
	endif
endif

gs_fillFr		= gs_bFills * gs_fillFr
gs_penFr_fg		= gs_bFills * gs_penFr_fg
gs_penFr_bg		= gs_bFills * gs_penFr_bg

gs_fillSash		= gs_bFills * gs_fillSash
gs_penSash_fg	= gs_bFills * gs_penSash_fg
gs_penSash_bg	= gs_bFills * gs_penSash_bg

if not(gs_shadow) then shadow off


! =============================================================================
! GS Window Functions
! =============================================================================

dim sillPoly[]
sillPoly[1] = 0

call "GS Window Functions",
	parameters all	iWindowShape			= iWindowShape,
					openingArchHeight		= openingArchHeight,
					gs_IsCurved				= gs_IsCurved,
					bParalellInCurvedWalls	= bParalellInCurvedWalls,
					lod3D					= lod3D,
					bDisabeMirroring		= bDisabeMirroring,
	returned_parameters	ac_unit_width,
						ac_unit_height,
						gs_frame_ovhg_left_out,
						gs_frame_ovhg_right_out,
						gs_frame_ovhg_top_out,
						gs_frame_ovhg_bottom_out,
						gs_frame_ovhg_left_in,
						gs_frame_ovhg_right_in,
						gs_frame_ovhg_top_in,
						gs_frame_ovhg_bottom_in,
						bMirrored,
						xform11, xform12, xform13, xform14,
						xform21, xform22, xform23, xform24,
						xform31, xform32, xform33, xform34,

						bOplines3d, opLineType3dIn, opLineType3dOut, gs_opline_style_m, gs_opLinePen,
						bShowAddHotspots, bShowAddHotspotsForIS, bShowUnitHotspots,

						sillRight,
						sillLeft,
						sillBottom,
						sillBottomOutside,
						sillSideHeight,
						sillSideAngle,
						sillPoly


! =============================================================================
! Block for operator mode
! =============================================================================

if GLOB_CONTEXT > 40 & GLOB_CONTEXT < 50 then
	addy gs_tolerance_lower
	prism_ 5, WIDO_FRAME_THICKNESS,
		-ac_unit_width/2,	0,	15,
		-ac_unit_width/2,	gs_shoulderHeight,	15,
		0,					ac_unit_height,		15,
		ac_unit_width/2,	gs_shoulderHeight,	15,
		ac_unit_width/2,	0,	15
	del 1
	end
endif


if lod3D = 0 then end

xform	xform11, xform12, xform13, xform14,
		xform21, xform22, xform23, xform24,
		xform31, xform32, xform33, xform34

muly -1
addy WIDO_FRAME_THICKNESS

temp = gs_frame_ovhg_left_out
gs_frame_ovhg_left_out = gs_frame_ovhg_left_in
gs_frame_ovhg_left_in = temp

temp = gs_frame_ovhg_right_out
gs_frame_ovhg_right_out = gs_frame_ovhg_right_in
gs_frame_ovhg_right_in = temp

temp = gs_frame_ovhg_top_in
gs_frame_ovhg_top_in = gs_frame_ovhg_top_out
gs_frame_ovhg_top_out = temp

temp = gs_frame_ovhg_bottom_in
gs_frame_ovhg_bottom_in = gs_frame_ovhg_bottom_out
gs_frame_ovhg_bottom_out = temp

if ((GLOB_PREVIEW_MODE = 2) | GLOB_CONTEXT = 46) then
	gs_sill_outside	= 0
	gs_board_inside	= 0
	gs_trim_inside	= 0
	gs_trim_outside	= 0
endif

if lod3D = 5 then	! Chinese
	fr_sect_fill_type	= gs_fillShCover
	fr_sect_fill_fg		= gs_penShCov_fg
	fr_sect_fill_bg		= gs_penShCov_bg
else
	fr_sect_fill_type	= gs_fillFr
	fr_sect_fill_fg		= gs_penFr_fg
	fr_sect_fill_bg		= gs_penFr_bg
endif


rotx 90

leftOvhg	=	max(gs_frame_ovhg_left_out, gs_frame_ovhg_left_in)
rightOvhg	=	leftOvhg
topOvhg		=	leftOvhg
bottomOvhg	=	leftOvhg


! =============================================================================
! Unit Hotspots
! =============================================================================

A = ac_unit_width
B = orig_ac_unit_height

if bShowUnitHotspots then
	unID=100

	hotspot  A/2, 0, 0,				unID	:unID=unID+1
	hotspot -A/2, 0, 0,				unID	:unID=unID+1
	hotspot  A/2, 0, gs_frame_thk,	unID	:unID=unID+1
	hotspot -A/2, 0, gs_frame_thk,	unID	:unID=unID+1

	hotspot  A/2, gs_shoulderHeight, gs_frame_thk, unID	:unID=unID+1
	hotspot -A/2, gs_shoulderHeight, gs_frame_thk, unID	:unID=unID+1

	hotspot 0, B, 0,			unID	:unID=unID+1
	hotspot 0, B, gs_frame_thk, unID

	!!! Shoulder Height
	unID=1300
	hotspot a/2,  0,					0, unID, gs_shoulderHeight, 1+128	:unID=unID+1
	hotspot a/2, -1,					0, unID, gs_shoulderHeight, 3		:unID=unID+1
	hotspot a/2,  gs_shoulderHeight,	0, unID, gs_shoulderHeight, 2

	unID=1310
	hotspot -a/2,  0,					0, unID, gs_shoulderHeight, 1+128	:unID=unID+1
	hotspot -a/2, -1,					0, unID, gs_shoulderHeight, 3		:unID=unID+1
	hotspot -a/2,  gs_shoulderHeight,	0, unID, gs_shoulderHeight, 2
endif


! =============================================================================
! Unit Hotspots for IS
! =============================================================================

if bShowAddHotspotsForIS then
	unID=108

	hotspot 0, 0, 0,			unID	:unID=unID+1
	hotspot 0, 0, gs_frame_thk,	unID
endif


! =============================================================================
! Casing
! =============================================================================

A = ac_unit_width
B = ac_unit_height

numCuts = 0

if (gs_trim_inside | gs_trim_outside) & lod3D <> 5 then

	tr_1 = -tr_ov_in/tan(pbeta)

	pen gs_frame_pen

! Straight Wall ---------------------------------------------------------------

	trimInWallholeMaskIn 	= 11 + (abs(gs_trim_offs_in) > EPS) * 4
	trimInWallholeMaskOut 	= 14 + (abs(gs_trim_offs_out) > EPS) * 1
	trimOnWallMaskIn 		= 14 + (abs(gs_trim_offs_in) > EPS) * 1
	trimOnWallMaskOut 		= 11 + (abs(gs_trim_offs_out) > EPS) * 4

	if abs(WIDO_ORIG_DIST) < EPS & abs(WALL_INCL) < EPS then
		if not(WIDO_REVEAL_SIDE) then
			addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS    ! Flip
			mulz -1
			addz -WALL_THICKNESS
		else
			addz -WIDO_SILL
		endif

		if gs_trim_inside then
			sect_fill gs_trim_fill_in, gs_trim_pen_bg_in, gs_trim_pen_fg_in, gs_trim_pen_cont
			material gs_trim_in_mat

			if gs_trim_width_in > 0 then
				addz WIDO_SILL + WIDO_FRAME_THICKNESS
				if gs_board_inside then
					prism_ 10,WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS,
						-a/2-tr_ov_out-leftOvhg,	-tr_ov_out-bottomOvhg+eps,						15,
						-a/2-tr_ov_out-leftOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),			15,
						 0,							b+leftOvhg/cos(palpha),							15,
						 a/2+tr_ov_out+RightOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),			15,
						 a/2+tr_ov_out+RightOvhg,	-tr_ov_out-bottomOvhg+eps,						15,
						 a/2-tr_ov_in+RightOvhg,	-tr_ov_out-bottomOvhg+eps,						trimInWallholeMaskIn,
						 a/2-tr_ov_in+RightOvhg,	gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),	trimInWallholeMaskIn,
						 0,							b-tr_ov_in/cos(palpha)+ leftOvhg/cos(palpha),	trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,		gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),	trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,		-tr_ov_out-bottomOvhg+eps,						trimInWallholeMaskIn
				else
					prism_ 12,WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS,
						-a/2-tr_ov_out-leftOvhg,-tr_ov_out-bottomOvhg,15,
						 a/2+tr_ov_out+RightOvhg,-tr_ov_out-bottomOvhg,15,
						 a/2+tr_ov_out+RightOvhg,gs_shoulderHeight+rightOvhg/tan(pbeta),15,
						 0,b+leftOvhg/cos(palpha),15,
						-a/2-tr_ov_out-leftOvhg,gs_shoulderHeight+rightOvhg/tan(pbeta),15,
						-a/2-tr_ov_out-leftOvhg,-tr_ov_out-bottomOvhg,-1,

						-a/2+tr_ov_in-leftOvhg,tr_ov_in-bottomOvhg,trimInWallholeMaskIn,
						 a/2-tr_ov_in+RightOvhg,tr_ov_in-bottomOvhg,trimInWallholeMaskIn,
						 a/2-tr_ov_in+RightOvhg,gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),trimInWallholeMaskIn,
						 0,b-tr_ov_in/cos(palpha)+ leftOvhg/cos(palpha),trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,tr_ov_in-bottomOvhg,-1
				endif
				del 1
			endif

			if gs_trim_thk_in > 0 then
				addz WALL_THICKNESS
				if gs_board_inside then
					base
					prism_ 4,gs_trim_thk_in,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,15,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),14,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,10
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1

					base
					prism_ 4,gs_trim_thk_in,
						a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,15,
						a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
						a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),14,
						a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,10
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				else
					base
					prism_ 4,gs_trim_thk_in,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,-gs_tw_bottom_in-gs_trim_offs_in-bottomOvhg,15,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,10
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1

					base
					prism_ 4,gs_trim_thk_in,
							a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,-gs_tw_bottom_in-gs_trim_offs_in-bottomOvhg,15,
							a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
							a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
							a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,10
						vert 0, 0, 0
						vert 0, 1, 0
						vert 1, 0, 0
						vert 0, 0, 1
						coor 2, -1, -2, -3, -4
						body 1

					base
					prism_ 4,gs_trim_thk_in,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,-gs_tw_bottom_in-gs_trim_offs_in-bottomOvhg,10,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,trimOnWallMaskIn,
						a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,10,
						a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,-gs_tw_bottom_in-gs_trim_offs_in-bottomOvhg,15
					vert 0, 0, 0
					vert 1, 0, 0
					vert 0, 1, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				endif

				base
				prism_ 4,gs_trim_thk_in,
					-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
					-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
					0,b+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_in)/cos(palpha),10,
					0,b+dyTopTWIn+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_in)/cos(palpha),15
				vert 0, 0, 0
				vert cos(palpha), sin(palpha), 0
				vert sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				base
				prism_ 4,gs_trim_thk_in,
					a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
					a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
					0,b+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_in)/cos(palpha),10,
					0,b+dyTopTWIn+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_in)/cos(palpha),15
				vert 0, 0, 0
				vert -cos(palpha), sin(palpha), 0
				vert -sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				del 1
			endif
		endif

		if not(WIDO_REVEAL_SIDE) then
			del 2
		endif

		del 1
	endif

	if abs(WIDO_ORIG_DIST) < EPS then
		if gs_trim_outside then

			if not(WIDO_REVEAL_SIDE) then
				addz WIDO_SILL - WALL_THICKNESS + WIDO_FRAME_THICKNESS    ! Flip
				mulz -1
				addz -WALL_THICKNESS
			else
				addz -WIDO_SILL
			endif

			sect_fill gs_trim_fill_out, gs_trim_pen_bg_out, gs_trim_pen_fg_out, gs_trim_pen_cont
			material gs_trim_out_mat

			if gs_sill_outside then
				sillmody		= SillBottomOutside
			else
				sillmody		= tr_ov_in - gs_trim_offs_out
			endif

			_offsetBottomCasingY = 0
			if not(bCasingBottomOutside) then
				if gs_sill_outside then
					addz WIDO_SILL
					gosub 300		!cutting below Sill
					del 1
				else
					_offsetBottomCasingY = (gs_trim_width_out+0.01)

					gosub "CasingCuttingOnParapet"
				endif
			endif

			if gs_sill_outside then

				addz WIDO_SILL
				gosub 200		!cutting with Sill
				del 1

				if gs_trim_width_out > 0 then
					prism_ 10, WIDO_SILL,
						-a/2-tr_ov_out-leftOvhg,	-tr_ov_out-bottomOvhg+sillmody+eps,				15,
						-a/2-tr_ov_out-leftOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),			15,
						 0,							b+leftOvhg/cos(palpha),							15,
						 a/2+tr_ov_out+RightOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),			15,
						 a/2+tr_ov_out+RightOvhg,	-tr_ov_out-bottomOvhg+sillmody+eps,				15,
						 a/2-tr_ov_in+RightOvhg,	-tr_ov_out-bottomOvhg+sillmody+eps,				trimInWallholeMaskOut,
						 a/2-tr_ov_in+RightOvhg,	gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),	trimInWallholeMaskOut,
						 0,							b-tr_ov_in/cos(palpha)+ leftOvhg/cos(palpha),	trimInWallholeMaskOut,
						-a/2+tr_ov_in-leftOvhg,		gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),	trimInWallholeMaskOut,
						-a/2+tr_ov_in-leftOvhg,		-tr_ov_out-bottomOvhg+sillmody+eps,				trimInWallholeMaskOut
				endif
			else
				if gs_trim_width_out > 0 then
					prism_ 12, WIDO_SILL,
						-a/2-tr_ov_out-leftOvhg,-tr_ov_out-bottomOvhg-_offsetBottomCasingY,15,
						 a/2+tr_ov_out+RightOvhg,-tr_ov_out-bottomOvhg-_offsetBottomCasingY,15,
						 a/2+tr_ov_out+RightOvhg,gs_shoulderHeight+rightOvhg/tan(pbeta),15,
						 0,b+leftOvhg/cos(palpha),15,
						-a/2-tr_ov_out-leftOvhg,gs_shoulderHeight+rightOvhg/tan(pbeta),15,
						-a/2-tr_ov_out-leftOvhg,-tr_ov_out-bottomOvhg-_offsetBottomCasingY,-1,

						-a/2+tr_ov_in-leftOvhg,tr_ov_in-bottomOvhg-_offsetBottomCasingY,trimInWallholeMaskOut,
						 a/2-tr_ov_in+RightOvhg,tr_ov_in-bottomOvhg-_offsetBottomCasingY,trimInWallholeMaskOut,
						 a/2-tr_ov_in+RightOvhg,gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),trimInWallholeMaskOut,
						 0,b-tr_ov_in/cos(palpha)+ leftOvhg/cos(palpha),trimInWallholeMaskOut,
						-a/2+tr_ov_in-leftOvhg,gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),trimInWallholeMaskOut,
						-a/2+tr_ov_in-leftOvhg,tr_ov_in-bottomOvhg-_offsetBottomCasingY,-1
				endif
			endif

			if gs_trim_thk_out > 0 then
				addz -gs_trim_thk_out

				base
				prism_ 4,gs_trim_thk_out,
					-a/2+tr_ov_in-gs_tw_right_out-gs_trim_offs_out-leftOvhg,sillmody-gs_tw_bottom_out-bottomOvhg-_offsetBottomCasingY,15,
					-a/2+tr_ov_in-gs_tw_right_out-gs_trim_offs_out-leftOvhg,gs_shoulderHeight+dyShoulderTWOut+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),10,
					-a/2+tr_ov_in-gs_trim_offs_out-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),trimOnWallMaskOut,
					-a/2+tr_ov_in-gs_trim_offs_out-leftOvhg,sillmody-bottomOvhg-_offsetBottomCasingY,10
				vert 0, 0, 0
				vert 0, 1, 0
				vert 1, 0, 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				base
				prism_ 4,gs_trim_thk_out,
						a/2-tr_ov_in+gs_tw_right_out+gs_trim_offs_out+RightOvhg,sillmody-gs_tw_bottom_out-bottomOvhg-_offsetBottomCasingY,15,
					a/2-tr_ov_in+gs_tw_right_out+gs_trim_offs_out+RightOvhg,gs_shoulderHeight+dyShoulderTWOut+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),10,
					a/2-tr_ov_in+gs_trim_offs_out+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),trimOnWallMaskOut,
					a/2-tr_ov_in+gs_trim_offs_out+RightOvhg,sillmody-bottomOvhg-_offsetBottomCasingY,10
				vert 0, 0, 0
				vert 0, 1, 0
				vert 1, 0, 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				base
				prism_ 4,gs_trim_thk_out,
					-a/2+tr_ov_in-gs_tw_right_out-gs_trim_offs_out-leftOvhg,sillmody-gs_tw_bottom_out-bottomOvhg-_offsetBottomCasingY,10,
					-a/2+tr_ov_in-gs_trim_offs_out-leftOvhg,sillmody-bottomOvhg-_offsetBottomCasingY,trimOnWallMaskOut,
					 a/2-tr_ov_in+gs_trim_offs_out+RightOvhg,sillmody-bottomOvhg-_offsetBottomCasingY,10,
					 a/2-tr_ov_in+gs_tw_right_out+gs_trim_offs_out+RightOvhg,sillmody-gs_tw_bottom_out-bottomOvhg-_offsetBottomCasingY,15
				vert 0, 0, 0
				vert 1, 0, 0
				vert 0, 1, 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1


				base
				prism_ 4,gs_trim_thk_out,
					-a/2+tr_ov_in-gs_tw_right_out-gs_trim_offs_out-leftOvhg,gs_shoulderHeight+dyShoulderTWOut+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),10,
					-a/2+tr_ov_in-gs_trim_offs_out-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),trimOnWallMaskOut,
					0,b+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_out)/cos(palpha),10,
					0,b+dyTopTWOut+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_out)/cos(palpha),15
				vert 0, 0, 0
				vert cos(palpha), sin(palpha), 0
				vert sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				base
				prism_ 4,gs_trim_thk_out,
					a/2-tr_ov_in+gs_tw_right_out+gs_trim_offs_out+RightOvhg,gs_shoulderHeight+dyShoulderTWOut+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),10,
					a/2-tr_ov_in+gs_trim_offs_out+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_out)/tan(pbeta),trimOnWallMaskOut,
					0,b+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_out)/cos(palpha),10,
					0,b+dyTopTWOut+tr_2-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_out)/cos(palpha),15
				vert 0, 0, 0
				vert -cos(palpha), sin(palpha), 0
				vert -sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				del 1
			endif

			if not(WIDO_REVEAL_SIDE) then
				del 2
			endif

			del 1

		if numCuts then
			for i=1 to numCuts
				cutend
			next i
		endif
		numCuts = 0

		endif
	endif

! Inclined Wall ---------------------------------------------------------------

	if abs(WIDO_ORIG_DIST) < EPS & abs(WALL_INCL) > EPS then

		wi = WALL_INCL    ! Inclination of the wall surfaces

		if not(WIDO_REVEAL_SIDE) then
			addz WIDO_SILL-WALL_THICKNESS+WIDO_FRAME_THICKNESS    ! Flip
			mulz -1
			addz -WALL_THICKNESS
		else
			addz -WIDO_SILL
		endif

		if gs_trim_inside then
			sect_fill gs_trim_fill_in, gs_trim_pen_bg_in, gs_trim_pen_fg_in, gs_trim_pen_cont
			material gs_trim_in_mat

			if not(SYMB_MIRRORED) & abs(SYMB_ROTANGLE-180)<EPS | SYMB_MIRRORED & abs(SYMB_ROTANGLE)<EPS then
				mulx -1  ! Rotated or mirrored window
			endif

			if gs_trim_width_in > 0 then
				rotx 90
				if wi>0 then
				cutpolya 4, 1, 0,
				   -a/2-tr_ov_out-leftOvhg,  WALL_THICKNESS-tan(wi)*(a/2+tr_ov_out+leftOvhg),   0,
					a/2+tr_ov_out+RightOvhg, WALL_THICKNESS+tan(wi)*(a/2+tr_ov_out+rightOvhg),  0,
				   -a/2-tr_ov_out-leftOvhg,  WALL_THICKNESS+tan(wi)*(a/2+tr_ov_out+rightOvhg),  0,
				   -a/2-tr_ov_out-leftOvhg,  WALL_THICKNESS-tan(wi)*(a/2+tr_ov_out+leftOvhg),  -1
				endif
				if wi<0 then
					cutpolya 4, 1, 0,
					   -a/2-tr_ov_out-leftOvhg,  WALL_THICKNESS-tan(wi)*(a/2+tr_ov_out+leftOvhg),   0,
						a/2+tr_ov_out+RightOvhg, WALL_THICKNESS-tan(wi)*(a/2+tr_ov_out+leftOvhg),   0,
						a/2+tr_ov_out+RightOvhg, WALL_THICKNESS+tan(wi)*(a/2+tr_ov_out+RightOvhg),  0,
					   -a/2-tr_ov_out-leftOvhg,  WALL_THICKNESS-tan(wi)*(a/2+tr_ov_out+leftOvhg),  -1
				endif
				del 1

				addz WIDO_SILL+WIDO_FRAME_THICKNESS
				if gs_board_inside then
					prism_ 10,WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS+abs(tan(wi))*(a/2+tr_ov_out+leftOvhg),
						-a/2-tr_ov_out-leftOvhg,	-tr_ov_out-bottomOvhg+eps,						15,
						-a/2-tr_ov_out-leftOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),			15,
						 0,							b+leftOvhg/cos(palpha),							15,
						 a/2+tr_ov_out+RightOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),			15,
						 a/2+tr_ov_out+RightOvhg,	-tr_ov_out-bottomOvhg+eps,						15,
						 a/2-tr_ov_in+RightOvhg,	-tr_ov_out-bottomOvhg+eps,						trimInWallholeMaskIn,
						 a/2-tr_ov_in+RightOvhg,	gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),	trimInWallholeMaskIn,
						 0,							b-tr_ov_in/cos(palpha)+ leftOvhg/cos(palpha),	trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,		gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),	trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,		-tr_ov_out-bottomOvhg+eps,						trimInWallholeMaskIn
				else
					prism_ 12,WALL_THICKNESS-WIDO_SILL-WIDO_FRAME_THICKNESS+abs(tan(wi))*(a/2+tr_ov_out+leftOvhg),
						-a/2-tr_ov_out-leftOvhg,-tr_ov_out-bottomOvhg,15,
						 a/2+tr_ov_out+RightOvhg,-tr_ov_out-bottomOvhg,15,
						 a/2+tr_ov_out+RightOvhg,gs_shoulderHeight+rightOvhg/tan(pbeta),15,
						 0,b+leftOvhg/cos(palpha),15,
						-a/2-tr_ov_out-leftOvhg,gs_shoulderHeight+rightOvhg/tan(pbeta),15,
						-a/2-tr_ov_out-leftOvhg,-tr_ov_out-bottomOvhg,-1,

						-a/2+tr_ov_in-leftOvhg,tr_ov_in-bottomOvhg,trimInWallholeMaskIn,
						 a/2-tr_ov_in+RightOvhg,tr_ov_in-bottomOvhg,trimInWallholeMaskIn,
						 a/2-tr_ov_in+RightOvhg,gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),trimInWallholeMaskIn,
						 0,b-tr_ov_in/cos(palpha)+ leftOvhg/cos(palpha),trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,gs_shoulderHeight+tr_1+rightOvhg/tan(pbeta),trimInWallholeMaskIn,
						-a/2+tr_ov_in-leftOvhg,tr_ov_in-bottomOvhg,-1
				endif
				del 1

				if abs(wi) > EPS then cutend
			endif

			if gs_trim_thk_in > 0 then
				if abs(gs_trim_offs_in)>EPS then
					lwam = 12
				else
					if not(WIDO_REVEAL_SIDE) exor (SYMB_MIRRORED exor (abs(SYMB_ROTANGLE-180)<EPS)) then
						lwam = 3
					else
						lwam = 0
					endif
				endif

				cutpolya 6, 1, 0,
					-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,6+lwam,
					 a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,6+lwam,
					 a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),6+lwam,
					 0,b-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_in)/cos(palpha),6+lwam,									!!+tr_2
					-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),6+lwam,
					-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,tr_ov_in-gs_trim_offs_in,-bottomOvhg-1

				addz WALL_THICKNESS
				roty -wi

				base
				prism_ 4,gs_trim_thk_in,
					-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
					-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
					0,b-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_in)/cos(palpha),10,					!!!+tr_2
					0,b+dyTopTWIn+tr_2+ (leftOvhg+gs_trim_offs_in)/cos(palpha),15
				vert 0, 0, 0
				vert cos(palpha), sin(palpha), 0
				vert sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				base
				prism_ 4,gs_trim_thk_in,
					a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
					a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
					0,b-tr_ov_in/cos(palpha)+ (leftOvhg+gs_trim_offs_in)/cos(palpha),10,									!!!+tr_2
					0,b+dyTopTWIn+tr_2+ (leftOvhg+gs_trim_offs_in)/cos(palpha),15
				vert 0, 0, 0
				vert -cos(palpha), sin(palpha), 0
				vert -sin(palpha), cos(palpha), 0
				vert 0, 0, 1
				coor 2, -1, -2, -3, -4
				body 1

				if gs_board_inside then
					base
					prism_ 4,gs_trim_thk_in,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,15,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
							-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,10
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1

					base
					prism_ 4,gs_trim_thk_in,
						a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,15,
						a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
						a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
						a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,-tr_ov_out-bottomOvhg-gs_trim_offs_in+eps,10
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				else
					base
					prism_ 4,gs_trim_thk_in,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,-gs_tw_bottom_in-gs_trim_offs_in-bottomOvhg,15,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,10
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1

					base
					prism_ 4,gs_trim_thk_in,
						a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,-gs_tw_bottom_in-gs_trim_offs_in-bottomOvhg,15,
						a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+dyShoulderTWIn+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),10,
						a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,gs_shoulderHeight+tr_1+(rightOvhg+gs_trim_offs_in)/tan(pbeta),trimOnWallMaskIn,
						a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,10
					vert 0, 0, 0
					vert 0, 1, 0
					vert 1, 0, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1

					base
					prism_ 4,gs_trim_thk_in,
						-a/2+tr_ov_in-gs_tw_right_in-gs_trim_offs_in-leftOvhg,-gs_tw_bottom_in-gs_trim_offs_in-bottomOvhg,10,
						-a/2+tr_ov_in-gs_trim_offs_in-leftOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,trimOnWallMaskIn,
						 a/2-tr_ov_in+gs_trim_offs_in+RightOvhg,tr_ov_in-gs_trim_offs_in-bottomOvhg,10,
						 a/2-tr_ov_in+gs_tw_right_in+gs_trim_offs_in+RightOvhg,-gs_tw_bottom_out-gs_trim_offs_in-bottomOvhg,15
					vert 0, 0, 0
					vert 1, 0, 0
					vert 0, 1, 0
					vert 0, 0, 1
					coor 2, -1, -2, -3, -4
					body 1
				endif

				del 2

				cutend
			endif

			if not(SYMB_MIRRORED) and abs(SYMB_ROTANGLE-180)<EPS or SYMB_MIRRORED and abs(SYMB_ROTANGLE)<EPS then del 1

		endif

		if not(WIDO_REVEAL_SIDE) then
			del 2
		endif

		del 1
	endif

! Curved Wall -----------------------------------------------------------------

	if abs(WIDO_ORIG_DIST) > EPS then

		if (WIDO_ORIG_DIST < 0) exor WIDO_REVEAL_SIDE then
			trimInWallholeMaskIn 	= 11 + (abs(gs_trim_offs_in) > EPS) * 4
			trimInWallholeMaskOut 	= 14 + (abs(gs_trim_offs_out) > EPS) * 1
			trimOnWallMaskIn 		= 14 + (abs(gs_trim_offs_in) > EPS) * 1
			trimOnWallMaskOut 		= 11 + (abs(gs_trim_offs_out) > EPS) * 4
		else
			trimInWallholeMaskIn 	= 14 + (abs(gs_trim_offs_in) > EPS) * 1
			trimInWallholeMaskOut 	= 11 + (abs(gs_trim_offs_out) > EPS) * 4
			trimOnWallMaskIn 		= 11 + (abs(gs_trim_offs_in) > EPS) * 4
			trimOnWallMaskOut 		= 14 + (abs(gs_trim_offs_out) > EPS) * 1
		endif

		resol WALL_RESOL

		mulz -1
		addz -WIDO_FRAME_THICKNESS

		!z axis curved wall calculation at subroutine 1000
		!off1, off2 etc. special offset values for outside, inside etc. elements "rfur" values
		!different from element to element!

		if gs_trim_inside then
			sect_fill gs_trim_fill_in, gs_trim_pen_bg_in, gs_trim_pen_fg_in, gs_trim_pen_cont
			material gs_trim_in_mat

			!1st part

			if gs_trim_thk_in > 0 then
				off1 = -gs_trim_thk_in
				off2 = 0
				off3 = WALL_THICKNESS
				off4 = -WALL_THICKNESS-gs_trim_thk_in
				gosub 1000

				x = gs_trim_width_in-gs_trim_offs_in
				ix = atn(x/r0)/180*pi*rfur

				xtw = x-gs_tw_right_in
				ixtw = atn(xtw/r0)/180*pi*rfur

				addz k_weitoffset
				mulz mulstat
				addz -rfur
				roty -AC_Hole_Position_Angle
				addz rfur

				if gs_board_inside then
					bprism_  gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,11,gs_trim_thk_in,rfur,
						xoffset+ax/2-ixtw,	0,																		15,
						xoffset+ax/2-ixtw,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn+dyShoulderTWIn,	15,
						xoffset,			b-dyTopWidthIn+dyTopOffsIn+dyTopTWIn,									15,
						xoffset-ax/2+ixtw,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn+dyShoulderTWIn,	15,
						xoffset-ax/2+ixtw,	0,																		15,
						xoffset-ax/2+ix,	0,																		trimOnWallMaskIn,
						xoffset-ax/2+ix,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn,					trimOnWallMaskIn,
						xoffset,			b-dyTopWidthIn+dyTopOffsIn,												trimOnWallMaskIn,
						xoffset+ax/2-ix,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn,					trimOnWallMaskIn,
						xoffset+ax/2-ix,	0,																		15,
						xoffset+ax/2-ixtw,	0,																		-1
				else
					bprism_  gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,12,gs_trim_thk_in,rfur,
						xoffset-ax/2+ixtw,	gs_trim_width_in-gs_trim_offs_in-gs_tw_bottom_in,						15,
						xoffset+ax/2-ixtw,	gs_trim_width_in-gs_trim_offs_in-gs_tw_bottom_in,						15,
						xoffset+ax/2-ixtw,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn+dyShoulderTWIn,	15,
						xoffset,			b-dyTopWidthIn+dyTopOffsIn+dyTopTWIn,									15,
						xoffset-ax/2+ixtw,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn+dyShoulderTWIn,	15,
						xoffset-ax/2+ixtw,	gs_trim_width_in-gs_trim_offs_in-gs_tw_bottom_in,						-1,

						xoffset-ax/2+ix,	gs_trim_width_in-gs_trim_offs_in,						trimOnWallMaskIn,
						xoffset+ax/2-ix,	gs_trim_width_in-gs_trim_offs_in,						trimOnWallMaskIn,
						xoffset+ax/2-ix,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn,	trimOnWallMaskIn,
						xoffset,			b-dyTopWidthIn+dyTopOffsIn,								trimOnWallMaskIn,
						xoffset-ax/2+ix,	gs_shoulderHeight-dyShoulderWidthIn+dyShoulderOffsIn,	trimOnWallMaskIn,
						xoffset-ax/2+ix,	gs_trim_width_in-gs_trim_offs_in,						-1
				endif
				del 5
			endif


			!2nd part

			if gs_trim_width_in > 0 then
				!Cutplanes at frame

				if WIDO_REVEAL_SIDE then
					cutplane 1,1,0,0
				else
					addz WIDO_FRAME_THICKNESS
					cutplane 1,1,0,1
					del 1
				endif

				off1=0
				off2=-WALL_THICKNESS
				off3=0
				off4=-WALL_THICKNESS
				gosub 1000

				x = gs_trim_width_in
				ix = atn(x/r0)/180*pi*rfur

				addz k_weitoffset
				mulz mulstat
				addz -rfur
				roty -AC_Hole_Position_Angle
				addz rfur

				if gs_board_inside then
					bprism_  gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,10,WALL_THICKNESS,rfur,
						xoffset-ax/2,		0,										15,
						xoffset-ax/2+ix,	0,										trimInWallholeMaskIn,
						xoffset-ax/2+ix,	gs_shoulderHeight-dyShoulderWidthIn,	trimInWallholeMaskIn,
						xoffset,			b-dyTopWidthIn,							trimInWallholeMaskIn,
						xoffset+ax/2-ix,	gs_shoulderHeight-dyShoulderWidthIn,	trimInWallholeMaskIn,
						xoffset+ax/2-ix,	0,										trimInWallholeMaskIn,
						xoffset+ax/2,		0,										15,
						xoffset+ax/2,		gs_shoulderHeight,						15,
						xoffset,			b,										15,
						xoffset-ax/2,		gs_shoulderHeight,						15
				else
					bprism_  gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,12,WALL_THICKNESS,rfur,
						xoffset-ax/2,	0,					15,
						xoffset+ax/2,	0,					15,
						xoffset+ax/2,	gs_shoulderHeight,	15,
						xoffset,		b,					15,
						xoffset-ax/2,	gs_shoulderHeight,	15,
						xoffset-ax/2,	0,					-1,

						xoffset-ax/2+ix,	gs_trim_width_in,						trimInWallholeMaskIn,
						xoffset+ax/2-ix,	gs_trim_width_in,						trimInWallholeMaskIn,
						xoffset+ax/2-ix,	gs_shoulderHeight-dyShoulderWidthIn,	trimInWallholeMaskIn,
						xoffset,			b-dyTopWidthIn,							trimInWallholeMaskIn,
						xoffset-ax/2+ix,	gs_shoulderHeight-dyShoulderWidthIn,	trimInWallholeMaskIn,
						xoffset-ax/2+ix,	gs_trim_width_in,						-1
				endif
				del 5

				cutend
			endif
		endif

		if gs_trim_outside then
			sect_fill gs_trim_fill_out, gs_trim_pen_bg_out, gs_trim_pen_fg_out, gs_trim_pen_cont
			material gs_trim_out_mat

			!1st part

			if gs_sill_outside then
				gosub 200		!cutting with Sill
			endif

			off1=WALL_THICKNESS
			off2=-WALL_THICKNESS-gs_trim_thk_out
			off3=-gs_trim_thk_out
			off4=0
			gosub 1000

			_offsetBottomCasingY = 0
			if not(bCasingBottomOutside) then
				if gs_sill_outside then
					gosub 300		!cutting below Sill
				else
					_offsetBottomCasingY = (gs_trim_width_out+0.01)

					gosub "CasingCuttingOnParapet"
				endif
			endif

			if gs_trim_thk_out > 0 then
				addz k_weitoffset
				mulz mulstat
				addz -rfur
				roty -AC_Hole_Position_Angle
				addz rfur

				x = gs_trim_width_out-gs_trim_offs_out
				ix = atn(x/r0)/180*pi*rfur

				xtw	= x-gs_tw_right_out
				ixtw = atn(xtw/r0)/180*pi*rfur

				if gs_sill_outside then
					sillmody		= SillBottomOutside
				else
					sillmody		= gs_trim_width_out - gs_trim_offs_out
				endif

				bprism_  gs_trim_out_mat,gs_trim_out_mat,gs_trim_out_mat,12,gs_trim_thk_out,rfur,
					xoffset-ax/2+ixtw,	sillmody-gs_tw_bottom_out-_offsetBottomCasingY,							15,
					xoffset+ax/2-ixtw,	sillmody-gs_tw_bottom_out-_offsetBottomCasingY,							15,
					xoffset+ax/2-ixtw,	gs_shoulderHeight-dyShoulderWidthOut+dyShoulderOffsOut+dyShoulderTWOut,	15,
					xoffset,			b-dyTopWidthOut+dyTopOffsOut+dyTopTWOut,								15,
					xoffset-ax/2+ixtw,	gs_shoulderHeight-dyShoulderWidthOut+dyShoulderOffsOut+dyShoulderTWOut,	15,
					xoffset-ax/2+ixtw,	sillmody-gs_tw_bottom_out-_offsetBottomCasingY,							-1,

					xoffset-ax/2+ix,	sillmody-_offsetBottomCasingY,							trimOnWallMaskOut,
					xoffset+ax/2-ix,	sillmody-_offsetBottomCasingY,							trimOnWallMaskOut,
					xoffset+ax/2-ix,	gs_shoulderHeight-dyShoulderWidthOut+dyShoulderOffsOut,	trimOnWallMaskOut,
					xoffset,			b-dyTopWidthOut+dyTopOffsOut,							trimOnWallMaskOut,
					xoffset-ax/2+ix,	gs_shoulderHeight-dyShoulderWidthOut+dyShoulderOffsOut,	trimOnWallMaskOut,
					xoffset-ax/2+ix,	sillmody-_offsetBottomCasingY,							-1
				del 5
			endif


			!2nd part

			if gs_trim_width_out > 0 then
				!Cutplanes at frame

				if WIDO_REVEAL_SIDE then
					addz WIDO_FRAME_THICKNESS
					cutplane 1,1,0,1
					del 1
				else
					cutplane 1,1,0,0
				endif

				off1=0
				off2=-WALL_THICKNESS
				off3=0
				off4=-WALL_THICKNESS
				gosub 1000

				x = gs_trim_width_out
				ix = atn(x/r0)/180*pi*rfur

				addz k_weitoffset
				mulz mulstat
				addz -rfur
				roty -AC_Hole_Position_Angle
				addz rfur

				if gs_sill_outside then
					bprism_  gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,10,WALL_THICKNESS,rfur,
						xoffset-ax/2,		0,										15,
						xoffset-ax/2+ix,	0,										trimInWallholeMaskOut,
						xoffset-ax/2+ix,	gs_shoulderHeight-dyShoulderWidthOut,	trimInWallholeMaskOut,
						xoffset,			b-dyTopWidthOut,						trimInWallholeMaskOut,
						xoffset+ax/2-ix,	gs_shoulderHeight-dyShoulderWidthOut,	trimInWallholeMaskOut,
						xoffset+ax/2-ix,	0,										trimInWallholeMaskOut,
						xoffset+ax/2,		0,										15,
						xoffset+ax/2,		gs_shoulderHeight,						15,
						xoffset,			b,										15,
						xoffset-ax/2,		gs_shoulderHeight,						15
				else
					bprism_  gs_trim_in_mat,gs_trim_in_mat,gs_trim_in_mat,12,WALL_THICKNESS,rfur,
						xoffset-ax/2,	-_offsetBottomCasingY,	15,
						xoffset+ax/2,	-_offsetBottomCasingY,	15,
						xoffset+ax/2,	gs_shoulderHeight,		15,
						xoffset,		b,						15,
						xoffset-ax/2,	gs_shoulderHeight,		15,
						xoffset-ax/2,	-_offsetBottomCasingY,	-1,

						xoffset-ax/2+ix,	gs_trim_width_out-_offsetBottomCasingY,	trimInWallholeMaskOut,
						xoffset+ax/2-ix,	gs_trim_width_Out-_offsetBottomCasingY,	trimInWallholeMaskOut,
						xoffset+ax/2-ix,	gs_shoulderHeight-dyShoulderWidthOut,	trimInWallholeMaskOut,
						xoffset,			b-dyTopWidthOut,						trimInWallholeMaskOut,
						xoffset-ax/2+ix,	gs_shoulderHeight-dyShoulderWidthOut,	trimInWallholeMaskOut,
						xoffset-ax/2+ix,	gs_trim_width_out-_offsetBottomCasingY,	-1
				endif

				del 5

				cutend
			endif
		endif

		del 2
	endif

	body -1
endif


if numCuts then
	for i=1 to numCuts
		cutend
	next i
endif
numCuts = 0


! =============================================================================
! Draw Frame
! =============================================================================

if lod3D = 1 then	! Simple
	if not(WIDO_REVEAL_SIDE) then
		frameThk  = gs_frame_thk/2 + gs_glass_thk/2
		frameThk2 = gs_frame_thk/2 - gs_glass_thk/2
	else
		frameThk  = gs_frame_thk/2 - gs_glass_thk/2
		frameThk2 = gs_frame_thk/2 + gs_glass_thk/2
	endif
else
	frameThk  = gs_frame_thk - gs_rebate_depth
	frameThk2 = gs_rebate_depth
endif

if gs_frame_mat = gs_frame_mat2 | frameThk < EPS | frameThk2 < EPS then
	loopNum = 1
	if gs_frame_mat = gs_frame_mat2 then
		frameThk = gs_frame_thk
	else
		frameThk = max(frameThk, frameThk2)
	endif
	if not(WIDO_REVEAL_SIDE) then
		frameMatTop		= gs_frame_mat
		frameMatBottom	= gs_frame_mat2
	else
		frameMatTop		= gs_frame_mat2
		frameMatBottom	= gs_frame_mat
	endif
	if WIDO_REVEAL_SIDE then
		frameMat = gs_frame_mat2
	else
		frameMat = gs_frame_mat
	endif
	frameMask = 15
else
	loopNum = 2
	if not(WIDO_REVEAL_SIDE) then
		frameMat  = gs_frame_mat2
		frameMat2 = gs_frame_mat
	else
		frameMat  = gs_frame_mat
		frameMat2 = gs_frame_mat2
	endif
	frameMatTop		= frameMat2
	frameMatBottom	= frameMat

	frameMask = 11
	frameMask2 = 14
endif

pen gs_frame_pen
sect_fill fr_sect_fill_type, fr_sect_fill_bg, fr_sect_fill_fg, gs_frame_pen_cont

palpha=atn((b-gs_shoulderHeight)/(a/2))
pbeta=(90+palpha)/2

dyfw1=(gs_frame_width)/tan(pbeta)      !dy at frame width
dyfw2=(gs_frame_width)/cos(palpha)

for i=1 to loopNum

	material frameMat

	! Right
	if gs_SashText_typ_num = 1 then
		put	A/2 + rightOvhg,	-bottomOvhg,							frameMask - 2,
			A/2 + rightOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),	10
	else
		put	A/2 + rightOvhg,	gs_frame_width,				frameMask,
			A/2 + rightOvhg,	gs_shoulderHeight-dyfw1,	10
	endif

	put	A/2 - gs_frame_width,	gs_shoulderHeight-dyfw1,	frameMask,
		A/2 - gs_frame_width,	gs_frame_width,				10

	base
	prism_ nsp/3,frameThk,
		get(nsp)
	gosub 3000

	! Left
	if gs_SashText_typ_num = 1 then
		put	-A/2 - leftOvhg,	-bottomOvhg,							frameMask - 2,
			-A/2 - leftOvhg,	gs_shoulderHeight+leftOvhg/tan(pbeta),	10
	else
		put	-A/2 - leftOvhg,	gs_frame_width,				frameMask,
			-A/2 - leftOvhg,	gs_shoulderHeight-dyfw1,	10
	endif

	put	-A/2 + gs_frame_width,	gs_shoulderHeight-dyfw1,	frameMask,
		-A/2 + gs_frame_width,	gs_frame_width,				10

	base
	prism_ nsp/3,frameThk,
		get(nsp)
	gosub 3000

	! Left Top
	base
	if gs_SashText_typ_num = 1 then
		put -A/2 - leftOvhg,	gs_shoulderHeight+leftOvhg/tan(pbeta),	10
	else
		put -A/2 - leftOvhg,	gs_shoulderHeight+leftOvhg/tan(pbeta),	frameMask,
			-A/2 - leftOvhg,	gs_shoulderHeight-dyfw1,				10
	endif
	put	-A/2 + gs_frame_width,	gs_shoulderHeight-dyfw1,				frameMask,
		 0,						B-dyfw2,								10,
		 0,						B+topOvhg/cos(palpha),					frameMask

	prism_ nsp/3,frameThk, get(nsp)

	vert 0, 0, 0
	vert cos(palpha), sin(palpha), 0
	vert sin(palpha), cos(palpha), 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	! Right Top
	base
	if gs_SashText_typ_num = 1 then
		put	A/2 + rightOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),	10
	else
		put	A/2 + rightOvhg,	gs_shoulderHeight+rightOvhg/tan(pbeta),	frameMask,
			A/2 + rightOvhg,	gs_shoulderHeight-dyfw1,				10
	endif
	put	A/2 - gs_frame_width,	gs_shoulderHeight-dyfw1,				frameMask,
		0,						B-dyfw2,								10,
		0,						B+topOvhg/cos(palpha),					frameMask

	prism_ nsp/3,frameThk, get(nsp)

	vert 0, 0, 0
	vert -cos(palpha), sin(palpha), 0
	vert -sin(palpha), cos(palpha), 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	! Bottom
	if gs_SashText_typ_num = 1 then
		put	-A/2 + gs_frame_width,	gs_frame_width,	10,
			-A/2 - leftOvhg,		-bottomOvhg,	frameMask,
			 A/2 + rightOvhg,		-bottomOvhg, 	10,
			 A/2 - gs_frame_width,	gs_frame_width, frameMask
	else
		put	-A/2 + gs_frame_width,	gs_frame_width, 10,
			-A/2 - leftOvhg,		gs_frame_width, frameMask - 2,
			-A/2 - leftOvhg,		-bottomOvhg, 	frameMask,
			 A/2 + rightOvhg,		-bottomOvhg, 	frameMask,
			 A/2 + rightOvhg,		gs_frame_width, 8,
			 A/2 - gs_frame_width,	gs_frame_width, frameMask
	endif

	base
	cprism_ frameMatTop, frameMatBottom, frameMat, nsp/3, frameThk,
			get(nsp)
	gosub 4000

	addz frameThk
	if loopNum > 1 then
		frameThk = frameThk2
		frameMatTop		= frameMat2
		frameMatBottom	= frameMat2
		frameMat		= frameMat2
		frameMask = frameMask2
	endif
next i
del loopNum

body -1


! =============================================================================
! Draw Casement
! =============================================================================

if lod3D = 5 then	! Chinese
	sh_sect_fill_type	= gs_fillShCover
	sh_sect_fill_fg		= gs_penShCov_fg
	sh_sect_fill_bg		= gs_penShCov_bg
else
	sh_sect_fill_type	= gs_fillSash
	sh_sect_fill_fg		= gs_penSash_fg
	sh_sect_fill_bg		= gs_penSash_bg
endif

add sashPosXArray[2][2], sashPosYArray[2][2], gs_frame_thk

call "Casement_Pentagon" parameters	A = sashWidthArray[2][2],
									B = sashHeightArray[2][2],
									gs_shoulderHeight = gs_shoulderHeight - gs_frame_width - dyfw1,
	lod3D							= lod3D,
	gs_rebate_width					= gs_rebate_width,
	gs_rebate_depth					= gs_rebate_depth,
	gs_frame_width					= gs_frame_width,
	gs_frame_thk					= gs_frame_thk,
	gs_sash_width					= gs_sash_width,
	gs_sash_thk						= gs_sash_thk,
	iMullionStyle					= iMullionStyle_01,
	iMullionPosition				= iMullionPosition,
	gw								= gw,
	gth								= gth,
	gs_glass_thk					= gs_glass_thk,
	vgn								= vgn_01,
	hgn								= hgn_01,
	gs_frame_pen					= gs_sash_pen,
	gs_glass_pen					= gs_glass_pen,
	gs_frame_mat					= gs_sash_mat2,
	gs_frame_mat2					= gs_sash_mat,
	gs_grill_mat					= gs_grill_mat2,
	gs_grill_mat2					= gs_grill_mat,
	gs_glass_mat					= gs_glass_mat,
	gs_panel_mat					= gs_panel_mat,
	gs_panel_mat2					= gs_panel_mat2,
	openingAngle_01					= openingAngle_01,
	gs_optype_m						= gs_optype_m_01,
	gs_sash_pen						= gs_sash_pen,
	gs_fillSash						= sh_sect_fill_type,
	gs_penSash_bg					= sh_sect_fill_bg,
	gs_penSash_fg					= sh_sect_fill_fg,
	gs_frame_pen_cont				= gs_frame_pen_cont,
	bOpeningDirectionInside			= WIDO_REVEAL_SIDE,
	gs_cust_panel					= gs_cust_panel_01,
	pentagon_unID					= 1000
!	bOplines3d						= bOplines3d,
!	opLineType3dIn					= opLineType3dIn,
!	opLineType3dOut					= opLineType3dOut,
!	gs_opline_style_m				= gs_opline_style_m,
!	gs_opLinePen					= gs_opLinePen

del 1


! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! =============================================================================


!! 3D-SUBROUTINES !!

"CasingCuttingOnParapet":
	! Cutting with top of parapet

	muly -1
	rotx -90

	material gs_trim_out_mat
	cutplane

	!rect 2,2
	numCuts = numCuts + 1

	del 2
return


200:


!Cutting with Sill

if vardim1(sillPoly) > 1 then
	for i = 1 to (vardim1(sillPoly)-1) step 2
		put sillPoly[i], sillPoly[i+1], 31
	next i

	roty 90
	muly -1
	add 0,gs_tolerance_lower, -sillLeft

	material gs_trim_out_mat

	cutform nsp/3, 1, 2,
		0, 0, 1, sillRight + sillLeft,
		get (nsp)
	numCuts = numCuts + 1

!	prism_ nsp/3, sillRight + sillLeft+3,
!		get (nsp)

	del 3
endif

return

300:

!Cutting below Sill
muly -1
add 0, -sillBottom-sillSideHeight + gs_tolerance_lower, 0
rotx -90 + sillSideAngle

material gs_trim_out_mat
cutplane

!rect 2,2
numCuts = numCuts + 1

del 3


return


1000:

!Curved Wall Calculation

r0=abs(WIDO_ORIG_DIST)

if WIDO_ORIG_DIST<0 then                !Wall is convex from "outside"

	if WIDO_REVEAL_SIDE then                            !Flip is on
		nframe=WIDO_FRAME_THICKNESS
		wsill=WIDO_SILL
		wthickness=WALL_THICKNESS

		mulstat=1
		rdpi=sqr(a*a/4+(r0+nframe)^2)
		r=sqr(a*a/4+r0*r0)
		kdpi=rdpi-r+WIDO_SILL
		rclo=r+kdpi                         !wall radius_2
		rfur=rclo-wthickness+off1           !wall radius_1
		calpha=asn(a/2/r)                   !window half angle
	else                                     !Flip is off
		nframe=0
		wsill=-WIDO_SILL
		wthickness=-WALL_THICKNESS

		mulstat=1
		r=sqr(a*a/4+r0*r0)
		rclo=r+wsill                         !wall radius_2
		rfur=rclo-wthickness+off2            !wall radius_1
		calpha=asn(a/2/r)                    !window half angle
	endif

else                                      !Wall is concave from "outside"

	if WIDO_REVEAL_SIDE then                              !Flip is on
		nframe=WIDO_FRAME_THICKNESS
		wsill=WIDO_SILL
		wthickness=WALL_THICKNESS

		mulstat=-1
		r0=r0-nframe
		r=sqr(a*a/4+r0*r0)
		rfur=r-wsill+off3                    !wall radius_1
		rclo=rfur+wthickness                 !wall radius_2
		calpha=asn(a/2/r)                    !window half angle
	else                                      !Flip is off
		nframe=WIDO_FRAME_THICKNESS
		wsill=-WIDO_SILL
		wthickness=-WALL_THICKNESS

		mulstat=-1
		rdpi=sqr(a*a/4+r0*r0)
		r0=r0-nframe
		r=sqr(a*a/4+r0*r0)
		kdpi=rdpi-r+WIDO_SILL
		rfur=r+kdpi+off4                     !wall radius_1
		rclo=rfur+wthickness                 !wall radius_2
		calpha=asn(a/2/r)                    !window half angle
	endif

endif


!Additional values

k_weitoffset=rfur*mulstat+WIDO_ORIG_DIST  !gs_window_sill value at window centerpoint
walpha=2*atn(a/2/r0)                      !Window Opening angle
xoffset=AC_Hole_Position_Angle/180*pi*rfur                    !Window centerpoint distance from wall origin
ax=walpha/180*pi*rfur                     !"a" curve lenght (at further radius)

return



3000:
! --- Texture directions -------------------------------------------------------

vert 0, 0, 0
vert 0, 1, 0
vert 1, 0, 0
vert 0, 0, 1
coor 2, -1, -2, -3, -4
body 1

return

4000:
! --- Texture directions -------------------------------------------------------

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1
coor 2, -1, -2, -3, -4
body 1

return
