
!!! Molding Straight

pen gs_cont_pen

add2 A, 0
rot2 180

fill gs_fill_type
poly2_B 5, 2+64,gs_fill_pen,gs_back_pen,
	A+profileWidth/TAN(-beta)*((iClosingSide = CLOSING_SIDE2 & bClosingEnds) or not(bClosingEnds)),	 	profileWidth, 	1,
	profileWidth/TAN(alpha)*((iClosingSide = CLOSING_SIDE1 & bClosingEnds) or not(bClosingEnds)), 		profileWidth, 	1,			!right side
	0, 																									0, 				1,
	A, 																									0, 				1,
	A+profileWidth/TAN(-beta)*((iClosingSide = CLOSING_SIDE2 & bClosingEnds) or not(bClosingEnds)), 	profileWidth, 	1

line_type 1
line2 0, 0,A, 0

line_type gs_line_type
poly2_ 5, 1,
	A+profileWidth/TAN(-beta)*((iClosingSide = CLOSING_SIDE2 & bClosingEnds) or not(bClosingEnds)), profileWidth, 1,
	profileWidth/TAN(alpha)*((iClosingSide = CLOSING_SIDE1 & bClosingEnds) or not(bClosingEnds)), profileWidth, 1,         !right side
	0, 0, 0,
	A, 0, 1,
	A+profileWidth/TAN(-beta)*((iClosingSide = CLOSING_SIDE2 & bClosingEnds) or not(bClosingEnds)), profileWidth, 1

if bClosingEnds then
	if iClosingSide = CLOSING_SIDE2 OR iClosingSide = CLOSING_SIDE_BOTH then line2 profileWidth, 0, 0, profileWidth
	if iClosingSide = CLOSING_SIDE1 OR iClosingSide = CLOSING_SIDE_BOTH then line2 A-profileWidth, 0, A, profileWidth
endif

del TOP

hotspot2 0, 0
hotspot2 A/2, -profileWidth

unID = 1

hotspot2 0, 0, unID, a, 1+256: unID=unID+1
hotspot2 -1, 0, unID, a, 3: unID=unID+1
hotspot2 a, 0, unID, a, 2: unID=unID+1

if not(bClosingEnds) | iClosingSide = CLOSING_SIDE2 then
	b_rr = profileWidth/SIN(beta)
	hotspot2 0, 0, unID, beta, 6+512: unID=unID+1
	hotspot2 b_rr, 0, unID, beta, 4+128: unID=unID+1
	hotspot2 b_rr*COS(beta), -b_rr*SIN(beta), unID, beta, 5: unID=unID+1
else
	hotspot2 0, 0, unID: unID=unID+1
	hotspot2 0, -profileWidth, unID: unID=unID+1
	hotspot2 profileWidth, 0, unID: unID=unID+1
endif

if not(bClosingEnds) | iClosingSide = CLOSING_SIDE1 then
	b_rr = profileWidth/SIN(alpha)
	hotspot2 a, 				0, 					unID, 	alpha, 6	: unID=unID+1
	hotspot2 a-b_rr, 			0, 					unID, 	alpha, 4+128: unID=unID+1
	hotspot2 a-b_rr*COS(alpha), -b_rr*SIN(alpha), 	unID, 	alpha, 5	: unID=unID+1
else
	hotspot2 0, -profileWidth, unID, a, 1+128: unID=unID+1
	hotspot2 -1, -profileWidth, unID, a, 3: unID=unID+1
	hotspot2 a, -profileWidth, unID, a, 2: unID=unID+1

	hotspot2 0, 0, unID, a, 1+128: unID=unID+1
	hotspot2 -1, 0, unID, a, 3: unID=unID+1
	hotspot2 a, 0, unID, a, 2: unID=unID+1

	hotspot2 a-profileWidth, 0, unID: unID=unID+1
endif
