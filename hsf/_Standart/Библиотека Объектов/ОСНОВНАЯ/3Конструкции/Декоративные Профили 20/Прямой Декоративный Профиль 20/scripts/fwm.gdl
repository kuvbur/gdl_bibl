
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "E4E1D513-1F1C-4581-9C2E-BA13C2B78006"
	_startID	= "7C4FF7CB-4730-4622-A470-EE5670213609"
	_endID		= "E6AAA8A7-5556-4E62-B9FC-F3D28B863C65"
gosub "paramNameChange_FWM"

	_startID	= "E6AAA8A7-5556-4E62-B9FC-F3D28B863C65"
	_endID		= "6CBDBC1C-5016-447E-8AFF-2AC14AF8FA42"
gosub "newparameters_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! newParameters
! ==============================================================================
"paramNameChange_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		_bHasParam = DELETED_PAR_VALUE("mtypes_m", _tempMoldingTypes)
		if _bHasParam then
			iMoldingStyle = _tempMoldingTypes
			parameters iMoldingStyle = iMoldingStyle
		endif

		_bHasParam2 = DELETED_PAR_VALUE("cnum", _iTempClosingSide)
		if _bHasParam2 then
			if _iTempClosingSide = 1 then
				bClosingEnds = 0
				iClosingSide = 1
			else
				bClosingEnds = 1
			endif
			if _iTempClosingSide = 2 then
				iClosingSide = CLOSING_SIDE1
			endif
			if _iTempClosingSide = 3 then
				iClosingSide = CLOSING_SIDE2
			endif
			if _iTempClosingSide = 4 then
				iClosingSide = CLOSING_SIDE_BOTH
			endif

			parameters bClosingEnds = bClosingEnds,
						iClosingSide = iClosingSide
		endif

		actualGuid = _endID
	endif
return

! ==============================================================================
! newparameters: profileWidth, profileHeight
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID then
		bHas = STORED_PAR_VALUE("B", _B)
		if bHas then
			profileWidth = _B
			parameters profileWidth = profileWidth
		endif
		
		bHas = STORED_PAR_VALUE("zzyzx", _zzyzx)
		if bHas then
			profileHeight = _zzyzx
			parameters profileHeight = profileHeight
		endif

		actualGuid = _endID
	endif
return

