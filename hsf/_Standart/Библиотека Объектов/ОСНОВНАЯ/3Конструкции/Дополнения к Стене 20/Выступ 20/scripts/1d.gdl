
! const values for parameter: gs_detlevel_3D_m
DLEV3D_OFF      = 0
DLEV3D_SIMPLE   = 1
DLEV3D_DETAILED = 2

EPS = 0.0001

bCompositeWall	= (WALL_SKINS_NUMBER > 0)
curvedWall		= (abs(WIDO_ORIG_DIST) > EPS)

wallIsLeft		= (SYMB_ROTANGLE > EPS) exor WIDO_REVEAL_SIDE
signWallIsLeft	= wallIsleft - not(wallIsleft)

! ==============================================================================
! Set Fill parameters for 2D and 3D
! ==============================================================================

if bUseCustomSymbolFill then
	sectPen			= symbolsectPen
	fillType		= symbolFillType
	fillThk			= WALL_THICKNESS
	fillPenFg		= symbolFillPen
	fillPenBg		= symbolFillBgPen
	bLocalFill		= 0
	bFitToSkinFill	= 0
else
	if not(bCompositeWall) then
		sectPen			= WALL_SECT_PEN
		fillType		= WALL_FILL
		fillThk			= WALL_THICKNESS
		fillPenFg		= WALL_FILL_PEN
		fillPenBg		= WALL_FBGD_PEN
		bLocalFill		= 0
		bFitToSkinFill	= 0
	else
		if (not(curvedWall) & (abs(SYMB_ROTANGLE) < EPS)) | (curvedWall  & not(wallIsLeft)) then
			idx = WALL_SKINS_NUMBER
			sectPen		= WALL_SKINS_PARAMS[idx][ 9]	! SKIN_LOWER_PEN
		else
			idx = 1
			sectPen		= WALL_SKINS_PARAMS[idx][ 7]	! SKIN_UPPER_PEN
		endif
		fillType		= WALL_SKINS_PARAMS[idx][ 1]	! SKIN_FILL
		fillThk			= WALL_SKINS_PARAMS[idx][ 2]	! SKIN_THICKNESS
		fillPenFg		= WALL_SKINS_PARAMS[idx][ 4]	! SKIN_FILL_PEN
		fillPenBg		= WALL_SKINS_PARAMS[idx][ 5]	! SKIN_FILL_BACK_PEN
		bLocalFill		= round_int(WALL_SKINS_PARAMS[idx][12])	! SKIN_FILL_ORIENTATION
		bFitToSkinFill	= round_int(WALL_SKINS_PARAMS[idx][16])	! SKIN_FILL_FIT_TO_SKIN
	endif
endif
