
if curvedWall then
	if WIDO_REVEAL_SIDE then
		mul2 1, -1
		add2 0, WIDO_SILL + WALL_THICKNESS + gs_recess_depth
		WOD = -WIDO_ORIG_DIST - WALL_THICKNESS - gs_recess_depth
	else
		add2 0, WIDO_SILL
		WOD = WIDO_ORIG_DIST
	endif
else
	WOD = 0
	if WIDO_REVEAL_SIDE then
		add2 0,-WIDO_SILL - gs_recess_depth
		if wallIsLeft exor SYMB_MIRRORED then
			rot2 WALL_INCL
		else
			rot2 -WALL_INCL
		endif
	else
		add2 0, WIDO_SILL
	endif
endif


! ==============================================================================

gs_recess_depth = max(gs_recess_depth, -WALL_THICKNESS + 0.01)

fill fillType
pen sectPen
line_property 2


! ==============================================================================

if GLOB_PREVIEW_MODE = 1 then
	line2 -A/2,0, -A/2-0.3,0
	line2  A/2,0,  A/2+0.3,0
	line2 -A/2-0.3,-WALL_THICKNESS, A/2+0.3,-WALL_THICKNESS
endif

if abs(gs_recess_depth) > EPS then
	if curvedWall & gs_recess_depth > 0 then
		put	-A/2,	gs_recess_depth,	0,
			 A/2,	gs_recess_depth,	0,
			 0,		WOD,				900,
			 A/2,	0,					0,
			-A/2,	0,					3000
	else
		put	-A/2,	gs_recess_depth,	0,
			 A/2,	gs_recess_depth,	0,
			 A/2,	0,					0,
			-A/2,	0,					0
	endif

	if gs_recess_depth > 0 then
		wallblock2{2} (NSP)/3, 2, 2,8 * (bLocalFill | bFitToSkinFill) + 32 * bFitToSkinFill + 128 * (bLocalFill & not(bFitToSkinFill)),
			fillPenFg, fillPenBg,
			0,0,
			1,0,0,fillThk,	0,
			get (NSP)
	else
		wallhole2 (NSP)/3, 0, 0, -1,
			0, 0, 0,
			get (NSP)
	endif

	wallline2 -A/2, gs_recess_depth, A/2, gs_recess_depth
	wallline2 -A/2, 0, -A/2, gs_recess_depth
	wallline2  A/2, 0,  A/2, gs_recess_depth
endif

if GLOB_FEEDBACK_MODE | GLOB_PREVIEW_MODE = 1 then
	line2 -A/2, gs_recess_depth, A/2, gs_recess_depth
	line2 -A/2, 0, -A/2, gs_recess_depth
	line2  A/2, 0,  A/2, gs_recess_depth
endif


unID = 1
hotspot2 -A/2, 0,				unID, gs_recess_depth, 1+128    :unID=unID+1
hotspot2 -A/2, gs_recess_depth,	unID, gs_recess_depth, 2        :unID=unID+1
hotspot2 -A/2, -1,				unID, gs_recess_depth, 3        :unID=unID+1

hotspot2 A/2,  0,				unID, gs_recess_depth, 1+128    :unID=unID+1
hotspot2 A/2,  gs_recess_depth,	unID, gs_recess_depth, 2        :unID=unID+1
hotspot2 A/2,  -1,				unID, gs_recess_depth, 3        :unID=unID+1

