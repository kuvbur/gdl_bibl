
rrr = REQUEST ("window_show_dim", "", bShowMarker)

if WIDO_REVEAL_SIDE then
	add2 0, -(WIDO_SILL-WALL_THICKNESS/2)
else
	add2 0,   WIDO_SILL-WALL_THICKNESS/2
endif

pen penFill
fill fillType

unID = 1
hotspot2 -a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, unID : unID = unID + 1
hotspot2  a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, unID : unID = unID + 1

add2 0,-c

hotspot2 -a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, unID : unID = unID + 1
hotspot2  a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, unID : unID = unID + 1

del 1

if GLOB_CONTEXT = 5 then
	line2	-a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2,
			 a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2
else
	wallline2	-a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2,
				 a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2
endif

if bTypeGas then

	if GLOB_CONTEXT = 5 then
		line2	 a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2,
				-a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2
	else
		wallline2	a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2,
					-a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2
	endif

	color = penFill
	put	-a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1,
		a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1,
		0, 0, 1,
		-a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, -1
	gosub "draw_poly"

	color = penFill
	put	-a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1,
		a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1,
		0, 0, 1,
		-a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1
	gosub "draw_poly"

	color = penFillBack
	put	a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1,
		0, 0, 1,
		a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1,
		a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1
	gosub "draw_poly"

	color = penFillBack
	put	-a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1,
		0, 0, 1,
		-a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1,
		-a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1
	gosub "draw_poly"

else

	color = penFill
	put	-a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1,
		 a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1,
		-a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1,
		-a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, -1
	gosub "draw_poly"

	color = penFillBack
	put	-a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1,
		 a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, 1,
		 a/2, WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, 1,
		-a/2,-WALL_THICKNESS/2+(WALL_THICKNESS-c)/2, -1
	gosub "draw_poly"

endif

pen WALL_SECT_PEN
wallline2 -a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2,  a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2
wallline2  a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2,  a/2,  WALL_THICKNESS/2-(WALL_THICKNESS-c)/2
wallline2  a/2,  WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, -a/2,  WALL_THICKNESS/2-(WALL_THICKNESS-c)/2
wallline2 -a/2,  WALL_THICKNESS/2-(WALL_THICKNESS-c)/2, -a/2, -WALL_THICKNESS/2+(WALL_THICKNESS-c)/2

del 1

pen penFill
line_property 0


! ==============================================================================
! Text Label
! ==============================================================================

! --- Oriented View ------------------------------ [

angleViewRot = 0
rrr = REQUEST ("View_Rotangle", "", angleViewRot)
totalRotate = (WALL_DIRECTION + angleViewRot + txtRotAngle * mirror) MOD 360
if totalrotate < eps then totalrotate = (360 + totalrotate) MOD 360

! --- Oriented View ------------------------------ ]

if iDisplayText = 1 | (iDisplayText = 3 & bShowMarker) then

	define style "styleText" fontType,fontSize,5,0
	set style "styleText"
	n = request ("Height_of_style", "styleText", heightStyle)
	heightStyle = heightStyle / 1000 * GLOB_SCALE

	totalWidth = str(int(A * 100 + 0.5), 2, 0)
	totalHeight = str(int(B * 100 + 0.5), 2, 0)
	contString = totalWidth + "/" + totalHeight

	widthe	= 0
	widtht1	= 0
	widtht2	= 0

	widthe  = stw(contString) / 1000 * GLOB_SCALE
	if bText1  then widtht1 = stw(textLine1) / 1000 * GLOB_SCALE
	if bText2  then widtht2 = stw(textLine2) / 1000 * GLOB_SCALE

	width = max(widthe, widtht1, widtht2)

	if width > EPS then

		alfa=atn(((1+bText1+bText2)*heightStyle)/(width/2))
		atlo=sqr((width/2)*(width/2)+((1+bText1+bText2)*heightStyle)*((1+bText1+bText2)*heightStyle))

	!***************************** text ***********************

		add2 0, -(WALL_THICKNESS-c)/2 * (WALL_THICKNESS < (c-eps))

		unID=unID+1
		add2 0, 0

		HOTSPOT2 txtOffsetX, 0, unID, txtOffsetY, 1+128 : unID=unID+1
		HOTSPOT2 txtOffsetX, txtOffsetY, unID, txtOffsetY, 2 : unID=unID+1
		HOTSPOT2 txtOffsetX, -0.1, unID, txtOffsetY, 3 : unID=unID+1

		HOTSPOT2 0, txtOffsetY, unID, txtOffsetX, 1+128 : unID=unID+1
		HOTSPOT2 txtOffsetX, txtOffsetY, unID, txtOffsetX, 2 : unID=unID+1
		HOTSPOT2 -0.1, txtOffsetY, unID, txtOffsetX, 3 : unID=unID+1

		del 1
		add2 txtOffsetX, txtOffsetY		! Text Offset

		HOTSPOT2 width/2, (1+bText1+bText2)*heightStyle, unID, txtRotAngle, 4+128 : unID=unID+1
		HOTSPOT2 atlo*cos(alfa+txtRotAngle), atlo*sin(alfa+txtRotAngle), unID, txtRotAngle, 5
		unID=unID+1
		HOTSPOT2 0, 0, unID, txtRotAngle, 6 : unID=unID+1

		del 1

		add2 txtOffsetX, txtOffsetY		! Text Offset

		rot2 txtRotAngle

		if typeTextRotation_m = 2 then ! Readable
			if abs(SYMB_ROTANGLE-180) < EPS then
				iReadable = (totalRotate < (gs_readable_angle + eps) | totalRotate > (gs_readable_angle + 180 + eps))
			else
				iReadable = (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps))
			endif
			rot2 180 * iReadable
			if iReadable then add2 0,-heightStyle * (1+bText1+bText2)
		endif

		add2 0, heightStyle/2

		pen pen_txt

		if bText2 then
			text2 (txtAlign=`Слева`)*(widtht2-width)/2-(txtAlign=`Справа`)*\
			(widtht2-width)/2, 0, textLine2
			add2 0, heightStyle
		endif
		if bText1 then
			text2 (txtAlign=`Слева`)*(widtht1-width)/2-(txtAlign=`Справа`)*\
			(widtht1-width)/2, 0, textLine1
			add2 0, heightStyle
		endif

		text2 (txtAlign=`Слева`)*(widthe-width)/2-(txtAlign=`Справа`)*\
		(widthe-width)/2, 0, contString

		if bText1 then del 1
		if bText2 then del 1

		del 3
		del 1

	endif
endif

end

"draw_poly":

	if GLOB_CONTEXT = 5 then
		poly2_b NSP/3, 1+2+64, color, color,
				get (NSP)
	else
		wallhole2 NSP/3, 2, color, color,
				0, 0, 0,
				get (NSP)
	endif
return
