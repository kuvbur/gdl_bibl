

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 		= -1
_idxTab = 1
TABID_DIMENSIONS	= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES 		= _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Наличника`
ui_current_page gs_ui_current_page

ui_page TABID_DIMENSIONS, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "dimensionsPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"


!--------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ======================================================================
end
! ======================================================================

"dimensionsPage":

	popupWidth	= 135
	popupHeight	= 96
	x1 = 0
	xEnd = 444
	xHalf = 223
	x2 = xHalf - popupWidth - 10
	x3 = xHalf  + 10

	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1

	ui_style 0, 1
	ui_outfield	`Стиль` ,x1,    yCurrent+4, 	xHalf-2,	outfieldHeight	

	yCurrent = yCurrent + dy
	ui_style 0, 0


	ui_infield{3} "iProfileType", 	 x1,   yCurrent, 	xHalf - x1-10, 96,
				4, 1, 2, 1,
				180, 90, 90, 90,
				2, `Фигурный Профиль`, TYPEPROFILE_MOLDED,
				1, `Квадратный Профиль`, TYPEPROFILE_SQUARE


! ------------------------------------------------------------------------------
	ui_separator xHalf , 0, xHalf , 152
! ------------------------------------------------------------------------------
	yCurrent 			= pageStartY + 1

	ui_style 0, 1
	ui_outfield	`Размеры` ,xHalf+5,    yCurrent+4, 	xHalf-2,	outfieldHeight	

	ui_style 0, 0

	uiPictX = 240
	uiPictY = 25
	uiPictWdt = 106
	uiPictHgt = 96
	uiPictMargin = 5
	uiPictInfWdt = 90
	infieldHeight 		= 19

	uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
	uiPicLeftEND = uiPicLeftStart - uiPictMargin

	uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

	if iProfileType = TYPEPROFILE_MOLDED then
		ui_pict 3, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 
	else
		ui_pict 2, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 
	endif

	ui_infield "b", uiPictX-10, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Длина`
	ui_infield "a", uiPictX+83, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина`

	ui_infield "ijw", uiPicRightStart, uiPictY-5, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Наличника`
	ui_infield "zzyzx", uiPicRightStart, uiPictY+35, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота`

return
	
"AppearancePage":
	dim st2DParamNames[][]
		st2DParamNames[1][1] = "gs_cont_pen" 			: st2DParamNames[1][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[2][1] = "gs_fill_type" 			: st2DParamNames[2][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[3][1] = "gs_fill_pen" 			: st2DParamNames[3][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[4][1] = "gs_back_pen" 			: st2DParamNames[4][2] = "ui_list_fillBgPen_flat(1)"

	dim st3DParamNames[][]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"		: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_shadow"				: st3DParamNames[2][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[][]
		stSurfaceParamNames[1][1] = "gs_trim_mat"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	

	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames


	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
return

"descriptionPage":

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return

