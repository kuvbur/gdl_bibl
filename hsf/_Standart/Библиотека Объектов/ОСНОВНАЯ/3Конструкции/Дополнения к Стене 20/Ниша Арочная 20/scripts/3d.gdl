
! -----------------
DepthArc = D
HeightArch = C
if GLOB_CONTEXT = 5 then roty 180
! -----------------
if gs_detlevel_3D_m = DLEV3D_OFF then end
Resol gs_resol

mat1 = WALL_MAT_A
mat2 = WALL_MAT_B
if gs_lesniche_mat <> 0 then
	mat3 = gs_lesniche_mat
else
	if AC_HoleSideMaterial = 1 then
		mat3 = WALL_MAT_B
	else
		mat3 = WALL_MAT_EDGE
	endif
endif

if GLOB_CONTEXT = 5 then
	mat1temp = mat1
	mat2temp = mat2
	mat1 = mat2temp
	mat2 = mat1temp
	if mat3 = WALL_MAT_B then mat3 = WALL_MAT_A
endif

pen WALL_VIEW_PEN

! Body -----------

addz -WIDO_SILL - WIDO_REVEAL_SIDE*(WALL_THICKNESS - 2*WIDO_SILL)
material mat3
rotx -90
muly DepthArc/(A/2)

! eliminate upper plane
addz B - HeightArch
cutplane 1, 1, 0, 0, 4
del 1

cprism_  mat1, mat3, mat3, 6, B,
	-a/2,	0,		64,
	-a/2,	-A/2,	64,
	a/2,	-A/2,	64,
	a/2,	0,		9+64,
	0,		0,		900+8+64,
	-a/2,	-180,	4000+8+64

cutend

del 2


! Head -----------
if HeightArch > EPS then
	if GLOB_CONTEXT = 5 | GLOB_CONTEXT = 6 | GLOB_CONTEXT = 23 then

		GROUP "wallbox"
			addy B-HeightArch
			muly HeightArch*2/a
			cprism_  mat1, mat3, mat3, 4, WIDO_SILL + DepthArc, !(WIDO_SILL + DepthArc + WIDO_REVEAL_SIDE*(WALL_THICKNESS-2*WIDO_SILL-DepthArc)),
					-a/2,			0,			8+64,
					0,				0,			900+8+64,
					-a/2,			-180,		4000+8+64,
					a/2,			0,			8+64
			del 2
		ENDGROUP

		GROUP "ellips"
			addy B-HeightArch
			mul 1,1,DepthArc*2/a
			rotx -90

			ellips HeightArch, a/2
			del 3
		ENDGROUP

		groupHead = SUBGROUP("wallbox","ellips")
		PLACEGROUP groupHead

	endif

	addy B-HeightArch
	mul 1,HeightArch*2/A,1
	addz -1
	wallniche 6, 1, 256,
		0,		0,		1, DepthArc+1,
		-a/2,	-(B-HeightArch)*(A/(HeightArch*2)),		79+16, mat3,
		-a/2,	0,										79+16, mat3,
		0,		0,										900, mat3,
		a/2,	-180,									4000 + 79+16, mat3,
		a/2,	0,										79+16, mat3,
		a/2,	-(B-HeightArch)*(A/(HeightArch*2)),		79+16, mat3
	del 3
endif

if GLOB_CONTEXT = 5 | GLOB_CONTEXT = 6 | GLOB_CONTEXT = 23 then
	rotx -90
	mul 1,DepthArc*2/a,1
	cprism_  mat1, mat2, mat3, 6, b-HeightArch,
			a/2,  0,    8+64+1+2,
			0,    0,    900,
			a/2,  -180, 4000,
			-a/2, 0,    8+64+1+2,
			-a/2, -a/2, 8+64,
			a/2,  -a/2, 8+64
	del 2
endif

if HeightArch > EPS & not(GLOB_CONTEXT = 5 | GLOB_CONTEXT = 6) then
	addy B-HeightArch
	mul 1,HeightArch*2/a,DepthArc*2/a

	revolve 3, 90, 0,
		a/2,0,1,
		0,0,901,
		a/2,179.99,4001

	del 2
endif

if GLOB_CONTEXT = 5 then del 1
