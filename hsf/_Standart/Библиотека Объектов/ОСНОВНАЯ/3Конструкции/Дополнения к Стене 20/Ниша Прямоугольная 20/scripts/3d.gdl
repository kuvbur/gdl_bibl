
IF gs_detlevel_3D_m = DLEV3D_OFF THEN END

pen WALL_VIEW_PEN

!!FOR OBJECT SETTING DIALOG PREVIEW!!


if gs_lesniche_mat <> 0 then
	mat1 = gs_lesniche_mat
	mat2 = gs_lesniche_mat
	mat3 = gs_lesniche_mat
else
	mat1 = WALL_MAT_A
	mat2 = WALL_MAT_B
	if AC_HoleSideMaterial = 1 then
		mat3 = WALL_MAT_A
	else
		mat3 = WALL_MAT_EDGE
	endif
endif

IF GLOB_CONTEXT=5 THEN
	IF not(WIDO_REVEAL_SIDE) THEN
		addz -WIDO_SILL
	ELSE
		addz WIDO_SILL-dep
	endif

	material mat3
	CUTPOLYA 4, 2, 0,
		a/2,0,15,
		a/2,b,15,
		-a/2,b,15,
		-a/2,0,15


	CPRISM_ WALL_MAT_A,WALL_MAT_B,WALL_MAT_EDGE,5,WALL_THICKNESS,
	a/2+0.2,-0.2,8,
	a/2+0.2,b+0.2,8,
	-a/2-0.2,b+0.2,8,
	-a/2-0.2,-0.2,8,
	a/2+0.2,-0.2,-1

	CUTEND

	CPRISM_ mat3,mat2,mat3,5,WALL_THICKNESS-dep,
	a/2,0,12,
	a/2,b,12,
	-a/2,b,12,
	-a/2,0,12,
	a/2,0,-1

	DEL 1
END
ENDIF


! ==============================================================================

pen WALL_VIEW_PEN

if not(WIDO_REVEAL_SIDE) then
	addz -WIDO_SILL
else
	addz WIDO_SILL-dep
endif

	if WIDO_REVEAL_SIDE then
		if wallIsLeft exor SYMB_MIRRORED then
			roty WALL_INCL
		else
			roty -WALL_INCL
		endif
	endif

! ==============================================================================

if gs_lesniche_mat <> 0 then
	material gs_lesniche_mat
else
	if AC_HoleSideMaterial = 1 then
		material WALL_MAT_B
	else
		material WALL_MAT_EDGE
	endif
endif

wallniche 5, 1, 2+16,
		0,0,1, dep,
		-A/2, 0,1+2+16,
		 A/2, 0,1+2+16,
		 A/2, B,1+2+16,
		-A/2, B,1+2+16,
		-A/2, 0,-1

unID = 1

hotspot  A/2,0,dep,unID:unID=unID+1
hotspot  A/2,B,dep,unID:unID=unID+1
hotspot -A/2,B,dep,unID:unID=unID+1
hotspot -A/2,0,dep,unID:unID=unID+1
