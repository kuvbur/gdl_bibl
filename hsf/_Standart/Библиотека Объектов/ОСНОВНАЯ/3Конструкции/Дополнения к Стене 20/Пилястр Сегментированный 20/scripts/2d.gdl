
if curvedWall then
	if WIDO_REVEAL_SIDE then
		mul2 1, -1
		add2 0, WIDO_SILL
		WOD = -WIDO_ORIG_DIST
	else
		add2 0, WIDO_SILL
		WOD = WIDO_ORIG_DIST
	endif
else
	WOD = 0
	if WIDO_REVEAL_SIDE then
		add2 0,-WIDO_SILL + WALL_THICKNESS - WIDO_FRAME_THICKNESS
		if wallIsLeft exor SYMB_MIRRORED then
			rot2 WALL_INCL
		else
			rot2 -WALL_INCL
		endif
	else
		add2 0, WIDO_SILL
	endif
endif


! ==============================================================================

gs_recess_depth = max(gs_recess_depth, -WALL_THICKNESS + 0.01)

if not(bCompositeWall) then
	fillType		= WALL_FILL
	fillThk			= WALL_THICKNESS
	fillPenFg		= WALL_FILL_PEN
	fillPenBg		= WALL_FBGD_PEN
	bLocalFill		= 0
	bFitToSkinFill	= 0
else
	if (not(curvedWall) & (abs(SYMB_ROTANGLE) < EPS)) | (curvedWall  & not(wallIsLeft)) then
		idx = WALL_SKINS_NUMBER
	else
		idx = 1
	endif
	fillType		= WALL_SKINS_PARAMS[idx][ 1]	! SKIN_FILL
	fillThk			= WALL_SKINS_PARAMS[idx][ 2]	! SKIN_THICKNESS
	fillPenFg		= WALL_SKINS_PARAMS[idx][ 4]	! SKIN_FILL_PEN
	fillPenBg		= WALL_SKINS_PARAMS[idx][ 5]	! SKIN_FILL_BACK_PEN
	bLocalFill		= round_int(WALL_SKINS_PARAMS[idx][12])	! SKIN_FILL_ORIENTATION
	bFitToSkinFill	= round_int(WALL_SKINS_PARAMS[idx][16])	! SKIN_FILL_FIT_TO_SKIN
endif

fill fillType
pen WALL_SECT_PEN
line_property 2


! ==============================================================================

if GLOB_CONTEXT = 5 then
	line2 -A/2,0, -A/2-0.3,0
	line2  A/2,0,  A/2+0.3,0
	line2 -A/2-0.3,-WALL_THICKNESS, A/2+0.3,-WALL_THICKNESS
endif

if abs(gs_recess_depth) > EPS then
	if gs_recess_depth > 0 then
		wallblock2{2} 4, 2, 2,8 * (bLocalFill | bFitToSkinFill) + 32 * bFitToSkinFill + 128 * (bLocalFill & not(bFitToSkinFill)),
			fillPenFg, fillPenBg,
			0,0,
			1,0,0,fillThk,	0,
			 0,WOD,900,
			-A/2, 0, 0,
			 A/2, 0, curvedWall * 3000,
			 0, gs_recess_depth, 0
	else
		wallhole2 4, 0, 0, -1,
			 0,0,0,
			-A/2, 0, 0,
			 0, gs_recess_depth, 0,
			 A/2, 0, 0,
			 0,   1, 0
	endif

	wallline2 -A/2, 0, 0, gs_recess_depth
	wallline2  A/2, 0, 0, gs_recess_depth
endif

if GLOB_CONTEXT = 22 | GLOB_CONTEXT = 5 then
	line2 -A/2, 0, 0, gs_recess_depth
	line2  A/2, 0, 0, gs_recess_depth
endif


unID = 1

!!!hotspot2 -A/2,  0,	unID: unID=unID+1
!!!hotspot2  A/2,  0,	unID: unID=unID+1

hotspot2 0,  0,					unID, gs_recess_depth, 1+128    :unID=unID+1
hotspot2 0,  gs_recess_depth,	unID, gs_recess_depth, 2        :unID=unID+1
hotspot2 0, -1,					unID, gs_recess_depth, 3        :unID=unID+1

