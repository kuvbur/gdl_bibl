
!! Coyne.gsm

IF gs_detlevel_3D_m = DLEV3D_OFF THEN END

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

!!---- Hotspot Editing
unID=1
!!---- Height Editing
hotspot 0,0,0,unID,zzyzx,1+256 : unID=unID+1
hotspot 0,0,-1,unID,zzyzx,3 : unID=unID+1
hotspot 0,0,zzyzx,unID,zzyzx,2 : unID=unID+1

hotspot thc/tan(cang/2),-thc,0,unID,zzyzx,1+256 : unID=unID+1
hotspot thc/tan(cang/2),-thc,-1,unID,zzyzx,3 : unID=unID+1
hotspot thc/tan(cang/2),-thc,zzyzx,unID,zzyzx,2 : unID=unID+1

PEN gs_cont_pen

IF iQuoinStyle = QUOINSTYLE_SINGLEFACE1 THEN
	MATERIAL gs_brick_mat

	ADDZ zzyzx
	CUTPLANE
	DEL 1

	nn=INT (zzyzx/(hfc+mfh+hsc+mfh))+1
	xx=thc/tan(cang/2)
	mft=thc/2
	xxx=mft/tan(cang/2)

	!!---- Height of First Coyne
	hotspot xx,-thc,0,unID,hfc,1+128 : unID=unID+1
	hotspot xx,-thc,-1,unID,hfc,3 : unID=unID+1
	hotspot xx,-thc,hfc,unID,hfc,2 : unID=unID+1

	!!---- Mortar Fill Height
	hotspot xx,-thc,hfc,unID,mfh,1+128 : unID=unID+1
	hotspot xx,-thc,hfc-1,unID,mfh,3 : unID=unID+1
	hotspot xx,-thc,hfc+mfh,unID,mfh,2 : unID=unID+1

	!!---- Height of Second Coyne
	hotspot xx,-thc,hfc+mfh,unID,hsc,1+128 : unID=unID+1
	hotspot xx,-thc,hfc+mfh-1,unID,hsc,3 : unID=unID+1
	hotspot xx,-thc,hfc+mfh+hsc,unID,hsc,2 : unID=unID+1

	!!---- Length
	hotspot 0,0,0,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,0,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,0,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,0,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,0,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,0,unID,thc,2 : unID=unID+1

	hotspot 0,0,zzyzx,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,zzyzx,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,zzyzx,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,zzyzx,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,zzyzx,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,zzyzx,unID,thc,2 : unID=unID+1
	!!----

	top_hgt=0
	FOR n=1 TO nn
		top_hgt=top_hgt+hfc
		GOSUB "singlecoyne_first_withhotspot"
		ADDZ hfc
		top_hgt=top_hgt+mfh
		GOSUB "singlecoyne_mortar"
		ADDZ mfh
		top_hgt=top_hgt+hsc
		GOSUB "singlecoyne_second_withhotspot"
		ADDZ hsc
		top_hgt=top_hgt+mfh
		GOSUB "singlecoyne_mortar"
		ADDZ mfh
	NEXT N
	DEL nn*4
	CUTEND
ENDIF

IF iQuoinStyle = QUOINSTYLE_SINGLEFACE2 THEN
	ADDZ zzyzx
	MATERIAL gs_brick_mat
	CUTPLANE
	DEL 1

	nn=INT (zzyzx/(hfc+spc))+1
	xx=thc/tan(cang/2)

	!!---- Height of First Coyne
	hotspot xx,-thc,0,unID,hfc,1+128 : unID=unID+1
	hotspot xx,-thc,-1,unID,hfc,3 : unID=unID+1
	hotspot xx,-thc,hfc,unID,hfc,2 : unID=unID+1

	!!---- Mortar Fill Height
	hotspot xx,-thc,hfc,unID,spc,1+128 : unID=unID+1
	hotspot xx,-thc,hfc-1,unID,spc,3 : unID=unID+1
	hotspot xx,-thc,hfc+spc,unID,spc,2 : unID=unID+1

	!!---- Length
	hotspot 0,0,0,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,0,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,0,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,0,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,0,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,0,unID,thc,2 : unID=unID+1

	hotspot 0,0,zzyzx,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,zzyzx,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,zzyzx,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,zzyzx,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,zzyzx,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,zzyzx,unID,thc,2 : unID=unID+1

	top_hgt=0
	FOR n=1 TO nn
		top_hgt=top_hgt+hfc
		GOSUB "singlecoyne_first_withhotspot"
		ADDZ hfc+spc
		top_hgt=top_hgt+spc+hfc
		GOSUB "singlecoyne_first_withhotspot"
		ADDZ hfc+spc
		top_hgt=top_hgt+spc
	NEXT N
	DEL nn*2
	CUTEND
ENDIF

IF iQuoinStyle = QUOINSTYLE_SINGLEBOARD THEN

	xx=thc/tan(cang/2)

	!!---- zzyzx
	hotspot xx, -thc, 0,				unID, zzyzx, 1+128		: unID=unID+1
	hotspot xx, -thc, -1,				unID, zzyzx, 3			: unID=unID+1
	hotspot xx, -thc, zzyzx,			unID, zzyzx, 2			: unID=unID+1

	!!---- Length
	hotspot 0, 0, 0,					unID, wfc_board, 1+128	: unID=unID+1
	hotspot 1, 0, 0,					unID, wfc_board, 3		: unID=unID+1
	hotspot -wfc_board, 0, 0,			unID, wfc_board, 2		: unID=unID+1

	hotspot -wfc_board, 0, 0,			unID, thc, 1+128		: unID=unID+1
	hotspot -wfc_board, 1, 0,			unID, thc, 3			: unID=unID+1
	hotspot -wfc_board, -thc, 0,		unID, thc, 2			: unID=unID+1

	hotspot 0, 0, zzyzx,				unID, wfc_board, 1+128	: unID=unID+1
	hotspot 1, 0, zzyzx,				unID, wfc_board, 3		: unID=unID+1
	hotspot -wfc_board, 0, zzyzx,		unID, wfc_board, 2		: unID=unID+1

	hotspot -wfc_board, 0, zzyzx,		unID, thc, 1+128		: unID=unID+1
	hotspot -wfc_board, 1, zzyzx,		unID, thc, 3			: unID=unID+1
	hotspot -wfc_board, -thc, zzyzx,	unID, thc, 2			: unID=unID+1

	gosub "singleboard_withhotspot"

ENDIF

IF iQuoinStyle = QUOINSTYLE_DOBFACE1 THEN
	ADDZ zzyzx
	MATERIAL gs_brick_mat
	CUTPLANE
	DEL 1

	nn=INT (zzyzx/(hfc+mfh+hsc+mfh))+1
	xx=thc/tan(cang/2)

	!!---- Height of First Coyne
	hotspot xx,-thc,0,unID,hfc,1+128 : unID=unID+1
	hotspot xx,-thc,-1,unID,hfc,3 : unID=unID+1
	hotspot xx,-thc,hfc,unID,hfc,2 : unID=unID+1

	!!---- Mortar Fill Height
	hotspot xx,-thc,hfc,unID,mfh,1+128 : unID=unID+1
	hotspot xx,-thc,hfc-1,unID,mfh,3 : unID=unID+1
	hotspot xx,-thc,hfc+mfh,unID,mfh,2 : unID=unID+1

	!!---- Height of Second Coyne
	hotspot xx,-thc,hfc+mfh,unID,hsc,1+128 : unID=unID+1
	hotspot xx,-thc,hfc+mfh-1,unID,hsc,3 : unID=unID+1
	hotspot xx,-thc,hfc+mfh+hsc,unID,hsc,2 : unID=unID+1

	!!---- Length
	hotspot 0,0,0,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,0,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,0,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,0,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,0,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,0,unID,thc,2 : unID=unID+1

	hotspot 0,0,zzyzx,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,zzyzx,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,zzyzx,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,zzyzx,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,zzyzx,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,zzyzx,unID,thc,2 : unID=unID+1

	rotz -cang
	hotspot 0,0,0,unID,wsc,1+128 : unID=unID+1
	hotspot 1,0,0,unID,wsc,3 : unID=unID+1
	hotspot -wsc,0,0,unID,wsc,2 : unID=unID+1

	hotspot -wsc,0,0,unID,thc,1+128 : unID=unID+1
	hotspot -wsc,-1,0,unID,thc,3 : unID=unID+1
	hotspot -wsc,thc,0,unID,thc,2 : unID=unID+1

	hotspot 0,0,zzyzx,unID,wsc,1+128 : unID=unID+1
	hotspot 1,0,zzyzx,unID,wsc,3 : unID=unID+1
	hotspot -wsc,0,zzyzx,unID,wsc,2 : unID=unID+1

	hotspot -wsc,0,zzyzx,unID,thc,1+128 : unID=unID+1
	hotspot -wsc,-1,zzyzx,unID,thc,3 : unID=unID+1
	hotspot -wsc,thc,zzyzx,unID,thc,2 : unID=unID+1
	del 1

	top_hgt=0
	FOR n=1 TO nn
		top_hgt=top_hgt+hfc
		MATERIAL gs_brick_mat
		GOSUB "doublecoyne_first_withhotspot"
		ADDZ hfc
		top_hgt=top_hgt+mfh
		GOSUB "doublecoyne_mortar"
		ADDZ mfh
		top_hgt=top_hgt+hsc
		GOSUB "doublecoyne_second_withhotspot"
		ADDZ hsc
		top_hgt=top_hgt+mfh
		GOSUB "doublecoyne_mortar"
		ADDZ mfh
	NEXT N
	DEL nn*4
	CUTEND
ENDIF

IF iQuoinStyle = QUOINSTYLE_DOBFACE2 THEN
	ADDZ zzyzx
	MATERIAL gs_brick_mat
	CUTPLANE
	DEL 1

	nn=INT (zzyzx/(hfc+spc))+1
	xx=thc/tan(cang/2)

	!!---- Height of First Coyne
	hotspot xx,-thc,0,unID,hfc,1+128 : unID=unID+1
	hotspot xx,-thc,-1,unID,hfc,3 : unID=unID+1
	hotspot xx,-thc,hfc,unID,hfc,2 : unID=unID+1

	!!---- Spacing Height
	hotspot xx,-thc,hfc,unID,spc,1+128 : unID=unID+1
	hotspot xx,-thc,hfc-1,unID,spc,3 : unID=unID+1
	hotspot xx,-thc,hfc+spc,unID,spc,2 : unID=unID+1

	!bug fixing: deleted hotspot editing
	unID = unID + 3

	!!---- Length
	hotspot 0,0,0,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,0,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,0,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,0,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,0,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,0,unID,thc,2 : unID=unID+1

	hotspot 0,0,zzyzx,unID,wfc,1+128 : unID=unID+1
	hotspot 1,0,zzyzx,unID,wfc,3 : unID=unID+1
	hotspot -wfc,0,zzyzx,unID,wfc,2 : unID=unID+1

	hotspot -wfc,0,zzyzx,unID,thc,1+128 : unID=unID+1
	hotspot -wfc,1,zzyzx,unID,thc,3 : unID=unID+1
	hotspot -wfc,-thc,zzyzx,unID,thc,2 : unID=unID+1

	rotz -cang
	hotspot 0,0,0,unID,wsc,1+128 : unID=unID+1
	hotspot 1,0,0,unID,wsc,3 : unID=unID+1
	hotspot -wsc,0,0,unID,wsc,2 : unID=unID+1

	hotspot -wsc,0,0,unID,thc,1+128 : unID=unID+1
	hotspot -wsc,-1,0,unID,thc,3 : unID=unID+1
	hotspot -wsc,thc,0,unID,thc,2 : unID=unID+1

	hotspot 0,0,zzyzx,unID,wsc,1+128 : unID=unID+1
	hotspot 1,0,zzyzx,unID,wsc,3 : unID=unID+1
	hotspot -wsc,0,zzyzx,unID,wsc,2 : unID=unID+1

	hotspot -wsc,0,zzyzx,unID,thc,1+128 : unID=unID+1
	hotspot -wsc,-1,zzyzx,unID,thc,3 : unID=unID+1
	hotspot -wsc,thc,zzyzx,unID,thc,2 : unID=unID+1
	del 1

	top_hgt=0
	MATERIAL gs_brick_mat
	FOR n=1 TO nn
		top_hgt=top_hgt+hfc
		GOSUB "doublecoyne_first_withhotspot"
		ADDZ hfc+spc
		top_hgt=top_hgt+spc+hfc
		GOSUB "doublecoyne_first_withhotspot"
		ADDZ hfc+spc
		top_hgt=top_hgt+spc
		NEXT N
	DEL nn*2
	CUTEND
ENDIF

if iQuoinStyle = QUOINSTYLE_DOBBOARD then
	xx = thc / tan(cang / 2)

	!!---- zzyzx
	hotspot xx, -thc, 0,			unID, zzyzx, 1+128		: unID=unID+1
	hotspot xx, -thc, -1,			unID, zzyzx, 3			: unID=unID+1
	hotspot xx, -thc, zzyzx,		unID, zzyzx, 2			: unID=unID+1

	!!---- Length
	hotspot 0, 0 ,0,				unID, wfc_board, 1+128	: unID=unID+1
	hotspot 1, 0, 0,				unID, wfc_board, 3		: unID=unID+1
	hotspot -wfc_board, 0, 0,		unID, wfc_board, 2		: unID=unID+1

	hotspot -wfc_board, 0, 0,		unID, thc, 1+128 		: unID=unID+1
	hotspot -wfc_board, 1, 0,		unID, thc, 3	 		: unID=unID+1
	hotspot -wfc_board, -thc, 0,	unID, thc, 2 			: unID=unID+1

	hotspot 0, 0, zzyzx,			unID, wfc_board, 1+128 	: unID=unID+1
	hotspot 1, 0, zzyzx,			unID, wfc_board, 3	 	: unID=unID+1
	hotspot -wfc_board, 0, zzyzx,	unID, wfc_board, 2		: unID=unID+1

	hotspot -wfc_board, 0, zzyzx,	unID, thc, 1+128 		: unID=unID+1
	hotspot -wfc_board, 1, zzyzx,	unID, thc, 3 			: unID=unID+1
	hotspot -wfc_board, -thc, zzyzx,unID, thc, 2 			: unID=unID+1

	rotz -cang
	hotspot 0, 0, 0,				unID, wsc_board, 1+128	: unID=unID+1
	hotspot 1, 0, 0,				unID, wsc_board, 3		: unID=unID+1
	hotspot -wsc_board, 0, 0,		unID, wsc_board, 2 		: unID=unID+1

	hotspot -wsc_board, 0, 0,		unID, thc, 1+128		: unID=unID+1
	hotspot -wsc_board, -1, 0,		unID, thc, 3			: unID=unID+1
	hotspot -wsc_board, thc, 0,		unID, thc, 2 			: unID=unID+1

	hotspot 0, 0, zzyzx,			unID, wsc_board, 1+128	: unID=unID+1
	hotspot 1, 0, zzyzx,			unID, wsc_board, 3		: unID=unID+1
	hotspot -wsc_board, 0, zzyzx,	unID, wsc_board, 2		: unID=unID+1

	hotspot -wsc_board, 0, zzyzx,	unID, thc, 1+128 		: unID=unID+1
	hotspot -wsc_board, -1, zzyzx,	unID, thc, 3			: unID=unID+1
	hotspot -wsc_board, thc, zzyzx,	unID, thc, 2			: unID=unID+1
	del 1

	gosub "doubleboard_withhotspot"

endif

!=================================================================================================
  END
!=================================================================================================



"singlecoyne_first_withhotspot":
	if top_hgt - hfc/2 < zzyzx then
		hotspot 0,		0, hfc/2, unID, wfc, 1+128	: unID=unID+1
		hotspot 1,		0, hfc/2, unID, wfc, 3		: unID=unID+1
		hotspot -wfc,	0, hfc/2, unID, wfc, 2		: unID=unID+1
	endif

	gosub "singlecoyne"
return

"singleboard_withhotspot":
	hotspot 0,			0, zzyzx/2, unID, wfc_board, 1+128	: unID=unID+1
	hotspot 1,			0, zzyzx/2, unID, wfc_board, 3		: unID=unID+1
	hotspot -wfc_board,	0, zzyzx/2, unID, wfc_board, 2		: unID=unID+1

	hfc = zzyzx
	wfc = wfc_board
	gs_brick_mat = gs_board_mat
	gosub "singlecoyne"
return

"singlecoyne":
	MATERIAL gs_brick_mat
	PRISM_  5, hfc,
			0,		0,		15,
			-wfc,	0,		15,
			-wfc,	-thc,	15,
			xx,		-thc,	15,
			0,		0,		-1
return

"singlecoyne_mortar":
	IF wsc<wfc THEN
		yyy=wsc-mft
	ELSE
		yyy=wfc-mft
	ENDIF

	MATERIAL gs_mortar_mat
	PRISM_  5,mfh,
			0,0,15,
			-yyy,0,15,
			-yyy,-mft,15,
			xxx,-mft,15,
			0,0,-1
return


"singlecoyne_second_withhotspot":
	If top_hgt-hsc/2<zzyzx Then
		hotspot 0,0,hsc/2,unID,wsc,1+128 : unID=unID+1
		hotspot 1,0,hsc/2,unID,wsc,3 : unID=unID+1
		hotspot -wsc,0,hsc/2,unID,wsc,2 : unID=unID+1
	EndIf

	MATERIAL gs_brick_mat
	PRISM_  5, hsc,
			0,		0,		15,
			-wsc,	0,		15,
			-wsc,	-thc,	15,
			xx,		-thc,	15,
			0,		0,		-1
return

"doublecoyne_first_withhotspot":
	if top_hgt - hfc / 2 < zzyzx then
		hotspot 0,		0, hfc/2, unID, wfc, 1+128	: unID=unID+1
		hotspot 1,		0, hfc/2, unID, wfc, 3		: unID=unID+1
		hotspot -wfc,	0, hfc/2, unID, wfc, 2		: unID=unID+1

		rotz -cang
		hotspot 0,		0, hfc/2, unID, wsc, 1+128	: unID=unID+1
		hotspot 1,		0, hfc/2, unID, wsc, 3		: unID=unID+1
		hotspot -wsc,	0, hfc/2, unID, wsc, 2		: unID=unID+1
		del 1
	endif

	gosub "doublecoyne_first"
return

"doubleboard_withhotspot":
	hotspot 0,			0, zzyzx/2, unID, wfc_board, 1+128	: unID=unID+1
	hotspot 1,			0, zzyzx/2, unID, wfc_board, 3		: unID=unID+1
	hotspot -wfc_board,	0, zzyzx/2, unID, wfc_board, 2		: unID=unID+1

	rotz -cang
	hotspot 0,			0, zzyzx/2, unID, wsc_board, 1+128	: unID=unID+1
	hotspot 1,			0, zzyzx/2, unID, wsc_board, 3		: unID=unID+1
	hotspot -wsc_board,	0, zzyzx/2, unID, wsc_board, 2		: unID=unID+1
	del 1

	hfc = zzyzx
	wfc = wfc_board
	wsc = wsc_board
	gs_brick_mat = gs_board_mat
	gosub "doublecoyne_first"
return

"doublecoyne_first":
	material gs_brick_mat
	prism_  7, hfc,
			0,									0,									15,
			-wfc,								0,									15,
			-wfc,								-thc,								15,
			xx,									-thc,								15,
			xx + (wsc + xx) * cos(180 - cang),	-thc + (wsc + xx) * sin(180 - cang),15,
			wsc * cos(180 - cang),				wsc * sin(180 - cang),				15,
			0,									0,									-1
return

"doublecoyne_mortar":
	mft=thc/2
	xxx=mft/tan(cang/2)

	IF wsc<wfc THEN
		yyy=wsc-mft
		ELSE
		yyy=wfc-mft
	ENDIF

	MATERIAL gs_mortar_mat
	PRISM_  7,mfh,
			0,0,15,
			-yyy,0,15,
			-yyy,-mft,15,
			xxx,-mft,15,
			xxx+(yyy+xxx)*cos(180-cang),-mft+(yyy+xxx)*sin(180-cang),15,
			(yyy)*cos(180-cang),(yyy)*sin(180-cang),15,
			0,0,-1
return

"doublecoyne_second_withhotspot":
	If top_hgt-hsc/2<zzyzx Then
		hotspot 0,0,hsc/2,unID,wsc,1+128 : unID=unID+1
		hotspot 1,0,hsc/2,unID,wsc,3 : unID=unID+1
		hotspot -wsc,0,hsc/2,unID,wsc,2 : unID=unID+1

		rotz -cang
		hotspot 0,0,hsc/2,unID,wfc,1+128 : unID=unID+1
		hotspot 1,0,hsc/2,unID,wfc,3 : unID=unID+1
		hotspot -wfc,0,hsc/2,unID,wfc,2 : unID=unID+1
		del 1
	EndIf

	MATERIAL gs_brick_mat
	PRISM_  7,hsc,
			0,0,15,
			-wsc,0,15,
			-wsc,-thc,15,
			xx,-thc,15,
			xx+(wfc+xx)*cos(180-cang),-thc+(wfc+xx)*sin(180-cang),15,
			wfc*cos(180-cang),wfc*sin(180-cang),15,
			0,0,-1
return
