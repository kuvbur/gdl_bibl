
! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 		= -1
_idxTab = 1
TABID_DIMENSIONS	= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES 		= _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Руста`
ui_current_page gs_ui_current_page

ui_page TABID_DIMENSIONS, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "dimensionsPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"


!--------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ======================================================================
end
! ======================================================================

"dimensionsPage":

	popupWidth	= 135
	popupHeight	= 96
	x1 = 0
	xEnd = 440
	xHalf = 223
	x2 = xHalf - popupWidth - 10
	x3 = xHalf  + 10
	x6 = 360
	x7 = 360

	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1

	ui_style 0, 1
	ui_outfield	`Стиль` ,x1,    yCurrent+4, 	xHalf-2,	outfieldHeight	

	yCurrent = yCurrent + dy
	ui_style 0, 0


	ui_infield{3} "iQuoinStyle", 	 x2,   yCurrent, 	xHalf - x2-10, 96,
				2, 1, 										! 1 QuoinChanger.png
				8, 2,
				120, 90, 120, 90,
				1, `Две Поверхности 1`,	QUOINSTYLE_DOBFACE1,	! picture indexes don't correspond with int values!!
				2, `Две Поверхности 2`,	QUOINSTYLE_DOBFACE2,
				3, `Две Поверхности 3`,	QUOINSTYLE_DOBBOARD,
				4, `Одна Поверхность 1`,	QUOINSTYLE_SINGLEFACE1,
				5, `Одна Поверхность 2`,	QUOINSTYLE_SINGLEFACE2,
				6, `Одна Поверхность 3`,	QUOINSTYLE_SINGLEBOARD

! ------------------------------------------------------------------------------
	ui_separator xHalf , 0, xHalf , yEnd
! ------------------------------------------------------------------------------
	yCurrent 			= pageStartY + 1

	ui_style 0, 1
	ui_outfield	`Размеры` ,xHalf+5,    yCurrent+4, 	xEnd-xHalf-6,	outfieldHeight	

	ui_style 0, 0
	yCurrent = yCurrent + dy

	ui_outfield	`Угол` ,xHalf+5,    yCurrent+4, 	xEnd - x7,	outfieldHeight	
	if iQuoinStyle = QUOINSTYLE_SINGLEFACE1 | iQuoinStyle = QUOINSTYLE_SINGLEFACE2 then
		ui_infield "cend", 	 x7,   yCurrent, 	xEnd - x7, infieldHeight
	else
		ui_infield "cang", 	 x7,   yCurrent, 	xEnd - x7, infieldHeight
	endif
	yCurrent = yCurrent + dy

	uiPictX = 260
	uiPictY = 65
	uiPictWdt = 67
	uiPictHgt = 175
	uiPictMargin = 5
	infieldHeight = 19

	uiPicLeftStart = uiPictX - 90 - uiPictMargin
	uiPicLeftEND = uiPicLeftStart - uiPictMargin

	uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin
	uiPictInfWdt = xEnd - uiPicRightStart

	if iQuoinStyle = QUOINSTYLE_DOBFACE1 then picindex = 2		! QuoinType1.png
	if iQuoinStyle = QUOINSTYLE_DOBFACE2 then picindex = 3		! QuoinType2.png
	if iQuoinStyle = QUOINSTYLE_SINGLEFACE1 then picindex = 4	! QuoinType3.png
	if iQuoinStyle = QUOINSTYLE_SINGLEFACE2 then picindex = 5	! QuoinType4.png
	if iQuoinStyle = QUOINSTYLE_DOBBOARD then picindex = 6		! QuoinType5.png
	if iQuoinStyle = QUOINSTYLE_SINGLEBOARD then picindex = 7	! QuoinType6.png
	UI_PICT picindex, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 

	ui_infield "thc", uiPicRightStart, uiPictY-6, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина Блоков`

	if iQuoinStyle <> QUOINSTYLE_SINGLEFACE2 & iQuoinStyle <> QUOINSTYLE_SINGLEBOARD then
		if	iQuoinStyle = QUOINSTYLE_SINGLEBOARD | iQuoinStyle = QUOINSTYLE_DOBBOARD then
			param = "wsc_board"
		else
			param = "wsc"
		endif
		ui_infield param, uiPicRightStart, uiPictY+20, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Второго Блока`
	endif 

	if iQuoinStyle <> QUOINSTYLE_SINGLEFACE2 & iQuoinStyle <> QUOINSTYLE_SINGLEBOARD then
		posY = uiPictY + 46
	else
		posY = uiPictY + 22
	endif
	if	iQuoinStyle = QUOINSTYLE_SINGLEBOARD | iQuoinStyle = QUOINSTYLE_DOBBOARD then
		param = "wfc_board"
	else
		param = "wfc"
	endif
	ui_infield param, uiPicRightStart, posY, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Первого Блока`


	if iQuoinStyle <> QUOINSTYLE_SINGLEBOARD & iQuoinStyle <> QUOINSTYLE_DOBBOARD then
		if iQuoinStyle <> QUOINSTYLE_DOBFACE2 & iQuoinStyle <> QUOINSTYLE_SINGLEFACE2 then
			ui_infield "mfh", uiPicRightStart, uiPictY+72, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина Шва`
		else
			ui_infield "spc", uiPicRightStart, uiPictY+72, uiPictInfWdt, infieldHeight UI_TOOLTIP `Зазор`
		endif
	
		if iQuoinStyle <> QUOINSTYLE_DOBFACE2 & iQuoinStyle <> QUOINSTYLE_SINGLEFACE2 then
			ui_infield "hsc", uiPicRightStart, uiPictY+120, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Второго Блока`
		endif 

		ui_infield "hfc", uiPicRightStart, uiPictY+150, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Первого Блока`
	endif 

return
	
"AppearancePage":
	dim st2DParamNames[][]
		st2DParamNames[1][1] = "gs_cont_pen" 			: st2DParamNames[1][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[2][1] = "gs_fill_type" 			: st2DParamNames[2][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[3][1] = "gs_fill_pen" 			: st2DParamNames[3][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[4][1] = "gs_back_pen" 			: st2DParamNames[4][2] = "ui_list_fillBgPen_flat(1)"

	dim st3DParamNames[][]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"		: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_shadow"				: st3DParamNames[2][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[][]
		if	iQuoinStyle = QUOINSTYLE_SINGLEBOARD | iQuoinStyle = QUOINSTYLE_DOBBOARD then
			stSurfaceParamNames[1][1] = "gs_board_mat"	: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"
		else
			stSurfaceParamNames[1][1] = "gs_brick_mat"	: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"
			if iQuoinStyle = QUOINSTYLE_SINGLEFACE1 | iQuoinStyle = QUOINSTYLE_DOBFACE1 then
				stSurfaceParamNames[2][1] = "gs_mortar_mat"	: stSurfaceParamNames[2][2] = "ui_list_surface_flat(1)"
			endif
		endif

	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
return

"descriptionPage":

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return

