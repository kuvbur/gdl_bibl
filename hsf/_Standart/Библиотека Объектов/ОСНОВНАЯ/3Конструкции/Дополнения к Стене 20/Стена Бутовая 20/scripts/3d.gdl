
IF gs_detlevel_3D_m = 0 and not(gs_2D_projection) THEN END

IF not(gs_shadow) THEN SHADOW OFF


! ------------------------------------------------------------------------------
! Hotspot Editing
! ------------------------------------------------------------------------------
unID=1

hotspot 0,0,0,unID,a,1+256 : unID=unID+1
hotspot -1,0,0,unID,a,3 : unID=unID+1
hotspot a,0,0,unID,a,2 : unID=unID+1

hotspot a/2,0,0,unID : unID=unID+1

hotspot a/2,0,0,unID,Slnt,6 : unID=unID+1
hotspot a/2,1,0,unID,Slnt,4+128 : unID=unID+1
hotspot a/2+1,0,0,unID,Slnt,7 : unID=unID+1
hotspot a/2,cos(Slnt)*zzyzx2/2,sin(Slnt)*zzyzx2/2,unID,Slnt,5 : unID=unID+1

rotx Slnt
hotspot -Adl,0,0,unID,zzyzx2,1+128,zzyzx : unID=unID+1
hotspot -Adl,-1,0,unID,zzyzx2,3,zzyzx : unID=unID+1
hotspot -Adl,zzyzx2,0,unID,zzyzx2,2,zzyzx : unID=unID+1

hotspot a/2,0,0,unID,zzyzx2,1+128,zzyzx : unID=unID+1
hotspot a/2,-1,0,unID,zzyzx2,3,zzyzx : unID=unID+1
hotspot a/2,zzyzx2,0,unID,zzyzx2,2,zzyzx : unID=unID+1

hotspot a+Adr,0,0,unID,zzyzx2,1+128,zzyzx : unID=unID+1
hotspot a+Adr,-1,0,unID,zzyzx2,3,zzyzx : unID=unID+1
hotspot a+Adr,zzyzx2,0,unID,zzyzx2,2,zzyzx : unID=unID+1
del 1

RESOL gs_resol
PEN gs_cont_pen
MATERIAL mat1


! ------------------------------------------------------------------------------
! Cutting Planes
! ------------------------------------------------------------------------------

! Miter cutting planes
roty -90
cutplane Mtre_L
del 1

! End of miter cutting planes
addx a
roty 90
cutplane Mtre_R
del 2

! Cuts top off wall
addz ZZYZX
rotz 90
cutplane -Tp_Wll
del 2

! Cuts bottom off wall
roty 180
rotz 90
cutplane -Bot_Wll
del 2


! ------------------------------------------------------------------------------
! Geometry
! ------------------------------------------------------------------------------

if Tp_Wll>0 then
	extraHeightForTopCut = tan(Tp_Wll)*Lgth + Bldrsze
else
	extraHeightForTopCut = 0
endif
if abs(Bot_Wll) > 0.00001 then
	extraHeightForBottomCut = tan(Bot_Wll)*Lgth + Bldrsze
else
	extraHeightForBottomCut = 0
endif

! Calculating hyp of wall from slant and height
Hgtcl=(ZZYZX/cos(90-Slnt))+(extraHeightForTopCut+extraHeightForBottomCut)

No_Bldrs=ceil(Lgth/(Bldrsze*0.75))		! Calculates number of boulders across
No_Rses=ceil(Hgtcl/(Bldrsze*0.67))		! Calculates number of boulders up

! Calc extra hgt for bottom cut
If Bot_Wll<0 Then addz extraHeightForBottomCut

Cntr=1

addx -Adl
rotx -90+Slnt
If GLOB_CONTEXT<20 Or GLOB_CONTEXT>40 Then
	Rw = 0
	for CntHgt=1 to No_Rses
		if Rw=0 then
			for Cnt=1 to No_Bldrs
				rot Cntr*5*rnd(10),Cntr*5*rnd(8),Cntr*5,Cntr*5*rnd(6)
				sphere Bldrsze*0.5
				del 1
				addx Lgth/(No_Bldrs-1)
				Cntr=Cntr+1
			next Cnt
			del Cnt-1
			Rw=1
		else		! Rw=1
			addx (Lgth/No_Bldrs)*0.5
			for Cnt=1 to No_Bldrs-1
				rot Cntr*5*rnd(5),Cntr*5*rnd(4),Cntr*5,Cntr*5*rnd(3)
				sphere Bldrsze*0.5
				del 1
				addx Lgth/(No_Bldrs-1)
				Cntr=Cntr+1
			next Cnt
			del Cnt-0
			Rw=0
		endif
		addz Hgtcl/No_Rses
	next CntHgt
Else
	prism_	5,	zzyzx/sin(Slnt),
			0, -Bldrsze/2, 15,
			a+Adl+Adr, -Bldrsze/2, 15,
			a+Adl+Adr, Bldrsze/2, 15,
			0, Bldrsze/2, 15,
			0, -Bldrsze/2, -1
EndIf

cutend
cutend
cutend
cutend
