
!! SW Header.gsm

if gs_detlevel_3D_m = DLEV3D_OFF then end

if gs_shadow=1 then SHADOW ON
if gs_shadow=0 then SHADOW OFF

pen gs_cont_pen
material fmat
RESOL gs_resol

ADD A/2,B,0
if gs_detlevel_3D_m = DLEV3D_SIMPLE then
rotx 90
prism_ 5, fth,
	a/2,    0,    15,
	a/2,   1",    79,
   -a/2,   1",  1079,
   -a/2,    0,    15,
	a/2,    0,    -1
del 1
end
endif


rotx 90
addy 1"

!!Frame!!

pen gs_cont_pen
material fmat

r=a/2
alpha=360/gs_resol

!For additional 2 points coordinate in base polyline
!to solve smooth surface problems
gamma=atn((5/16")/(fth*2/8))
delta=atn((fth*1/16)/(1 9/16"))


alpha=-alpha
for i=1 to gs_resol/2+3 step 1
put r*cos(alpha),r*sin(alpha),0
alpha=alpha+360/gs_resol
next i

ee=0.00001       !"small" value

!! Model Solid
tubea 11,nsp/3,1+2+16+32,
0,0,0,
2 1/2",0,0,

2 1/2",fth*11/16-ee,1,
2 1/2",fth*11/16,0,
2 1/2"-ee*cos(delta),fth*11/16+ee*sin(delta),1,

15/16"+ee*cos(delta),fth*12/16-ee*sin(delta),1,
15/16",fth*12/16,0,
15/16"-ee*sin(gamma),fth*12/16+ee*cos(gamma),1,

5/8",fth,0,
0,fth,0,
0,0,0,

get (nsp)

body -1

prism_ 5, b,
	-a/2, -1",  15,
	 a/2, -1",  15,
	 a/2,   0,  15,
	-a/2,   0,  15,
	-a/2, -1",  -1

kr=(a/2-fw)/8

prism_ 7, fth,
	kr+3/4", 0, 79,
	0,  1", 800,
   -kr-3/4", 0, 1015,
	-kr, 0, 79,
	0, 1", 800,
	kr, 0, 1015,
	kr+3/4", 0, -1

prism_ 4, fth-1/2",
	kr, 0, 79,
	0,  1", 800,
   -kr, 0, 1015,
	kr, 0, -1


!! Louvers !!
material lmat
addz 1/4"

! Cut Plane angle calculation for louvers

beta=360/gs_resol
dev=(a/2-fw)-(a/2-fw)*cos(beta/2)

rotz beta/2
for i=1 to gs_resol/2 step 1
addx a/2-fw-dev
cutplane 0,1,1,0
del 1
rotz beta
next i
del gs_resol/2+1

cutpolya 4, 2, 0,
	kr+3/4", 0, 79,
	0,  1", 800,
   -kr-3/4", 0, 1015,
	kr+3/4", 0, -1

for h=1 to 2
for i=1 to nol/2

alfa=180/nol
r=a+fw
szi=13
fi=alfa*(sin(szi))^2
plane_ 4,
	0,      0,    0,  15,
	r,      0,    0,  15,
	r*cos(fi),r*sin(fi), pi/180*r*alfa*sin(szi)*cos(szi), 15,
	0,      0,    0,  -1
plane_ 4,
	0, 0, 0, 15,
	r*cos(fi),r*sin(fi), pi/180*r*alfa*sin(szi)*cos(szi), 15,
	r*cos(alfa),r*sin(alfa), 0, 15,
	0, 0, 0, -1
base
vert 0,0,0
vert 0,0,-1
vert 0,1,0
vert 1,0,0
coor 2,-1,-2,-3,-4
body 1

rotz alfa

next i
del nol/2
mulx -1
next h
del 2

cutend

for j=1 to gs_resol/2
cutend
next j

del 1

poly_  7,
	a/2-fw, 0, 79,
	0,  1", 800,
   -a/2+fw, 0, 1015,
   -kr-3/4", 0, 79,
	0,  1", 800,
	kr+3/4", 0, 1015,
	a/2-fw, 0, -1




del 3

end
