
!! Half Round Gable Louver.gsm

if gs_detlevel_3D_m = DLEV3D_OFF then end

if gs_shadow=1 then SHADOW ON
if gs_shadow=0 then SHADOW OFF

alpha=360/gs_resol
ADDx a/2
rotx 90
addy 1"

!!Trim!!

if trm=1 then

pen gs_cont_pen
material fmat
resol gs_resol
addy -1"
prism_ 12,tth,
-a/2-tw,-tw,15,
 a/2+tw,-tw,15,
 a/2+tw,1",79,
 0,1,800,
-a/2-tw,1",1079,
-a/2-tw,-tw,-1,

-a/2,0,15,
 a/2,0,15,
 a/2,1",79,
 0,1,800,
-a/2,1",1079,
-a/2,0,-1
del 1

endif



!!Frame!!

pen gs_cont_pen
material fmat

r=a/2

!For additional 2 points coordinate in base polyline
!to solve smooth surface problems
gamma=atn((5/16")/(fth*2/8))
delta=atn((fth*1/16)/(1 9/16"))

alpha=-alpha
for i=1 to gs_resol/2+3 step 1
put r*cos(alpha),r*sin(alpha),0
alpha=alpha+360/gs_resol
next i

ee=0.00001       !"small" value

!! Model Solid
tubea 11,nsp/3,1+2+16+32,
0,0,0,
2 1/2",0,0,

2 1/2",fth*11/16-ee,1,
2 1/2",fth*11/16,0,
2 1/2"-ee*cos(delta),fth*11/16+ee*sin(delta),1,

15/16"+ee*cos(delta),fth*12/16-ee*sin(delta),1,
15/16",fth*12/16,0,
15/16"-ee*sin(gamma),fth*12/16+ee*cos(gamma),1,

5/8",fth,0,
0,fth,0,
0,0,0,

get (nsp)

body -1

add -a/2,-1",0
block a,1",bth
del 1


!!Louvers!!

pen gs_cont_pen
material lmat

! Cut Plane angle calculation

alpha=360/gs_resol
dev=(a/2-2.5")-(a/2-2.5")*cos(alpha/2)

rotz alpha/2
for i=1 to gs_resol/2 step 1
addx a/2-2.5"-dev
cutplane 0,1,1,0
del 1
rotz alpha
next i
del gs_resol/2+1


n=int(a/lw)+1

addx a/2
roty -90

for i=1 to int(n/2) step 1
prism_ 7,a,
!!0,0,15,
fth*5/8,0,15,
fth*5/8,lw*2/7,15,
fth*1/8,lw,15,
0,lw,15,
0,lw*5/7,15,
fth*4/8,0,15,
fth*5/8,0,-1
!!0,0,-1


base
vert 0,0,0
vert 0,0,-1
vert 0,1,0
vert 1,0,0
coor 2,-1,-2,-3,-4
body 1
addy lw
next i
del int(n/2)+2


for i=1 to gs_resol/2 step 1
cutend
next i




del top

end
