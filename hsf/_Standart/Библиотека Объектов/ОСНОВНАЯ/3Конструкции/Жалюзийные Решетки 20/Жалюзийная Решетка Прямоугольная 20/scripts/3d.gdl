
!! Vertical Gable Louver.gsm

if gs_detlevel_3D_m = DLEV3D_OFF then end

if gs_shadow=1 then SHADOW ON
if gs_shadow=0 then SHADOW OFF

!!alpha=360/gs_resol
ADDx a/2

rotx 90
addy 1"

!!Trim!!

if trm=1 then

pen gs_cont_pen
material fmat
addy -1"
prism_ 10,tth,
-a/2-tw,-tw,15,
 a/2+tw,-tw,15,
 a/2+tw,zzyzx+tw,15,
-a/2-tw,zzyzx+tw,15,
-a/2-tw,-tw,-1,
-a/2,0,15,
 a/2,0,15,
 a/2,zzyzx,15,
-a/2,zzyzx,15,
-a/2,0,-1

del 1

endif


!!Frame!!

pen gs_cont_pen
material fmat

r=a/2

!For additional 2 points coordinate in base polyline
!to solve smooth surface problems
gamma=atn((5/16")/(fth*2/8))
delta=atn((fth*1/16)/(1 9/16"))

ee=0.00001       !"small" value

!! Model Solid
tube 11,6,1+2+16+32,
0,0,0,
2 1/2",0,0,
2 1/2",fth*11/16-ee,1,
2 1/2",fth*11/16,0,
2 1/2"-ee*cos(delta),fth*11/16+ee*sin(delta),1,
15/16"+ee*cos(delta),fth*12/16-ee*sin(delta),1,
15/16",fth*12/16,0,
15/16"-ee*sin(gamma),fth*12/16+ee*cos(gamma),1,
5/8",fth,0,
0,fth,0,
0,0,0,

 a/2,-1",0,0,
 a/2,0,0,0,
 a/2,zzyzx-1",0,0,
-a/2,zzyzx-1",0,0,
-a/2,0,0,0,
-a/2,-1",0,0

body -1

add -a/2,-1",0
block a,1",bth
del 1



!!Louvers!!

pen gs_cont_pen
material lmat


addy zzyzx-2 1/2"-1"
cutplane 1,0,1,0
del 1

n=int(zzyzx/lw)+1

addx a/2-2 1/2"
roty -90
for i=1 to int(n) step 1
prism_ 7,a-2*2 1/2",
!!!0,0,15,
fth*5/8,0,15,
fth*5/8,lw*2/7,15,
fth*1/8,lw,15,
0,lw,15,
0,lw*5/7,15,
fth*4/8,0,15,
fth*5/8,0,-1
!!!0,0,-1


base
vert 0,0,0
vert 0,0,-1
vert 0,1,0
vert 1,0,0
coor 2,-1,-2,-3,-4
body 1
addy lw
next i
del int(n)+2

cutend

del 3

end
