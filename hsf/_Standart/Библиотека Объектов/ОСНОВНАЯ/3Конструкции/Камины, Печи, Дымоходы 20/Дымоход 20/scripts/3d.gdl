
! ==============================================================================
! prefabChimney_m
! ==============================================================================
!block 4, 2, 1

if gs_detlevel_3D_m = 0 then end
if gs_shadow = 0 then shadow off


! ==============================================================================
! MEP
! ==============================================================================

IF bShowMEP THEN

	IF useSysMat THEN
		matFlue 	= sMat
		matDoor = sMat
		matCap = sMat
		gs_con_mat 	= sMat
	ENDIF

	PEN gs_cont_pen
	resol gs_resol

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 14,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 14,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 14,
		ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 14,
		ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConPosZ_5=ConPosZ_5, ConLength_5=ConLength_5, ConEdit_5 = 14,
		ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConPosZ_6=ConPosZ_6, ConLength_6=ConLength_6, ConEdit_6 = 14,
		ConPosX_7=ConPosX_7, ConPosY_7=ConPosY_7, ConPosZ_7=ConPosZ_7, ConLength_7=ConLength_7, ConEdit_7 = 14,
		ConPosX_8=ConPosX_8, ConPosY_8=ConPosY_8, ConPosZ_8=ConPosZ_8, ConLength_8=ConLength_8, ConEdit_8 = 14,
		ConPosX_9=ConPosX_9, ConPosY_9=ConPosY_9, ConPosZ_9=ConPosZ_9, ConLength_9=ConLength_9, ConEdit_9 = 14,
		ConPosX_10=ConPosX_10, ConPosY_10=ConPosY_10, ConPosZ_10=ConPosZ_10, ConLength_10=ConLength_10, ConEdit_10 = 14

ENDIF

pen gs_cont_pen
material matFlue
resol gs_resol

! ==============================================================================
! Hotspot editing
! ==============================================================================

unIDBegin = 1
unID = unIDBegin

hotspot -_flueFullRadius,	 0,					0, unID : unID = unID + 1
hotspot  _flueFullRadius,	 0,					0, unID : unID = unID + 1
hotspot  0,					 _flueFullRadius,	0, unID : unID = unID + 1
hotspot  0,					-_flueFullRadius,	0, unID : unID = unID + 1

hotspot 0,	0,	0,		unID,	ZZYZX,	1+128	: unID = unID + 1
hotspot 0,	0,	ZZYZX,	unID,	ZZYZX,	2		: unID = unID + 1
hotspot 0,	0,	-1,		unID,	ZZYZX,	3		: unID = unID + 1

if bBottomDoor then
	rotDoorRadius = _flueFullRadius + clDoorProfileThk

	hotspot 0,								-_flueFullRadius - clDoorProfileThk,	zDoorPos - _unitHeight/2,		unID, doorAngle, 4+128	: unID = unID+1
	hotspot sin(doorAngle) * rotDoorRadius,	-cos(doorAngle) * rotDoorRadius,		zDoorPos - _unitHeight/2,		unID, doorAngle, 5		: unID = unID+1
	hotspot 0,								 0,										zDoorPos - _unitHeight/2,		unID, doorAngle, 6		: unID = unID+1
	hotspot 0,								 0,										zDoorPos - _unitHeight/2 + 1,	unID, doorAngle, 7		: unID = unID+1

	hotspot sin(doorAngle) * rotDoorRadius, -cos(doorAngle) * rotDoorRadius, 0,			unID, zDoorPos, 1 + 128: unID = unID+1
	hotspot sin(doorAngle) * rotDoorRadius, -cos(doorAngle) * rotDoorRadius, zDoorPos,	unID, zDoorPos, 2: unID = unID+1
	hotspot sin(doorAngle) * rotDoorRadius, -cos(doorAngle) * rotDoorRadius, -1,		unID, zDoorPos, 3: unID = unID+1
endif

if bTopDoor then
	rotTopDoorRadius = _flueFullRadius + clDoorProfileThk

	hotspot 0,										-_flueFullRadius - clDoorProfileThk,	zTopDoorPos - _unitHeight/2, unID, topDoorAngle, 4+128: unID = unID+1
	hotspot sin(topDoorAngle) * rotTopDoorRadius,	-cos(topDoorAngle) * rotTopDoorRadius,	zTopDoorPos - _unitHeight/2, unID, topDoorAngle, 5: unID = unID+1
	hotspot 0,										 0,										zTopDoorPos - _unitHeight/2, unID, topDoorAngle, 6: unID = unID+1
	hotspot 0,										 0,										zTopDoorPos - _unitHeight/2 + 1, unID, topDoorAngle, 7: unID = unID+1

	hotspot sin(topDoorAngle) * rotTopDoorRadius, -cos(topDoorAngle) * rotTopDoorRadius, 0,				unID, zTopDoorPos, 1 + 128: unID = unID+1
	hotspot sin(topDoorAngle) * rotTopDoorRadius, -cos(topDoorAngle) * rotTopDoorRadius, zTopDoorPos,	unID, zTopDoorPos, 2: unID = unID+1
	hotspot sin(topDoorAngle) * rotTopDoorRadius, -cos(topDoorAngle) * rotTopDoorRadius, -1,			unID, zTopDoorPos, 3: unID = unID+1
endif

if bShowMEP then
	rotRadius_1		= _flueFullRadius
	rotRadius_2		= _flueFullRadius
	rotRadius_3		= _flueFullRadius
	rotRadius_4		= _flueFullRadius
	rotRadius_5		= _flueFullRadius
	rotRadius_6		= _flueFullRadius
	rotRadius_7		= _flueFullRadius
	rotRadius_8		= _flueFullRadius
	rotRadius_9		= _flueFullRadius
	rotRadius_10	= _flueFullRadius

	unID = unIDBegin + 50
	if nSmokeCon > 0 then
		hotspot 0, -_flueFullRadius, zSmokePos[1] - _flueRadius, unID, smokeAngle_1, 4+128: unID = unID + 1
		hotspot sin(smokeAngle_1) * rotRadius_1, -cos(smokeAngle_1) * rotRadius_1, zSmokePos[1] - _flueRadius, unID, smokeAngle_1, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[1] - _flueRadius, unID, smokeAngle_1, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[1] - _flueRadius + 1, unID, smokeAngle_1, 7: unID = unID+1
	endif

	if nSmokeCon > 1 then
		hotspot 0, -_flueFullRadius, zSmokePos[2] - _flueRadius, unID, smokeAngle_2, 4+128: unID = unID+1
		hotspot sin(smokeAngle_2) * rotRadius_2, -cos(smokeAngle_2) * rotRadius_2, zSmokePos[2] - _flueRadius, unID, smokeAngle_2, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[2] - _flueRadius, unID, smokeAngle_2, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[2] - _flueRadius + 1, unID, smokeAngle_2, 7: unID = unID+1
	endif

	if nSmokeCon > 2 then
		hotspot 0, -_flueFullRadius, zSmokePos[3] - _flueRadius, unID, smokeAngle_3, 4+128: unID = unID+1
		hotspot sin(smokeAngle_3) * rotRadius_3, -cos(smokeAngle_3) * rotRadius_3, zSmokePos[3] - _flueRadius, unID, smokeAngle_3, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[3] - _flueRadius, unID, smokeAngle_3, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[3] - _flueRadius + 1, unID, smokeAngle_3, 7: unID = unID+1
	endif

	if nSmokeCon > 3 then
		hotspot 0, -_flueFullRadius, zSmokePos[4] - _flueRadius, unID, smokeAngle_4, 4+128: unID = unID+1
		hotspot sin(smokeAngle_4) * rotRadius_4, -cos(smokeAngle_4) * rotRadius_4, zSmokePos[4] - _flueRadius, unID, smokeAngle_4, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[4] - _flueRadius, unID, smokeAngle_4, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[4] - _flueRadius + 1, unID, smokeAngle_4, 7: unID = unID+1
	endif

	if nSmokeCon > 4 then
		hotspot 0, -_flueFullRadius, zSmokePos[5] - _flueRadius, unID, smokeAngle_5, 4+128: unID = unID+1
		hotspot sin(smokeAngle_5) * rotRadius_5, -cos(smokeAngle_5) * rotRadius_5, zSmokePos[5] - _flueRadius, unID, smokeAngle_5, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[5] - _flueRadius, unID, smokeAngle_5, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[5] - _flueRadius + 1, unID, smokeAngle_5, 7: unID = unID+1
	endif

	if nSmokeCon > 5 then
		hotspot 0, -_flueFullRadius, zSmokePos[6] - _flueRadius, unID, smokeAngle_6, 4+128: unID = unID+1
		hotspot sin(smokeAngle_6) * rotRadius_6, -cos(smokeAngle_6) * rotRadius_6, zSmokePos[6] - _flueRadius, unID, smokeAngle_6, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[6] - _flueRadius, unID, smokeAngle_6, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[6] - _flueRadius + 1, unID, smokeAngle_6, 7: unID = unID+1
	endif

	if nSmokeCon > 6 then
		hotspot 0, -_flueFullRadius, zSmokePos[7] - _flueRadius, unID, smokeAngle_7, 4+128: unID = unID+1
		hotspot sin(smokeAngle_7) * rotRadius_7, -cos(smokeAngle_7) * rotRadius_7, zSmokePos[7] - _flueRadius, unID, smokeAngle_7, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[7] - _flueRadius, unID, smokeAngle_7, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[7] - _flueRadius + 1, unID, smokeAngle_7, 7: unID = unID+1
	endif

	if nSmokeCon > 7 then
		hotspot 0, -_flueFullRadius, zSmokePos[8] - _flueRadius, unID, smokeAngle_8, 4+128: unID = unID+1
		hotspot sin(smokeAngle_8) * rotRadius_8, -cos(smokeAngle_8) * rotRadius_8, zSmokePos[8] - _flueRadius, unID, smokeAngle_8, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[8] - _flueRadius, unID, smokeAngle_8, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[8] - _flueRadius + 1, unID, smokeAngle_8, 7: unID = unID+1
	endif

	if nSmokeCon > 8 then
		hotspot 0, -_flueFullRadius, zSmokePos[9] - _flueRadius, unID, smokeAngle_9, 4+128: unID = unID+1
		hotspot sin(smokeAngle_9) * rotRadius_9, -cos(smokeAngle_9) * rotRadius_9, zSmokePos[9] - _flueRadius, unID, smokeAngle_9, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[9] - _flueRadius, unID, smokeAngle_9, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[9] - _flueRadius + 1, unID, smokeAngle_9, 7: unID = unID+1
	endif

	if nSmokeCon > 9 then
		hotspot 0, -_flueFullRadius, zSmokePos[10] - _flueRadius, unID, smokeAngle_10, 4+128: unID = unID+1
		hotspot sin(smokeAngle_10) * rotRadius_10, -cos(smokeAngle_10) * rotRadius_10, zSmokePos[10] - _flueRadius, unID, smokeAngle_10, 5: unID = unID+1
		hotspot 0, 0, zSmokePos[10] - _flueRadius, unID, smokeAngle_10, 6: unID = unID+1
		hotspot 0, 0, zSmokePos[10] - _flueRadius + 1, unID, smokeAngle_10, 7: unID = unID+1
	endif
endif


! ==============================================================================
! Chimney 3D
! ==============================================================================

gosub "flueCutting"

if bBottomDoor then
	doorCuttingDepth = _flueFullRadius
	if abs(doorAngle) < eps then
		add 0, -_flueFullRadius, zDoorPos - clDoorHeight/2
	endif
	if abs(doorAngle - 90) < eps then
		add _flueFullRadius, 0, zDoorPos - clDoorHeight/2
	endif
	if abs(doorAngle - 180) < eps then
		add 0, _flueFullRadius, zDoorPos - clDoorHeight/2
	endif
	if abs(doorAngle - 270) < eps then
		add -_flueFullRadius, 0, zDoorPos - clDoorHeight/2
	endif
	rotz doorAngle
	condensWater = 1
	gosub "cleaningDoor"
	del 2
endif

if bTopDoor then
	doorCuttingDepth = _flueFullRadius
	if abs(topDoorAngle) < eps then
		add 0, -_flueFullRadius, zTopDoorPos - clDoorHeight/2
	endif
	if abs(topDoorAngle - 90) < eps then
		add _flueFullRadius, 0, zTopDoorPos - clDoorHeight/2
	endif
	if abs(topDoorAngle - 180) < eps then
		add 0, _flueFullRadius, zTopDoorPos - clDoorHeight/2
	endif
	if abs(topDoorAngle - 270) < eps then
		add -_flueFullRadius, 0, zTopDoorPos - clDoorHeight/2
	endif
	rotz topDoorAngle
	condensWater = 0
	gosub "cleaningDoor"
	del 2
endif

if bShowMEP then
	for iSmokeCon = 1 to nSmokeCon
		add 0, 0, zSmokePos[iSmokeCon]
		rotz smokeAngle[iSmokeCon]
		gosub "smokeConnection"
		del 2
	next iSmokeCon
endif

prism_ 2, blockHeight,
	0, 0, 915+64,
	_flueRadius + flueThickness, 360, 4015+64

if bBottomDoor then
	cutend
	cutend
endif

if bTopDoor then
	cutend
endif

cutend


! ==============================================================================
! Cap
! ==============================================================================

if bShowCap then
	material matCap
	capHeight = 0.03
	capOverhang = 0.01

	gosub "flueCutting"

	add 0, 0, ZZYZX - coneHeight
	cone coneHeight, _flueFullRadius + 0.02, _flueRadius + 0.02, 90, 90
	del 1

	cutend
endif


end


"flueCutting":

	addz flueThickness

	cutform 2, 1, 1 + 16 + 64 + 128,
		0, 0, -1, 0,
		0,				0,		915 + 16  + 64,
		_flueRadius,	360,	4015 + 16 + 64

	del 1

return


"cleaningDoor":

	material matDoor

	tube 7, 8, 1 + 2,
		0, 0, 0,
		_flueFullRadius, 0, 0,
		_flueFullRadius, clDoorProfileThk, 0,
		-clDoorProfileThk, clDoorProfileThk, 0,
		-clDoorProfileThk, -3 * clDoorProfileThk, 0,
		0, -3 * clDoorProfileThk, 0,
		0, 0, -1,
	
		-clDoorWidth1/2, 0, 0, 0,
		clDoorWidth1/2, 0, 0, 0,
		clDoorWidth1/2, 0, clDoorHeight, 0,
		-clDoorWidth1/2, 0, clDoorHeight, 0,
		-clDoorWidth1/2, 0, 0, 0,
		clDoorWidth1/2, 0, 0, 0,
		clDoorWidth1/2, 0, clDoorHeight, 0,
		-clDoorWidth1/2, 0, clDoorHeight, 0
	
	add 0, -clDoorProfileThk, clDoorProfileThk + 0.001
	prism_ 5, clDoorHeight - 2 * (clDoorProfileThk + 0.001),
		-clDoorWidth1/2 + (clDoorProfileThk + 0.001), 0, 15,
		clDoorWidth1/2 - (clDoorProfileThk + 0.001), 0, 15,
		clDoorWidth1/2 - (clDoorProfileThk + 0.001), clDoorProfileThk, 15,
		-clDoorWidth1/2 + (clDoorProfileThk + 0.001), clDoorProfileThk, 15,
		-clDoorWidth1/2 + (clDoorProfileThk + 0.001), 0, -1
	del 1
	
	material matFlue
	rotx 90

	cutform 4, 1, 1 + 64 + 128,
		0, 0, -1, doorCuttingDepth,
		-clDoorWidth1/2, 0, 7 + 16,
		clDoorWidth1/2, 0, 7 + 16,
		clDoorWidth1/2, clDoorHeight, 7 + 16,
		-clDoorWidth1/2, clDoorHeight, 7 + 16
	del 1

	if condensWater then
		condensConRadius = 0.015
		addz -3 * condensConRadius
		rotx 90
		material matDoor

		prism_ 4, 2 * condensConRadius,
			0, 0, 915 + 64,
			1.2 * condensConRadius, 360, 4015 + 64,
	
			0, 0, 915 + 64,
			condensConRadius, 360, 4015 + 64
	
		material matFlue
		cutform 2, 1, 1 + 16 + 64 + 128,
			0, 0, -1, doorCuttingDepth,
			0, 0, 915 + 16  + 64,
			condensConRadius, 360, 4015 + 16 + 64
		del 2
		resol gs_resol
	endif

return


"smokeConnection":

	cutform 2, 1, 1 + 8 + 16 + 64 + 128,
		0, 0, -1, 0,
		0,					0,		915 + 16  + 64,
		_flueFullRadius,	360,	4015 + 16 + 64

	rotx 90
	prism_ 4, _flueFullRadius,
		0, 0, 915+64,
		_flueFullRadius, 360, 4015+64,
		0, 0, 915+64,
		_flueRadius,	360,	4015 + 16 + 64

	del 1
	cutend

return
