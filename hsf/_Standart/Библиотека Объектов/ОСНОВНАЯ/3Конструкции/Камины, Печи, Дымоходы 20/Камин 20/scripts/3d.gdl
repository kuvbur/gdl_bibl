
if gs_detlevel_3D_m = 0 then end
if gs_shadow then SHADOW ON
if not(gs_shadow) then SHADOW OFF

pen gs_cont_pen
add a/2,b,0

material matToe
	prism 4,theight,
	-a/2,0,
	a/2,0,
	a/2,-b,
	-a/2,-b

material matWall

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

addz theight

gosub 3000 + iStyle
gosub "Hotspots"

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Fireplace 01
! -----------------------------------------------------------------------------

3001:
	prism_ 12,zzyzx-theight-topHeight,
	-a/2*0.8,0,15,
	a/2*0.8,0,15,
	a/2*0.8,-b*0.8,15,
	a*0.2,-b*0.8,15,
	a*0.2,-b*0.7,15,
	a/2*0.75,-b*0.7,15,
	a/2*0.75,-0.05,15,
	-a/2*0.75,-0.05,15,
	-a/2*0.75,-b*0.7,15,
	-a*0.2,-b*0.7,15,
	-a*0.2,-b*0.8,15,
	-a/2*0.8,-b*0.8,15


	material matToe

	addz zzyzx-theight-topHeight-0.14

	if bBeam then
		prism 4,0.14,
		a*0.2,-b*0.7,
		a*0.2,-b*0.8,
		-a*0.2,-b*0.8,
		-a*0.2,-b*0.7
	endif

	glh = min (zzyzx-theight-topHeight-0.35,0.4)

	! --- Top ---
	material matTop
	addz 0.14
		prism 4,topHeight,
		-a/2*0.9,0,
		a/2*0.9,0,
		a/2*0.9,-b*0.9,
		-a/2*0.9,-b*0.9

	! --- Chimney ---
	addz topHeight


	if abs(flueHeight) > EPS then
	  material chimmat
	  ruled 4,1+2+4+16+32,
			-a*0.3,0,0,
			a*0.3,0,0,
			a*0.3,-b*0.7,0,
			-a*0.3,-b*0.7,0,

			-a*0.3+flueHeight/2,0,flueHeight,
			a*0.3-flueHeight/2,0,flueHeight,
			a*0.3-flueHeight/2,-b*0.2,flueHeight,
			-a*0.3+flueHeight/2,-b*0.2,flueHeight
	endif

	del 4

	! --- Glass plate ---

	material postmat
	resol 8

	add a*0.15,-b*0.8,theight
	cylind glh,0.01
	del 1

	add -a*0.15,-b*0.8,theight
	cylind glh,0.01
	del 1

	body -1
	material matGlassPlate
	resol 36

	add 0,-b*0.8+0.005,theight
	rotx 90
	prism_ 5,0.005,
		-a*0.13,0,15,
		a*0.13,0,15,
		a*0.13,glh,15,
		0,0,915,
		-a*0.13,glh,3015
	del 2

	! --- fire ---

	gosub "Fire1"

	if iFireType = 2 then		! --- Image
		define material "pictmat" 0,
			1, 1, 1,	    ! --- surface RGB
			1, 1, 0, 0.2,	! --- ambient, diffuse, specular, transparent
			0, 0,
			1, 1, 0,	! --- specular color RGB
			0, 0, 0,	! --- emission color RGB
			0

		material "pictmat"

		pictStartX	= -a*0.2
		pictX		= 2*abs(pictStartX)

		rotx 90
		add pictStartX, theight, b*0.7

		! --- Main picture ---
		picture "fire_1", pictX, zzyzx-theight-topHeight-bBeam*0.14, 0

		! --- Back (Wood) picture ---
		addz -0.01
		picture "fire_1_a", pictX, zzyzx-theight-topHeight-bBeam*0.14, 1
		del 3
	endif


	if b < 0.6 then del 1

	del top

return


! -----------------------------------------------------------------------------
! Fireplace 02
! -----------------------------------------------------------------------------

3002:
	prism_ 8,zzyzx-theight-topHeight,
		-a/2*0.8,0,15,
		a/2*0.8,0,15,
		a/2*0.8,-b*0.8,15,
		a/2*0.6,-b*0.8,15,
		a/2*0.6,-b*0.1,15,
		-a/2*0.6,-b*0.1,15,
		-a/2*0.6,-b*0.8,15,
		-a/2*0.8,-b*0.8,15

	glh = min(zzyzx-theight-topHeight-0.35,0.4)

	! --- Top ---
	addz zzyzx-theight-topHeight
	material matTop
	prism 4,topHeight/2,
		-a/2*0.9,0,
		a/2*0.9,0,
		a/2*0.9,-b*0.9,
		-a/2*0.9,-b*0.9

	addz topHeight/2
	prism 4,topHeight/2,
		-a/2*0.93,0,
		a/2*0.93,0,
		a/2*0.93,-b*0.93,
		-a/2*0.93,-b*0.93
	del 1


	! --- Chimney ---

	addz topHeight
	if abs(flueHeight)  > EPS then
		material chimmat
		ruled 4,1+2+4+16+32,
			-a*0.3,0,0,
			a*0.3,0,0,
			a*0.3,-b*0.7,0,
			-a*0.3,-b*0.7,0,

			-a*0.3+flueHeight/2,0,flueHeight,
			a*0.3-flueHeight/2,0,flueHeight,
			a*0.3-flueHeight/2,-b*0.2,flueHeight,
			-a*0.3+flueHeight/2,-b*0.2,flueHeight
	endif

	del 3

	! --- Glass plate ---

	material postmat

	add a*0.25,-b*0.8,theight
	cylind 0.2,0.01
	del 1

	add -a*0.25,-b*0.8,theight
	cylind 0.2,0.01
	add 0,-0.01,+0.05
	brick a/2,0.02,0.02
	addz 0.1
	brick a/2,0.02,0.02
	del 3

	body -1
	material matGlassPlate

	add -a*0.3,-b*0.8+0.005,theight
	brick a*0.6,0.01,zzyzx-theight-topHeight
	del 1

	! --- fire ---

	gosub "Fire1"


	if iFireType = 2 then		!! Image
		define material "pictmat2" 0,
			1, 1, 1,	! surface RGB
			1, 0, 0, 0,	! ambient, diffuse, specular, transparent
			0, 0,
			0, 0, 0,	! specular color RGB
			0, 0, 0,	! emission color RGB
			0

		material "pictmat2"

		pictStartX	= -a/2*0.6
		pictX		= 2*abs(pictStartX)

		rotx 90
		add pictStartX, theight, b*0.8-0.015

		! --- Main picture ---
		picture "fire_1", pictX, zzyzx-theight-topHeight, 0

		! --- Back (Wood) picture ---
		addz -0.01
		picture "fire_1_a", pictX, zzyzx-theight-topHeight, 1
		del 3
	endif

	if b < 0.6 then del 1

	del top

return

! -----------------------------------------------------------------------------
! Fire 1
! -----------------------------------------------------------------------------

"Fire1":
	if iFireType = 1 then		! --- Model ---
		add 0,-b/2,theight
		rotx 90
		material flamemat

		if b < 0.6 then mulz b/0.6

		for i=1 to 3
			poly_    95,
					   -0.129043,     0.302607,      1,
					   -0.346118,     0.224719,    900,
						0.0,     -31.1873,   4001,
					   -0.120081,     0.178941,      1,
					   -0.3843,     0.379142,    900,
						0.0,      13.3511,   4001,
					   -0.080992,     0.245364,      1,
					   -0.189805,     0.289821,    900,
						0.0,      30.548,   4001,
					   -0.0735,     0.30684,      1,
						0.050842,     0.32425,    900,
						0.0,     -45.079,   4001,
					   -0.049287,     0.4,      1,
						0.23656,     0.390482,    900,
						0.0,      14.6556,   4001,
					   -0.042395,     0.327369,      1,
					   -0.281144,     0.273306,    900,
						0.0,     -26.1583,   4001,
					   -0.043014,     0.216578,      1,
					   -0.235291,     0.333978,    900,
						0.0,      15.1135,   4001,
					   -0.019055,     0.270772,      1,
					   -0.120953,     0.298418,    900,
						0.0,      29.2016,   4001,
					   -0.018517,     0.323999,      1,
						0.094462,     0.353887,    900,
						0.0,     -14.7193,   4001,
					   -0.022403,     0.353686,      1,
					   -0.079215,     0.275515,    900,
						0.0,     -21.5781,   4001,
						0.002364,     0.327314,      1,
					   -0.070116,     0.28175,    900,
						0.0,     -19.2236,   4001,
						0.013325,     0.300909,      1,
					   -0.23017,     0.246977,    900,
						0.0,    -6.602259,   4001,
						0.017911,     0.272555,      1,
						0.348755,     0.311202,    900,
						0.0,      10.4851,   4001,
						0.030468,     0.212994,      1,
					   -0.068765,     0.302066,    900,
						0.0,      57.3478,   4001,
						0.059771,     0.337559,      1,
						0.196422,     0.375604,    900,
						0.0,    -4.163671,   4001,
						0.057369,     0.347581,      1,
						0.102858,     0.356725,    900,
						0.0,     -13.9188,   4001,
						0.056505,     0.358791,      1,
						0.106785,     0.356181,    900,
						0.0,     -21.4352,   4001,
						0.060937,     0.376986,      1,
					   -0.199912,     0.210733,    900,
						0.0,     -37.0772,   4001,
						0.108432,     0.18611,      1,
						0.015841,     0.270529,    900,
						0.0,      47.1962,   4001,
						0.140692,     0.2811,      1,
						0.008912,     0.18327,    900,
						0.0,     -39.9676,   4001,
						0.172751,     0.173598,      1,
						0.373824,     0.164068,    900,
						0.0,      15.3334,   4001,
						0.177388,     0.120088,      1,
						0.094885,     0.096785,    900,
						0.0,     -79.9669,   4001,
						0.132204,     0.019603,      1,
						0.061295,     0.197184,    900,
						0.0,     -17.2592,   4001,
						0.076324,     0.006561,      1,
					   -0.132883,     1.993124,    900,
						0.0,    -2.197404,   4001,
						0.0,     0.0,      1,
					   -0.001792,     0.0,      1,
						0.006552,     0.783794,    900,
						0.0,    -9.709621,   4001,
					   -0.133863,     0.012635,      1,
					   -0.12247,     0.085503,    900,
						0.0,     -39.9421,   4001,
					   -0.177987,     0.03695,      1,
					   -0.101524,     0.095576,    900,
						0.0,     -46.9528,   4001,
					   -0.196561,     0.111436,      1,
					   -0.065986,     0.091885,    900,
						0.0,     -16.181,   4001,
					   -0.18594,     0.147049,      1,
						0.096429,     0.014348,    900,
						0.0,    -7.542846,   4001,
					   -0.166077,     0.182967,      1,
					   -0.359028,     0.303356,    900,
						0.0,      22.9183,   4001,
					   -0.134427,     0.267609,      1,
						 14.2607,    -1.929091,    900,
						0.0,    -0.139325,   4001,
					   -0.129043,     0.302607,     -1

		roty 60
		next i

		del 5

		! --- woods ---

		add -a*0.25,-b*0.6,theight
		material woodmat
		block a*0.5,b*0.15,zzyzx*0.1
		addy b*0.18
		block a*0.5,b*0.15,zzyzx*0.1
		addz zzyzx*0.1
		rotz -10
		block a*0.5,b*0.15,zzyzx*0.1
		del 4
	endif
return

! -----------------------------------------------------------------------------
! Hotspots
! -----------------------------------------------------------------------------

"Hotspots":

	unID=1

	hotspot   0,0,0,unID : unID=unID+1
	hotspot   a,0,0,unID : unID=unID+1
	hotspot   a,b,0,unID : unID=unID+1
	hotspot   0,b,0,unID : unID=unID+1

	hotspot   a/2,b,0,unID : unID=unID+1
	hotspot   a/2,b,zzyzx,unID : unID=unID+1
	hotspot   a/2,b/2,0,unID : unID=unID+1
	hotspot   a/2,b/2,zzyzx,unID : unID=unID+1

	add a/2, b, 0
	ab = 0.90 + 0.03 * (iStyle = 2)
	hotspot -a/2*ab,-b*ab,zzyzx,unID : unID=unID+1
	hotspot a/2*ab,-b*ab,zzyzx,unID : unID=unID+1
	hotspot -a/2*ab,0,zzyzx,unID : unID=unID+1
	hotspot a/2*ab,0,zzyzx,unID : unID=unID+1

	addz zzyzx
	hotspot	-a*0.3+flueHeight/2,0,0,unID,flueHeight,1+128 : unID=unID+1
	hotspot	-a*0.3+flueHeight/2,0,-1,unID,flueHeight,3 : unID=unID+1
	hotspot	-a*0.3+flueHeight/2,0,flueHeight,unID,flueHeight,2 : unID=unID+1

	hotspot	a*0.3-flueHeight/2,0,0,unID,flueHeight,1+128 : unID=unID+1
	hotspot	a*0.3-flueHeight/2,0,-1,unID,flueHeight,3 : unID=unID+1
	hotspot	a*0.3-flueHeight/2,0,flueHeight,unID,flueHeight,2 : unID=unID+1
	del 2

return


