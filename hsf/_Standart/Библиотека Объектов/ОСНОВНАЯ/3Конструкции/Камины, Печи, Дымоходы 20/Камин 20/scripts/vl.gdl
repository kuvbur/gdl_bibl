
call "FM_types" parameters all

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------

values "gs_detlevel_3D" `Детальный`,`Откл.`
values "gs_detlevel_3D_m" 2, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D_m" then
	gs_detlevel_3D = `Детальный`
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	parameters gs_detlevel_3D = gs_detlevel_3D
else
	gs_detlevel_3D_m = 2
	if gs_detlevel_3D = `Откл.` then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
endif
if gs_detlevel_3D_m = 0 then
	hideparameter "gs_shadow"
	lock "gs_shadow"
endif

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle" 1, 2

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	parameters stStyle = stStyle
endif

! -----------------------------------------------------------------------------
! Default Sizes
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then
	a = defaultSizes[iStyle][1]
	b = defaultSizes[iStyle][2]
	zzyzx = defaultSizes[iStyle][3]
	parameters a = a, b = b, zzyzx = zzyzx
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if iStyle = 2 then hideparameter "bBeam"

if gs_cont_pen = 0 then parameters gs_cont_pen = 2
if gs_fill_pen = 0 then parameters gs_fill_pen = 91

values "flueHeight" range [0, a*0.3*2]


! -----------------------------------------------------------------------------
! Fire parameters
! -----------------------------------------------------------------------------

values "fireType" stFireType
values "iFireType" 0, 1, 2

if GLOB_MODPAR_NAME = "fireType" then
	iFireType = 0
	for i = 1 to vardim1(stFireType)
		if fireType = stFireType[i] then	iFireType = 3-i
	next i
	parameters iFireType = iFireType
else
	iFireType = max(min(iFireType,2),0)
	parameters fireType = stFireType[3-iFireType]
endif

if iFireType <> 1 then		! Not "Model"
	hideparameter "flamemat", "woodmat"
	lock "flamemat", "woodmat"
endif

! -----------------------------------------------------------------------------
! UI
! -----------------------------------------------------------------------------

values "gs_ui_current_page" 1

