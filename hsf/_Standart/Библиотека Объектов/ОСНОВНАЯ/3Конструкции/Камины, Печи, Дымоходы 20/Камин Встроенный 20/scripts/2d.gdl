
unID = 1
pen gs_cont_pen
fill gs_fill_type

drawindex 40
! ======================================================================
! BODY
! ======================================================================
if fpStyle_m = STYLE_5 then
	cornerX = fbWidth/sqr(2)
	poly2_b 6, 1+2+64,gs_fill_pen,gs_back_pen,
		0, 0, 1,
		A-cornerX, 0, 1,
		A, cornerX, 1,
		A, B, 1,
		0, B, 1,
		0, 0, -1

	hotspot2   0, B, unID, A, 1+256		: unID=unID+1
	hotspot2  -1, B, unID, A, 3 		: unID=unID+1
	hotspot2   A, B, unID, A, 2 		: unID=unID+1

	hotspot2  0, 0,  unID, B, 1+256		: unID=unID+1
	hotspot2  0, -1, unID, B, 3 		: unID=unID+1
	hotspot2  0, B,  unID, B, 2 		: unID=unID+1

	hotspot2  A, 0,  unID, B, 1+128		: unID=unID+1
	hotspot2  A, -1, unID, B, 3 		: unID=unID+1
	hotspot2  A, B,  unID, B, 2 		: unID=unID+1

	hotspot2  A-fbWidth/sqr(2), 0, unID, fbWidth, 1+128					: unID=unID+1
	hotspot2  A-fbWidth/sqr(2)-sqr(2), -sqr(2), unID, fbWidth, 3 		: unID=unID+1
	hotspot2  A, fbWidth/sqr(2), unID, fbWidth,2 						: unID=unID+1

	hotspot2  A, fbWidth/sqr(2),  unID, fbWidth, 1+128					: unID=unID+1
	hotspot2  A+sqr(2), fbWidth/sqr(2)+sqr(2), unID, fbWidth, 3 		: unID=unID+1
	hotspot2  A-fbWidth/sqr(2), 0, unID, fbWidth,2 						: unID=unID+1

else
	CALL "Resize_A_B_ZZYZX" Parameters ALL
	poly2_b 5, 1+2+64,gs_fill_pen,gs_back_pen,
		0,0,1,
		A,0,1,
		A,B,1,
		0,B,1,
		0,0,-1
endif


! ======================================================================
! FLUE
! ======================================================================
if bFlue and bShowFlue2D then
	drawindex 50
	add2 A/2, B/2
	flue_diameter = flue_diameter/2

	fill fillTypeFlue
	poly2_b 4, 1+2+64,gs_cont_pen,gs_back_pen,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,1,
		0,0,900,
		0,-180,4001,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,-1

	poly2_b 4, 1+2+64,gs_back_pen,gs_back_pen,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,1,
		0,0,900,
		0,180,4001,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,-1

	circle2 0, 0, flue_diameter

	hotspot2  0, 0, unID : unID = unID + 1
	del 1
	fill gs_fill_type
else
	unID = unID + 1
endif

if gs_detlevel_2D_m < 2 then end

! ======================================================================
! CORNICE
! ======================================================================
if bCornice then
	drawindex 30
	overHang = corniceOverh
	height	 = corniceHeight
	offset	 = corniceOffset
	gosub "Details"
	if fpStyle_m = STYLE_2 then
		mul2 1,-1
		add2 0, -B
		gosub "Details"
		del 2
	endif
endif

! ======================================================================
! FLUSH
! ======================================================================
if bFlush then
	drawindex 20
	overHang = flushOverh
	height	 = flushHeight
	offset	 = flushOffset
	gosub "Details"
	if fpStyle_m = STYLE_2 then
		mul2 1,-1
		add2 0, -B
		gosub "Details"
		del 2
	endif
endif


! ======================================================================
! SOCLE
! ======================================================================
if bSocle and socleHeight > EPS then
	drawindex 10
	overHang = socleOverh
	height	 = socleHeight
	offset	 = socleOffset
	gosub "Details"
	if fpStyle_m = STYLE_2 then
		mul2 1,-1
		add2 0, -B
		gosub "Details"
		del 2
	endif
endif



END

! ======================================================================
! SubRoutines
! ======================================================================

"Details":
	if fpStyle_m = STYLE_1 or fpStyle_m = STYLE_2 then
		poly2_b 5, 1+2+64,gs_fill_pen,gs_back_pen,
			fpWidthSide-overHang, 0, 1,
			fpWidthSide-overHang, -offset, 1,
			fpWidthSide+fbWidth+overHang, -offset, 1,
			fpWidthSide+fbWidth+overHang, 0, 1,
			fpWidthSide-overHang, 0, -1

		hotspot2  fpWidthSide-overHang, -offset, unID 					: unID=unID+1
		hotspot2  fpWidthSide+fbWidth+overHang, -offset, unID 			: unID=unID+1
	endif

	if fpStyle_m = STYLE_3 then
		poly2_b 9, 1+2+64,gs_fill_pen,gs_back_pen,
			0, fbOpenWidthSide+overHang, 1,
			0, 0, 1,
			A, 0, 1,
			A, fbOpenWidthSide+overHang, 1,
			A+offset, fbOpenWidthSide+overHang, 1,
			A+offset, -offset, 1,
			-offset, -offset, 1,
			-offset, fbOpenWidthSide+overHang, 1,
			0, fbOpenWidthSide+overHang, -1

		hotspot2  -offset, fbOpenWidthSide+overHang, unID 				: unID=unID+1
		hotspot2  A+offset, fbOpenWidthSide+overHang, unID				: unID=unID+1
		hotspot2  -offset, -offset, unID 								: unID=unID+1
		hotspot2  A+offset, -offset, unID								: unID=unID+1
	endif

	if fpStyle_m = STYLE_4 then
		poly2_b 7, 1+2+64,gs_fill_pen,gs_back_pen,
			A-fbWidth-minWall-overHang, -offset, 1,
			A-fbWidth-minWall-overHang, 0, 1,
			A, 0, 1,
			A, fbOpenWidthSide+overHang, 1,
			A+offset, fbOpenWidthSide+overHang, 1,
			A+offset, -offset, 1,
			A-fbWidth-minWall-overHang, -offset, -1

		hotspot2  A-minWall-fbWidth-overHang, -offset, unID 			: unID=unID+1
		hotspot2  A+offset, fbOpenWidthSide+overHang, unID 				: unID=unID+1
		hotspot2  A+offset, -offset, unID 								: unID=unID+1
	endif

	if fpStyle_m = STYLE_5 then
		poly2_b 9, 1+2+64,gs_fill_pen,gs_back_pen,
			A-cornerX-fbOpenWidthSide-overHang, -offset, 1,
			A-cornerX-fbOpenWidthSide-overHang, 0, 1,
			A-cornerX, 0, 1,
			A, cornerX, 1,
			A, cornerX+fbOpenWidthSide+overHang, 1,
			A+offset, cornerX+fbOpenWidthSide+overHang, 1,
			A+offset, cornerX-tan(halfAlfa)*(offset), 1,
			A-cornerX+tan(halfAlfa)*(offset), -offset, 1,
			A-cornerX-fbOpenWidthSide-overHang, -offset, -1

		hotspot2  A-cornerX-fbOpenWidthSide-overHang, -offset, unID 	: unID=unID+1
		hotspot2  A+offset, cornerX+fbOpenWidthSide+overHang, unID		: unID=unID+1
		hotspot2  A-cornerX+tan(halfAlfa)*(offset), -offset, unID 		: unID=unID+1
		hotspot2  A+offset, cornerX-tan(halfAlfa)*(offset), unID		: unID=unID+1
	endif
return
