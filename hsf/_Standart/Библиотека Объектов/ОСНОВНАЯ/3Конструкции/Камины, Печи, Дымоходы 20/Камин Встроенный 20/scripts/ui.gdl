
TABID_ROOT				= -1
TABID_GEOMETRY			= 1
TABID_DETAILS			= 2
TABID_REPRESENTATION	= 3
TABID_SURFACES			= 4

dim stFireplaceSettingsUI[27]
	stFireplaceSettingsUI[1] = `Параметры Камина`
	stFireplaceSettingsUI[2] = `2D-отображение`
	stFireplaceSettingsUI[3] = `2D-детализация`
	stFireplaceSettingsUI[4] = `Перо Контура`
	stFireplaceSettingsUI[5] = `Тип Штриховки`
	stFireplaceSettingsUI[6] = `Перо Штриховки`
	stFireplaceSettingsUI[7] = `Перо Фона Штриховки`
	stFireplaceSettingsUI[8] = `3D-отображение`
	stFireplaceSettingsUI[9] = `3D-детализация`
	stFireplaceSettingsUI[10] = `Разрешение`
	stFireplaceSettingsUI[11] = `Отбрасывание Теней`
	stFireplaceSettingsUI[12] = `Показать Огонь в 3D`
	stFireplaceSettingsUI[13] = `Камин`
	stFireplaceSettingsUI[14] = `Обрамление Топки`
	stFireplaceSettingsUI[15] = `Остекление Топки`
	stFireplaceSettingsUI[16] = `Дымоход`
	stFireplaceSettingsUI[17] = `Карниз`
	stFireplaceSettingsUI[18] = `Под`
	stFireplaceSettingsUI[19] = `Цоколь`
	stFireplaceSettingsUI[20] = `Стиль Камина`
	stFireplaceSettingsUI[21] = `План Камина`
	stFireplaceSettingsUI[22] = `Высота`
	stFireplaceSettingsUI[23] = `Диаметр`
	stFireplaceSettingsUI[24] = `Детали`
	stFireplaceSettingsUI[25] = `Свес и Высота`
	stFireplaceSettingsUI[26] = `Смещение и Отступ`
	stFireplaceSettingsUI[27] = `Толщина Стенки`

dim stToolTips[19]
	stToolTips[1] = `Ширина Камина`
	stToolTips[2] = `Глубина Камина`
	stToolTips[3] = `Высота Камина`
	stToolTips[4] = `Ширина Камина Сбоку`
	stToolTips[5] = `Ширина Портала`
	stToolTips[6] = `Глубина Топки`
	stToolTips[7] = `Ширина Портала Сбоку`
	stToolTips[8] = `Отступ Остекления Топки`
	stToolTips[9] = `Выступ Карниза`
	stToolTips[10] = `Высота Карниза`
	stToolTips[11] = `Смещение Карниза`
	stToolTips[12] = `Выступ Пода`
	stToolTips[13] = `Высота Пода`
	stToolTips[14] = `Смещение Пода`
	stToolTips[15] = `Свес Цоколя`
	stToolTips[16] = `Высота Цоколя`
	stToolTips[17] = `Смещение Цоколя`
	stToolTips[18] = `Высота Портала`
	stToolTips[19] = `Высота Топки`



! ======================================================================


ui_current_page gs_ui_current_page



ui_dialog stFireplaceSettingsUI[1],444,266

if TABID_GEOMETRY > 0 then
	ui_page TABID_GEOMETRY, TABID_ROOT, `Геометрия Камина`, "uiTab_Dimension_flat(1)"
	gosub "geometryPage"
endif

if TABID_DETAILS > 0 then
	ui_page TABID_DETAILS, TABID_ROOT, `Детали Камина`, "uiTab_Fireplace_flat(1)"
	gosub "detailPage"
endif

if TABID_REPRESENTATION > 0 then
	ui_page TABID_REPRESENTATION, TABID_ROOT, `Отображение`, "uiTab_2DRepr_flat(1)"
	gosub "representationPage"
endif

if TABID_SURFACES > 0 then
	ui_page TABID_SURFACES , TABID_ROOT, `Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "surfacePage"
endif

! ======================================================================
end
! ======================================================================

"geometryPage":
	! ======================================================================
	! Page 1
	! ======================================================================
	
	pageStartY = 5	
	x1		= 5		! start Position X Coordinate
	yStart	= pageStartY   	
	yStart2	= pageStartY + 57
	xHalfLevel 	= 280
	xEndLevel	= 440
	PictStartX	= 120
	PictStartY	= pageStartY + 115
	
	hRow		= 18
	hPictBox	= 41
	hCheckBox	= 24
	lineSpace	= 28
	
	wDescript1	= 105
	wBox1		= 80
	wBox2		= 60
	wDescript2	= (xEndLevel - wBox2) - (xHalfLevel + 10) - 1
	wPictBox	= 275
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[20],	x1,		yStart+5,	wDescript1,	hRow										! Fireplace Style
	ui_style 0, 0
	ui_infield{3} "fpStyle_m",x1 + wDescript1 +1,	yStart,	wPictBox, hPictBox,
			4, 10, 5, 1, 55, 41, 53, 41,																				! ui_fp_type.png
				1, stStyle[1], 1,
				2, stStyle[2], 2,
				3, stStyle[3], 3,
				4, stStyle[4], 4,
				5, stStyle[5], 5 ui_tooltip stFireplaceSettingsUI[20]
	y1 = yStart + hPictBox + 5
	
	ui_separator 1, y1, 444, y1
	ui_separator xHalfLevel-2, y1+5, xHalfLevel-2, pageStartY + 231
	
	
	! ------ 1. Column - Type -------
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[21],	x1, yStart2,	wDescript1,	hRow											! Fireplace Plan
	ui_style 0, 0
	
	iPict = 10 + fpStyle_m
	if fpStyle_m = STYLE_1 or fpStyle_m = STYLE_2 then
		ui_pict iPict, PictStartX, PictStartY, 98, 77 , 1	!ui_fp_plan_1.png ui_fp_plan_2.png							! iPict (png)
		ui_infield "A", PictStartX+8,	PictStartY-24,	wBox1,	hRow  ui_tooltip stToolTips[1]							! Fireplace Width
		ui_infield "B", PictStartX-wBox1-4,	PictStartY+30,	wBox1,	hRow  ui_tooltip stToolTips[2]						! Fireplace Depth
		ui_infield "fpWidthSide", PictStartX-wBox1+25,	PictStartY+80,	wBox1,	hRow  ui_tooltip stToolTips[4]			! Fireplace Width Side
		ui_infield "fbWidth", PictStartX+32,	PictStartY+80,	wBox1,	hRow  ui_tooltip stToolTips[5]					! Firebox Width
	endif
	
	if fpStyle_m = STYLE_3 or fpStyle_m = STYLE_4 then
		ui_pict iPict, PictStartX-30, PictStartY, 98, 77 , 1	!ui_fp_plan_3.png ui_fp_plan_4.png						! iPict (png)
		ui_infield "A", PictStartX+8-30,	PictStartY-24,	wBox1,	hRow  ui_tooltip stToolTips[1]						! Fireplace Width
		ui_infield "B", PictStartX-wBox1-4-30,	PictStartY+30,	wBox1,	hRow  ui_tooltip stToolTips[2]					! Fireplace Depth
		ui_infield "fbOpenWidthSide", PictStartX+102-30,	PictStartY+42,	wBox1,	hRow  ui_tooltip stToolTips[7]		! Firebox Opening Width Side
		if fpStyle_m = STYLE_4 then
				ui_infield "fbWidth", PictStartX+30-30,	PictStartY+82,	wBox1,	hRow  ui_tooltip stToolTips[5]			! Firebox Width
		endif
	endif
	
	if fpStyle_m = STYLE_5 then
		ui_pict iPict, PictStartX-30, PictStartY, 100, 100 , 1	!ui_fp_plan_5.png										! iPict (png)
		ui_infield "A", PictStartX+8-30,	PictStartY-24,	wBox1,	hRow  ui_tooltip stToolTips[1]						! Fireplace Width
		ui_infield "B", PictStartX-wBox1-4-30,	PictStartY+45,	wBox1,	hRow  ui_tooltip stToolTips[2]					! Fireplace Depth
		ui_infield "fbOpenWidthSide", PictStartX+102-30,	PictStartY+42,	wBox1,	hRow  ui_tooltip stToolTips[7]		! Firebox Opening Width Side
		ui_infield "fbWidth", PictStartX+102-30,	PictStartY+75,	wBox1,	hRow  ui_tooltip stToolTips[5]				! Firebox Width
	endif
	
	
	! ------ 2. Column - Flue -------
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[16],	xHalfLevel+10, yStart2,	wDescript1,	hRow									! Flue
	ui_style 0, 0
	
	y1 = yStart2 + lineSpace
	ui_infield{3} "bFlue", xHalfLevel + 10, y1, wDescript1, hRow,														! Flue
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stFireplaceSettingsUI[16], 0,
		1, stFireplaceSettingsUI[16], 1	ui_tooltip stFireplaceSettingsUI[16]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bFlue)
	ui_outfield stFireplaceSettingsUI[22], xHalfLevel + 10,	y1,	wDescript2,	hRow, flag									! Height
	ui_infield "flue_height", xEndLevel - wBox2, y1-2, wBox2, hRow  ui_tooltip stFireplaceSettingsUI[22]
	y1 = y1 + lineSpace
	
	flag = 4 * (not(bFlue) or flue_height<EPS)
	ui_outfield stFireplaceSettingsUI[23], xHalfLevel + 10,	y1,	wDescript2,	hRow, flag									! Diameter
	ui_infield "flue_diameter", xEndLevel - wBox2, y1-2, wBox2, hRow  ui_tooltip stFireplaceSettingsUI[23]
	y1 = y1 + lineSpace
	
	flag = 4 * (not(bFlue) or flue_height<EPS)
	ui_outfield stFireplaceSettingsUI[27], xHalfLevel + 10,	y1,	wDescript2,	hRow, flag									! Diameter
	ui_infield "flue_wallthk", xEndLevel - wBox2, y1-2,	wBox2, hRow  ui_tooltip stFireplaceSettingsUI[27]
	y1 = y1 + lineSpace
return	

"detailPage":
	! ======================================================================
	! Page 2
	! ======================================================================
	
	pageStartY = 5
	x1		= 5
	x2		= 100
	yStart	= pageStartY 
	yStart2	= pageStartY + 45
	xHalfLevel 	= 292
	xEndLevel	= 440
	
	hRow		= 18
	hCheckBox	= 24
	lineSpace	= 28
	
	wDescript1	= 105
	wDescript2	= 80
	wDescript3	= 140
	wBox1		= 75
	wCheckBox	= 20
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[24],	x1,		yStart+5,	wDescript2,	hRow											! Details
	ui_style 0, 0
	ui_outfield stFireplaceSettingsUI[17],	x2+wCheckBox+1,		yStart+5,	wDescript2,	hRow								! Cornice
	ui_infield "bCornice",	x2,	yStart,	wCheckBox, hCheckBox ui_tooltip stFireplaceSettingsUI[18]
	ui_outfield stFireplaceSettingsUI[18],	x2+120+wCheckBox+1,		yStart+5,	wDescript2,	hRow							! Flush Hearth
	ui_infield "bFlush",	x2+120,	yStart,	wCheckBox, hCheckBox ui_tooltip stFireplaceSettingsUI[18]
	flag = 4 * (socleHeight <= EPS)
	ui_outfield stFireplaceSettingsUI[19],	x2+240+wCheckBox+1,		yStart+5,	wDescript2,	hRow, flag						! Socle
	ui_infield "bSocle",	x2+240,	yStart,	wCheckBox, hCheckBox ui_tooltip stFireplaceSettingsUI[19]
	
	y1 = yStart + lineSpace +5
	
	ui_separator 1, y1, 444, y1
	ui_separator xHalfLevel-5, y1+5, xHalfLevel-5, pageStartY + 231
	
	! ------ 1. Column - Type -------
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[25],	x1, yStart2,	wDescript3,	hRow												! Overhang and Height
	ui_style 0, 0
	
	mask = 0
	mask = bCornice*1 + bFlush*2 + bSocle*4
	PictStartY	= pageStartY + 84
	
	if fpStyle_m = STYLE_1 or fpStyle_m = STYLE_2 then
		wPict = 119
		PictStartX	= 84
		iPict = 50 + mask
	endif
	if fpStyle_m = STYLE_3 or fpStyle_m = STYLE_4 then
		wPict = 99
		PictStartX	= 90
		iPict = 60 + mask
	endif
	if fpStyle_m = STYLE_5 then
		wPict = 101
		PictStartX	= 90
		iPict = 70 + mask
	endif
	
	ui_pict iPict, PictStartX, PictStartY, wPict, 135 , 1																! iPict (png)
	ui_infield "ZZYZX", PictStartX+wPict+1,	PictStartY-8,	wBox1,	hRow  ui_tooltip stToolTips[3]						! Fireplace Height
	ui_infield "fbOpenH", PictStartX-wBox1-1,	PictStartY+97+bFlush*23,	wBox1,	hRow  ui_tooltip stToolTips[18]		! Firebox Opening Height
	ui_infield "fbH", PictStartX-wBox1-1,	PictStartY+56,	wBox1,	hRow  ui_tooltip stToolTips[19]						! Firebox Height
	
	if bCornice then
		ui_infield "corniceOverh", PictStartX+wPict+1,	PictStartY+38,	wBox1,	hRow  ui_tooltip stToolTips[9]			! Cornice Overhang
		ui_infield "corniceHeight", PictStartX-wBox1-1,	PictStartY+28,	wBox1,	hRow  ui_tooltip stToolTips[10]			! Cornice Height
	endif
	if bFlush then
		ui_infield "flushOverh", PictStartX+wPict+1,	PictStartY+72,	wBox1,	hRow  ui_tooltip stToolTips[12]			! Flush Hearth Overhang
		ui_infield "flushHeight", PictStartX-wBox1-1,	PictStartY+93,	wBox1,	hRow  ui_tooltip stToolTips[10]			! Flush Hearth Height
	endif
	if bSocle then
		ui_infield "socleOverh", PictStartX+wPict+1,	PictStartY+122,	wBox1,	hRow  ui_tooltip stToolTips[15]			! Socle Overhang
	endif
	
	if fpStyle_m = STYLE_3 or fpStyle_m = STYLE_4 then
	endif
	
	if fpStyle_m = STYLE_5 then
	endif
	
	
	
	! ------ 2. Column - Section -------
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[26],	xHalfLevel+10, yStart2,	wDescript3,	hRow							! Offset and Setback
	ui_style 0, 0
	
	PictStartX	= xHalfLevel
	PictStartY	= pageStartY + 90
	if fpStyle_m = STYLE_2 then
		iPict = 30 + mask
	else
		iPict = 20 + mask
	endif
	
	ui_pict iPict, PictStartX, PictStartY, 70, 126, 1															! iPict (png)
	ui_infield "fbGlassBack", PictStartX+71,	PictStartY+40,	wBox1,	hRow  ui_tooltip stToolTips[8]			! Firebox Glass Set-Back
	if bCornice then
		ui_infield "corniceOffset", PictStartX+71,	PictStartY+13,	wBox1,	hRow  ui_tooltip stToolTips[11]		! Cornice Offset
	endif
	if bFlush then
		ui_infield "flushOffset", PictStartX+71,	PictStartY+66,	wBox1,	hRow  ui_tooltip stToolTips[14]		! Flush Offset
	endif
	if bSocle then
		ui_infield "socleOffset", PictStartX+71,	PictStartY+112,	wBox1,	hRow  ui_tooltip stToolTips[17]		! Socle Offset
	endif
return
	
"representationPage":
	! ======================================================================
	! Page 3
	! ======================================================================
	
	pageStartY 	= 5
	x1			= 5
	yStart		= pageStartY 
	xHalfLevel 	= 218
	xEndLevel	= 440
	
	hRow		= 18
	hPenBox		= 22
	hFillBox	= 24
	hMatBox		= 24
	lineSpace1	= 30
	lineSpace2	= 34
	
	wDescript1	= 150
	wDescript2	= 80
	wDescript3	= 65
	wDescript4	= 145
	wBox1		= 90
	wPenBox		= 35
	wPictBox	= 30
	wFillBox	= 130
	wMatBox		= 130
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[2],	x1,		yStart,	wDescript1,	hRow									! 2D Representation
	ui_style 0, 0
	y1 = yStart + lineSpace2
	
	ui_outfield stFireplaceSettingsUI[3], x1 , y1, wDescript2, hRow												! 2D Detail Level
	ui_infield{3} "gs_detlevel_2D_m", xHalfLevel - wBox1, y1,	wBox1, hRow,
			8, "", 4, 1, 0, 0, 0, 0,
			1, stDetlevel2D[1], 2,
			3, stDetlevel2D[2], 1   ui_tooltip stFireplaceSettingsUI[3]
	y1 = y1 + lineSpace1
	
	ui_pict 2, x1, y1-3, 24, 17 , 1 ui_tooltip stFireplaceSettingsUI[4]											! ui_contour_pen(1).png
	ui_outfield stFireplaceSettingsUI[4],	x1+wPictBox,	y1,	wDescript4,	hRow								! Contour Pen
	ui_infield "gs_cont_pen",	xHalfLevel - wPenBox, y1-3, wPenBox, hPenBox  ui_tooltip stFireplaceSettingsUI[4]
	y1 = y1 + lineSpace1
	
	ui_pict 4, x1, y1-3, 13, 17 , 1 ui_tooltip stFireplaceSettingsUI[5]											! ui_fill_type.png
	ui_outfield stFireplaceSettingsUI[5], x1+wPictBox, y1, wDescript2,	hRow									! Fill Type
	y1 = y1 + lineSpace1
	ui_infield "gs_fill_type",	x1+wPictBox, y1-6, xHalfLevel-x1-wPictBox, hFillBox  ui_tooltip stFireplaceSettingsUI[5]
	y1 = y1 + lineSpace1
	
	ui_pict 3, x1, y1-3, 24, 17 , 1 ui_tooltip stFireplaceSettingsUI[6]											! ui_fill_pen.png
	ui_outfield stFireplaceSettingsUI[6], x1+wPictBox, y1, wDescript4,	hRow									! Fill Pen
	ui_infield "gs_fill_pen",	xHalfLevel - wPenBox, y1-4, wPenBox, hPenBox  ui_tooltip stFireplaceSettingsUI[6]
	y1 = y1 + lineSpace1
	
	ui_pict 1, x1, y1-3, 24, 17 , 1 ui_tooltip stFireplaceSettingsUI[7]											! ui_back_pen(1).png
	ui_outfield stFireplaceSettingsUI[7], x1+wPictBox, y1, wDescript4,	hRow									! Fill Background Pen
	ui_infield "gs_back_pen",	xHalfLevel - wPenBox, y1-4, wPenBox, hPenBox  ui_tooltip stFireplaceSettingsUI[7]
	
	ui_separator xHalfLevel+ 5, yStart, xHalfLevel+5, pageStartY + 225
	
	! ------ 2. Column - 3D Representation -------
	
	ui_style 0,1
	ui_outfield stFireplaceSettingsUI[8], xHalfLevel + 10, yStart, wDescript1,	hRow							! 3D Representation
	ui_style 0,0
	y1 = yStart + lineSpace2
	
	ui_outfield stFireplaceSettingsUI[9], xHalfLevel + 10 , y1, wDescript2, hRow								! 3D Detail Level
	ui_infield{3} "gs_detlevel_3D_m", xEndLevel - wBox1, y1,	wBox1, hRow,
			8, "", 4, 1, 0, 0, 0, 0,
			1, stDetlevel3D[1], 2,
			2, stDetlevel3D[2], 1,
			3, stDetlevel3D[3], 0   ui_tooltip stFireplaceSettingsUI[9]
	y1 = y1 + lineSpace1
	
	iFlag = (gs_detlevel_3D_m < 2) * 4
	
	ui_outfield stFireplaceSettingsUI[10], xHalfLevel + 10,	y1,	wDescript2,	hRow, iFlag							! Resolution
	ui_infield "gs_resol", xEndLevel - wBox1,	y1,	wBox1,		hRow  ui_tooltip stFireplaceSettingsUI[10]
	y1 = y1 + lineSpace1
	
	ui_infield{3} "gs_shadow", xHalfLevel + 10, y1, wDescript1, hRow,											! Shadow
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stFireplaceSettingsUI[11], 0,
		1, stFireplaceSettingsUI[11], 1	ui_tooltip stFireplaceSettingsUI[11]
	y1 = y1 + lineSpace1
	
	ui_infield{3} "bFire", xHalfLevel + 10, y1, wDescript1, hRow,												! Show Fire in 3D
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stFireplaceSettingsUI[12], 0,
		1, stFireplaceSettingsUI[12], 1	ui_tooltip stFireplaceSettingsUI[12]
	y1 = y1 + lineSpace1
return
	
"surfacePage":
	! ======================================================================
	! Page 4
	! ======================================================================
	
	pageStartY 	= 5
	x1			= 5
	yStart		= pageStartY 
	xHalfLevel 	= 295
	xEndLevel	= 440
	
	hRow		= 18
	hMatBox		= 24
	lineSpace	= 32
	
	wDescript1	= 122
	wMatBox		= 150
	wPictBox	= 15
	
	y1 = yStart + 10
	ui_pict 5, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[13]										! ui_mat(1).png
	ui_outfield stFireplaceSettingsUI[13], x1 + wPictBox, y1, wDescript1,	hRow								! Fireplace
	ui_infield "mat_fp", xHalfLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[13]
	y1 = y1 + lineSpace
	
	ui_pict 5, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[14]										! ui_mat(1).png
	ui_outfield stFireplaceSettingsUI[14], x1 + wPictBox, y1, wDescript1,	hRow								! Firebox Frame
	ui_infield "mat_fb", xHalfLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[14]
	y1 = y1 + lineSpace
	
	ui_pict 5, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[15]										! ui_mat(1).png
	ui_outfield stFireplaceSettingsUI[15], x1 + wPictBox, y1, wDescript1,	hRow								! Firebox Glass
	ui_infield "mat_glass", xHalfLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[15]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bFlue)
	if bFlue then
		ui_pict 5, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[16]									! ui_mat(1).png
	else
		ui_pict 6, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[16]									! ui_mat_lock(1).png
	endif
	ui_outfield stFireplaceSettingsUI[16], x1 + wPictBox, y1, wDescript1,	hRow, flag  						! Flue
	ui_infield "mat_flue", xHalfLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[16]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bCornice)
	if bCornice then
		ui_pict 5, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[17]									! ui_mat(1).png
	else
		ui_pict 6, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[17]									! ui_mat_lock(1).png
	endif
	ui_outfield stFireplaceSettingsUI[17], x1 + wPictBox, y1, wDescript1,	hRow, flag  						! Cornice
	ui_infield "mat_corn", xHalfLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[17]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bFlush)
	if bFlush then
		ui_pict 5, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[18]									! ui_mat(1).png
	else
		ui_pict 6, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[18]									! ui_mat_lock(1).png
	endif
	ui_outfield stFireplaceSettingsUI[18], x1 + wPictBox, y1, wDescript1,	hRow, flag  						! Flush Hearth
	ui_infield "mat_flush", xHalfLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[18]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bSocle)
	if bSocle then
		ui_pict 5, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[19]									! ui_mat(1).png
	else
		ui_pict 6, x1, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[19]									! ui_mat_lock(1).png
	endif
	ui_outfield stFireplaceSettingsUI[19], x1 + wPictBox, y1, wDescript1,	hRow, flag  						! Socle
	ui_infield "mat_socle", xHalfLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[19]
	y1 = y1 + lineSpace
return
