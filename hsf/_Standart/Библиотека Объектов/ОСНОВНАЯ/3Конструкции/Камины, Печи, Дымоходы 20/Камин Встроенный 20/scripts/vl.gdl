
call "FM_types" parameters all

values "gs_ui_current_page" 1, 2, 3, 4
values "a" range [0.5, ]
values "b" range [0.35, ]
values "zzyzx" range [0.6, ]


! ----- Style Settings -----
values "fpStyle" stStyle
values "fpStyle_m" STYLE_1, STYLE_2, STYLE_3, STYLE_4, STYLE_5
values "tempStyle" STYLE_1, STYLE_2, STYLE_3, STYLE_4, STYLE_5
if GLOB_MODPAR_NAME = "fpStyle" then
	fpStyle_m = 1
	for i = 1 to vardim1(stStyle)
		if fpStyle = stStyle[i] then	fpStyle_m = i
	next i
	parameters fpStyle_m = fpStyle_m
else
	fpStyle_m = max(min(fpStyle_m,5),1)
	parameters fpStyle = stStyle[fpStyle_m]
endif

if GLOB_MODPAR_NAME = "fpStyle" or GLOB_MODPAR_NAME = "fpStyle_m" then
	if fpStyle_m <> tempStyle then			! Style Change
		if fpStyle_m = STYLE_5 then
			A = lengthDefaults[1][1]
			B = lengthDefaults[1][2]
			fbWidth = lengthDefaults[1][3]
			fbOpenWidthSide = lengthDefaults[1][4]
			parameters A=A, B=B, fbWidth=fbWidth, fbOpenWidthSide=fbOpenWidthSide
		endif
		if fpStyle_m = STYLE_4 then
			A = lengthDefaults[2][1]
			B = lengthDefaults[2][2]
			fbWidth = lengthDefaults[2][3]
			fbOpenWidthSide = lengthDefaults[2][4]
			parameters A=A, B=B, fbWidth=fbWidth, fbOpenWidthSide=fbOpenWidthSide
		endif
		if fpStyle_m = STYLE_3 then
			A = lengthDefaults[3][1]
			B = lengthDefaults[3][2]
			fbOpenWidthSide = lengthDefaults[3][4]
			parameters A=A, B=B, fbOpenWidthSide=fbOpenWidthSide
		endif
		if fpStyle_m = STYLE_2 or fpStyle_m = STYLE_1 and  (tempStyle<>STYLE_1 and tempStyle<>STYLE_2) then
			A = lengthDefaults[4][1]
			B = lengthDefaults[4][2]
			fbWidth = lengthDefaults[4][3]
			fpWidthSide = lengthDefaults[4][4]
			parameters A=A, B=B, fbWidth=fbWidth, fpWidthSide=fpWidthSide
		endif
		tempStyle = fpStyle_m
		parameters tempStyle = tempStyle
	endif
endif


if fpStyle_m = STYLE_1 then
	values "fbWidth" range [0.2, A-2*minWall]
	if fbWidth < B then
		fbDepth = fbWidth
		parameters fbDepth = fbDepth
	else
		fbDepth = B - minWall
		parameters fbDepth = fbDepth
	endif
endif

if fpStyle_m = STYLE_2 then
	values "fbWidth" range [0.2, A-2*minWall]
	fbDepth = B
	parameters fbDepth = fbDepth
endif

if fpStyle_m = STYLE_3 or fpStyle_m = STYLE_4 then
	values "fbOpenWidthSide" range [0.2, B-minWall]
	fbDepth = fbOpenWidthSide
	if fpStyle_m = STYLE_4 then
		values "fbWidth" range [0.2, A-2*minWall]
	else
		fbWidth = A - 2*fbGlassBack
		parameters fbWidth = fbWidth
		lock "fbWidth"
	endif
	parameters fbDepth = fbDepth
endif

if fpStyle_m = STYLE_5 then
	values "fbOpenWidthSide" range [0.1, MIN(A,B)-minWall-fbWidth/sqr(2)]
	values "fbWidth" range [0.2, (MIN(A,B)-minWall-fbOpenWidthSide)*sqr(2)]
	fbDepth = fbOpenWidthSide*sqr(2)
	parameters fbDepth = fbDepth
endif


! ----- Basic -----
values "fpWidthSide" range [minWall, A-minWall-fbWidth]
values "fbOpenH" range [0, ZZYZX-fbH-minWall]
values "fbH" range [0.3, ZZYZX-fbOpenH-minWall]
lock "fbFrame", "fbDepth"
if fpStyle_m < STYLE_3 then
	hideparameter "fbOpenWidthSide"
	lock "fbOpenWidthSide"
else
	hideparameter "fpWidthSide"
	lock "fpWidthSide"
endif


! ----- 3D Representation -----
values "gs_detlevel_3D" stDetlevel3D		! Off(0), Simple(1), Detailed(2)
values "gs_detlevel_3D_m" 0, 1, 2
if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	for i = 1 to vardim1(stDetlevel3D)
		if gs_detlevel_3D = stDetlevel3D[i] then	gs_detlevel_3D_m = 3-i
	next i
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D_m = max(min(gs_detlevel_3D_m,2),0)
	parameters gs_detlevel_3D = stDetlevel3D[3-gs_detlevel_3D_m]
endif

values "gs_resol" range [3, )
if gs_resol<3 then parameters gs_resol=3

if gs_cont_pen=0 then parameters gs_cont_pen=2
if gs_fill_pen=0 then parameters gs_fill_pen=91


! ----- 2D Representation -----
values "gs_detlevel_2D" stDetlevel2D		! Simple(1), Detailed(2)
values "gs_detlevel_2D_m" 1, 2
if GLOB_MODPAR_NAME = "gs_detlevel_2D" then
	gs_detlevel_2D_m = 1
	for i = 1 to vardim1(stDetlevel2D)
		if gs_detlevel_2D = stDetlevel2D[i] then	gs_detlevel_2D_m = 3-i
	next i
	parameters gs_detlevel_2D_m = gs_detlevel_2D_m
else
	gs_detlevel_2D_m = max(min(gs_detlevel_2D_m,2),1)
	parameters gs_detlevel_2D = stDetlevel2D[3-gs_detlevel_2D_m]
endif


! ----- Flue -----
values "flue_height"   range (0, )
values "flue_diameter" range (0, )
values "flue_wallthk"  range (0, flue_diameter/2)
if not(bFlue) then
	hideparameter	"flue_height",
					"flue_diameter",
					"flue_wallthk",
					"mat_flue",
					"bShowFlue2D"
	lock	"flue_height",
			"flue_diameter",
			"flue_wallthk",
			"mat_flue",
			"bShowFlue2D"
endif


! ----- Details -----
values "fbGlassBack" range [0, )
if fpStyle_m=STYLE_1 or fpStyle_m=STYLE_2 then
	values "corniceOverh" range [0, min(fpWidthSide, A-fpWidthSide-fbWidth)]
	values "socleOverh" range [0, min(fpWidthSide, A-fpWidthSide-fbWidth)]
	values "flushOverh" range [0, )
endif
if fpStyle_m=STYLE_3 then
	values "corniceOverh" range [0, B-fbOpenWidthSide]
	values "socleOverh" range [0, B-fbOpenWidthSide]
	values "flushOverh" range [0, B-fbOpenWidthSide]
endif
if fpStyle_m=STYLE_4 then
	values "corniceOverh" range [0, min(B-fbOpenWidthSide, A-fbWidth-max(minWall,fbGlassBack))]
	values "socleOverh" range [0, min(B-fbOpenWidthSide, A-fbWidth-max(minWall,fbGlassBack))]
	values "flushOverh" range [0, )
endif
if fpStyle_m=STYLE_5 then
	values "corniceOverh" range [0, min(B-fbOpenWidthSide-cornerX, A-fbOpenWidthSide-cornerX)]
	values "socleOverh" range [0, min(B-fbOpenWidthSide-cornerX, A-fbOpenWidthSide-cornerX)]
	values "flushOverh" range [0, )
endif
values "corniceHeight" range (0, ZZYZX-fbOpenH-fbH]
values "flushHeight" range (0, fbOpenH]
values "corniceOffset" range (0, )
values "flushOffset" range (0, )
values "socleOffset" range (0, )

if not(bCornice) then
	hideparameter "corniceOverh", "corniceHeight", "corniceOffset", "mat_corn"
	lock "corniceOverh", "corniceHeight", "corniceOffset", "mat_corn"
endif

if not(bFlush) then
	hideparameter "flushOverh", "flushHeight", "flushOffset", "mat_flush"
	lock "flushOverh", "flushHeight", "flushOffset", "mat_flush"
endif

if bFlush then
	socleHeight = fbOpenH - flushHeight
else
	socleHeight = fbOpenH
endif
hideparameter "socleHeight"
lock "socleHeight"
parameters socleHeight = socleHeight

if not(bSocle) then
	hideparameter "socleOverh", "socleHeight", "socleOffset", "mat_socle"
	lock "socleOverh", "socleHeight", "socleOffset", "mat_socle"
endif

if socleHeight <= EPS then
	bSocle = 0
	lock "bSocle"
	parameters bSocle = bSocle
endif

if gs_detlevel_3D_m = 1 then lock "gs_resol"
