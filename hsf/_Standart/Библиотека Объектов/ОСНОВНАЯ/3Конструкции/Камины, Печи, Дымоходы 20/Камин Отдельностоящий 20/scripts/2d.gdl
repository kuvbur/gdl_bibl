
!!Fireplace Freestanding.gsm


unID = 1
pen gs_cont_pen
fill gs_fill_type

! ======================================================================
! BODY
! ======================================================================

if iStoveType = 1 then gosub "Rectangle"				! Modern Type 1
if iStoveType = 2 then gosub "Modern_Type_2"			! Modern Type 2
if iStoveType = 3 then gosub "Rectangle"				! Modern Type 3
if iStoveType = 4 then gosub "Modern_Type_4"			! Modern Type 4
if iStoveType = 5 then gosub "Rectangle"				! Modern Type 5
if iStoveType = 6 then gosub "Traditional_Type_1"		! Traditional Type 1
if iStoveType = 7 then gosub "Traditional_Type_2"		! Traditional Type 2
if iStoveType = 8 then gosub "Rectangle"				! Traditional Type 3



! ======================================================================
! FLUE
! ======================================================================
if bFlue and bShowFlue2D then
	flue_diameter = flue_diameter / 2
	if iStoveType = 4 or iStoveType = 2 or iStoveType = 6 then
		if iStoveType = 4 then
			add2 0, A/2
		else
			if iStoveType = 2 then
				add2 0, y1+circleOffset2
			else
				add2 a/2, b+flue_diameter
			endif
		endif
	else
		add2 A/2, B
	endif

	add2 0, -0.05-flue_diameter
	fill fillTypeFlue
	poly2_b 4, 1+2+64,gs_cont_pen,gs_back_pen,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,1,
		0,0,900,
		0,-180,4001,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,-1

	poly2_b 4, 1+2+64,gs_back_pen,gs_back_pen,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,1,
		0,0,900,
		0,180,4001,
		-cos(45)*flue_diameter,-sin(45)*flue_diameter,-1

	circle2  0, 0, flue_diameter
	hotspot2 0, 0, unID : unID = unID + 1

	del 2
else
	unID = unID + 1
endif


END

! ======================================================================
! SubRoutines
! ======================================================================
"Rectangle":
	CALL "Resize_A_B_ZZYZX" Parameters ALL
	hotspot2 A/2, B, unID : unID = unID + 1
	hotspot2 A/2, B/2, unID : unID = unID + 1

	poly2_b 5, 1+2+64,gs_fill_pen,gs_back_pen,
		0,0,1,
		A,0,1,
		A,B,1,
		0,B,1,
		0,0,-1
return

"Modern_Type_4":
	hotspot2  -A/2, 0, unID, A, 1+256		: unID=unID+1
	hotspot2  -A/2-1, 0, unID, A, 3 		: unID=unID+1
	hotspot2   A/2, 0, unID, A, 2 			: unID=unID+1

	hotspot2   0, -A/2,  unID, A, 1+256		: unID=unID+1
	hotspot2   0, -A/2-1, unID, A, 3 		: unID=unID+1
	hotspot2   0, A/2,  unID, A, 2 			: unID=unID+1

	hotspot2 0, 0, unID : unID = unID + 1

	poly2_b 2, 1+2+64,gs_fill_pen,gs_back_pen,
		0, 0, 901,
		A/2,360,4001
return


"Modern_Type_2":
	alfa = 30
	origoOffset = 0.02

	radius1 = A/2 + origoOffset
	x1 = cos(alfa) * radius1 - origoOffset
	y1 = sin(alfa) * radius1
	circleOffset1 = 0.10
	circleOffset2 = B - circleOffset1 - 2*y1
	alfa1 = ATN(circleOffset1/x1)
	atfogo1 = sqr(circleOffset1*circleOffset1 + x1*x1)
	cirleRadius1 = (atfogo1/2)/cos(90-alfa1)
	alfa2 = ATN(circleOffset2/x1)
	atfogo2 = sqr(circleOffset2*circleOffset2 + x1*x1)
	cirleRadius2 = (atfogo2/2)/cos(90-alfa2)

	hotspot2  -A/2, 0, unID, A, 1+256		: unID=unID+1
	hotspot2  -A/2-1, 0, unID, A, 3 		: unID=unID+1
	hotspot2   A/2, 0, unID, A, 2 			: unID=unID+1

	hotspot2   0, -y1-circleOffset1,  unID, B, 1+256		: unID=unID+1
	hotspot2   0, -y1-circleOffset1-1, unID, B, 3 			: unID=unID+1
	hotspot2   0, y1+circleOffset2,  unID, B, 2 			: unID=unID+1

	hotspot2 0, 0, unID : unID = unID + 1

	hotspot2   x1,  y1, unID		: unID=unID+1
	hotspot2  -x1,  y1, unID		: unID=unID+1
	hotspot2  -x1, -y1, unID		: unID=unID+1
	hotspot2   x1, -y1, unID		: unID=unID+1

	drawindex 10
	poly2_b 2, 1+2+64,gs_fill_pen,gs_back_pen,
		0, 0, 901,
		y1+circleOffset1,360,4001

	drawindex 20
	poly2_b 18, 1+2+64,gs_fill_pen,gs_back_pen,
		-x1, -y1, 1,
		0, -y1-circleOffset1+cirleRadius1, 901,
		0, -y1-circleOffset1,3001,
		0, -y1-circleOffset1+cirleRadius1, 901,
		x1, -y1, 3001,
		-origoOffset, 0, 901,
		A/2, 0, 3001,
		-origoOffset, 0, 901,
		x1, y1, 3001,
		0, y1+circleOffset2-cirleRadius2, 901,
		0, y1+circleOffset2, 3001,
		0, y1+circleOffset2-cirleRadius2, 901,
		-x1, y1, 3001,
		origoOffset, 0, 901,
		-A/2, 0, 3001,
		origoOffset, 0, 901,
		-x1, -y1, 3001,
		-x1, -y1, -1
return


"Traditional_Type_1":
	CALL "Resize_A_B_ZZYZX" Parameters ALL
	hotspot2 A/2, B, unID : unID = unID + 1
	hotspot2 A/2, B/2, unID : unID = unID + 1

	mul2 A/0.940 , B/0.508

	hotspot2       0.470,        0.648, unID : unID = unID + 1
	hotspot2       0.470,       -0.139, unID : unID = unID + 1

	poly2_b 37, 1+2+64,gs_fill_pen,gs_back_pen,
				  0.0762,        0.508,      1,
				 0.27305,        0.508,      1,
			0.335196158184, 0.591903841816,      1,
				  0.4699,       0.4572,    900,
					   0,          -90,   4001,
			0.604603841816, 0.591903841816,      1,
				 0.66675,        0.508,      1,
				  0.8636,        0.508,      1,
				  0.8636,       0.4318,    900,
					   0,          -90,   4001,
				  0.9398,       0.4318,      1,
				  0.9398,       0.0508,      1,
				  0.8636,       0.0508,    900,
					   0,          -90,   4001,
				  0.8636,      -0.0254,      1,
				  0.8636,      -0.1016,    900,
					   0,           60,   4001,
			0.7976088642316,      -0.0635,      1,
			0.7257692412329, -0.01777810304871,    900,
					   0, -40.05106109747,   4001,
			0.7513397397001, -0.09900354796323,      1,
				  0.4699, 0.8800489104697,    900,
					   0, -32.07579555296,   4001,
			0.1884602602999, -0.09900354796323,      1,
			0.2140307587671, -0.01777810304871,    900,
					   0, -40.05106109747,   4001,
			0.1421911357684,      -0.0635,      1,
				  0.0762,      -0.1016,    900,
					   0,           60,   4001,
				  0.0762,      -0.0254,      1,
				  0.0762,       0.0508,    900,
					   0,          -90,   4001,
			2.567023199909E-017,       0.0508,      1,
			2.181969719923E-016,       0.4318,      1,
				  0.0762,       0.4318,    900,
					   0,          -90,   4001,
				  0.0762,        0.508,      1


	arc2           0.0762,       0.0762,       0.0762,          180,          270
	arc2           0.8636,       0.0762,       0.0762,          270,            0

	del 1
return

"Traditional_Type_2":
	CALL "Resize_A_B_ZZYZX" Parameters ALL
	hotspot2 A/2, B, unID : unID = unID + 1
	hotspot2 A/2, B/2, unID : unID = unID + 1

	cirleSize = 0.04
	alfa = ATN(cirleSize/(A/2))
	cirleRadius	= (A/4)/cos(90-alfa)

	poly2_b 8, 1+2+64,gs_fill_pen,gs_back_pen,
		0, B, 15,
		A, B, 15,
		A, cirleSize, 79,
		A/2, cirleRadius, 900,
		A/2, 0, 3079,
		A/2, cirleRadius, 900,
		0, cirleSize, 3079,
		0, B, -1
return
