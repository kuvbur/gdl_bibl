
TABID_ROOT				= -1
TABID_TYPES				= 1
TABID_REP_SURFACE		= 2


dim stFireplaceSettingsUI[24]
	stFireplaceSettingsUI[1] = `Параметры Камина`
	stFireplaceSettingsUI[2] = `Стиль`
	stFireplaceSettingsUI[3] = `Тип`
	stFireplaceSettingsUI[4] = `Дымоход`
	stFireplaceSettingsUI[5] = `Высота`
	stFireplaceSettingsUI[6] = `Диаметр`
	stFireplaceSettingsUI[7] = `Параметры`
	stFireplaceSettingsUI[8] = `Отсек для Выпечки`
	stFireplaceSettingsUI[9] = `Открытый Дровник`
	stFireplaceSettingsUI[10] = `2D-отображение`
	stFireplaceSettingsUI[11] = `Перо Контура`
	stFireplaceSettingsUI[12] = `Тип Штриховки`
	stFireplaceSettingsUI[13] = `Перо Штриховки`
	stFireplaceSettingsUI[14] = `Перо Фона Штриховки`
	stFireplaceSettingsUI[15] = `3D-отображение`
	stFireplaceSettingsUI[16] = `3D-детализация`
	stFireplaceSettingsUI[17] = `Разрешение`
	stFireplaceSettingsUI[18] = `Отбрасывание Теней`
	stFireplaceSettingsUI[19] = `Плита`
	stFireplaceSettingsUI[20] = `Дымоход`
	stFireplaceSettingsUI[21] = `Стекло`
	stFireplaceSettingsUI[22] = `Показать Огонь в 3D`
	stFireplaceSettingsUI[23] = `Дерево`
	stFireplaceSettingsUI[24] = `Толщина Стенки`




! ======================================================================

ui_dialog stFireplaceSettingsUI[1],444,266
ui_current_page gs_ui_current_page




if TABID_TYPES > 0 then
	ui_page TABID_TYPES, TABID_ROOT, `Тип и Дополнительные Элементы`, "uiTab_Dimension_flat(1)"
	gosub "typesPage"
endif

if TABID_REP_SURFACE > 0 then
	ui_page TABID_REP_SURFACE, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "repAndSurfacePage"
endif

! ======================================================================
end
! ======================================================================

"typesPage":
	! ======================================================================
	! Page 1
	! ======================================================================
	
	pageStartY = 5	
	x1		= 15
	yStart	= pageStartY + 3  
	xHalfLevel 	= 240
	xEndLevel	= 440
	
	hRow		= 18
	hPictBox	= 90
	hCheckBox	= 24
	lineSpace	= 24
	
	wDescript1	= 65
	wDescript2	= 110
	wDescript3	= 165
	wBox1		= 125
	wPictBox	= 290
	
	y1 = yStart
	
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[3],	x1, y1,	wDescript1,	hRow																! Type
	ui_style 0, 0
	ui_infield{3} "iStoveType",x1 + wDescript1 +1,	y1,	wPictBox, 100,
				2, 10, 10, 2, 75, 100, 75, 85,															! ui_fireplace_freestand_1.png
				1, stType[1], 1,
				2, stType[2], 2,
				3, stType[3], 3,
				4, stType[4], 4,
				5, stType[5], 5,
				6, stType[6], 6,
				7, stType[7], 7,
				8, stType[8], 8 ui_tooltip stFireplaceSettingsUI[3]
	y1 = y1 + lineSpace/2 + hPictBox
	
	ui_separator x1, y1, 444-x1, y1
	ui_separator xHalfLevel, y1+4, xHalfLevel, pageStartY + 231
	y1 = y1 + lineSpace/2
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[4],	x1,		y1,	wDescript2,	hRow															! Flue
	ui_outfield stFireplaceSettingsUI[7],	xHalfLevel+x1,		y1,	wDescript2,	hRow												! Options
	ui_style 0, 0
	y1 = y1 + lineSpace
	
	ui_outfield stFireplaceSettingsUI[4],	x1+hCheckBox,	y1,	wDescript2,	hRow													! Flue
	ui_infield "bFlue",	x1+4,	y1-2,	hRow,	hRow	ui_tooltip stFireplaceSettingsUI[4]
	flag = 4 * not(iStoveType = 4 | iStoveType = 5)
	ui_outfield stFireplaceSettingsUI[8],	xHalfLevel+x1+hCheckBox,	y1,	wDescript3,	hRow, flag									! Backing Compartment
	ui_infield "bComp",	xHalfLevel+x1+4,	y1-2,	hRow,	hRow	ui_tooltip stFireplaceSettingsUI[8]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bFlue)
	ui_outfield stFireplaceSettingsUI[5],	x1+hCheckBox,	y1,	wDescript2,	hRow, flag												! Flue Height
	ui_infield "flue_height",	x1+hCheckBox+wDescript2+2,	y1-2,	wDescript1,	hRow	ui_tooltip stFireplaceSettingsUI[5]
	flag = 4 * not(iStoveType < 6 and iStoveType <> 2)
	ui_outfield stFireplaceSettingsUI[9],	xHalfLevel+x1+hCheckBox,	y1,	wDescript3,	hRow, flag										! Firewood Storage Open
	ui_infield "bOpen",	xHalfLevel+x1+4,	y1-2,	hRow,	hRow	ui_tooltip stFireplaceSettingsUI[9]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bFlue)
	ui_outfield stFireplaceSettingsUI[6],	x1+hCheckBox,	y1,	wDescript2,	hRow, flag												! Flue Diameter
	ui_infield "flue_diameter",	x1+hCheckBox+wDescript2+2,	y1-2,	wDescript1,	hRow	ui_tooltip stFireplaceSettingsUI[6]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bFlue)
	ui_outfield stFireplaceSettingsUI[24],	x1+hCheckBox,	y1,	wDescript2,	hRow, flag												! Flue Diameter
	ui_infield "flue_wallthk",	x1+hCheckBox+wDescript2+2,	y1-2,	wDescript1,	hRow	ui_tooltip stFireplaceSettingsUI[24]
return

"repAndSurfacePage":
	! ======================================================================
	! Page 2
	! ======================================================================
	
	pageStartY 	= 5
	x1			= 5
	yStart		= pageStartY
	xHalfLevel 	= 218
	xEndLevel	= 440
	
	hRow		= 18
	hPenBox		= 22
	hFillBox	= 24
	hMatBox		= 24
	lineSpace	= 26
	
	wDescript1	= 150
	wDescript2	= 80
	wDescript3	= 65
	wDescript4	= 145
	wBox1		= 90
	wPenBox		= 35
	wPictBox	= 30
	wFillBox	= 130
	wMatBox		= 130
	
	ui_style 0, 1
	ui_outfield stFireplaceSettingsUI[10],	x1,		yStart,	wDescript1,	hRow									! 2D Representation
	ui_style 0, 0
	y1 = yStart + lineSpace
	
	ui_pict 2, x1, y1-3, 24, 17 , 1 ui_tooltip stFireplaceSettingsUI[11]										! ui_contour_pen(1).png
	ui_outfield stFireplaceSettingsUI[11],	x1+wPictBox,	y1,	wDescript4,	hRow								! Contour Pen
	ui_infield "gs_cont_pen",	xHalfLevel - wPenBox, y1-1, wPenBox, hPenBox  ui_tooltip stFireplaceSettingsUI[11]
	y1 = y1 + lineSpace
	
	ui_pict 4, x1, y1-3, 13, 17 , 1 ui_tooltip stFireplaceSettingsUI[12]										! ui_fill_type.png
	ui_outfield stFireplaceSettingsUI[12], x1+wPictBox, y1, wDescript2,	hRow									! Fill Type
	y1 = y1 + lineSpace
	ui_infield "gs_fill_type",	x1+wPictBox, y1-6, xHalfLevel-x1-wPictBox, hFillBox  ui_tooltip stFireplaceSettingsUI[12]
	y1 = y1 + lineSpace
	
	ui_pict 3, x1, y1-3, 24, 17 , 1 ui_tooltip stFireplaceSettingsUI[13]										! ui_fill_pen.png
	ui_outfield stFireplaceSettingsUI[13], x1+wPictBox, y1, wDescript4,	hRow									! Fill Pen
	ui_infield "gs_fill_pen",	xHalfLevel - wPenBox, y1-4, wPenBox, hPenBox  ui_tooltip stFireplaceSettingsUI[13]
	y1 = y1 + lineSpace
	
	ui_pict 1, x1, y1-3, 24, 17 , 1 ui_tooltip stFireplaceSettingsUI[14]										! ui_back_pen(1).png
	ui_outfield stFireplaceSettingsUI[14], x1+wPictBox, y1, wDescript4,	hRow									! Fill Background Pen
	ui_infield "gs_back_pen",	xHalfLevel - wPenBox, y1-4, wPenBox, hPenBox  ui_tooltip stFireplaceSettingsUI[14]
	
	ui_separator xHalfLevel+ 5, yStart, xHalfLevel+5, pageStartY + 225
	
	! ------ 2. Column - 3D Representation -------
	
	ui_style 0,1
	ui_outfield stFireplaceSettingsUI[15], xHalfLevel + 10, yStart, wDescript1,	hRow							!3D Representation
	ui_style 0,0
	y1 = yStart + lineSpace
	
	ui_outfield stFireplaceSettingsUI[16], xHalfLevel + 10 , y1, wDescript2, hRow								! 3D Detail Level
	ui_infield{3} "gs_detlevel_3D_m", xEndLevel - wBox1, y1,	wBox1, hRow,
			8, "", 4, 1, 0, 0, 0, 0,
			1, stDetlevel3D[1], 2,
			2, stDetlevel3D[2], 1,
			3, stDetlevel3D[3], 0   ui_tooltip stFireplaceSettingsUI[16]
	y1 = y1 + lineSpace
	
	iFlag = (gs_detlevel_3D_m < 2) * 4
	
	ui_outfield stFireplaceSettingsUI[17], xHalfLevel + 10,	y1,	wDescript2,	hRow, iFlag							! Resolution
	ui_infield "gs_resol", xEndLevel - wBox1,	y1,	wBox1,		hRow  ui_tooltip stFireplaceSettingsUI[17]
	y1 = y1 + lineSpace
	
	ui_infield{3} "gs_shadow", xHalfLevel + 10, y1-2, wDescript1, hRow,											! Shadow
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stFireplaceSettingsUI[18], 0,
		1, stFireplaceSettingsUI[18], 1	ui_tooltip stFireplaceSettingsUI[18]
	y1 = y1 + lineSpace
	
	ui_infield{3} "bFire", xHalfLevel + 10, y1-2, wDescript1, hRow,												! Show Fire in 3D
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stFireplaceSettingsUI[22], 0,
		1, stFireplaceSettingsUI[22], 1	ui_tooltip stFireplaceSettingsUI[22]
	y1 = y1 + lineSpace
	
	! ------ 2. Column - Materials -------
	ui_pict 5, xHalfLevel+10, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[19]								! ui_mat(1).png
	ui_outfield stFireplaceSettingsUI[19],xHalfLevel + 26, y1, wDescript3,	hRow								! Stove
	ui_infield "mat_stove", xEndLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[19]
	y1 = y1 + lineSpace
	
	flag = 4 * not(bFlue)
	if bFlue then
		ui_pict 5, xHalfLevel+10, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[20]							! ui_mat(1).png
	else
		ui_pict 6, xHalfLevel+10, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[20]							! ui_mat_lock(1).png
	endif
	ui_outfield stFireplaceSettingsUI[20],xHalfLevel + 26, y1, wDescript3,	hRow, flag 							! Flue
	ui_infield "mat_flue", xEndLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[20]
	y1 = y1 + lineSpace
	
	ui_pict 5, xHalfLevel+10, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[21]								! ui_mat(1).png
	ui_outfield stFireplaceSettingsUI[21],xHalfLevel + 26, y1, wDescript3,	hRow								! Glass Door
	ui_infield "mat_glass", xEndLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[21]
	y1 = y1 + lineSpace
	
	flag = 4 * (iStoveType > 5 or iStoveType = 2)
	if flag then
		ui_pict 6, xHalfLevel+10, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[20]							! ui_mat_lock(1).png
	else
		ui_pict 5, xHalfLevel+10, y1-3, 12, 17 , 1 ui_tooltip stFireplaceSettingsUI[20]							! ui_mat(1).png
	endif
	ui_outfield stFireplaceSettingsUI[23],xHalfLevel + 26, y1, wDescript3,	hRow, flag 							! Wood
	ui_infield "mat_wood_1", xEndLevel - wMatBox, y1-4, wMatBox, hMatBox  ui_tooltip stFireplaceSettingsUI[20]
return
	



