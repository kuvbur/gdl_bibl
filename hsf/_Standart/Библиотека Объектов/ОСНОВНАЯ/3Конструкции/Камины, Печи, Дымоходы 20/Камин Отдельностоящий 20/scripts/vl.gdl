
call "FM_types" parameters all

! ----- Defined Dimensions -----
dim defA[8]
	defA[1] = 0.675
	defA[2] = 0.552
	defA[3] = 0.504
	defA[4] = 0.510
	defA[5] = 0.495
	defA[6] = 0.940
	defA[7] = 0.600
	defA[8] = 0.500

dim defB[8]
	defB[1] = 0.550
	defB[2] = 0.455
	defB[3] = 0.408
	defB[4] = 0.510
	defB[5] = 0.486
	defB[6] = 0.508
	defB[7] = 0.400
	defB[8] = 0.300

dim defZZYZX[8]
	defZZYZX[1] = 0.750
	defZZYZX[2] = 1.060
	defZZYZX[3] = 0.960
	defZZYZX[4] = 1.200
	defZZYZX[5] = 1.280
	defZZYZX[6] = 0.813
	defZZYZX[7] = 0.550
	defZZYZX[8] = 0.550

dim defLegHeight[8]
	defLegHeight[1] = 0.200
	defLegHeight[2] = 0.400
	defLegHeight[3] = 0.300
	defLegHeight[4] = 0.400
	defLegHeight[5] = 0.400
	defLegHeight[6] = 0.203
	defLegHeight[7] = 0.100
	defLegHeight[8] = 0.080

dim defTopHeight[8]
	defTopHeight[1] = 0
	defTopHeight[2] = 0
	defTopHeight[3] = 0
	defTopHeight[4] = 0.200
	defTopHeight[5] = 0.200
	defTopHeight[6] = 0
	defTopHeight[7] = 0
	defTopHeight[8] = 0


! ----- Style Settings -----

values "stoveType" stType
values "iStoveType" 1, 2, 3, 4, 5, 6, 7, 8
if GLOB_MODPAR_NAME = "stoveType" then
	iStoveType = 1
	for i = 1 to vardim1(stType)
		if stoveType = stType[i] then	iStoveType = i
	next i
	parameters iStoveType = iStoveType
else
	iStoveType = max(min(iStoveType,8),1)
	parameters stoveType = stType[iStoveType]
endif
if GLOB_MODPAR_NAME = "stoveType" or GLOB_MODPAR_NAME = "iStoveType" then
	gosub "Type_Change"
endif


maxDimChange = 0.2
values "A" range [defA [iStoveType]-maxDimChange, defA [iStoveType]+maxDimChange]
if iStoveType = 2 then
	values "B" range [0.3, A]
else
	values "B" range [0.3, ]
endif
values "ZZYZX" range [defZZYZX [iStoveType] - defLegHeight [iStoveType], defZZYZX [iStoveType] + 2*defLegHeight [iStoveType]]


! ----- 3D Representation -----
values "gs_detlevel_3D" stDetlevel3D		! Off(0), Simple(1), Detailed(2)
values "gs_detlevel_3D_m" 0, 1, 2
if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	for i = 1 to vardim1(stDetlevel3D)
		if gs_detlevel_3D = stDetlevel3D[i] then	gs_detlevel_3D_m = 3-i
	next i
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D_m = max(min(gs_detlevel_3D_m,2),0)
	parameters gs_detlevel_3D = stDetlevel3D[3-gs_detlevel_3D_m]
endif

values "gs_resol" range [3, )


if gs_cont_pen=0 then parameters gs_cont_pen=2
if gs_fill_pen=0 then parameters gs_fill_pen=91
values "flue_height" range (zzyzx, )
if flue_height<zzyzx then parameters flue_height=zzyzx
values "flue_diameter" range (0, )
values "flue_wallthk"  range (0, flue_diameter/2)

if not(bFlue) then
	hideparameter "flue_height", "flue_diameter", "flue_wallthk", "mat_flue", "bShowFlue2D"
	lock "flue_height", "flue_diameter", "flue_wallthk", "mat_flue", "bShowFlue2D"
endif

if iStoveType > 5 then
	hideparameter "bComp", "bOpen", "mat_wood_1"
	lock "bComp", "bOpen", "mat_wood_1"
else
	if iStoveType < 4 then
		hideparameter "bComp"
		lock "bComp"
	endif
	if iStoveType = 2 then
		hideparameter "bOpen", "mat_wood_1"
		lock "bOpen", "mat_wood_1"
	endif
endif

if gs_detlevel_3D_m = 1 then lock "gs_resol"

!! ----- Height Settings -----
if GLOB_MODPAR_NAME = "ZZYZX" then
	legHeight = legHeight - (tempZZYZX - ZZYZX)

	if legHeight > defLegHeight [iStoveType] then
		legHeight = defLegHeight [iStoveType]
	endif
	if legHeight < EPS then
		legHeight = 0
	endif
	tempZZYZX = ZZYZX
	parameters legHeight = legHeight, tempZZYZX=tempZZYZX
endif


!! ----- Round (Modern Type 4)-----
if iStoveType = 4 then
	lock "B"
	b = b
	parameters B = B
endif

END

!-----Change Type-----
"Type_Change":
	A 		  = defA[iStoveType]
	B 		  = defB[iStoveType]
	ZZYZX	  = defZZYZX[iStoveType]
	tempZZYZX = defZZYZX[iStoveType]
	topHeight = defTopHeight[iStoveType]
	legHeight = defLegHeight[iStoveType]
	parameters a = a, b = b, ZZYZX = ZZYZX, tempZZYZX=tempZZYZX, topHeight = topHeight, legHeight = legHeight
return

! ----- User Interface -----
values "gs_ui_current_page" 1, 2


