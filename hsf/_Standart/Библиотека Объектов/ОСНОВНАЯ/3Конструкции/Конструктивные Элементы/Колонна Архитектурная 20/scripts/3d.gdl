
if not(gs_shadow) then SHADOW OFF

PEN gs_cont_pen
IF tnum=1 THEN END

unID=1
hotspot 0,0,0,unID,zzyzx,1+256 : unID=unID+1
hotspot 0,0,-1,unID,zzyzx,3 : unID=unID+1
hotspot 0,0,zzyzx,unID,zzyzx,2 : unID=unID+1

MATERIAL mat

IF iPlacementType=3 OR iPlacementType=4 THEN
	ADDY dca*(iPlacementType=4)
	CUTPLANE -90
	DEL 1
else
	IF iPlacementType=2 THEN
		CUTPOLYA 4, 2, 0,
			0, 0.0001, 15,
			coldb*2, 0.0001, 15,
			coldb*2, coldb*2, 15,
			0, coldb*2, 15
	ENDIF
ENDIF


RESOL 12*(tnum=2)+24*(tnum=3)

MATERIAL mat


zzyzx=zzyzx-zboc-zcoc-zpoc

MATERIAL mat
o_1=1/36: o_2=3/72
enum=6
gamma=(ACS((coldt/2)/(coldb/2)))/enum
IF not(entasis) THEN
	beta_1=ATN((coldb/2-coldt/2)/(zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2)))
else
	beta_1=ATN((((coldb/2)*COS(gamma*5))-coldt/2)/((zzyzx*(2/3))/enum-coldt*(o_1+o_2)))
endif


!----------------------------> pedesztal <-----------------------------

if poc=1 then

	if iOrnamentType = 1 then
		GeomParam  = 14
		GeomParam0 = 23
		GeomParam1 = 17
		GeomParam2 = 22.75
		GeomParam3 = 25
		GeomParam4 = 0.25
		GeomParam5 = 22
		GeomParam6 = 0.75
		GeomParam7 = 0.5
		GeomParam8 = 18.35
		GeomParam9 = 10.85
		GeomParam10 = 1.5
		GeomParam11 = 1.15
		GeomParam12 = 0
	else
		if iOrnamentType = 2 THEN
			GeomParam  = 14
			GeomParam0 = 24
			GeomParam1 = 19
			GeomParam2 = 23.75
			GeomParam3 = 27
			GeomParam4 = 0.40
			GeomParam5 = 22.65
			GeomParam6 = 0.25
			GeomParam7 = 0.3
			GeomParam8 = 19.25
			GeomParam9 = 12.10
			GeomParam10 = 2.45
			GeomParam11 = 1.15
			GeomParam12 = 0
		else
			GeomParam  = 14
			GeomParam0 = 26.5
			if iOrnamentType = 3 then
				GeomParam1 = 21
			else
				GeomParam1 = 24
			endif
			GeomParam2 = 26
			GeomParam3 = 30
			GeomParam4 = 0.40
			GeomParam5 = 24.5
			GeomParam6 = 0.25
			GeomParam7 = 0.35
			GeomParam8 = 19.5
			if iOrnamentType = 3 then
				GeomParam9 = 13.3
				addpart=0
				div=1
			else
				GeomParam9 = 16.3
				addpart=(0.25/19)*(coldb/2)
				div=2
			endif
			GeomParam10 = 2.65
			GeomParam11 = 1.70
			GeomParam12 = GeomParam9
		endif
	endif


	xypoc=coldb*(GeomParam3/GeomParam)
	xy=xypoc*(GeomParam0/GeomParam3)

	if iOrnamentType = 1 or iOrnamentType = 2 or iOrnamentType = 3 then
		hotspot -xy/2,-xy/2,0,unID    : unID=unID+1
		hotspot xy/2,-xy/2,0,unID     : unID=unID+1
		if iPlacementType=1 then
			hotspot xy/2,xy/2,0,unID  : unID=unID+1
			hotspot -xy/2,xy/2,0,unID : unID=unID+1
		else
			if iPlacementType=2 then
				hotspot xy/2,0,0,unID : unID=unID+1
				hotspot 0,0,0,unID    : unID=unID+1
				hotspot 0,xy/2,0,unID : unID=unID+1
				hotspot -xy/2,xy/2,0,unID : unID=unID+1
			else
				if iPlacementType=3 then
					hotspot xy/2,0,0,unID : unID=unID+1
					hotspot -xy/2,0,0,unID : unID=unID+1
				else
					hotspot xy/2,dca,0,unID : unID=unID+1
					hotspot -xy/2,dca,0,unID : unID=unID+1
				endif
			endif
		endif
	endif

	if tnum=3 then
		ADD -xy/2, -xy/2, 0: zzz=zpoc*(3/GeomParam1)
		BLOCK xy, xy, zzz: DEL 1
		zz=zzz: xy=xypoc*(GeomParam2/GeomParam3)
		ADD -xy/2, -xy/2, zz: zzz=zpoc*(GeomParam4/GeomParam1)
		BLOCK xy, xy, zzz: DEL 1
		zz=zz+zzz: xy=xypoc*(GeomParam5/GeomParam3)
		ADD -xy/2, -xy/2, zz: zzz=zpoc*(GeomParam6/GeomParam1)
		if iOrnamentType = 2 then
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(21.55/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.25/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(20.45/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.25/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(19.25/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(12.10/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(20.25/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.4/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(23.5/27): zzz=zpoc*(0.7/GeomParam1)
			ADD -xy/2, xy/2, zz+zzz
			ROTX 180
			FPRISM_ mat, mat, mat, mat,
					4, zzz, 45, zzz,
					0, 0, 15,
					0, xy, 15,
					xy, xy, 15,
					xy, 0, 15
			DEL 2
			zz=zz+zzz: xy=xypoc*(24.75/27)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(1.05/19)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(26/27): zzz=zpoc*(0.3/19)
			ADD -xy/2, xy/2, zz+zzz
			ROTX 180
		endif
		if iOrnamentType = 3 or iOrnamentType = 4 or iOrnamentType = 5 then
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(23.5/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.40/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(22/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.25/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(21/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.40/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(19.5/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(GeomParam12/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(19.75/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.25/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(20/GeomParam3)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.25/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(23/GeomParam3): zzz=zpoc*(0.5/GeomParam1)
			ADD -xy/2, xy/2, zz+zzz
			ROTX 180
		endif
		FPRISM_ mat, mat, mat, mat,
				4, zzz, 45, zzz,
				0, 0, 15,
				0, xy, 15,
				xy, xy, 15,
				xy, 0, 15
		DEL 1
		if iOrnamentType <> 1 then DEL 1
		if iOrnamentType = 1 then
			zz=zz+zzz: xy=xypoc*(18.85/25)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.15/17)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(18.35/25)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(10.85/17)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(19.15/25)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.3/17)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(19.95/25)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.3/17)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(22.25/25)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(0.9/17)
			BLOCK xy, xy, zzz: DEL 1
		endif
		if iOrnamentType = 3 or iOrnamentType = 4 or iOrnamentType = 5 then
			zz=zz+zzz: xy=xypoc*(23/27)
			ADD -xy/2, -xy/2, zz: zzz=zpoc*(1.3/GeomParam1)
			BLOCK xy, xy, zzz: DEL 1
			zz=zz+zzz: xy=xypoc*(25/27): zzz=zpoc*(0.35/GeomParam1)
			ADD -xy/2, xy/2, zz+zzz
			ROTX 180
			FPRISM_ mat, mat, mat, mat,
					4, zzz, 45, zzz,
					0, 0, 15,
					0, xy, 15,
					xy, xy, 15,
					xy, 0, 15
			DEL 2
		endif
		zz=zz+zzz: xy=xypoc
		ADD -xy/2, -xy/2, zz: zzz=zpoc*(GeomParam7/GeomParam1)
		BLOCK xy, xy, zzz: DEL 1
	else
		ADD -xy/2, -xy/2, 0: zzz=zpoc*(3/GeomParam1)
		BLOCK xy, xy, zzz: DEL 1
		zz=zzz: xy=xy
		ADD -xy/2, -xy/2, zz: zzz=zpoc*(GeomParam11/GeomParam1)
		FPRISM_ mat, mat, mat, mat,
				4, zzz, ATN((zzz)/((xy-xypoc*(GeomParam8/GeomParam3))/2)), zzz,
				0, 0, 15,
				0, xy, 15,
				xy, xy, 15,
				xy, 0, 15
		DEL 1
		zz=zz+zzz: xy=xypoc*(GeomParam8/GeomParam3)
		ADD -xy/2, -xy/2, zz: zzz=zpoc*(GeomParam9/GeomParam1)
		BLOCK xy, xy, zzz: DEL 1
		zz=zz+zzz: xy=xypoc*(GeomParam3/GeomParam3): zzz=zpoc*(GeomParam10/GeomParam1)
		ADD -xy/2, xy/2, zz+zzz
		ROTX 180
		if iOrnamentType = 2 then
			FPRISM_ mat, mat, mat, mat,
					4, zzz, ATN((zpoc*(GeomParam10/GeomParam1))/((xypoc*(GeomParam3/GeomParam3)-xypoc*(GeomParam8/GeomParam3))/2)), zzz,
					0, 0, 15,
					0, xy, 15,
					xy, xy, 15,
					xy, 0, 15
		else
			FPRISM_ mat, mat, mat, mat,
					4, zzz, ATN((zzz)/((xypoc*(GeomParam3/GeomParam3)-xypoc*(GeomParam8/GeomParam3))/2)), zzz,
					0, 0, 15,
					0, xy, 15,
					xy, xy, 15,
					xy, 0, 15
		endif
		DEL 2
		zz=zz+zzz: xy=xypoc*(GeomParam3/GeomParam3)
		ADD -xy/2, -xy/2, zz: zzz=zpoc*(GeomParam7/GeomParam1)
		BLOCK xy, xy, zzz: DEL 1
	endif
else
	if iOrnamentType = 3 then
		div=1
	endif
endif

ADDZ zpoc


!-----------------------> oszlop alja-teteje <------------------------

ADDZ zboc

omega=360/cannum

RESOL cannum

ROTZ omega/2

alpha_1=90-beta_1*not(entasis): cold=coldb: lr=cold*(o_2)
IF alpha_1>0 THEN lr=(cold*(o_2))/(1+SIN(alpha_1-90))
cord=lr-lr*(COS(alpha_1-90)): rcb=cold/2+lr-cord: cb=cold*o_1

	GOSUB 10

alpha_1=90+beta_1: cold=coldt: lr=cold*(o_2)
IF alpha_1>0 THEN lr=(cold*(o_2))/(1+SIN(alpha_1-90))
cord=lr-lr*(COS(alpha_1-90)): rct=cold/2+lr-cord: ct=cold*o_1

ADDZ zzyzx
ROTX 180
	GOSUB 10
DEL 4


!---------------------------> oszlop alap <---------------------------

zz=0
IF boc=1 THEN

	bxy=coldb*(8/6)

	hotspot -bxy/2,-bxy/2,0,unID : unID=unID+1
	hotspot bxy/2,-bxy/2,0,unID : unID=unID+1
	If iPlacementType=1 Then
		hotspot bxy/2,bxy/2,0,unID : unID=unID+1
		hotspot -bxy/2,bxy/2,0,unID : unID=unID+1
	Else
		If iPlacementType=2 Then
			hotspot bxy/2,0,0,unID : unID=unID+1
			hotspot 0,0,0,unID : unID=unID+1
			hotspot 0,bxy/2,0,unID : unID=unID+1
			hotspot -bxy/2,bxy/2,0,unID : unID=unID+1
		Else
			If iPlacementType=3 Then
				hotspot bxy/2,0,0,unID : unID=unID+1
				hotspot -bxy/2,0,0,unID : unID=unID+1
			Else
				hotspot bxy/2,dca,0,unID : unID=unID+1
				hotspot -bxy/2,dca,0,unID : unID=unID+1
			EndIf
		EndIf
	EndIf

	if iOrnamentType = 1 or iOrnamentType = 2 then
		ratio = 9/17
	else
		ratio = 6/19
	endif

	ADD -bxy/2, -bxy/2, 0
	BLOCK bxy, bxy, ratio*(coldb/2)
	DEL 1

	ADDZ ratio*(coldb/2)
	db=bxy
	Num=0

	if iOrnamentType = 1 then
		rr=bxy/2-rcb
		rrr=coldb/2-ratio*(coldb/2)-rr
	else
		if iOrnamentType = 2 then
			rr=(bxy/2-rcb)*0.80
			rrr=coldb/2-(ratio+3/34)*(coldb/2)-rr
		else
			rr=bxy/2-rcb-(2/19)*(coldb/2)
			rrr=(5/19)*(coldb/2)-rr
		endif
	endif

	alpha_f=ACS(rrr/rr)
	ff=11*(tnum=3)+4*(tnum=2)
	if iOrnamentType = 4 OR iOrnamentType = 5 then
		ff_1=ff
		ff_2=7*(tnum=3)+3*(tnum=2)
	endif
	alpha_ff=(180-alpha_f)/(ff-1)
	GOSUB 20

	if iOrnamentType <> 1 then
		if iOrnamentType = 2 then
			zz=rrr+rr
			ff=7*(tnum=3)+3*(tnum=2)
			rr=(3/68)*(coldb/2)
		else
			if iOrnamentType = 3 then
				GeomParam1 = 1
				GeomParam2 = 3
				GeomParam3 = 4
			else
				GeomParam1 = 0.75
				GeomParam2 = 1.15
				GeomParam3 = GeomParam2
			endif
			ADDZ rr+rrr
			CIRCLE db/2-rr-(1/38)*(coldb/2)
			DEL 1
			zz=rrr+rr+(GeomParam1/19)*(coldb/2)
			PUT zz-(GeomParam1/19)*(coldb/2), (bxy-rr*2-(1/19)*(coldb/2))/2, 0
			Num=Num+1
			ADDZ zz
			CIRCLE db/2-rr-(1/38)*(coldb/2)
			DEL 1
			db=bxy-rr*2-(1/19)*(coldb/2)
			rr=(((GeomParam2/19)*(coldb/2))^2+((db/2-(rcb)+addpart)/div)^2)/((GeomParam2/19)*(coldb/2)*2)
			rrr=(GeomParam2/19)*(coldb/2)-rr
			alpha_f=ACS(rrr/rr)
			if iOrnamentType <> 3 then
				fff=ff
				ff=ff_2
			endif
			alpha_ff=(180-alpha_f)/(ff-1)
			if iOrnamentType <> 3 then
				xb=(db/2-(rcb))
				xd=db
			endif
			GOSUB 21
			zz=zz+(GeomParam3/19)*(coldb/2)
			if iOrnamentType <> 3 then
				rr=(0.6/19)*(coldb/2)
				rrr=rr
				db=(rcb+xb/2+rr-(0.125/19)*(coldb/2))*2
				alpha_f=ACS(rrr/rr)
				ff=ff_2
				alpha_ff=(180-alpha_f)/(ff-1)
				GOSUB 20
				ADDZ zz
				CIRCLE db/2-rr: DEL 1
				zz=zz+(1.2/19)*(coldb/2)
				ADDZ zz
				CIRCLE db/2-rr: DEL 1
				db=db-rr*2
				rr=((((1.15/19)*(coldb/2))^2+((xd/2-(rcb)-(0.25/19)*(coldb/2))/2)^2)/((1.15/19)*(coldb/2)*2))
				rrr=(1.15/19)*(coldb/2)-rr
				alpha_f=ACS(rrr/rr)
				ff=ff_2
				alpha_ff=(180-alpha_f)/(ff-1)
				GOSUB 21
				zz=zz+(1.9/19)*(coldb/2)
			endif
			ADDZ zz-(GeomParam1/19)*(coldb/2)
			CIRCLE rcb
			DEL 1
			ADDZ zz
			CIRCLE rcb
			DEL 1
			rr=(1.5/19)*(coldb/2)
		endif
		rrr=rr
		db=(rcb+rr)*2
		alpha_f=0
		alpha_ff=(180-alpha_f)/(ff-1)
		GOSUB 20
	endif

	ROTY -90

	REVOLVE Num, 360, 1+2+4+8+16+32,
		GET(NSP)
	DEL 2

endif


!---------------------------> oszlop fej <----------------------------

if coc=1 then
	if iOrnamentType = 1 then
		GeomParam1 = 20
		GeomParam2 = 5
		GeomParam3 = 160
		GeomParam4 = 4
		GeomParam5 = 80
	endif

	if iOrnamentType = 2 then
		GeomParam1 = 40
		GeomParam2 = 1.5
		GeomParam3 = 80
		GeomParam4 = 8.5
		GeomParam5 = 20
	endif


	if iOrnamentType = 3 then
		GeomParam1 = 20
		GeomParam2 = 5
		GeomParam3 = 160
		GeomParam4 = 4
		GeomParam5 = 80
	endif

	if iOrnamentType = 4 and iOrnamentType = 5 then
		if iOrnamentType = 4 then
			GeomParam1 = 0
			GeomParam2 = 0.20
			GeomParam3 = 1.2
			GeomParam4 = 2.52
		else
			GeomParam1 = 45
			GeomParam2 = 0.30
			GeomParam3 = 1
			GeomParam4 = 2.23
		endif

		ADDZ zboc+zzyzx

		coldb_1=1.00
		coldt_1=0.90

		MUL coldt/coldt_1, coldt/coldt_1, zcoc/(coldb_1*(7/6))

		if tnum=3 then
			RESOL 1.5*gs_resol  !!24
			CYLIND coldb_1*(7/6)-0.10, (coldt_1*0.95)/2
		endif
		if tnum=2 then
			RESOL gs_resol		!!16
			CONE coldb_1*(7/6)-0.10, (coldt_1*1.00)/2, (coldt_1*1.10)/2, 90, 90
		endif

		aa=coldt_1*1.30: bb=aa: yy=0.08: alpha=180-2*ATN((aa/2)/yy)
		rr=(aa/2)/SIN(alpha)
		ADD 0, -bb/2, coldb_1*(7/6)-0.10
		mm=15*(tnum=3)+7*(tnum=2)

		mmm=mm/2+0.5
		alpha_i=alpha/(mmm-1)
		FOR i=1 TO mmm
			PUT rr*SIN(alpha_i*(i-1)), yy-rr+rr*COS(alpha_i*(i-1)), 8
		NEXT i
		alpha_i=(alpha*2)/(mm-1)
		FOR i=1 TO mm
			PUT aa/2-yy+rr-rr*COS(-alpha+alpha_i*(i-1)), bb/2+rr*SIN(-alpha+alpha_i*(i-1)), 8
		NEXT i
		FOR i=1 TO mm
			PUT rr*SIN(alpha-alpha_i*(i-1)), bb-yy+rr-rr*COS(alpha-alpha_i*(i-1)), 8
		NEXT i
		FOR i=1 TO mm
			PUT -aa/2+yy-rr+rr*COS(alpha-alpha_i*(i-1)), bb/2+rr*SIN(alpha-alpha_i*(i-1)), 8
		NEXT i
		mmm=mm/2+0.5
		alpha_i=alpha/(mmm-1)
		FOR i=1 TO mmm
			PUT rr*SIN(-alpha+alpha_i*(i-1)), yy-rr+rr*COS(-alpha+alpha_i*(i-1)), 8
		NEXT i

		PRISM_ 1+mm*4, 0.10,
			GET (NSP)

		FOR ii=1 TO 4*(tnum=3)+1*(tnum=2)
			if ii=1 AND tnum=3 then
				PUT 0, 0, 0
				PUT -0.001, 0, 0
				PUT -0.001, 0.025, 0
				PUT 0, 0.025, 0
				m=4
			endif

			if ii=1 AND tnum=2 then
				PUT 0, 0, 0
				PUT -0.001, 0, 0
				PUT -0.045, 0.10, 0
				PUT 0, 0.10, 0
				m=4
			endif

			if ii=2 then
				PUT 0, 0.025, 0
				PUT -0.02, 0.025, 0
				PUT -0.02, 0.05, 0
				PUT 0, 0.05, 0
				m=4
			endif

			if ii=3 then
				PUT 0, 0.05, 0
				PUT -0.02, 0.05, 0
				PUT -0.04, 0.075, 0
				PUT 0, 0.075, 0
				m=4
			endif

			if ii=4 then
				PUT 0, 0.075, 0
				PUT -0.04, 0.075, 0
				PUT -0.045, 0.10, 0
				PUT 0, 0.10, 0
				m=4
			endif

			PUT -1, yy, 0, 0
			mmm=mm/2+0.5
			alpha_i=alpha/(mmm-1)
			FOR i=1 TO mmm
				PUT rr*SIN(alpha_i*(i-1)), yy-rr+rr*COS(alpha_i*(i-1)), 0, 0
			NEXT i
			alpha_i=(alpha*2)/(mm-1)
			FOR i=1 TO mm
				PUT aa/2-yy+rr-rr*COS(-alpha+alpha_i*(i-1)), bb/2+rr*SIN(-alpha+alpha_i*(i-1)), 0, 0
			NEXT i
			FOR i=1 TO mm
				PUT rr*SIN(alpha-alpha_i*(i-1)), bb-yy+rr-rr*COS(alpha-alpha_i*(i-1)), 0, 0
			NEXT i
			FOR i=1 TO mm
				PUT -aa/2+yy-rr+rr*COS(alpha-alpha_i*(i-1)), bb/2+rr*SIN(alpha-alpha_i*(i-1)), 0, 0
			NEXT i
			mmm=mm/2+0.5
			alpha_i=alpha/(mmm-1)
			FOR i=1 TO mmm
				PUT rr*SIN(-alpha+alpha_i*(i-1)), yy-rr+rr*COS(-alpha+alpha_i*(i-1)), 0, 0
			NEXT i
			PUT 1, yy, 0, 0

			TUBE m, 3+mm*4, 1+2,
				GET (NSP)
		NEXT ii
		DEL 1


		FOR kk=1 TO Num
			ROTZ GeomParam1+(360/Num)*(kk-1)
			ADDX -0.60

			gg=4
			voluteh=GeomParam2
			eyer=voluteh/16
			eyes=eyer/6
			volut=0.02: volud=coldt*16/13: voludd=0.15
			baset=voludd-volut*2*(tnum=3)

			ADD 0, 0, coldb_1*(7/6)-0.10-eyer*9

			if tnum=3 then
				ADDY -(voludd/2-volut)
				mm=0
				GOSUB 45
				DEL 1
				ADDY (voludd/2)
				GOSUB 45
				DEL 1
			endif

			mm=0
			ADD 0, voludd/2-volut, 0
			GOSUB 35
			DEL 1

			ADD 0, 0, eyer*5
			ROTZ 90
			ROTX 90
			PRISM_ 8, 0.30,
			   voludd/2-volut*(tnum=3), 0, 10,
			   voludd/2-volut*(tnum=3), eyer*3, 10,
			   voludd/2, eyer*3, 10-2*(tnum=2),
			   voludd/2, eyer*4, 10,
			   -voludd/2, eyer*4, 10,
			   -voludd/2, eyer*3, 10,
			   -voludd/2+volut*(tnum=3), eyer*3, 10-2*(tnum=2),
			   -voludd/2+volut*(tnum=3), 0, 10
			DEL 6
		NEXT kk

		if tnum=3 then

			if iOrnamentType = 5 then
				FOR i=1 TO 20
					ROTZ (360/20)*(i-1)
					ADD 0, (coldt_1*0.95)/2, coldb_1*(7/6)-0.25
					RESOL gs_resol
					MULY 0.5
					SPHERE 0.04
					DEL 3
				NEXT i
			endif

			FOR i=1 TO 10
				ROTZ (360/10)*(i-1)
				ADDX -(coldt_1)/2
				MUL 0.67, 1.4, GeomParam3
				CALL "compositefl 80"
				DEL 3
			NEXT i

			FOR i=1 TO 8
				ROTZ (360/8)*(i-1)
				ADDX -(coldt_1*0.95)/2
				MUL 1, 1.4, GeomParam4
				CALL "compositefl 80"
				DEL 3
			NEXT i
		endif

		DEL 2
	else
		Num=0
		ADDZ zboc+zzyzx
		ff_1=11*(tnum=3)+4*(tnum=2)
		ff_2=7*(tnum=3)+3*(tnum=2)
		zz=0

		rr=(1/20)*zcoc
		rrr=rr
		db=(rct+rr)*2
		alpha_f=ACS(rrr/rr)
		ff=ff_2
		alpha_ff=(180-alpha_f)/(ff-1)
		GOSUB 20
		ADDZ rr+rrr
		CIRCLE rct
		DEL 1
		if iOrnamentType = 3 then
			dd=(3/20)*zcoc
			zd=(8/20-1/160)*zcoc
			beta=180-2*(ATN(zd/dd))
			rr=dd+zd/(TAN(beta))
		else
			zz=rr+rrr+(5/20)*zcoc
			rr=(1/20)*zcoc
			db=rct*2
		endif
		rrr=rr
		if iOrnamentType = 3 then
			ff=ff_1
			alpha_ff=beta/(ff-1)
			alpha_f=90-beta
			db=(rct+dd)*2
			zz=zz-(rr-zd)+(1/160)*zcoc
			GOSUB 20
			zz=zz+rr+(6/20)*zcoc
			rr=(3/20)*zcoc
			db=db
		else
			ff=ff_2
			alpha_ff=90/(ff-1)
			GOSUB 22
			ADDZ zz+rr
			CIRCLE db/2+rr
			DEL 1
			zz=zz+rr+(1/GeomParam1)*zcoc
			ADDZ zz
			CIRCLE db/2+rr
			DEL 1
			rr=(GeomParam2/20)*zcoc
			db=(rct+rr+rrr)*2
			rrr=rr
			if iOrnamentType = 2 then
				ff=ff_2
				alpha_ff=90/(ff-1)
				GOSUB 20
				zz=zz+rr
				ADDZ zz
				CIRCLE db/2
				DEL 1
				rr=(8.5/40)*zcoc
				db=db+rr*2
				rrr=rr
			endif
			ff=ff_1
			alpha_ff=90/(ff-1)
			GOSUB 20
			PUT zz+rr, db/2+(1/GeomParam3)*zcoc, 0
			Num=Num+1
			PUT zz+rr+(1/160)*zcoc, db/2+(1/GeomParam3)*zcoc, 1
			Num=Num+1
			if iOrnamentType = 2 then
				PUT zz+rr+(8.5/GeomParam1)*zcoc, db/2+(1/80)*zcoc, 1
				Num=Num+1
			endif
			zz=zz+rr+(GeomParam4/GeomParam1)*zcoc
			rr=(1/20)*zcoc
			db=db+(1/GeomParam5)*zcoc
		endif
		rrr=rr
		ff=ff_2
		alpha_ff=90/(ff-1)
		GOSUB 22
		ADDZ zz+rr
		CIRCLE db/2+rr
		DEL 1
		PUT zz+(1/GeomParam1)*zcoc+rr, db/2+rr, 0
		Num=Num+1

		ROTY -90
		REVOLVE Num, 360, 1+2+4+8+16+32,
				GET(NSP)
		DEL 1


		if iOrnamentType = 3 then
			gg=4

			voluteh=coldb/2
			eyer=voluteh/16
			eyes=eyer/6

			volut=0.015
			volud=coldt*16/13
			voludd=(db/2+rr/4)*2

			baset=voludd-volut*2*(tnum=3)

			ADDZ 4*eyer

			if tnum=3 then
				ADDY -(voludd/2-volut)
				mm=0
				GOSUB 40
				DEL 1
				ADDY (voludd/2)
				mm=0
				GOSUB 40
				DEL 1
			endif

			mm=0
			ADD -volud/2, voludd/2-volut*(tnum=3), 0
			GOSUB 35
			DEL 1
			mm=1
			ADD volud/2, voludd/2-volut*(tnum=3), 0
			GOSUB 35
			DEL 1

			ADD -(volud-eyes*6)/2, 0, eyer*5
			ROTZ 90
			ROTX 90
			PRISM_ 8, (volud-eyes*6),
			   voludd/2-volut*(tnum=3), 0, 10,
			   voludd/2-volut*(tnum=3), eyer*3, 10,
			   voludd/2, eyer*3, 10-2*(tnum=2),
			   voludd/2, eyer*4, 10,
			   -voludd/2, eyer*4, 10,
			   -voludd/2, eyer*3, 10,
			   -voludd/2+volut*(tnum=3), eyer*3, 10-2*(tnum=2),
			   -voludd/2+volut*(tnum=3), 0, 10
			DEL 5
		else
			DEL 1
		endif
	endif
endif


!---------------------------> oszlop test <---------------------------

ADDZ zboc

if not(canelure) then
	omega=360/cannum

	RESOL cannum

	ROTZ omega/2
	ADDZ coldb*(o_1+o_2)
		entnum=6
		gamma=(ACS((coldt/2)/(coldb/2)))/6

	if not(entasis) then
		hh=zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2)
		mm=0
		columnrb=coldb/2 :columnrt=coldt/2
	else
		hh=(zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2))/3
		mm=1
		columnrb=coldb/2 :columnrt=coldb/2
	endif

	eee=0
	PUT 0, columnrb, 0
	PUT hh, columnrt, mm
	eee=eee+2

	if entasis then
		mm=1
		FOR ii=1 TO entnum
			IF ii=entnum THEN mm=0
			hh=((zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2))*2/3)/entnum
			columnr=(coldb/2)*COS(gamma*ii)
			PUT (zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2))/3+hh*ii, columnr, mm
			eee=eee+1
		NEXT ii
	ENDIF

	ROTY -90
	REVOLVE eee, 360, 1+2+4+8+16+32,
		GET(NSP)
	DEL 3

	if iPlacementType <> 1 then CUTEND
	END
endif


GROUP "column_1"
r_b=coldb/2
if not(entasis) OR abs(coldb-coldt)<eps then
	ADDZ coldb*(o_1+o_2)
		r_b=(coldb/2)
		r_t=(coldt/2)
		zz_c=zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2)
		GOSUB 50
	DEL 1
else
	ADDZ coldb*(o_1+o_2)
		r_b=(coldb/2)
		r_t=(coldb/2)
		zz_c=zzyzx/3-coldb*(o_1+o_2)
		GOSUB 50
	DEL 1
	ADDZ zzyzx/3

	FOR i=1 TO enum
		ADDZ ((zzyzx*(2/3))/enum)*(i-1)
		r_b = (coldb/2) * COS(gamma*(i-1))
		r_t = (coldb/2) * COS(gamma*(i))
		zz_c = (zzyzx*(2/3))/enum-coldt*(o_1+o_2)*(i=enum)
		GOSUB 50
		DEL 1
	NEXT i

	DEL 1
endif

ENDGROUP


!-----------------------------> kanelura <----------------------------

cold=coldb

alpha_c=360/cannum
alpha_cc=alpha_c*(percent/100)

canw=(cold/2)*(SIN(alpha_cc/2))
cant=(cold/2)*(COS(alpha_cc/2))

beta_c=180-2*ATN(canw/candb)
canr=canw/(SIN(beta_c))

cantb=cant+(canr-candb)


cold=coldt

alpha_c=360/cannum
alpha_cc=alpha_c*(percent/100)

canw=(cold/2)*(SIN(alpha_cc/2))
cant=(cold/2)*(COS(alpha_cc/2))

beta_c=ASN(canw/canr)
candt=canw/(TAN((180-beta_c)/2))

cantt=cant+(canr-candt)

ss=canr-SQR(canr^2-(canr-candb)^2)
beta_cc=ATN((cantb-cantt)/(zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2))) ! kanelura beforgatas


GROUP "canelure_1"
	RESOL gs_resol	!!16
	ADD cantb, 0, coldb*(o_1+o_2)
	ROTY -beta_cc
	ADDZ canr-ss+0.005
	CYLIND (zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2))/COS(beta_cc)-(canr-ss)*2-0.01, canr
	MULZ 1-0.4*(percent=100)
	SPHERE canr
	DEL 1
	ADDZ (zzyzx-coldb*(o_1+o_2)-coldt*(o_1+o_2))/COS(beta_cc)-(canr-ss)*2-0.01
	MULZ 1-0.4*(percent=100)
	SPHERE canr
	DEL 5
ENDGROUP

GROUP "canelure_2"
	RESOL gs_resol	!!16

	ADD cantb, 0, coldb*(o_1+o_2)+canr-ss+0.005

	PUT 0, 0, -0.10, 0
	PUT 0, 0, 0, 0
	PUT 0, 0, zzyzx/3-coldb*(o_1+o_2)-(canr-ss)-0.005, 0
	FOR iii=1 TO enum+1
		cold=coldb*COS(gamma*iii)
		alpha_c=360/cannum
		alpha_cc=alpha_c*(percent/100)
		canw=(cold/2)*(SIN(alpha_cc/2))
		cant=(cold/2)*(COS(alpha_cc/2))
		beta_c=ASN(canw/canr)
		candt=canw/(TAN((180-beta_c)/2))
		canti=cant+(canr-candt)
		sss=canr-SQR(canr^2-(canr-candb)^2)

		if iii=5 then
			cant5=canti
			cant5=cant5
		endif
		if iii=6 then
			cant6=canti
			beta_cc=atn((cant5-cant6)/(((zzyzx*(2/3))/enum)-(coldt*(o_1+o_2))))
		endif

		PUT -(cantb-canti)+((canr-sss)+0.005)*SIN(beta_cc)*(iii=6), 0, zzyzx/3-coldb*(o_1+o_2)-(canr-ss)-0.005+((zzyzx*(2/3))/enum)*(iii)-(coldt*(o_1+o_2))*(iii>5)-((canr-sss)+0.005)*COS(beta_cc)*(iii=6), 0
	NEXT iii

	TUBE 2, 3+enum+1, 1+2+16+32,
		0, 0, 901,
		canr, 360, 4001,
		GET (NSP)

	MULZ 1-0.4*(percent=100)
	SPHERE canr
	DEL 1
	ADD -((cantb-cant6)-((canr-sss)+0.005)*SIN(beta_cc)), 0, zzyzx-coldb*(o_1+o_2)-(canr-ss)-0.005-coldt*(o_1+o_2)-((canr-sss)+0.005)*COS(beta_cc)
	ROTY -beta_cc
	MULZ 1-0.4*(percent=100)
	SPHERE canr
	DEL 4
ENDGROUP


group_1="canelure_1"
if entasis AND NOT(abs(coldb-coldt)<eps) then group_1="canelure_2"

ccc=SUBGROUP("column_1", group_1)

FOR j=1 TO cannum
	ROTZ (360/cannum)*(j-1)
	PLACEGROUP ccc
	DEL 1
NEXT j

KILLGROUP "column_1"
killgroup "canelure_1"
killgroup "canelure_2"
killgroup ccc


DEL 2


if iPlacementType <> 1 then CUTEND

END ! END ! END ! END ! END ! END ! END ! END ! END ! END ! END ! END
! RETURN ! RETURN ! RETURN ! RETURN ! RETURN ! RETURN ! RETURN ! RETURN



10: !----------------------> oszlop alja-teteje <---------------------
	ADDZ cold*(o_1+o_2)
	CUTPLANE 0: DEL 1
	PUT 0, cold/2+lr-cord, 0
	ff=9*(tnum=3)+3*(tnum=2): alpha_ff=alpha_1/(ff-1)
	FOR i=1 TO ff
		PUT cold*(o_1)+lr-lr*COS(alpha_ff*(i-1)), cold/2+lr-lr*SIN(alpha_ff*(i-1))-cord, 1
	NEXT i
	PUT cold*(o_1)+lr-lr*COS(alpha_1)+lr, cold/2+lr-lr*SIN(alpha_1)-cord+lr*TAN(alpha_1-90), 0
	PUT cold*(o_1)+lr-lr*COS(alpha_1)+lr, 0, 0
	ROTY -90
	REVOLVE 3+ff, 360, 1+2+4+8+16+32,
		GET(NSP)
	DEL 1
	CUTEND
	ADDZ cold*(o_1)
	CIRCLE cold/2+lr-cord: DEL 1
	RETURN

20:
	FOR i=1 TO ff
		PUT zz+rrr-rr*COS(alpha_f+alpha_ff*(i-1)), db/2-rr+rr*SIN(alpha_f+alpha_ff*(i-1)), 1
		Num=Num+1
	NEXT i
	RETURN

21:
	FOR i=1 TO ff
		PUT zz+rr-rr*COS(alpha_ff*(i-1)), db/2-rr*SIN(alpha_ff*(i-1)), 1
		Num=Num+1
	NEXT i
	RETURN

22:
	FOR i=1 TO ff
		PUT zz+rr*SIN(alpha_ff*(i-1)), db/2+rr-rr*COS(alpha_ff*(i-1)), 1
		Num=Num+1
	NEXT i
	RETURN


30:
	ROTZ 180*(mm=1)
	ROTX 90
	ADDZ -(volut)*(mm=1)

	PRISM_ 2, volut,
	   0, 0, 979,
	   eyer, 360, 4079

	ggg=1
	Num=0
	PUT eyes, eyer, 79
	Num=Num+1
	ff=gg-ggg
	rr=eyer-eyes

	for i=1 to 4
		sig1=1
		sig2=1
		if i=1 then sig2=-1
		if i=2 then
			sig1=-1
			sig2=-1
		endif
		if i=3 then sig1=-1
		ff=ff+ggg
		alpha_ff=90/ff
		rr=rr+eyes*2
		FOR j=1 TO ff
			PUT sig1*eyes+rr*SIN((i-1)*90+alpha_ff*j), sig2*eyes+rr*COS((i-1)*90+alpha_ff*j), 79
			Num=Num+1
		NEXT j
	next i

	for k=1 to 2
		PUT eyes, 0, 279
		Num=Num+1
		if k=1 then
			mulnum1=4
			mulnum2=2
		else
			mulnum1=6
			mulnum2=3
		endif
		for i=1 to 4
			sig1=1
			sig2=1
			if i=1 then sig2=-1
			if i=2 then
				sig1=-1
				sig2=-1
			endif
			if i=3 then sig1=-1
			ff=ff+ggg
			alpha_ff=90/ff
			rr=rr+eyes*(mulnum1-(i=1))
			FOR j=1 TO ff
				PUT sig1*eyes*mulnum2+rr*SIN((i-1)*90+alpha_ff*j), sig2*eyes*mulnum2+rr*COS((i-1)*90+alpha_ff*j), 79-(66*(j=ff-1)+71*(j=ff))*(k=2 & i=4)
				Num=Num+1
			NEXT j
		next i
	next k

	PUT eyer, -90, 213
	PUT eyes*0.25, -180, 213
	Num=Num+2

	for i=1 to 4
		sig1=1
		sig2=1
		if i=2 then sig1=-1
		if i=3 then
			sig1=-1
			sig2=-1
		endif
		if i=4 then sig2=-1
		ff=ff+ggg
		alpha_ff=90/ff
		if i=1 then
			rr=rr-eyer
		else
			rr=rr-eyes*5.5
		endif
		FOR j=1 TO ff
			PUT sig1*eyes*2.75-rr*SIN((i-1)*90+alpha_ff*j), sig2*eyes*2.75+rr*COS((i-1)*90+alpha_ff*j), 79
			Num=Num+1
		NEXT j
		ggg=-1
	next i

	for k=1 to 2
		if k=1 then
			mulnum1=3.5
			mulnum2=1.75
		else
			mulnum1=(1.5+(i=1)-(i=4)*0.2)
			mulnum2=0.75
		endif
		PUT eyes, 180, 279
		Num=Num+1

		for i=1 to 4
			sig1=1
			sig2=1
			if i=2 then sig1=-1
			if i=3 then
				sig1=-1
				sig2=-1
			endif
			if i=4 then sig2=-1
			ff=ff+ggg
			alpha_ff=90/ff
			rr=rr-eyes*(mulnum1+(i=1)-(i=4 & k=2)*0.2)
			FOR j=1 TO ff
				PUT sig1*eyes*mulnum2-rr*SIN((i-1)*90+alpha_ff*j), sig2*eyes*mulnum2+rr*COS((i-1)*90+alpha_ff*j), 79
				Num=Num+1
			NEXT j
		next i
	next k

	PUT 0, eyer, 79
	Num=Num+1

	PRISM_ Num, volut,
		GET (NSP)

	DEL 3
	RETURN


35:
	ROTZ 180*(mm=1)
	ROTX 90
	ADDZ -baset*(mm=1)
	ggg=1
	ff=gg+7*ggg

	rr=eyer+eyes*22

	PUT eyes*3, -eyes*3+rr+eyes*5, 79

	for i=1 to 4
		sig1=1
		sig2=1
		if i=1 then sig2=-1
		if i=2 then
			sig1=-1
			sig2=-1
		endif
		if i=3 then sig1=-1
		ff=ff+ggg
		alpha_ff=90/ff
		rr=rr+eyes*(6-(i=1))
		FOR j=1 TO ff
			PUT sig1*eyes*3+rr*SIN((i-1)*90+alpha_ff*j), sig2*eyes*3+rr*COS((i-1)*90+alpha_ff*j), 79-(66*(j=ff-1)+71*(j=ff))*(i=4)
		NEXT j
	next i

	PRISM_ ff*4-ggg*5, baset,
		GET (NSP)
	DEL 3
	RETURN


40:
	ADDX -volud/2
	GOSUB 30
	DEL 1
	mm=1
	ADDX volud/2
	GOSUB 30
	DEL 1
	RETURN


45:
	GOSUB 30
	RETURN


50:
	PUT 0, 0, 1
		PUT r_b*COS(-omega*1/2), r_b*SIN(-omega*1/2), 1
		PUT r_b*COS(omega*1/2), r_b*SIN(omega*1/2), 1

	PUT 0, 0, zz_c
		PUT r_t*COS(-omega*1/2), r_t*SIN(-omega*1/2), zz_c
		PUT r_t*COS(omega*1/2), r_t*SIN(omega*1/2), zz_c

	RULED NSP/6,  1+2+4+64,
		GET (NSP)
	RETURN
