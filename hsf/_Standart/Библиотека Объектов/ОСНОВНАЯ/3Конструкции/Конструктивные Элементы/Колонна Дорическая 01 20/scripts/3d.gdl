
!!Column Doric 01.gsm


IF gs_detlevel_3D_m = DLEV3D_OFF THEN END
IF gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 12

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

PEN gs_cont_pen
RESOL gs_resol

!!---- Hotspot Editing
unID=1
hotspot 0,0,0,unID,zzyzx,1+256 : unID=unID+1
hotspot 0,0,-1,unID,zzyzx,3 : unID=unID+1
hotspot 0,0,zzyzx,unID,zzyzx,2 : unID=unID+1

for i=1 to 4
	hotspot 0,0,0,unID,A2,1+128,A : unID=unID+1
	hotspot -1,0,0,unID,A2,3,A : unID=unID+1
	hotspot A2,0,0,unID,A2,2,A : unID=unID+1

	hotspot 0,B2,zzyzx,unID,A2,1+128,A : unID=unID+1
	hotspot -1,B2,zzyzx,unID,A2,3,A : unID=unID+1
	hotspot A2,B2,zzyzx,unID,A2,2,A : unID=unID+1

	hotspot A2,0,zzyzx,unID,B2,1+128,B : unID=unID+1
	hotspot A2,-1,zzyzx,unID,B2,3,B : unID=unID+1
	hotspot A2,B2,zzyzx,unID,B2,2,B : unID=unID+1
	rotz 90
next i
del 4

ROTY -90
R=ZZYZX/10
S=(A*.999)*.5
T=R/10
MATERIAL gs_column_mat
REVOLVE 19,360,1,
	0,S,1,
	R,S,1,
	R*2,S*.994,1,
	R*3,S*.99,1,
	R*4,S*.975,1,
	R*5,S*.95,1,
	R*6,S*.915,1,
	R*7,S*.87,1,
	R*8,S*.815,1,
	R*9,S*.760,1,
	R*9+T*1.4,S*.760,0,   !COLUMN CAPITAL
	R*9+T*2.6,S*.78,1,
	R*9+T*3.8,S*.82,1,
	R*9+T*4.95,S*.885,1,
	R*9+T*5.83,S*.96,1,
	R*9+T*6.45,S*.999,1,
	R*9+T*7.20,S*.999,1,
	R*9+T*7.67,S*.97,1,
	R*9+T*8,S*.87,1

DEL 1
ADDZ R*9+8*T
MATERIAL gs_capital_mat
PRISM_ 4,ZZYZX-(R*9+8*T),
	-A/2,-A/2,15,
	-A/2,A/2,15,
	A/2,A/2,15,
	A/2,-A/2,15

DEL 1
