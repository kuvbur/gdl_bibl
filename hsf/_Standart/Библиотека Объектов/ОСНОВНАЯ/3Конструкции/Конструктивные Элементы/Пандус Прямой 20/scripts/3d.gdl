
if gs_detlevel_3D_m = 0 then end
if not (gs_shadow) then shadow off

rotz -90


! --- Coordinates for ramp route -----------------------------------------------

if bAscent then
	put 0, -lengthAscent,		heightAscent,			0,
		0, 0,					0,						0,
		0, lengthAscent,		heightAscent,			0,
		0, a-lengthAscent,		zzyzx - heightAscent,	0,
		0, a,					zzyzx,					0,
		0, a + lengthAscent,	zzyzx - heightAscent,	0
else
	put 0, -a,		zzyzx,	0,
		0,  0,		0,		0,
		0,  a,		zzyzx,	0,
		0,  2 * a,	0,		0
endif


! --- Ramp ---------------------------------------------------------------------

material matRamp
tube 4, nsp/4, 1+2+16+32,
		b/2, -thicknessRamp,	0,
		b/2, 0,					0,
	   -b/2, 0,					0,
	   -b/2, -thicknessRamp,	0,

		use (nsp)


! --- Side Flanges -------------------------------------------------------------

if bSideFlange1_right then
	material matSideFlange1_right
	addx b / 2 - widthSideFlange2_right * bSideFlange2_right - widthSideFlange1_right / 2
	tube 4, nsp/4, 1+2+16+32,
			widthSideFlange1_right / 2, 0,							0,
			widthSideFlange1_right / 2, heightSideFlange1_right,	0,
		   -widthSideFlange1_right / 2, heightSideFlange1_right,	0,
		   -widthSideFlange1_right / 2, 0,							0,

			use (nsp)
	del 1
endif

if bSideFlange1_left then
	material matSideFlange1_left
	addx -b / 2 + widthSideFlange2_left * bSideFlange2_left + widthSideFlange1_left / 2
	tube 4, nsp/4, 1+2+16+32,
			widthSideFlange1_left / 2, 0,						0,
			widthSideFlange1_left / 2, heightSideFlange1_left,	0,
		   -widthSideFlange1_left / 2, heightSideFlange1_left,	0,
		   -widthSideFlange1_left / 2, 0,						0,

			use (nsp)
	del 1
endif

if bSideFlange2_right then
	material matSideFlange2_right
	addx b / 2 - widthSideFlange2_right / 2
	tube 4, nsp/4, 1+2+16+32,
			widthSideFlange2_right / 2, 0,							0,
			widthSideFlange2_right / 2, heightSideFlange2_right,	0,
		   -widthSideFlange2_right / 2, heightSideFlange2_right,	0,
		   -widthSideFlange2_right / 2, 0,							0,

			use (nsp)
	del 1
endif

if bSideFlange2_left then
	addx -b / 2 + widthSideFlange2_left / 2
	material matSideFlange2_left
	tube 4, nsp/4, 1+2+16+32,
			widthSideFlange2_left / 2, 0,						0,
			widthSideFlange2_left / 2, heightSideFlange2_left,	0,
		   -widthSideFlange2_left / 2, heightSideFlange2_left,	0,
		   -widthSideFlange2_left / 2, 0,						0,

			use (nsp)
	del 1
endif


del 1

end
