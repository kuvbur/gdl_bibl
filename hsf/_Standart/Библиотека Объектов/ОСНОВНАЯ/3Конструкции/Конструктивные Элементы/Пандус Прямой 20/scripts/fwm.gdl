
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "2CF93B07-9957-4611-9CB4-15170506A575"
	_startID	= "52FFEB90-0CCF-42E3-8ADA-81C1BF5C7195"
	_endID		= "719387E9-73C2-4B45-87BF-898C67C079FA"
gosub "newparameters_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! newparameters
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID | actualGuid = _autoID then

		bHas =	STORED_PAR_VALUE	("bDiffSides", _bDiffSides) &\

				STORED_PAR_VALUE	("widthUsable", _widthUsable) &\
				STORED_PAR_VALUE	("widthSideFlange1", _widthSideFlange1) &\
				STORED_PAR_VALUE	("bSideFlange1", _bSideFlange1) &\
				STORED_PAR_VALUE	("widthSideFlange2", _widthSideFlange2) &\

				STORED_PAR_VALUE	("widthSideFlange1_right", _widthSideFlange1_right) &\
				STORED_PAR_VALUE	("bSideFlange1_right", _bSideFlange1_right) &\
				STORED_PAR_VALUE	("widthSideFlange1_left", _widthSideFlange1_left) &\
				STORED_PAR_VALUE	("bSideFlange1_left", _bSideFlange1_left) &\

				STORED_PAR_VALUE	("widthSideFlange2_right", _widthSideFlange2_right) &\
				STORED_PAR_VALUE	("bSideFlange2_right", _bSideFlange2_right) &\
				STORED_PAR_VALUE	("widthSideFlange2_left", _widthSideFlange2_left) &\
				STORED_PAR_VALUE	("bSideFlange2_left", _bSideFlange2_left)
		if bHas then
			if not (bDiffSides) then
				B = _widthUsable + 2 * widthSideFlange1 * bSideFlange1 + 2 * widthSideFlange2 * bSideFlange2
			else
				B = _widthUsable + widthSideFlange1_right * bSideFlange1_right + widthSideFlange1_left * bSideFlange1_left +\
								  widthSideFlange2_right * bSideFlange2_right + widthSideFlange2_left * bSideFlange2_left
			endif
			parameters B = B
		endif

		iCalculationMethod = 3		! Dimensions
		parameters iCalculationMethod = iCalculationMethod

		bHas = 	DELETED_PAR_VALUE	("rise", _rise) &\
				DELETED_PAR_VALUE	("endRise ", _endRise) &\
				STORED_PAR_VALUE	("bAscent", _bAscent) &\
				STORED_PAR_VALUE	("lengthAscent", _lengthAscent)

		if bHas then
			percentage 			= _rise
			ascentPercentage 	= _endRise

			parameters 	percentage			= percentage,
						ascentPercentage 	= ascentPercentage

			heightAscent = _lengthAscent * ((2 - _bAscent) * _rise / 2) / 100

			parameters heightAscent = heightAscent

		endif

		bHas =	STORED_PAR_VALUE("iFloorPlanDisplayType", _iFloorPlanDisplayType)

		if _iFloorPlanDisplayType = 3 then	! Symbolic

			bHas =	STORED_PAR_VALUE("gs_cont_pen", _gs_cont_pen)
	
			if bHas then
				penBreakLineBelow 	= _gs_cont_pen
				penBreakLineAbove 	= _gs_cont_pen
				gs_cont_pen			= _gs_cont_pen
				overheadPen			= _gs_cont_pen

		
				parameters 	penBreakLineBelow 	= penBreakLineBelow,
							penBreakLineAbove 	= penBreakLineAbove,
							gs_cont_pen			= _gs_cont_pen,
							overheadPen			= overheadPen
			endif
	
			bHas =	STORED_PAR_VALUE("penArrow", _penArrow)
			if bHas then
				penStartSymbolBelow = _penArrow
				penStartSymbolAbove = _penArrow
				penEndSymbolBelow 	= _penArrow
				penEndSymbolAbove 	= _penArrow
				penWalkingBelow 	= _penArrow
				penWalkingAbove 	= _penArrow
		
				parameters 	penStartSymbolBelow = penStartSymbolBelow,
							penStartSymbolAbove = penStartSymbolAbove,
							penEndSymbolBelow 	= penEndSymbolBelow,
							penEndSymbolAbove 	= penEndSymbolAbove,
							penWalkingBelow 	= penWalkingBelow,
							penWalkingAbove 	= penWalkingAbove
			endif

			bHas =	DELETED_PAR_VALUE("symbolType2d_m", _symbolType2d_m) &\
					STORED_PAR_VALUE("lineCont", _lineCont) &\
					STORED_PAR_VALUE("lineDashed", _lineDashed)

			if bHas then
				if _symbolType2d_m = 1 then	! Dashed over Horizontal Cut
					bShowBreakline 		= 1
					lineCont 			= _lineCont	
					lineDashed 			= _lineDashed
					ltBreakLineBelow 	= _lineCont
					ltBreakLineAbove 	= _lineDashed
					ltWalkingBelow 		= _lineCont
					ltWalkingAbove 		= _lineDashed
				else
					lineCont 			= _lineCont	
					lineDashed 			= _lineCont
					ltBreakLineBelow 	= _lineCont
					ltBreakLineAbove 	= _lineCont
					ltWalkingBelow 		= _lineCont
					ltWalkingAbove 		= _lineCont
	
					if _symbolType2d_m = 2 then ! Horizontal Cut
						bShowBreakline = 1
					else	! Complete
						bShowBreakline = 0
					endif
				endif
			endif

			parameters 	bShowBreakline 		= bShowBreakline,
						lineCont 			= lineCont,
						lineDashed 			= lineDashed,	
						ltBreakLineBelow 	= ltBreakLineBelow,
						ltBreakLineAbove 	= ltBreakLineAbove,
						ltWalkingBelow 		= ltWalkingBelow,
						ltWalkingAbove 		= ltWalkingAbove

			bHas =	STORED_PAR_VALUE("arrowSize", _arrowSize)
			if bHas then
				arrowWidth	= _arrowSize * 2
				arrowHeight	= _arrowSize
				iEndSymbol	= 2

				parameters 	arrowWidth	= arrowWidth,
							arrowHeight	= arrowHeight,
							iEndSymbol	= iEndSymbol
			endif
		endif



		actualGuid = _endID
	endif
return

