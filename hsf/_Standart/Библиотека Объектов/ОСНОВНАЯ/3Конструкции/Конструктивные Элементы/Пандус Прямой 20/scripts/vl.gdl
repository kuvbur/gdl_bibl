
call "FM_types" parameters all

hideparameter all "A", "B", "ZZYZX"

values "thicknessRamp" range (0,)
values "ratioLength" range (0, )
values "ratioHeight" range (0, ratioLength]
values "percentage" range (0, 100]

values "heightAscent" range (0, ZZYZX / 2)
if iFloorPlanDisplayType = DISPLAY_SYMBOLIC and bShowBreakline and iWalkingLineType = WALKINGLINE_TRIANGLE then
	values "lengthAscent" range [heightAscent, a/2 - (B*tan(breaklineAngle)/2) )
else
	values "lengthAscent" range [heightAscent, a/2 - 0.1]
endif

values "ratioAscentLength" range (0, )
values "ratioAscentHeight" range (0, ratioAscentLength]
values "ascentPercentage" range (0, 100]

values "arrowWidth" range (0, )
values "arrowHeight" range (0, )
values "widthStartSymbol" range (0, )
values "heightStartSymbol" range (0, )

values "zzyzx" range (heightAscent * 2, )

parameters ac_bottomlevel	= 0
parameters ac_toplevel		= ZZYZX

if GLOB_MODPAR_NAME = "widthStartSymbolHalf" then
	widthStartSymbol = widthStartSymbolHalf * 2
	parameters widthStartSymbol = widthStartSymbol
else
	if GLOB_MODPAR_NAME = "widthStartSymbol" then
		widthStartSymbolHalf = widthStartSymbol / 2
		parameters widthStartSymbolHalf = widthStartSymbolHalf
	endif
endif

if GLOB_MODPAR_NAME = "heightStartSymbolHalf" then
	heightStartSymbol = heightStartSymbolHalf * 2
	parameters heightStartSymbol = heightStartSymbol
else
	if GLOB_MODPAR_NAME = "heightStartSymbol" then
		heightStartSymbolHalf = heightStartSymbol / 2
		parameters heightStartSymbolHalf = heightStartSymbolHalf
	endif
endif

if GLOB_MODPAR_NAME = "arrowWidthHalf" then
	parameters arrowWidth = arrowWidthHalf * 2
else
	parameters arrowWidthHalf 	= arrowWidth / 2
endif

values{2} "iCalculationMethod" 	METHOD_RATIO, 		`Соотношение`,
								METHOD_PERCENTAGE, 	`Проценты`,
								METHOD_DIMENSIONS, 	`Размеры`

values{2} "iTextType1" 	TEXT_PERCENTAGE,				`Проценты`,
						TEXT_RATIO,						`Соотношение`,
						TEXT_SEGMENTLENGTH, 			`Длина Сегмента`,
						TEXT_FULLLENGTH, 				`Полная Длина`,
						TEXT_CUSTOM,					`Специальный`	

if iTextType1 = TEXT_PERCENTAGE then
	values{2} "iTextType2" 	TEXT_RATIO,						`Соотношение`,
							TEXT_SEGMENTLENGTH, 			`Длина Сегмента`,
							TEXT_FULLLENGTH, 				`Полная Длина`,
							TEXT_CUSTOM,					`Специальный`
else
	if iTextType1 = TEXT_RATIO then
		values{2} "iTextType2" 	TEXT_PERCENTAGE,				`Проценты`,
								TEXT_SEGMENTLENGTH, 			`Длина Сегмента`,
								TEXT_FULLLENGTH, 				`Полная Длина`,
								TEXT_CUSTOM,					`Специальный`
	else
		if iTextType1 = TEXT_SEGMENTLENGTH then
			values{2} "iTextType2" 	TEXT_PERCENTAGE,				`Проценты`,
									TEXT_RATIO,						`Соотношение`,
									TEXT_FULLLENGTH, 				`Полная Длина`,
									TEXT_CUSTOM,					`Специальный`	

		else
			if iTextType1 = TEXT_FULLLENGTH then
				values{2} "iTextType2" 	TEXT_PERCENTAGE,				`Проценты`,
										TEXT_RATIO,						`Соотношение`,
										TEXT_SEGMENTLENGTH, 			`Длина Сегмента`,
										TEXT_CUSTOM,					`Специальный`
			else	! TEXT_CUSTOM
				values{2} "iTextType2" 	TEXT_PERCENTAGE,				`Проценты`,
										TEXT_RATIO,						`Соотношение`,
										TEXT_SEGMENTLENGTH, 			`Длина Сегмента`,
										TEXT_FULLLENGTH, 				`Полная Длина`
			endif
		endif
	endif
endif

values{2} "iWalkingLineType" 	WALKINGLINE_SIMPLE,		`Простая Линия`,
								WALKINGLINE_TRIANGLE,	`Треугольник`

values{2} "iStartSymbol" 	1, 	`Стиль 1`,
							2, 	`Стиль 2`,
							3, 	`Стиль 3`

values{2} "iEndSymbol" 	1, 	`Стиль 1`,
						2, 	`Стиль 2`,
						3, 	`Стиль 3`,
						4, 	`Стиль 4`,
						5, 	`Стиль 5`,
						6, 	`Стиль 6`

! --- Usable width - Full width ------------------------------------------------

if GLOB_MODPAR_NAME = "widthUsable" |\
	GLOB_MODPAR_NAME = "bSideFlange1" or GLOB_MODPAR_NAME = "bSideFlange2" |\
	GLOB_MODPAR_NAME = "bSideFlange1_right" or GLOB_MODPAR_NAME = "bSideFlange1_left" |\
	GLOB_MODPAR_NAME = "bSideFlange2_right" or GLOB_MODPAR_NAME = "bSideFlange2_left"  |\

	GLOB_MODPAR_NAME = "widthSideFlange1" |\
	GLOB_MODPAR_NAME = "widthSideFlange1_right" |\
	GLOB_MODPAR_NAME = "widthSideFlange1_left" |\
	GLOB_MODPAR_NAME = "widthSideFlange2" |\
	GLOB_MODPAR_NAME = "widthSideFlange2_right" |\
	GLOB_MODPAR_NAME = "widthSideFlange2_left" then
	if not (bDiffSides) then
		b = widthUsable + 2*widthSideFlange1*bSideFlange1 + 2*widthSideFlange2*bSideFlange2
	else
		b = widthUsable + widthSideFlange1_right*bSideFlange1_right + widthSideFlange1_left*bSideFlange1_left +\
						  widthSideFlange2_right*bSideFlange2_right + widthSideFlange2_left*bSideFlange2_left
	endif
	parameters b = b
else
	if not (bDiffSides) then
		widthUsable = b - 2*widthSideFlange1*bSideFlange1 - 2*widthSideFlange2*bSideFlange2
	else
		widthUsable = b - widthSideFlange1_right*bSideFlange1_right - widthSideFlange1_left*bSideFlange1_left -\
						  widthSideFlange2_right*bSideFlange2_right - widthSideFlange2_left*bSideFlange2_left
	endif
	parameters widthUsable = widthUsable
endif

! --- Dimension of Side Flanges ------------------------------------------------

if not(bDiffSides) then
	parameters 	bSideFlange1_left 		= bSideFlange1,
				widthSideFlange1_left 	= widthSideFlange1,
				heightSideFlange1_left 	= heightSideFlange1,
				matSideFlange1_left 	= matSideFlange1,
				bSideFlange1_right 		= bSideFlange1,
				widthSideFlange1_right 	= widthSideFlange1,
				heightSideFlange1_right = heightSideFlange1,
				matSideFlange1_right 	= matSideFlange1,
				bSideFlange2_left 		= bSideFlange2,
				widthSideFlange2_left 	= widthSideFlange2,
				heightSideFlange2_left 	= heightSideFlange2,
				matSideFlange2_left 	= matSideFlange2,
				bSideFlange2_right 		= bSideFlange2,
				widthSideFlange2_right 	= widthSideFlange2,
				heightSideFlange2_right = heightSideFlange2,
				matSideFlange2_right 	= matSideFlange2
endif

! --- 2D symbol type, 3D detail level ------------------------------------------

values "floorPlanDisplayType" stFloorPlanDisplayType
VALUES "iFloorPlanDisplayType" 	DISPLAY_PROJECTED, \
								DISPLAY_OVERHEAD_PROJ, \
								DISPLAY_SYMBOLIC, \
								DISPLAY_OUTLINES, \
								DISPLAY_OVERHEAD_ALL

if GLOB_MODPAR_NAME = "floorPlanDisplayType" then
	for i = 1 to vardim1(stFloorPlanDisplayType)
	   if floorPlanDisplayType = stFloorPlanDisplayType[i]	then iFloorPlanDisplayType = i
	next i
	parameters iFloorPlanDisplayType = iFloorPlanDisplayType
else
	floorPlanDisplayType = stFloorPlanDisplayType[max(min(iFloorPlanDisplayType, vardim1(stFloorPlanDisplayType)), 1)]
	parameters floorPlanDisplayType = floorPlanDisplayType
endif

if iFloorPlanDisplayType = DISPLAY_SYMBOLIC then
	if not(bShowBreakline) then
		lock 	"lineDashed", "bShowAboveBreak", "penBreakLineBelow", "penBreakLineAbove",
				"ltBreakLineBelow", "ltBreakLineAbove", "bStop", "penWalkingAbove", "ltWalkingAbove", 
				"bShowAboveBreak", "penStartSymbolAbove", "penEndSymbolAbove"
		hideparameter 	"lineDashed", "bShowAboveBreak", "penBreakLineBelow", "penBreakLineAbove",
						"ltBreakLineBelow", "ltBreakLineAbove", "bStop", "penWalkingAbove", "ltWalkingAbove",
						"bShowAboveBreak", "penStartSymbolAbove", "penEndSymbolAbove"
	endif
	if not(bWalkingLine) then
		lock 	"iWalkingLineType", "penWalkingBelow", "ltWalkingBelow", "penWalkingAbove", "ltWalkingAbove", "bReverse", "bStop", "bStartSymbol", "iStartSymbol", 
				"bShowAboveBreak", "penStartSymbolBelow", "penStartSymbolAbove", "widthStartSymbol", "bEndSymbol", "iEndSymbol", "penEndSymbolBelow",
				"penEndSymbolAbove", "arrowWidth", "arrowHeight"
		hideparameter 	"iWalkingLineType", "penWalkingBelow", "ltWalkingBelow", "penWalkingAbove", "ltWalkingAbove", "bReverse", "bStop", "bStartSymbol", "iStartSymbol", 
						"bShowAboveBreak", "penStartSymbolBelow", "penStartSymbolAbove", "widthStartSymbol", "bEndSymbol", "iEndSymbol", "penEndSymbolBelow",
						"penEndSymbolAbove", "arrowWidth", "arrowHeight"
	endif
	
	if iWalkingLineType = WALKINGLINE_TRIANGLE then
		parameters 	bStartSymbol = 0,
					bEndSymbol = 0
		lock "bStartSymbol", "bEndSymbol"
		hideparameter "bStartSymbol", "bEndSymbol"
	endif

	if not(bStartSymbol) then
		lock "iStartSymbol", "bShowAboveBreak", "penStartSymbolBelow", "penStartSymbolAbove", "penStartSymbolBelow", "widthStartSymbol", "heightStartSymbol"
		hideparameter "iStartSymbol", "bShowAboveBreak", "penStartSymbolBelow", "penStartSymbolAbove", "penStartSymbolBelow", "widthStartSymbol", "heightStartSymbol"
	else
		if iStartSymbol <> 3 then
			lock "heightStartSymbol"
			hideparameter "heightStartSymbol"
		else
			lock "penStartSymbolBelow", "penStartSymbolAbove"
			hideparameter "penStartSymbolBelow", "penStartSymbolAbove"
		endif
	endif

	if not(bShowAboveBreak) then
		lock "penStartSymbolAbove"
		hideparameter "penStartSymbolAbove"
	endif

	if not(bEndSymbol) then
		lock "iEndSymbol", "penEndSymbolBelow", "penEndSymbolAbove", "arrowWidth", "arrowHeight"
		hideparameter "iEndSymbol", "penEndSymbolBelow", "penEndSymbolAbove", "arrowWidth", "arrowHeight"
	else
		if iEndSymbol = 1 or iEndSymbol = 3 or iEndSymbol = 5 then
			lock "arrowWidth"
			hideparameter "arrowWidth"
		endif

		if iEndSymbol = 1 or iEndSymbol = 2 then
			lock "penEndSymbolBelow", "penEndSymbolAbove"
			hideparameter "penEndSymbolBelow", "penEndSymbolAbove"
		endif
	endif

	if bStop then
		lock "penWalkingAbove", "ltWalkingAbove", "bShowAboveBreak", "penStartSymbolAbove", "penEndSymbolAbove"
		hideparameter "penWalkingAbove", "ltWalkingAbove", "bShowAboveBreak", "penStartSymbolAbove", "penEndSymbolAbove"
	endif
	if not(bShowText1) then
		lock "iTextType1", "TextPrefix1"
		hideparameter "TextPrefix1", "TextPrefix1"
	endif

	if not(bShowText2) then
		lock "iTextType2", "TextPrefix2"
		hideparameter "TextPrefix2", "TextPrefix2"
	endif

	if (not(bShowText1) and not(bShowText2)) then
		lock "CustomText", "fontType", "tx_size", "tx_size_point", "typeTextRotation_m"
		hideparameter "CustomText", "fontType", "tx_size", "tx_size_point", "typeTextRotation_m"
	endif

 	if (iTextType1 <> 0 and iTextType2 <> 0) or (not(bShowText1) and iTextType1 = 0) or (not(bShowText2) and iTextType2 = 0) then
		lock "CustomText"
		hideparameter "CustomText"
	endif
else
	lock 	"bShowBreakline", "penBreakLineBelow", "ltBreakLineBelow", "penBreakLineAbove", "ltBreakLineAbove", "bWalkingLine",
			"iWalkingLineType", "bReverse", "bStop", "penWalkingBelow", "ltWalkingBelow", "penWalkingAbove", "ltWalkingAbove",
			"bStartSymbol", "bShowAboveBreak", "iStartSymbol", "fillStartSymbol", "penStartSymbolBelow", "penStartSymbolAbove",
			"widthStartSymbol", "widthStartSymbolHalf", "heightStartSymbol", "heightStartSymbolHalf", "bEndSymbol", "iEndSymbol",
			"fillEndSymbol", "penEndSymbolBelow", "penEndSymbolAbove", "arrowWidth", "arrowWidthHalf", "arrowHeight",
			"bShowText1", "iTextType1", "TextPrefix1", "bShowText2", "iTextType2", "TextPrefix2", "CustomText"
	hideparameter  	"bShowBreakline", "penBreakLineBelow", "ltBreakLineBelow", "penBreakLineAbove", "ltBreakLineAbove", "bWalkingLine",
					"iWalkingLineType", "bReverse", "bStop", "penWalkingBelow", "ltWalkingBelow", "penWalkingAbove", "ltWalkingAbove",
					"bStartSymbol", "bShowAboveBreak", "iStartSymbol", "fillStartSymbol", "penStartSymbolBelow", "penStartSymbolAbove",
					"widthStartSymbol", "widthStartSymbolHalf", "heightStartSymbol", "heightStartSymbolHalf", "bEndSymbol", "iEndSymbol",
					"fillEndSymbol", "penEndSymbolBelow", "penEndSymbolAbove", "arrowWidth", "arrowWidthHalf", "arrowHeight",
					"bShowText1", "iTextType1", "TextPrefix1", "bShowText2", "iTextType2", "TextPrefix2", "CustomText"
endif

if gs_detlevel_3d_m = 0 then	! Off
	lock "gs_shadow"
	hideparameter "gs_shadow"
endif

if iFloorPlanDisplayType = DISPLAY_OUTLINES then
	lock			"lineOverhead",
					"overheadPen"
	hideparameter	"lineOverhead",
					"overheadPen"
endif
if iFloorPlanDisplayType = DISPLAY_OVERHEAD_ALL then
	lock			"lineDashed",
					"gs_cont_pen",
					"penText",
					"fontType",
					"tx_size",
					"tx_size_point",
					"typeTextRotation",
					"typeTextRotation_m",
					"bFill",
					"gs_fill_type",
					"gs_fill_pen",
					"gs_back_pen"
	hideparameter	"lineDashed",
					"gs_cont_pen",
					"penText",
					"fontType",
					"tx_size",
					"tx_size_point",
					"typeTextRotation",
					"typeTextRotation_m",
					"bFill",
					"gs_fill_type",
					"gs_fill_pen",
					"gs_back_pen"
endif
if NOT(bFill) then
	lock			"gs_fill_type",
					"gs_fill_pen",
					"gs_back_pen"
	hideparameter	"gs_fill_type",
					"gs_fill_pen",
					"gs_back_pen"
endif

values "gs_detlevel_3D"  `Детальный`,`Откл.`
values "gs_detlevel_3D_m"  2, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3d" then
	if gs_detlevel_3d = `Детальный`	then gs_detlevel_3d_m = 2
	if gs_detlevel_3d = `Откл.`		then gs_detlevel_3d_m = 0
	parameters gs_detlevel_3d_m = gs_detlevel_3d_m
else
	if gs_detlevel_3d_m = 2		then gs_detlevel_3d = `Детальный`
	if gs_detlevel_3d_m = 0		then gs_detlevel_3d = `Откл.`
	parameters gs_detlevel_3d = gs_detlevel_3d
endif

! --- Hiding unused parameters -------------------------------------------------

if not (bDiffSides) then
	hideparameter	"bSideFlange1_left", "bSideFlange1_right", "widthSideFlange1_left",
					"widthSideFlange1_right", "heightSideFlange1_left", "heightSideFlange1_right",
					"matSideFlange1_left", "matSideFlange1_right",
					"bSideFlange2_left", "bSideFlange2_right", "widthSideFlange2_left",
					"widthSideFlange2_right", "heightSideFlange2_left", "heightSideFlange2_right",
					"matSideFlange2_left", "matSideFlange2_right"
else
	hideparameter	"bSideFlange1", "widthSideFlange1", "heightSideFlange1", "matSideFlange1",
					"bSideFlange2", "widthSideFlange2", "heightSideFlange2", "matSideFlange2"
endif


! --- Hiding unused parameters -------------------------------------------------

if not (bSideFlange1) then
	lock "widthSideFlange1", "heightSideFlange1", "matSideFlange1"
	hideparameter "widthSideFlange1", "heightSideFlange1", "matSideFlange1"
endif
if not (bSideFlange2) then
	lock "widthSideFlange2", "heightSideFlange2", "matSideFlange2"
	hideparameter "widthSideFlange2", "heightSideFlange2", "matSideFlange2"
endif

if not (bSideFlange1_left) then
	lock "widthSideFlange1_left", "heightSideFlange1_left", "matSideFlange1_left"
	hideparameter "widthSideFlange1_left", "heightSideFlange1_left", "matSideFlange1_left"
endif
if not (bSideFlange2_left) then
	lock "widthSideFlange2_left", "heightSideFlange2_left", "matSideFlange2_left"
	hideparameter "widthSideFlange2_left", "heightSideFlange2_left", "matSideFlange2_left"
endif

if not (bSideFlange1_right) then
	lock "widthSideFlange1_right", "heightSideFlange1_right", "matSideFlange1_right"
	hideparameter "widthSideFlange1_right", "heightSideFlange1_right", "matSideFlange1_right"
endif
if not (bSideFlange2_right) then
	lock "widthSideFlange2_right", "heightSideFlange2_right", "matSideFlange2_right"
	hideparameter "widthSideFlange2_right", "heightSideFlange2_right", "matSideFlange2_right"
endif

if iCalculationMethod = METHOD_RATIO then
	lock "percentage", "ascentPercentage"
	hideparameter "percentage", "ascentPercentage"
else
	if iCalculationMethod = METHOD_PERCENTAGE then
		lock "ratioLength", "ratioHeight", "ratioAscentLength", "ratioAscentHeight"
		hideparameter "ratioLength", "ratioHeight", "ratioAscentLength", "ratioAscentHeight"
	else	! METHOD_DIMENSIONS
		lock   "percentage", "ascentPercentage", "ratioLength", "ratioHeight", "ratioAscentLength", "ratioAscentHeight"
		hideparameter  "percentage", "ascentPercentage", "ratioLength", "ratioHeight", "ratioAscentLength", "ratioAscentHeight"
	endif
endif

if not(bAscent) then
	lock "lengthAscent", "heightAscent", "ascentPercentage", "ratioAscentLength", "ratioAscentHeight"
	hideparameter "lengthAscent", "heightAscent", "ascentPercentage", "ratioAscentLength", "ratioAscentHeight"
endif

! --- Font Size, Position ----------------------------------------------------------------

values "typeTextRotation" `Читаемый`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	typeTextRotation_m = 2
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	typeTextRotation = `Читаемый`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

Dot_in_mm = 25.4 / 72
if GLOB_MODPAR_NAME = "tx_size" then
	parameters tx_size_point = tx_size / Dot_in_mm
	tx_size_point = tx_size / Dot_in_mm
endif
if GLOB_MODPAR_NAME = "tx_size_point" then
	parameters tx_size = tx_size_point * Dot_in_mm
	tx_size = tx_size_point * Dot_in_mm
endif

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

