
IF gs_detlevel_3D_m = DLEV3D_OFF THEN END
if not(gs_shadow) then shadow off
IF iBeamType <> BEAM_STRAIGHT THEN angy=0

pen gs_cont_pen
resol gs_resol

Unid=1

!!! *** Length

HOTSPOT 0, 0, 0, unID, A, 1+256 : unID=unID+1
HOTSPOT -1, 0, 0, unID, A, 3 : unID=unID+1
HOTSPOT A, 0, 0, unID, A, 2 : unID=unID+1


ROTx angy

!!! *** Flange Width (Bottom)

HOTSPOT 0, -FlangeWBottom/2, 0, unID : unID=unID+1
HOTSPOT 0, FlangeWBottom/2, 0, unID : unID=unID+1
HOTSPOT A, -FlangeWBottom/2, 0, unID : unID=unID+1
HOTSPOT A, FlangeWBottom/2, 0, unID : unID=unID+1

!!! *** Total Height and Flange Width (Top)

h1 = ZZYZX
h2 = ZZYZX
IF iBeamType = BEAM_STRAIGHT THEN
	HOTSPOT 0, 0, 0, unID, ZZYZX, 1+128 : unID=unID+1
	HOTSPOT 0, 0, -1, unID, ZZYZX, 3 : unID=unID+1
	HOTSPOT 0, 0, ZZYZX, unID, ZZYZX, 2 : unID=unID+1

	HOTSPOT 0, -FlangeWTop/2, ZZYZX, unID : unID=unID+1
	HOTSPOT 0, FlangeWTop/2, ZZYZX, unID : unID=unID+1
	HOTSPOT A, -FlangeWTop/2, ZZYZX, unID : unID=unID+1
	HOTSPOT A, FlangeWTop/2, ZZYZX, unID : unID=unID+1
ENDIF

IF iBeamType = BEAM_STRAIGHT OR iBeamType = BEAM_INCLINED THEN
	HOTSPOT A, 0, 0, unID, ZZYZX, 1+128 : unID=unID+1
	HOTSPOT A, 0, -1, unID, ZZYZX, 3 : unID=unID+1
	HOTSPOT A, 0, ZZYZX, unID, ZZYZX, 2 : unID=unID+1
ENDIF

IF iBeamType = BEAM_INCLINED_2WAY THEN
	HOTSPOT A/2, 0, 0, unID, ZZYZX, 1+128 : unID=unID+1
	HOTSPOT A/2, 0, -1, unID, ZZYZX, 3 : unID=unID+1
	HOTSPOT A/2, 0, ZZYZX, unID, ZZYZX, 2 : unID=unID+1
ENDIF

!!! *** End Height and Flange Width (Top)

IF iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY THEN
	h1 = ZZYZX2
	HOTSPOT 0, 0, 0, unID, ZZYZX2, 1+128 : unID=unID+1
	HOTSPOT 0, 0, -1, unID, ZZYZX2, 3 : unID=unID+1
	HOTSPOT 0, 0, ZZYZX2, unID, ZZYZX2, 2 : unID=unID+1

	HOTSPOT 0, -FlangeWTop/2, ZZYZX2, unID : unID=unID+1
	HOTSPOT 0, FlangeWTop/2, ZZYZX2, unID : unID=unID+1

	IF iBeamType = BEAM_INCLINED_2WAY THEN
		h2 = ZZYZX2
		HOTSPOT A, 0, 0, unID, ZZYZX2, 1+128 : unID=unID+1
		HOTSPOT A, 0, -1, unID, ZZYZX2, 3 : unID=unID+1
		HOTSPOT A, 0, ZZYZX2, unID, ZZYZX2, 2 : unID=unID+1

		HOTSPOT A, -FlangeWTop/2, ZZYZX2, unID : unID=unID+1
		HOTSPOT A, FlangeWTop/2, ZZYZX2, unID : unID=unID+1
	ENDIF
ENDIF

UnIDTmp = UnID !!! Compatibility requirement - New hotspots
UnID = 200

!!! >>> Width (Bottom)
HOTSPOT A,0,0,unID,FlangeWBottom2,1+128, FlangeWBottom : unID=unID+1
HOTSPOT A,-FlangeWBottom2,0,unID,FlangeWBottom2,2, FlangeWBottom : unID=unID+1
HOTSPOT A,1,0,unID,FlangeWBottom2,3, FlangeWBottom : unID=unID+1

HOTSPOT A,0,0,unID,FlangeWBottom2,1+128, FlangeWBottom : unID=unID+1
HOTSPOT A,FlangeWBottom2,0,unID,FlangeWBottom2,2, FlangeWBottom : unID=unID+1
HOTSPOT A,-1,0,unID,FlangeWBottom2,3, FlangeWBottom : unID=unID+1

HOTSPOT 0,0,0,unID,FlangeWBottom2,1+128, FlangeWBottom : unID=unID+1
HOTSPOT 0,-FlangeWBottom2,0,unID,FlangeWBottom2,2, FlangeWBottom : unID=unID+1
HOTSPOT 0,1,0,unID,FlangeWBottom2,3, FlangeWBottom : unID=unID+1

HOTSPOT 0,0,0,unID,FlangeWBottom2,1+128, FlangeWBottom : unID=unID+1
HOTSPOT 0,FlangeWBottom2,0,unID,FlangeWBottom2,2, FlangeWBottom : unID=unID+1
HOTSPOT 0,-1,0,unID,FlangeWBottom2,3, FlangeWBottom : unID=unID+1

!!! >>> Width (Top)
HOTSPOT A,0,h2,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT A,-FlangeWTop2,h2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT A,1,h2,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

HOTSPOT A,0,h2,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT A,FlangeWTop2,h2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT A,-1,h2,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

HOTSPOT 0,0,h1,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT 0,-FlangeWTop2,h1,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT 0,1,ZZYZX,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

HOTSPOT 0,0,h1,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT 0,FlangeWTop2,h1,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT 0,-1,h1,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

UnID = UnIDTmp

ROTy 90
ROTz 90
MATERIAL gs_rbeam_mat

if Shape=1 then
	if gs_shap_m = HOLE_RECTANGULAR then
		gosub "holeRectangular"
	endif

	if gs_shap_m = HOLE_CIRCULAR then
		gosub "holeCircular"
	endif
	del 1
endif


IF iBeamType = BEAM_STRAIGHT OR iBeamType = BEAM_INCLINED THEN
	if iBeamType = BEAM_STRAIGHT then ZZYZX2 = ZZYZX
	if supor then
		addz a
		roty 90
		cutpolya 5, 2, 0,
			-1,0,15,
			supowr,0,15,
			supowr,FlangeHgtBottom+SlopeHgtBottom,15,
			-1,FlangeHgtBottom+SlopeHgtBottom,15,
			-1,0,-1
		del 2
	endif

	if supol then
		roty 90
		cutpolya 5, 2, 0,
			1,0,15,
			-supowl,0,15,
			-supowl,FlangeHgtBottom+SlopeHgtBottom,15,
			1,FlangeHgtBottom+SlopeHgtBottom,15,
			1,0,-1
		del 1
	endif

	RULED 12,1+2+4+16+32,
		FlangeWBottom/2,0,0,
		FlangeWBottom/2,FlangeHgtBottom,0,
		FlangeWBottom/2-(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,0,
		FlangeWBottom/2-(FlangeWBottom-WebThk)/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
		FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
		FlangeWTop/2,ZZYZX2,0,
		-FlangeWTop/2,ZZYZX2,0,
		-FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
		-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
		-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,0,
		-FlangeWBottom/2,FlangeHgtBottom,0,
		-FlangeWBottom/2,0,0,
		FlangeWBottom/2,0,A,
		FlangeWBottom/2,FlangeHgtBottom,A,
		FlangeWBottom/2-(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,A,
		FlangeWBottom/2-(FlangeWBottom-WebThk)/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A,
		FlangeWTop/2,ZZYZX-FlangeHgtTop,A,
		FlangeWTop/2,ZZYZX,A,
		-FlangeWTop/2,ZZYZX,A,
		-FlangeWTop/2,ZZYZX-FlangeHgtTop,A,
		-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A,
		-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,A,
		-FlangeWBottom/2,FlangeHgtBottom,A,
		-FlangeWBottom/2,0,A

	If 	supol then cutend
	If 	supor then cutend

else

	if supor then
		addz a
		roty 90
		cutpolya 5, 2, 0,
			-1,0,15,
			supowr,0,15,
			supowr,FlangeHgtBottom+SlopeHgtBottom,15,
			-1,FlangeHgtBottom+SlopeHgtBottom,15,
			-1,0,-1
		del 2
	endif

	if supol then
		roty 90
		cutpolya 5, 2, 0,
			1,0,15,
			-supowl,0,15,
			-supowl,FlangeHgtBottom+SlopeHgtBottom,15,
			1,FlangeHgtBottom+SlopeHgtBottom,15,
			1,0,-1
		del 1
	endif

	for kk=1 to 2
		ruled 12,1+2+4+16,
			FlangeWBottom/2,0,0,
			FlangeWBottom/2,FlangeHgtBottom,0,
			FlangeWBottom/2-(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,0,
			FlangeWBottom/2-(FlangeWBottom-WebThk)/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
			FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
			FlangeWTop/2,ZZYZX2,0,
			-FlangeWTop/2,ZZYZX2,0,
			-FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
			-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
			-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,0,
			-FlangeWBottom/2,FlangeHgtBottom,0,
			-FlangeWBottom/2,0,0,
			FlangeWBottom/2,0,A/2,
			FlangeWBottom/2,FlangeHgtBottom,A/2,
			FlangeWBottom/2-(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,A/2,
			FlangeWBottom/2-(FlangeWBottom-WebThk)/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A/2,
			FlangeWTop/2,ZZYZX-FlangeHgtTop,A/2,
			FlangeWTop/2,ZZYZX,A/2,
			-FlangeWTop/2,ZZYZX,A/2,
			-FlangeWTop/2,ZZYZX-FlangeHgtTop,A/2,
			-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A/2,
			-FlangeWBottom/2+(FlangeWBottom-WebThk)/2,FlangeHgtBottom+SlopeHgtBottom,A/2,
			-FlangeWBottom/2,FlangeHgtBottom,A/2,
			-FlangeWBottom/2,0,A/2
		rotx 180
		rotz 180
		addz -A
	next kk
	del 6

	if supol then cutend
	if supor then cutend

endif

if Shape=1 then
	for i=1 to hnum
		if gs_shap_m = HOLE_RECTANGULAR then cutend
		if gs_shap_m = HOLE_CIRCULAR then cutend
	next i
endif

END

"holeRectangular":
	addz -((a+0.02)/(hnum))/2
	addy pos
	roty 90
	for i=1 to hnum
		HOTSPOT 0,	0,	0,								unID,	gs_hole_pos[i],	1+128	:unID=unID+1 !!base
		HOTSPOT 1,	0,	0,								unID,	gs_hole_pos[i],	3		:unID=unID+1 !!lgth ref
		HOTSPOT -gs_hole_pos[i],	0,	0,	unID,	gs_hole_pos[i],	2		:unID=unID+1 !!lgth mov
		addx -gs_hole_pos[i]-rectw/2
		cutpolya 4, 2, 0,
			0,0,15,
			rectw,0,15,
			rectw,recth,15,
			0,recth,15
		del 1
	next i
	del 2
return

"holeCircular":
	addz -((a+0.02)/(hnum))/2
	addy pos
	roty 90
	for i=1 to hnum
		HOTSPOT 0,	0,	0,								unID,	gs_hole_pos[i],	1+128	:unID=unID+1 !!base
		HOTSPOT 1,	0,	0,								unID,	gs_hole_pos[i],	3		:unID=unID+1 !!lgth ref
		HOTSPOT -gs_hole_pos[i],	0,	0,	unID,	gs_hole_pos[i],	2		:unID=unID+1 !!lgth mov
		addx -gs_hole_pos[i]
		addy cdia/2
		cutpolya 2, 2, 0,
			0,0,979,
			cdia/2,360,4079
		del 2
	next i
	del 2
return

