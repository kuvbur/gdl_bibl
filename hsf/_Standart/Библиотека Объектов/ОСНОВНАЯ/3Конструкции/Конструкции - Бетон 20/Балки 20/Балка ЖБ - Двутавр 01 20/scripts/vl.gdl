
hideparameter all "A", "B", "Zzyzx" 

if GLOB_UI_BUTTON_ID = 1 then
	gs_ui_current_page = 2
	parameters gs_ui_current_page = gs_ui_current_page
endif

if GLOB_UI_BUTTON_ID = 2 then
	gs_ui_current_page = 3
	parameters gs_ui_current_page = gs_ui_current_page
endif

if not(supol) then lock "supowl" 
if not(supor) then lock "supowr" 

call "FM_types" parameters all


values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_OFF, `Откл.`

values{2} "iDet2D" 	DETLEVEL_2D_DETAILED, `Детальный`,
					DETLEVEL_2D_SIMPLE, `Простой`

values "cdia" range [0, min(zzyzx-pos, a-hnum*rectw)]
values "recth" range [0, zzyzx-pos]
values "rectw" range [0, a-hnum*rectw]
values "pos" range [0, zzyzx]

values{2} "iBeamType" BEAM_STRAIGHT, stBeamType[1],
					BEAM_INCLINED, stBeamType[2],
					BEAM_INCLINED_2WAY, stBeamType[3]

values "a" RANGE [0.3, )
values "B" RANGE (0, )
values "ZZYZX" RANGE [FlangeHgtBottom+FlangeHgtTop+SlopeHgtTop+SlopeHgtBottom, )
values "ZZYZX2" RANGE [FlangeHgtBottom+FlangeHgtTop+SlopeHgtTop+SlopeHgtBottom, )
values "WebThk" RANGE (0, )
values "FlangeHgtBottom" RANGE (0, )
values "FlangeHgtTop" RANGE (0, )
values "SlopeHgtTop" RANGE [0, )
values "SlopeHgtBottom" RANGE [0, )

values{2} "iHoleContour2D" 	HOLECONTOUR2D_OFF, `Только Ось`,
							HOLECONTOUR2D_ON, `Ось с Контуром`
							
values "FlangeWBottom" RANGE (WebThk, )
values "FlangeWTop" RANGE (WebThk, )
values "FlangeWBottom2" RANGE (WebThk/2, )
values "FlangeWTop2" RANGE (WebThk/2, )
values "gs_resol" range [3, )

values{2} "gs_shap_m" HOLE_RECTANGULAR, stHoleShape[1],
					HOLE_CIRCULAR, stHoleShape[2]

if not(Shape) and gs_shap_m <> HOLE_CIRCULAR then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

values "angy" RANGE (-90, 90)
values "pos_2Dx" RANGE (zzyzx, a)
values "B2" RANGE (0, )

if LibraryLangCode = "USA" then
	values "ibeam_sectype" "AASHTO",
		"Custom"

	if ibeam_sectype="AASHTO" then
		values "ibeam_type" '28" Type I',
			 '28" Type I Mod',
			 '36" Type II',
			 '45" Type III',
			 '54" Type IV',
			 '63" Type V'
	endif

	! set back to custom if main parameters are changed
	if ibeam_sectype <> "Custom" then
		if GLOB_MODPAR_NAME = "ZZYZX" or GLOB_MODPAR_NAME = "B" or\
			GLOB_MODPAR_NAME = "FlangeWTop" or GLOB_MODPAR_NAME = "WebThk" then
			ibeam_sectype = "Custom"
			parameters ibeam_sectype = ibeam_sectype
		endif
	endif

	! --- Cross-section Data ---
	if ibeam_sectype="AASHTO" then
		if ibeam_type='28" Type I' then
			zzyzx = 28"
			b = 16"
			FlangeWTop = 12"
			WebThk = 6"
		endif
		if ibeam_type='28" Type I Mod' then
			zzyzx = 28"
			b = 18"
			FlangeWTop = 14"
			WebThk = 8"
		endif
		if ibeam_type='36" Type II' then
			zzyzx = 36"
			b = 18"
			FlangeWTop = 12"
			WebThk = 6"
		endif
		if ibeam_type='45" Type III' then
			zzyzx = 45"
			b = 22"
			FlangeWTop = 16"
			WebThk = 7"
		endif
		if ibeam_type='54" Type IV' then
			zzyzx = 54"
			b = 26"
			FlangeWTop = 20"
			WebThk = 8"
		endif
		if ibeam_type='63" Type V' then
			zzyzx = 63"
			b = 28"
			FlangeWTop = 42"
			WebThk = 8"
		endif
		parameters zzyzx = zzyzx, b = b, FlangeWTop = FlangeWTop, WebThk = WebThk
	else
		lock "ibeam_type"
	endif
else
	hideparameter "ibeam_sectype", "ibeam_type"
endif

PARAMETERS B=FlangeWBottom
LOCK "B"

if not(Shape) then
	hideparameter "gs_shap_m",
				"hnum",
				"pos",
				"cdia",
				"rectw",
				"recth",
				"iHoleContour2D"
	lock "gs_shap_m",
		"hnum",
		"pos",
		"cdia",
		"rectw",
		"recth",
		"iHoleContour2D"
endif

IF iBeamType = BEAM_STRAIGHT THEN
	HIDEPARAMETER "ZZYZX2"
ELSE
	HIDEPARAMETER "angy"
ENDIF

if not(supol) then HIDEPARAMETER "supowl"
if not(supor) then HIDEPARAMETER "supowr"

if gs_shap_m = HOLE_RECTANGULAR then
	HIDEPARAMETER "cdia"
	lock "cdia"
endif
if gs_shap_m = HOLE_CIRCULAR then
	HIDEPARAMETER "rectw", "recth"
	lock "rectw", "recth"
endif

if GLOB_MODPAR_NAME = "B2" then
	B = B2 * 2
	parameters B = B
else
	B2 = B / 2
	parameters B2 = B2
endif

if GLOB_MODPAR_NAME = "FlangeWTop2" then
	FlangeWTop = FlangeWTop2 * 2
	parameters FlangeWTop = FlangeWTop
else
	FlangeWTop2 = FlangeWTop / 2
	parameters FlangeWTop2 = FlangeWTop2
endif

if GLOB_MODPAR_NAME = "FlangeWBottom2" then
	FlangeWBottom = FlangeWBottom2 * 2
	parameters FlangeWBottom = FlangeWBottom
else
	FlangeWBottom2 = FlangeWBottom / 2
	parameters FlangeWBottom2 = FlangeWBottom2
endif

IF gs_cont_pen=0 then parameters gs_cont_pen=10
IF gs_fill_pen=0 then parameters gs_fill_pen=19

if not(axis) then
	parameters only_axis=0
	lock "only_axis", "axis_line_type", "axis_pen"
endif
if not(showsect) then
	lock "cross_fill"
endif
if axis and only_axis then
	lock	"gs_fill_type",
				"gs_fill_pen",
				"gs_back_pen",
				"showsect",
				"cross_fill",
				"iDet2D",
				"DetailLineType",
				"gs_cont_pen"
endif

if iDet2D = DETLEVEL_2D_SIMPLE then
	lock "DetailLineType"
endif

IF ABS(angy)>EPS THEN lock	"iDet2D",
							"DetailLineType"!!! >>> angy<>0

es_length = a
es_surface = FlangeWBottom*FlangeHgtBottom+FlangeWTop*FlangeHgtTop+WebThk*(zzyzx-FlangeHgtBottom-FlangeHgtTop)+SlopeHgtBottom*(FlangeWBottom-WebThk)+SlopeHgtTop*(FlangeWTop-WebThk)
incl_volume = 0
if iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY then
	incl_volume = (zzyzx-zzyzx2)*es_length*WebThk
endif
holes_volume = 0
if Shape then
	if gs_shap_m = HOLE_CIRCULAR then
		holes_volume = hnum*(cdia/2)^2*PI*WebThk
	else
		holes_volume = hnum*rectw*recth*WebThk
	endif
endif
es_volume = es_length*es_surface - incl_volume - holes_volume
PARAMETERS es_length=es_length,
		   es_surface = es_surface,
		   es_volume = es_volume

CALL "Precast_quantity_calc" parameters A = A,
	bMetricDimension = bMetricDimension,
	es_density		= es_density,
	es_density_us	= es_density_us,
	es_volume		= es_volume,
	es_surface		= es_surface

