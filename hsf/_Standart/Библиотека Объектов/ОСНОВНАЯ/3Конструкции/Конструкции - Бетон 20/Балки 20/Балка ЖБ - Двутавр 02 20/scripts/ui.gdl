
! =============================================================================
! Localization
! =============================================================================
uiBeamType	= `Тип Балки`
uiRot		= `Угол Поворота`
uiEndHgt	= `Высота в Конце`
uiSupol		= `Выступ со Стороны 1`
uiSupor		= `Выступ со Стороны 2`
uiHole		= `Отверстие`
uiHoleNum	=  `Количество Отверстий`

dim stColShape[]
	stColShape[1] = `Круглая`
	stColShape[2] = `Прямоугольный`

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 			= -1
_idxTab 			= 1
TABID_GEOMETRY 		= _idxTab : _idxTab = _idxTab + 1
TABID_SUPPORT 		= _idxTab : _idxTab = _idxTab + 1
TABID_HOLE	 		= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES        = _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS 	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры балки`
ui_current_page gs_ui_current_page

ui_page TABID_GEOMETRY, TABID_ROOT , `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "geometryPage"

ui_page TABID_SUPPORT, 	TABID_GEOMETRY , `Основание`, "uiTab_Support_flat(1)"
gosub "SupportPage"

ui_page TABID_HOLE, 	TABID_GEOMETRY , `Отверстие`, "uiTab_BeamsHole_flat(1)"
gosub "HolePage":

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Свойства и Описания`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

 "geometryPage":

	x1 = 0
	x2 = 140
	x3 = 230
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 400
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	
	ui_style 0, 0
	
	ui_outfield	uiBeamType ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_infield{3} "iBeamType", 	 x2 - 1,   yCurrent-1, 	x3 - x2 + 1, infieldHeight+2,
				2, 1, 3, 1, ! "BeamType"
				75, 15, 75, 15,
				1, "", BEAM_STRAIGHT,
				2, "", BEAM_INCLINED,
				3, "", BEAM_INCLINED_2WAY

		ui_infield{3} "supol", 	 x4,   yCurrent, 	x6-x4, infieldHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", uiSupol, 0,
				"", uiSupol, 1

	yCurrent = yCurrent + dy


	if iBeamType <> 1 then
		ui_outfield	uiEndHgt ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight
		ui_infield "ZZYZX2", 	 x2,   yCurrent, 	x3 - x2, infieldHeight
	else
		ui_pict 2, x1, yCurrent, 18, 18, 1 !"uiRotangle"
		ui_outfield	uiRot ,x1+20,    yCurrent+4, 	x2 - x1 - 22,	outfieldHeight
		ui_infield "angy", 	 x2,   yCurrent, 	x3 - x2, infieldHeight
	endif

	ui_infield{3} "supor", 	 x4,   yCurrent, 		x6-x4, infieldHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", uiSupor, 0,
				"", uiSupor, 1

	ui_pict_button ui_function, "", "uiFunctionSupport_flat(1)", x7, yCurrent-1, xEnd-x7, infieldHeight+2, 3 !

	yCurrent = yCurrent + dy

	ui_infield{3} "Shape", 	 x4,   yCurrent, 	x6-x4, infieldHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", uiHole, 0,
				"", uiHole, 1

	ui_pict_button ui_function, "", "uiFunctionHole_flat(1).png", x7, yCurrent-1, xEnd-x7, infieldHeight+2, 2 !

	yCurrent = yCurrent + dy

	ui_separator 0, yCurrent, xEnd, yCurrent

	uiPictX = 125
	uiPictY = yCurrent + 24
	uiPictWdt = 120
	uiPictHgt = 145
	uiPictMargin = 3
	uiPictInfWdt = 90

	uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
	uiPicLeftEND = uiPicLeftStart - uiPictMargin

	uiPicTop = uiPictY - uiPictMargin - infieldHeight + 2

	uiPicBelow = uiPictY + uiPictHgt + uiPictMargin

	uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

	UI_PICT 5, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreBeamI02"

	ui_infield "FlangeWTop", uiPictX+20, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Фланца`

	ui_infield "FlangeWBottom", uiPictX+20, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Фланца`

	ui_infield "FlangeHgtTop", uiPicRightStart, uiPictY+8, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Полки`
	ui_infield "SlopeHgtTop1", uiPicRightStart, uiPictY+28, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Уклона`
	ui_infield "SlopeHgtTop2", uiPicRightStart, uiPictY+48, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Уклона`

	ui_infield "WebThk", uiPicRightStart, uiPictY+70, uiPictInfWdt, infieldHeight UI_TOOLTIP `Толщина Стенки`
	ui_infield "SlopeHgtBottom", uiPicRightStart, uiPictY+98, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Уклона`
	ui_infield "FlangeHgtBottom", uiPicRightStart, uiPictY+118, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Полки`

return

! ------------------------------------------------------------------------------
"SupportPage":
! ------------------------------------------------------------------------------
	x1 = 0
	x2 = 140
	x3 = 230
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 400
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	
	ui_style 0, 0
	
	ui_infield{3} "supol", 	 x1,   yCurrent, 	x3-x1, infieldHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", uiSupol, 0,
				"", uiSupol, 1

	yCurrent = yCurrent + dy

	ui_infield{3} "supor", 	 x1,   yCurrent, 	x3-x1, infieldHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", uiSupor, 0,
				"", uiSupor, 1

	yCurrent = yCurrent + dy

	ui_separator 0, yCurrent, xEnd, yCurrent

	uiPictX = 120
	uiPictY = yCurrent + 70
	uiPictWdt = 213
	uiPictHgt = 45
	uiPictMargin = 5
	uiPictInfWdt = 90

	uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
	uiPicLeftEND = uiPicLeftStart - uiPictMargin

	uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3

	uiPicBelow = uiPictY + uiPictHgt + uiPictMargin

	uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

	UI_PICT 6, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreBeamI02_Support"

	ui_infield "supowl", uiPictX-20, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Опорного Выступа`
	ui_infield "supowr", uiPictX+150, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Опорного Выступа`

return

! ------------------------------------------------------------------------------
"HolePage":
! ------------------------------------------------------------------------------
	
	call "ui_beamHole_m" parameters all	Shape			= Shape,
											gs_shap_m		= gs_shap_m,
											hnum			= hnum,
											pos				= pos,
											cdia			= cdia,
											rectw			= rectw,
											recth			= recth,
											iHoleContour2D	= iHoleContour2D


return


! ------------------------------------------------------------------------------
"AppearancePage":
! ------------------------------------------------------------------------------

	dim st2DParamNames[][2]
		st2DParamNames[1][1] = "iDet2D"			: st2DParamNames[1][2] = "ui_list_detailLevel2D_flat(1)"
		st2DParamNames[2][1] = "gs_cont_pen" 	: st2DParamNames[2][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[3][1] = "gs_fill_type" 	: st2DParamNames[3][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[4][1] = "gs_fill_pen" 	: st2DParamNames[4][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[5][1] = "gs_back_pen" 	: st2DParamNames[5][2] = "ui_list_fillBgPen_flat(1)"
		st2DParamNames[6][1] = "DetailLineType" : st2DParamNames[6][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[7][1] = "axis"			: st2DParamNames[7][2] = "ui_list_axis2D_flat(1)"
		st2DParamNames[8][1] = "only_axis" 		: st2DParamNames[8][2] = "ui_list_axis2D_flat(1)"
		st2DParamNames[9][1] = "axis_line_type" : st2DParamNames[9][2] = "ui_list_axisLineType_flat(1)"
		st2DParamNames[10][1] = "axis_pen" 		: st2DParamNames[10][2] = "ui_list_axisLinePen_flat(1)"
		st2DParamNames[11][1] = "showsect" 		: st2DParamNames[11][2] = ""
		if showsect then
			st2DParamNames[12][1] = "cross_fill" 	: st2DParamNames[12][2] = "ui_list_fillType_flat(1)"
		endif

	dim st3DParamNames[3][2]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_resol"			: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
		st3DParamNames[3][1] = "gs_shadow"			: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "gs_rbeam_mat"	: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	file_dependence "ui_list_axis2D_flat(1)"
	file_dependence "ui_list_axisLineType_flat(1)"
	file_dependence "ui_list_axisLinePen_flat(1)"

return

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------

	dim stProp[]
	if LibraryLangCode = "USA" then
		stProp[1] = "es_density_us"
	else
		stProp[1] = "es_density"
	endif
	stProp[2] = "es_length"
	if LibraryLangCode = "USA" then
		stProp[3] = "es_surface_us"
		stProp[4] = "es_volume_us"
		stProp[5] = "es_mass_us"
	else
		stProp[3] = "es_surface"
		stProp[4] = "es_volume"
		stProp[5] = "es_mass"
	endif

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_structure_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										bProp = 1,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stProp		= stProp,
										stParamNames = stParamNames
return

