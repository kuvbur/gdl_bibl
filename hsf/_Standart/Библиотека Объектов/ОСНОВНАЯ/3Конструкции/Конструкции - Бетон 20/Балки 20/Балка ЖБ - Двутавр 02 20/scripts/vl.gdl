
hideparameter all "A", "B", "Zzyzx" 

if GLOB_UI_BUTTON_ID = 3 then
	gs_ui_current_page = 2
	parameters gs_ui_current_page = gs_ui_current_page
endif

if GLOB_UI_BUTTON_ID = 2 then
	gs_ui_current_page = 3
	parameters gs_ui_current_page = gs_ui_current_page
endif

if not(supol) then lock "supowl" 
if not(supor) then lock "supowr" 

call "FM_types" parameters all


IF GLOB_UI_BUTTON_ID = 1 THEN parameters gs_ui_current_page = 1


values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_OFF, `Откл.`

values{2} "iDet2D" 	DETLEVEL_2D_DETAILED, `Детальный`,
					DETLEVEL_2D_SIMPLE, `Простой`

values "cdia" range [0, min(zzyzx-pos, a-hnum*rectw)]
values "recth" range [0, zzyzx-pos]
values "rectw" range [0, a-hnum*rectw]
values "pos" range [0, zzyzx]

values{2} "iBeamType" BEAM_STRAIGHT, stBeamType[1],
					BEAM_INCLINED, stBeamType[2],
					BEAM_INCLINED_2WAY, stBeamType[3]

values "a" RANGE [0.3, )
values "B" RANGE (0, )
values "ZZYZX" RANGE [FlangeHgtBottom+FlangeHgtTop+SlopeHgtTop1+SlopeHgtTop2+SlopeHgtBottom, )
values "ZZYZX2" RANGE [FlangeHgtBottom+FlangeHgtTop+SlopeHgtTop1+SlopeHgtTop2+SlopeHgtBottom, )
values "WebThk" RANGE (0, )
values "FlangeHgtBottom" RANGE (0, )
values "FlangeHgtTop" RANGE (0, )
values "SlopeHgtTop1" RANGE [0, )
values "SlopeHgtTop2" RANGE [0, )
values "SlopeHgtBottom" RANGE [0, )

values{2} "iHoleContour2D" 	HOLECONTOUR2D_OFF, `Только Ось`,
							HOLECONTOUR2D_ON, `Ось с Контуром`

values "gs_resol" range [3, )

values{2} "gs_shap_m" HOLE_RECTANGULAR, stHoleShape[1],
					HOLE_CIRCULAR, stHoleShape[2]

if not(Shape) and gs_shap_m <> HOLE_CIRCULAR then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

values "FlangeWBottom" RANGE (WebThk, )
values "FlangeWTop" RANGE (WebThk, )
values "FlangeWBottom2" RANGE (WebThk/2, )
values "FlangeWTop2" RANGE (WebThk/2, )

values "angy" RANGE (-90, 90)
values "pos_2Dx" RANGE (zzyzx, a)
values "B2" RANGE (0, )

if LibraryLangCode = "USA" then
	values "ibeam_sectype" "Bulb Tee",
		"Custom"
	if ibeam_sectype="Bulb Tee" then
		VALUES "ibeam_type" '45" Bulb Tee',
			'54" Bulb Tee',
			'56" Bulb Tee',
			'63" Bulb Tee',
			'65" Bulb Tee',
			'72" Bulb Tee',
			'74" Bulb Tee',
			'81" Bulb Tee'
	endif

	! set back to custom if main parameters are changed
	if ibeam_sectype <> "Custom" then
		if GLOB_MODPAR_NAME = "ZZYZX" or GLOB_MODPAR_NAME = "B" or\
			GLOB_MODPAR_NAME = "FlangeWBottom" or GLOB_MODPAR_NAME = "WebThk" or\
			GLOB_MODPAR_NAME = "FlangeHgtBottom" or GLOB_MODPAR_NAME = "FlangeHgtTop" then
			ibeam_sectype = "Custom"
			parameters ibeam_sectype = ibeam_sectype
		endif
	endif

	! --- Cross-section Data ---
	if ibeam_sectype="Bulb Tee" then
		if ibeam_type='45" Bulb Tee' then
			zzyzx = 45"
			FlangeWBottom = 26"
			b = 30"
			WebThk = 6"
			FlangeHgtBottom = 7.5"
			FlangeHgtTop = 6"
		endif
		if ibeam_type='54" Bulb Tee' then
			zzyzx = 54"
			FlangeWBottom = 26"
			b = 42"
			WebThk = 6"
			FlangeHgtBottom = 6"
			FlangeHgtTop = 3.5"
		endif
		if ibeam_type='56" Bulb Tee' then
			zzyzx = 56"
			FlangeWBottom = 26"
			b = 42"
			WebThk = 6"
			FlangeHgtBottom = 8"
			FlangeHgtTop = 3.5"
		endif
		if ibeam_type='63" Bulb Tee' then
			zzyzx = 63"
			FlangeWBottom = 26"
			b = 42"
			WebThk = 6"
			FlangeHgtBottom = 6"
			FlangeHgtTop = 3.5"
		endif
		if ibeam_type='65" Bulb Tee' then
			zzyzx = 65"
			FlangeWBottom = 26"
			b = 42"
			WebThk = 6"
			FlangeHgtBottom = 8"
			FlangeHgtTop = 3.5"
		endif
		if ibeam_type='72" Bulb Tee' then
			zzyzx = 72"
			FlangeWBottom = 26"
			b = 42"
			WebThk = 6"
			FlangeHgtBottom = 6"
			FlangeHgtTop = 3.5"
		endif
		if ibeam_type='74" Bulb Tee' then
			zzyzx = 74"
			FlangeWBottom = 26"
			b = 42"
			WebThk = 6"
			FlangeHgtBottom = 8"
			FlangeHgtTop = 3.5"
		endif
		if ibeam_type='81" Bulb Tee' then
			zzyzx = 81"
			FlangeWBottom = 26"
			b = 42"
			WebThk = 6"
			FlangeHgtBottom = 8"
			FlangeHgtTop = 3.5"
		endif
		parameters zzyzx = zzyzx, FlangeWBottom = FlangeWBottom, b = b, WebThk = WebThk, FlangeHgtBottom = FlangeHgtBottom, FlangeHgtTop = FlangeHgtTop
	else
		lock "ibeam_type"
	endif
else
	hideparameter "ibeam_sectype", "ibeam_type"
endif

PARAMETERS B=FlangeWBottom
LOCK "B"

if not(Shape) then
	hideparameter "gs_shap_m",
				"hnum",
				"pos",
				"cdia",
				"rectw",
				"recth",
				"iHoleContour2D"
	lock "gs_shap_m",
		"hnum",
		"pos",
		"cdia",
		"rectw",
		"recth",
		"iHoleContour2D"
endif

IF iBeamType = BEAM_STRAIGHT THEN
	HIDEPARAMETER "ZZYZX2"
ELSE
	HIDEPARAMETER "angy"
ENDIF

if not(supol) then HIDEPARAMETER "supowl"
if not(supor) then HIDEPARAMETER "supowr"

if gs_shap_m = HOLE_RECTANGULAR then
	HIDEPARAMETER "cdia"
	lock "cdia"
endif
if gs_shap_m = HOLE_CIRCULAR then
	HIDEPARAMETER "rectw", "recth"
	lock "rectw", "recth"
endif

if GLOB_MODPAR_NAME = "B2" then
	B = B2 * 2
	parameters B = B
else
	B2 = B / 2
	parameters B2 = B2
endif

if GLOB_MODPAR_NAME = "FlangeWTop2" then
	FlangeWTop = FlangeWTop2 * 2
	parameters FlangeWTop = FlangeWTop
else
	FlangeWTop2 = FlangeWTop / 2
	parameters FlangeWTop2 = FlangeWTop2
endif

if GLOB_MODPAR_NAME = "FlangeWBottom2" then
	FlangeWBottom = FlangeWBottom2 * 2
	parameters FlangeWBottom = FlangeWBottom
else
	FlangeWBottom2 = FlangeWBottom / 2
	parameters FlangeWBottom2 = FlangeWBottom2
endif

IF gs_cont_pen=0 then parameters gs_cont_pen=10
IF gs_fill_pen=0 then parameters gs_fill_pen=19

if not(axis) then
	parameters only_axis=0
	lock "only_axis", "axis_line_type", "axis_pen"
endif
if not(showsect) then
	lock "cross_fill"
endif
if axis and only_axis then
	lock	"gs_fill_type",
				"gs_fill_pen",
				"gs_back_pen",
				"showsect",
				"cross_fill",
				"iDet2D",
				"DetailLineType",
				"gs_cont_pen"
endif

if iDet2D = DETLEVEL_2D_SIMPLE then
	lock "DetailLineType"
endif

IF ABS(angy)>EPS THEN lock	"iDet2D",
							"DetailLineType"!!! >>> angy<>0

es_length = a
es_surface = FlangeWBottom*FlangeHgtBottom+FlangeWTop*FlangeHgtTop+WebThk*(zzyzx-FlangeHgtBottom-FlangeHgtTop)+SlopeHgtBottom*(FlangeWBottom-WebThk)+SlopeHgtTop2^2+SlopeHgtTop1*(FlangeWTop-WebThk-2*SlopeHgtTop2)
incl_volume = 0
if iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY then
	incl_volume = (zzyzx-zzyzx2)*es_length*WebThk
endif
holes_volume = 0
if Shape then
	if gs_shap_m = HOLE_CIRCULAR then
		holes_volume = hnum*(cdia/2)^2*PI*WebThk
	else
		holes_volume = hnum*rectw*recth*WebThk
	endif
endif
es_volume = es_length*es_surface - incl_volume - holes_volume
PARAMETERS es_length=es_length,
		   es_surface = es_surface,
		   es_volume = es_volume

CALL "Precast_quantity_calc" parameters A = A,
	bMetricDimension = bMetricDimension,
	es_density		= es_density,
	es_density_us	= es_density_us,
	es_volume		= es_volume,
	es_surface		= es_surface

