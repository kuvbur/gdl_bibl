
if iBeamType <> BEAM_STRAIGHT then angy=0

Unid=1
hLineID = 1000

! --- Length

HOTSPOT2 0, 0, unID, A, 1+256 : unID=unID+1
HOTSPOT2 -1, 0, unID, A, 3 : unID=unID+1
HOTSPOT2 A, 0, unID, A, 2 : unID=unID+1

! --- Length/2

HOTSPOT2 A/2,0,unID : unID=unID+1

FlangeWBottom = 0
FlangeWidthMax=MAX(FlangeWBottom,FlangeWTop)
FlangeWidthMin=MIN(FlangeWBottom,FlangeWTop)


if only_axis=0 AND ABS(angy)<EPS then
	! --- Flange Width (MAX)
	HOTSPOT2 0,FlangeWidthMax/2,unID : unID=unID+1
	HOTSPOT2 0,-FlangeWidthMax/2,unID : unID=unID+1

	HOTSPOT2 A,FlangeWidthMax/2,unID : unID=unID+1
	HOTSPOT2 A,-FlangeWidthMax/2,unID : unID=unID+1

	if iDet2D = DETLEVEL_2D_DETAILED then
		! --- Flange Width (MIN)
		HOTSPOT2 0,FlangeWidthMin/2,unID : unID=unID+1
		HOTSPOT2 0,-FlangeWidthMin/2,unID : unID=unID+1

		HOTSPOT2 A,FlangeWidthMin/2,unID : unID=unID+1
		HOTSPOT2 A,-FlangeWidthMin/2,unID : unID=unID+1

		! --- Web Thickness
		HOTSPOT2 0,WebThk/2,unID : unID=unID+1
		HOTSPOT2 0,-WebThk/2,unID : unID=unID+1

		HOTSPOT2 A,WebThk/2,unID : unID=unID+1
		HOTSPOT2 A,-WebThk/2,unID : unID=unID+1
	endif
endif

!!! *** Width
IF ABS(angy)<EPS AND only_axis=0 THEN		!! angy=0
	IF iDet2D = DETLEVEL_2D_DETAILED THEN
		UnIDTmp = UnID !!! Compatibility requirement - New hotspots
		UnID = 200

		!!! >>> Web Thickness
		HOTSPOT2 A,0,unID,WebThk2,1+128, WebThk : unID=unID+1
		HOTSPOT2 A,-WebThk2,unID,WebThk2,2, WebThk : unID=unID+1
		HOTSPOT2 A,1,unID,WebThk2,3, WebThk : unID=unID+1

		HOTSPOT2 A,0,unID,WebThk2,1+128, WebThk : unID=unID+1
		HOTSPOT2 A,WebThk2,unID,WebThk2,2, WebThk : unID=unID+1
		HOTSPOT2 A,-1,unID,WebThk2,3, WebThk : unID=unID+1

		HOTSPOT2 0,0,unID,WebThk2,1+128, WebThk : unID=unID+1
		HOTSPOT2 0,-WebThk2,unID,WebThk2,2, WebThk : unID=unID+1
		HOTSPOT2 0,1,unID,WebThk2,3, WebThk : unID=unID+1

		HOTSPOT2 0,0,unID,WebThk2,1+128, WebThk : unID=unID+1
		HOTSPOT2 0,WebThk2,unID,WebThk2,2, WebThk : unID=unID+1
		HOTSPOT2 0,-1,unID,WebThk2,3, WebThk : unID=unID+1

		!!! >>> Width (Top)
		HOTSPOT2 A,0,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
		HOTSPOT2 A,-FlangeWTop2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
		HOTSPOT2 A,1,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

		HOTSPOT2 A,0,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
		HOTSPOT2 A,FlangeWTop2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
		HOTSPOT2 A,-1,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

		HOTSPOT2 0,0,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
		HOTSPOT2 0,-FlangeWTop2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
		HOTSPOT2 0,1,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

		HOTSPOT2 0,0,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
		HOTSPOT2 0,FlangeWTop2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
		HOTSPOT2 0,-1,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

		UnID = UnIDTmp !!! Compatibility requirement - New hotspots
	ENDIF
ENDIF

PEN gs_cont_pen
FILL gs_fill_type


if ABS(angy)<EPS then		!!! >>> angy=0
	ADD2 0, -FlangeWidthMax/2

	if only_axis=0 then
		POLY2_B 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			A, 0, 1,
			a, FlangeWidthMax, 1,
			0, FlangeWidthMax, 1,
			0, 0, -1

		if iBeamType = BEAM_INCLINED_2WAY then
			if iDet2D = DETLEVEL_2D_DETAILED THEN
				LINE2 A/2,FlangeWidthMax/2+FlangeWTop/2,A/2,FlangeWidthMax/2-FlangeWTop/2
			else
				LINE2 A/2,0,A/2,FlangeWidthMax
			endif
		endif

		if iDet2D = DETLEVEL_2D_DETAILED then
			LINE_TYPE DetailLineType
				LINE2 0,FlangeWidthMax/2+WebThk/2,a,FlangeWidthMax/2+WebThk/2
				LINE2 0,FlangeWidthMax/2-WebThk/2,a,FlangeWidthMax/2-WebThk/2
			LINE_TYPE 1

		endif
	endif

	DEL 1
else
	if only_axis=0 then
		PROJECT2{2} 3, 270, 3+32,
			gs_back_pen,0,0,0
	endif
endif


if showsect AND only_axis=0 then
	LINE_TYPE 1
	HOTSPOT2 0, 0, unID, pos_2Dx, 1+128 : unID=unID+1
	HOTSPOT2 0, -1', unID, pos_2Dx, 3 : unID=unID+1
	HOTSPOT2 pos_2Dx, 0, unID, pos_2Dx, 2 : unID=unID+1
	ADD2 pos_2Dx, 0
	ROT2 90+angy

	if iBeamType = BEAM_STRAIGHT then ZZYZX2=ZZYZX

	FILL cross_fill
	POLY2 8,7,
		WebThk/2,0,
		WebThk/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,
		FlangeWTop/2,ZZYZX2-FlangeHgtTop,
		FlangeWTop/2,ZZYZX2,
		-FlangeWTop/2,ZZYZX2,
		-FlangeWTop/2,ZZYZX2-FlangeHgtTop,
		-WebThk/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,
		-WebThk/2,0

	if iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY then
		POLY2 8,1,
			WebThk/2,0,
			WebThk/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,
			FlangeWTop/2,ZZYZX-FlangeHgtTop,
			FlangeWTop/2,ZZYZX,
			-FlangeWTop/2,ZZYZX,
			-FlangeWTop/2,ZZYZX-FlangeHgtTop,
			-WebThk/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,
			-WebThk/2,0
	endif
	DEL 2
endif


if axis then
	PEN axis_pen
	LINE_TYPE axis_line_type
	HOTLINE2 0, 0, a, 0, hLineID
	LINE2 0, 0, a, 0
endif

if Shape then
	PEN gs_cont_pen
	add2 -((a+0.02)/(hnum))/2,0
	for i=1 to hnum
		add2 gs_hole_pos[i],0
		line2 0,-b/2-.07,0,b/2+.07
		del 1
	next i

	if iHoleContour2D = HOLECONTOUR2D_ON then
		if gs_shap_m = HOLE_RECTANGULAR then
			holeOffset = rectw
		else
			holeOffset = cdia
		endif
		add2 -holeOffset/2,0
		for i=1 to hnum
			add2 gs_hole_pos[i],0
			line2 0,-b/2,0,b/2
			line2 holeOffset,-b/2,holeOffset,b/2
			del 1
		next i
		del 1
	endif
	del 1
endif
