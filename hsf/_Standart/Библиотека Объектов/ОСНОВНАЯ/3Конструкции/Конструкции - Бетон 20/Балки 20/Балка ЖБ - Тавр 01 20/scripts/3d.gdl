
IF gs_detlevel_3D_m = DLEV3D_OFF THEN END
if not(gs_shadow) then shadow off
IF iBeamType <> BEAM_STRAIGHT THEN angy=0

PEN gs_cont_pen
resol gs_resol

unID = 1

!!! *** Length

HOTSPOT 0, 0, 0, unID, A, 1+256 : unID=unID+1
HOTSPOT -1, 0, 0, unID, A, 3 : unID=unID+1
HOTSPOT A, 0, 0, unID, A, 2 : unID=unID+1


ROTx angy

!!! *** WebThk (Bottom)

HOTSPOT 0, -WebThk/2, 0, unID : unID=unID+1
HOTSPOT 0, WebThk/2, 0, unID : unID=unID+1
HOTSPOT A, -WebThk/2, 0, unID : unID=unID+1
HOTSPOT A, WebThk/2, 0, unID : unID=unID+1

!!! *** Total Height and Flange Width (Top)

h1 = ZZYZX
h2 = ZZYZX
IF iBeamType = BEAM_STRAIGHT THEN
	HOTSPOT 0, 0, 0, unID, ZZYZX, 1+128 : unID=unID+1
	HOTSPOT 0, 0, -1, unID, ZZYZX, 3 : unID=unID+1
	HOTSPOT 0, 0, ZZYZX, unID, ZZYZX, 2 : unID=unID+1

	HOTSPOT 0, -FlangeWTop/2, ZZYZX, unID : unID=unID+1
	HOTSPOT 0, FlangeWTop/2, ZZYZX, unID : unID=unID+1
	HOTSPOT A, -FlangeWTop/2, ZZYZX, unID : unID=unID+1
	HOTSPOT A, FlangeWTop/2, ZZYZX, unID : unID=unID+1

ENDIF

IF iBeamType = BEAM_STRAIGHT OR iBeamType = BEAM_INCLINED THEN
	HOTSPOT A, 0, 0, unID, ZZYZX, 1+128 : unID=unID+1
	HOTSPOT A, 0, -1, unID, ZZYZX, 3 : unID=unID+1
	HOTSPOT A, 0, ZZYZX, unID, ZZYZX, 2 : unID=unID+1
ENDIF

IF iBeamType = BEAM_INCLINED_2WAY THEN
	HOTSPOT A/2, 0, 0, unID, ZZYZX, 1+128 : unID=unID+1
	HOTSPOT A/2, 0, -1, unID, ZZYZX, 3 : unID=unID+1
	HOTSPOT A/2, 0, ZZYZX, unID, ZZYZX, 2 : unID=unID+1
ENDIF

!!! *** End Height and Flange Width (Top)

IF iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY THEN
	h1 = ZZYZX2
	HOTSPOT 0, 0, 0, unID, ZZYZX2, 1+128 : unID=unID+1
	HOTSPOT 0, 0, -1, unID, ZZYZX2, 3 : unID=unID+1
	HOTSPOT 0, 0, ZZYZX2, unID, ZZYZX2, 2 : unID=unID+1

	HOTSPOT 0, -FlangeWTop/2, ZZYZX2, unID : unID=unID+1
	HOTSPOT 0, FlangeWTop/2, ZZYZX2, unID : unID=unID+1

	IF iBeamType = BEAM_INCLINED_2WAY THEN
	   h2 = ZZYZX2
		HOTSPOT A, 0, 0, unID, ZZYZX2, 1+128 : unID=unID+1
		HOTSPOT A, 0, -1, unID, ZZYZX2, 3 : unID=unID+1
		HOTSPOT A, 0, ZZYZX2, unID, ZZYZX2, 2 : unID=unID+1

		HOTSPOT A, -FlangeWTop/2, ZZYZX2, unID : unID=unID+1
		HOTSPOT A, FlangeWTop/2, ZZYZX2, unID : unID=unID+1

	ENDIF
ENDIF

UnIDTmp = UnID !!! Compatibility requirement - New hotspots
UnID = 200

!!! >>> Web Thickness
HOTSPOT A,0,0,unID,WebThk2,1+128, WebThk : unID=unID+1
HOTSPOT A,-WebThk2,0,unID,WebThk2,2, WebThk : unID=unID+1
HOTSPOT A,1,unID,0,WebThk2,3, WebThk : unID=unID+1

HOTSPOT A,0,0,unID,WebThk2,1+128, WebThk : unID=unID+1
HOTSPOT A,WebThk2,0,unID,WebThk2,2, WebThk : unID=unID+1
HOTSPOT A,-1,0,unID,WebThk2,3, WebThk : unID=unID+1

HOTSPOT 0,0,0,unID,WebThk2,1+128, WebThk : unID=unID+1
HOTSPOT 0,-WebThk2,0,unID,WebThk2,2, WebThk : unID=unID+1
HOTSPOT 0,1,0,unID,WebThk2,3, WebThk : unID=unID+1

HOTSPOT 0,0,0,unID,WebThk2,1+128, WebThk : unID=unID+1
HOTSPOT 0,WebThk2,0,unID,WebThk2,2, WebThk : unID=unID+1
HOTSPOT 0,-1,0,unID,WebThk2,3, WebThk : unID=unID+1

!!! >>> Width (Top)
HOTSPOT A,0,h2,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT A,-FlangeWTop2,h2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT A,1,h2,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

HOTSPOT A,0,h2,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT A,FlangeWTop2,h2,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT A,-1,h2,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

HOTSPOT 0,0,h1,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT 0,-FlangeWTop2,h1,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT 0,1,ZZYZX,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

HOTSPOT 0,0,h1,unID,FlangeWTop2,1+128, FlangeWTop : unID=unID+1
HOTSPOT 0,FlangeWTop2,h1,unID,FlangeWTop2,2, FlangeWTop : unID=unID+1
HOTSPOT 0,-1,h1,unID,FlangeWTop2,3, FlangeWTop : unID=unID+1

UnID = UnIDTmp

ROTy 90
ROTz 90
MATERIAL gs_rbeam_mat

if Shape=1 then
	if gs_shap_m = HOLE_RECTANGULAR then
		gosub "holeRectangular"
	endif

	if gs_shap_m = HOLE_CIRCULAR then
		gosub "holeCircular"
	endif
	del 1
endif

IF iBeamType = BEAM_STRAIGHT OR iBeamType = BEAM_INCLINED THEN

	IF iBeamType = BEAM_STRAIGHT THEN ZZYZX2=ZZYZX

		If 	supor=1 then
			valtx=supohr/TAN(90-supanr)

			add -FlangeWTop,0,a
			roty 90
			cutpolya 5, 2, 0,
				0,0,15,
				supowr+valtx,0,15,
				supowr,supohr,15,
				0,supohr,15,
				0,0,-1
			del 2
		endif

		If 	supol=1 then
			valtx=supohl/TAN(90-supanl)

			roty -90
			add 0,0,-FlangeWTop

			cutpolya 5, 2,0,
				0,0,15,
				supowl+valtx,0,15,
				supowl,supohl,15,
				0,supohl,15,
				0,0,-1
			del 2
		endif

	RULED 8,1+2+4+16+32,
		WebThk/2,0,0,
		WebThk/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
		FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
		FlangeWTop/2,ZZYZX2,0,
		-FlangeWTop/2,ZZYZX2,0,
		-FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
		-WebThk/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
		-WebThk/2,0,0,
		WebThk/2,0,A,
		WebThk/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A,
		FlangeWTop/2,ZZYZX-FlangeHgtTop,A,
		FlangeWTop/2,ZZYZX,A,
		-FlangeWTop/2,ZZYZX,A,
		-FlangeWTop/2,ZZYZX-FlangeHgtTop,A,
		-WebThk/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A,
		-WebThk/2,0,A

	If 	supor=1 then cutend
	If 	supol=1 then cutend

ELSE

	FOR kk=1 TO 2
		RULED 8,1+2+4+16,
			WebThk/2,0,0,
			WebThk/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
			FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
			FlangeWTop/2,ZZYZX2,0,
			-FlangeWTop/2,ZZYZX2,0,
			-FlangeWTop/2,ZZYZX2-FlangeHgtTop,0,
			-WebThk/2,ZZYZX2-FlangeHgtTop-SlopeHgtTop,0,
			-WebThk/2,0,0,
			WebThk/2,0,A/2,
			WebThk/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A/2,
			FlangeWTop/2,ZZYZX-FlangeHgtTop,A/2,
			FlangeWTop/2,ZZYZX,A/2,
			-FlangeWTop/2,ZZYZX,A/2,
			-FlangeWTop/2,ZZYZX-FlangeHgtTop,A/2,
			-WebThk/2,ZZYZX-FlangeHgtTop-SlopeHgtTop,A/2,
			-WebThk/2,0,A/2
		ROTx 180
		ROTz 180
		ADDz -A
	NEXT kk
	DEL 6

ENDIF

if Shape=1 then
	for i=1 to hnum
		if gs_shap_m = HOLE_RECTANGULAR then cutend
		if gs_shap_m = HOLE_CIRCULAR then cutend
	next i
endif

DEL 2
DEL 1
END

"holeRectangular":

	addz -((a+0.02)/(hnum))/2
	addy pos
	roty 90
	for i=1 to hnum
		HOTSPOT 0,	0,	0,					unID,	gs_hole_pos[i],	1+128	:unID=unID+1 !!base
		HOTSPOT 1,	0,	0,					unID,	gs_hole_pos[i],	3		:unID=unID+1 !!lgth ref
		HOTSPOT -gs_hole_pos[i],	0,	0,	unID,	gs_hole_pos[i],	2		:unID=unID+1 !!lgth mov
		addx -gs_hole_pos[i]-rectw/2
		cutpolya 4, 2, 0,
			0,0,15,
			rectw,0,15,
			rectw,recth,15,
			0,recth,15
		del 1
	next i
	del 2
return

"holeCircular":

	addz -((a+0.02)/(hnum))/2
	addy pos
	roty 90
	for i=1 to hnum
		HOTSPOT 0,	0,	0,								unID,	gs_hole_pos[i],	1+128	:unID=unID+1 !!base
		HOTSPOT 1,	0,	0,								unID,	gs_hole_pos[i],	3		:unID=unID+1 !!lgth ref
		HOTSPOT -gs_hole_pos[i],	0,	0,	unID,	gs_hole_pos[i],	2		:unID=unID+1 !!lgth mov
		addx -gs_hole_pos[i]
		addy cdia/2
		cutpolya 2, 2, 0,
			0,0,979,
			cdia/2,360,4079
		del 2
	next i
	del 2
return

