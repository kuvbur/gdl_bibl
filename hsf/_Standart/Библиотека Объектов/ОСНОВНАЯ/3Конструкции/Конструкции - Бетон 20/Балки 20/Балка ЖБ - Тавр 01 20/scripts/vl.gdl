
hideparameter all "A", "B", "Zzyzx" 

if GLOB_UI_BUTTON_ID = 1 then
	gs_ui_current_page = 2
	parameters gs_ui_current_page = gs_ui_current_page
endif

if GLOB_UI_BUTTON_ID = 2 then
	gs_ui_current_page = 3
	parameters gs_ui_current_page = gs_ui_current_page
endif

if not(supol) then lock "supowl"  , "supohl", "supanl"
if not(supor) then lock "supowr" , "supohr", "supanr"

call "FM_types" parameters all


values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_OFF, `Откл.`
values{2} "iDet2D" 	DETLEVEL_2D_DETAILED, `Детальный`,
					DETLEVEL_2D_SIMPLE, `Простой`

values "cdia" range [0, min(zzyzx-pos, a-hnum*rectw)]
values "recth" range [0, zzyzx-pos]
values "rectw" range [0, a-hnum*rectw]
values "pos" range [0, zzyzx]

values "BeamType" stBeamType
values "iBeamType" BEAM_STRAIGHT, BEAM_INCLINED, BEAM_INCLINED_2WAY

if GLOB_MODPAR_NAME = "BeamType" then
	iBeamType = BEAM_STRAIGHT
	if BeamType = stBeamType[2] then iBeamType = BEAM_INCLINED
	if BeamType = stBeamType[3] then iBeamType = BEAM_INCLINED_2WAY
	parameters iBeamType = iBeamType
else
	BeamType = stBeamType[1]
	if iBeamType = BEAM_INCLINED then BeamType = stBeamType[2]
	if iBeamType = BEAM_INCLINED_2WAY then BeamType = stBeamType[3]
	parameters BeamType = BeamType
endif

VALUES "a" RANGE [0.3, )
VALUES "B" RANGE (0, )
VALUES "ZZYZX" RANGE [FlangeHgtTop+SlopeHgtTop+SlopeHgtBottom, )
VALUES "ZZYZX2" RANGE [FlangeHgtTop+SlopeHgtTop+SlopeHgtBottom, )
VALUES "WebThk" RANGE (0, FlangeWTop)
VALUES "FlangeHgtTop" RANGE (0, )
VALUES "SlopeHgtTop" RANGE [0, )
VALUES "SlopeHgtBottom" RANGE [0, )

values{2} "iHoleContour2D" 	HOLECONTOUR2D_OFF, `Только Ось`,
							HOLECONTOUR2D_ON, `Ось с Контуром`

values "gs_resol" range [3, )

values "shap" stHoleShape
values "gs_shap_m" HOLE_RECTANGULAR, HOLE_CIRCULAR

IF GLOB_MODPAR_NAME = "shap" then
	gs_shap_m = HOLE_RECTANGULAR
	if shap = stHoleShape[2] then gs_shap_m = HOLE_CIRCULAR
	parameters gs_shap_m = gs_shap_m
else
	shap = stHoleShape[1]
	if gs_shap_m = 2 then shap = stHoleShape[2]
	parameters shap = shap
endif

if not(Shape) and gs_shap_m <> HOLE_CIRCULAR then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

VALUES "FlangeWTop" RANGE (WebThk, )
VALUES "WebThk2" RANGE (0,FlangeWTop/2)
VALUES "FlangeWTop2" RANGE (WebThk/2, )

VALUES "angy" RANGE (-90, 90)
VALUES "pos_2Dx" RANGE (zzyzx, a)
VALUES "B2" RANGE (0, )

PARAMETERS B=FlangeWTop
LOCK "B"

if not(Shape) then
	hideparameter	"shap",
				"gs_shap_m",
				"hnum",
				"pos",
				"cdia",
				"rectw",
				"recth",
				"iHoleContour2D"
	lock	"shap",
		"gs_shap_m",
		"hnum",
		"pos",
		"cdia",
		"rectw",
		"recth",
		"iHoleContour2D"
endif

if supol=0 then HIDEPARAMETER "supowl","supanl","supohl"
if supor=0 then HIDEPARAMETER "supowr","supanr","supohr"

if gs_shap_m = HOLE_RECTANGULAR then
	HIDEPARAMETER "cdia"
	lock "cdia"
endif
if gs_shap_m = HOLE_CIRCULAR then
	HIDEPARAMETER "rectw", "recth"
	lock "rectw", "recth"
endif

IF iBeamType = BEAM_STRAIGHT THEN
	HIDEPARAMETER "ZZYZX2"
	ELSE
	HIDEPARAMETER "angy"
ENDIF

if GLOB_MODPAR_NAME = "B2" then
	B = B2 * 2
	parameters B = B
else
	B2 = B / 2
	parameters B2 = B2
endif

if GLOB_MODPAR_NAME = "FlangeWTop2" then
	FlangeWTop = FlangeWTop2 * 2
	parameters FlangeWTop = FlangeWTop
else
	FlangeWTop2 = FlangeWTop / 2
	parameters FlangeWTop2 = FlangeWTop2
endif

if GLOB_MODPAR_NAME = "WebThk2" then
	WebThk = WebThk2 * 2
	parameters WebThk = WebThk
else
	WebThk2 = WebThk / 2
	parameters WebThk2 = WebThk2
endif

IF gs_cont_pen=0 then parameters gs_cont_pen=10
IF gs_fill_pen=0 then parameters gs_fill_pen=19

if not(axis) then
	parameters only_axis=0
	lock "only_axis", "axis_line_type", "axis_pen"
endif
if not(showsect) then
	lock "cross_fill"
endif
if axis and only_axis then
	lock	"gs_fill_type",
				"gs_fill_pen",
				"gs_back_pen",
				"showsect",
				"cross_fill",
				"iDet2D",
				"DetailLineType",
				"gs_cont_pen"
endif

if iDet2D = DETLEVEL_2D_SIMPLE then
	lock "DetailLineType"
endif

IF ABS(angy)>EPS THEN lock	"iDet2D",
							"DetailLineType"!!! >>> angy<>0

es_length=a
es_surface = b*zzyzx
incl_volume = 0
if iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY then
	incl_volume = (zzyzx-zzyzx2)*es_length*WebThk
endif
holes_volume = 0
if Shape then
	if gs_shap_m = HOLE_CIRCULAR then
		holes_volume = hnum*(cdia/2)^2*PI*b
	else
		holes_volume = hnum*rectw*recth*b
	endif
endif
es_volume = es_length*es_surface - incl_volume - holes_volume
PARAMETERS es_length=es_length,
		   es_surface = es_surface,
		   es_volume = es_volume

CALL "Precast_quantity_calc" parameters A = A,
	bMetricDimension = bMetricDimension,
	es_density		= es_density,
	es_density_us	= es_density_us,
	es_volume		= es_volume,
	es_surface		= es_surface
