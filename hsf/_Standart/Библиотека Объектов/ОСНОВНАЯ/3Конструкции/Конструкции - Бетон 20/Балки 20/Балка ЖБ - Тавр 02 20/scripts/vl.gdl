
hideparameter all "A", "B", "Zzyzx" 

call "FM_types" parameters all

values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_OFF, `Откл.`
values{2} "iDet2D" 	DETLEVEL_2D_DETAILED, `Детальный`,
					DETLEVEL_2D_SIMPLE, `Простой`

VALUES "a" RANGE (0.3, )
LOCK "b"
VALUES "angy" RANGE (-90, 90)
VALUES "itee_w1" RANGE [itee_w2+0.05, )
VALUES "itee_w2" RANGE [0.05, itee_w1-0.05]
VALUES "itee_h1" RANGE [0.05, zzyzx-0.1]
VALUES "zzyzx" RANGE [0.2, )
VALUES "pos_2Dx" RANGE (zzyzx, a)
VALUES "B2" RANGE [(itee_w2+0.05)/2, )
VALUES "w22" RANGE [(0.05)/2, (itee_w1-0.05)/2]

if GLOB_MODPAR_NAME ="B2" then
	 itee_w1 = B2*2
	PARAMETERS itee_w1 = itee_w1
else
	B2 = itee_w1/2
	PARAMETERS B2 = B2
endif

IF GLOB_MODPAR_NAME ="w22" then
	itee_w2 = w22 * 2
	PARAMETERS itee_w2 = itee_w2
else
	w22 = itee_w2/2
	PARAMETERS w22 = w22
endif

if LibraryLangCode = "USA" then
	VALUES "itee_sectype" "26ITxx",
		"28ITxx",
		"34ITxx",
		"40ITxx",
		"Custom"

	if itee_sectype = "26ITxx" then
		VALUES "itee_type" "26IT20/8",
			 "26IT24/8",
			 "26IT28/8",
			 "26IT24/12",
			 "26IT28/12",
			 "26IT32/12",
			 "26IT32/20",
			 "26IT36/20",
			 "26IT40/20",
			 "26IT36/24",
			 "26IT40/24",
			 "26IT44/24",
			 "26IT44/32",
			 "26IT48/32",
			 "26IT52/32"
	endif

	if itee_sectype = "28ITxx" then
		VALUES "itee_type" "28IT20/12",
			 "28IT24/12",
			 "28IT28/16",
			 "28IT32/20",
			 "28IT36/24",
			 "28IT40/24",
			 "28IT44/28",
			 "28IT48/32",
			 "28IT52/36",
			 "28IT56/40",
			 "28IT60/44"
	endif

	if itee_sectype = "34ITxx" then
		VALUES "itee_type" "34IT20/12",
			 "34IT24/12",
			 "34IT28/16",
			 "34IT32/20",
			 "34IT36/24",
			 "34IT40/24",
			 "34IT44/28",
			 "34IT48/32",
			 "34IT52/36",
			 "34IT56/40",
			 "34IT60/44"
	endif

	if itee_sectype = "40ITxx" then
		VALUES "itee_type" "40IT20/12",
			 "40IT24/12",
			 "40IT28/16",
			 "40IT32/20",
			 "40IT36/24",
			 "40IT40/24",
			 "40IT44/28",
			 "40IT48/32",
			 "40IT52/36"
	endif

	if itee_sectype="Custom" then
		lock "itee_type"
	endif

	if GLOB_MODPAR_NAME = "itee_type" or GLOB_MODPAR_NAME = "itee_sectype" then
		if itee_sectype <> "Custom" then
			qq0= ""
			qq1= ""
			qq= SPLIT (itee_type, "%n%s%n%s%n", xx, qq0, yy, qq1, hh)
			zzyzx = yy*0.0254
			itee_w1 = xx*0.0254
			itee_w2 =xx*0.0254-16"
			itee_h1 =hh*0.0254
			parameters zzyzx = zzyzx, itee_w1 = itee_w1,\
					   itee_w2 = itee_w2, itee_h1 = itee_h1
		endif
	endif

	! set back to custom if main parameters are changed
	if itee_sectype <> "Custom" then
		if GLOB_MODPAR_NAME = "ZZYZX" or GLOB_MODPAR_NAME = "itee_w1" or\
			GLOB_MODPAR_NAME = "itee_w2" or GLOB_MODPAR_NAME = "itee_h1" then
			itee_sectype = "Custom"
			parameters itee_sectype = itee_sectype
		endif
	endif
else
	hideparameter "itee_sectype", "itee_type"
endif

if not(axis) then
	parameters only_axis=0
	lock "only_axis", "axis_line_type", "axis_pen"
endif
if not(showsect) then
	lock "cross_fill"
endif
if axis and only_axis then
	lock	"gs_fill_type",
				"gs_fill_pen",
				"gs_back_pen",
				"showsect",
				"cross_fill",
				"iDet2D",
				"DetailLineType",
				"gs_cont_pen"
endif

if iDet2D = DETLEVEL_2D_SIMPLE OR ABS(angy)<EPS then	!! angy=0
	lock "DetailLineType"
endif


FOR w=1 TO NSP/3
	PUT GET (2)
	qq= GET (1)
NEXT w
IF NSP>0 THEN
	PUT 1
	PUT GET (NSP-1)
ENDIF

IF gs_cont_pen=0 then parameters gs_cont_pen=10
IF gs_fill_pen=0 then parameters gs_fill_pen=19

es_length=a
es_surface = b*zzyzx - (b-itee_w2)*itee_h1
es_volume = es_surface*es_length
PARAMETERS es_length=es_length,
		   es_surface = es_surface,
		   es_volume = es_volume

CALL "Precast_quantity_calc" parameters A = A,
	bMetricDimension = bMetricDimension,
	es_density		= es_density,
	es_density_us	= es_density_us,
	es_volume		= es_volume,
	es_surface		= es_surface
