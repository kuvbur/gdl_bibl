
hideparameter all "A", "B", "Zzyzx" 

if GLOB_UI_BUTTON_ID = 1 then
	parameters gs_ui_current_page = 2
endif

if GLOB_UI_BUTTON_ID = 2 then
	parameters gs_ui_current_page = 3
endif

call "FM_types" parameters all

values "a" RANGE (0.3, )
values "angy" RANGE (-90, 90)

values "pos_2Dx" RANGE (zzyzx, a)
values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_OFF, `Откл.`
values{2} "iDet2D" 	DETLEVEL_2D_DETAILED, `Детальный`,
					DETLEVEL_2D_SIMPLE, `Простой`

values{2} "iHoleContour2D" 	HOLECONTOUR2D_OFF, `Только Ось`,
							HOLECONTOUR2D_ON, `Ось с Контуром`

values "cdia" range [0, min(zzyzx-pos, a-hnum*rectw)]
values "recth" range [0, zzyzx-pos]
values "rectw" range [0, a-hnum*rectw]
values "pos" range [0, zzyzx]
values "gs_resol" range [3, )

values "BeamType" stBeamType
values "iBeamType" BEAM_STRAIGHT, BEAM_INCLINED, BEAM_INCLINED_2WAY

if GLOB_MODPAR_NAME = "BeamType" then
	iBeamType = BEAM_STRAIGHT
	if BeamType = stBeamType[2] then iBeamType = BEAM_INCLINED
	if BeamType = stBeamType[3] then iBeamType = BEAM_INCLINED_2WAY
	parameters iBeamType = iBeamType
else
	BeamType = stBeamType[1]
	if iBeamType = BEAM_INCLINED then BeamType = stBeamType[2]
	if iBeamType = BEAM_INCLINED_2WAY then BeamType = stBeamType[3]
	parameters BeamType = BeamType
endif

if LibraryLangCode = "USA" then
	values "rbeam_sectype" "12RBxx",
		"16RBxx",
		"18RBxx",
		"Custom"

	if rbeam_sectype="12RBxx" then
		values "rbeam_type" "12RB16",
			 "12RB20",
			 "12RB24",
			 "12RB28",
			 "12RB32",
			 "12RB36"
	endif

	if rbeam_sectype="16RBxx" then
		values "rbeam_type" "16RB24",
			"16RB28",
			"16RB32",
			"16RB36",
			"16RB40"
	endif

	if rbeam_sectype="18RBxx" then
		values "rbeam_type" "18RB24",
			"18RB28",
			"18RB32",
			"18RB36"
	endif

	if rbeam_sectype="Custom" then
		lock "rbeam_type"
	endif

	if GLOB_MODPAR_NAME = "rbeam_type" or GLOB_MODPAR_NAME = "rbeam_sectype" then
		if rbeam_sectype<>"Custom" then
			qq0= ""
			qq= SPLIT (rbeam_type, "%n%s%n", xx, qq0, yy)
			zzyzx = yy*0.0254
			rbeam_w1 = xx*0.0254
			rbeam_w2 = xx*0.0254
			parameters zzyzx = zzyzx, rbeam_w1 = rbeam_w1, rbeam_w2 = rbeam_w2
		endif
	endif

	! set back to custom if main parameters are changed
	if rbeam_sectype <> "Custom" then
		if GLOB_MODPAR_NAME = "ZZYZX" or GLOB_MODPAR_NAME = "rbeam_w1" or\
			GLOB_MODPAR_NAME = "rbeam_w2" then
			rbeam_sectype = "Custom"
			parameters rbeam_sectype = rbeam_sectype
		endif
	endif
else
	hideparameter "rbeam_sectype", "rbeam_type"
endif

values "shap" stHoleShape
values "gs_shap_m" HOLE_RECTANGULAR, HOLE_CIRCULAR

IF GLOB_MODPAR_NAME = "shap" then
	gs_shap_m = HOLE_RECTANGULAR
	if shap = stHoleShape[2] then gs_shap_m = HOLE_CIRCULAR
	parameters gs_shap_m = gs_shap_m
else
	shap = stHoleShape[1]
	if gs_shap_m = 2 then shap = stHoleShape[2]
	parameters shap = shap
endif

if not(Shape) and gs_shap_m <> HOLE_CIRCULAR then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

if not(Shape) then
	hideparameter	"shap",
				"gs_shap_m",
				"hnum",
				"pos",
				"cdia",
				"rectw",
				"recth",
				"iHoleContour2D"
	lock	"shap",
		"gs_shap_m",
		"hnum",
		"pos",
		"cdia",
		"rectw",
		"recth",
		"iHoleContour2D"
endif

if gs_shap_m = HOLE_RECTANGULAR then
	HIDEPARAMETER "cdia"
	lock "cdia"
endif
if gs_shap_m = HOLE_CIRCULAR then
	HIDEPARAMETER "rectw", "recth"
	lock "rectw", "recth"
endif

if not(axis) then
	parameters only_axis=0
	lock "only_axis", "axis_line_type", "axis_pen"
endif
if not(showsect) then
	lock "cross_fill"
endif
if axis and only_axis then
	lock	"gs_fill_type",
				"gs_fill_pen",
				"gs_back_pen",
				"showsect",
				"cross_fill",
				"iDet2D",
				"DetailLineType",
				"gs_cont_pen"
endif

if iDet2D = DETLEVEL_2D_SIMPLE then
	lock "DetailLineType"
endif

IF iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY THEN
	hideparameter	"iDet2D",
				"DetailLineType"
	lock	"iDet2D",
		"DetailLineType"
endif
IF iBeamType = BEAM_STRAIGHT THEN
	if iDet2D = DETLEVEL_2D_SIMPLE or (ABS(angy)<EPS and ABS(rbeam_w2-rbeam_w1)<EPS) THEN LOCK "DetailLineType"
ENDIF


IF iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY THEN
	PUT 2
	PUT -b/2+0.001, 0, 1,
		-b/2+0.001, zzyzx2, 1
	GOSUB 0
	ns= NSP
	PUT 2
	PUT -b/2+0.001, 0, 1,
		-b/2+0.001, zzyzx, 1
	PUT GET (ns)
	GOSUB 0
ENDIF

FOR w=1 TO NSP/3
	PUT GET (2)
	qq= GET (1)
NEXT w

IF NSP>0 THEN
	PUT 1+1*(iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY)
	PUT GET (NSP-1)
ENDIF

if iBeamType = BEAM_STRAIGHT then
	if GLOB_MODPAR_NAME = "w12" then
		parameters rbeam_w1 = w12*2
	else
		parameters w12 = rbeam_w1/2
	endif

	if GLOB_MODPAR_NAME = "w22" then
		parameters rbeam_w2 = w22*2
	else
		parameters w22 = rbeam_w2/2
	endif

	B = max(rbeam_w1, rbeam_w2)
	parameters B = B
	parameters B2 = B/2
endif

if iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY then
	if GLOB_MODPAR_NAME = "B2" then
		parameters B = B2*2
	else
		parameters B2 = B/2
	endif

	rbeam_w1 = B
	parameters rbeam_w1 = rbeam_w1
	parameters w12 = rbeam_w1/2
endif

IF gs_cont_pen=0 then parameters gs_cont_pen=10
IF gs_fill_pen=0 then parameters gs_fill_pen=19

es_length = a
es_surface = (rbeam_w1+rbeam_w2)/2*zzyzx
holes_volume = 0
incl_volume = 0
if iBeamType = BEAM_INCLINED | iBeamType = BEAM_INCLINED_2WAY then
	incl_volume = (zzyzx-zzyzx2)*es_length*b
endif
if Shape then
	if gs_shap_m = HOLE_CIRCULAR then
		holes_volume = hnum*(cdia/2)^2*PI*(rbeam_w1+rbeam_w2)/2
	else
		holes_volume = hnum*rectw*recth*(rbeam_w1+rbeam_w2)/2
	endif
endif
es_volume = es_length*es_surface - incl_volume - holes_volume
PARAMETERS es_length=es_length,
		   es_surface = es_surface,
		   es_volume = es_volume

CALL "Precast_quantity_calc" parameters A = A,
	bMetricDimension = bMetricDimension,
	es_density		= es_density,
	es_density_us	= es_density_us,
	es_volume		= es_volume,
	es_surface		= es_surface
