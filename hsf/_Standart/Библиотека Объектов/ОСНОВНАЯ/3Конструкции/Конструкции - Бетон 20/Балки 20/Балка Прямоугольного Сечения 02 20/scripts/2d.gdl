
Unid=1
hLineID = 1000

! --- Length

HOTSPOT2 0, 0, unID, A, 1+256 : unID=unID+1
HOTSPOT2 -1, 0, unID, A, 3 : unID=unID+1
HOTSPOT2 A, 0, unID, A, 2 : unID=unID+1

! --- Length/2

HOTSPOT2 A/2,0,unID : unID=unID+1

! --- Extension (Right)

HOTSPOT2 0, 0, unID, ExtRight, 1+256 : unID=unID+1
HOTSPOT2 -1, 0, unID, ExtRight, 3 : unID=unID+1
HOTSPOT2 ExtRight, 0, unID, ExtRight, 2 : unID=unID+1

! --- Extension (Left)

HOTSPOT2 A, 0, unID, ExtLeft, 1+256 : unID=unID+1
HOTSPOT2 A+1, 0, unID, ExtLeft, 3 : unID=unID+1
HOTSPOT2 A-ExtLeft, 0, unID, ExtLeft, 2 : unID=unID+1

! --- Beam Width
if ABS(angy)<EPS AND only_axis=0 then	!! angy=0

	HOTSPOT2 A,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 A,-b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 A,1,unID,B2,3, B : unID=unID+1

	HOTSPOT2 A,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 A,b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 A,-1,unID,B2,3, B : unID=unID+1

	HOTSPOT2 0,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 0,-b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 0,1,unID,B2,3, B : unID=unID+1

	HOTSPOT2 0,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 0,b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 0,-1,unID,B2,3, B : unID=unID+1

endif


PEN gs_cont_pen
FILL gs_fill_type

if ABS(angy)<EPS then		!!! >>> angy=0
	ADD2 0, -B/2

	if only_axis=0 then

		POLY2_B 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			A, 0, 1,
			a, B, 1,
			0, B, 1,
			0, 0, -1


		LINE2 ExtRight,0,ExtRight,B
		LINE2 A-ExtLeft,0,A-ExtLeft,B
	endif

	DEL 1

else
	if only_axis=0 then
		PROJECT2{2} 3, 270, 3+32,
			gs_back_pen,0,0,0
	endif
endif


if showsect AND only_axis=0 then
	LINE_TYPE 1
	HOTSPOT2 0, 0, unID, pos_2Dx, 1+128 : unID=unID+1
	HOTSPOT2 0, -1', unID, pos_2Dx, 3 : unID=unID+1
	HOTSPOT2 pos_2Dx, 0, unID, pos_2Dx, 2 : unID=unID+1
	ADD2 pos_2Dx, 0
	ROT2 90+angy
	FILL cross_fill
	POLY2 4,7,
		-B/2,0,
		B/2,0,
		B/2,ZZYZX,
		-B/2,ZZYZX


	DEL 2
endif


if axis then
	PEN axis_pen
	LINE_TYPE axis_line_type
	HOTLINE2 0, 0, a, 0, hLineID
	LINE2 0, 0, a, 0
endif

if Shape then
	PEN gs_cont_pen
	add2 ExtRight,0
	for i=1 to hnum
		add2 gs_hole_pos[i],0
		line2 0,-b/2-.07,0,b/2+.07
		del 1
	next i

	if iHoleContour2D = HOLECONTOUR2D_ON then
		if gs_shap_m = HOLE_RECTANGULAR then
			holeOffset = rectw
		else
			holeOffset = cdia
		endif
		add2 -holeOffset/2,0
		for i=1 to hnum
			add2 gs_hole_pos[i],0
			line2 0,-b/2,0,b/2
			line2 holeOffset,-b/2,holeOffset,b/2
			del 1
		next i
		del 1
	endif
	del 1
endif
