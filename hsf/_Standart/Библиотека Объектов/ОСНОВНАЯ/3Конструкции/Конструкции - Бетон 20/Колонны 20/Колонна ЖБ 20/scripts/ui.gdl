
! =============================================================================
! Localization
! =============================================================================
uiShape	= `Форма`
uiCC1		= `Капитель Колонны 1`
uiCC2		= `Капитель Колонны 2`

dim stColShape[]
	stColShape[1] = `Круг`
	stColShape[2] = `Прямоугольник`

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 			= -1
_idxTab 			= 1
TABID_GEOMETRY 		= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES        = _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS 	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Колонны`
ui_current_page gs_ui_current_page

ui_page TABID_GEOMETRY, TABID_ROOT , `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "geometryPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Свойства и Описания`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

 "geometryPage":

	x1 = 0
	x2 = 140
	x3 = 230
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 400
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	
	ui_style 0, 0
	
	ui_outfield	uiShape ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_infield{3} "iColumnSectionType", 	 x2,   yCurrent-1, 	x3 - x2, infieldHeight+2,
				8, "", 3, 1,
				75, 15, 75, 15,
				1, stColShape[1], 1,
				2, stColShape[2], 2

	yCurrent = yCurrent + dy

	ui_infield{3} "bColumnHead", 	 x1,   yCurrent, 	x2-x1-1, infieldHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", uiCC1, 0,
				"", uiCC1, 1

	ui_infield{3} "iColumnHeadType", 	 x2,   yCurrent-1, 	x3 - x2, infieldHeight+2,
				8, "", 3, 1,
				75, 15, 75, 15,
				1, stColShape[1], 1,
				2, stColShape[2], 2

	ui_infield{3} "TypeOfHead_2", 	 x4,   yCurrent, 	xEnd-x4, infieldHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", uiCC2, 0,
				"", uiCC2, 1

	yCurrent = yCurrent + dy

	ui_separator 0, yCurrent, xEnd, yCurrent

	uiPictX = 125
	uiPictY = yCurrent + 20
	uiPictWdt = 109
	uiPictHgt = 170
	uiPictMargin = 5
	uiPictInfWdt = 90

	uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
	uiPicLeftEND = uiPicLeftStart - uiPictMargin

	uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3

	uiPicBelow = uiPictY + uiPictHgt + uiPictMargin

	uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin


	if not(bColumnHead) & not(TypeOfHead_2) then
		UI_PICT 1, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreCol"
	endif

	if bColumnHead & not(TypeOfHead_2) then
		UI_PICT 2, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreCol_1"
	endif

	if not(bColumnHead) & TypeOfHead_2 then
		UI_PICT 4, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreCol_2"
	endif

	if bColumnHead & TypeOfHead_2 then
		UI_PICT 3, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreCol_1_2"
	endif

	if bColumnHead then
		ui_infield "xOfHead_1", uiPicLeftStart, uiPictY-8, uiPictInfWdt, infieldHeight UI_TOOLTIP `Свес Капители 1`
		ui_infield "HeightOfHead_1", uiPicLeftStart, uiPictY+13, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Капители 1`
		if TypeOfHead_2 then
			ui_infield "HeightOfHead_2", uiPicLeftStart, uiPictY+34, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Капители 2`
			ui_infield "xOfHead_2", uiPicLeftStart, uiPictY+55, uiPictInfWdt, infieldHeight UI_TOOLTIP `Свес Капители 2`
		endif
	endif
	if not(bColumnHead) & TypeOfHead_2 then
		ui_infield "HeightOfHead_2", uiPicLeftStart, uiPictY+20, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Капители 2`
		ui_infield "xOfHead_2", uiPicLeftStart, uiPictY+40, uiPictInfWdt, infieldHeight UI_TOOLTIP `Свес Капители 2`
	endif
	ui_infield "A", uiPictX+20, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Размер 1`

	ui_infield "ZZYZX", uiPicRightStart, uiPictY+70, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота`

return

! ------------------------------------------------------------------------------
"AppearancePage":
! ------------------------------------------------------------------------------

	dim st2DParamNames[11][2]
		st2DParamNames[1][1] = "iDet2D"			: st2DParamNames[1][2] = "ui_list_detailLevel2D_flat(1)"
		st2DParamNames[2][1] = "gs_line_type"	: st2DParamNames[2][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[3][1] = "gs_cont_pen" 	: st2DParamNames[3][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[4][1] = "gs_fill_type" 	: st2DParamNames[4][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[5][1] = "gs_fill_pen" 	: st2DParamNames[5][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[6][1] = "gs_back_pen" 	: st2DParamNames[6][2] = "ui_list_fillBgPen_flat(1)"
		st2DParamNames[7][1] = "capital_line_type"	: st2DParamNames[7][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[8][1] = "capital_cont_pen" 	: st2DParamNames[8][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[9][1] = "capital_fill_type"	: st2DParamNames[9][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[10][1] = "capital_fill_pen" 	: st2DParamNames[10][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[11][1] = "capital_back_pen" 	: st2DParamNames[11][2] = "ui_list_fillBgPen_flat(1)"

	dim st3DParamNames[3][2]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_resol"			: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
		st3DParamNames[3][1] = "gs_shadow"			: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "gs_column_mat"	: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

return

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------

	dim stProp[]
	if LibraryLangCode = "USA" then
		stProp[1] = "es_density_us"
	else
		stProp[1] = "es_density"
	endif
	stProp[2] = "es_length"
	if LibraryLangCode = "USA" then
		stProp[3] = "es_surface_us"
		stProp[4] = "es_volume_us"
		stProp[5] = "es_mass_us"
	else
		stProp[3] = "es_surface"
		stProp[4] = "es_volume"
		stProp[5] = "es_mass"
	endif

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_structure_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										bProp = 1,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stProp		= stProp,
										stParamNames = stParamNames
return

