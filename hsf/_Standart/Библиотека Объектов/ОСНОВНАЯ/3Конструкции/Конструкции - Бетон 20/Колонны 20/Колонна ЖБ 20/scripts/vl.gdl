
hideparameter all ”A”, ”B”, ”Zzyzx” 

call "FM_types" parameters all

values "A" range [0, )
values "B" range [0, )
values "HeightOfHead_1" range [0, )
values "HeightOfHead_2" range [0, )

values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_OFF, `Откл.`
values{2} "iDet2D" 	DETLEVEL_2D_DETAILED, `Детальный`,
					DETLEVEL_2D_SIMPLE, `Простой`

values{2} "iColumnSectionType" 	COLSECT_CIRCULAR, `Круг`,
								COLSECT_RECTANGULAR, `Прямоугольник`

values{2} "iColumnHeadType"		COLHEAD_CIRCULAR, `Круг`,
								COLHEAD_RECTANGULAR, `Прямоугольник`

values "xOfHead_2" RANGE (0, ]
values "xOfHead_1" RANGE [xOfHead_2, ]

if not(bColumnHead) then lock "iColumnHeadType"

values "gs_resol" RANGE [3,)

IF iColumnSectionType = COLSECT_CIRCULAR THEN
	B = A
	PARAMETERS B = B
	B2 = B/2
	PARAMETERS B2 = B2
	LOCK "B"
ENDIF

IF GLOB_MODPAR_NAME = "A" THEN PARAMETERS A2 = A/2
IF GLOB_MODPAR_NAME = "A2" THEN PARAMETERS A = A2*2

IF GLOB_MODPAR_NAME = "B" THEN PARAMETERS B2 = B/2
IF GLOB_MODPAR_NAME = "B2" THEN PARAMETERS B = B2*2

IF GLOB_MODPAR_NAME = "xOfHead_1" THEN PARAMETERS TempxOfHead_1 = SQR(2*xOfHead_1*xOfHead_1)
IF GLOB_MODPAR_NAME = "TempxOfHead_1" THEN PARAMETERS xOfHead_1 = SQR((TempxOfHead_1*TempxOfHead_1)/2)

IF GLOB_MODPAR_NAME = "xOfHead_2" THEN PARAMETERS TempxOfHead_2 = SQR(2*xOfHead_2*xOfHead_2)
IF GLOB_MODPAR_NAME = "TempxOfHead_2" THEN PARAMETERS xOfHead_2 = SQR((TempxOfHead_2*TempxOfHead_2)/2)

IF gs_cont_pen=0 then parameters gs_cont_pen=2
IF capital_cont_pen=0 then parameters capital_cont_pen=2
IF gs_fill_pen=0 then parameters gs_fill_pen=91
IF capital_fill_pen=0 then parameters capital_fill_pen=91

IF iDet2D = DETLEVEL_2D_SIMPLE or iColumnHeadType = COLHEAD_OFF AND TypeOfHead_2 = 0 THEN
	LOCK "capital_line_type","capital_cont_pen","capital_fill_type","capital_fill_pen","capital_back_pen"
ENDIF

IF iColumnSectionType <> COLSECT_CIRCULAR AND iColumnHeadType <> COLHEAD_CIRCULAR THEN LOCk "gs_resol"

parameters ac_bottomlevel = 0
parameters ac_toplevel = ZZYZX

es_length = zzyzx
if iColumnSectionType = COLSECT_CIRCULAR then
	es_surface = PI*(a^2)
else
	es_surface = a*b
endif
head1_volume = 0
head2_volume = 0
if iColumnHeadType <> COLHEAD_OFF then
	if iColumnHeadType = COLHEAD_CIRCULAR then
		head1_volume = HeightOfHead_1 * PI * (a/2+xOfHead_1)^2
	else				! `Rectangular`
		head1_volume = HeightOfHead_1 * (a+2*xOfHead_1) * (b+2*xOfHead_1)
	endif
endif
if TypeOfHead_2 then
	heightTot = (a+xOfHead_2)*HeightOfHead_2 / (xOfHead_2)
	head2_volume = (a+2*xOfHead_2)^2*heightTot/3 - (a^2)*(heightTot-HeightOfHead_2)/3
endif
head_volume  = head1_volume + head2_volume
es_volume = es_surface*(zzyzx-HeightOfHead_1*(iColumnHeadType <> COLHEAD_OFF)-TypeOfHead_2*HeightOfHead_2)+head_volume
PARAMETERS es_length=es_length, es_surface = es_surface, es_volume = es_volume

CALL "Precast_quantity_calc" parameters A = A,
	bMetricDimension = bMetricDimension,
	es_density		= es_density,
	es_density_us	= es_density_us,
	es_volume		= es_volume,
	es_surface		= es_surface
