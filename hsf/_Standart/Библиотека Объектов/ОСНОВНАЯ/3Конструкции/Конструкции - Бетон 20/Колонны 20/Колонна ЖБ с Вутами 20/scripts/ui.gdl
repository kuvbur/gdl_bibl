
! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 			= -1
_idxTab 			= 1
TABID_GEOMETRY 		= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES        = _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS 	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Колонны`
ui_current_page gs_ui_current_page

ui_page TABID_GEOMETRY, TABID_ROOT , `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "geometryPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Свойства и Описания`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

 "geometryPage":
 	! ======================================================================
 	! Page 1
 	! ======================================================================
	
 	pageStartY 	= 5	
 	y0			= pageStartY	
 	x1			= 81		! start Position X Coordinate in first column
 	x2			= 181		! start Position X Coordinate in second column
 	x3			= 393
 	x4 			= 224
 	x5 			= x4 + 74
 	x6 			= x5 + 74

 	PictStartX	= 2
 	PictStartY	= pageStartY + 10
	
 	hRow		= 18
 	hPictBox	= 41
 	hCheckBox	= 24
 	lineSpace	= 28
	
 	widthOutf	= 210
 	heightOutf	= 15
	
 	widthInf	= 72
 	widthInf2	= 50
 	widthInf3	= 90
 	heightInf	= 19
	
 	! ------ 1. Column - Picture -------

 	ui_pict 1, 2, pageStartY + 8, 73, 192, 1					! ColumnGeometry.png
 	ui_pict 2, 75, pageStartY + 58, 39, 66, 1					! HaunchGeometry.png

 	ui_infield "Bracket_length", 	x1, pageStartY + 35,	widthInf,	heightInf

 	ui_infield "Bracket_angle", 	x1, pageStartY + 125,	widthInf3,	heightInf

 	ui_infield "Bracket_height", 	2, pageStartY + 204,	widthInf,	heightInf
	
 	! ------ 2. Column - Parameters -------
	
 	ui_separator x2-4, pageStartY-4, x2-4, 265	

 	ui_style 0, 1
 	ui_outfield `1. Количество Вутов Слева`,	x2, y0,	widthOutf,	heightOutf									
 	ui_style 0, 0
	
 	ui_infield{3} "NumOfBracket_1", 	x3, y0-4,	widthInf2,	heightInf,
 					8, "",
 					4,
 					1, 20, 20, 20, 20,
 					1, '0', 0,
 					1, '1', 1,
 					1, '2', 2,
 					1, '3', 3 
 	y0 = y0 + 17

 	ui_outfield `Z-возвышение`,	x2, y0,	widthOutf,	heightOutf
 	y0 = y0 + 19

 	ui_infield "elevationRight_1", 	x4, y0-4,	widthInf,	heightInf
 	ui_infield "elevationRight_2", 	x5, y0-4,	widthInf,	heightInf	
 	ui_infield "elevationRight_3", 	x6, y0-4,	widthInf,	heightInf

 	y0 = y0 + 25

 	ui_style 0, 1
 	ui_outfield `2. Количество Вутов Справа`,	x2, y0,	widthOutf,	heightOutf									
 	ui_style 0, 0
	
 	ui_infield{3} "NumOfBracket_3", 	x3, y0-4,	widthInf2,	heightInf,
 					8, "",
 					4,
 					1, 20, 20, 20, 20,
 					1, '0', 0,
 					1, '1', 1,
 					1, '2', 2,
 					1, '3', 3 
 	y0 = y0 + 17

 	ui_outfield `Z-возвышение`,	x2, y0,	widthOutf,	heightOutf
 	y0 = y0 + 19

 	ui_infield "elevationLeft_1", 	x4, y0-4,	widthInf,	heightInf
 	ui_infield "elevationLeft_2", 	x5, y0-4,	widthInf,	heightInf	
 	ui_infield "elevationLeft_3", 	x6, y0-4,	widthInf,	heightInf

 	y0 = y0 + 25

 	ui_style 0, 1
 	ui_outfield `3. Количество Вутов Спереди`,	x2, y0,	widthOutf,	heightOutf									
 	ui_style 0, 0
	
 	ui_infield{3} "NumOfBracket_4", 	x3, y0-4,	widthInf2,	heightInf,
 					8, "",
 					4,
 					1, 20, 20, 20, 20,
 					1, '0', 0,
 					1, '1', 1,
 					1, '2', 2,
 					1, '3', 3 
 	y0 = y0 + 17

 	ui_outfield `Z-возвышение`,	x2, y0,	widthOutf,	heightOutf
 	y0 = y0 + 19

 	ui_infield "elevationFront_1", 	x4, y0-4,	widthInf,	heightInf
 	ui_infield "elevationFront_2", 	x5, y0-4,	widthInf,	heightInf	
 	ui_infield "elevationFront_3", 	x6, y0-4,	widthInf,	heightInf

 	y0 = y0 + 25

 	ui_style 0, 1
 	ui_outfield `4. Количество Вутов Сзади`,	x2, y0,	widthOutf,	heightOutf									
 	ui_style 0, 0
	
 	ui_infield{3} "NumOfBracket_2", 	x3, y0-4,	widthInf2,	heightInf,
 					8, "",
 					4,
 					1, 20, 20, 20, 20,
 					1, '0', 0,
 					1, '1', 1,
 					1, '2', 2,
 					1, '3', 3 
 	y0 = y0 + 17

 	ui_outfield `Z-возвышение`,	x2, y0,	widthOutf,	heightOutf
 	y0 = y0 + 19

 	ui_infield "elevationBack_1", 	x4, y0-4,	widthInf,	heightInf
 	ui_infield "elevationBack_2", 	x5, y0-4,	widthInf,	heightInf	
 	ui_infield "elevationBack_3", 	x6, y0-4,	widthInf,	heightInf

 	y0 = y0 + 25
 return

! ------------------------------------------------------------------------------
"AppearancePage":
! ------------------------------------------------------------------------------

	dim st2DParamNames[7][2]
		st2DParamNames[1][1] = "iDet2D"				: st2DParamNames[1][2] = "ui_list_detailLevel2D_flat(1)"
		st2DParamNames[2][1] = "gs_cont_pen" 		: st2DParamNames[2][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[3][1] = "gs_fill_type"		: st2DParamNames[3][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[4][1] = "gs_fill_pen" 		: st2DParamNames[4][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[5][1] = "gs_back_pen" 		: st2DParamNames[5][2] = "ui_list_fillBgPen_flat(1)"
		st2DParamNames[6][1] = "LineTypeOfColumn"	: st2DParamNames[6][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[7][1] = "LineTypeOfHaunch" 	: st2DParamNames[7][2] = "ui_list_contLineType_flat(1)"

	dim st3DParamNames[2][2]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_shadow"			: st3DParamNames[2][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "gs_column_mat"	: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

return

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------

	dim stProp[]
	if LibraryLangCode = "USA" then
		stProp[1] = "es_density_us"
	else
		stProp[1] = "es_density"
	endif
	stProp[2] = "es_length"
	if LibraryLangCode = "USA" then
		stProp[3] = "es_surface_us"
		stProp[4] = "es_volume_us"
		stProp[5] = "es_mass_us"
	else
		stProp[3] = "es_surface"
		stProp[4] = "es_volume"
		stProp[5] = "es_mass"
	endif

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_structure_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										bProp = 1,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stProp		= stProp,
										stParamNames = stParamNames
return
