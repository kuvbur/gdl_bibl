
!iObjectCode
PRE_SLAB = 13

rrr = request("Working_length_unit", "", w_units_form)
bMetricDimension = strstr(w_units_form, "m")             ! m, cm, mm

! const values for parameter: gs_detlevel_3D_m
DLEV3D_OFF      = 0
DLEV3D_SIMPLE   = 1
DLEV3D_DETAILED = 2

! const values for parameter: iSlabType
SLAB_TYPE_A = 1
SLAB_TYPE_B = 2
SLAB_TYPE_C = 3
SLAB_TYPE_D = 4
SLAB_TYPE_E = 5
SLAB_TYPE_F = 6
SLAB_TYPE_FLAT = 7

EPS = 0.0001

dim stSlabType[7]
	stSlabType[1] = `Пустотное "A"`
	stSlabType[2] = `Пустотное "B"`
	stSlabType[3] = `Пустотное "C"`
	stSlabType[4] = `Пустотное "D"`
	stSlabType[5] = `Пустотное "E"`
	stSlabType[6] = `Пустотное "F"`
	stSlabType[7] = `Непустотелое`

dim stDetail[3]
	stDetail[1] = `Детальный`
	stDetail[2] = `Простой`
	stDetail[3] = `Откл.`

bDetailed = (gs_detlevel_3D_m = DLEV3D_DETAILED)

! =============================================================================

if GLOB_SCRIPT_TYPE = 2 then
	maskStraight = 1
	maskCurved	 = 1
else
	maskStraight = 15
	maskCurved	 = 79
endif

if iSlabType = SLAB_TYPE_FLAT then
	if bDetailed then
		put -B/2 + 0.001, 0, maskStraight,
			-B/2 + 0.001, 0.50*zzyzx, maskStraight,
			-B/2 + 1", 0.55*zzyzx, maskStraight,
			-B/2 + 1", 0.65*zzyzx, maskStraight,
			-B/2 + 0.001, 0.70*zzyzx, maskStraight,
			-B/2 + 0.001, zzyzx, maskStraight,

			 B/2 - 0.001, zzyzx, maskStraight,
			 B/2 - 0.001, 0.70*zzyzx, maskStraight,
			 B/2 - 1", 0.65*zzyzx, maskStraight,
			 B/2 - 1", 0.55*zzyzx, maskStraight,
			 B/2 - 0.001, 0.50*zzyzx, maskStraight,
			 B/2 - 0.001, 0, maskStraight
	else
		put -B/2 + 0.001, 0, maskStraight,
			-B/2 + 1", zzyzx, maskStraight,

			 B/2 - 1", zzyzx, maskStraight,
			 B/2 - 0.001, 0, maskStraight
	endif
endif

if iSlabType <> SLAB_TYPE_FLAT then

! Contour ---------------------------------------------------------------------

	if bDetailed THEN
		if iSlabType = SLAB_TYPE_B then
			put -B/2 + 0.001, 0, maskStraight,
				-B/2 + 0.10", 0.25*zzyzx, maskStraight,
				-B/2 + 0.80", 0.30*zzyzx, maskStraight,
				-B/2 + 0.90", 0.45*zzyzx, maskStraight,
				-B/2 + 0.20", 0.55*zzyzx, maskStraight,
				-B/2 + 0.25", 0.72*zzyzx, maskStraight,
				-B/2 + 1.75", 0.88*zzyzx, maskStraight,
				-B/2 + 1.75", zzyzx, maskStraight,

				 B/2 - 1.75", zzyzx, maskStraight,
				 B/2 - 1.75", 0.88*zzyzx, maskStraight,
				 B/2 - 0.25", 0.72*zzyzx, maskStraight,
				 B/2 - 0.20", 0.55*zzyzx, maskStraight,
				 B/2 - 0.90", 0.45*zzyzx, maskStraight,
				 B/2 - 0.80", 0.30*zzyzx, maskStraight,
				 B/2 - 0.10", 0.25*zzyzx, maskStraight,
				 B/2 - 0.001, 0, maskStraight,

				-B/2 + 0.001, 0, maskCurved,
				-B/2 + 0.001, 0, -1
		else
			put -B/2 + 0.001, 0, maskStraight,
				-B/2 + 0.50", 0.50*zzyzx, maskStraight,
				-B/2 + 1", 0.55*zzyzx, maskStraight,
				-B/2 + 1", 0.65*zzyzx, maskStraight,
				-B/2 + 0.70", 0.70*zzyzx, maskStraight,
				-B/2 + 1", zzyzx, maskStraight,

				 B/2 - 1", zzyzx, maskStraight,
				 B/2 - 0.70", 0.70*zzyzx, maskStraight,
				 B/2 - 1", 0.65*zzyzx, maskStraight,
				 B/2 - 1", 0.55*zzyzx, maskStraight,
				 B/2 - 0.50", 0.50*zzyzx, maskStraight,
				 B/2 - 0.001, 0, maskStraight,

				-B/2 + 0.001, 0, maskCurved,
				-B/2 + 0.001, 0, -1
		endif
	else
		put -B/2 + 0.001, 0, maskStraight,
			-B/2 + 1", zzyzx, maskStraight,

			 B/2 - 1", zzyzx, maskStraight,
			 B/2 - 0.001, 0, maskStraight,

			-B/2 + 0.001, 0, -1
	endif

! Holes -----------------------------------------------------------------------

	if bDetailed * (GLOB_SCRIPT_TYPE <> 2) | ShowHoles2D * (GLOB_SCRIPT_TYPE = 2) then

		if iSlabType = SLAB_TYPE_A then
			dt = 1"
			dy = zzyzx / 2
			t1 = 4'/8
			n1 = int((B - dt * 2) / t1 + 0.5)
			if n1 > 0 then
				t1 = (B - dt * 2) / n1
				x1 = t1 / 3
				y1 = 0.8 * zzyzx
				r1 = y1 / 2
				t0 = dt + t1 / 2
				if r1 > x1 then
					y2 = sqr(r1^2 - x1^2)
				endif

				for w = -B/2 + t0 to B/2 - 0.01 step t1
					put w, dy, 900 + maskCurved
					if r1 > x1 then
						put w + x1, dy + y2, maskCurved,
							w + x1, dy - y2, maskCurved,
							w - x1, dy - y2, 3000 + maskCurved,
							w - x1, dy + y2, maskCurved,
							w + x1, dy + y2, 3000 + maskCurved,
							w + x1, dy + y2, -1
					else
						put r1, 360, 4000 + maskCurved
					endif
				next w
			endif
		endif

		if iSlabType = SLAB_TYPE_B then
			dt = 2"
			dy = zzyzx / 2
			y1 = 0.70 * zzyzx
			r1 = y1 / 2
			t1 = r1 * 2 + 1.5"
			n1 = int((B - dt * 2) / t1 + 0.1)
			if n1 > 0 then
				t1 = (B - dt * 2) / n1
				x1 = t1 / 3
				t0 = dt + t1 / 2
				for w = -B/2 + t0 to B/2 - 0.01 step t1
					put w, dy, 900 + maskCurved,
						r1, 360, 4000 + maskCurved
				next w
			endif
		endif
		if iSlabType = SLAB_TYPE_C then
			dt = 1"
			dy = zzyzx / 2
			y1 = 0.73 * zzyzx
			r1 = y1 / 2
			t1 = r1 * 2 + zzyzx / 6
			n1 = int((B - dt * 2) / t1 + 0.1)
			if n1 > 0 then
				t1 = (B - dt*2) / n1
				x1 = t1 * 0.4
				t0 = dt + t1 / 2
				for w = -B/2 + t0 to B/2 - 0.01 step t1
					put w, dy, 900 + maskCurved,
						r1, 360, 4000 + maskCurved
				next w
			endif
		endif

		if iSlabType = SLAB_TYPE_D then
			dt = 3/4"
			dy = zzyzx / 2
			t1 = 3'-4"/11
			n1 = int((B - dt * 2) / t1 + 0.5)
			if n1 > 0 then
				t1 = (B - dt * 2) / n1
				x1 = t1 / 3
				y1 = 0.8 * zzyzx
				r1 = min(y1 / 2, x1)
				t0 = dt + t1 / 2
				for w = -B/2 + t0 to B/2 - 0.01 step t1
					if y1 / 2 > x1 then
						y2 = y1 / 2 - x1
						put w,	   dy - y2, 900 + maskCurved,
							w + x1,dy + y2, maskCurved,
							w + x1,dy - y2, maskCurved,
							w - x1,dy - y2, 1000 + maskCurved,
							w - x1,dy + y2, maskCurved,
							w + x1,dy + y2, 1000 + maskCurved,
							w + x1,dy + y2, -1
					else
						put w, dy, 900 + maskCurved,
							r1, 360, 4000 + maskCurved
					endif
				next w
			endif
		endif

		if iSlabType = SLAB_TYPE_E then
			dt = 1"
			dy = zzyzx*0.55
			t1 = 4'/3
			n1 = int((B - dt * 2) / t1 + 0.5)
			if n1 > 0 then
				t1 = (B - dt * 2) / n1
				x1 = t1 * 0.85 / 2
				y1 = 0.6 * zzyzx / 2
				s1 = y1 / 6
				s2 = y1 / 3
				t0 = dt+t1/2
				for w = -B/2 + t0 to B/2 - 0.01 step t1
					put w - x1 + s2,dy - y1, maskStraight,
						w + x1 - s2,dy - y1, maskStraight,
						w + x1,		dy - y1 + s2, maskStraight,
						w + x1,		dy + y1 - s1, maskStraight,
						w + x1 - s1,dy + y1, maskStraight,
						w - x1 + s1,dy + y1, maskStraight,
						w - x1,		dy + y1 - s1, maskStraight,
						w - x1,		dy - y1 + s1, maskStraight,
						w - x1 + s2,dy - y1, -1
				next w
			endif
		endif

		if iSlabType = SLAB_TYPE_F then
			dt = 1"
			dy = zzyzx * 0.45
			y1 = 0.7 * zzyzx
			r1 = y1 / 2
			t1 = r1 * 2 + 1.5"
			n1 = int ((B - dt * 2) / t1 + 0.1)
			if n1 > 0 then
				t1 = (B - dt * 2) / n1
				x1 = t1 / 3
				t0 = dt + t1 / 2
				for w = -B/2 + t0 to B/2 - 0.01 step t1
					put w, dy, 900 + maskCurved,
						r1, 360, 4000 + maskCurved
				next w
			endif
		endif
	endif
endif


