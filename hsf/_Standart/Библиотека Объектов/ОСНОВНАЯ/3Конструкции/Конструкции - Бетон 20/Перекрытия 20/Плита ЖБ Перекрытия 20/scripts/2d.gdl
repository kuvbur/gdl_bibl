
unID = 1
hLineID = 1000

! Length
hotspot2  0, 0, unID, A, 1+256 : unID=unID+1
hotspot2 -1, 0, unID, A, 3 : unID=unID+1
hotspot2  A, 0, unID, A, 2 : unID=unID+1

hotspot2  0, B, unID, A, 1+256 : unID=unID+1
hotspot2 -1, B, unID, A, 3 : unID=unID+1
hotspot2  A, B, unID, A, 2 : unID=unID+1

! Width
hotspot2 A,  0, unID, B, 1+256 : unID=unID+1
hotspot2 A, -1, unID, B, 3 : unID=unID+1
hotspot2 A,  B, unID, B, 2 : unID=unID+1

hotspot2 0,  0, unID, B, 1+256 : unID=unID+1
hotspot2 0, -1, unID, B, 3 : unID=unID+1
hotspot2 0,  B, unID, B, 2 : unID=unID+1

! Center
hotspot2 A/2, B/2, unID : unID=unID+1

hotline2 0,0,A,0, hLineID : hLineID = hLineID + 1
hotline2 A,0,A,B, hLineID : hLineID = hLineID + 1
hotline2 0,B,A,B, hLineID : hLineID = hLineID + 1
hotline2 0,0,0,B, hLineID : hLineID = hLineID + 1


pen gs_cont_pen
fill gs_fill_type

add2 0,B/2

poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
	0, -B/2, 1,
	0,  B/2, 1,
	A,  B/2, 1,
	A, -B/2, 1,
	0, -B/2, -1

if showsect then

	hotspot2 0,		  0, unID, pos_2Dx, 1+128 : unID=unID+1
	hotspot2 0,		 -1, unID, pos_2Dx, 3 : unID=unID+1
	hotspot2 pos_2Dx, 0, unID, pos_2Dx, 2 : unID=unID+1

	add2 pos_2Dx, 0
	rot2 90
	fill cross_fill
	poly2_ NSP/3, 7, get(NSP)
	if bTopLayer then
		line2 -B/2, zzyzx+slab_top_th,
			   B/2, zzyzx+slab_top_th
	endif
	del 2
endif

del 1
