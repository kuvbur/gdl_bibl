


sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
flangeThick=flangeThick/1000
Rad=Rad/1000


PEN gs_cont_pen
hLineID = 1000
FILL gs_fill_type
LINE_TYPE 1

IF Angle=0 THEN GOTO 100
IF Angle=90 OR Angle=-90 THEN GOTO 200
IF Angle>0 AND Angle<90 OR Angle<0 AND Angle>-90 THEN GOTO 300


100:


unID=100

IF (axis=1) THEN
	PEN axis_line_pen
	LINE_TYPE axis_line_type
	LINE2 0, 0, A*cos(Angle),0
	HOTLINE2 0, 0, A*cos(Angle),0, hLineID
ENDIF

HOTSPOT2 0, 0, unID, a, 1+256: unID=unID+1	!!! AXIS (Length)
HOTSPOT2 -1, 0, unID, a, 3: unID=unID+1
HOTSPOT2 a, 0, unID, a, 2: unID=unID+1

HOTSPOT2 a/2, 0,unID : unID=unID+1			!!! AXIS (Length/2)


PEN gs_cont_pen
FILL gs_fill_type
LINE_TYPE 1

IF axis=1 AND only_axis=0 OR axis=0 THEN



	110:


	IF iCutModeBottom = CUTMODE_PERPENDICULAR OR iCutModeBottom = CUTMODE_ALONGVERTICAL THEN

		!!! *** Cut. Angle (1)
		IF gs_cutang1_bot=0 THEN
			P1xCutMod12=0
			ELSE
			P1xCutMod12=(sectWidth/2)*TAN(gs_cutang1_bot)
		ENDIF

		!!! *** Cut. Angle (2)
		IF gs_cutang2_bot=0 THEN
			P2xCutMod12=0
			ELSE
			P2xCutMod12=(sectWidth/2)*TAN(gs_cutang2_bot)
		ENDIF

		!!! *** Contour
			POLY2_b 6, 1, gs_fill_pen, gs_back_pen,
				0,0,1,
				P1xCutMod12,sectWidth/2,1,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,1,
				P2xCutMod12,-sectWidth/2,1,
				0,0,-1

		!!! *** Background Fill
		IF GLOB_CONTEXT<>22 THEN	!! (DO NOT SHOW FILL IN HOTSPOT EDITING)
			POLY2_b 6, 2+64, gs_fill_pen, gs_back_pen,
				0,0,0,
				P1xCutMod12,sectWidth/2,0,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,0,
				P2xCutMod12,-sectWidth/2,0,
				0,0,-1
		ENDIF

		!!! *** Hotspots
		!!! *** Cut. Angle (1)
		P1CutMod12_RR=(sectWidth/2)/COS(gs_cutang1_bot)

		IF bCutPlanes THEN
			ROT2 -30
				unID=110
				HOTSPOT2 0,0, unID, temp_cutang1_bot, 6+512, gs_cutang1_bot : unID=unID+1 !Center
				HOTSPOT2 -P1CutMod12_RR,0, unID, temp_cutang1_bot, 4+128,  gs_cutang1_bot : unID=unID+1 !Base
				HOTSPOT2 -COS(temp_cutang1_bot) * P1CutMod12_RR, SIN(temp_cutang1_bot) * P1CutMod12_RR,  unID, \
          temp_cutang1_bot, 5,  gs_cutang1_bot : unID=unID+1 !Move
			DEL 1
		ELSE
				unID=113
				HOTSPOT2 0,sectWidth/2 : unID=unID+1
		ENDIF


		!!! *** Cut. Angle (2)
		P2CutMod12_RR=(sectWidth/2)/COS(gs_cutang2_bot)

		IF bCutPlanes THEN
			ROT2 30
				unID=114
				HOTSPOT2 0,0, unID, temp_cutang2_bot, 6, gs_cutang2_bot : unID=unID+1 !Center
				HOTSPOT2 -P2CutMod12_RR,0, unID, temp_cutang2_bot, 4+128,  gs_cutang2_bot : unID=unID+1 !Base
				HOTSPOT2 -COS(temp_cutang2_bot) * P2CutMod12_RR, -SIN(temp_cutang2_bot) * P2CutMod12_RR,  unID, \
          temp_cutang2_bot, 5,  gs_cutang2_bot : unID=unID+1 !Move
			DEL 1
		ELSE
				unID=117
				HOTSPOT2 0,-sectWidth/2 : unID=unID+1
		ENDIF

	ENDIF

	120:


	IF iCutModeTop = CUTMODE_PERPENDICULAR OR iCutModeTop = CUTMODE_ALONGVERTICAL THEN

		!!! *** Cut. Angle (3)
		IF gs_cutang3_up=0 THEN
			P3xCutMod12=0
			ELSE
			P3xCutMod12=(sectWidth/2)*TAN(gs_cutang3_up)
		ENDIF

		!!! *** Cut. Angle (4)
		IF gs_cutang4_up=0 THEN
			P4xCutMod12=0
			ELSE
			P4xCutMod12=(sectWidth/2)*TAN(gs_cutang4_up)
		ENDIF

		!!! *** Contour
			POLY2_b 6, 1, gs_fill_pen, gs_back_pen,
				A,0,1,
				A-P3xCutMod12,sectWidth/2,1,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,1,
				A-P4xCutMod12,-sectWidth/2,1,
				A,0,-1

		!!! *** Contour and Background Fill
		IF GLOB_CONTEXT<>22 THEN	!! (DO NOT SHOW FILL IN HOTSPOT EDITING)
			POLY2_b 6, 2+64, gs_fill_pen, gs_back_pen,
				A,0,0,
				A-P3xCutMod12,sectWidth/2,0,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,0,
				A-P4xCutMod12,-sectWidth/2,0,
				A,0,-1
		ENDIF

		!!! *** Hotspots
		!!! *** Cut. Angle (3)
		P3CutMod3_RR=(sectWidth/2)/COS(gs_cutang3_up)


		IF bCutPlanes THEN

			ADD2 A,0
			ROT2 30
				unID=120
				HOTSPOT2 0,0, unID, temp_cutang3_up, 6, gs_cutang3_up : unID=unID+1 !Center
				HOTSPOT2 P3CutMod3_RR,0, unID, temp_cutang3_up, 4+128,  gs_cutang3_up : unID=unID+1 !Base
				HOTSPOT2 COS(temp_cutang3_up) * P3CutMod3_RR, SIN(temp_cutang3_up) * P3CutMod3_RR,  unID, \
          temp_cutang3_up, 5,  gs_cutang3_up : unID=unID+1 !Move
			DEL 2
		ELSE
				unID=123
				HOTSPOT2 a,sectWidth/2 : unID=unID+1
		ENDIF


		!!! *** Cut. Angle (4)
		P4CutMod3_RR=(sectWidth/2)/COS(gs_cutang4_up)

		IF bCutPlanes THEN
			ADD2 A,0
			ROT2 -30
				unID=124
				HOTSPOT2 0,0, unID, temp_cutang4_up, 6+512, gs_cutang4_up : unID=unID+1 !Center
				HOTSPOT2 P4CutMod3_RR,0, unID, temp_cutang4_up, 4+128,  gs_cutang4_up : unID=unID+1 !Base
				HOTSPOT2 COS(temp_cutang4_up) * P4CutMod3_RR, -SIN(temp_cutang4_up) * P4CutMod3_RR,  unID, \
          temp_cutang4_up, 5,  gs_cutang4_up : unID=unID+1 !Move
			DEL 2

		ELSE
				unID=127
				HOTSPOT2 a,-sectWidth/2 : unID=unID+1
		ENDIF

	ENDIF


	130:

	unID=130

	IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN

		IF gs_cutang1_bot=0 THEN
			P1xCutMod3=0
			ELSE
			P1xCutMod3=(sectWidth/2)*TAN(gs_cutang1_bot)
		ENDIF


		IF gs_cutang2_bot=0 THEN
			P2xCutMod3=0
			ELSE
			P2xCutMod3=(sectWidth/2)*TAN(gs_cutang2_bot)
		ENDIF


		!!! *** MIN. Horiz. Length

		tempP12CutMod3=MIN(0,P1xCutMod3,P2xCutMod3)


		!!! *** Lines Types
		IF P1xCutMod3<0 THEN
			tempLineOrigo=DashedLineType
			ELSE
			tempLineOrigo=1
		ENDIF

			IF P2xCutMod3<P1xCutMod3 AND  P2xCutMod3<=0 THEN
			tempLineAng2=1
			ELSE
			tempLineAng2=DashedLineType
		ENDIF

		!!! *** ORIGO
		LINE_TYPE tempLineOrigo
		IF gs_cutang1_bot<>-gs_cutang2_bot OR -gs_cutang1_bot<>gs_cutang2_bot THEN LINE2 0,sectWidth/2,0,-sectWidth/2

		!!! *** Cut. Angle (1)
		LINE_TYPE 1
		LINE2 P1xCutMod3,sectWidth/2,P1xCutMod3,-sectWidth/2
		IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 P1xCutMod3,0,unID : unID=unID+1

		!!! *** Cut. Angle (2)
		LINE_TYPE tempLineAng2
		IF gs_cutang1_bot<>gs_cutang2_bot THEN
			LINE2 P2xCutMod3,sectWidth/2,P2xCutMod3,-sectWidth/2
			IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 P2xCutMod3,0,unID : unID=unID+1
		ENDIF

		LINE_TYPE 1

		!!! *** Contour
			POLY2_b 5, 1, gs_fill_pen, gs_back_pen,
				tempP12CutMod3,sectWidth/2,1,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,1,
				tempP12CutMod3,-sectWidth/2,0,
				tempP12CutMod3,sectWidth/2,-1

			IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 tempP12CutMod3,sectWidth/2,unID : unID=unID+1
			IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 tempP12CutMod3,-sectWidth/2,unID : unID=unID+1

		!!! *** Background Fill
		IF GLOB_CONTEXT<>22 THEN	!! (DO NOT SHOW FILL IN HOTSPOT EDITING)
			POLY2_b 5, 2+64, gs_fill_pen, gs_back_pen,
				tempP12CutMod3,sectWidth/2,0,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,0,
				tempP12CutMod3,-sectWidth/2,0,
				tempP12CutMod3,sectWidth/2,-1
		ENDIF

	ENDIF

	140:

	unID=140

	IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN

		IF gs_cutang3_up=0 THEN
			P3xCutMod3=0
			ELSE
			P3xCutMod3=(sectWidth/2)*TAN(gs_cutang3_up)
		ENDIF


		IF gs_cutang4_up=0 THEN
			P4xCutMod3=0
			ELSE
			P4xCutMod3=(sectWidth/2)*TAN(gs_cutang4_up)
		ENDIF


		!!! *** MIN. Horiz. Length

		tempP34CutMod3=MIN(0,P3xCutMod3,P4xCutMod3)


		!!! *** Lines Types
		IF P3xCutMod3<0 THEN
			tempLineOrigo=DashedLineType
			ELSE
			tempLineOrigo=1
		ENDIF

		IF P4xCutMod3<P3xCutMod3 AND  P4xCutMod3<=0 THEN
			tempLineAng4=1
			ELSE
			tempLineAng4=DashedLineType
		ENDIF

		!!! *** ORIGO
		LINE_TYPE tempLineOrigo
		IF gs_cutang3_up<>-gs_cutang4_up OR -gs_cutang3_up<>gs_cutang4_up THEN LINE2 A,sectWidth/2,A,-sectWidth/2

		!!! *** Cut. Angle (3)
		LINE_TYPE 1
		LINE2 A-P3xCutMod3,sectWidth/2,A-P3xCutMod3,-sectWidth/2
		IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 A-P3xCutMod3,0,unID : unID=unID+1

		!!! *** Cut. Angle (4)
		LINE_TYPE tempLineAng4
		IF gs_cutang3_up<>gs_cutang4_up THEN
			LINE2 A-P4xCutMod3,sectWidth/2,A-P4xCutMod3,-sectWidth/2
			IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 A-P4xCutMod3,0,unID : unID=unID+1
		ENDIF

		LINE_TYPE 1

		!!! *** Contour
			POLY2_b 5, 1, gs_fill_pen, gs_back_pen,
				A-tempP34CutMod3,sectWidth/2,1,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,1,
				A-tempP34CutMod3,-sectWidth/2,1,
				A-tempP34CutMod3,sectWidth/2,-1

			IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 A-tempP34CutMod3,sectWidth/2,unID : unID=unID+1
			IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 A-tempP34CutMod3,-sectWidth/2,unID : unID=unID+1

		!!! *** Background Fill
		IF GLOB_CONTEXT<>22 THEN	!! (DO NOT SHOW FILL IN HOTSPOT EDITING)
			POLY2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				A-tempP34CutMod3,sectWidth/2,0,
				A/2,sectWidth/2,0,
				A/2,-sectWidth/2,0,
				A-tempP34CutMod3,-sectWidth/2,0,
				A-tempP34CutMod3,sectWidth/2,-1
		ENDIF


	ENDIF

ENDIF

150:
END


200:

210:

unID=210

IF iRotAxis = ROT_AXIS THEN

PEN gs_cont_pen
IF axis=1 AND only_axis=0 OR axis=0 THEN PROJECT2{2} 3,270,3+32, gs_back_pen,0,0,0

	IF iInsertPoint = INSERTPOINT_TOP AND Angle=90 OR iInsertPoint = INSERTPOINT_BOTTOM AND Angle=-90 THEN ADD2 0,sectDepth/2
	IF iInsertPoint = INSERTPOINT_BOTTOM AND Angle=90 OR iInsertPoint = INSERTPOINT_TOP AND Angle=-90 THEN ADD2 0,-sectDepth/2

	HOTSPOT2 0, 0, unID, a, 1+256: unID=unID+1	!!! AXIS (Length)
	HOTSPOT2 -1, 0, unID, a, 3: unID=unID+1
	HOTSPOT2 a, 0, unID, a, 2: unID=unID+1

	HOTSPOT2 a/2, 0,unID : unID=unID+1			!!! AXIS (Length/2)

	IF gs_ShowMoreHtps2D=1 THEN
		HOTSPOT2 0, sectDepth/2,unID : unID=unID+1			!!! BOUNDING BOX
		HOTSPOT2 A, sectDepth/2,unID : unID=unID+1
		HOTSPOT2 A, -sectDepth/2,unID : unID=unID+1
		HOTSPOT2 0, -sectDepth/2,unID : unID=unID+1
	ENDIF

	IF (axis=1) THEN
		PEN axis_line_pen
		LINE_TYPE axis_line_type

		LINE2 0, 0, A,0
		HOTLINE2 0, 0, A,0, hLineID
	ENDIF

	IF iInsertPoint <> INSERTPOINT_CENTER THEN DEL 1

PEN gs_cont_pen

ENDIF


220:

unID=220

IF iRotAxis = ROT_CROSSECT THEN

	PEN gs_cont_pen

	IF iInsertPoint = INSERTPOINT_TOP AND Angle=90 OR iInsertPoint = INSERTPOINT_BOTTOM AND Angle=-90 THEN ADD2 0,-sectWidth/2
	IF iInsertPoint = INSERTPOINT_CENTER THEN ADD2 -sectDepth/2,-sectWidth/2
	IF iInsertPoint = INSERTPOINT_BOTTOM AND Angle=90 OR iInsertPoint = INSERTPOINT_TOP AND Angle=-90 THEN ADD2 -sectDepth,-sectWidth/2

	HOTSPOT2 sectDepth/2,sectWidth/2,unID : unID=unID+1		!!! ORIGO
	IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 sectDepth,sectWidth/2,unID : unID=unID+1
	IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 0,sectWidth/2,unID : unID=unID+1

	IF axis=1 AND only_axis=0 OR axis=0 THEN

			POLY2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,0,1+32,
				0,sectWidth,1+32,
				flangeThick,sectWidth,1+32,
				flangeThick,sectThick,1+32,
				sectDepth-flangeThick,sectThick,1+32,
				sectDepth-flangeThick,sectWidth,1+32,
				sectDepth,sectWidth,1+32,
				sectDepth,0,1+32,
				0,0,-1

			IF gs_ShowMoreHtps2D=1 THEN
				HOTSPOT2 sectDepth,sectWidth,unID : unID=unID+1	!!! BOUNDING BOX
				HOTSPOT2 sectDepth,0,unID : unID=unID+1
				HOTSPOT2 0,0,unID : unID=unID+1
				HOTSPOT2 0,sectWidth,unID : unID=unID+1
			ENDIF
	ENDIF

	IF (axis=1) THEN
		PEN axis_line_pen
		LINE_TYPE axis_line_type

		LINE2 -0.05, sectWidth/2, sectDepth+0.05,sectWidth/2
		LINE2 sectDepth/2, -0.05, sectDepth/2,sectWidth+0.05

		PEN gs_cont_pen

		IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 sectDepth/2,0,unID : unID=unID+1
		IF gs_ShowMoreHtps2D=1 THEN HOTSPOT2 sectDepth/2,sectWidth,unID : unID=unID+1

	ENDIF

	DEL 1

PEN gs_cont_pen

ENDIF
END

300:

unID=300

PEN gs_cont_pen

IF axis=1 AND only_axis=0 OR axis=0 THEN PROJECT2{2} 3,270,3+32, gs_back_pen,0,0,0
IF (axis=1) THEN
	PEN axis_line_pen
	LINE_TYPE axis_line_type
	IF iRotAxis = ROT_AXIS THEN
		LINE2 0, 0, A,0
		HOTLINE2 0, 0, A,0, hLineID

		HOTSPOT2 0, 0, unID, a, 1+256: unID=unID+1	!!! AXIS (Length)
		HOTSPOT2 -1, 0, unID, a, 3: unID=unID+1
		HOTSPOT2 a, 0, unID, a, 2: unID=unID+1

		HOTSPOT2 A/2,0,unID : unID=unID+1			!!! AXIS (Length/2)

	ENDIF
	IF iRotAxis = ROT_CROSSECT THEN

		IF iInsertPoint = INSERTPOINT_BOTTOM THEN ADD2 -sectDepth/2*sin(Angle),0
		IF iInsertPoint = INSERTPOINT_TOP THEN ADD2 sectDepth/2*sin(Angle),0

			LINE2 0, 0, A*cos(Angle),0
			HOTLINE2 0, 0, A*cos(Angle),0, hLineID

			HOTSPOT2 0, 0, unID, a_temp, 1+256, A: unID=unID+1		!!! AXIS (Length/2)
			HOTSPOT2 -1, 0, unID, a_temp, 3, A: unID=unID+1
			HOTSPOT2 a_temp, 0, unID, a_temp, 2, A: unID=unID+1

			HOTSPOT2 A*cos(Angle)/2,0,unID : unID=unID+1	!!! AXIS (Length)

			IF gs_ShowMoreHtps2D=1 THEN
				HOTSPOT2 0,sectWidth/2,unID : unID=unID+1			!!! BOUNDING BOX
				HOTSPOT2 0,-sectWidth/2,unID : unID=unID+1
				HOTSPOT2 A*cos(Angle),sectWidth/2,unID : unID=unID+1
				HOTSPOT2 A*cos(Angle),-sectWidth/2,unID : unID=unID+1
			ENDIF
		IF iInsertPoint <> INSERTPOINT_CENTER THEN DEL 1
	ENDIF
ENDIF
PEN gs_cont_pen
END



