
IF gs_detlevel_3D_m = 0 THEN END

sectWidth	= sectWidth	/1000
sectDepth	= sectDepth	/1000
sectThick	= sectThick	/1000
Rad		= Rad /1000
toeRad		= toeRad /1000

! Revision of Cutting Angles

! Cut1,Cut2
gs_cutang12_bot = (-gs_cutang1_bot)	!!<gs_cutang12_bot=(-gs_cutang2_bot)
gs_cutang21_bot = gs_cutang2_bot		!!<gs_cutang21_bot=gs_cutang1_bot
gs_cutang43_up = (-gs_cutang4_up)		!!<gs_cutang43_up=(-gs_cutang3_up)
gs_cutang34_up = gs_cutang3_up		!!<gs_cutang34_up=(gs_cutang4_up)

! Cut3
gs_cutang23_bot=-gs_cutang2_bot
gs_cutang33_up=-gs_cutang3_up


pen gs_cont_pen
material col_mat
if gs_shadow=0 then
	shadow off
else
	shadow on
endif

if iInsertPoint <> INSERTPOINT_BOTTOM then
	mulz -1
	Angle=Angle*(-1)
endif

rotz 90
rotx 90
if iRotAxis = ROT_CROSSECT then rotx -Angle
if iRotAxis = ROT_AXIS then rotz Angle

if iInsertPoint = INSERTPOINT_CENTER then
	Addy -sectDepth/2
endif

unID=1

! ==============================================================================
! HOTSPOTS
! ==============================================================================

hotspot 0,sectDepth/2,0,unID : unID=unID+1				! BEAM LENGTH in AXIS
hotspot 0,sectDepth/2,0,unID,a,1+256 : unID=unID+1
hotspot 0,sectDepth/2,-1,unID,a,3 : unID=unID+1
hotspot 0,sectDepth/2,a,unID,a,2 : unID=unID+1

if iRotAxis = ROT_AXIS then

	if iInsertPoint <> INSERTPOINT_BOTTOM then temp_ang=temp_ang*(-1)
		rotz -(temp_ang)

		HOTSPOT 0,0,a/2,unID,temp_ang,6,Angle : unID=unID+1
		HOTSPOT 0,sectDepth,a/2,unID,temp_ang,4+128,Angle : unID=unID+1
		HOTSPOT 0,0,a/2+1,unID,temp_ang,7,Angle : unID=unID+1
		HOTSPOT -(sectDepth)*sin(temp_ang),(sectDepth)*cos(temp_ang),a/2,unID,temp_ang,5,Angle : unID=unID+1

	DEL 1

	! lower side
	if iCutModeBottom = CUTMODE_PERPENDICULAR then
		hotspot -sectWidth/2,sectDepth/1,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
		hotspot -sectWidth/2,0,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,0,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		else
			hotspot sectWidth/2,sectDepth/1,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		endif
	endif
	if iCutModeBottom = CUTMODE_ALONGHORIZONTAL then
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot -sectWidth/2,sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		else
			hotspot -sectWidth/2,0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		endif
	endif

	! upper side
	if iCutModeTop = CUTMODE_PERPENDICULAR then
		hotspot -sectWidth/2,sectDepth/1,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
		hotspot -sectWidth/2,0,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,0,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		else
			hotspot sectWidth/2,sectDepth/1,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		endif
    endif

	if iCutModeTop = CUTMODE_ALONGHORIZONTAL then
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot -sectWidth/2,sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		else
			hotspot -sectWidth/2,0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		endif
	endif
endif

unID=100
if iRotAxis = ROT_CROSSECT then
	! rotation of axis

	if iInsertPoint <> INSERTPOINT_BOTTOM then temp_ang=temp_ang*(-1)
	if iInsertPoint = INSERTPOINT_CENTER then Addy sectDepth/2

	ROTx temp_ang
		HOTSPOT 0,0,0,unID,temp_ang,6,Angle : unID=unID+1
		HOTSPOT 0,0,1,unID,temp_ang,4+128,Angle : unID=unID+1
		HOTSPOT -1,0,0,unID,temp_ang,7,Angle : unID=unID+1
		HOTSPOT 0,a/2*sin(temp_ang),a/2*cos(temp_ang),unID,temp_ang,5,Angle : unID=unID+1
	DEL 1

	if iInsertPoint = INSERTPOINT_CENTER then DEL 1

	! lower side
	IF iCutModeBottom = CUTMODE_PERPENDICULAR then
		hotspot -sectWidth/2,sectDepth/1,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
		hotspot -sectWidth/2,0,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
		IF iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,0,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		else
			hotspot sectWidth/2,sectDepth/1,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		endif
	endif
	IF iCutModeBottom = CUTMODE_ALONGVERTICAL then
		hotspot -sectWidth/2,sectDepth/1,sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang21_bot)/cos(Angle)),unID : unID=unID+1
		hotspot -sectWidth/2,0,-sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang21_bot)/cos(Angle)),unID : unID=unID+1
		IF iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,0,-sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang12_bot)/cos(Angle)),unID : unID=unID+1
		else
			hotspot sectWidth/2,sectDepth/1,sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang12_bot)/cos(Angle)),unID : unID=unID+1
		endif
	endif
	IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL then
		IF iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot -sectWidth/2,sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		else
			hotspot -sectWidth/2,0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		endif
	endif

	! upper side
	if iCutModeTop = CUTMODE_PERPENDICULAR then
		hotspot -sectWidth/2,sectDepth/1,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
		hotspot -sectWidth/2,0,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,0,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		else
			hotspot sectWidth/2,sectDepth/1,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		endif
	endif
	
	if iCutModeTop = CUTMODE_ALONGVERTICAL then
		hotspot -sectWidth/2,sectDepth/1,a+sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang43_up)/cos(Angle)),unID : unID=unID+1
		hotspot -sectWidth/2,0,a-sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang43_up)/cos(Angle)),unID : unID=unID+1
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,0,a-sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang34_up)/cos(Angle)),unID : unID=unID+1
		else
			hotspot sectWidth/2,sectDepth/1,a+sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang34_up)/cos(Angle)),unID : unID=unID+1
		endif
	endif
	
	if iCutModeTop = CUTMODE_ALONGHORIZONTAL then
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		else
			hotspot sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -sectWidth/2,0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		endif
	endif
endif


! ==============================================================================
! CUTPOLY
! ==============================================================================
nCutPoly = 0

! Cutplanes on the bottom

dz = 0

IF iCutModeBottom = CUTMODE_PERPENDICULAR then
	ROTx -90
	if ABS(gs_cutang2_bot) > EPS or ABS(gs_cutang1_bot) > EPS then
		dz=max(2*sectWidth/2*tan(gs_cutang21_bot),2*sectWidth/2*tan(-gs_cutang1_bot),1)
		CUTPOLYA 4,0,0,
				0,0,15,
				sectWidth/2,sectWidth/2*tan(gs_cutang12_bot),15,
				sectWidth/2,dz,15,
				0,dz,15
		CUTPOLYA 4,0,0,
				0,0,15,
				-sectWidth/2,sectWidth/2*tan(-gs_cutang21_bot),15,
				-sectWidth/2,dz,15,
				0,dz,15
		nCutPoly = nCutPoly + 2
	endif
	DEL 1
endif

if iCutModeBottom = CUTMODE_ALONGVERTICAL then
	  if iRotAxis = ROT_AXIS then
		ROTx -90
		ROTy -Angle
			IF ABS(gs_cutang2_bot)>0.001 or ABS(gs_cutang1_bot)>0.001 THEN
			dz=max(2*((sectWidth/2)*sin(Angle)+(sectDepth/2)*cos(Angle))*tan(gs_cutang12_bot), \
        2*((sectWidth/2)*sin(Angle)+(sectDepth/2)*cos(Angle))*tan(gs_cutang21_bot),1)
			ADDy 0
			CUTPOLYA 4,0,0,
				0,0,15,
				(sectWidth/1)*sin(Angle)+(sectDepth/2)*cos(Angle),((sectWidth/1)*sin(Angle)+(sectDepth/2)*cos(Angle))*tan(gs_cutang12_bot),15,
				sectWidth/1+0.001,dz,15,
				0,dz,15

			CUTPOLYA 4,0,0,
				0,0,15,
				-sectWidth/2-0.005,-sectWidth/2*tan(gs_cutang21_bot),15,
				-sectWidth/2-0.005,dz,15,
				0,dz,15
			DEL 1
			ENDIF
		DEL 2
	  endif

	  if iRotAxis = ROT_CROSSECT then
		ROTx Angle-90
		ADDy sectDepth/2*sin(Angle)
			dz=max(2*sectWidth/2*tan(gs_cutang21_bot)+sectDepth*sin(Angle),2*sectWidth/2*tan(gs_cutang12_bot)+sectDepth*sin(Angle),1)
			dzz=dz*cos(Angle)+ABS(sectDepth*sin(Angle))
			CUTPOLYA 4,0,0,
				0,0,15,
				sectWidth/2,sectWidth/2*tan(gs_cutang12_bot),15,
				sectWidth/2,dzz,15,
				0,dzz,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-sectWidth/2,-sectWidth/2*tan(gs_cutang21_bot),15,
				-sectWidth/2,dzz,15,
				0,dzz,15
		DEL 2
	  endif
endif

if iCutModeBottom = CUTMODE_ALONGHORIZONTAL then
	IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 then
		dz = abs(max((-sectDepth/1)*tan(gs_cutang1_bot),(sectDepth/1)*tan(gs_cutang23_bot),1))
		ADDy sectDepth/2
		ROTy 90
		ROTz 90
		if iInsertPoint = INSERTPOINT_BOTTOM then
			CUTPOLYA 6,0,0,
					(sectDepth/1),-dz,15,
					(sectDepth/1),(sectDepth/1)*tan(gs_cutang1_bot),15,
					0,0,15,
					-(sectDepth/1),-(sectDepth/1)*tan(gs_cutang23_bot),15,
					-(sectDepth/1),-dz,15,
					0,-dz,15
		else
			CUTPOLYA 6,0,0,
					(sectDepth/1),-dz,15,
					(sectDepth/1),(sectDepth/1)*tan(-gs_cutang23_bot),15,
					0,0,15,
					-(sectDepth/1),-(sectDepth/1)*tan(-gs_cutang1_bot),15,
					-(sectDepth/1),-dz,15,
					0,-dz,15
		endif
		DEL 3
	endif
endif

! --- Cutplanes on the top ---

dz2 = 0
addz a
if iCutModeTop = CUTMODE_PERPENDICULAR then
	ROTx -90
	if ABS(gs_cutang4_up)>0.001 or ABS(gs_cutang3_up)>0.001 then
		dz2 = max(1,ABS(2*((sectWidth/2)*tan(gs_cutang43_up))),ABS(2*((sectWidth/2)*tan(-gs_cutang34_up))))
		CUTPOLYA 4,0,0,
				0,0,15,
				sectWidth/2,-sectWidth/2*tan(-gs_cutang34_up),15,
				sectWidth/2,-dz2,15,
				0,-dz2,15
		CUTPOLYA 4,0,0,
				0,0,15,
				-sectWidth/2,-sectWidth/2*tan(gs_cutang43_up),15,
				-sectWidth/2,-dz2,15,
				0,-dz2,15
	ENDIF
	DEL 1
endif
if iCutModeTop = CUTMODE_ALONGVERTICAL then
	if iRotAxis = ROT_CROSSECT THEN ADDz -sectDepth/2*tan(Angle)
	ROTx Angle-90
	dz2 = max(ABS(2*sectWidth/2*tan(gs_cutang43_up)+sectDepth*sin(Angle)),ABS(2*sectWidth/2*tan(gs_cutang34_up)+sectDepth*sin(Angle)),1)
	dzz2 = dz2*cos(Angle)+ABS(sectDepth*sin(Angle))
	CUTPOLYA 4,0,0,
			0,0,15,
			sectWidth/2,-sectWidth/2*tan(-gs_cutang34_up),15,
			sectWidth/2,-dzz2,15,
			0,-dzz2,15
	CUTPOLYA 4,0,0,
			0,0,15,
			-sectWidth/2,-sectWidth/2*tan(gs_cutang43_up),15,
			-sectWidth/2,-dzz2,15,
			0,-dzz2,15
	DEL 1
	if iRotAxis = ROT_CROSSECT then DEL 1
endif

if iCutModeTop = CUTMODE_ALONGHORIZONTAL then
	if ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 then
		dz2=max(ABS(sectDepth/1*tan(gs_cutang33_up)),ABS(sectDepth/1*tan(gs_cutang4_up)),1)
		ADDy sectDepth/2
		ROTy 90
		ROTz 90
		if iInsertPoint = INSERTPOINT_BOTTOM then
			CUTPOLYA 6,0,0,
				(sectDepth/1)/cos(gs_cutang33_up),dz2,15,
				(sectDepth/1)/cos(gs_cutang4_up),((sectDepth/1)/cos(gs_cutang4_up))*tan(gs_cutang33_up),15,
				0,0,15,
				-(sectDepth/1)/cos(gs_cutang33_up),((sectDepth/1)/cos(gs_cutang33_up))*tan(-gs_cutang4_up),15,
				-(sectDepth/1)/cos(gs_cutang33_up),dz2,15,
				0,dz2,15
		else
			CUTPOLYA 6,0,0,
				(sectDepth/1)/cos(-gs_cutang4_up),dz2,15,
				(sectDepth/1)/cos(-gs_cutang33_up),((sectDepth/1)/cos(-gs_cutang33_up))*tan(-gs_cutang4_up),15,
				0,0,15,
				-(sectDepth/1)/cos(-gs_cutang4_up),((sectDepth/1)/cos(-gs_cutang4_up))*tan(gs_cutang33_up),15,
				-(sectDepth/1)/cos(-gs_cutang4_up),dz2,15,
				0,dz2,15
		endif
		DEL 3
	endif
endif
del 1

ADDz -dz

! ==============================================================================
!
! ==============================================================================

if iInsertPoint <> INSERTPOINT_BOTTOM then
	addy sectDepth
	muly -1
endif

IF  iFlangePos = FLANGEPOS_TOP THEN
	rotz 180
	roty 180
	ADDz -(a+dz+dz2)
	ADDy -sectDepth
ENDIF

! SIMPLE
ADDx -sectWidth/2

if gs_detlevel_3D_m = 1 | GLOB_CONTEXT=2 then
	PRISM_ 7, a+dz+dz2,
			0,0,15,
			sectWidth,0,15,
			sectWidth,sectThick,15,
			sectThick,sectThick,15,
			sectThick,sectDepth,15,
			0,sectDepth,15,
			0,0,-1
else
	RESOL gs_resol

	PRISM_ 13,a+dz+dz2,
			sectWidth,0,15,
			sectWidth,sectThick-toeRad,79,
			sectWidth-toeRad,sectThick-toeRad,979,
			sectWidth-toeRad,sectThick,3079,
			sectThick+Rad,sectThick,79,
			sectThick+Rad,sectThick+Rad,979,
			sectThick,sectThick+Rad,3079,
			sectThick,sectDepth-toeRad,79,
			sectThick-toeRad,sectDepth-toeRad,979,
			sectThick-toeRad,sectDepth,3079,
			0,sectDepth,15,
			0,0,15,
			sectWidth,0,-1

endif
del 1

IF  iFlangePos = FLANGEPOS_TOP  THEN DEL 4

IF iInsertPoint <> INSERTPOINT_BOTTOM THEN DEL 2

DEL 3

IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
	IF iCutModeBottom = CUTMODE_PERPENDICULAR THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeBottom = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_AXIS THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang1_bot>=0 or gs_cutang2_bot>=0 THEN
	IF iCutModeBottom = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_CROSSECT THEN :CUTEND:CUTEND: ENDIF
!ENDIF

IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
	IF iCutModeTop = CUTMODE_PERPENDICULAR THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang3_up>=0 or gs_cutang4_up>=0 THEN
	IF iCutModeTop = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_CROSSECT THEN :CUTEND:CUTEND: ENDIF
!ENDIF

DEL 1

IF iInsertPoint <> INSERTPOINT_BOTTOM THEN DEl 1
IF iInsertPoint = INSERTPOINT_CENTER THEN DEL 1

End
