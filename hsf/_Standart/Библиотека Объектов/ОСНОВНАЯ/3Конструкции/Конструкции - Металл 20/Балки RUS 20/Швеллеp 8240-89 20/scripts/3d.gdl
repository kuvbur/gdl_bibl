ws=ws/1000
ds=ds/1000
wt=wt/1000
ft=ft/1000
rr=rr/1000
tr=tr/1000
dbf=dbf/1000


!!!********* Revision of Cutting Angles

!!!********* Cut1,Cut2
gs_cutang12_bot=(-gs_cutang1_bot)	!!<gs_cutang12_bot=(-gs_cutang2_bot)
gs_cutang21_bot=gs_cutang2_bot		!!<gs_cutang21_bot=gs_cutang1_bot
gs_cutang43_up=(-gs_cutang4_up)		!!<gs_cutang43_up=(-gs_cutang3_up)
gs_cutang34_up=(gs_cutang3_up)		!!<gs_cutang34_up=(gs_cutang4_up)

!!!********* Cut3
gs_cutang23_bot=-gs_cutang2_bot
gs_cutang33_up=-gs_cutang3_up

!!!*************************************


pen gs_cont_pen
material col_mat
if gs_shadow=0 THEN
	shadow off
else
	shadow on
endif

IF gs_stationary <>`Внизу` THEN
	mulz -1
	ang=ang*(-1)
ENDIF

rotz 90
rotx 90
IF gs_rot_axis=rot_axis_mtr[2] THEN rotx -ang
IF gs_rot_axis=rot_axis_mtr[1] THEN rotz ang

IF gs_stationary=`По центру` THEN
	Addy -ds/2
ENDIF

unID=1

!!!*************************************
!!!************************************* HOTSPOTS
!!!*************************************

hotspot 0,ds/2,0,unID : unID=unID+1
hotspot 0,ds/2,0,unID,a,1+256 : unID=unID+1
hotspot 0,ds/2,-1,unID,a,3 : unID=unID+1
hotspot 0,ds/2,a,unID,a,2 : unID=unID+1

IF gs_rot_axis=rot_axis_mtr[1] THEN

	IF gs_stationary <>`Внизу` THEN temp_ang=temp_ang*(-1)
	rotz -(temp_ang)

		HOTSPOT 0,0,a/2,unID,temp_ang,6,ang : unID=unID+1
		HOTSPOT 0,ds,a/2,unID,temp_ang,4+128,ang : unID=unID+1
		HOTSPOT 0,0,a/2+1,unID,temp_ang,7,ang : unID=unID+1
		HOTSPOT -(ds)*sin(temp_ang),(ds)*cos(temp_ang),a/2,unID,temp_ang,5,ang : unID=unID+1

	DEL 1

	!!!!!***************** lower side
	IF gs_cutmode_bot=cut_mod_mtr[1] THEN
		hotspot ws/2,ds/1,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot ws/2,0,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot -ws/2,ds/1,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
		hotspot -ws/2,0,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
	ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			hotspot ws/2,ds/1,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -ws/2,ds/1,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ELSE
			hotspot ws/2,0,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -ws/2,0,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ENDIF
	ENDIF
	!!!!!***************** upper side
	IF gs_cutmode_up=cut_mod_mtr[1] THEN
		hotspot ws/2,ds/1,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot ws/2,0,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot -ws/2,ds/1,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
		hotspot -ws/2,0,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
	ENDIF
	IF gs_cutmode_up=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			hotspot ws/2,ds/1,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -ws/2,ds/1,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ELSE
			hotspot ws/2,0,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -ws/2,0,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ENDIF
	ENDIF
ENDIF

IF gs_rot_axis=rot_axis_mtr[2] THEN
	!!!!!***************** axis points

	IF gs_stationary <>`Внизу` THEN temp_ang=temp_ang*(-1)
	IF gs_stationary=`По центру` THEN Addy ds/2

	ROTx temp_ang
		HOTSPOT 0,0,0,unID,temp_ang,6,ang : unID=unID+1
		HOTSPOT 0,0,1,unID,temp_ang,4+128,ang : unID=unID+1
		HOTSPOT -1,0,0,unID,temp_ang,7,ang : unID=unID+1
		HOTSPOT 0,a/2*sin(temp_ang),a/2*cos(temp_ang),unID,temp_ang,5,ang : unID=unID+1
	DEL 1

	IF gs_stationary=`По центру` THEN DEL 1

	!!!!!***************** lower side
	IF gs_cutmode_bot=cut_mod_mtr[1] THEN
		hotspot ws/2,ds/1,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot ws/2,0,-ws/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot -ws/2,ds/1,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
		hotspot -ws/2,0,ws/2*tan(gs_cutang21_bot),unID : unID=unID+1
	ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[2] THEN
		hotspot ws/2,ds/1,ds/2*tan(ang)-(ws/2*tan(gs_cutang12_bot)/cos(ang)),unID : unID=unID+1
		hotspot ws/2,0,-ds/2*tan(ang)-(ws/2*tan(gs_cutang12_bot)/cos(ang)),unID : unID=unID+1
		hotspot -ws/2,ds/1,ds/2*tan(ang)+(ws/2*tan(gs_cutang21_bot)/cos(ang)),unID : unID=unID+1
		hotspot -ws/2,0,-ds/2*tan(ang)+(ws/2*tan(gs_cutang21_bot)/cos(ang)),unID : unID=unID+1
	ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			hotspot ws/2,ds/1,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -ws/2,ds/1,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -ws/2,0,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ELSE
			hotspot ws/2,0,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -ws/2,0,ds/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -ws/2,ds/1,-ds/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ENDIF
	ENDIF
	!!!!!***************** upper side
	IF gs_cutmode_up=cut_mod_mtr[1] THEN
		hotspot ws/2,ds/1,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot ws/2,0,a-ws/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot -ws/2,ds/1,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
		hotspot -ws/2,0,a+ws/2*tan(gs_cutang43_up),unID : unID=unID+1
	ENDIF
	IF gs_cutmode_up=cut_mod_mtr[2] THEN
		hotspot ws/2,ds/1,a+ds/2*tan(ang)-(ws/2*tan(gs_cutang34_up)/cos(ang)),unID : unID=unID+1
		hotspot ws/2,0,a-ds/2*tan(ang)-(ws/2*tan(gs_cutang34_up)/cos(ang)),unID : unID=unID+1
		hotspot -ws/2,ds/1,a+ds/2*tan(ang)+(ws/2*tan(gs_cutang43_up)/cos(ang)),unID : unID=unID+1
		hotspot -ws/2,0,a-ds/2*tan(ang)+(ws/2*tan(gs_cutang43_up)/cos(ang)),unID : unID=unID+1
	ENDIF
	IF gs_cutmode_up=cut_mod_mtr[3] THEN
		IF gs_stationary=`Внизу` THEN
			hotspot ws/2,ds/1,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -ws/2,ds/1,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -ws/2,0,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ELSE
			hotspot ws/2,0,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -ws/2,0,a+ds/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -ws/2,ds/1,a-ds/2*tan(gs_cutang4_up),unID : unID=unID+1
		ENDIF
	ENDIF
ENDIF

IF gs_detlevel_3D=`Откл.` THEN end

!!!*************************************
!!!************************************* CUTPOLY
!!!*************************************

dz = 0
dz2 = 0

!!!!!!*********************************** Cutplanes on the bottom

IF gs_cutmode_bot=cut_mod_mtr[1] THEN !!!!!***************************** Perpendicular to Axis
	ROTx -90
		IF ABS(gs_cutang2_bot)>0.001 or ABS(gs_cutang1_bot)>0.001 THEN
		dz=max(2*ws/2*tan(gs_cutang21_bot),2*ws/2*tan(-gs_cutang1_bot),1)
		CUTPOLYA 4,0,0,
			0,0,15,
			ws/2,ws/2*tan(gs_cutang12_bot),15,
			ws/2,dz,15,
			0,dz,15

		CUTPOLYA 4,0,0,
			0,0,15,
			-ws/2,ws/2*tan(-gs_cutang21_bot),15,
			-ws/2,dz,15,
			0,dz,15
		ENDIF
	DEL 1
ENDIF

IF gs_cutmode_bot=cut_mod_mtr[2] THEN !!!!!***************************** Along a Vertical Axis

	  IF gs_rot_axis=rot_axis_mtr[1] THEN
		ROTx -90
		ROTy -ang
			IF ABS(gs_cutang2_bot)>0.001 or ABS(gs_cutang1_bot)>0.001 THEN
			dz=max(2*((ws/2)*sin(ang)+(ds/2)*cos(ang))*tan(gs_cutang12_bot),2*((ws/2)*sin(ang)+(ds/2)*cos(ang))*tan(gs_cutang21_bot),1)
			ADDy 0
			CUTPOLYA 4,0,0,
				0,0,15,
				(ws/1)*sin(ang)+(ds/2)*cos(ang),((ws/1)*sin(ang)+(ds/2)*cos(ang))*tan(gs_cutang12_bot),15,
				ws/1+0.001,dz,15,
				0,dz,15

			CUTPOLYA 4,0,0,
				0,0,15,
				-ws/2-0.005,-ws/2*tan(gs_cutang21_bot),15,
				-ws/2-0.005,dz,15,
				0,dz,15
			DEL 1
			ENDIF
		DEL 2
	  ENDIF

	  IF gs_rot_axis=rot_axis_mtr[2] THEN
		ROTx ang-90
		ADDy ds/2*sin(ang)
			dz=max(2*ws/2*tan(gs_cutang21_bot)+ds*sin(ang),2*ws/2*tan(gs_cutang12_bot)+ds*sin(ang),1)
			dzz=dz*cos(ang)+ABS(ds*sin(ang))
			CUTPOLYA 4,0,0,
				0,0,15,
				ws/2,ws/2*tan(gs_cutang12_bot),15,
				ws/2,dzz,15,
				0,dzz,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-ws/2,-ws/2*tan(gs_cutang21_bot),15,
				-ws/2,dzz,15,
				0,dzz,15
		DEL 2
	  ENDIF

ENDIF

IF gs_cutmode_bot=cut_mod_mtr[3] THEN !!!!!***************************** Along a Horizontal Axis
	IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
		dz=abs(max((-ds/1)*tan(gs_cutang1_bot),(ds/1)*tan(gs_cutang23_bot),1))
		ADDy ds/2
		ROTy 90
		ROTz 90
		IF gs_stationary=`Внизу` THEN
				CUTPOLYA 6,0,0,
					(ds/1),-dz,15,
					(ds/1),(ds/1)*tan(gs_cutang1_bot),15,
					0,0,15,
					-(ds/1),-(ds/1)*tan(gs_cutang23_bot),15,
					-(ds/1),-dz,15,
					0,-dz,15
		ELSE
				CUTPOLYA 6,0,0,
					(ds/1),-dz,15,
					(ds/1),(ds/1)*tan(-gs_cutang23_bot),15,
					0,0,15,
					-(ds/1),-(ds/1)*tan(-gs_cutang1_bot),15,
					-(ds/1),-dz,15,
					0,-dz,15
		ENDIF
		DEL 3
	ENDIF
ENDIF

!!!!!!*********************************** Cutplanes on the top

ADDz a
	IF gs_cutmode_up=cut_mod_mtr[1] THEN !!!!!***************************** Perpendicular to Axis
	  ROTx -90
		  IF ABS(gs_cutang4_up)>0.001 or ABS(gs_cutang3_up)>0.001 THEN
			dz2=max(1,ABS(2*((ws/2)*tan(gs_cutang43_up))),ABS(2*((ws/2)*tan(-gs_cutang34_up))))
			CUTPOLYA 4,0,0,
				0,0,15,
				ws/2,-ws/2*tan(-gs_cutang34_up),15,
				ws/2,-dz2,15,
				0,-dz2,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-ws/2,-ws/2*tan(gs_cutang43_up),15,
				-ws/2,-dz2,15,
				0,-dz2,15
		  ENDIF
	  DEL 1
	ENDIF

	IF gs_cutmode_up=cut_mod_mtr[2] THEN !!!!!***************************** Along a Vertical Axis
	  IF gs_rot_axis=rot_axis_mtr[2] THEN ADDz -ds/2*tan(ang)

		  ROTx ang-90
				dz2=max(ABS(2*ws/2*tan(gs_cutang43_up)+ds*sin(ang)),ABS(2*ws/2*tan(gs_cutang34_up)+ds*sin(ang)),1)
				dzz2=dz2*cos(ang)+ABS(ds*sin(ang))
				CUTPOLYA 4,0,0,
					0,0,15,
					ws/2,-ws/2*tan(-gs_cutang34_up),15,
					ws/2,-dzz2,15,
					0,-dzz2,15
				CUTPOLYA 4,0,0,
					0,0,15,
					-ws/2,-ws/2*tan(gs_cutang43_up),15,
					-ws/2,-dzz2,15,
					0,-dzz2,15
		  DEL 1
	  IF gs_rot_axis=rot_axis_mtr[2] THEN DEL 1
	ENDIF

	IF gs_cutmode_up=cut_mod_mtr[3] THEN !!!!!***************************** Along a Horizontal Axis
		IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
		dz2=max(ABS(ds/1*tan(gs_cutang33_up)),ABS(ds/1*tan(gs_cutang4_up)),1)
			ADDy ds/2
			ROTy 90
			ROTz 90
			IF gs_stationary=`Внизу` THEN
				CUTPOLYA 6,0,0,
					(ds/1)/cos(gs_cutang33_up),dz2,15,
					(ds/1)/cos(gs_cutang4_up),((ds/1)/cos(gs_cutang4_up))*tan(gs_cutang33_up),15,
					0,0,15,
					-(ds/1)/cos(gs_cutang33_up),((ds/1)/cos(gs_cutang33_up))*tan(-gs_cutang4_up),15,
					-(ds/1)/cos(gs_cutang33_up),dz2,15,
					0,dz2,15
			ELSE
				CUTPOLYA 6,0,0,
					(ds/1)/cos(-gs_cutang4_up),dz2,15,
					(ds/1)/cos(-gs_cutang33_up),((ds/1)/cos(-gs_cutang33_up))*tan(-gs_cutang4_up),15,
					0,0,15,
					-(ds/1)/cos(-gs_cutang4_up),((ds/1)/cos(-gs_cutang4_up))*tan(gs_cutang33_up),15,
					-(ds/1)/cos(-gs_cutang4_up),dz2,15,
					0,dz2,15
			ENDIF
			DEL 3
		ENDIF
	ENDIF

DEL 1

ADDz -dz

!!!*************************************
!!!************************************* Simple
!!!*************************************

FlangeAng=ATN(((ds-dbf)/2-rr-ft)/(ws/2-wt-rr))

FlangeSlope1=ft-((ws/2)*tan(FlangeAng))
FlangeSlope2=(ws-wt)*tan(FlangeAng)
FlangeSlope3=(ws-wt-rr-tr)*tan(FlangeAng)


IF gs_detlevel_3D=`Простой` OR glob_context=2 THEN
	ADDx -ws/2

			PRISM_ 9,a+dz+dz2,
				ws,0,15,
				ws,FlangeSlope1,15,
				wt,FlangeSlope1+FlangeSlope2,15,
				wt,ds-FlangeSlope1-FlangeSlope2,15,
				ws,ds-FlangeSlope1,15,
				ws,ds,15,
				0,ds,15,
				0,0,15,
				ws,0,-1

	DEL 1
ENDIF

!!!*************************************
!!!************************************* Detailed
!!!*************************************

IF gs_detlevel_3D=`Детальный` AND glob_context<>2 THEN
	ADDx -ws/2

		ToeStartY2=(ds-dbf)/2-rr-FlangeSlope3-tr

			!!! Curved Resolution
			S=90/gs_resol
			FOR k=0 TO 90 STEP S
				PUT ws-tr+tr*cos(k),ToeStartY2+tr*sin(k),79
			NEXT k
			FOR k=0 TO 90 STEP S
				PUT wt+rr-rr*sin(k),(ds-dbf)/2-rr*cos(k),79
			NEXT k
			FOR k=90 TO 180 STEP S
				PUT wt+rr-rr*sin(k),ds-(ds-dbf)/2-rr*cos(k),79
			NEXT k
			FOR k=270 TO 360 STEP S
				PUT ws-tr+tr*cos(k),ds-ToeStartY2+tr*sin(k),79
			NEXT k
			PRISM_ 7+NSP/3, a+dz+dz2,
				ws,0,15,
				ws,ToeStartY2,79,
				GET(NSP/4),
				GET(NSP/3),
				GET(NSP/2),
				GET(NSP),
				ws,ds-ToeStartY2,79,
				ws,ds,15,
				0,ds,15,
				0,0,15,
				ws,0,-1

	DEL 1
ENDIF

DEL 3

IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
	IF gs_cutmode_bot=cut_mod_mtr[1] THEN :CUTEND:CUTEND: ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[2] AND gs_rot_axis=rot_axis_mtr[1] THEN :CUTEND:CUTEND: ENDIF
	IF gs_cutmode_bot=cut_mod_mtr[3] THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang1_bot>=0 or gs_cutang2_bot>=0 THEN
	IF gs_cutmode_bot=cut_mod_mtr[2] AND gs_rot_axis=rot_axis_mtr[2] THEN :CUTEND:CUTEND: ENDIF
!ENDIF

IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
	IF gs_cutmode_up=cut_mod_mtr[1] THEN :CUTEND:CUTEND: ENDIF
	IF gs_cutmode_up=cut_mod_mtr[3] THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang3_up>=0 or gs_cutang4_up>=0 THEN
	IF gs_cutmode_up=cut_mod_mtr[2] AND gs_rot_axis=rot_axis_mtr[2] THEN :CUTEND:CUTEND: ENDIF
!ENDIF

DEL 1

IF gs_stationary <>`Внизу` THEN DEl 1
IF gs_stationary=`По центру` THEN DEL 1

End
