
sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
flangeThick=flangeThick/1000
Rad=Rad/1000
RRadius=Rad


IF gs_shadow=0 THEN
	SHADOW OFF
ELSE
	SHADOW ON
ENDIF


IF gs_detlevel_3D_m = 0 THEN END


PEN gs_cont_pen
MATERIAL col_mat


IF iInsertPoint = INSERTPOINT_CENTER THEN ADDz -sectDepth/2
IF iInsertPoint = INSERTPOINT_TOP THEN ADDz -sectDepth

IF iArcResolType = ARCRES_SEGMNUM THEN		!! ARC RESOL CALCULATION
	TempArcResol=SegNum
ELSE
	TempArcResol = max(1, INT ((2*ArcRadius*PI/SegLength)*(ArcAngle/360)))
ENDIF

IF gs_detlevel_3D_m = 1 OR GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN
!!3D FEEDBACK AT HOTSPOT EDITING = SIMPLE MODEL

		FOR i=-1 TO TempArcResol+1 STEP 1
			PUT ArcRadius*COS(ArcAngle*i/TempArcResol)
			PUT ArcRadius*SIN(ArcAngle*i/TempArcResol)
			PUT 0
			PUT 0
		NEXT i

		TUBE 12,TempArcResol+3,1+2+16+32,
			-sectWidth/2,0,0,
			sectWidth/2,0,0,
			sectWidth/2,flangeThick,0,
			sectThick/2,flangeThick,0,
			sectThick/2,sectDepth-flangeThick,0,
			sectWidth/2,sectDepth-flangeThick,0,
			sectWidth/2,sectDepth,0,
			-sectWidth/2,sectDepth,0,
			-sectWidth/2,sectDepth-flangeThick,0,
			-sectThick/2,sectDepth-flangeThick,0,
			-sectThick/2,flangeThick,0,
			-sectWidth/2,flangeThick,0,
			GET(nsp)

	ELSE

			!!! Curved Resolution
			S=90/gs_resol
			FOR k=0 TO 90 STEP S
				PUT sectThick/2+Rad-Rad*sin(k),flangeThick+Rad-Rad*cos(k),1
			NEXT k
			FOR k=90 TO 180 STEP S
				PUT sectThick/2+Rad-Rad*sin(k),sectDepth-flangeThick-Rad-Rad*cos(k),1
			NEXT k
			FOR k=180 TO 90 STEP -S
				PUT -sectThick/2-Rad+(Rad)*sin(k),sectDepth-flangeThick-Rad-(Rad)*cos(k),1
			NEXT k
			FOR k=90 TO 0 STEP -S
				PUT -sectThick/2-Rad+(Rad)*sin(k),flangeThick+Rad-(Rad)*cos(k),1
			NEXT k


		FOR i=-1 TO TempArcResol+1 STEP 1
			PUT ArcRadius*COS(ArcAngle*i/TempArcResol)
			PUT ArcRadius*SIN(ArcAngle*i/TempArcResol)
			PUT 0
			PUT 0
		NEXT i



		TUBE 8+(NSP-4*(TempArcResol+3))/3,TempArcResol+3,1+2+16+32,
			-sectWidth/2,0,0,
			sectWidth/2,0,0,
			sectWidth/2,flangeThick,0,
			GET((NSP-4*(TempArcResol+3))/4),
			GET((NSP-4*(TempArcResol+3))/3),
			sectWidth/2,sectDepth-flangeThick,0,
			sectWidth/2,sectDepth,0,
			-sectWidth/2,sectDepth,0,
			-sectWidth/2,sectDepth-flangeThick,0,
			GET((NSP-4*(TempArcResol+3))/2),
			GET((NSP-4*(TempArcResol+3))),
			-sectWidth/2,flangeThick,0,

			GET(nsp)
ENDIF


unID=1

!!! ARC ANGLE
HOTSPOT 0,0,sectDepth/2,unID,ArcAngle,6 : unID=unID+1
HOTSPOT (ArcRadius),0,sectDepth/2,unID,ArcAngle,4+256 : unID=unID+1
HOTSPOT (ArcRadius)*cos(ArcAngle),(ArcRadius)*sin(ArcAngle),sectDepth/2,unID,ArcAngle,5 : unID=unID+1
HOTSPOT 0,0,sectDepth, unID, ArcAngle,7 	: unID=unID+1

!!! Htps at section
FOR kk=1 to 2
	ADDx ArcRadius
		HOTSPOT -sectWidth/2,0,0,unID : unID=unID+1
		HOTSPOT sectWidth/2,0,0,unID : unID=unID+1

		HOTSPOT 0,0,ZZYZX,unID : unID=unID+1
		HOTSPOT -sectWidth/2,0,ZZYZX,unID : unID=unID+1
		HOTSPOT sectWidth/2,0,ZZYZX,unID : unID=unID+1
	DEL 1
	ROTz ArcAngle
NEXT kk
DEL 2


!!!LENGHT
HOTSPOT 0,0,0,unID,ArcRadius,1+256 : unID=unID+1
HOTSPOT ArcRadius,0,0,unID,ArcRadius, 2 : unID=unID+1
HOTSPOT -1,0,0,unID,ArcRadius,3 : unID=unID+1

ROTz ArcAngle
	HOTSPOT 0,0,0,unID,ArcRadius,1+256 : unID=unID+1
	HOTSPOT ArcRadius,0,0,unID,ArcRadius, 2 : unID=unID+1
	HOTSPOT -1,0,0,unID,ArcRadius,3 : unID=unID+1
DEL 1

IF iInsertPoint = INSERTPOINT_CENTER THEN DEL 1
IF iInsertPoint = INSERTPOINT_TOP THEN DEL 1





