

File_Dependence "HEA(14).txt"	! --- for using in parameters script

sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
flangeThick=flangeThick/1000
Rad=Rad/1000
RRadius=Rad


unID=1
hLineID = 1000

!!! ARC RADIUS
ROT2 ArcAngle/2
	HOTSPOT2 0,0,unID,ArcRadius,1 : unID=unID+1
	HOTSPOT2 ArcRadius,0,unID,ArcRadius,2 : unID=unID+1
	HOTSPOT2 -1,0,unID,ArcRadius,3 : unID=unID+1
DEL 1

!!! ARC ANGLE
HOTSPOT2 0,0,unID,ArcAngle,6 : unID=unID+1
HOTSPOT2 (ArcRadius),0,unID,ArcAngle,4+256 : unID=unID+1
HOTSPOT2 (ArcRadius)*cos(ArcAngle),(ArcRadius)*sin(ArcAngle),unID,ArcAngle,5 : unID=unID+1


IF (axis=1) THEN

	PEN axis_line_pen
	LINE_TYPE axis_line_type
	ARC2 0,0,ArcRadius,0,ArcAngle
	HOTARC2 0,0,ArcRadius,0,ArcAngle, hLineID

ENDIF


IF axis=1 AND only_axis=0 OR axis=0 THEN

	PEN gs_cont_pen
	FILL gs_fill_type
	LINE_TYPE 1

	IF ABS(ArcAngle-360)<EPS THEN		!! ArcAngle = 360

		POLY2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 900,
			(ArcRadius+sectWidth/2), 360, 4001,
			0, 0, 900,
			(ArcRadius-sectWidth/2), 360, 4001

		ELSE

		POLY2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
			ArcRadius+sectWidth/2, 0, 1,
			0,0,900,
			0,ArcAngle,4001,
			(ArcRadius-sectWidth/2)*COS(ArcAngle),(ArcRadius-sectWidth/2)*SIN(ArcAngle),1,
			0,0,900,
			0,-ArcAngle,4001,
			ArcRadius-sectWidth/2, 0, -1

	ENDIF

	IF gs_ShowMoreHtps2D=1 THEN
		HOTSPOT2 ArcRadius-sectWidth/2,0,unID : unID=unID+1
		HOTSPOT2 ArcRadius+sectWidth/2,0,unID : unID=unID+1

		ROT2 ArcAngle
			HOTSPOT2 ArcRadius-sectWidth/2,0,unID : unID=unID+1
			HOTSPOT2 ArcRadius+sectWidth/2,0,unID : unID=unID+1
		DEL 1
	ENDIF

ENDIF

