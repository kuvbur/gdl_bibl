
DIM stInsertPointValues[3]
	stInsertPointValues[1] = `Вверху`
	stInsertPointValues[2] = `По центру`
	stInsertPointValues[3] = `Внизу`

toolTipWidth		= `Ширина (мм)`
toolTipDepth		= `Глубина (мм)`
toolTipThickness 	= `Толщина (мм)`
toolTipBottomWidth	= `Ширина Внизу (мм)`
toolTipTopWidth		= `Ширина вверху (мм)`
toolTipWidthSect 	= `Ширина профиля (мм)`
toolTipDepthSect 	= `Высота Профиля (мм)`
toolTipWebThk		= `Толщина стенки (мм)`
toolTipRootRad		= `Радиус основания (мм)`
toolTipFlangeThk	= `Толщина фланца (мм)`
toolTipDepthFillets	= `Глубина между скругл. (мм)`
toolTipInnerDepth	= `Внутр. глуб. между скругл. (мм)`
toolTipSerialSize	= `Серийный Размер (мм)`
toolTipToeRad		= `Радиус подошвы (мм)`
toolTipWallThk		= `Толщина стенки (мм)`
toolTipDiameterSect	= `Диаметр в Сечении (мм)`
toolTipLip			= `Загибание (мм)`

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT = -1
_idxTab = 1
TABID_DIMENSION = _idxTab : _idxTab = _idxTab + 1
TABID_PLACEMENT = _idxTab : _idxTab = _idxTab + 1
TABID_REPRES = _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS = _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Профиля`
ui_current_page gs_ui_current_page

ui_page TABID_DIMENSION, TABID_ROOT, `Размеры`, "uiTab_Dimension_flat(1)"
gosub "dimensionPage"

ui_page TABID_PLACEMENT, TABID_ROOT, `Размещение`, "uiTab_BeamPlacement_flat(1)"
gosub "placementPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Свойства и Описания`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

"dimensionPage":
	x1 = 0
	x2 = 120
	x3 = 210
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 360
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1

	stUIProfile = `Размер (Станд. BS)`
	
	ui_style 0, 1
	
	ui_outfield	stUIProfile ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_style 0,0
	ui_infield{2} "Profile", 	 x2,   yCurrent, 	x3 - x2, infieldHeight

	ui_outfield	`Длина Элемента`, x4, yCurrent+4, x5 - x4, outfieldHeight
	ui_infield	"A", x6, yCurrent,xEnd - x6, infieldHeight ui_tooltip `Длина Секции`

	yCurrent = yCurrent + dy


	ui_separator 0, yCurrent, xEnd, yCurrent

	pageStartY = 31						!control some infield position with this
	yCurr = pageStartY + 11
	x1 = 16
	x2 = 31
	infieldWidth = 99
	mod_1 = 0
	mod_2 = 0
		
	yCurr = yCurr + 28
		
	picHeight	= 170
	picWidth	= 180
	picPosX		= 120
	picPosY		= yCurrent + 40
	infieldWidth = 69
	
	picIndex = 1		! "UI_C1.png"
	posSectWidth = 270

	ui_infield	"sectWidth",	picPosX+picWidth/2-infieldWidth/2+10+mod_1,	picPosY+picHeight+2, infieldWidth,	19 	ui_tooltip toolTipWidth
	ui_infield	"sectDepth",	picPosX-infieldWidth-2,	picPosY + 70, infieldWidth,	19 	ui_tooltip toolTipDepth
	ui_infield	"sectThick",	picPosX+picWidth/2-infieldWidth/2+10+mod_2,	picPosY - 20,		infieldWidth,	19 	ui_tooltip toolTipThickness

	ui_infield	"flangeThick",	picPosX+picWidth+2,	pageStartY + 47,		infieldWidth,	19 ui_tooltip toolTipFlangeThk
	ui_infield	"Rad",			picPosX+picWidth+2,	pageStartY + 133,		infieldWidth,	19 ui_tooltip toolTipRootRad
	ui_infield	"filletDepth",	picPosX-infieldWidth-2,	pageStartY + 147,	infieldWidth,	19 ui_tooltip toolTipDepthFillets
	ui_infield	"flangeDepth",	picPosX+picWidth+2,	pageStartY + 95,	infieldWidth,	19 ui_tooltip toolTipInnerDepth

	ui_pict	picIndex, picPosX, picPosY, picWidth, picHeight, 1

return

"placementPage":
	x1 = 0
	x2 = 120
	x3 = 170
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 360
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	ui_style 0,0
	
	ui_outfield	`Точка Вставки`, x1, yCurrent+4, x5 - x4, outfieldHeight
	ui_infield{3}	"iInsertPoint", 	 x3,   yCurrent, 	x6 - x3, infieldHeight,
				8, "", 1, 1,
				0, 0, 0, 0, 
				"", stInsertPointValues[1], INSERTPOINT_TOP,
				"", stInsertPointValues[2], INSERTPOINT_CENTER,
				"", stInsertPointValues[3], INSERTPOINT_BOTTOM

	yCurrent = yCurrent + dy

	ui_separator 0, yCurrent, xEnd, yCurrent

	uiPictX = 170
	uiPictY = yCurrent + 60
	uiPictWdt = 149
	uiPictHgt = 76
	uiPictMargin = 5
	uiPictInfWdt = 90

	uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
	uiPicLeftEND = uiPicLeftStart - uiPictMargin

	uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3

	uiPicBelow = uiPictY + uiPictHgt + uiPictMargin

	uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

	UI_PICT 2, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreBeamInvT"

	ui_infield "ArcAngle", uiPicLeftStart, uiPictY+45, uiPictInfWdt, infieldHeight UI_TOOLTIP `Угол Дуги`

	ui_infield "ArcRadius", uiPictX+30, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Радиус Дуги по Оси`

return

"AppearancePage":
	dim st2DParamNames[9][2]
		st2DParamNames[1][1] = "axis" 				: st2DParamNames[1][2] = "ui_list_axis2D_flat(1)"
		st2DParamNames[2][1] = "only_axis" 			: st2DParamNames[2][2] = "ui_list_axis2D_flat(1)"
		st2DParamNames[3][1] = "axis_line_type" 	: st2DParamNames[3][2] = "ui_list_axisLineType_flat(1)"
		st2DParamNames[4][1] = "axis_line_pen" 		: st2DParamNames[4][2] = "ui_list_axisLinePen_flat(1)"
		st2DParamNames[5][1] = "gs_ShowMoreHtps2D"	: st2DParamNames[5][2] = "ui_list_extraHS_flat(1)"
		st2DParamNames[6][1] = "gs_cont_pen" 		: st2DParamNames[6][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[7][1] = "gs_fill_type" 		: st2DParamNames[7][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[8][1] = "gs_fill_pen" 		: st2DParamNames[8][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[9][1] = "gs_back_pen" 		: st2DParamNames[9][2] = "ui_list_fillBgPen_flat(1)"

	dim st3DParamNames[6][2]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_resol"			: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
		st3DParamNames[3][1] = "stArcResolType"		: st3DParamNames[3][2] = "ui_list_resol_flat(1)"
		st3DParamNames[4][1] = "SegLength"			: st3DParamNames[4][2] = ""
		st3DParamNames[5][1] = "SegNum"				: st3DParamNames[5][2] = ""
		st3DParamNames[6][1] = "gs_shadow"			: st3DParamNames[6][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "col_mat"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										bShowIconColumn = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	file_dependence "ui_list_axis2D_flat(1)"
	file_dependence "ui_list_axisLineType_flat(1)"
	file_dependence "ui_list_axisLinePen_flat(1)"
	file_dependence "ui_list_extraHS_flat(1)"


return

"descriptionPage":

	dim stProp[10]
		stProp[1] = "secondMomentX"
		stProp[2] = "secondMomentY"
		stProp[3] = "gyrationRadX"
		stProp[4] = "gyrationRadY"
		stProp[5] = "elastModX"
		stProp[6] = "elastModY"
		stProp[7] = "plastModX"
		stProp[8] = "plastModY"
		stProp[9] = "sectArea"
		stProp[10] = "massPerMeter"


	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_structure_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										bProp = 1,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stProp		= stProp,
										stParamNames = stParamNames
return



