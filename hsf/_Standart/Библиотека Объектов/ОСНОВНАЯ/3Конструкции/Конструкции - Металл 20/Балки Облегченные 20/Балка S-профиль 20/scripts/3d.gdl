
sectDepth=sectDepth/1000
sectThick=sectThick/1000
Rad=sectThick
sectLip=sectLip/1000
TopWidth=TopWidth/1000
BottomWidth=BottomWidth/1000

sectWidth=(TopWidth+BottomWidth-sectThick)

!!!********* Revision of Cutting Angles

!!!********* Cut1,Cut2
gs_cutang12_bot=(-gs_cutang1_bot)	!!<gs_cutang12_bot=(-gs_cutang2_bot)
gs_cutang21_bot=gs_cutang2_bot		!!<gs_cutang21_bot=gs_cutang1_bot
gs_cutang43_up=(-gs_cutang4_up)		!!<gs_cutang43_up=(-gs_cutang3_up)
gs_cutang34_up=(gs_cutang3_up)		!!<gs_cutang34_up=(gs_cutang4_up)

!!!********* Cut3
gs_cutang23_bot=-gs_cutang2_bot
gs_cutang33_up=-gs_cutang3_up

!!!*************************************


pen gs_cont_pen
material col_mat
if gs_shadow=0 THEN
	shadow off
else
	shadow on
endif

IF iInsertPoint <> INSERTPOINT_BOTTOM THEN
	mulz -1
	Angle=Angle*(-1)
ENDIF

rotz 90
rotx 90
IF iRotAxis = ROT_CROSSECT THEN rotx -Angle
IF iRotAxis = ROT_AXIS THEN rotz Angle

IF iInsertPoint = INSERTPOINT_CENTER THEN
	Addy -sectDepth/2
ENDIF

unID=1

! ==============================================================================
! HOTSPOTS
! ==============================================================================

hotspot 0,sectDepth/2,0,unID : unID=unID+1
hotspot 0,sectDepth/2,0,unID,a,1+256 : unID=unID+1
hotspot 0,sectDepth/2,-1,unID,a,3 : unID=unID+1
hotspot 0,sectDepth/2,a,unID,a,2 : unID=unID+1

IF iRotAxis = ROT_AXIS THEN

	IF iInsertPoint <> INSERTPOINT_BOTTOM THEN temp_ang=temp_ang*(-1)
	rotz -(temp_ang)

		HOTSPOT 0,0,a/2,unID,temp_ang,6,Angle : unID=unID+1
		HOTSPOT 0,sectDepth,a/2,unID,temp_ang,4+128,Angle : unID=unID+1
		HOTSPOT 0,0,a/2+1,unID,temp_ang,7,Angle : unID=unID+1
		HOTSPOT -(sectDepth)*sin(temp_ang),(sectDepth)*cos(temp_ang),a/2,unID,temp_ang,5,Angle : unID=unID+1

	DEL 1

	! lower side
	IF iCutModeBottom = CUTMODE_PERPENDICULAR THEN
		hotspot 0,sectDepth/1,0,unID : unID=unID+1
		hotspot 0,0,0,unID : unID=unID+1
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot (TopWidth-sectThick/2),sectDepth/1,-(TopWidth-sectThick/2)*tan(gs_cutang12_bot),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),0,(BottomWidth-sectThick/2)*tan(gs_cutang21_bot),unID : unID=unID+1
		ELSE
			hotspot (TopWidth-sectThick/2),0,-(TopWidth-sectThick/2)*tan(gs_cutang12_bot),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,(BottomWidth-sectThick/2)*tan(gs_cutang21_bot),unID : unID=unID+1
		ENDIF
	ENDIF
	IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot (TopWidth-sectThick/2),sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot 0,sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot 0,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ELSE
			hotspot (TopWidth-sectThick/2),0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot 0,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot 0,0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
		ENDIF
	ENDIF
	! upper side
	IF iCutModeTop = CUTMODE_PERPENDICULAR THEN
		hotspot 0,0,a,unID : unID=unID+1
		hotspot 0,sectDepth/1,a,unID : unID=unID+1
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot (TopWidth-sectThick/2),sectDepth/1,a-(TopWidth-sectThick/2)*tan(gs_cutang34_up),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),0,a+(BottomWidth-sectThick/2)*tan(gs_cutang43_up),unID : unID=unID+1
		ELSE
			hotspot (TopWidth-sectThick/2),0,a-(TopWidth-sectThick/2)*tan(gs_cutang34_up),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,a+(BottomWidth-sectThick/2)*tan(gs_cutang43_up),unID : unID=unID+1
		ENDIF
	ENDIF

	IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			HOTSPOT (TopWidth-sectThick/2),sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			HOTSPOT -(BottomWidth-sectThick/2),0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			HOTSPOT 0,sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			HOTSPOT 0,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			ELSE
			HOTSPOT (TopWidth-sectThick/2),0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			HOTSPOT 0,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			HOTSPOT -(BottomWidth-sectThick/2),sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			HOTSPOT 0,0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
		ENDIF
	ENDIF
ENDIF

unID=100
IF iRotAxis = ROT_CROSSECT THEN
	! axis points

	IF iInsertPoint <> INSERTPOINT_BOTTOM THEN temp_ang=temp_ang*(-1)
	IF iInsertPoint = INSERTPOINT_CENTER THEN Addy sectDepth/2

	ROTx temp_ang
		HOTSPOT 0,0,0,unID,temp_ang,6,Angle : unID=unID+1
		HOTSPOT 0,0,1,unID,temp_ang,4+128,Angle : unID=unID+1
		HOTSPOT -1,0,0,unID,temp_ang,7,Angle : unID=unID+1
		HOTSPOT 0,a/2*sin(temp_ang),a/2*cos(temp_ang),unID,temp_ang,5,Angle : unID=unID+1
	DEL 1

	IF iInsertPoint = INSERTPOINT_CENTER THEN DEL 1

	! lower side
	IF iCutModeBottom = CUTMODE_PERPENDICULAR THEN
		hotspot 0,0,0,unID : unID=unID+1
		hotspot 0,sectDepth/1,0,unID : unID=unID+1

		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot sectWidth/2,0,-sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang12_bot)/cos(Angle)),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),sectDepth/1,-(TopWidth-sectThick/2)*tan(gs_cutang12_bot),unID : unID=unID+1
		ELSE
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,(BottomWidth-sectThick/2)*tan(gs_cutang21_bot),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),0,-(TopWidth-sectThick/2)*tan(gs_cutang12_bot),unID : unID=unID+1
		ENDIF
	ENDIF

	IF iCutModeBottom = CUTMODE_ALONGVERTICAL THEN
		hotspot 0,sectDepth/1,sectDepth/2*tan(Angle),unID : unID=unID+1
		hotspot 0,0,-sectDepth/2*tan(Angle),unID : unID=unID+1

		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot -(BottomWidth-sectThick/2),0,-sectDepth/2*tan(Angle)-(-(BottomWidth-sectThick/2)*tan(gs_cutang21_bot)/cos(Angle)),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),sectDepth/1,sectDepth/2*tan(Angle)-((TopWidth-sectThick/2)*tan(gs_cutang12_bot)/cos(Angle)),unID : unID=unID+1
		ELSE
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,sectDepth/2*tan(Angle)-(-(BottomWidth-sectThick/2)*tan(gs_cutang21_bot)/cos(Angle)),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),0,-sectDepth/2*tan(Angle)-((TopWidth-sectThick/2)*tan(gs_cutang12_bot)/cos(Angle)),unID : unID=unID+1
		ENDIF
	ENDIF

	IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot (TopWidth-sectThick/2),sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot 0,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ELSE
			hotspot (TopWidth-sectThick/2),0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot 0,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		ENDIF
	ENDIF
	! upper side
	IF iCutModeTop = CUTMODE_PERPENDICULAR THEN
		hotspot 0,0,a,unID : unID=unID+1
		hotspot 0,sectDepth/1,a,unID : unID=unID+1

		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot -(BottomWidth-sectThick/2),0,a+(BottomWidth-sectThick/2)*tan(gs_cutang43_up),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),sectDepth/1,a-(TopWidth-sectThick/2)*tan(gs_cutang34_up),unID : unID=unID+1
		ELSE
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,a+(BottomWidth-sectThick/2)*tan(gs_cutang43_up),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),0,a-(TopWidth-sectThick/2)*tan(gs_cutang34_up),unID : unID=unID+1
		ENDIF
	ENDIF

	IF iCutModeTop = CUTMODE_ALONGVERTICAL THEN
		hotspot 0,sectDepth/1,a+sectDepth/2*tan(Angle),unID : unID=unID+1
		hotspot 0,0,a-sectDepth/2*tan(Angle),unID : unID=unID+1
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot -(BottomWidth-sectThick/2),0,a-sectDepth/2*tan(Angle)-(-(BottomWidth-sectThick/2)*tan(gs_cutang43_up)/cos(Angle)),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),sectDepth/1,a+sectDepth/2*tan(Angle)-((TopWidth-sectThick/2)*tan(gs_cutang34_up)/cos(Angle)),unID : unID=unID+1
		ELSE
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,a+sectDepth/2*tan(Angle)-(-(BottomWidth-sectThick/2)*tan(gs_cutang43_up)/cos(Angle)),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),0,a-sectDepth/2*tan(Angle)-((TopWidth-sectThick/2)*tan(gs_cutang34_up)/cos(Angle)),unID : unID=unID+1

		ENDIF
	ENDIF

	IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			hotspot -(BottomWidth-sectThick/2),0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot 0,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		ELSE
			hotspot -(BottomWidth-sectThick/2),sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot (TopWidth-sectThick/2),0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot 0,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		ENDIF
	ENDIF
ENDIF

IF gs_detlevel_3D_m = 0 THEN end

! ==============================================================================
! CUTPOLY
! ==============================================================================

! ------------------------------------------------------------------------------
! Cutplanes on the bottom
! ------------------------------------------------------------------------------

dz = 0

IF iCutModeBottom = CUTMODE_PERPENDICULAR THEN	! Perpendicular to Axis
	ROTx -90
		IF ABS(gs_cutang2_bot)>0.001 or ABS(gs_cutang1_bot)>0.001 THEN
		dz=max(2*(BottomWidth-sectThick/2)*tan(gs_cutang21_bot),2*(TopWidth-sectThick/2)*tan(-gs_cutang1_bot),1)

		CUTPOLYA 4,0,0,
			0,0,15,
			(TopWidth-sectThick/2),(TopWidth-sectThick/2)*tan(gs_cutang12_bot),15,
			(TopWidth-sectThick/2),dz,15,
			0,dz,15

		CUTPOLYA 4,0,0,
			0,0,15,
			-(BottomWidth-sectThick/2),(BottomWidth-sectThick/2)*tan(-gs_cutang21_bot),15,
			-(BottomWidth-sectThick/2),dz,15,
			0,dz,15
		ENDIF
	DEL 1
ENDIF

IF iCutModeBottom = CUTMODE_ALONGVERTICAL THEN	! Along the Vertical Axis

	  IF iRotAxis = ROT_CROSSECT THEN
		ROTx Angle-90
		ADDy sectDepth/2*sin(Angle)
			dz=max(2*(BottomWidth-sectThick/2)*tan(gs_cutang21_bot)+sectDepth*sin(Angle), \
        2*(TopWidth-sectThick/2)*tan(gs_cutang12_bot)+sectDepth*sin(Angle),1)
			dzz=dz*cos(Angle)+ABS(sectDepth*sin(Angle))
			CUTPOLYA 4,0,0,
				0,0,15,
				(TopWidth-sectThick/2),(TopWidth-sectThick/2)*tan(gs_cutang12_bot),15,
				(TopWidth-sectThick/2),dzz,15,
				0,dzz,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-(BottomWidth-sectThick/2),-(BottomWidth-sectThick/2)*tan(gs_cutang21_bot),15,
				-(BottomWidth-sectThick/2),dzz,15,
				0,dzz,15
		DEL 2
	  ENDIF

ENDIF

IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN	! Along a Horizontal Axis
	IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
		dz=abs(max((-sectDepth/1)*tan(gs_cutang1_bot),(sectDepth/1)*tan(gs_cutang23_bot),1))
		ADDy sectDepth/2
		ROTy 90
		ROTz 90
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
				CUTPOLYA 6,0,0,
					(sectDepth/1),-dz,15,
					(sectDepth/1),(sectDepth/1)*tan(gs_cutang1_bot),15,
					0,0,15,
					-(sectDepth/1),-(sectDepth/1)*tan(gs_cutang23_bot),15,
					-(sectDepth/1),-dz,15,
					0,-dz,15
		ELSE
				CUTPOLYA 6,0,0,
					(sectDepth/1),-dz,15,
					(sectDepth/1),(sectDepth/1)*tan(-gs_cutang23_bot),15,
					0,0,15,
					-(sectDepth/1),-(sectDepth/1)*tan(-gs_cutang1_bot),15,
					-(sectDepth/1),-dz,15,
					0,-dz,15
		ENDIF
		DEL 3
	ENDIF
ENDIF


! ------------------------------------------------------------------------------
! Cutplanes on the top
! ------------------------------------------------------------------------------

ADDz a
dz2 = 0
IF iCutModeTop = CUTMODE_PERPENDICULAR THEN
  ROTx -90
	  IF ABS(gs_cutang4_up)>0.001 or ABS(gs_cutang3_up)>0.001 THEN
		dz2=max(1,ABS(2*((BottomWidth-sectThick/2)*tan(gs_cutang43_up))),ABS(2*((TopWidth-sectThick/2)*tan(-gs_cutang34_up))))
		CUTPOLYA 4,0,0,
			0,0,15,
			(TopWidth-sectThick/2),-(TopWidth-sectThick/2)*tan(-gs_cutang34_up),15,
			(TopWidth-sectThick/2),-dz2,15,
			0,-dz2,15
		CUTPOLYA 4,0,0,
			0,0,15,
			-(BottomWidth-sectThick/2),-(BottomWidth-sectThick/2)*tan(gs_cutang43_up),15,
			-(BottomWidth-sectThick/2),-dz2,15,
			0,-dz2,15
	  ENDIF
  DEL 1
ENDIF

IF iCutModeTop = CUTMODE_ALONGVERTICAL THEN
  IF iRotAxis = ROT_CROSSECT THEN ADDz -sectDepth/2*tan(Angle)

	  ROTx Angle-90
			dz2=max(ABS(2*(BottomWidth-sectThick/2)/2*tan(gs_cutang43_up)+sectDepth*sin(Angle)), \
        ABS(2*(TopWidth-sectThick/2)*tan(gs_cutang34_up)+sectDepth*sin(Angle)),1)
			dzz2=dz2*cos(Angle)+ABS(sectDepth*sin(Angle))
			CUTPOLYA 4,0,0,
				0,0,15,
				(TopWidth-sectThick/2),-(TopWidth-sectThick/2)*tan(-gs_cutang34_up),15,
				(TopWidth-sectThick/2),-dzz2,15,
				0,-dzz2,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-(BottomWidth-sectThick/2),-(BottomWidth-sectThick/2)*tan(gs_cutang43_up),15,
				-(BottomWidth-sectThick/2),-dzz2,15,
				0,-dzz2,15
	  DEL 1
  IF iRotAxis = ROT_CROSSECT THEN DEL 1
ENDIF

IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN
	IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
	dz2=max(ABS(sectDepth/1*tan(gs_cutang33_up)),ABS(sectDepth/1*tan(gs_cutang4_up)),1)
		ADDy sectDepth/2
		ROTy 90
		ROTz 90
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			CUTPOLYA 6,0,0,
				(sectDepth/1)/cos(gs_cutang33_up),dz2,15,
				(sectDepth/1)/cos(gs_cutang4_up),((sectDepth/1)/cos(gs_cutang4_up))*tan(gs_cutang33_up),15,
				0,0,15,
				-(sectDepth/1)/cos(gs_cutang33_up),((sectDepth/1)/cos(gs_cutang33_up))*tan(-gs_cutang4_up),15,
				-(sectDepth/1)/cos(gs_cutang33_up),dz2,15,
				0,dz2,15
		ELSE
			CUTPOLYA 6,0,0,
				(sectDepth/1)/cos(-gs_cutang4_up),dz2,15,
				(sectDepth/1)/cos(-gs_cutang33_up),((sectDepth/1)/cos(-gs_cutang33_up))*tan(-gs_cutang4_up),15,
				0,0,15,
				-(sectDepth/1)/cos(-gs_cutang4_up),((sectDepth/1)/cos(-gs_cutang4_up))*tan(gs_cutang33_up),15,
				-(sectDepth/1)/cos(-gs_cutang4_up),dz2,15,
				0,dz2,15
		ENDIF
		DEL 3
	ENDIF
ENDIF

DEL 1

ADDz -dz


IF iInsertPoint <> INSERTPOINT_BOTTOM THEN
	addy sectDepth
	muly -1
ENDIF

! ==============================================================================
! Simple
! ==============================================================================

RRadius=sectThick*2
sectWidth=sectWidth/2+sectThick/2	!! Section Width
ADDy sectDepth/2
IF gs_detlevel_3D_m = 1 OR GLOB_CONTEXT=2 THEN

	PRISM_ 13,a+dz+dz2,
			sectThick/2, -sectDepth/2, 15,
			sectThick/2, sectDepth/2-sectThick, 15,
			TopWidth-sectThick/2-sectThick, sectDepth/2-sectThick, 15,
			TopWidth-sectThick/2-sectThick, sectDepth/2-sectLip, 15,
			TopWidth-sectThick/2, sectDepth/2-sectLip, 15,
			TopWidth-sectThick/2, sectDepth/2, 15,
			-sectThick/2, sectDepth/2,15,
			-sectThick/2, -sectDepth/2+sectThick,15,
			-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectThick,15,
			-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectLip,15,
			-(BottomWidth-sectThick/2), -sectDepth/2+sectLip,15,
			-(BottomWidth-sectThick/2), -sectDepth/2,15,
			sectThick/2, -sectDepth/2, -1

ENDIF

! ==============================================================================
! Detailed
! ==============================================================================

IF gs_detlevel_3D_m = 2 AND GLOB_CONTEXT<>2 THEN
	! Curved Resolution
	S=90/gs_resol
	FOR k=0 TO 90 STEP S
		PUT -sectThick/2-Rad+(Rad+sectThick)*sin(k),-sectDepth/2+sectThick+Rad-(Rad+sectThick)*cos(k),79
	NEXT k
	FOR k=90 TO 180 STEP S
		PUT sectThick/2+Rad-Rad*sin(k),sectDepth/2-sectThick-Rad-Rad*cos(k),79
	NEXT k
	FOR k=180 TO 270 STEP S
		PUT TopWidth-sectThick/2-sectThick-Rad-Rad*sin(k),sectDepth/2-sectThick-Rad-Rad*cos(k),79
	NEXT k
	FOR k=270 TO 180 STEP -S
		PUT TopWidth-sectThick/2-sectThick-Rad-(Rad+sectThick)*sin(k),sectDepth/2-sectThick-Rad-(Rad+sectThick)*cos(k),79
	NEXT k
	FOR k=180 TO 90 STEP -S
		PUT -sectThick/2+sectThick+Rad-(Rad+sectThick)*sin(k),sectDepth/2-sectThick-Rad-(Rad+sectThick)*cos(k),79
	NEXT k
	FOR k=90 TO 0 STEP -S
		PUT -sectThick/2-Rad+(Rad)*sin(k),-sectDepth/2+sectThick+Rad-(Rad)*cos(k),79
	NEXT k
	FOR k=0 TO -90 STEP -S
		PUT -(BottomWidth-sectThick/2)+sectThick+Rad+(Rad)*sin(k),-sectDepth/2+sectThick+Rad-(Rad)*cos(k),79
	NEXT k
	FOR k=-90 TO 0 STEP S
		PUT -(BottomWidth-sectThick/2)+sectThick+Rad+(Rad+sectThick)*sin(k),-sectDepth/2+sectThick+Rad-(Rad+sectThick)*cos(k),79
	NEXT k

	PRISM_ 5+NSP/3,a+dz+dz2,
			GET(NSP/8),
			GET(NSP/7),
			GET(NSP/6),
			TopWidth-sectThick/2-sectThick, sectDepth/2-sectLip, 15,
			TopWidth-sectThick/2, sectDepth/2-sectLip, 15,
			GET(NSP/5),
			GET(NSP/4),
			GET(NSP/3),
			GET(NSP/2),
			-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectLip,15,
			-(BottomWidth-sectThick/2), -sectDepth/2+sectLip,15,
			GET(NSP),
			-sectThick/2-Rad, -sectDepth/2, -1
ENDIF
DEl 1

IF iInsertPoint <> INSERTPOINT_BOTTOM THEN DEL 2

DEL 3

IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
	IF iCutModeBottom = CUTMODE_PERPENDICULAR THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeBottom = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_AXIS THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN :CUTEND: ENDIF
ENDIF

	IF iCutModeBottom = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_CROSSECT THEN :CUTEND:CUTEND: ENDIF


IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
	IF iCutModeTop = CUTMODE_PERPENDICULAR THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN :CUTEND: ENDIF
ENDIF

	IF iCutModeTop = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_CROSSECT THEN :CUTEND:CUTEND: ENDIF


DEL 1

IF iInsertPoint <> INSERTPOINT_BOTTOM THEN DEl 1
IF iInsertPoint = INSERTPOINT_CENTER THEN DEL 1

End
