
sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
Rad=sectThick

! --- Revision of Cutting Angles ---

! --- Cut1,Cut2
gs_cutang12_bot=(-gs_cutang1_bot)	! <gs_cutang12_bot=(-gs_cutang2_bot)
gs_cutang21_bot=gs_cutang2_bot		! <gs_cutang21_bot=gs_cutang1_bot
gs_cutang43_up=(-gs_cutang4_up)		! <gs_cutang43_up=(-gs_cutang3_up)
gs_cutang34_up=(gs_cutang3_up)		! <gs_cutang34_up=(gs_cutang4_up)

! -- Cut3
gs_cutang23_bot=-gs_cutang2_bot
gs_cutang33_up=-gs_cutang3_up


pen gs_cont_pen
material col_mat
if gs_shadow=0 then
	shadow off
else
	shadow on
endif

if iInsertPoint <> INSERTPOINT_BOTTOM then
	mulz -1
	Angle=Angle*(-1)
endif

rotz 90
rotx 90
if iRotAxis = ROT_CROSSECT then rotx -Angle
if iRotAxis = ROT_AXIS then rotz Angle

if iInsertPoint = INSERTPOINT_CENTER then
	Addy -sectDepth/2
endif

unID=1

! ==============================================================================
! HOTSPOTS
! ==============================================================================

hotspot 0,sectDepth/2,0,unID : unID=unID+1
hotspot 0,sectDepth/2,0,unID,a,1+256 : unID=unID+1
hotspot 0,sectDepth/2,-1,unID,a,3 : unID=unID+1
hotspot 0,sectDepth/2,a,unID,a,2 : unID=unID+1

if iRotAxis = ROT_AXIS then

	if iInsertPoint <> INSERTPOINT_BOTTOM then temp_ang=temp_ang*(-1)
	rotz -(temp_ang)

	HOTSPOT 0,0,a/2,unID,temp_ang,6,Angle : unID=unID+1
	HOTSPOT 0,sectDepth,a/2,unID,temp_ang,4+128,Angle : unID=unID+1
	HOTSPOT 0,0,a/2+1,unID,temp_ang,7,Angle : unID=unID+1
	HOTSPOT -(sectDepth)*sin(temp_ang),(sectDepth)*cos(temp_ang),a/2,unID,temp_ang,5,Angle : unID=unID+1

	DEL 1

	! --- lower side
	if iCutModeBottom = CUTMODE_PERPENDICULAR then
		hotspot sectWidth/2,sectDepth/1,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot sectWidth/2,0,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot -sectWidth/2,sectDepth/1,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
		hotspot -sectWidth/2,0,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
	endif
	if iCutModeBottom = CUTMODE_ALONGHORIZONTAL then
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		else
			hotspot sectWidth/2,0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -sectWidth/2,0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		endif
	endif

	! --- upper side
	if iCutModeTop = CUTMODE_PERPENDICULAR then
		hotspot sectWidth/2,sectDepth/1,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot sectWidth/2,0,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot -sectWidth/2,sectDepth/1,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
		hotspot -sectWidth/2,0,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
	endif
	if iCutModeTop = CUTMODE_ALONGHORIZONTAL then
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		else
			hotspot sectWidth/2,0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -sectWidth/2,0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		endif
	endif
endif

if iRotAxis = ROT_CROSSECT then
	! axis points

	if iInsertPoint <> INSERTPOINT_BOTTOM then temp_ang=temp_ang*(-1)
	if iInsertPoint = INSERTPOINT_CENTER then Addy sectDepth/2

	ROTx temp_ang
	HOTSPOT 0,0,0,unID,temp_ang,6,Angle : unID=unID+1
	HOTSPOT 0,0,1,unID,temp_ang,4+128,Angle : unID=unID+1
	HOTSPOT -1,0,0,unID,temp_ang,7,Angle : unID=unID+1
	HOTSPOT 0,a/2*sin(temp_ang),a/2*cos(temp_ang),unID,temp_ang,5,Angle : unID=unID+1
	DEL 1

	if iInsertPoint = INSERTPOINT_CENTER then DEL 1

	! --- lower side
	if iCutModeBottom = CUTMODE_PERPENDICULAR then
		hotspot sectWidth/2,sectDepth/1,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot sectWidth/2,0,-sectWidth/2*tan(gs_cutang12_bot),unID : unID=unID+1
		hotspot -sectWidth/2,sectDepth/1,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
		hotspot -sectWidth/2,0,sectWidth/2*tan(gs_cutang21_bot),unID : unID=unID+1
	endif
	if iCutModeBottom = CUTMODE_ALONGVERTICAL then
		hotspot sectWidth/2,sectDepth/1,sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang12_bot)/cos(Angle)),unID : unID=unID+1
		hotspot sectWidth/2,0,-sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang12_bot)/cos(Angle)),unID : unID=unID+1
		hotspot -sectWidth/2,sectDepth/1,sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang21_bot)/cos(Angle)),unID : unID=unID+1
		hotspot -sectWidth/2,0,-sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang21_bot)/cos(Angle)),unID : unID=unID+1
	endif
	if iCutModeBottom = CUTMODE_ALONGHORIZONTAL then
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,0,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		else
			hotspot sectWidth/2,0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
			hotspot -sectWidth/2,0,sectDepth/2*tan(gs_cutang1_bot),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,-sectDepth/2*tan(gs_cutang23_bot),unID : unID=unID+1
		endif
	endif

	! --- upper side
	IF iCutModeTop = CUTMODE_PERPENDICULAR then
		hotspot sectWidth/2,sectDepth/1,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot sectWidth/2,0,a-sectWidth/2*tan(gs_cutang34_up),unID : unID=unID+1
		hotspot -sectWidth/2,sectDepth/1,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
		hotspot -sectWidth/2,0,a+sectWidth/2*tan(gs_cutang43_up),unID : unID=unID+1
	endif
	if iCutModeTop = CUTMODE_ALONGVERTICAL then
		hotspot sectWidth/2,sectDepth/1,a+sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang34_up)/cos(Angle)),unID : unID=unID+1
		hotspot sectWidth/2,0,a-sectDepth/2*tan(Angle)-(sectWidth/2*tan(gs_cutang34_up)/cos(Angle)),unID : unID=unID+1
		hotspot -sectWidth/2,sectDepth/1,a+sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang43_up)/cos(Angle)),unID : unID=unID+1
		hotspot -sectWidth/2,0,a-sectDepth/2*tan(Angle)+(sectWidth/2*tan(gs_cutang43_up)/cos(Angle)),unID : unID=unID+1
	endif
	if iCutModeTop = CUTMODE_ALONGHORIZONTAL then
		if iInsertPoint = INSERTPOINT_BOTTOM then
			hotspot sectWidth/2,sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,0,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		else
			hotspot sectWidth/2,0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
			hotspot -sectWidth/2,0,a+sectDepth/2*tan(gs_cutang33_up),unID : unID=unID+1
			hotspot -sectWidth/2,sectDepth/1,a-sectDepth/2*tan(gs_cutang4_up),unID : unID=unID+1
		endif
	endif
endif

if gs_detlevel_3D_m = 0 then end

! ==============================================================================
! CUTPOLY
! ==============================================================================

! ------------------------------------------------------------------------------
! Cutplanes on the bottom
! ------------------------------------------------------------------------------

dz = 0

IF iCutModeBottom = CUTMODE_PERPENDICULAR THEN	! Perpendicular to Axis
	ROTx -90
		IF ABS(gs_cutang2_bot)>0.001 or ABS(gs_cutang1_bot)>0.001 THEN
		dz=max(2*sectWidth/2*tan(gs_cutang21_bot),2*sectWidth/2*tan(-gs_cutang1_bot),1)
		CUTPOLYA 4,0,0,
			0,0,15,
			sectWidth/2,sectWidth/2*tan(gs_cutang12_bot),15,
			sectWidth/2,dz,15,
			0,dz,15

		CUTPOLYA 4,0,0,
			0,0,15,
			-sectWidth/2,sectWidth/2*tan(-gs_cutang21_bot),15,
			-sectWidth/2,dz,15,
			0,dz,15
		ENDIF
	DEL 1
ENDIF

IF iCutModeBottom = CUTMODE_ALONGVERTICAL THEN	! Along a Vertical Axis

	  IF iRotAxis = ROT_AXIS THEN
		ROTx -90
		ROTy -Angle
			IF ABS(gs_cutang2_bot)>0.001 or ABS(gs_cutang1_bot)>0.001 THEN
			dz=max(2*((sectWidth/2)*sin(Angle)+(sectDepth/2)*cos(Angle))*tan(gs_cutang12_bot), \
        2*((sectWidth/2)*sin(Angle)+(sectDepth/2)*cos(Angle))*tan(gs_cutang21_bot),1)
			ADDy 0
			CUTPOLYA 4,0,0,
				0,0,15,
				(sectWidth/1)*sin(Angle)+(sectDepth/2)*cos(Angle),((sectWidth/1)*sin(Angle)+(sectDepth/2)*cos(Angle))*tan(gs_cutang12_bot),15,
				sectWidth/1+0.001,dz,15,
				0,dz,15

			CUTPOLYA 4,0,0,
				0,0,15,
				-sectWidth/2-0.005,-sectWidth/2*tan(gs_cutang21_bot),15,
				-sectWidth/2-0.005,dz,15,
				0,dz,15
			DEL 1
			ENDIF
		DEL 2
	  ENDIF

	  IF iRotAxis = ROT_CROSSECT THEN
		ROTx Angle-90
		ADDy sectDepth/2*sin(Angle)
!			IF gs_cutang1_bot>=0 or gs_cutang2_bot>=0 THEN
			dz=max(2*sectWidth/2*tan(gs_cutang21_bot)+sectDepth*sin(Angle),2*sectWidth/2*tan(gs_cutang12_bot)+sectDepth*sin(Angle),1)
			dzz=dz*cos(Angle)+ABS(sectDepth*sin(Angle))
			CUTPOLYA 4,0,0,
				0,0,15,
				sectWidth/2,sectWidth/2*tan(gs_cutang12_bot),15,
				sectWidth/2,dzz,15,
				0,dzz,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-sectWidth/2,-sectWidth/2*tan(gs_cutang21_bot),15,
				-sectWidth/2,dzz,15,
				0,dzz,15
!			ENDIF
		DEL 2
	  ENDIF

ENDIF

IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN	! Along a Horizontal Axis
	IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
		dz=abs(max((-sectDepth/1)*tan(gs_cutang1_bot),(sectDepth/1)*tan(gs_cutang23_bot),1))
		ADDy sectDepth/2
		ROTy 90
		ROTz 90
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
				CUTPOLYA 6,0,0,
					(sectDepth/1),-dz,15,
					(sectDepth/1),(sectDepth/1)*tan(gs_cutang1_bot),15,
					0,0,15,
					-(sectDepth/1),-(sectDepth/1)*tan(gs_cutang23_bot),15,
					-(sectDepth/1),-dz,15,
					0,-dz,15
		ELSE
				CUTPOLYA 6,0,0,
					(sectDepth/1),-dz,15,
					(sectDepth/1),(sectDepth/1)*tan(-gs_cutang23_bot),15,
					0,0,15,
					-(sectDepth/1),-(sectDepth/1)*tan(-gs_cutang1_bot),15,
					-(sectDepth/1),-dz,15,
					0,-dz,15
		ENDIF
		DEL 3
	ENDIF
ENDIF


! ------------------------------------------------------------------------------
! Cutplanes on the top
! ------------------------------------------------------------------------------

ADDz a
dz2 = 0
IF iCutModeTop = CUTMODE_PERPENDICULAR THEN
  ROTx -90
	  IF ABS(gs_cutang4_up)>0.001 or ABS(gs_cutang3_up)>0.001 THEN
		dz2=max(1,ABS(2*((sectWidth/2)*tan(gs_cutang43_up))),ABS(2*((sectWidth/2)*tan(-gs_cutang34_up))))
		CUTPOLYA 4,0,0,
			0,0,15,
			sectWidth/2,-sectWidth/2*tan(-gs_cutang34_up),15,
			sectWidth/2,-dz2,15,
			0,-dz2,15
		CUTPOLYA 4,0,0,
			0,0,15,
			-sectWidth/2,-sectWidth/2*tan(gs_cutang43_up),15,
			-sectWidth/2,-dz2,15,
			0,-dz2,15
	  ENDIF
  DEL 1
ENDIF

IF iCutModeTop = CUTMODE_ALONGVERTICAL THEN
  IF iRotAxis = ROT_CROSSECT THEN ADDz -sectDepth/2*tan(Angle)

	  ROTx Angle-90
!			  IF gs_cutang4_up>=0 or gs_cutang3_up>=0 THEN
			dz2=max(ABS(2*sectWidth/2*tan(gs_cutang43_up)+sectDepth*sin(Angle)),ABS(2*sectWidth/2*tan(gs_cutang34_up)+sectDepth*sin(Angle)),1)
			dzz2=dz2*cos(Angle)+ABS(sectDepth*sin(Angle))
			CUTPOLYA 4,0,0,
				0,0,15,
				sectWidth/2,-sectWidth/2*tan(-gs_cutang34_up),15,
				sectWidth/2,-dzz2,15,
				0,-dzz2,15
			CUTPOLYA 4,0,0,
				0,0,15,
				-sectWidth/2,-sectWidth/2*tan(gs_cutang43_up),15,
				-sectWidth/2,-dzz2,15,
				0,-dzz2,15
!			  ENDIF
	  DEL 1
  IF iRotAxis = ROT_CROSSECT THEN DEL 1
ENDIF

IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN
	IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
	dz2=max(ABS(sectDepth/1*tan(gs_cutang33_up)),ABS(sectDepth/1*tan(gs_cutang4_up)),1)
		ADDy sectDepth/2
		ROTy 90
		ROTz 90
		IF iInsertPoint = INSERTPOINT_BOTTOM THEN
			CUTPOLYA 6,0,0,
				(sectDepth/1)/cos(gs_cutang33_up),dz2,15,
				(sectDepth/1)/cos(gs_cutang4_up),((sectDepth/1)/cos(gs_cutang4_up))*tan(gs_cutang33_up),15,
				0,0,15,
				-(sectDepth/1)/cos(gs_cutang33_up),((sectDepth/1)/cos(gs_cutang33_up))*tan(-gs_cutang4_up),15,
				-(sectDepth/1)/cos(gs_cutang33_up),dz2,15,
				0,dz2,15
		ELSE
			CUTPOLYA 6,0,0,
				(sectDepth/1)/cos(-gs_cutang4_up),dz2,15,
				(sectDepth/1)/cos(-gs_cutang33_up),((sectDepth/1)/cos(-gs_cutang33_up))*tan(-gs_cutang4_up),15,
				0,0,15,
				-(sectDepth/1)/cos(-gs_cutang4_up),((sectDepth/1)/cos(-gs_cutang4_up))*tan(gs_cutang33_up),15,
				-(sectDepth/1)/cos(-gs_cutang4_up),dz2,15,
				0,dz2,15
		ENDIF
		DEL 3
	ENDIF
ENDIF

DEL 1

ADDz -dz



IF gs_detlevel_3D_m = 1 OR GLOB_CONTEXT=2 THEN

	! ==========================================================================
	! Simple
	! ==========================================================================
	put	sectThick,sectThick,15
	put	sectThick,sectDepth-sectThick,15
	put	0,sectDepth,15
	put	0,0,15
else
	! ==========================================================================
	! Detailed
	! ==========================================================================
	S=90/gs_resol	! Curved Resolution
	FOR k=0 TO 90 STEP S
		PUT sectThick+Rad-Rad*sin(k),sectThick+Rad-Rad*cos(k),79
	NEXT k
	FOR k=90 TO 180 STEP S
		PUT sectThick+Rad-Rad*sin(k),sectDepth-sectThick-Rad-Rad*cos(k),79
	NEXT k
	FOR k=180 TO 90 STEP -S
		PUT sectThick+Rad-(Rad+sectThick)*sin(k),sectDepth-sectThick-Rad-(Rad+sectThick)*cos(k),79
	NEXT k
	FOR k=90 TO 0 STEP -S
		PUT sectThick+Rad-(Rad+sectThick)*sin(k),sectThick+Rad-(Rad+sectThick)*cos(k),79
	NEXT k
ENDIF

ADDx -sectWidth/2
PRISM_ 5+NSP/3,a+dz+dz2,
			sectWidth,0,15,
			sectWidth,sectThick,15,
			GET(NSP/4),
			GET(NSP/3),
			sectWidth,sectDepth-sectThick,15,
			sectWidth,sectDepth,15,
			GET(NSP/2),
			GET(NSP),
			sectWidth,0,-1
DEL 1

DEL 3

IF ABS(gs_cutang1_bot)>0.001 or ABS(gs_cutang2_bot)>0.001 THEN
	IF iCutModeBottom = CUTMODE_PERPENDICULAR THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeBottom = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_AXIS THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeBottom = CUTMODE_ALONGHORIZONTAL THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang1_bot>=0 or gs_cutang2_bot>=0 THEN
	IF iCutModeBottom = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_CROSSECT THEN :CUTEND:CUTEND: ENDIF
!ENDIF

IF ABS(gs_cutang3_up)>0.001 or ABS(gs_cutang4_up)>0.001 THEN
	IF iCutModeTop = CUTMODE_PERPENDICULAR THEN :CUTEND:CUTEND: ENDIF
	IF iCutModeTop = CUTMODE_ALONGHORIZONTAL THEN :CUTEND: ENDIF
ENDIF
!IF gs_cutang3_up>=0 or gs_cutang4_up>=0 THEN
	IF iCutModeTop = CUTMODE_ALONGVERTICAL AND iRotAxis = ROT_CROSSECT THEN :CUTEND:CUTEND: ENDIF
!ENDIF

DEL 1

IF iInsertPoint <> INSERTPOINT_BOTTOM THEN DEl 1
IF iInsertPoint = INSERTPOINT_CENTER THEN DEL 1

End
