


sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
flangeThick=flangeThick/1000
Rad=Rad/1000
toeRad=toeRad/1000
filletDepth=filletDepth/1000

PEN gs_cont_pen
FILL gs_fill_type

unID=1
hLineID = 1000

HOTSPOT2 0, 0,unID : unID=unID+1
HOTSPOT2  0, -sectDepth/2,unID : unID=unID+1
HOTSPOT2 0, sectDepth/2,unID : unID=unID+1


IF axis=1 AND only_axis=0 OR axis=0 THEN

	HOTSPOT2 -a/2, -b/2,unID : unID=unID+1
	HOTSPOT2 a/2, -b/2,unID : unID=unID+1
	HOTSPOT2 a/2, b/2,unID : unID=unID+1
	HOTSPOT2 -a/2, b/2,unID : unID=unID+1

	FlangeAng=ATN(((sectDepth-filletDepth)/2-Rad-flangeThick)/(sectWidth/4-sectThick/2-Rad))

	FlangeSlope1=flangeThick-((sectWidth/4)*tan(FlangeAng))
	FlangeSlope2=(sectWidth-sectThick)/2*tan(FlangeAng)
	FlangeSlope3=(sectWidth/2-sectThick/2-Rad-toeRad)*tan(FlangeAng)


	ADD2 0,-sectDepth/2
		IF gs_detlevel_2D_m = 1 THEN

			POLY2_b 13, 1+2+4+32, gs_fill_pen, gs_back_pen,
				sectWidth/2,0,1+32,
				sectWidth/2,FlangeSlope1,1+32,
				sectThick/2,FlangeSlope1+FlangeSlope2,1+32,
				sectThick/2,sectDepth-FlangeSlope1-FlangeSlope2,1+32,
				sectWidth/2,sectDepth-FlangeSlope1,1+32,
				sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth-FlangeSlope1,1+32,
				-sectThick/2,sectDepth-FlangeSlope1-FlangeSlope2,1+32,
				-sectThick/2,FlangeSlope1+FlangeSlope2,1+32,
				-sectWidth/2,FlangeSlope1,1+32,
				-sectWidth/2,0,1+32,
				sectWidth/2,0,-1

			gosub "hotlines2D"

			IF gs_ShowMoreHtps2D=1 THEN
				HOTSPOT2 sectWidth/2, FlangeSlope1,unID : unID=unID+1
				HOTSPOT2 sectThick/2, FlangeSlope1+FlangeSlope2,unID : unID=unID+1
				HOTSPOT2 sectThick/2, sectDepth-(FlangeSlope1+FlangeSlope2),unID : unID=unID+1
				HOTSPOT2 a/2, sectDepth-FlangeSlope1,unID : unID=unID+1

				HOTSPOT2 -sectWidth/2, FlangeSlope1,unID : unID=unID+1
				HOTSPOT2 -sectThick/2, FlangeSlope1+FlangeSlope2,unID : unID=unID+1
				HOTSPOT2 -sectThick/2, sectDepth-(FlangeSlope1+FlangeSlope2),unID : unID=unID+1
				HOTSPOT2 -a/2, sectDepth-FlangeSlope1,unID : unID=unID+1
			ENDIF

		ENDIF

		IF gs_detlevel_2D_m = 2 THEN

		ToeStartY2=(sectDepth-filletDepth)/2-Rad-FlangeSlope3-toeRad

			POLY2_b 29, 1+2+4+32, gs_fill_pen, gs_back_pen,
				sectWidth/2,0,1+32,
				sectWidth/2,ToeStartY2,1+32,
				sectWidth/2-toeRad,ToeStartY2,900+32,
				sectWidth/2-toeRad,ToeStartY2+toeRad,3001+32,
				sectThick/2+Rad,(sectDepth-filletDepth)/2-Rad,1+32,
				sectThick/2+Rad,(sectDepth-filletDepth)/2,900+32,
				sectThick/2,(sectDepth-filletDepth)/2,3001+32,
				sectThick/2,sectDepth-(sectDepth-filletDepth)/2,1+32,
				sectThick/2+Rad,sectDepth-(sectDepth-filletDepth)/2,900+32,
				sectThick/2+Rad,sectDepth-(sectDepth-filletDepth)/2+Rad,3001+32,
				sectWidth/2-toeRad,sectDepth-ToeStartY2-toeRad,1+32,
				sectWidth/2-toeRad,sectDepth-ToeStartY2,900+32,
				sectWidth/2,sectDepth-ToeStartY2,3001+32,
				sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth-ToeStartY2,1+32,
				-sectWidth/2+toeRad,sectDepth-ToeStartY2,900+32,
				-sectWidth/2+toeRad,sectDepth-ToeStartY2-toeRad,3001+32,
				-sectThick/2-Rad,sectDepth-(sectDepth-filletDepth)/2+Rad,1+32,
				-sectThick/2-Rad,sectDepth-(sectDepth-filletDepth)/2,900+32,
				-sectThick/2,sectDepth-(sectDepth-filletDepth)/2,3001+32,
				-sectThick/2,(sectDepth-filletDepth)/2,1+32,
				-sectThick/2-Rad,(sectDepth-filletDepth)/2,900+32,
				-sectThick/2-Rad,(sectDepth-filletDepth)/2-Rad,3001+32,
				-sectWidth/2+toeRad,ToeStartY2+toeRad,1+32,
				-sectWidth/2+toeRad,ToeStartY2,900+32,
				-sectWidth/2,ToeStartY2,3001+32,
				-sectWidth/2,0,1+32,
				sectWidth/2,0,-1

			gosub "hotlines2D"

			IF gs_ShowMoreHtps2D=1 THEN
				HOTSPOT2 sectThick/2,(sectDepth-filletDepth)/2,unID : unID=unID+1
				HOTSPOT2 sectThick/2,sectDepth-(sectDepth-filletDepth)/2,unID : unID=unID+1
				HOTSPOT2 -sectThick/2,(sectDepth-filletDepth)/2,unID : unID=unID+1
				HOTSPOT2 -sectThick/2,sectDepth-(sectDepth-filletDepth)/2,unID : unID=unID+1
			ENDIF

		ENDIF
	DEL 1


ENDIF

unID=100
IF (axis=1) THEN
	PEN axis_line_pen
	LINE_TYPE axis_line_type

	LINE2 -axis_XDim, 0, axis_XDim,0
	LINE2 0, -axis_YDim, 0,axis_YDim


	FOR k1=1 TO 2
		HOTSPOT2 0, 0, unID, axis_XDim, 1+128: unID=unID+1	!!! Axis X Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_XDim, 3: unID=unID+1
		HOTSPOT2 axis_XDim, 0, unID, axis_XDim, 2: unID=unID+1
	ROT2 180
	NEXT k1
	DEL 2

	FOR k2=1 TO 2
	ROT2 90
		HOTSPOT2 0, 0, unID, axis_YDim, 1+128: unID=unID+1	!!! Axis Y Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_YDim, 3: unID=unID+1
		HOTSPOT2 axis_YDim, 0, unID, axis_YDim, 2: unID=unID+1
	DEL 1
	ROT2 180
	NEXT k2
	DEL 2

	IF only_axis=1 THEN
		HOTSPOT2 -sectWidth/2, 0,unID : unID=unID+1
		HOTSPOT2 sectWidth/2, 0,unID : unID=unID+1
	ENDIF

ENDIF

end

"hotlines2D":
	HOTLINE2 0,0,0,sectDepth, hLineID : hLineID = hLineID + 1
	HOTLINE2 -sectWidth/2,0,sectWidth/2,0, hLineID : hLineID = hLineID + 1
	HOTLINE2 -sectWidth/2,sectDepth,sectWidth/2,sectDepth, hLineID : hLineID = hLineID + 1
return

