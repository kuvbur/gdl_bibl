


sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
flangeThick=flangeThick/1000
Rad=Rad/1000

PEN gs_cont_pen
FILL gs_fill_type

unID=1
hLineID = 1000

HOTSPOT2 0, 0,unID : unID=unID+1
HOTSPOT2  0, -sectDepth/2,unID : unID=unID+1
HOTSPOT2 0, sectDepth/2,unID : unID=unID+1


IF axis=1 AND only_axis=0 OR axis=0 THEN

	RRadius=Rad

	HOTSPOT2 -a/2, -b/2,unID : unID=unID+1
	HOTSPOT2 a/2, -b/2,unID : unID=unID+1
	HOTSPOT2 a/2, b/2,unID : unID=unID+1
	HOTSPOT2 -a/2, b/2,unID : unID=unID+1

	IF gs_ShowMoreHtps2D=1 THEN

		HOTSPOT2 a/2, -b/2+flangeThick,unID : unID=unID+1
		HOTSPOT2 a/2, b/2-flangeThick,unID : unID=unID+1
		HOTSPOT2 -a/2, b/2-flangeThick,unID : unID=unID+1
		HOTSPOT2 -a/2, -b/2+flangeThick,unID : unID=unID+1

	ENDIF

	ADD2 0,-sectDepth/2

		IF gs_detlevel_2D_m = 1 THEN
			POLY2_b 13, 1+2+4+32, gs_fill_pen, gs_back_pen,
				-sectWidth/2,0,1+32,
				sectWidth/2,0,1+32,
				sectWidth/2,flangeThick,1+32,
				sectThick/2,flangeThick,1+32,
				sectThick/2,sectDepth-flangeThick,1+32,
				sectWidth/2,sectDepth-flangeThick,1+32,
				sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth-flangeThick,1+32,
				-sectThick/2,sectDepth-flangeThick,1+32,
				-sectThick/2,flangeThick,1+32,
				-sectWidth/2,flangeThick,1+32,
				-sectWidth/2,0,-1

			gosub "hotlines2D"
		ENDIF

		IF gs_detlevel_2D_m = 2 THEN
			POLY2_b 17, 1+2+4+32, gs_fill_pen, gs_back_pen,
				-sectWidth/2,0,1+32,
				sectWidth/2,0,1+32,
				sectWidth/2,flangeThick,1+32,
				sectThick/2+RRadius,flangeThick,1+32,
				sectThick/2,flangeThick+RRadius,1001+32,
				sectThick/2,sectDepth-flangeThick-RRadius,1+32,
				sectThick/2+RRadius,sectDepth-flangeThick,1001+32,
				sectWidth/2,sectDepth-flangeThick,1+32,
				sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth,1+32,
				-sectWidth/2,sectDepth-flangeThick,1+32,
				-sectThick/2-RRadius,sectDepth-flangeThick,1+32,
				-sectThick/2,sectDepth-flangeThick-RRadius,1001+32,
				-sectThick/2,flangeThick+RRadius,1+32,
				-sectThick/2-RRadius,flangeThick,1001+32,
				-sectWidth/2,flangeThick,1+32,
				-sectWidth/2,0,-1

			gosub "hotlines2D"
		ENDIF

	DEL 1

	IF gs_ShowMoreHtps2D=1 THEN
		IF gs_detlevel_2D_m = 1 THEN
			HOTSPOT2 sectThick/2, -b/2+flangeThick,unID : unID=unID+1
			HOTSPOT2 sectThick/2, b/2-flangeThick,unID : unID=unID+1
			HOTSPOT2 -sectThick/2, b/2-flangeThick,unID : unID=unID+1
			HOTSPOT2 -sectThick/2, -b/2+flangeThick,unID : unID=unID+1
		ELSE
			HOTSPOT2 sectThick/2+RRadius, -b/2+flangeThick,unID : unID=unID+1
			HOTSPOT2 sectThick/2, -b/2+flangeThick+RRadius,unID : unID=unID+1
			HOTSPOT2 sectThick/2+RRadius, b/2-flangeThick,unID : unID=unID+1
			HOTSPOT2 sectThick/2, b/2-flangeThick-RRadius,unID : unID=unID+1
			HOTSPOT2 -sectThick/2-RRadius, b/2-flangeThick,unID : unID=unID+1
			HOTSPOT2 -sectThick/2, b/2-flangeThick-RRadius,unID : unID=unID+1
			HOTSPOT2 -sectThick/2-RRadius, -b/2+flangeThick,unID : unID=unID+1
			HOTSPOT2 -sectThick/2, -b/2+flangeThick+RRadius,unID : unID=unID+1
		ENDIF
	ENDIF

ENDIF

unID=100
IF (axis=1) THEN
	PEN axis_line_pen
	LINE_TYPE axis_line_type

	LINE2 -axis_XDim, 0, axis_XDim,0
	LINE2 0, -axis_YDim, 0,axis_YDim


	FOR k1=1 TO 2
		HOTSPOT2 0, 0, unID, axis_XDim, 1+128: unID=unID+1	!!! Axis X Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_XDim, 3: unID=unID+1
		HOTSPOT2 axis_XDim, 0, unID, axis_XDim, 2: unID=unID+1
	ROT2 180
	NEXT k1
	DEL 2

	FOR k2=1 TO 2
	ROT2 90
		HOTSPOT2 0, 0, unID, axis_YDim, 1+128: unID=unID+1	!!! Axis Y Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_YDim, 3: unID=unID+1
		HOTSPOT2 axis_YDim, 0, unID, axis_YDim, 2: unID=unID+1
	DEL 1
	ROT2 180
	NEXT k2
	DEL 2

	IF only_axis=1 THEN
		HOTSPOT2 -sectWidth/2, 0,unID : unID=unID+1
		HOTSPOT2 sectWidth/2, 0,unID : unID=unID+1
	ENDIF

ENDIF

end

"hotlines2D":
	HOTLINE2 0,0,0,sectDepth, hLineID : hLineID = hLineID + 1
	HOTLINE2 -sectWidth/2,0,sectWidth/2,0, hLineID : hLineID = hLineID + 1
	HOTLINE2 -sectWidth/2,sectDepth,sectWidth/2,sectDepth, hLineID : hLineID = hLineID + 1
return

