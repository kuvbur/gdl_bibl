
sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
flangeThick=flangeThick/1000
Rad=Rad/1000
toeRad=toeRad/1000
filletDepth=filletDepth/1000

pen gs_cont_pen
material col_mat
if gs_shadow=0 then
	SHADOW off
else
	SHADOW on
endif

IF gs_detlevel_3D_m = 0 THEN END


FlangeAng=ATN(((sectDepth-filletDepth)/2-Rad-flangeThick)/(sectWidth/2-sectThick-Rad))

FlangeSlope1=flangeThick-((sectWidth/2)*tan(FlangeAng))
FlangeSlope2=(sectWidth-sectThick)*tan(FlangeAng)
FlangeSlope3=(sectWidth-sectThick-Rad-toeRad)*tan(FlangeAng)

!---------Cutplanes
modz = zzyzx
IF bCutPlanes Then

	wsds = sqr(sectDepth^2 + sectWidth^2)

	!!!-------------------- Top Cutting
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then
		modz = modz+tan(60)* wsds/2

		Rotz gs_cutdir_top
		addz zzyzx
		rotx 90

		cutpoly 5,
				-wsds/2,tan(60)* wsds /2,
				wsds/2,tan(60)* wsds /2,
				wsds/2, tan(gs_cutang1_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang2_bot)* wsds /2

		del 3
	Endif

	!!!-------------------- Bottom Cutting
	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		modz = modz+tan(60)* wsds /2

		Rotz gs_cutdir_but
		rotx 90

		cutpoly 5,
				-wsds/2,-tan(60)* wsds /2,
				wsds/2,-tan(60)* wsds /2,
				wsds/2, tan(gs_cutang3_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang4_bot)* wsds /2

		del 2
		addz -tan(60)*wsds/2
	Endif
Endif
!---------Cutplanes

ADD 0,-sectDepth/2,0
	IF gs_detlevel_3D_m = 1 THEN

			PRISM_ 13,modz,
				sectWidth/2,0,15,
				sectWidth/2,FlangeSlope1,15,
				sectThick/2,FlangeSlope1+FlangeSlope2,15,
				sectThick/2,sectDepth-FlangeSlope1-FlangeSlope2,15,
				sectWidth/2,sectDepth-FlangeSlope1,15,
				sectWidth/2,sectDepth,15,
				-sectWidth/2,sectDepth,15,
				-sectWidth/2,sectDepth-FlangeSlope1,15,
				-sectThick/2,sectDepth-FlangeSlope1-FlangeSlope2,15,
				-sectThick/2,FlangeSlope1+FlangeSlope2,15,
				-sectWidth/2,FlangeSlope1,15,
				-sectWidth/2,0,15,
				sectWidth/2,0,-1

	ENDIF
	IF gs_detlevel_3D_m = 2 THEN

		ToeStartY2=(sectDepth-filletDepth)/2-Rad-FlangeSlope3-toeRad

			!!! Curved Resolution
			S=90/gs_resol
			FOR k=0 TO 90 STEP S
				PUT sectWidth/2-toeRad+toeRad*cos(k),ToeStartY2+toeRad*sin(k),79
			NEXT k
			FOR k=0 TO 90 STEP S
				PUT sectThick/2+Rad-Rad*sin(k),(sectDepth-filletDepth)/2-Rad*cos(k),79
			NEXT k
			FOR k=90 TO 180 STEP S
				PUT sectThick/2+Rad-Rad*sin(k),sectDepth-(sectDepth-filletDepth)/2-Rad*cos(k),79
			NEXT k
			FOR k=270 TO 360 STEP S
				PUT sectWidth/2-toeRad+toeRad*cos(k),sectDepth-ToeStartY2+toeRad*sin(k),79
			NEXT k

			FOR k=0 TO 90 STEP S
				PUT -sectWidth/2+toeRad-toeRad*cos(k),sectDepth-ToeStartY2-toeRad*sin(k),79
			NEXT k
			FOR k=0 TO 90 STEP S
				PUT -sectThick/2-Rad+Rad*sin(k),sectDepth-(sectDepth-filletDepth)/2+Rad*cos(k),79
			NEXT k
			FOR k=90 TO 180 STEP S
				PUT -sectThick/2-Rad+Rad*sin(k),(sectDepth-filletDepth)/2+Rad*cos(k),79
			NEXT k
			FOR k=270 TO 360 STEP S
				PUT -sectWidth/2+toeRad-toeRad*cos(k),ToeStartY2-toeRad*sin(k),79
			NEXT k

			PRISM_ 7+NSP/3,modz,
				sectWidth/2,0,15,
				sectWidth/2,ToeStartY2,79,
				GET(NSP/8),
				GET(NSP/7),
				GET(NSP/6),
				GET(NSP/5),
				sectWidth/2,sectDepth,15,
				-sectWidth/2,sectDepth,15,
				-sectWidth/2,sectDepth-ToeStartY2,79,
				GET(NSP/4),
				GET(NSP/3),
				GET(NSP/2),
				GET(NSP),
				-sectWidth/2,0,15,
				sectWidth/2,0,-1

	ENDIF
DEL 1

!---------Cutplanes
IF bCutPlanes Then
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then cutend

	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		del 1
		cutend
	Endif
Endif
!---------Cutplanes

unID=1
HOTSPOT 0,0,0,unID,ZZYZX,1+256 : unID=unID+1
HOTSPOT 0,0,-1,unID,ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,ZZYZX,unID,ZZYZX,2 : unID=unID+1

!---------Bottom Hotspots
cutAngleHSBot1 = gs_cutang3_bot
cutAngleHSBot3 = gs_cutang4_bot

if iCutModeColBottom = CUTMODE_ALONG_X then
	HPBotx = sectDepth
	cutAngleHSBot2 = gs_cutang4_bot
	cutAngleHSBot4 = gs_cutang3_bot
else
	HPBotx = sectWidth
	cutAngleHSBot2 = gs_cutang3_bot
	cutAngleHSBot4 = gs_cutang4_bot
endif

rotx 90

HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHSBot2)* HPBotx/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHSBot3)* HPBotx/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHSBot4)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHSBot1)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
del 1

!---------Top Hotspots
cutAngleHS1 = gs_cutang1_bot
cutAngleHS3 = gs_cutang2_bot

if iCutModeColTop = CUTMODE_ALONG_X then
	HPx = sectDepth
	cutAngleHS2 = gs_cutang2_bot
	cutAngleHS4 = gs_cutang1_bot
else
	HPx = sectWidth
	cutAngleHS2 = gs_cutang1_bot
	cutAngleHS4 = gs_cutang2_bot
endif

addz zzyzx
rotx 90

HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHS2)* HPx/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHS3)* HPx/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHS4)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHS1)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
del 2

end




