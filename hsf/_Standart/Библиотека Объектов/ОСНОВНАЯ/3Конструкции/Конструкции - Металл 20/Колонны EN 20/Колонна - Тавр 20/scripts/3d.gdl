
sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
Rad=Rad/1000

pen gs_cont_pen
material col_mat
if gs_shadow=0 then
	shadow off
else
	shadow on
endif

IF gs_detlevel_3D_m = 0 THEN END

!---------Cutplanes
modz = zzyzx
IF bCutPlanes Then

	wsds = sqr(sectDepth^2 + sectWidth^2)

	!!!-------------------- Top Cutting
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then
		modz = modz+tan(60)* wsds/2

		Rotz gs_cutdir_top
		addz zzyzx
		rotx 90

		cutpoly 5,
				-wsds/2,tan(60)* wsds /2,
				wsds/2,tan(60)* wsds /2,
				wsds/2, tan(gs_cutang1_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang2_bot)* wsds /2

		del 3
	Endif

	!!!-------------------- Bottom Cutting
	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		modz = modz+tan(60)* wsds /2

		Rotz gs_cutdir_but
		rotx 90

		cutpoly 5,
				-wsds/2,-tan(60)* wsds /2,
				wsds/2,-tan(60)* wsds /2,
				wsds/2, tan(gs_cutang3_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang4_bot)* wsds /2

		del 2
		addz -tan(60)*wsds/2
	Endif
Endif
!---------Cutplanes


RRadius=Rad

ADD 0,-sectDepth/2,0
	IF gs_detlevel_3D_m = 1 THEN
		PRISM_  9, modz,
			sectThick/2,0,15,
			sectThick/2,sectDepth-sectThick,15,
			sectWidth/2,sectDepth-sectThick,15,
			sectWidth/2,sectDepth,15,
			-sectWidth/2,sectDepth,15,
			-sectWidth/2,sectDepth-sectThick,15,
			-sectThick/2,sectDepth-sectThick,15,
			-sectThick/2,0,15,
			sectThick/2,0,-1
	ENDIF
	IF gs_detlevel_3D_m = 2 THEN
	RESOL gs_resol
		PRISM_  11, modz,
			sectThick/2,0,15,
			sectThick/2,sectDepth-sectThick-RRadius,79,
			sectThick/2+RRadius,sectDepth-sectThick,1079,
			sectWidth/2,sectDepth-sectThick,15,
			sectWidth/2,sectDepth,15,
			-sectWidth/2,sectDepth,15,
			-sectWidth/2,sectDepth-sectThick,15,
			-sectThick/2-RRadius,sectDepth-sectThick,79,
			-sectThick/2,sectDepth-sectThick-RRadius,1079,
			-sectThick/2,0,15,
			sectThick/2,0,-1
	ENDIF
DEL 1

!---------Cutplanes
IF bCutPlanes Then
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then cutend

	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		del 1
		cutend
	Endif
Endif
!---------Cutplanes

unID=1
HOTSPOT 0,0,0,unID,ZZYZX,1+256 : unID=unID+1
HOTSPOT 0,0,-1,unID,ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,ZZYZX,unID,ZZYZX,2 : unID=unID+1
!---------Bottom Hotspots
cutAngleHSBot1 = gs_cutang3_bot
cutAngleHSBot3 = gs_cutang4_bot

if iCutModeColBottom = CUTMODE_ALONG_X then
	HPBotx = sectDepth
	HPBotxMiddle = sectDepth
	cutAngleHSBot2 = gs_cutang4_bot
	cutAngleHSBot4 = gs_cutang3_bot
else
	HPBotx = sectWidth
	HPBotxMiddle = sectThick
	cutAngleHSBot2 = gs_cutang3_bot
	cutAngleHSBot4 = gs_cutang4_bot
endif

rotx 90

HOTSPOT sectThick/2,	bCutPlanes*tan(cutAngleHSBot2)* HPBotxMiddle/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectThick/2,	bCutPlanes*tan(cutAngleHSBot3)* HPBotxMiddle/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHSBot4)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHSBot1)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
del 1

!---------Top Hotspots
cutAngleHS1 = gs_cutang1_bot
cutAngleHS3 = gs_cutang2_bot

if iCutModeColTop = CUTMODE_ALONG_X then
	HPx = sectDepth
	HPxMiddle = sectDepth
	cutAngleHS2 = gs_cutang2_bot
	cutAngleHS4 = gs_cutang1_bot
else
	HPx = sectWidth
	HPxMiddle = sectThick
	cutAngleHS2 = gs_cutang1_bot
	cutAngleHS4 = gs_cutang2_bot
endif

addz zzyzx
rotx 90

HOTSPOT sectThick/2,	bCutPlanes*tan(cutAngleHS2)* HPxMiddle/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectThick/2,	bCutPlanes*tan(cutAngleHS3)* HPxMiddle/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHS4)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHS1)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
del 2

end
