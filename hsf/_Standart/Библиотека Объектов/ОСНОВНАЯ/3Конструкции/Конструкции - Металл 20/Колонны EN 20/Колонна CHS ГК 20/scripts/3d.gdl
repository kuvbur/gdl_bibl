
sectDepth=sectDepth/1000
sectThick=sectThick/1000


pen gs_cont_pen
material col_mat
if gs_shadow=0 then
	shadow off
else
	shadow on
endif

if gs_detlevel_3D_m = 0 then end

!---------Cutplanes
modz = zzyzx
IF bCutPlanes Then
	sectWidth = sectDepth
	wsds = sqr(sectDepth^2 + sectWidth^2)

	!!!-------------------- Top Cutting
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then
		modz = modz+tan(60)* wsds/2

		Rotz gs_cutdir_top
		addz zzyzx
		rotx 90

		cutpoly 5,
				-wsds/2,tan(60)* wsds /2,
				wsds/2,tan(60)* wsds /2,
				wsds/2, tan(gs_cutang1_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang2_bot)* wsds /2

		del 3
	Endif

	!!!-------------------- Bottom Cutting
	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		modz = modz+tan(60)* wsds /2

		Rotz gs_cutdir_but
		rotx 90

		cutpoly 5,
				-wsds/2,-tan(60)* wsds /2,
				wsds/2,-tan(60)* wsds /2,
				wsds/2, tan(gs_cutang3_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang4_bot)* wsds /2

		del 2
		addz -tan(60)*wsds/2
	Endif
Endif
!---------Cutplanes

if gs_detlevel_3D_m = 2 then
	resol gs_resol
	prism_  3, modz,
			0, 0, 979,
			sectDepth/2, 360, 4079,
			sectDepth/2-sectThick, 360, 4079
endif

!---------Cutplanes
IF bCutPlanes Then
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then cutend

	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		del 1
		cutend
	Endif
Endif
!---------Cutplanes

unID=1
HOTSPOT 0,0,0,unID,ZZYZX,1+256 : unID=unID+1
HOTSPOT 0,0,-1,unID,ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,ZZYZX,unID,ZZYZX,2 : unID=unID+1

!---------Bottom Hotspots
rotx 90

if iCutModeColBottom = CUTMODE_ALONG_X then
	HOTSPOT sectDepth/2,	0,												0, 				unID : unID = unID+1
	HOTSPOT -sectDepth/2,	0,												0, 				unID : unID = unID+1
	HOTSPOT 0,				bCutPlanes*tan(gs_cutang4_bot)* sectDepth/2,	sectDepth/2, 	unID : unID = unID+1
	HOTSPOT 0,				bCutPlanes*tan(gs_cutang3_bot)* sectDepth/2,	-sectDepth/2, 	unID : unID = unID+1
else
	HOTSPOT sectDepth/2,	bCutPlanes*tan(gs_cutang3_bot)* sectDepth/2,	0, 				unID : unID = unID+1
	HOTSPOT -sectDepth/2,	bCutPlanes*tan(gs_cutang4_bot)* sectDepth/2,	0, 				unID : unID = unID+1
	HOTSPOT 0,				0,												sectDepth/2, 	unID : unID = unID+1
	HOTSPOT 0,				0,												-sectDepth/2, 	unID : unID = unID+1
endif
del 1

!---------Top Hotspots
addz zzyzx
rotx 90

if iCutModeColTop = CUTMODE_ALONG_X then
	HOTSPOT sectDepth/2,	0,												0, 				unID : unID = unID+1
	HOTSPOT -sectDepth/2,	0,												0, 				unID : unID = unID+1
	HOTSPOT 0,				bCutPlanes*tan(gs_cutang2_bot)* sectDepth/2,	sectDepth/2, 	unID : unID = unID+1
	HOTSPOT 0,				bCutPlanes*tan(gs_cutang1_bot)* sectDepth/2,	-sectDepth/2, 	unID : unID = unID+1
else
	HOTSPOT sectDepth/2,	bCutPlanes*tan(gs_cutang1_bot)* sectDepth/2,	0, 				unID : unID = unID+1
	HOTSPOT -sectDepth/2,	bCutPlanes*tan(gs_cutang2_bot)* sectDepth/2,	0, 				unID : unID = unID+1
	HOTSPOT 0,				0,												sectDepth/2, 	unID : unID = unID+1
	HOTSPOT 0,				0,												-sectDepth/2, 	unID : unID = unID+1
endif
del 2

end

