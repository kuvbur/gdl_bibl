

! --------------------------------------------------------------------------------
! hide All Parameters list
! --------------------------------------------------------------------------------

hideparameter all "A", "B", "ZZYZX"

dataFileName = "CHS Hot-Finished(14).txt"
file_dependence "CHS Hot-Finished(14).txt"
sor=63          ! Number of Lines
prof=18         ! Number of Sections

if GLOB_MODPAR_NAME = "Profile" or siz_mtr[1] = "" then
	dim vekt[18]
	dim namevekt[18]
	for i = 1 to prof
		vekt[i] = 0
		namevekt[i] = ""
	next i

	szelv=open("text", dataFileName, "separator='\t', mode=ro, Library")
	j=1
	for i=2 to sor+1
		v1 = 0
		v2 = 0
		n=input(szelv, i, 2, v1, v2)
		nincs = 1
		for k=1 to prof
			if vekt[k]=v2 then
				nincs = 0
			endif
		next k
		if nincs = 1 then
			vekt[j] = v2
			namevekt[j] = v1
			j=j+1
		endif
	next i

	namevekt[prof] = `СПЕЦИАЛЬНЫЙ`

	siz_mtr = namevekt

	parameters siz_mtr = siz_mtr

	if Profile<>`СПЕЦИАЛЬНЫЙ` then
		for i=1 to prof
			if namevekt[i]=Profile then
				parameters sectDepth=vekt[i]
				sectDepth=vekt[i]
			endif
		next i
	endif

	close szelv
endif

call "FM_types" parameters all

if gs_cont_pen=0 then parameters gs_cont_pen=28
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19
IF axis_line_pen=0 THEN PARAMETERS axis_line_pen=68

VALUES "gs_resol" RANGE [3,)

values "sectThick" range (0, sectDepth/2)

dim stDetLevel3d[3]
	stDetLevel3d[1] = `Простой`
	stDetLevel3d[2] = `Детальный`
	stDetLevel3d[3] = `Откл.`

values "gs_detlevel_3D" stDetLevel3d[2],
			stDetLevel3d[3]
values "gs_detlevel_3D_m" 2, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	if gs_detlevel_3D = stDetLevel3d[3] then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetLevel3d[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetLevel3d[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

!---------Cutplanes
dim stCutModeColValues[2]
	stCutModeColValues[1] = `Вдоль оси X`
	stCutModeColValues[2] = `Вдоль оси Y`

values "stCutModeColTop" stCutModeColValues
values "iCutModeColTop" CUTMODE_ALONG_X, CUTMODE_ALONG_Y

if GLOB_MODPAR_NAME = "stCutModeColTop" then
	iCutModeColTop = CUTMODE_ALONG_X
	if stCutModeColTop = stCutModeColValues[2] then iCutModeColTop = CUTMODE_ALONG_Y
	parameters iCutModeColTop = iCutModeColTop
else
	stCutModeColTop = stCutModeColValues[1]
	if iCutModeColTop = CUTMODE_ALONG_Y then stCutModeColTop = stCutModeColValues[2]
	parameters stCutModeColTop = stCutModeColTop
endif

values "stCutModeColBottom" stCutModeColValues
values "iCutModeColBottom" CUTMODE_ALONG_X, CUTMODE_ALONG_Y

if GLOB_MODPAR_NAME = "stCutModeColBottom" then
	iCutModeColBottom = CUTMODE_ALONG_X
	if stCutModeColBottom = stCutModeColValues[2] then iCutModeColBottom = CUTMODE_ALONG_Y
	parameters iCutModeColBottom = iCutModeColBottom
else
	stCutModeColBottom = stCutModeColValues[1]
	if iCutModeColBottom = CUTMODE_ALONG_Y then stCutModeColBottom = stCutModeColValues[2]
	parameters stCutModeColBottom = stCutModeColBottom
endif
values "gs_cutdir_top" RANGE [-90,90]
values "gs_cutdir_but" RANGE [-90,90]
values "gs_cutang1_bot" RANGE [-60,60]
values "gs_cutang2_bot" RANGE [-60,60]
values "gs_cutang3_bot" RANGE [-60,60]
values "gs_cutang4_bot" RANGE [-60,60]

IF iCutModeColTop = CUTMODE_ALONG_Y Then
	parameters gs_cutdir_top = 0.00
				gs_cutdir_top = 0.00
Else
	parameters gs_cutdir_top = 90.00
				gs_cutdir_top = 90.00
Endif

IF iCutModeColBottom = CUTMODE_ALONG_Y Then
	parameters gs_cutdir_but = 0.00
				gs_cutdir_but = 0.00
Else
	parameters gs_cutdir_but = 90.00
				gs_cutdir_but = 90.00
Endif

IF NOT(bCutPlanes) Then
	hideparameter "stCutModeColTop", "iCutModeColTop", "stCutModeColBottom", "iCutModeColBottom", "gs_cutang1_bot","gs_cutang2_bot","gs_cutang3_bot","gs_cutang4_bot"
	Lock "stCutModeColTop", "iCutModeColTop", "stCutModeColBottom", "iCutModeColBottom", "gs_cutang1_bot","gs_cutang2_bot","gs_cutang3_bot","gs_cutang4_bot"
Endif
!---------Cutplanes


values "Profile" siz_mtr

if Profile<>`СПЕЦИАЛЬНЫЙ` & Profile <> lastProfile then

	lastProfile = Profile
	parameters lastProfile = Profile

	szelv=open("TEXT", "CHS Hot-Finished(14).txt", "SEPARATOR='\t', mode=ro, Library")

	for i=2 to sor+1
		n=input(szelv, i, 3, ker)
		if sectDepth=ker then
			n=input(szelv, i, 4, osectThick)
			put osectThick
		endif
	next i

	values "sectThick" use(NSP)

	szelv=open("text", "CHS Hot-Finished(14).txt", "separator='\t', mode=ro, Library")

	for i=2 to sor+1
		n=input(szelv, i, 3, ker1, ker2)
		if (sectDepth=ker1) and (sectThick=ker2) then n=input(szelv, i, 5, omassPerMeter, osectArea, odpt, osecondMoment, ogyrationRad, oelastMod, oplastMod)
	next i
	close szelv

	parameters a=sectDepth/1000
	a=sectDepth/1000
	lock "a"
	parameters b=sectDepth/1000
	b=sectDepth/1000
	lock "b"
	parameters massPerMeter=omassPerMeter
	massPerMeter=omassPerMeter
	lock "massPerMeter"
	parameters secondMoment=osecondMoment
	secondMoment=osecondMoment
	lock "secondMoment"
	parameters gyrationRad=ogyrationRad
	gyrationRad=ogyrationRad
	lock "gyrationRad"
	parameters elastMod=oelastMod
	elastMod=oelastMod
	lock "elastMod"
	parameters plastMod=oplastMod
	plastMod=oplastMod
	lock "plastMod"
	parameters sectArea=osectArea
	sectArea=osectArea
	lock "sectArea"
else


ENDIF


 if Profile<>`СПЕЦИАЛЬНЫЙ` then
	lock "sectDepth"
	lock "a"
	lock "b"
	lock "massPerMeter"
	lock "secondMoment"
	lock "gyrationRad"
	lock "elastMod"
	lock "plastMod"
	lock "sectArea"
	lock "massSum"
 else
	values "sectDepth" range (0.001, )

	parameters massPerMeter=massPerMeter
	parameters sectDepth=sectDepth
	parameters sectThick=sectThick
	parameters secondMoment=secondMoment
	parameters gyrationRad=gyrationRad
	parameters elastMod=elastMod
	parameters plastMod=plastMod
	parameters sectArea=sectArea
	if GLOB_MODPAR_NAME = "sectDepth" then
		A = sectDepth/1000
		parameters A = A
		B = sectDepth/1000
		parameters B = B
	else
		if GLOB_MODPAR_NAME = "A" then
			sectDepth = A*1000
			parameters sectDepth = sectDepth
			B = A
			parameters B = B
		else
			sectDepth = B*1000
			parameters sectDepth = sectDepth
			A = B
			parameters A = A
		endif
	endif
endif

massSum=massPerMeter*ZZYZX
parameters massSum=massSum
lock "massSum"


IF GLOB_MODPAR_NAME="Profile" THEN
	PARAMETERS axis_XDim=a/2+0.05
	axis_XDim=a/2+0.05

	PARAMETERS axis_YDim=b/2+0.05
	axis_YDim=b/2+0.05
ENDIF

if not(axis) then
	lock "only_axis", "axis_line_type", "axis_line_pen", "axis_XDim", "axis_YDim"
endif
if axis and only_axis then
	lock "gs_cont_pen", "gs_fill_type", "gs_fill_pen", "gs_back_pen", "gs_ShowMoreHtps2D"
endif

parameters ac_bottomlevel = 0
parameters ac_toplevel = ZZYZX
