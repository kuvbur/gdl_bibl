
sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000

pen gs_cont_pen
material col_mat
if gs_shadow=0 then
	shadow off
else
	shadow on
endif

IF gs_detlevel_3D_m = 0 THEN end

!---------Cutplanes
modz = zzyzx
IF bCutPlanes Then

	wsds = sqr(sectDepth^2 + sectWidth^2)

	!!!-------------------- Top Cutting
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then
		modz = modz+tan(60)* wsds/2

		Rotz gs_cutdir_top
		addz zzyzx
		rotx 90

		cutpoly 5,
				-wsds/2,tan(60)* wsds /2,
				wsds/2,tan(60)* wsds /2,
				wsds/2, tan(gs_cutang1_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang2_bot)* wsds /2

		del 3
	Endif

	!!!-------------------- Bottom Cutting
	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		modz = modz+tan(60)* wsds /2

		Rotz gs_cutdir_but
		rotx 90

		cutpoly 5,
				-wsds/2,-tan(60)* wsds /2,
				wsds/2,-tan(60)* wsds /2,
				wsds/2, tan(gs_cutang3_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang4_bot)* wsds /2

		del 2
		addz -tan(60)*wsds/2
	Endif
Endif
!---------Cutplanes

RRadius=sectThick*2

IF gs_detlevel_3D_m = 1 THEN
	PRISM_ 10,modz,
		-sectWidth/2,-sectDepth/2,15,
		sectWidth/2,-sectDepth/2,15,
		sectWidth/2,sectDepth/2,15,
		-sectWidth/2,sectDepth/2,15,
		-sectWidth/2,-sectDepth/2,-1,
		sectThick-sectWidth/2,sectThick-sectDepth/2,15,
		sectWidth/2-sectThick,sectThick-sectDepth/2,15,
		sectWidth/2-sectThick,sectDepth/2-sectThick,15,
		sectThick-sectWidth/2,sectDepth/2-sectThick,15,
		sectThick-sectWidth/2,sectThick-sectDepth/2,-1
ENDIF
IF gs_detlevel_3D_m = 2 THEN
	RESOL gs_resol
		PRISM_ 28,modz,
			-sectWidth/2+RRadius,-sectDepth/2,79,
			sectWidth/2-RRadius,-sectDepth/2,79,
			sectWidth/2-RRadius,-sectDepth/2+RRadius,979,
			0,90,4079,
			sectWidth/2,sectDepth/2-RRadius,79,
			sectWidth/2-RRadius,sectDepth/2-RRadius,979,
			0,90,4079,
			-sectWidth/2+RRadius,sectDepth/2,79,
			-sectWidth/2+RRadius,sectDepth/2-RRadius,979,
			0,90,4079,
			-sectWidth/2,-sectDepth/2+RRadius,79,
			-sectWidth/2+RRadius,-sectDepth/2+RRadius,979,
			0,90,4079,
			-sectWidth/2+RRadius,-sectDepth/2,-1,

			-sectWidth/2+RRadius,-sectDepth/2+sectThick,79,
			sectWidth/2-RRadius,-sectDepth/2+sectThick,79,
			sectWidth/2-RRadius,-sectDepth/2+RRadius,979,
			0,90,4079,
			sectWidth/2-sectThick,sectDepth/2-RRadius,79,
			sectWidth/2-RRadius,sectDepth/2-RRadius,979,
			0,90,4079,
			-sectWidth/2+RRadius,sectDepth/2-sectThick,79,
			-sectWidth/2+RRadius,sectDepth/2-RRadius,979,
			0,90,4079,
			-sectWidth/2+sectThick,-sectDepth/2+RRadius,79,
			-sectWidth/2+RRadius,-sectDepth/2+RRadius,979,
			0,90,4079,
			-sectWidth/2+RRadius,-sectDepth/2+sectThick,-1
ENDIF

!---------Cutplanes
IF bCutPlanes Then
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then cutend

	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		del 1
		cutend
	Endif
Endif
!---------Cutplanes

unID=1
HOTSPOT 0,0,0,unID,ZZYZX,1+256 : unID=unID+1
HOTSPOT 0,0,-1,unID,ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,ZZYZX,unID,ZZYZX,2 : unID=unID+1

!---------Bottom Hotspots
cutAngleHSBot1 = gs_cutang3_bot
cutAngleHSBot3 = gs_cutang4_bot

if iCutModeColBottom = CUTMODE_ALONG_X then
	HPBotx = sectDepth
	cutAngleHSBot2 = gs_cutang4_bot
	cutAngleHSBot4 = gs_cutang3_bot
else
	HPBotx = sectWidth
	cutAngleHSBot2 = gs_cutang3_bot
	cutAngleHSBot4 = gs_cutang4_bot
endif

rotx 90

HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHSBot2)* HPBotx/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHSBot3)* HPBotx/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHSBot4)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHSBot1)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
del 1

!---------Top Hotspots
cutAngleHS1 = gs_cutang1_bot
cutAngleHS3 = gs_cutang2_bot

if iCutModeColTop = CUTMODE_ALONG_X then
	HPx = sectDepth
	cutAngleHS2 = gs_cutang2_bot
	cutAngleHS4 = gs_cutang1_bot
else
	HPx = sectWidth
	cutAngleHS2 = gs_cutang1_bot
	cutAngleHS4 = gs_cutang2_bot
endif

addz zzyzx
rotx 90

HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHS2)* HPx/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHS3)* HPx/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHS4)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHS1)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
del 2

end




