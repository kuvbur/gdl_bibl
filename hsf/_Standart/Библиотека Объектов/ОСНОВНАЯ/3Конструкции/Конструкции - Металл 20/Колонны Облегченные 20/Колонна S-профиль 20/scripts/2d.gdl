

sectDepth=sectDepth/1000
sectThick=sectThick/1000
TopWidth=TopWidth/1000
BottomWidth=BottomWidth/1000
sectLip=sectLip/1000
Rad=sectThick

PEN gs_cont_pen
FILL gs_fill_type

unID = 1
hLineID = 1000

HOTSPOT2 0, 0,unID : unID=unID+1

IF axis=1 AND only_axis=0 OR axis=0 THEN

	RRadius=sectThick*2

		IF gs_detlevel_2D_m = 1 THEN

		POLY2_b 13, 1+2+4+64, gs_fill_pen, gs_back_pen,
				sectThick/2, -sectDepth/2, 1+32,
				sectThick/2, sectDepth/2-sectThick, 1+32,
				TopWidth-sectThick/2-sectThick, sectDepth/2-sectThick, 1+32,
				TopWidth-sectThick/2-sectThick, sectDepth/2-sectLip, 1+32,
				TopWidth-sectThick/2, sectDepth/2-sectLip, 1+32,
				TopWidth-sectThick/2, sectDepth/2, 1+32,
				-sectThick/2, sectDepth/2,1+32,
				-sectThick/2, -sectDepth/2+sectThick,1+32,
				-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectThick,1+32,
				-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectLip,1+32,
				-(BottomWidth-sectThick/2), -sectDepth/2+sectLip,1+32,
				-(BottomWidth-sectThick/2), -sectDepth/2,1+32,
				sectThick/2, -sectDepth/2, -1

			HOTLINE2 0,-sectDepth/2,0,sectDepth/2, hLineID

		ENDIF
		IF gs_detlevel_2D_m = 2 THEN

		POLY2_b 29, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-sectThick/2-Rad, -sectDepth/2, 1+32,
				-sectThick/2-Rad, -sectDepth/2+sectThick+Rad, 900+32,
				sectThick/2, -sectDepth/2+sectThick+Rad, 3001+32,
				sectThick/2, sectDepth/2-sectThick-Rad, 1+32,
				sectThick/2+Rad, sectDepth/2-sectThick-Rad, 900+32,
				sectThick/2+Rad, sectDepth/2-sectThick, 3001+32,
				TopWidth-sectThick/2-sectThick-Rad, sectDepth/2-sectThick, 1+32,
				TopWidth-sectThick/2-sectThick-Rad, sectDepth/2-sectThick-Rad, 900+32,
				TopWidth-sectThick/2-sectThick, sectDepth/2-sectThick-Rad, 3001+32,
				TopWidth-sectThick/2-sectThick, sectDepth/2-sectLip, 1+32,
				TopWidth-sectThick/2, sectDepth/2-sectLip, 1+32,
				TopWidth-sectThick/2, sectDepth/2-sectThick-Rad, 1+32,
				TopWidth-sectThick/2-sectThick-Rad, sectDepth/2-sectThick-Rad, 900+32,
				TopWidth-sectThick/2-sectThick-Rad, sectDepth/2, 3001+32,
				-sectThick/2+sectThick+Rad, sectDepth/2,1+32,
				-sectThick/2+sectThick+Rad, sectDepth/2-sectThick-Rad,900+32,
				-sectThick/2, sectDepth/2-sectThick-Rad,3001+32,
				-sectThick/2, -sectDepth/2+sectThick+Rad,1+32,
				-sectThick/2-Rad, -sectDepth/2+sectThick+Rad,900+32,
				-sectThick/2-Rad, -sectDepth/2+sectThick,3001+32,
				-(BottomWidth-sectThick/2)+sectThick+Rad, -sectDepth/2+sectThick,1+32,
				-(BottomWidth-sectThick/2)+sectThick+Rad, -sectDepth/2+sectThick+Rad,900+32,
				-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectThick+Rad,3001+32,
				-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectLip,1+32,
				-(BottomWidth-sectThick/2), -sectDepth/2+sectLip,1+32,
				-(BottomWidth-sectThick/2), -sectDepth/2+sectThick+Rad,1+32,
				-(BottomWidth-sectThick/2)+sectThick+Rad, -sectDepth/2+sectThick+Rad,900+32,
				-(BottomWidth-sectThick/2)+sectThick+Rad, -sectDepth/2,3001+32,
				-sectThick/2-Rad, -sectDepth/2, -1

			HOTLINE2 0,-sectDepth/2,0,sectDepth/2, hLineID

		ENDIF

	IF gs_ShowMoreHtps2D=1 THEN

		HOTSPOT2  sectThick/2, -sectDepth/2,unID : unID=unID+1
		HOTSPOT2 -sectThick/2, sectDepth/2,unID : unID=unID+1
		HOTSPOT2 TopWidth-sectThick/2, sectDepth/2,unID : unID=unID+1
		HOTSPOT2 TopWidth-sectThick/2, sectDepth/2-sectLip,unID : unID=unID+1
		HOTSPOT2 -(BottomWidth-sectThick/2), -sectDepth/2,unID : unID=unID+1
		HOTSPOT2 -(BottomWidth-sectThick/2), -sectDepth/2+sectLip,unID : unID=unID+1


	ENDIF

ENDIF

unID=100
IF (axis=1) THEN
	PEN axis_line_pen
	LINE_TYPE axis_line_type

	LINE2 -axis_XDim, 0, axis_XDim,0
	LINE2 0, -axis_YDim, 0,axis_YDim


	FOR k1=1 TO 2
		HOTSPOT2 0, 0, unID, axis_XDim, 1+128: unID=unID+1	!!! Axis X Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_XDim, 3: unID=unID+1
		HOTSPOT2 axis_XDim, 0, unID, axis_XDim, 2: unID=unID+1
	ROT2 180
	NEXT k1
	DEL 2

	FOR k2=1 TO 2
	ROT2 90
		HOTSPOT2 0, 0, unID, axis_YDim, 1+128: unID=unID+1	!!! Axis Y Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_YDim, 3: unID=unID+1
		HOTSPOT2 axis_YDim, 0, unID, axis_YDim, 2: unID=unID+1
	DEL 1
	ROT2 180
	NEXT k2
	DEL 2

	IF only_axis=1 AND gs_ShowMoreHtps2D=1 THEN

		HOTSPOT2 0, -sectDepth/2,unID : unID=unID+1
		HOTSPOT2 0, sectDepth/2,unID : unID=unID+1
		HOTSPOT2 TopWidth-sectThick/2, 0,unID : unID=unID+1
		HOTSPOT2 -(BottomWidth-sectThick/2), 0,unID : unID=unID+1

	ENDIF

ENDIF


