
sectDepth = sectDepth/1000
sectThick = sectThick/1000
Rad = sectThick
sectLip = sectLip/1000
TopWidth = TopWidth/1000
BottomWidth = BottomWidth/1000

PEN gs_cont_pen
MATERIAL col_mat
if gs_shadow=0 then
	SHADOW off
else
	SHADOW on
endif

if gs_detlevel_3D_m = 0 then end

!---------Cutplanes
modz = zzyzx
unIDCut = 100

if bCutPlanes Then

	wsds = sqr(sectDepth^2 + (BottomWidth+TopWidth)^2)

	!!!-------------------- Top Cutting
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then
		modz = modz+tan(60)* wsds/2

		Rotz gs_cutdir_top
		addz zzyzx
		rotx 90

		cutpoly 5,
				-wsds/2,tan(60)* wsds /2,
				wsds/2,tan(60)* wsds /2,
				wsds/2, tan(gs_cutang1_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang2_bot)* wsds /2

		del 3
	endif

	!!!-------------------- Bottom Cutting
	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		modz = modz+tan(60)* wsds /2

		Rotz gs_cutdir_but
		rotx 90

		cutpoly 5,
				-wsds/2,-tan(60)* wsds /2,
				wsds/2,-tan(60)* wsds /2,
				wsds/2, tan(gs_cutang3_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang4_bot)* wsds /2

		del 2
		addz -tan(60)*wsds/2
	endif
endif

!---------Cutplanes

RRadius=sectThick*2

if gs_detlevel_3D_m = 1 then
	PRISM_ 13,modz,
			sectThick/2, -sectDepth/2, 15,
			sectThick/2, sectDepth/2-sectThick, 15,
			TopWidth-sectThick/2-sectThick, sectDepth/2-sectThick, 15,
			TopWidth-sectThick/2-sectThick, sectDepth/2-sectLip, 15,
			TopWidth-sectThick/2, sectDepth/2-sectLip, 15,
			TopWidth-sectThick/2, sectDepth/2, 15,
			-sectThick/2, sectDepth/2,15,
			-sectThick/2, -sectDepth/2+sectThick,15,
			-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectThick,15,
			-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectLip,15,
			-(BottomWidth-sectThick/2), -sectDepth/2+sectLip,15,
			-(BottomWidth-sectThick/2), -sectDepth/2,15,
			sectThick/2, -sectDepth/2, -1

endif
if gs_detlevel_3D_m = 2 then

	!!! Curved Resolution
	S=90/gs_resol
	FOR k=0 TO 90 STEP S
		PUT -sectThick/2-Rad+(Rad+sectThick)*sin(k),-sectDepth/2+sectThick+Rad-(Rad+sectThick)*cos(k),79
	NEXT k
	FOR k=90 TO 180 STEP S
		PUT sectThick/2+Rad-Rad*sin(k),sectDepth/2-sectThick-Rad-Rad*cos(k),79
	NEXT k
	FOR k=180 TO 270 STEP S
		PUT TopWidth-sectThick/2-sectThick-Rad-Rad*sin(k),sectDepth/2-sectThick-Rad-Rad*cos(k),79
	NEXT k
	FOR k=270 TO 180 STEP -S
		PUT TopWidth-sectThick/2-sectThick-Rad-(Rad+sectThick)*sin(k),sectDepth/2-sectThick-Rad-(Rad+sectThick)*cos(k),79
	NEXT k
	FOR k=180 TO 90 STEP -S
		PUT -sectThick/2+sectThick+Rad-(Rad+sectThick)*sin(k),sectDepth/2-sectThick-Rad-(Rad+sectThick)*cos(k),79
	NEXT k
	FOR k=90 TO 0 STEP -S
		PUT -sectThick/2-Rad+(Rad)*sin(k),-sectDepth/2+sectThick+Rad-(Rad)*cos(k),79
	NEXT k
	FOR k=0 TO -90 STEP -S
		PUT -(BottomWidth-sectThick/2)+sectThick+Rad+(Rad)*sin(k),-sectDepth/2+sectThick+Rad-(Rad)*cos(k),79
	NEXT k
	FOR k=-90 TO 0 STEP S
		PUT -(BottomWidth-sectThick/2)+sectThick+Rad+(Rad+sectThick)*sin(k),-sectDepth/2+sectThick+Rad-(Rad+sectThick)*cos(k),79
	NEXT k

	PRISM_ 5+NSP/3,modz,
		GET(NSP/8),
		GET(NSP/7),
		GET(NSP/6),
		TopWidth-sectThick/2-sectThick, sectDepth/2-sectLip, 15,
		TopWidth-sectThick/2, sectDepth/2-sectLip, 15,
		GET(NSP/5),
		GET(NSP/4),
		GET(NSP/3),
		GET(NSP/2),
		-(BottomWidth-sectThick/2)+sectThick, -sectDepth/2+sectLip,15,
		-(BottomWidth-sectThick/2), -sectDepth/2+sectLip,15,
		GET(NSP),
		-sectThick/2-Rad, -sectDepth/2, -1

endif

!---------Cutplanes
if bCutPlanes Then
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS then cutend

	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS then
		del 1
		cutend
	endif
endif
!---------Cutplanes

unID=1
HOTSPOT 0,0,0,      unID,   ZZYZX,   1+256   : unID=unID+1
HOTSPOT 0,0,-1,     unID,   ZZYZX,   3       : unID=unID+1
HOTSPOT 0,0,ZZYZX,  unID,   ZZYZX,   2       : unID=unID+1

!---------Bottom Hotspots
rotx 90
if iCutModeColBottom = CUTMODE_ALONG_X then
	HOTSPOT TopWidth-sectThick/2,		bCutPlanes*tan(gs_cutang3_bot)* sectDepth/2,		-sectDepth/2,	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang4_bot)* sectDepth/2,		sectDepth/2,	unID : unID = unID+1

	HOTSPOT TopWidth-sectThick/2,		bCutPlanes*tan(gs_cutang3_bot)* (sectDepth/2-sectLip),		-sectDepth/2+sectLip,	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang4_bot)* (sectDepth/2-sectLip),		sectDepth/2-sectLip,	unID : unID = unID+1

	HPxMiddleBot = sectDepth
	cutAngleHSBot1 = gs_cutang3_bot
	cutAngleHSBot3 = gs_cutang4_bot
else
	HOTSPOT TopWidth-sectThick/2,		bCutPlanes*tan(gs_cutang3_bot)* (TopWidth-sectThick/2),		-sectDepth/2,	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang4_bot)* (BottomWidth-sectThick/2),	sectDepth/2,	unID : unID = unID+1

	HOTSPOT TopWidth-sectThick/2,		bCutPlanes*tan(gs_cutang3_bot)* (TopWidth-sectThick/2),		-sectDepth/2+sectLip,	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang4_bot)* (BottomWidth-sectThick/2),	sectDepth/2-sectLip,	unID : unID = unID+1

	HPxMiddleBot = sectThick
	cutAngleHSBot1 = gs_cutang4_bot
	cutAngleHSBot3 = gs_cutang3_bot
endif

HOTSPOT sectThick/2,	bCutPlanes*tan(cutAngleHSBot3)* HPxMiddleBot/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectThick/2,	bCutPlanes*tan(cutAngleHSBot1)* HPxMiddleBot/2,	-sectDepth/2,	unID : unID = unID+1
del 1

!---------Top Hotspots
addz zzyzx
rotx 90

if iCutModeColTop = CUTMODE_ALONG_X then
	HOTSPOT TopWidth-sectThick/2,	bCutPlanes*tan(gs_cutang1_bot)* sectDepth/2,		-sectDepth/2, 	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang2_bot)* sectDepth/2,	sectDepth/2, 	unID : unID = unID+1

	HOTSPOT TopWidth-sectThick/2,	bCutPlanes*tan(gs_cutang1_bot)* (sectDepth/2-sectLip),		-sectDepth/2+sectLip, 	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang2_bot)* (sectDepth/2-sectLip),	sectDepth/2-sectLip, 	unID : unID = unID+1

	HPxMiddle = sectDepth
	cutAngleHS1 = gs_cutang1_bot
	cutAngleHS3 = gs_cutang2_bot
else
	HOTSPOT TopWidth-sectThick/2,	bCutPlanes*tan(gs_cutang1_bot)* (TopWidth-sectThick/2),			-sectDepth/2, 	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang2_bot)* (BottomWidth-sectThick/2),	sectDepth/2, 	unID : unID = unID+1

	HOTSPOT TopWidth-sectThick/2,	bCutPlanes*tan(gs_cutang1_bot)* (TopWidth-sectThick/2),			-sectDepth/2+sectLip, 	unID : unID = unID+1
	HOTSPOT -BottomWidth+sectThick/2,	bCutPlanes*tan(gs_cutang2_bot)* (BottomWidth-sectThick/2),	sectDepth/2-sectLip, 	unID : unID = unID+1

	HPxMiddle = sectThick
	cutAngleHS1 = gs_cutang2_bot
	cutAngleHS3 = gs_cutang1_bot
endif

HOTSPOT sectThick/2,	bCutPlanes*tan(cutAngleHS3)* HPxMiddle/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectThick/2,	bCutPlanes*tan(cutAngleHS1)* HPxMiddle/2,	-sectDepth/2, 	unID : unID = unID+1
del 2

end

