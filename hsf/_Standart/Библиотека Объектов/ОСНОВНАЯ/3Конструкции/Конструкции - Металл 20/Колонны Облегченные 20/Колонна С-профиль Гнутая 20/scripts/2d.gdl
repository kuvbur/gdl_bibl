

sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
sectLip=sectLip/1000
Rad=sectThick

PEN gs_cont_pen
FILL gs_fill_type

unID = 1
hLineID = 1000

HOTSPOT2 0, 0,unID : unID=unID+1

IF axis=1 AND only_axis=0 OR axis=0 THEN

	ADD2 -sectWidth/2,-sectDepth/2

		IF gs_detlevel_2D_m = 1 THEN

			POLY2_b 13, 1+2+4+64, gs_fill_pen, gs_back_pen,
				sectWidth,0,1+32,
				sectWidth,sectLip,1+32,
				sectWidth-sectThick,sectLip,1+32,
				sectWidth-sectThick,sectThick,1+32,
				sectThick,sectThick,1+32,
				sectThick,sectDepth-sectThick,1+32,
				sectWidth-sectThick,sectDepth-sectThick,1+32,
				sectWidth-sectThick,sectDepth-sectLip,1+32,
				sectWidth,sectDepth-sectLip,1+32,
				sectWidth,sectDepth,1+32,
				0,sectDepth,1+32,
				0,0,1+32,
				sectWidth,0,-1

			gosub "hotlines2D"
		ENDIF

		IF gs_detlevel_2D_m = 2 THEN

			POLY2_b 29, 1+2+4+64, gs_fill_pen, gs_back_pen,
				sectWidth-sectThick-Rad,0,1+32,
				sectWidth-sectThick-Rad,sectThick+Rad,900+32,
				sectWidth,sectThick+Rad,3001+32,
				sectWidth,sectLip,1+32,
				sectWidth-sectThick,sectLip,1+32,
				sectWidth-sectThick,sectThick+Rad,1+32,
				sectWidth-sectThick-Rad,sectThick+Rad,900+32,
				sectWidth-sectThick-Rad,sectThick,3001+32,
				sectThick+Rad,sectThick,1+32,
				sectThick+Rad,sectThick+Rad,900+32,
				sectThick,sectThick+Rad,3001+32,
				sectThick,sectDepth-sectThick-Rad,1+32,
				sectThick+Rad,sectDepth-sectThick-Rad,900+32,
				sectThick+Rad,sectDepth-sectThick,3001+32,
				sectWidth-sectThick-Rad,sectDepth-sectThick,1+32,
				sectWidth-sectThick-Rad,sectDepth-sectThick-Rad,900+32,
				sectWidth-sectThick,sectDepth-sectThick-Rad,3001+32,
				sectWidth-sectThick,sectDepth-sectLip,1+32,
				sectWidth,sectDepth-sectLip,1+32,
				sectWidth,sectDepth-sectThick-Rad,1+32,
				sectWidth-sectThick-Rad,sectDepth-sectThick-Rad,900+32,
				sectWidth-sectThick-Rad,sectDepth,3001+32,
				sectThick+Rad,sectDepth,1+32,
				sectThick+Rad,sectDepth-sectThick-Rad,900+32,
				0,sectDepth-sectThick-Rad,3001+32,
				0,sectThick+Rad,1+32,
				sectThick+Rad,sectThick+Rad,900+32,
				sectThick+Rad,0,3001+32,
				sectWidth-sectThick-Rad,0,-1

			gosub "hotlines2D"
		ENDIF

	DEL 1

	IF gs_ShowMoreHtps2D=1 THEN

		HOTSPOT2 -a/2, -b/2,unID : unID=unID+1
		HOTSPOT2 a/2, -b/2,unID : unID=unID+1
		HOTSPOT2 a/2, b/2,unID : unID=unID+1
		HOTSPOT2 -a/2, b/2,unID : unID=unID+1
		HOTSPOT2 a/2, -b/2+sectLip,unID : unID=unID+1
		HOTSPOT2 a/2, b/2-sectLip,unID : unID=unID+1

	ENDIF

ENDIF

unID=100
IF (axis=1) THEN
	PEN axis_line_pen
	LINE_TYPE axis_line_type

	LINE2 -axis_XDim, 0, axis_XDim,0
	LINE2 0, -axis_YDim, 0,axis_YDim


	FOR k1=1 TO 2
		HOTSPOT2 0, 0, unID, axis_XDim, 1+128: unID=unID+1	!!! Axis X Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_XDim, 3: unID=unID+1
		HOTSPOT2 axis_XDim, 0, unID, axis_XDim, 2: unID=unID+1
	ROT2 180
	NEXT k1
	DEL 2

	FOR k2=1 TO 2
	ROT2 90
		HOTSPOT2 0, 0, unID, axis_YDim, 1+128: unID=unID+1	!!! Axis Y Dim. from Origo
		HOTSPOT2 -1, 0, unID, axis_YDim, 3: unID=unID+1
		HOTSPOT2 axis_YDim, 0, unID, axis_YDim, 2: unID=unID+1
	DEL 1
	ROT2 180
	NEXT k2
	DEL 2

	IF gs_ShowMoreHtps2D=1 THEN

		HOTSPOT2 -sectWidth/2, 0,unID : unID=unID+1
		HOTSPOT2  0, -sectDepth/2,unID : unID=unID+1
		HOTSPOT2 0, sectDepth/2,unID : unID=unID+1

		IF only_axis=1 THEN HOTSPOT2 sectWidth/2, 0,unID : unID=unID+1

	ENDIF

ENDIF

end

"hotlines2D":
	HOTLINE2 0, 0,	0,	sectDepth,	hLineID :hLineID = hLineID + 1
	HOTLINE2 0, 0,	sectWidth,	0,	hLineID :hLineID = hLineID + 1
	HOTLINE2 0, sectDepth,	sectWidth,	sectDepth,	hLineID :hLineID = hLineID + 1
return


