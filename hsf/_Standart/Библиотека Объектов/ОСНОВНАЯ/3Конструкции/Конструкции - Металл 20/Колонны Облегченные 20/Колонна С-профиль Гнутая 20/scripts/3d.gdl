
sectWidth=sectWidth/1000
sectDepth=sectDepth/1000
sectThick=sectThick/1000
Rad=sectThick
sectLip=sectLip/1000

PEN gs_cont_pen
MATERIAL col_mat
IF gs_shadow=0 THEN
	SHADOW off
ELSE
	SHADOW on
ENDIF

IF gs_detlevel_3D_m = 0 THEN end

!---------Cutplanes
modz = zzyzx
unID = 100
IF bCutPlanes Then

	wsds = sqr(sectDepth^2 + sectWidth^2)

	!!!-------------------- Top Cutting
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then
		modz = modz+tan(60)* wsds/2

		Rotz gs_cutdir_top
		addz zzyzx
		rotx 90

		cutpoly 5,
				-wsds/2,tan(60)* wsds /2,
				wsds/2,tan(60)* wsds /2,
				wsds/2, tan(gs_cutang1_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang2_bot)* wsds /2

		del 3
	endif

	!!!-------------------- Bottom Cutting
	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		modz = modz+tan(60)* wsds /2

		Rotz gs_cutdir_but
		rotx 90

		cutpoly 5,
				-wsds/2,-tan(60)* wsds /2,
				wsds/2,-tan(60)* wsds /2,
				wsds/2, tan(gs_cutang3_bot)* wsds /2,
				0,0,
				-wsds/2, tan(gs_cutang4_bot)* wsds /2

		del 2
		addz -tan(60)*wsds/2
	endif
endif

!---------Cutplanes


ADD -sectWidth/2,-sectDepth/2,0

	IF gs_detlevel_3D_m = 1 THEN

			PRISM_ 13,modz,
				sectWidth,0,15,
				sectWidth,sectLip,15,
				sectWidth-sectThick,sectLip,15,
				sectWidth-sectThick,sectThick,15,
				sectThick,sectThick,15,
				sectThick,sectDepth-sectThick,15,
				sectWidth-sectThick,sectDepth-sectThick,15,
				sectWidth-sectThick,sectDepth-sectLip,15,
				sectWidth,sectDepth-sectLip,15,
				sectWidth,sectDepth,15,
				0,sectDepth,15,
				0,0,15,
				sectWidth,0,-1

	ENDIF
	IF gs_detlevel_3D_m = 2 THEN

			!!! Curved Resolution
			S=90/gs_resol
			FOR k=0 TO 90 STEP S
				PUT sectWidth-sectThick-Rad+(Rad+sectThick)*sin(k),sectThick+Rad-(Rad+sectThick)*cos(k),79
			NEXT k
			FOR k=90 TO 0 STEP -S
				PUT sectWidth-sectThick-Rad+(Rad)*sin(k),sectThick+Rad-(Rad)*cos(k),79
			NEXT k
			FOR k=0 TO 90 STEP S
				PUT sectThick+Rad-Rad*sin(k),sectThick+Rad-Rad*cos(k),79
			NEXT k
			FOR k=90 TO 180 STEP S
				PUT sectThick+Rad-Rad*sin(k),sectDepth-sectThick-Rad-Rad*cos(k),79
			NEXT k
			FOR k=180 TO 270 STEP S
				PUT sectWidth-sectThick-Rad-Rad*sin(k),sectDepth-sectThick-Rad-Rad*cos(k),79
			NEXT k
			FOR k=270 TO 180 STEP -S
				PUT sectWidth-sectThick-Rad-(Rad+sectThick)*sin(k),sectDepth-sectThick-Rad-(Rad+sectThick)*cos(k),79
			NEXT k
			FOR k=180 TO 90 STEP -S
				PUT sectThick+Rad-(Rad+sectThick)*sin(k),sectDepth-sectThick-Rad-(Rad+sectThick)*cos(k),79
			NEXT k
			FOR k=90 TO 0 STEP -S
				PUT sectThick+Rad-(Rad+sectThick)*sin(k),sectThick+Rad-(Rad+sectThick)*cos(k),79
			NEXT k

			PRISM_ 5+(NSP/3),modz,
				GET(NSP/8),
				sectWidth,sectLip,15,
				sectWidth-sectThick,sectLip,15,
				GET(NSP/7),
				GET(NSP/6),
				GET(NSP/5),
				GET(NSP/4),
				sectWidth-sectThick,sectDepth-sectLip,15,
				sectWidth,sectDepth-sectLip,15,
				GET(NSP/3),
				GET(NSP/2),
				GET(NSP),
				sectWidth-sectThick-Rad,0,-1

	ENDIF
DEL 1

!---------Cutplanes
IF bCutPlanes Then
	If ABS(gs_cutang1_bot) > EPS OR ABS(gs_cutang2_bot) > EPS Then cutend

	If ABS(gs_cutang3_bot) > EPS OR ABS(gs_cutang4_bot) > EPS Then
		del 1
		cutend
	Endif
Endif
!---------Cutplanes

unID=1
HOTSPOT 0,0,0,      unID,   ZZYZX,   1+256   : unID=unID+1
HOTSPOT 0,0,-1,     unID,   ZZYZX,   3       : unID=unID+1
HOTSPOT 0,0,ZZYZX,  unID,   ZZYZX,   2       : unID=unID+1

!---------Bottom Hotspots
cutAngleHSBot1 = gs_cutang3_bot
cutAngleHSBot3 = gs_cutang4_bot

if iCutModeColBottom = CUTMODE_ALONG_X then
	HPBotx = sectDepth
	cutAngleHSBot2 = gs_cutang4_bot
	cutAngleHSBot4 = gs_cutang3_bot
else
	HPBotx = sectWidth
	cutAngleHSBot2 = gs_cutang3_bot
	cutAngleHSBot4 = gs_cutang4_bot
endif

rotx 90

HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHSBot2)* HPBotx/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHSBot3)* HPBotx/2,	sectDepth/2,	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHSBot4)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHSBot1)* HPBotx/2,	-sectDepth/2,	unID : unID = unID+1
del 1

if iCutModeColBottom = CUTMODE_ALONG_X then
	HOTSPOT sectWidth/2,	sectDepth/2-sectLip,	bCutPlanes*tan(cutAngleHSBot1)* (HPBotx/2-sectLip), unID
	unID=unID+1
	HOTSPOT sectWidth/2,	-sectDepth/2+sectLip,	bCutPlanes*tan(cutAngleHSBot2)* (HPBotx/2-sectLip), unID
	unID=unID+1
else
	HOTSPOT sectWidth/2,	sectDepth/2-sectLip,	bCutPlanes*tan(cutAngleHSBot1)* HPBotx/2, unID
	unID=unID+1
	HOTSPOT sectWidth/2,	-sectDepth/2+sectLip,	bCutPlanes*tan(cutAngleHSBot2)* HPBotx/2, unID
	unID=unID+1
endif

!---------Top Hotspots
cutAngleHS1 = gs_cutang1_bot
cutAngleHS3 = gs_cutang2_bot

if iCutModeColTop = CUTMODE_ALONG_X then
	HPx = sectDepth
	cutAngleHS2 = gs_cutang2_bot
	cutAngleHS4 = gs_cutang1_bot
else
	HPx = sectWidth
	cutAngleHS2 = gs_cutang1_bot
	cutAngleHS4 = gs_cutang2_bot
endif

addz zzyzx
rotx 90

HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHS2)* HPx/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHS3)* HPx/2,	sectDepth/2, 	unID : unID = unID+1
HOTSPOT -sectWidth/2,	bCutPlanes*tan(cutAngleHS4)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
HOTSPOT sectWidth/2,	bCutPlanes*tan(cutAngleHS1)* HPx/2,	-sectDepth/2, 	unID : unID = unID+1
del 1

if iCutModeColTop = CUTMODE_ALONG_X then
	HOTSPOT sectWidth/2,	sectDepth/2-sectLip,	bCutPlanes*tan(cutAngleHS1)* (HPx/2-sectLip), unID
	unID=unID+1
	HOTSPOT sectWidth/2,	-sectDepth/2+sectLip,	bCutPlanes*tan(cutAngleHS2)* (HPx/2-sectLip), unID
	unID=unID+1
else
	HOTSPOT sectWidth/2,	sectDepth/2-sectLip,	bCutPlanes*tan(cutAngleHS1)* HPx/2, unID
	unID=unID+1
	HOTSPOT sectWidth/2,	-sectDepth/2+sectLip,	bCutPlanes*tan(cutAngleHS2)* HPx/2, unID
	unID=unID+1
endif

del 1
end

