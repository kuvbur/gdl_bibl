
!! Web Beam Curved.gsm

if gs_detlevel_3D_m = 0 then end

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

material mat1
pen gs_cont_pen

!!---- Hotspot Editing
unID=1

hotspot 0,0,0,unID,A,1+256 : unID=unID+1
hotspot -1,0,0,unID,A,3 : unID=unID+1
hotspot A,0,0,unID,A,2 : unID=unID+1

hotspot 0,0,0,unID,E,1+128 : unID=unID+1
hotspot 0,0,-1,unID,E,3 : unID=unID+1
hotspot 0,0,E,unID,E,2 : unID=unID+1

hotspot A,0,0,unID,F,1+128 : unID=unID+1
hotspot A,0,-1,unID,F,3 : unID=unID+1
hotspot A,0,F,unID,F,2 : unID=unID+1

hotspot 0,0,E,unID,B2,1+128,B : unID=unID+1
hotspot 0,-1,E,unID,B2,3,B : unID=unID+1
hotspot 0,B2,E,unID,B2,2,B : unID=unID+1

hotspot 0,0,E,unID,B2,1+128,B : unID=unID+1
hotspot 0,1,E,unID,B2,3,B : unID=unID+1
hotspot 0,-B2,E,unID,B2,2,B : unID=unID+1

hotspot A,0,F,unID,B2,1+128,B : unID=unID+1
hotspot A,-1,F,unID,B2,3,B : unID=unID+1
hotspot A,B2,F,unID,B2,2,B : unID=unID+1

hotspot A,0,F,unID,B2,1+128,B : unID=unID+1
hotspot A,1,F,unID,B2,3,B : unID=unID+1
hotspot A,-B2,F,unID,B2,2,B : unID=unID+1

hotspot A/2,B2,0,unID,zzyzx,1+128 : unID=unID+1
hotspot A/2,B2,-1,unID,zzyzx,3 : unID=unID+1
hotspot A/2,B2,zzyzx,unID,zzyzx,2 : unID=unID+1

hotspot A/2,-B2,0,unID,zzyzx,1+128 : unID=unID+1
hotspot A/2,-B2,-1,unID,zzyzx,3 : unID=unID+1
hotspot A/2,-B2,zzyzx,unID,zzyzx,2 : unID=unID+1



!!---- Hotspot Editing

xLaenge             = A !PARAM X-Dimension
yQuerschnittsbreite = B !PARAM Y-Dimension
zQuerschnittshoehe  = C !PARAM Querschnittshohe
dMaterial           = D !PARAM Materialstarke [mm]
zAufsetzhoehe1      = E !PARAM Auflagehohe 1
zAufsetzhoehe2      = F !PARAM Auflagehohe 2
zScheitelhoehe      = G !PARAM Scheitelhohe
dAbstand            = I !PARAM Ungefahrer Loch-Abstand
dToleranz           = J !PARAM Toleranz (kleiner = runder)
bFusspunktstrich    = K !PARAM Grundriss mit Fusspunktstrichss
nLinientyp          = gs_line_type !PARAM Linientyp fur Grundriss



kwDegree = pi/180

toler dToleranz

rotx 90

dMaterial = 0.001 * dMaterial ! Wir wollen in [m] rechnen.

! Wir wollen mit zAufsetzhoehe1 = Null rechnen.

addy zAufsetzhoehe1
zAufsetzhoehe2 = zAufsetzhoehe2 - zAufsetzhoehe1
zScheitelhoehe = zScheitelhoehe - zAufsetzhoehe1
zAufsetzhoehe1 = 0

! Abk??rzungen f??r die Formeln:

a = xLaenge
g = zAufsetzhoehe2
h = zScheitelhoehe - zQuerschnittshoehe

! Der Kreis:

if abs(g)<EPS then

  ! Die Bogenenden liegen auf gleicher Hohe:

  x = a/2

else

  if g > 0 then
	x = a*h/g - sqr(h*h - a*a*h/g - g*h + a*a*h*h/g/g)
  else
	x = a*h/g + sqr(h*h - a*a*h/g - g*h + a*a*h*h/g/g)
  endif

endif

y = (h*h - x*x)/2/h
r = h - y

! Der Radialvektor am linken Bogenende:

x1 = -x
y1 = -y

l = sqr(x1*x1 + y1*y1)

x1 = x1/l
y1 = y1/l

! Der Radialvektor am rechten Bogenende:

x2 = a - x
y2 = g - y

l = sqr(x2*x2 + y2*y2)

x2 = x2/l
y2 = y2/l



r1 = dMaterial
r2 = zQuerschnittshoehe - dMaterial
r3 = zQuerschnittshoehe



  ! Der Radiuswinkel am linken Bogenende:

  if abs(x1)<EPS then
	w1 = 90
  else
	w1 = atn(y1/x1)
  endif

  if x1 < 0 then
	w1 = w1 + 180
  endif

  ! Der Radiuswinkel am rechten Bogenende:

  if abs(x2)<EPS then
	w2 = 90
  else
	w2 = atn(y2/x2)
  endif

  if x2 < 0 then
	w2 = w2 + 180
  endif

  wBogen = w1 - w2 ! Der Bogenwinkel

! Steg mit Lochern:

nLoecher = 0 ! Nimm an, das wir keine Locher haben.

if bHole then
  ! Erzeuge die Locher:

  rLoch = 0.5 * holeDiameter ! Lochradius

  rMitte = r + zQuerschnittshoehe / 2

  wLoch = holeDiameter / rMitte / kwDegree
  wAbstand = dAbstand / rMitte / kwDegree

  nLoecher = int(0.5 + (wBogen - wAbstand) / (wLoch + wAbstand))
  wAbstand = (wBogen - nLoecher * wLoch) / (nLoecher + 1)

  for w = w2 + wAbstand + wLoch/2 to w1 step wAbstand + wLoch
	  xLoch = x + rMitte * cos(w)
	  yLoch = y + rMitte * sin(w)

	  put xLoch, yLoch, 900 + 15,   ! Loch-Mittelpunkt
			  rLoch, 360, 4000 + 15
  next w
endif

addz -yQuerschnittsbreite/2

! Untergurt:

prism_ 6, yQuerschnittsbreite,
  0, 0, 79,
  x, y, 900 + 79, ! Kreismittelpunkt
  0, -wBogen, 4000 + 79, ! Bogen unten
  a + x2 * r1, g + y2 * r1, 79,
  x, y, 900 + 79, ! Kreismittelpunkt
  0, wBogen, 4000 + 15

! Obergurt:

prism_ 6, yQuerschnittsbreite,
  x1 * r2, y1 * r2, 79,
  x, y, 900 + 79, ! Kreismittelpunkt
  0, -wBogen, 4000 + 79, ! Bogen unten
  a + x2 * r3, g + y2 * r3, 79,
  x, y, 900 + 15, ! Kreismittelpunkt
  0, wBogen, 4000 + 79

del 1

addz -dMaterial/2

if nLoecher = 0 then

  prism_ 6, dMaterial,
	x1 * r1, y1 * r1, 15,
	x, y, 900 + 15, ! Kreismittelpunkt
	0, -wBogen, 4000 + 15, ! Bogen unten
	a + x2 * r2, g + y2 * r2, 15,
	x, y, 900 + 15, ! Kreismittelpunkt
	0, wBogen, 4000 + 15

else

  prism_ 7 + nsp/3, dMaterial,
	x1 * r1, y1 * r1, 15,
	x, y, 900 + 15, ! Kreismittelpunkt
	0, -wBogen, 4000 + 15, ! Bogen unten
	a + x2 * r2, g + y2 * r2, 15,
	x, y, 900 + 15, ! Kreismittelpunkt
	0, wBogen, 4000 + 15,
	0, 0, -1,
	get(nsp)

endif
