
! Space Frame

PEN gs_cont_pen
LINE_TYPE gs_line_type
fill gs_fill_type

len_x=A/ncx
len_y=B/ncy
dia=brad/(sqr(2)/2)
len=sqr((len_x/2)^2+(len_y/2)^2)
alfa=atn((len_y/2)/(len_x/2))

! --- Hotspot Editing ---
unID=1
hotspot2 0,0,unID,a,1+256 : unID=unID+1
hotspot2 -1,0,unID,a,3 : unID=unID+1
hotspot2 a,0,unID,a,2 : unID=unID+1

hotspot2 0,b,unID,a,1+256 : unID=unID+1
hotspot2 -1,b,unID,a,3 : unID=unID+1
hotspot2 a,b,unID,a,2 : unID=unID+1

hotspot2 0,0,unID,b,1+256 : unID=unID+1
hotspot2 0,-1,unID,b,3 : unID=unID+1
hotspot2 0,b,unID,b,2 : unID=unID+1

hotspot2 a,0,unID,b,1+256 : unID=unID+1
hotspot2 a,-1,unID,b,3 : unID=unID+1
hotspot2 a,b,unID,b,2 : unID=unID+1

! --- Lower Surface ---
add2 len_x/2, len_y/2
for j=1 to ncy
	for i=1 to ncx
		If gs_detlevel_2D_m = 2 Then
			If i<>ncx Then
				poly2_b	7, 1+2+64, gs_fill_pen, gs_back_pen,
						0, 0, 1,
						brad, -brad, 1,
						len_x-brad, -brad, 1,
						len_x, 0, 1,
						len_x-brad, brad, 1,
						brad, brad, 1,
						0, 0, -1
			EndIf

			If j<>ncy Then
				poly2_b	7, 1+2+64, gs_fill_pen, gs_back_pen,
						0, 0, 1,
						brad, brad, 1,
						brad, len_y-brad, 1,
						0, len_y, 1,
						-brad, len_y-brad, 1,
						-brad, brad, 1,
						0, 0, -1
			EndIf

				If ehp Then hotspot2 0,0,unID : unID=unID+1
				poly2_b	2, 1+2+64, gs_fill_pen, gs_back_pen,
						0, 0, 901,
						rs, 360, 4001

			for k=1 to 4
				If k=1 Then
					rot2 alfa
					num=1
				EndIf
				If k=2 Then
					mul2 -1,1
					num=1
				EndIf
				If k=3 Then
					del 2
					rot2 180-alfa
				EndIf
				If k=4 Then
					mul2 -1,1
				EndIf

				poly2_b	7, 1+2+64, gs_fill_pen, gs_back_pen,
						0, 0, 1,
						brad, -brad, 1,
						len-brad, -brad, 1,
						len, 0, 1,
						len-brad, brad, 1,
						brad, brad, 1,
						0, 0, -1
			next k
			del 2
		Else
			If i<>ncx Then line2 0,0,len_x,0
			If j<>ncy Then line2 0,0,0,len_y
				If ehp Then hotspot2 0,0,unID : unID=unID+1
				circle2 0,0,rs

			line2 -len_x/2,-len_y/2,0,0
			line2 -len_x/2,len_y/2,0,0
			line2 len_x/2,-len_y/2,0,0
			line2 len_x/2,len_y/2,0,0
		EndIf
		add2 A/ncx, 0
	next i
	del ncx
	add2 0, B/ncy
next j
del ncy+1

! --- Upper Surface ---
for j=1 to ncy+1
	for i=1 to ncx+1
		If gs_detlevel_2D_m = 2 Then
			If i<>ncx+1 Then
				poly2_b	7, 1+2+64, gs_fill_pen, gs_back_pen,
						0, 0, 1,
						brad, -brad, 1,
						len_x-brad, -brad, 1,
						len_x, 0, 1,
						len_x-brad, brad, 1,
						brad, brad, 1,
						0, 0, -1
			EndIf

			If j<>ncy+1 Then
				poly2_b	7, 1+2+64, gs_fill_pen, gs_back_pen,
						0, 0, 1,
						brad, brad, 1,
						brad, len_y-brad, 1,
						0, len_y, 1,
						-brad, len_y-brad, 1,
						-brad, brad, 1,
						0, 0, -1
			EndIf

				If ehp Then hotspot2 0,0,unID : unID=unID+1
				poly2_b	2, 1+2+64, gs_fill_pen, gs_back_pen,
						0, 0, 901,
						rs, 360, 4001
		Else
			If i<>ncx+1 And j<>ncy+1 Then
				line2 0,0,len_x/2,len_y/2
				line2 0,len_y,len_x/2,len_y/2
				line2 len_x,0,len_x/2,len_y/2
				line2 len_x,len_y,len_x/2,len_y/2
			EndIf

			!!---- Upper Surface
			If i<>ncx+1 Then line2 0,0,len_x,0
			If j<>ncy+1 Then line2 0,0,0,len_y
				If ehp Then hotspot2 0,0,unID : unID=unID+1
				circle2 0,0,rs
		EndIf

		add2 A/ncx, 0
	next i
	del ncx+1
	add2 0, B/ncy
next j
del ncy+1


