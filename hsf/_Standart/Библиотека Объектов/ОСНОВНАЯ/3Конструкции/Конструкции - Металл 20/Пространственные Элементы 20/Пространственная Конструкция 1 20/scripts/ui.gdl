
n = REQUEST ("Working_length_unit", "", workingLengthUnit)

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 			= -1
_idxTab 			= 1
TABID_DIMENSION 	= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES 		= _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS 	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Пространственной Конструкции`
ui_current_page gs_ui_current_page

ui_page TABID_DIMENSION, TABID_ROOT , `Размеры`, "uiTab_Dimension_flat(1)"
gosub "dimensionPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Описание`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

! --------------------------------------------------------------------------------
"dimensionPage":
! --------------------------------------------------------------------------------
	x1 = 0
	x2 = 170
	x3 = 230
	x4 = x2 + 70
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 400
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	
	ui_style 0, 0
	
	ui_outfield	`Количество Ячеек (X)` ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_infield "ncx", 	 x2,   yCurrent, 	x3 - x2, infieldHeight

	ui_infield{3} "sph", 	 x4,   yCurrent, 	xEnd-x4, infieldHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"", `Шарниры`, 0,
			"", `Шарниры`, 1

	yCurrent = yCurrent + dy


	ui_outfield	`Количество Ячеек (Y)` ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_infield "ncy", 	 x2,   yCurrent, 	x3 - x2, infieldHeight


	yCurrent = yCurrent + dy

	ui_separator 0, yCurrent, xEnd, yCurrent

	uiPictX = 90
	uiPictY = 52
	uiPictWdt = 203
	uiPictHgt = 190
	uiPictMargin = 5
	uiPictInfWdt = 90
	infieldHeight 		= 19

	uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
	uiPicLeftEND = uiPicLeftStart - uiPictMargin

	uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3

	uiPicBelow = uiPictY + uiPictHgt + uiPictMargin

	uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

	if sph then
		UI_PICT 2, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreCol"
		ui_infield "rs", uiPicRightStart, uiPictY, uiPictInfWdt, infieldHeight UI_TOOLTIP `Радиус Шарниров`
	else
		UI_PICT 1, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"PreCol"
	endif
	ui_infield "brad", uiPicRightStart, uiPictY+24, uiPictInfWdt, infieldHeight UI_TOOLTIP `Радиус Стержней`

	ui_outfield STR(workingLengthUnit, B/ncy ), uiPicRightStart, uiPictY+140, uiPictInfWdt, infieldHeight UI_TOOLTIP `Расстояние`
	
	ui_outfield STR(workingLengthUnit, A/ncx), uiPictX-48, uiPicBelow, uiPictInfWdt, infieldHeight, 1 UI_TOOLTIP `Расстояние`
return

! ------------------------------------------------------------------------------
"AppearancePage":
! ------------------------------------------------------------------------------

	dim st2DParamNames[7][2]
		st2DParamNames[1][1] = "gs_detlevel_2D" : st2DParamNames[1][2] = "ui_list_detailLevel2D_flat(1)"
		st2DParamNames[2][1] = "ehp" 			: st2DParamNames[2][2] = "ui_list_extraHS_flat(1)"
		st2DParamNames[3][1] = "gs_line_type" 	: st2DParamNames[3][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[4][1] = "gs_cont_pen" 	: st2DParamNames[4][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[5][1] = "gs_fill_type" 	: st2DParamNames[5][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[6][1] = "gs_fill_pen" 	: st2DParamNames[6][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[7][1] = "gs_back_pen" 	: st2DParamNames[7][2] = "ui_list_fillBgPen_flat(1)"

	dim st3DParamNames[3][2]
		st3DParamNames[1][1] = "gs_detlevel_3D"			: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_resol"				: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
		st3DParamNames[3][1] = "gs_shadow"				: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "mat1"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										bShowIconColumn = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	file_dependence "ui_list_extraHS_flat(1)"
return

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_structure_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										bProp = 0,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
