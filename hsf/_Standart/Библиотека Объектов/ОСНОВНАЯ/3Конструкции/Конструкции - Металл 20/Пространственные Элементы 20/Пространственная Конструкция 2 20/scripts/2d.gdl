
!! Spaceframe Asymmetric.gsm

hx=sqr(b^2-(b/2)^2)*cos(90-ang)

pen gs_cont_pen
fill gs_fill_type

!!---- Hotspot Editing
unID=1

If abs(ang-90)>eps Then
	hotspot2 0,0,unID,A2,1+256,A : unID=unID+1
	hotspot2 -1,0,unID,A2,3,A : unID=unID+1
	hotspot2 A2,0,unID,A2,2,A : unID=unID+1

	hotspot2 -hx,B/2,unID,A2,1+256,A : unID=unID+1
	hotspot2 -hx-1,B/2,unID,A2,3,A : unID=unID+1
	hotspot2 -hx+A2,B/2,unID,A2,2,A : unID=unID+1

	hotspot2 0,B,unID,A2,1+256,A : unID=unID+1
	hotspot2 -1,B,unID,A2,3,A : unID=unID+1
	hotspot2 A2,B,unID,A2,2,A : unID=unID+1

	hotspot2 0,0,unID,B,1+256 : unID=unID+1
	hotspot2 0,-1,unID,B,3 : unID=unID+1
	hotspot2 0,B,unID,B,2 : unID=unID+1

	hotspot2 A2/2,0,unID,B,1+256 : unID=unID+1
	hotspot2 A2/2,-1,unID,B,3 : unID=unID+1
	hotspot2 A2/2,B,unID,B,2 : unID=unID+1

	hotspot2 A2,0,unID,B,1+256 : unID=unID+1
	hotspot2 A2,-1,unID,B,3 : unID=unID+1
	hotspot2 A2,B,unID,B,2 : unID=unID+1

	hotspot2 -hx+A2/2,B/2,unID : unID=unID+1

	If GLOB_CONTEXT<20 Or GLOB_CONTEXT>40 Then
		pen gs_fill_pen
		DRAWINDEX 20
		project2{3} 3,270,3+32+64+1024, 15, gs_back_pen, 0, 0, 0,
			parameters	gs_2D_projection = 1
		pen gs_cont_pen
		DRAWINDEX 30
		project2{3} 3,270,2+32+1024, 15, gs_back_pen, 0, 0, 0,
			parameters	gs_2D_projection = 1
	Else
		project2{3} 3,270,1, 15,
			parameters	gs_2D_projection = 1
	EndIf
Else
	hotspot2 0,0,unID,b,1+256 : unID=unID+1
	hotspot2 0,-1,unID,b,3 : unID=unID+1
	hotspot2 0,b,unID,b,2 : unID=unID+1

	rot2 60
	hotspot2 0,0,unID,b,1+256 : unID=unID+1
	hotspot2 0,-1,unID,b,3 : unID=unID+1
	hotspot2 0,b,unID,b,2 : unID=unID+1
	del 1

	for i=1 to 3
		poly2_b	5, 1+2+64, gs_fill_pen, gs_back_pen,
				-Dd, 0, 1,
				Dd, 0, 1,
				Dd, B, 1,
				-Dd, B, 1,
				-Dd, 0, -1
		add2 0,b
		rot2 120
	next i
	del 6

	for i=1 to 3
		poly2_b	2, 1+2+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				Dh, 360, 4001
		add2 0,b
		rot2 120
	next i
	del 6
EndIf
