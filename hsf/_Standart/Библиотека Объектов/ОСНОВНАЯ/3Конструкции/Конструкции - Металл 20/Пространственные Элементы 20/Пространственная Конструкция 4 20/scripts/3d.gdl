
if gs_detlevel_3D_m = 0 and not(gs_2D_projection) then end
if gs_detlevel_3D_m = 0 and gs_2D_projection and (GLOB_VIEW_TYPE = 3 or GLOB_VIEW_TYPE = 6) then end
if gs_detlevel_3D_m = 1 then
	rh=6
	rd=3
endif

IF not(gs_shadow) THEN SHADOW OFF

! --- Hotspot Editing ----------------------------------------------------------

unID = 1

! --- Angle Editing
for i=1 to 2
	hotspot 0,0,0,unID,ang,6: unID=unID+1
	hotspot 1,0,0,unID,ang,4+128 : unID=unID+1
	hotspot 0,-1,0,unID,ang,7 : unID=unID+1
	hotspot a/2*cos(ang),0,a/2*sin(ang),unID,ang,5 : unID=unID+1
	addy b
next i
del 2

roty -ang
! --- Length Editing
hotspot 0,0,0,unID,a,1+256 : unID=unID+1
hotspot -1,0,0,unID,a,3 : unID=unID+1
hotspot a,0,0,unID,a,2 : unID=unID+1

hotspot 0,b,0,unID,a,1+256 : unID=unID+1
hotspot -1,b,0,unID,a,3 : unID=unID+1
hotspot a,b,0,unID,a,2 : unID=unID+1

rotx 60
hotspot 0,b,0,unID,a,1+256 : unID=unID+1
hotspot -1,b,0,unID,a,3 : unID=unID+1
hotspot a,b,0,unID,a,2 : unID=unID+1
del 1

! --- Section Editing
hotspot 0,0,0,unID,b,1+256 : unID=unID+1
hotspot 0,-1,0,unID,b,3 : unID=unID+1
hotspot 0,b,0,unID,b,2 : unID=unID+1

rotx 60
hotspot 0,0,0,unID,b,1+128 : unID=unID+1
hotspot 0,-1,0,unID,b,3 : unID=unID+1
hotspot 0,b,0,unID,b,2 : unID=unID+1

hotspot a,0,0,unID,b,1+128 : unID=unID+1
hotspot a,-1,0,unID,b,3 : unID=unID+1
hotspot a,b,0,unID,b,2 : unID=unID+1
del 1

hotspot a,0,0,unID,b,1+256 : unID=unID+1
hotspot a,-1,0,unID,b,3 : unID=unID+1
hotspot a,b,0,unID,b,2 : unID=unID+1

del 1

! --- 3D model -----------------------------------------------------------------

pen gs_cont_pen

aa=b*tan(30)
x=int(a/aa)
Abe=(a-x*aa)/2

group "struts"
	material chordmat

	rotz 90
	rotx 90-ang
	Resol Rh
	for i=1 to 3
		cylind A,Dh
		addx b
		rotz 120
	next i
	del 8
endgroup

group "chords"
	material strutmat

	xx=int(x/2)
	Ld=b/cos(30)

	rotz 90
	rotx 90-ang
	addz Abe

	resol  Rd
	for j=1 to 2
		rotz 60
		for k=1 to xx
			roty 60
			cylind Ld,Dd
			del 1

			addz 2*aa
			roty 120
			cylind Ld,Dd
			del 1
		next k
		if x>2*xx then
			roty 60
			cylind Ld,Dd
			del 1
		endif
		rotz -60
		addx b
		addz -xx*2*aa
		rotz 120
	next j

	rotz 60
	for n=1 to xx
		addz aa
		roty 120
		cylind Ld,Dd
		del 1

		roty 60
		cylind Ld,Dd
		del 1

		addz aa
	next n
	if x>2*xx then
		addx b
		roty -60
		cylind Ld,Dd
		del 2
	endif
	del top
endgroup

if gs_detlevel_3D_m = 2 and (GLOB_CONTEXT<20 or GLOB_CONTEXT>40) then
	result=subgroup("chords","struts")
	placegroup "struts"
	placegroup result
else
	placegroup "struts"
	placegroup "chords"
endIf

end
