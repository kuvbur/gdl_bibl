

PEN gs_cont_pen
FILL gs_fill_type

Unid = 1
hLineID = 1000

!!! *** Length

HOTSPOT2 0, 0, unID, A, 1+256 : unID=unID+1
HOTSPOT2 -1, 0, unID, A, 3 : unID=unID+1
HOTSPOT2 A, 0, unID, A, 2 : unID=unID+1

!!! *** Length/2

HOTSPOT2 A/2,0,unID : unID=unID+1

!!! *** Frame Width
IF gs_detlevel_2D_m <> DETLEVEL_2D_AXIS_ONLY THEN

	HOTSPOT2 A,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 A,-b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 A,1,unID,B2,3, B : unID=unID+1

	HOTSPOT2 A,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 A,b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 A,-1,unID,B2,3, B : unID=unID+1

	HOTSPOT2 0,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 0,-b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 0,1,unID,B2,3, B : unID=unID+1

	HOTSPOT2 0,0,unID,B2,1+128, B : unID=unID+1
	HOTSPOT2 0,b2,unID,B2,2, B : unID=unID+1
	HOTSPOT2 0,-1,unID,B2,3, B : unID=unID+1

ENDIF

ADD2 a/2,0

HOTLINE2 -a/2, 0, a/2, 0, hLineID

IF axis THEN
	PEN axis_pen
	LINE_TYPE axis_line_type
	LINE2 -a/2, 0, a/2, 0
	LINE_TYPE contour_line_type
ENDIF

if gs_detlevel_2D_m = DETLEVEL_2D_AXIS_ONLY then end

ACCUTPLANEHEIGHT = (GLOB_CSTORY_ELEV+GLOB_CUTPLANES_INFO [1])-GLOB_HSTORY_ELEV

if iFloorPlanDisplayType = DISPLAY_OVERHEAD then
	if ACCUTPLANEHEIGHT >= ZZYZX then
		LINE_TYPE contour_line_type
		PEN gs_cont_pen
		gosub "overhead"
	else
		LINE_TYPE contour_line_type
		PEN cut_pen
		gosub "cut2"
	endif
endif

if iFloorPlanDisplayType = DISPLAY_OVERHEAD_PROJ then
	if ACCUTPLANEHEIGHT >= ZZYZX then
		LINE_TYPE contour_line_type
		PEN gs_cont_pen
		gosub "overhead"
	else
		LINE_TYPE overhead_Line_Type
		PEN gs_cont_pen
		gosub "cut1"
	endif
endif

if iFloorPlanDisplayType = DISPLAY_SYMBOLIC then
	LINE_TYPE overhead_Line_Type
	PEN gs_cont_pen
	gosub "cut1"
endif

if iFloorPlanDisplayType = DISPLAY_OUTLINES then
	LINE_TYPE contour_line_type
	PEN gs_cont_pen
	gosub "overhead"
endif

if iFloorPlanDisplayType = DISPLAY_OVERHEAD_ALL then
	LINE_TYPE overhead_Line_Type
	cut_pen = gs_cont_pen
	contour_line_type = overhead_Line_Type
	PEN cut_pen
	gosub "cut1"
endif

del 1

end
!===================================================================================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
!===================================================================================================================================

! ---------------------------------------------------------
"base_width_htsp":
! ---------------------------------------------------------
	ADD2 -a/2,0
	HOTSPOT2 0, 0, unID, h1c, 1+128 : unID=unID+1
	HOTSPOT2 -1, 0, unID, h1c, 3 : unID=unID+1
	HOTSPOT2 h1c, 0, unID, h1c, 2 : unID=unID+1

	HOTSPOT2 A, 0, unID, h3c, 1+128 : unID=unID+1
	HOTSPOT2 A+1, 0, unID, h3c, 3 : unID=unID+1
	HOTSPOT2 A-h3c, 0, unID, h3c, 2 : unID=unID+1
	del 1
return

! ---------------------------------------------------------
"top_width_htsp":
! ---------------------------------------------------------
	ADD2 -a/2,0
	HOTSPOT2 0, 0, unID, h2c, 1+128 : unID=unID+1
	HOTSPOT2 -1, 0, unID, h2c, 3 : unID=unID+1
	HOTSPOT2 h2c, 0, unID, h2c, 2 : unID=unID+1

	HOTSPOT2 A, 0, unID, h4c, 1+128 : unID=unID+1
	HOTSPOT2 A+1, 0, unID, h4c, 3 : unID=unID+1
	HOTSPOT2 A-h4c, 0, unID, h4c, 2 : unID=unID+1
	del 1
return

! ---------------------------------------------------------
"1_200":
! ---------------------------------------------------------
	drawindex 10
	if iFill_method = 2 then
		POLY2_B 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-a/2, -b/2, 1,
			-a/2, b/2, 1,
			 a/2, b/2, 1,
			 a/2, -b/2, 1,
			-a/2, -b/2, -1
	else
		rect2 -A/2, -B/2, A/2, B/2
	endif
return


! ---------------------------------------------------------
"1_100":
! ---------------------------------------------------------
	gosub "1_200"

return


! ---------------------------------------------------------
"1_50":
! ---------------------------------------------------------
	gosub "top_width_htsp"
	gosub "base_width_htsp"
	gosub "1_100"

	line_type overhead_Line_Type
	line2 	-A/2+h1c, B/2, -A/2+h1c, -B/2
	line2	A/2-h3c, B/2, A/2-h3c, -B/2

	add2 -A/2, 0
	for i=1 to numofbeam-1
		if abs (BETA[i+2]+BETA[i+1]) > 0.01 then
			line2	h2c+BEAMLENGTH_0[i]*cos (ang)+SECTHEIGHT[i]*sin (ang), B/2, h2c+BEAMLENGTH_0[i]*cos (ang)+SECTHEIGHT[i]*sin (ang), -B/2
		endif
	next i
	del 1
return


! ---------------------------------------------------------
"top_width":
! ---------------------------------------------------------
	if abs(abs(ang) - abs(atn(ehm/h2c))) < EPS then
		line_type overhead_Line_Type
	else
		line_type contour_line_type
	endif

	line2 	-A/2+h2c, B/2, -A/2+h2c, -B/2

	if abs(abs(ang) - abs(atn(ehm2/h4c))) < EPS then
		line_type overhead_Line_Type
	else
		line_type contour_line_type
	endif

	line2	A/2-h4c, B/2, A/2-h4c, -B/2
return


! ---------------------------------------------------------
"cut_eave":
! ---------------------------------------------------------
	gosub "base_width_htsp"
	PEN cut_pen
	LINE_TYPE contour_line_type
	if iFill_method > 1  then
		_frame_fill = 1+4
	else
		_frame_fill = 1+2+4+64
	endif

	drawindex 40
		POLY2_B 13, _frame_fill, gs_fill_pen, gs_back_pen,
			-A/2, 				-B/2, 		1,
			-A/2+dp,			-B/2,		1,
			-A/2+dp,			-dw/2, 		1,
			-A/2+h1c-dp,		-dw/2, 		1,
			-A/2+h1c-dp,		-B/2, 		1,
			-A/2+h1c,			-B/2, 		1,
			-A/2+h1c,			B/2, 		1,
			-A/2+h1c-dp,		B/2, 		1,
			-A/2+h1c-dp,		dw/2, 		1,
			-A/2+dp,			dw/2, 		1,
			-A/2+dp,			B/2, 		1,
			-A/2,				B/2, 		1,
			-A/2,				-B/2,		-1

		POLY2_B 13, _frame_fill, gs_fill_pen, gs_back_pen,
			A/2, 				-B/2, 		1,
			A/2-dp,				-B/2,		1,
			A/2-dp,				-dw/2, 		1,
			A/2-h3c+dp,			-dw/2, 		1,
			A/2-h3c+dp,			-B/2, 		1,
			A/2-h3c,			-B/2, 		1,
			A/2-h3c,			B/2, 		1,
			A/2-h3c+dp,			B/2, 		1,
			A/2-h3c+dp,			dw/2, 		1,
			A/2-dp,				dw/2, 		1,
			A/2-dp,				B/2, 		1,
			A/2,				B/2, 		1,
			A/2,				-B/2,		-1

		LINE_TYPE overhead_Line_Type
		PEN gs_cont_pen
return


! ---------------------------------------------------------
"overhead":
! ---------------------------------------------------------
	if (gs_detlevel_2D_m = DETLEVEL_2D_SCALE_SENSITIVE and GLOB_SCALE <= 50) or gs_detlevel_2D_m = DETLEVEL_2D_1_50 then
		gosub "1_50"
		gosub "top_width"
	else
		if (gs_detlevel_2D_m = DETLEVEL_2D_SCALE_SENSITIVE and (GLOB_SCALE > 50 and GLOB_SCALE < 200)) or gs_detlevel_2D_m = DETLEVEL_2D_1_100 then
			gosub "1_100"
		else
			gosub "1_200"
		endif
	endif
return


! ---------------------------------------------------------
"cut1":
! ---------------------------------------------------------
	gosub "base_width_htsp"

	if (gs_detlevel_2D_m = DETLEVEL_2D_SCALE_SENSITIVE and GLOB_SCALE > 50) or (gs_detlevel_2D_m = DETLEVEL_2D_1_100 or gs_detlevel_2D_m = DETLEVEL_2D_1_200) then
		PEN cut_pen
		LINE_TYPE contour_line_type

		line2 -A/2, B/2, -A/2, -B/2									! cut eave, left
		line2 -A/2, 0, -A/2+h1c, 0
		line2 -A/2+h1c, B/2, -A/2+h1c, -B/2

		line2 A/2, B/2, A/2, -B/2									! cut eave, right
		line2 A/2, 0, A/2-h3c, 0
		line2 A/2-h3c, B/2, A/2-h3c, -B/2

		LINE_TYPE overhead_Line_Type
		PEN gs_cont_pen

		gosub "1_200"
	endif

	if (gs_detlevel_2D_m = DETLEVEL_2D_SCALE_SENSITIVE and GLOB_SCALE <= 50) or gs_detlevel_2D_m = DETLEVEL_2D_1_50 then
		gosub "1_50"
		gosub "cut_eave"

		LINE_TYPE overhead_Line_Type
		line2	A/2-h4c, B/2, A/2-h4c, -B/2
		line2 	-A/2+h2c, B/2, -A/2+h2c, -B/2
	endif
return


! -----------------------------------------------------------------------------
"cut2":
! -----------------------------------------------------------------------------
	gosub "base_width_htsp"

	if iFill_method = 2 then
		POLY2_B 5, 2+4+64, gs_fill_pen, gs_back_pen,
		-a/2, -b/2, 1,
		-a/2, b/2, 1,
		 a/2, b/2, 1,
		 a/2, -b/2, 1,
		-a/2, -b/2, -1
	endif

	if (gs_detlevel_2D_m = DETLEVEL_2D_SCALE_SENSITIVE and GLOB_SCALE >50) or (gs_detlevel_2D_m = DETLEVEL_2D_1_100 or gs_detlevel_2D_m = DETLEVEL_2D_1_200) then
		line2 -A/2, B/2, -A/2, -B/2									! cut eave, left
		line2 -A/2, 0, -A/2+h1c, 0
		line2 -A/2+h1c, B/2, -A/2+h1c, -B/2

		line2 A/2, B/2, A/2, -B/2									! cut eave, right
		line2 A/2, 0, A/2-h3c, 0
		line2 A/2-h3c, B/2, A/2-h3c, -B/2
	endif

	if (gs_detlevel_2D_m = DETLEVEL_2D_SCALE_SENSITIVE and GLOB_SCALE <= 50) or gs_detlevel_2D_m = DETLEVEL_2D_1_50 then
		gosub "cut_eave"
	endif
return
