
PEN gs_cont_pen

if gs_detlevel_3D_m = 1 then
	tct=0
	bct=0
	wt=0
	st=0
	gs_resol=3
endif

if gs_detlevel_3D_m = 0 then END

if gs_shadow=0 then SHADOW off

RESOL gs_resol

MATERIAL gs_joist_mat

gosub 10000	! HOTSPOTOK



ADDx a/2

!!!!!! start of TOP CHORD  !!!!!!!!!!!!!!!!!!!!!!!!!!

gosub 10
	FOR I=-1 TO 1 STEP 2
		MULy I
		ADDy -dist/2

				TUBE 6, 7, 1+2+16+32,
					USE (NSP),

					(-tcl+tco)*(ABS(tcxl-0)<0.0001)+((-tcl/2)-2*tcxl*COS(angl))*(ABS(tcxl-0)>=0.0001),   0, tcmh*(ABS(tcxl-0)<0.0001)+(tcsh-2*tcxl*SIN(angl))*(ABS(tcxl-0)>=0.0001), 0,
					-tcl/2-tcxl*COS(angl),    0, tcsh-tcxl*SIN(angl), 0,
					-tcl/2,   		0, tcsh, 0,
					 0-tco,   		0, tcmh, 0,
					 tcl/2,   		0, tceh, 0,
					 tcl/2+tcxr*COS(angr),    0, tceh-tcxr*SIN(angr), 0,
					(tcl-tco)*(ABS(tcxr-0)<0.0001)+((tcl/2)+2*tcxr*COS(angr))*(ABS(tcxr-0)>=0.0001),   0, tcmh*(ABS(tcxr-0)<0.0001)+(tceh-2*tcxr*SIN(angr))*(ABS(tcxr-0)>=0.0001), 0

		DEL 2
	NEXT I


gosub 9999

!!!!!! end of TOP CHORD  !!!!!!!!!!!!!!!!!!!!!!!!!!


!!!!!! start of BOTTOM CHORD  !!!!!!!!!!!!!!!!!!!!!!!!!!

gosub 20
	ADDz -depth
	MULz -1
		FOR I=-1 TO 1 STEP 2
			MULy I
			ADDy -dist/2

					TUBE 6, 7, 1+2+16+32,
						USE (NSP),

						-bcl/2-bcxl*(plustype=1)-bcplus-1,   0, 0, 0,
						-bcl/2-bcxl*(plustype=1)-bcplus,   0, 0, 0,
						-bcl/2-bcplus,   0, 0, 0,
							 0,   0, 0, 0,
						 bcl/2+bcplus,   0, 0, 0,
						 bcl/2+bcxr*(plustype=1)+bcplus,   0, 0, 0,
						 bcl/2+bcxr*(plustype=1)+bcplus+1,   0, 0, 0
			DEL 2
		NEXT I
	DEL 2


gosub 9999

!!!!!! end of BOTTOM CHORD  !!!!!!!!!!!!!!!!!!!!!!!!!!


!!!!!! start of SEAT left  !!!!!!!!!!!!!!!!!!!!!!!!!!

if plustype=1 then
	sd=swl
	gosub 40

		ADDz -sdl
		ADDx -tcl/2
		ROTy 90
			FOR I=-1 TO 1 STEP 2
				MULy I
				ADDy dist/2+tct

					PRISM_ 6, sll,
						USE (NSP)

				DEL 2
			NEXT I
		DEL 3


	gosub 9999
endif

!!!!!! end of SEAT left   !!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!! start of SEAT right  !!!!!!!!!!!!!!!!!!!!!!!!!!

if plustype=1 then
	sd=swr
	gosub 40

		ADDz tcr*(tctype=2)-sdr
		ADDx tcl/2
		ROTy 90
		MULz -1
			FOR I=-1 TO 1 STEP 2
				MULy I
				ADDy dist/2+tct

					PRISM_ 6, slr,
						USE (NSP)

				DEL 2
			NEXT I
		DEL 4


	gosub 9999
endif

!!!!!! end of SEAT right   !!!!!!!!!!!!!!!!!!!!!!!!!!

ADDx -a/2

	if tctype=3 then
		ADDx a
		ROTy angr
		ADDz -tct
		CUTPLANE
		DEL 3
	endif

	ROTy -angl
	ADDz -tct
	CUTPLANE
	DEL 2

	ADDz -ZZYZX+bct+tcr
	ROTy 180
	CUTPLANE
	DEL 2
DEL 1

!!!!!! Profile in the end !!!!!!!!!!!!!!!!!!!!!!!!!!

if plustype=2 then

		FOR I=-1 TO 1 STEP 2
			ADDz -ZZYZX+tcr
			ADDx (-a/2+ww/2)*I
			MULy I
			ADDy (dist/2+tct)*(iWebtype <> WEBTYPE_ROUND)
				h=ZZYZX-tcr+tcr*(tctype=2)*(I=-1)
				websz=ww
				EEEE=I
				gosub 30+iWebtype

			DEL 4
		NEXT I

endif

!!!!!! Profile in the end   !!!!!!!!!!!!!!!!!!!!!!!!!!


!!!!!! start of INTERIOR PANEL of WEB  !!!!!!!!!!!!!!!!!!!!!!!!!!
ADDz -depth

if tctype=3 then
	FOR I=1 TO numofinpanel
		tcrtar=tcr
		tcr1=(tcl/(tcl/2-tco))*tcr
		tcrint1=((tcl-2*epl)/(tcl))*(tcr1)/(numofinpanel)
		tcrend1=((epl)/(tcl))*(tcr1)
		tcr2=-(tcl/(tcl/2+tco))*tcr
		tcrint2=((tcl-2*epl)/(tcl))*(tcr2)/(numofinpanel)
		tcrend2=((epl)/(tcl))*(tcr2)

	if FRA(numofpanel/2)<0.1 then
		depth_1=depth+(tcrend1+(I-1)*(tcrint1))*(I<(onumkod+1))+(tcrend2+(I-1)*(tcrint2)-tcr2)*(I>onumkod)
		depth_2=depth+(tcrend1+(I-1/2)*(tcrint1))*(I<(onumkod+1))+(tcrend2+(I-1/2)*(tcrint2)-tcr2)*(I>onumkod)
		depth_3=depth+(tcrend1+(I)*(tcrint1))*(I<(onumkod))+(tcrend2+(I)*(tcrint2)-tcr2)*(I>onumkod-1)
	endif
	if FRA(numofpanel/2)>0.1 then
		depth_1=depth+(tcrend1+(I-1)*(tcrint1))*(I<(onumkod+1))+(tcrend2+(I-1)*(tcrint2)-tcr2)*(I>onumkod+1)
		depth_2=depth+(tcrend1+(I-1/2)*(tcrint1))*(I<(onumkod+1))+(tcrend2+(I-1/2)*(tcrint2)-tcr2)*(I>onumkod+1)
		depth_3=depth+(tcrend1+(I)*(tcrint1))*(I<(onumkod))+(tcrend2+(I)*(tcrint2)-tcr2)*(I>onumkod)
	endif
		ipwh1=SQR(depth_1*depth_1+ipl/2*ipl/2)	! length of the first profile of an interior panel of web
		ipwh2=depth_2							! length of the second (vertical) profile of an interior panel of web
		ipwh3=SQR(depth_3*depth_3+ipl/2*ipl/2)	! length of the third profile of an interior panel of web


		ADDx I*ipl-ipl*(numofinpanel)/2-ipl/2
			EEEE=1-2*(FRA(I/2)>0.01)
			h=ipwh2
			websz=whw
			if bWebVert then
				gosub 30+iWebtypeVert
			endif

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)<0.01))) then
			ADDx -whw/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE  then
			ADDx 0-whw/2*(iWebtypeVert = WEBTYPE_DOUBLE OR iWebtypeVert = WEBTYPE_SINGLE)
			ROTy 90
			CUTPLANE
			DEL 2
			ADDx -ipl/2
			ROTy -90
			CUTPLANE
			DEL 2
		endif
			EEEE=1
			h=ipwh1
			websz=ww
			MULx -1
			ROTy 90-ATN(depth_1/(ipl/2))
				gosub 30+iWebtype
			DEL 2

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)<0.01))) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE  then
			CUTEND
			CUTEND
		endif

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)>0.01))) then
			ADDx whw/2+0.001
			ROTy -90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE then
			ADDx 0+whw/2*(iWebtypeVert = WEBTYPE_DOUBLE OR iWebtypeVert = WEBTYPE_SINGLE)
			ROTy -90
			CUTPLANE
			DEL 2
			ADDx ipl/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
			EEEE=-1
			h=ipwh3
			websz=ww
			MULx 1
			ROTy 90-ATN(depth_3/(ipl/2))
				gosub 30+iWebtype
			DEL 2

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)>0.01))) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE  then
			CUTEND
			CUTEND
		endif

		DEL 1
		tcr=tcrtar
	NEXT I
endif
if tctype<>3 then
	FOR I=1 TO numofinpanel
		tcrint=((tcl-2*epl)/(tcl))*(tcr)/(numofinpanel)
		tcrend=((epl)/(tcl))*(tcr)
		depth_1=depth+tcrend+(I-1)*(tcrint)
		depth_2=depth+tcrend+(I-1/2)*(tcrint)
		depth_3=depth+tcrend+(I)*(tcrint)
		ipwh1=SQR(depth_1*depth_1+ipl/2*ipl/2)	! length of the first profile of an interior panel of web
		ipwh2=depth_2							! length of the second (vertical) profile of an interior panel of web
		ipwh3=SQR(depth_3*depth_3+ipl/2*ipl/2)	! length of the third profile of an interior panel of web


		ADDx I*ipl-ipl*(numofinpanel)/2-ipl/2
			EEEE=1-2*(FRA(I/2)>0.01)
			h=ipwh2
			websz=whw
			if bWebVert then
				gosub 30+iWebtypeVert
			endif

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)<0.01))) then
			ADDx -whw/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE  then
			ADDx 0-whw/2*(iWebtypeVert = WEBTYPE_DOUBLE OR iWebtypeVert = WEBTYPE_SINGLE)
			ROTy 90
			CUTPLANE
			DEL 2
			ADDx -ipl/2
			ROTy -90
			CUTPLANE
			DEL 2
		endif

			EEEE=1
			h=ipwh1
			websz=ww
			MULx -1
			ROTy 90-ATN(depth_1/(ipl/2))
				gosub 30+iWebtype
			DEL 2

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)<0.01))) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE  then
			CUTEND
			CUTEND
		endif


		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)>0.01))) then
			ADDx whw/2
			ROTy -90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE  then
			ADDx 0+whw/2*(iWebtypeVert = WEBTYPE_DOUBLE OR iWebtypeVert = WEBTYPE_SINGLE)
			ROTy -90
			CUTPLANE
			DEL 2
			ADDx ipl/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
			EEEE=-1
			h=ipwh3
			websz=ww
			MULx 1
			ROTy 90-ATN(depth_3/(ipl/2))
				gosub 30+iWebtype
			DEL 2
		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND (FRA(I/2)>0.01))) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE  then
			CUTEND
			CUTEND
		endif

		DEL 1

	NEXT I
endif
DEL 1
!!!!!! end of INTERIOR PANEL of WEB  !!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!! start of END PANEL of WEB  !!!!!!!!!!!!!!!!!!!!!!!!!!
ADDz -depth

if tctype=3 then
	FOR I=-1 TO 1 STEP 2

		tcrint=((tcl-2*epl)/(tcl))*(tcr)/(numofinpanel)
		tcrend=(((epl)/(tcl/2-tco))*(tcr))*(I=1)+(((epl)/(tcl/2+tco))*(tcr))*(I=-1)
		edepth_1=depth
		edepth_2=depth+(tcrend/2)
		edepth_3=depth+(tcrend)
		epwh1=SQR((tcl-bcl)*(tcl-bcl)/4+(edepth_1)*(edepth_1))							! length of the first profile of end panel of web
		epwh2=SQR((tcl-bcl-epl)*(tcl-bcl-epl)/4+(edepth_2)*(edepth_2))					! length of the second (vertical) profile of end panel of web
		epwh3=SQR((bcl-numofinpanel*ipl)*(bcl-numofinpanel*ipl)/4+(edepth_3)*(edepth_3)) ! length of the third profile of end panel of web


		MULx I
		ADDx -bcl/2
			EEEE=1-2*(I=-1)
			h=epwh2
			websz=whw
			MULx -1
			ROTy 90-ATN(edepth_2/((tcl-bcl-epl)/2))+180*(ATN(edepth_2/((tcl-bcl-epl)/2))<0)
			if bWebVert then
				gosub 30+iWebtypeVert
			endif
			DEL 2

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND  I=1 )) then
			ADDx -whw/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			ADDx 0
			ROTy 90
			CUTPLANE
			DEL 2
			ADDx -(a-bcl)/2
			ROTy -90
			CUTPLANE
			DEL 2
		endif
			EEEE=1*I
			h=epwh1
			websz=ww
			MULx -1
			ROTy 90-ATN(edepth_1/((tcl-bcl)/2))
				gosub 30+iWebtype
			DEL 2
		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND  I=1 )) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			CUTEND
			CUTEND
		endif


		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND  I=-1 )) then
			ADDx whw/2
			ROTy -90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			ADDx 0
			ROTy -90
			CUTPLANE
			DEL 2
			ADDx epl-(a-bcl)/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
			EEEE=-1*I
			h=epwh3
			websz=ww
			MULx 1
			ROTy 90-ATN(edepth_3/((bcl-numofinpanel*ipl)/2))
				gosub 30+iWebtype
			DEL 2
		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND  I=-1 )) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			CUTEND
			CUTEND
		endif

		DEL 2

	NEXT I
endif

if tctype<>3 then

	FOR I=-1 TO 1 STEP 2

		tcrint=((tcl-2*epl)/(tcl))*(tcr)/(numofinpanel)
		tcrend=((epl)/(tcl))*(tcr)
		edepth_1=depth+(tcr)*(I=-1)
		edepth_2=depth+(tcrend/2)*(I=1)+(tcr-tcrend/2)*(I=-1)
		edepth_3=depth+(tcrend)*(I=1)+(tcr-tcrend)*(I=-1)
		epwh1=SQR((tcl-bcl)*(tcl-bcl)/4+(edepth_1)*(edepth_1))							! length of the first profile of end panel of web
		epwh2=SQR((tcl-bcl-epl)*(tcl-bcl-epl)/4+(edepth_2)*(edepth_2))					! length of the second (vertical) profile of end panel of web
		epwh3=SQR((bcl-numofinpanel*ipl)*(bcl-numofinpanel*ipl)/4+(edepth_3)*(edepth_3)) ! length of the third profile of end panel of web


		MULx I
		ADDx -bcl/2
			EEEE=1-2*(I=-1)
			h=epwh2
			websz=whw
			MULx -1
			ROTy 90-ATN(edepth_2/((tcl-bcl-epl)/2))+180*(ATN(edepth_2/((tcl-bcl-epl)/2))<0)
			if bWebVert then
				gosub 30+iWebtypeVert
			endif
			DEL 2

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND I=1 )) then
			ADDx -whw/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			ADDx 0
			ROTy 90
			CUTPLANE
			DEL 2
			ADDx -(a-bcl)/2
			ROTy -90
			CUTPLANE
			DEL 2
		endif
			EEEE=1*I
			h=epwh1
			websz=ww
			MULx -1
			ROTy 90-ATN(edepth_1/((tcl-bcl)/2))
				gosub 30+iWebtype
			DEL 2

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND  I=1 )) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			CUTEND
			CUTEND
		endif
		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND  I=-1 )) then
			ADDx whw/2
			ROTy -90
			CUTPLANE
			DEL 2
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			ADDx 0
			ROTy -90
			CUTPLANE
			DEL 2
			ADDx epl-(a-bcl)/2
			ROTy 90
			CUTPLANE
			DEL 2
		endif
			EEEE=-1*I
			h=epwh3
			websz=ww
			MULx 1
			ROTy 90-ATN(edepth_3/((bcl-numofinpanel*ipl)/2))
				gosub 30+iWebtype
			DEL 2

		if iWebtype = WEBTYPE_SINGLE AND (iWebtypeVert = WEBTYPE_DOUBLE  OR (iWebtypeVert = WEBTYPE_SINGLE AND  I=-1 )) then
			CUTEND
		endif
		if iWebtype = WEBTYPE_DOUBLE & ( not(bWebVert) | iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_DOUBLE | iWebtypeVert = WEBTYPE_CRIMPLED ) then
			CUTEND
			CUTEND
		endif
		DEL 2

	NEXT I

endif

DEL 1
!!!!!! end of END PANEL of WEB  !!!!!!!!!!!!!!!!!!!!!!!!!!

if tctype=3 then CUTEND
CUTEND
CUTEND

DEL 1



END


!!------------------------------------------  END  -----------------------------------------
!!------------------------------------------  END  -----------------------------------------
!!------------------------------------------  END  -----------------------------------------


10:		! top chord profile_1 double angle

	PUT	   0,    0, 0
	PUT	-tcw,    0, 0
	PUT	-tcw, -tct, 0
	PUT	-tct, -tct, 0
	PUT	-tct, -tcw, 0
	PUT	   0, -tcw, 0


RETURN
20:		! bottom chord profile_1 double angle

	PUT	   0,    0, 0
	PUT	-bcw,    0, 0
	PUT	-bcw, -bct, 0
	PUT	-bct, -bct, 0
	PUT	-bct, -bcw, 0
	PUT	   0, -bcw, 0


RETURN

29:		! None vertical web profile

RETURN

30:		! ROD web profile

		  PRISM_      7,      h,
			 -dist/2,    0,      15+64,
				   0,    0,    900,
				   0,  180,   4001,
			 dist/2,    0,      15+64,
				   0,    0,    900,
				   0,  180,   4001,
			 -dist/2,    0,      -1
RETURN
31:		! Double Angle web profiles

		FOR QQQ=-1 TO 1 STEP 2
			MULy QQQ
			MULx -1
			ADDx websz/2
			ADDy dist/2+tct
				PRISM_ 6, h,
						 0,    0, 15,
						-websz,    0, 15,
						-websz,  wt, 15,
						-wt, wt, 15,
						-wt, websz, 15,
						0, websz, 15

			DEL 4
		NEXT QQQ
RETURN
32:		! Single Angle web profiles


			MULy EEEE
			MULx -1
			ADDx websz/2
			ADDy dist/2+tct
				PRISM_ 6, h,
						 0,    0, 15,
						-websz,    0, 15,
						-websz,  wt, 15,
						-wt, wt, 15,
						-wt, websz, 15,
						0, websz, 15

			DEL 4

RETURN
33:		!CRImped Angle

hcrimp=MAX(tcw, bcw)
hp=MAX(tcw+0.03, bcw+0.03)
wcrimped=ww
tcrimped=wt

h=h-2*(hcrimp+hp)

ADDx -wcrimped/2
ADDz h/2+hcrimp+hp
ROTz -45
FOR TTT=-1 TO 1 STEP 2
MULz TTT
ADDz h/2


	SWEEP 4, 5, 56.25/5, 1, 1+2+4,
		0, 0, 0,
		wcrimped, 0, 0,
		wcrimped, tcrimped, 0,
		0, tcrimped, 0,

		0, 0, 0,
		0, 0, 1*hcrimp/4,
		0, 0, 2*hcrimp/4,
		0, 0, 3*hcrimp/4,
		0, 0, hcrimp

	ROTz 90
	MULy -1
	SWEEP 4, 5, 56.25/5, 1, 1+2+4,
		0, 0, 0,
		wcrimped, 0, 0,
		wcrimped, tcrimped, 0,
		0, tcrimped, 0,

		0, 0, 0,
		0, 0, 1*hcrimp/4,
		0, 0, 2*hcrimp/4,
		0, 0, 3*hcrimp/4,
		0, 0, hcrimp
	DEL 2

	ROTz 45
	ROTx 90
	ADDy hcrimp
	ADDz -tcrimped
	PRISM_ 4, 2*tcrimped,
		0, 0, 10,
		wcrimped, 0, 14,
		wcrimped, hp, 14,
		0, hp, 14
	DEL 4



DEL 2
NEXT TTT


ADDz -h/2
	PRISM_  6, h,
		0, 0, 10,
		wcrimped, 0, 10,
		wcrimped, tcrimped, 10,
		tcrimped, tcrimped, 10,
		tcrimped, wcrimped, 10,
		0, wcrimped, 10
DEL 1


DEL 3

RETURN

40:		! SEAT profile_1 double angle

	PUT	   0,    0, 15
	PUT	-sd,    0, 15
	PUT	-sd,  st, 15
	PUT	-st, st, 15
	PUT	-st, sd, 15
	PUT	   0, sd, 15


RETURN


9999:	! verem urites
FOR WWW=1 TO nsp
	qq=GET(1)
NEXT WWW
RETURN

10000:			! hotspotok

unid=1

	!!! >>> BASE LENGTH

	hotspot  0, 0, 0, unID, 	A, 1+256    : unID=unID+1
	hotspot -1,	0, 0, unID, 	A, 3    : unID=unID+1
	hotspot  A, 0, 0, unID,  A, 2   : unID=unID+1

	hotspot  0, 0, (-sdl)*(plustype=1)-(ZZYZX-tcr)*(plustype=2), unID, 	A, 1+256    : unID=unID+1
	hotspot -1,	0, (-sdl)*(plustype=1)-(ZZYZX-tcr)*(plustype=2), unID, 	A, 3    : unID=unID+1
	hotspot  A, 0, (-sdr+tcr*(tctype=2))*(plustype=1)-(ZZYZX-tcr)*(plustype=2), unID,  A, 2   : unID=unID+1

	!!! >>> Length of End Panel

	hotspot  0, 	0, epl*TAN(angl), 				 unID, epl, 1+128      : unID=unID+1
	hotspot -1,		0, epl*TAN(angl), 				 unID, epl, 3    	    : unID=unID+1
	hotspot  epl,   0, epl*TAN(angl), 				 unID, epl, 2    		: unID=unID+1

	hotspot  a, 	0, tcr*(tctype=2)+epl*TAN(angr), 				 unID, epl, 1+128      : unID=unID+1
	hotspot  a+1,	0, tcr*(tctype=2)+epl*TAN(angr), 				 unID, epl, 3    	    : unID=unID+1
	hotspot  a-epl, 0, tcr*(tctype=2)+epl*TAN(angr), 				 unID, epl, 2    		: unID=unID+1

	!!! >>> Bottom Chord Dist. from End

	hotspot 0, 		0, -depth, 	unID, bcl2, 1+128        : unID=unID+1
	hotspot -1,		0, -depth, 	unID, bcl2, 3    	    : unID=unID+1
	hotspot bcl2,   0, -depth, 	unID, bcl2, 2    		: unID=unID+1

	hotspot a, 		         0, -depth, 	unID, bcl2, 1+128        : unID=unID+1
	hotspot a+1,			 0, -depth, 	unID, bcl2, 3    	    : unID=unID+1
	hotspot a-bcl2,    		 0, -depth, 	unID, bcl2, 2    		: unID=unID+1

	!!! >>> Joist Depth

	hotspot  a/2-tco, 	0, 		 tcr, 	     unID, ZZYZX, 1+128       : unID=unID+1
	hotspot  a/2-tco,	0, 		 tcr+1, 	 unID, ZZYZX, 3    	    : unID=unID+1
	hotspot  a/2-tco,   0,  	 tcr-ZZYZX, unID, ZZYZX, 2    		: unID=unID+1

if tctype<>1 then
	!!! >>> Height of Ridge
	hotspot  a/2-tco, 	0, 		 0, 	unID, tcr, 1+128       : unID=unID+1
	hotspot  a/2-tco,	0, 		 0-1, 	unID, tcr, 3    	    : unID=unID+1
	hotspot  a/2-tco,   0,  	 tcr,   unID, tcr, 2    		: unID=unID+1
endif

	!!! >>> Top Chord Extension (Side 2)
	hotspot  0, 	 dist/2+tcw, 		 0, 	unID, tcxl, 1+128       : unID=unID+1
	hotspot  0+1,	 dist/2+tcw, 		 0, 	unID, tcxl, 3    	    : unID=unID+1
	hotspot  -tcxl*COS(angl),   dist/2+tcw,  	 -tcxl*SIN(angl),   unID, tcxl, 2    		: unID=unID+1

	hotspot  0, 	 -dist/2-tcw, 		 0, 	unID, tcxl, 1+128       : unID=unID+1
	hotspot  0+1,	 -dist/2-tcw, 		 0, 	unID, tcxl, 3    	    : unID=unID+1
	hotspot  -tcxl*COS(angl),   -dist/2-tcw,  	 -tcxl*SIN(angl),   unID, tcxl, 2    		: unID=unID+1

	!!! >>> Top Chord Extension (Side 1)
	hotspot  a, 	 			 dist/2+tcw, 	tcr*(tctype=2), 	unID, tcxr, 1+128       : unID=unID+1
	hotspot  a-1,				 dist/2+tcw, 	tcr*(tctype=2), 	unID, tcxr, 3    	    : unID=unID+1
	hotspot  a+tcxr*COS(angr),   dist/2+tcw,  	-tcxr*SIN(angr)+tcr*(tctype=2),   unID, tcxr, 2    		: unID=unID+1

	hotspot  a, 	 			 -dist/2-tcw, 	tcr*(tctype=2), 	unID, tcxr, 1+128       : unID=unID+1
	hotspot  a-1,				 -dist/2-tcw, 	tcr*(tctype=2), 	unID, tcxr, 3    	    : unID=unID+1
	hotspot  a+tcxr*COS(angr),   -dist/2-tcw,  	-tcxr*SIN(angr)+tcr*(tctype=2),   unID, tcxr, 2    		: unID=unID+1

if plustype=1 then

	!!! >>> Bottom Chord Extension (Side 2)

	hotspot  a/2-bcl/2, 	 -bcw-dist/2, 		 -depth, 	unID, bcxl, 1+128       : unID=unID+1
	hotspot  a/2-bcl/2+1,	 -bcw-dist/2, 		 -depth, 	unID, bcxl, 3    	    : unID=unID+1
	hotspot  a/2-bcl/2-bcxl, -bcw-dist/2,  	     -depth,    unID, bcxl, 2    		: unID=unID+1

	hotspot  a/2-bcl/2, 	 bcw+dist/2, 		 -depth, 	unID, bcxl, 1+128       : unID=unID+1
	hotspot  a/2-bcl/2+1,	 bcw+dist/2, 		 -depth, 	unID, bcxl, 3    	    : unID=unID+1
	hotspot  a/2-bcl/2-bcxl, bcw+dist/2,  	     -depth,    unID, bcxl, 2    		: unID=unID+1

	!!! >>> Bottom Chord Extension (Side 1)

	hotspot  a/2+bcl/2, 	   -bcw-dist/2, 		 -depth, 	unID, bcxr, 1+128       : unID=unID+1
	hotspot  a/2+bcl/2-1,	   -bcw-dist/2, 		 -depth, 	unID, bcxr, 3    	    : unID=unID+1
	hotspot  a/2+bcl/2+bcxr,   -bcw-dist/2,  	     -depth,    unID, bcxr, 2    		: unID=unID+1

	hotspot  a/2+bcl/2, 	   bcw+dist/2, 		 -depth, 	unID, bcxr, 1+128       : unID=unID+1
	hotspot  a/2+bcl/2-1,	   bcw+dist/2, 		 -depth, 	unID, bcxr, 3    	    : unID=unID+1
	hotspot  a/2+bcl/2+bcxr,   bcw+dist/2,  	 -depth,    unID, bcxr, 2    		: unID=unID+1

	!!! >>> Depth of Seat (Side 2)

	hotspot sll/2, 		     0, 0,    unID, sdl, 1+128      : unID=unID+1
	hotspot sll/2,			 0, 1,    unID, sdl, 3    	    : unID=unID+1
	hotspot sll/2,    		 0, -sdl, unID, sdl, 2    		: unID=unID+1

	!!! >>> Seat Length (Side 2)

	hotspot 0, 		     0, -sdl,    unID, sll, 1+128       : unID=unID+1
	hotspot -1,			 0, -sdl,    unID, sll, 3    	    : unID=unID+1
	hotspot sll,    	 0, -sdl,    unID, sll, 2    		: unID=unID+1

	!!! >>> Seat Length (Side 1)

	hotspot a, 		     0, -sdr+tcr*(tctype=2),    unID, slr, 1+128       : unID=unID+1
	hotspot a+1,	     0, -sdr+tcr*(tctype=2),    unID, slr, 3    	    : unID=unID+1
	hotspot a-slr,    	 0, -sdr+tcr*(tctype=2),    unID, slr, 2    		: unID=unID+1

	!!! >>> Depth of Seat (Side 1)

	hotspot a-slr/2, 		     0, tcr*(tctype=2),    unID, sdr, 1+128      : unID=unID+1
	hotspot a-slr/2,			 0, tcr*(tctype=2)+1,    unID, sdr, 3    	    : unID=unID+1
	hotspot a-slr/2,    		 0, -sdr+tcr*(tctype=2), unID, sdr, 2    		: unID=unID+1
endif
if tctype=3 then
	!!! >>> Distance of Ridge from Center
	hotspot a/2, 		     0, tcr-0.05,    unID, tco, 1+128      : unID=unID+1
	hotspot a/2+1,			 0, tcr-0.05,    unID, tco, 3    	    : unID=unID+1
	hotspot a/2-tco,    	 0, tcr-0.05,    unID, tco, 2    		: unID=unID+1
endif

RETURN


