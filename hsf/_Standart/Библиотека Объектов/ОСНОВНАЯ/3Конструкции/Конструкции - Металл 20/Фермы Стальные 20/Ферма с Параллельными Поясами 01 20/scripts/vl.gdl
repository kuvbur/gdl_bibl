
hideparameter all "A", "B", "Zzyzx" 

if GLOB_UI_BUTTON_ID = 1 then
	parameters gs_ui_current_page = 2
endif

if GLOB_UI_BUTTON_ID = 2 then
	parameters gs_ui_current_page = 3
endif

call "FM_types" parameters all

parameters ac_bottomlevel = -zzyzx

dim stWebtypeValues[4]
	stWebtypeValues[1] = `Круглый стержень`
	stWebtypeValues[2] = `Одинарный угол`
	stWebtypeValues[3] = `Двойной угол`
	stWebtypeValues[4] = `Закрученный угол`

values{2} "iWebtype"	WEBTYPE_ROUND, stWebtypeValues[1],
						WEBTYPE_SINGLE, stWebtypeValues[2],
						WEBTYPE_DOUBLE, stWebtypeValues[3],
						WEBTYPE_CRIMPLED, stWebtypeValues[4]

values{2} "iWebtypeVert"	WEBTYPE_ROUND, stWebtypeValues[1],
						WEBTYPE_SINGLE, stWebtypeValues[2],
						WEBTYPE_DOUBLE, stWebtypeValues[3],
						WEBTYPE_CRIMPLED, stWebtypeValues[4]

if not(bWebVert) then
	hideparameter "iWebtypeVert", "stWebtypeVert", "whw"
	lock "iWebtypeVert", "stWebtypeVert", "whw"
endif

dim stDetLevel3d[3]
	stDetLevel3d[1] = `Детальный`
	stDetLevel3d[2] = `Простой`
	stDetLevel3d[3] = `Откл.`

values{2} "gs_detlevel_3D_m"	2, stDetLevel3d[1],
							1, stDetLevel3d[2],
							0, stDetLevel3d[3]

dim stDetLevel2d[2]
	stDetLevel2d[1] = `Детальный`
	stDetLevel2d[2] = `Простой`

values{2} "gs_detlevel_2D_m"	2, stDetLevel2d[1],
								1, stDetLevel2d[2]

values "dist" range (0,)

if iWebtype = WEBTYPE_ROUND & iWebtypeVert = WEBTYPE_ROUND | iWebtype = WEBTYPE_ROUND & iWebtypeVert = WEBTYPE_NONE then
	hideparameter "web_dim", "ww", "whw", "wt"
	lock "web_dim", "ww", "whw", "wt"
endif
if iWebtypeVert = WEBTYPE_ROUND | iWebtypeVert = WEBTYPE_NONE then
	hideparameter "whw"
	lock "whw"
endif
if iWebtype = WEBTYPE_ROUND then
	hideparameter "ww"
	lock "ww"
endif

if plustype = 2 then
	hideparameter "seat", "swl", "sll", "swr", "slr", "st", "sdl", "sdr"
	lock "seat", "swl", "sll", "swr", "slr", "st", "sdl", "sdr"
endif

values "gs_resol" range [3, )
if iWebtype <> WEBTYPE_ROUND & iWebtypeVert <> WEBTYPE_ROUND then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

values "ZZYZX" range (0, ]
values "A" range (0, ]

parameters B=dist+2*tcw
lock "B"

!!! >>> Top Chord
values "tcw" range (0, )
values "tct" range (0, )
values "tcxl" range [0, ]
values "tcxr" range [0, ]
values "p_tcxl" range [0, ]
values "p_tcxr" range [0, ]

!!! >>> Bottom Chord
values "bcw" range (0, )
values "bct" range (0, )
values "bcxl" range [0, bcl2]
values "bcxr" range [0, bcl2]

!!! >>> Web
values "ww" range (0, )
values "whw" range (0, )
values "wt" range (0, )

!!! >>> Seat Bearing
values "swl" range (0, )
values "swr" range (0, )
values "st" range (0, )
values "sll" range (0, )
values "slr" range (0, )
values "SeatExtLeft" range [0, tcxl]
values "SeatExtRight" range [0, tcxr]

if ABS(sll-0)<=0.001 then
	sll=0.01
	parameters sll=0.01
endif
if ABS(slr-0)<=0.001 then
	slr=0.01
	parameters slr=0.01
endif

if GLOB_MODPAR_NAME="tcxl" & ABS(tcxl)<EPS then 		!!! >>> tcxl=0
	sll=0.05
	parameters sll=0.05
endif
if GLOB_MODPAR_NAME="tcxr" & ABS(tcxr)<EPS then 		!!! >>> tcxr=0
	slr=0.05
	parameters slr=0.05
endif

if plustype=2 then
	parameters bcxl=0
	parameters bcxr=0
endif

if plustype = 1 then values "bcl2" range [MAX(bcxl, bcxr), epl)
if plustype = 2 then values "bcl2" range (0, epl)

values "epl" range (bcl2, a/2)

values "tcr" range (0, ZZYZX]
values "numofpanel" range [3, ]

values "onum_p" onum2
values "tco" tco2

values "tctype" 1, 2, 3

values "sdl" range [swl, tcw+swl]
values "sdr" range [swr, tcw+swr]

if not(axis) then
	parameters only_axis=0
	lock "only_axis", "axis_line_type", "axis_pen"
endif

if axis and only_axis then
	lock "gs_fill_type", "gs_fill_pen", "gs_back_pen", "gs_detlevel_2D_m", "gs_cont_pen"
endif

if gs_cont_pen = 0 then parameters gs_cont_pen = 90
if gs_fill_pen = 0 then parameters gs_fill_pen = 19
if axis_pen = 0 then parameters axis_pen = 69

