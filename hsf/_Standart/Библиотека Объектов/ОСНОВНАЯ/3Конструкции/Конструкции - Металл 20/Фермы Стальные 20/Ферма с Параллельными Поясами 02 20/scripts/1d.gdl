
EPS=0.001

tctype=tctype
plustype=plustype

! iWebtype, iWebtypeVert
WEBTYPE_ROUND = 0
WEBTYPE_SINGLE = 2
WEBTYPE_DOUBLE = 1
WEBTYPE_CRIMPLED = 3
WEBTYPE_NONE = -1

!!! >>> tctype=1 parallel chords
!!! >>> tctype=2 single pitched top chords
!!! >>> tctype=3 double pitched top chords

!!! >>> plustype=1  underslung
!!! >>> plustype=2  square ends


DIM onum2 []
DIM tco2 []


!!! GEOMETRY dimensions   --------------------------------------------------------------------------------------------------

dist=dist						! distance between the  profiles of chord
depth=ZZYZX				     	! Joist depth
tcr=tcr*(tctype<>1)			    ! ridge of top chord
depth=depth-tcr					! Joist depth
epl=epl							! end panel length
numofpanel=numofpanel			! Number of sumpanel
numofinpanel=numofpanel-2		! Number of interiorpanel
ipl=(a-2*epl)/(numofinpanel)	! interior panel length



k=1
FOR I=-INT(numofpanel/2)+1 TO INT(numofpanel/2)-1
	k=k
	if (I=0 & FRA(numofpanel/2)<0.1) then GOTO 100000
		onum2 [k]=I
		if FRA(numofpanel/2)>0.1 then tco2 [k]=2*I*ipl/2   !!!*(tctype=3)
		if FRA(numofpanel/2)<0.1 then tco2 [k]=(2*I-1+2*(I<0))*(ipl/2)   !!!*(tctype=3)
	k=k+1
	100000:

NEXT I

if tctype=3 then
	if FRA(numofpanel/2)<0.1 then
		if GLOB_MODPAR_NAME="onum_p" | GLOB_MODPAR_NAME="A" | GLOB_MODPAR_NAME="epl"  | GLOB_MODPAR_NAME="numofpanel" then
					   tco=(2*onum_p-1+2*(onum_p<0))*(ipl/2)   !*(tctype=3)
			parameters tco=(2*onum_p-1+2*(onum_p<0))*(ipl/2)   !*(tctype=3)
		endif
		if GLOB_MODPAR_NAME="tco" | GLOB_MODPAR_NAME="A" | GLOB_MODPAR_NAME="epl"  | GLOB_MODPAR_NAME="numofpanel" then
					   onum_p=0.5*(1-2*(tco<0)+tco/(ipl/2))  !*(tctype=3)
			parameters onum_p=0.5*(1-2*(tco<0)+tco/(ipl/2))  !*(tctype=3)
		endif
	endif

	if FRA(numofpanel/2)>0.1 then
		if GLOB_MODPAR_NAME="onum_p" | GLOB_MODPAR_NAME="A" | GLOB_MODPAR_NAME="epl"  | GLOB_MODPAR_NAME="numofpanel"  then
					   tco=2*onum_p*(ipl/2)   !*(tctype=3)
			parameters tco=2*onum_p*(ipl/2)   !*(tctype=3)
		endif
		if GLOB_MODPAR_NAME="tco" | GLOB_MODPAR_NAME="A" | GLOB_MODPAR_NAME="epl"  | GLOB_MODPAR_NAME="numofpanel" then
					   onum_p=0.5*tco/(ipl/2)  !*(tctype=3)
			parameters onum_p=0.5*tco/(ipl/2)  !*(tctype=3)
		endif
	endif
else
	tco=0
	onum_p=0
endif



if FRA(numofpanel/2)<0.1 then onum=2*onum_p-1+2*(onum_p<0)		! number of offset distance ( 1=ipl/2 )
if FRA(numofpanel/2)>0.1 then onum=2*onum_p						! number of offset distance ( 1=ipl/2 )


onumkod=numofinpanel/2+(1/2)*(FRA(onum/2)>0.1)-onum/2

!!! TOP CHORD profiles dimensions   --------------------------------------------------------------------------------------

tcw=tcw  				! L shaped top chord angle width
tct=tct	  				! thickness of top chord profile
tca=2*(tcw-tct/2)*tct	! area of TOP CHORDS

! Bottom CHORD profiles dimensions   -----------------------------------------------------------------------------------

bcw=bcw   				! L shaped bottom chord angle width
bct=bct	  				! thickness of bottom chord profile
bca=2*(bcw-bct/2)*bct	! area of BOTTOM CHORDS

!!! WEB profiles dimensions   --------------------------------------------------------------------------------------------

ww=ww      ! L shaped web angle width
whw=whw    ! L shaped web angle width vertical
wt=wt	   ! thickness of web profile

wca=(2*(ww-wt/2)*wt)*(iWebtype <> WEBTYPE_ROUND)+(((dist/2)*(dist/2)-(dist/2-wt)*(dist/2-wt))*pi/4)*(iWebtype = WEBTYPE_ROUND)	! area of WEB (non-vertical)
wcva=(2*(ww-wt/2)*wt)*(iWebtypeVert <> WEBTYPE_ROUND & iWebtypeVert <> WEBTYPE_NONE )+(((dist/2)*(dist/2)-(dist/2-wt)*(dist/2-wt))*pi/4)*(iWebtypeVert = WEBTYPE_ROUND)

!!! Geometry dimensions of TOP CHORD   -----------------------------------------------------------------------------------

tcl=A						! length of top chord
tcsh=0									! start height of top chord
tcmh=tcr*(tctype=3)+tcr/2*(tctype=2)	! middle height of top chord
tceh=tcr*(tctype=2)						! end height of top chord
if tctype<>1 then
	angl=90-ATN((tcl/2-tco)/tcr)*(tctype=3)-ATN((tcl)/tcr)*(tctype=2)
	angr=90-ATN((tcl/2+tco)/tcr)*(tctype=3)-(180-ATN((tcl)/tcr))*(tctype=2)
else
	angl=0
	angr=0
endif

!!! Geometry dimensions of BOTTOM CHORD

bcl=(a-2*bcl2) 							! length of bottom chord
bcxl=bcxl								! Bottom Chord extension left side
bcxr=bcxr								! Bottom Chord extension right side
bcplus=((a-bcl)/2)*(plustype=2)

!!! SEAT profiles dimensions   --------------------------------------------------------------------------------------------

swl=swl	     ! Seat L width left
swr=swr 	 ! Seat L width right
sdl=sdl      ! Seat depth left
sdr=sdr      ! Seat depth right
sll=sll	 	 ! Seat length left
slr=slr	     ! Seat length right
st=st	 	 ! thickness of seat profile

if plustype=1 then			! area of Seats
	sla=2*(swl-st/2)*st
	sra=2*(swr-st/2)*st
else
	sla=0
	sra=0
endif

if GLOB_MODPAR_NAME="tcxl"  then
	parameters p_tcxl=tcxl*cos(angl)
	parameters _ptl=0
else
	if NOT(_ptl) | GLOB_MODPAR_NAME="p_tcxl"  then
		tcxl=p_tcxl/cos(angl)
		parameters tcxl=p_tcxl/cos(angl)
	endif
endif

if GLOB_MODPAR_NAME="tcxr"  then
	parameters p_tcxr=tcxr*cos(angr)
	parameters _ptr=0
else
	if NOT(_ptr) | GLOB_MODPAR_NAME="p_tcxr"  then
		tcxr=p_tcxr/cos(angr)
		parameters tcxr=p_tcxr/cos(angr)
	endif
endif


