
! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		del 1
	endif
endif

! ==============================================================================

if not(gs_2D_projection) & gs_detlevel_3D_m = DLEV3D_OFF then goto 100	! END

if	not(gs_shadow)	then shadow off
if		gs_shadow	then shadow on

pen gs_cont_pen
material mat1


!!---- HOTSPOT Editing
unID=1

HOTSPOT 0,0,0,unID,A,1+256 : unID=unID+1
HOTSPOT -1,0,0,unID,A,3 : unID=unID+1
HOTSPOT A,0,0,unID,A,2 : unID=unID+1

!!---- Angle Editing
ADDx A/2
	HOTSPOT 0,0,0,unID,neig,6 : unID=unID+1
	HOTSPOT 0,1,0,unID,neig,4+128 : unID=unID+1
	HOTSPOT 1,0,0,unID,neig,7 : unID=unID+1
	HOTSPOT 0,cos(neig),sin(neig),unID,neig,5 : unID=unID+1
DEL 1

!!---- Length Editing
rotx neig
	HOTSPOT A/2,0,0,unID,gs_struc_length,1+256 : unID=unID+1
	HOTSPOT A/2,-1,0,unID,gs_struc_length,3 : unID=unID+1
	HOTSPOT A/2,gs_struc_length,0,unID,gs_struc_length,2 : unID=unID+1

	HOTSPOT A/2,0,0,unID,dist,1+128 : unID=unID+1
	HOTSPOT A/2,-1,0,unID,dist,3 : unID=unID+1
	HOTSPOT A/2,dist,0,unID,dist,2 : unID=unID+1
del 1

L1XY=B/(SIN(schneid1))
ADDz ZZYZX
	HOTSPOT 0, 0, 0, unID, schneid1, 6: unID=unID+1
	HOTSPOT L1XY, 0, 0, unID, schneid1, 4+128: unID=unID+1
	HOTSPOT L1XY *COS(schneid1), L1XY *SIN(schneid1), 0, unID, schneid1, 5: unID=unID+1
DEL 1

L2XY=B/(SIN(schneid2))
ADD	A, 0 , ZZYZX
	HOTSPOT 0, 0, 0, unID, schneid2, 6: unID=unID+1
	HOTSPOT L2XY, 0, 0, unID, schneid2, 4+128: unID=unID+1
	HOTSPOT L2XY *COS(schneid2), L2XY *SIN(schneid2), 0, unID, schneid2, 5: unID=unID+1
DEL 1



rotz schneid1
cutplane -90
del 1

addx a
rotz schneid2
cutplane 90
del 2

addy b
cutplane -90
del 1

um1=min(0,-b*tan(schneid1-90))
um2=max(0,b*tan(90-schneid2))

addx um1
rotx neig
gesamt=-um1+a+um2
bb=b/cos(neig)
j=int(bb/dist)+1

for i=1 to j step 1
	Base
	PRISM 4,h,
			0,0,
			gesamt,0,
			gesamt,v,
			0,v

	vert 0, 0, 0

	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4
	body 1

	addy dist
next i

cutend
cutend
cutend

del top

!!! *** 3D Hotspots in 3D
s1x=B/tan(schneid1)
s2x=B/tan(schneid2)
s12z=B*tan(neig)



100:
! ==============================================================================

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then cutend
	if gs_2D_projection_type <> 6 then cutend
	if AC_symb_show_projection_to <> 3 then cutend
endif

END
