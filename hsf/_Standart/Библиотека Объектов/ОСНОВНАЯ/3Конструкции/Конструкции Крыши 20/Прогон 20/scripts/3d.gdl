
! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		del 1
	endif
endif

! ==============================================================================

if not(gs_2D_projection) & gs_detlevel_3D_m = 0 then goto 100	! END

if	not(gs_shadow)	then shadow off
if		gs_shadow	then shadow on

pen gs_cont_pen
material mat1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!! *** Length
Unid=1

HOTSPOT 0,0,0,unID,A,1+256 : unID=unID+1
HOTSPOT -1,0,0,unID,A,3 : unID=unID+1
HOTSPOT A,0,0,unID,A,2 : unID=unID+1


!!! *** Rotation Angle

ROTx 90
	HOTSPOT 0,0,0,unID,kipp,6 : unID=unID+1
	HOTSPOT 0,0,H,unID,kipp,4+128 : unID=unID+1
	HOTSPOT -1,0,0,unID,kipp,7 : unID=unID+1
	HOTSPOT 0,H*sin(kipp),H*cos(kipp),unID,kipp,5 : unID=unID+1
DEL 1

ADDx A
ROTx 90
	HOTSPOT 0,0,0,unID,kipp,6 : unID=unID+1
	HOTSPOT 0,0,H,unID,kipp,4+128 : unID=unID+1
	HOTSPOT -1,0,0,unID,kipp,7 : unID=unID+1
	HOTSPOT 0,H*sin(kipp),H*cos(kipp),unID,kipp,5 : unID=unID+1
DEL 2

!!! *** Profile Height

ROTx 90-kipp
	HOTSPOT  A/2, 0, 0, unID,H,1+256 : unID=unID+1
	HOTSPOT  A/2, 0, -1,unID,H,3 : unID=unID+1
	HOTSPOT  A/2, 0, H,unID,H,2 : unID=unID+1
DEL 1
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

hm=abs(v/2*cos(kipp-90))+abs(h*cos(kipp))
hn=abs(v/2*cos(kipp-90))

if kipp<=90 then
nn=hm
mm=hn
else
nn=hn
mm=hm
endif


!*******************

if abs(schneid1-90)<eps then
um1=0
else
um1=abs(min(-nn/tan(schneid1),mm/tan(schneid1)))
endif

rotz schneid1
cutplane -90


del 1

!*******************

addx a

if abs(schneid2-90)<eps then
um2=0
else
um2=abs(max(-nn/tan(schneid2),mm/tan(schneid2)))
endif

rotz schneid2
cutplane 90

del 2


gesamt=a+um1+um2


roty 90
addz -um1
rotz -kipp
addy -h/2


IF abs(kipp-90)>eps and abs(schneid1-90)>eps or abs(kipp-90)>eps and abs(schneid2-90)>eps then goto 20

	H1y=v/2/tan(schneid1)
	H2y=v/2/tan(schneid2)

	If schneid1<=90 then
	ADDz H1y
	else
	ADDz -H1y
	ENDIF

		!!! Hotspots on bottom
		HOTSPOT v/2,h/2,-H1y,	Unid	:Unid=Unid+1
		HOTSPOT -v/2,-h/2,H1y,	Unid	:Unid=Unid+1
		HOTSPOT v/2,-h/2,-H1y,	Unid	:Unid=Unid+1

		!!! Hotspots on top
		HOTSPOT v/2,h/2,A-H2y,	Unid	:Unid=Unid+1
		HOTSPOT -v/2,h/2,A+H2y,	Unid	:Unid=Unid+1
		HOTSPOT -v/2,-h/2,A+H2y,	Unid	:Unid=Unid+1
		HOTSPOT v/2,-h/2,A-H2y,	Unid	:Unid=Unid+1

	DEL 1

20:

	!!! ** Beam

	base
		PRISM_ 4,gesamt,
			v/2,-h/2,15,
			v/2,h/2,15,

			-v/2,h/2,15,
			-v/2,-h/2,15

	vert 1,0, 1
	vert 1, 0, 0
	vert 0, 0, 1
	vert 0, 1, 0
	coor 2, -1, -2, -3, -4
	body 1

cutend
cutend
del top

IF abs(kipp-90)>eps and abs(schneid1-90)>eps or abs(kipp-90)>eps and abs(schneid2-90)>eps then

		!!! Hotspots on Axis
		kipp_y=v/2*sin(kipp)
		kipp_z=v/2*cos(kipp)
		kipp1_x=(v/2*sin(kipp))*tan(90-schneid1)
		kipp2_x=(v/2*sin(kipp))*tan(90-schneid2)

		HOTSPOT 0,0,0,	Unid	:Unid=Unid+1
		HOTSPOT a,0,0,	Unid	:Unid=Unid+1
		HOTSPOT -(H*cos(kipp))*tan(90-schneid1),-H*cos(kipp),H*sin(kipp),	Unid	:Unid=Unid+1
		HOTSPOT A-(H*cos(kipp))*tan(90-schneid2),-H*cos(kipp),H*sin(kipp),	Unid	:Unid=Unid+1

		!!! Hotspots on bottom
		HOTSPOT (v/2*sin(kipp))*tan(90-schneid1),kipp_y,kipp_z,	Unid	:Unid=Unid+1
		HOTSPOT A+(v/2*sin(kipp))*tan(90-schneid2),kipp_y,kipp_z,	Unid	:Unid=Unid+1

		HOTSPOT -(v/2*sin(kipp))*tan(90-schneid1),-kipp_y,-kipp_z,	Unid	:Unid=Unid+1
		HOTSPOT A-(v/2*sin(kipp))*tan(90-schneid2),-kipp_y,-kipp_z,	Unid	:Unid=Unid+1

		!!! Hotspots on top
		HOTSPOT -(H*cos(kipp))*tan(90-schneid1)+kipp1_x,-H*cos(kipp)+kipp_y,H*sin(kipp)+kipp_z,	Unid	:Unid=Unid+1
		HOTSPOT A-(H*cos(kipp))*tan(90-schneid2)+kipp2_x,-H*cos(kipp)+kipp_y,H*sin(kipp)+kipp_z,	Unid	:Unid=Unid+1


		HOTSPOT -(H*cos(kipp))*tan(90-schneid1)-kipp1_x,-H*cos(kipp)-kipp_y,H*sin(kipp)-kipp_z,	Unid	:Unid=Unid+1
		HOTSPOT A-(H*cos(kipp))*tan(90-schneid2)-kipp2_x,-H*cos(kipp)-kipp_y,H*sin(kipp)-kipp_z,	Unid	:Unid=Unid+1

endif


100:
! ==============================================================================

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then cutend
	if gs_2D_projection_type <> 6 then cutend
	if AC_symb_show_projection_to <> 3 then cutend
endif

END
