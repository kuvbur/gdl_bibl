
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

parameters gs_GLOB_MODPAR_NAME = GLOB_MODPAR_NAME

! ==============================================================================
! 2D Display Options
! ==============================================================================

values "gs_symb_display_option" stDisplayOptions
values "gs_symb_show_projection_to" stProjectionTypes

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "gs_use_3D_view_attributes"
	lock "gs_use_3D_view_attributes"
endif

if gs_use_3D_view_attributes then
	hideparameter "AC_uncut_pen"
	lock "AC_uncut_pen"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 4 | AC_symb_display_option = 5 then
	hideparameter "gs_symb_show_projection_to"
	lock "gs_symb_show_projection_to"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "AC_cut_linetype", "AC_uncut_pen"
	lock "AC_cut_linetype", "AC_uncut_pen"
endif

if AC_symb_display_option = 1 | AC_symb_display_option = 3 | AC_symb_display_option = 4 then
	hideparameter "AC_overhead_linetype", "AC_overhead_pen"
	lock "AC_overhead_linetype", "AC_overhead_pen"
endif

if AC_symb_display_option = 4 then
	hideparameter "AC_cut_linetype"
	lock "AC_cut_linetype"
endif

if AC_symb_display_option = 5 then
	hideparameter "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock "gs_fill_type", "gs_fill_pen", "gs_back_pen"
endif

! ==============================================================================

values{2} "gs_detlevel_3D_m"	2, `Детальный`,
								0, `Откл.`

if gs_detlevel_3D_m = 0 then
	lock "gs_shadow"
	hideparameter "gs_shadow"
endif

values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

values{2} "iRoofConstrSymbType" 	SYMBTYPE_WITHAXIS, `с Осью`,
									SYMBTYPE_WITHOUTAXIS, `без Оси`,
									SYMBTYPE_ONLYAXIS, `только Ось`

if iRoofConstrSymbType = SYMBTYPE_WITHOUTAXIS then
	lock "gs_axis_line", "gs_axis_line_pen"
endif

if iRoofConstrSymbType = SYMBTYPE_ONLYAXIS then
	lock	"gs_cont_pen", "gs_fill_type", "gs_fill_pen", "gs_back_pen", "gs_ShowMoreHtps2D"
	hideparameter	"gs_cont_pen", "gs_fill_type", "gs_fill_pen", "gs_back_pen", "gs_ShowMoreHtps2D"

	lock	"gs_symb_display_option" ,"gs_symb_show_projection_to",
			"AC_cut_linetype", "AC_uncut_pen", "AC_overhead_linetype", "AC_overhead_pen"
	hideparameter	"gs_symb_display_option" ,"gs_symb_show_projection_to",
					"AC_cut_linetype", "AC_uncut_pen", "AC_overhead_linetype", "AC_overhead_pen"
endif

if gs_cont_pen = 0 then parameters gs_cont_pen = 2
if gs_fill_pen = 0 then parameters gs_fill_pen = 91

values "A" range[0.01,)
values "h" range(0,)
values "neig1" range[atn(H/(A/2)),90]
values "neig2" range[atn(H/(A/2)),90]


if neig1 >= 90 & neig1 < 270/2 then parameters neig1 = 90		!! 1. Roof Pitch
if neig1 >= 270/2 & neig1 <= 360 then parameters neig1 = atn(H/(A/2))
if neig1 <= atn(H/(A/2)) then parameters neig1 = atn(H/(A/2))

if neig2 >= 90 & neig1 < 270/2 then parameters neig2 = 90		!! 1. Roof Pitch
if neig2 >= 270/2 & neig2 <= 360 then parameters neig2 = atn(H/(A/2))
if neig2 <= atn(H/(A/2)) then parameters neig2 = atn(H/(A/2))

zzyzx = h
ac_toplevel		= 0
ac_bottomlevel	= -zzyzx

parameters	zzyzx			= zzyzx,
			ac_toplevel		= ac_toplevel,
			ac_bottomlevel	= ac_bottomlevel

if GLOB_MODPAR_NAME = "A"		then parameters temp_a = A / 2
if GLOB_MODPAR_NAME = "temp_a"	then parameters A = temp_a * 2

if GLOB_MODPAR_NAME ="B"	then parameters B2 = B / 2
if GLOB_MODPAR_NAME ="B2"	then parameters B = B2 * 2

values "temp_a" range[0.01/2,)


! ==============================================================================
! Lumber Size
! ==============================================================================

parameters LumberSize = A
lock "LumberSize"


! ==============================================================================
! Text Label
! ==============================================================================

MM_IN_A_DOT = 25.4 / 72

if not(gs_text_show) then
	lock	"gs_text_font_pen", "fontType",
			"gs_text_font_size_mm", "gs_text_font_size_pt",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"gs_text_pos_x", "gs_text_pos_y", "typeTextRotation"
	hideparameter	"gs_text_font_pen", "fontType",
					"gs_text_font_size_mm", "gs_text_font_size_pt",
					"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
					"gs_text_pos_x", "gs_text_pos_y", "typeTextRotation"
else
	w_units_form = ""
	rrr = request("Working_length_unit", "", w_units_form)
	bMetricDimension = strstr(w_units_form, "m")		! m, cm, mm

	values "gs_text_font_size_mm"	range(0,]
	values "gs_text_font_size_pt"	range(0,]

	if GLOB_MODPAR_NAME = "gs_text_font_size_pt" & gs_text_font_size_pt > EPS then
		parameters gs_text_font_size_mm = gs_text_font_size_pt * MM_IN_A_DOT
	else
		parameters gs_text_font_size_pt = gs_text_font_size_mm / MM_IN_A_DOT
	endif

	if bMetricDimension then
		lock "gs_text_font_size_pt"
		hideparameter "gs_text_font_size_pt"
	endif

	for i=1 to 255: put i: next i
	values "gs_text_font_pen" get(nsp)
endif

! ==============================================================================
! for GER listing
! ==============================================================================

rm_ProfileWidth		= B
rm_ProfileHeight	= h
parameters	rm_ProfileWidth		= rm_ProfileWidth,
			rm_ProfileHeight	= rm_ProfileHeight


