
! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		del 1
	endif
endif

! ==============================================================================

if not(gs_2D_projection) & gs_detlevel_3D_m = 0 then goto 100	! END

if	not(gs_shadow)	then shadow off
if		gs_shadow	then shadow on

pen gs_cont_pen
material mat1


rotx 90

xm=-h*sin(neig)
ym=h*cos(neig)

if ABS (schneid-(neig+90))>EPS then 	!!schneid<>neig+90
xm=xm+h/tan(schneid-neig)*cos(neig)
ym=ym+h/tan(schneid-neig)*sin(neig)
endif

addz -b/2

base

PRISM 9,b,
	0,0,
	gs_struc_length-brfi/2,(gs_struc_length-brfi/2)*tan(neig),
	gs_struc_length-brfi/2,(gs_struc_length)*tan(neig),
	gs_struc_length,(gs_struc_length)*tan(neig),
	gs_struc_length,(gs_struc_length)*tan(neig)+h/cos(neig),
	-(ueb+brfu/2)+xm,-(ueb+brfu/2)*tan(neig)+ym,
	-(ueb+brfu/2),-(ueb+brfu/2)*tan(neig),
	-brfu/2,-brfu/2*tan(neig),
	-brfu/2,0

vert 0, 0, 0
vert gs_struc_length,gs_struc_length*tan(neig),0
vert -gs_struc_length*tan(neig),gs_struc_length,0
vert 0, 0, 1

coor 2, -1, -2, -3, -4
body 1

del top

!!! *** 3D HOTSPOTs / Editable
Unid=1

!!! *** Roof Pitch

L1=(gs_struc_length)/2

HOTSPOT 0,0,0,unID,neig,6 : unID=unID+1
HOTSPOT L1,0,0,unID,neig,4+128 : unID=unID+1
HOTSPOT 0,-1,0,unID,neig,7 : unID=unID+1
HOTSPOT L1*COS(neig),0,L1*SIN(neig),unID,neig,5 : unID=unID+1

!!! *** Length of Span

add  0, 0, (gs_struc_length)*tan(neig)
HOTSPOT 0, 0, 0, unID, gs_struc_length, 1+128, gs_struc_length            : unID=unID+1
HOTSPOT -1, 0, 0, unID, gs_struc_length, 3, gs_struc_length                : unID=unID+1
HOTSPOT gs_struc_length, 0, 0, unID, gs_struc_length, 2, gs_struc_length : unID=unID+1
del 1

!!! *** Overhang

LOverhang = (ueb+brfu/2)/COS(neig)

roty -neig
HOTSPOT 0, 0, 0, unID, ueb2, 1+128, ueb : unID=unID+1
HOTSPOT 1, 0, 0, unID, ueb2, 3, ueb : unID=unID+1
HOTSPOT -LOverhang, 0, 0, unID, ueb2, 2, ueb : unID=unID+1
del 1

!!! *** Width of Wall Plate

HOTSPOT 0, 0, 0, unID, brfu2, 1, brfu      : unID=unID+1
HOTSPOT 1, 0, 0, unID, brfu2, 3, brfu      : unID=unID+1
HOTSPOT -brfu2, 0, 0, unID, brfu2, 2, brfu : unID=unID+1

add  gs_struc_length, 0, (gs_struc_length)*tan(neig)
HOTSPOT 0, 0, 0, unID, brfi2, 1+128, brfi  : unID=unID+1
HOTSPOT 1, 0, 0, unID, brfi2, 3, brfi      : unID=unID+1
HOTSPOT -brfi2, 0, 0, unID, brfi2, 2, brfi : unID=unID+1
del 1

!!! *** Profile Height

HOTSPOT 0, 0, 0, unID,  H, 1+128 : unID=unID+1
HOTSPOT -H*sin(neig), 0, H*cos(neig), unID, H, 2 : unID=unID+1
HOTSPOT H*sin(neig), 0, -H*cos(neig), unID,  H, 3 : unID=unID+1

!!! *** Cutting Plane Rafterend
LCut=H/SIN(schneid-neig)

ADD -(ueb+brfu/2), 0, -(ueb+brfu/2)*tan(neig)
HOTSPOT 0, 0, 0, unID, schneid, 6: unID=unID+1
HOTSPOT LCut, 0, 0, unID, schneid, 4+128: unID=unID+1
HOTSPOT LCut*COS(schneid), 0, LCut*SIN(schneid), unID, schneid, 5: unID=unID+1
DEL 1

!!! *** 3D HOTSPOTs / Not Editable

Hz=H/cos(neig)
OVr=H/sin(schneid-neig)
OVx=OVr*sin(90-schneid)
OVz=OVr*cos(90-schneid)

!!!! Hotspots on Top
Hotspot (gs_struc_length),b/2,(gs_struc_length)*tan(neig), Unid	:Unid=Unid+1
Hotspot (gs_struc_length),-b/2,(gs_struc_length)*tan(neig),Unid	:Unid=Unid+1

Hotspot (gs_struc_length),b/2,(gs_struc_length)*tan(neig)+Hz, Unid	:Unid=Unid+1
Hotspot (gs_struc_length),-b/2,(gs_struc_length)*tan(neig)+Hz,Unid	:Unid=Unid+1

!!!! Hotspots at Overhanging
Hotspot -(ueb+brfu/2),b/2,-(ueb+brfu/2)*tan(neig), Unid	:Unid=Unid+1
Hotspot -(ueb+brfu/2),-b/2,-(ueb+brfu/2)*tan(neig),Unid	:Unid=Unid+1

Hotspot -(ueb+brfu/2)+OVx,b/2,-(ueb+brfu/2)*tan(neig)+OVz, Unid	:Unid=Unid+1
Hotspot -(ueb+brfu/2)+OVx,-b/2,-(ueb+brfu/2)*tan(neig)+OVz,Unid	:Unid=Unid+1


100:
! ==============================================================================

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then cutend
	if gs_2D_projection_type <> 6 then cutend
	if AC_symb_show_projection_to <> 3 then cutend
endif

END
