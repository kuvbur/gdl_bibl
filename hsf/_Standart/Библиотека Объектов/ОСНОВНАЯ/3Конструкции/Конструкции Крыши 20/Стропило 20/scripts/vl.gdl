
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

parameters gs_GLOB_MODPAR_NAME = GLOB_MODPAR_NAME

! ==============================================================================
! 2D Display Options
! ==============================================================================

values "gs_symb_display_option" stDisplayOptions
values "gs_symb_show_projection_to" stProjectionTypes

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "gs_use_3D_view_attributes"
	lock "gs_use_3D_view_attributes"
endif

if gs_use_3D_view_attributes then
	hideparameter "AC_uncut_pen"
	lock "AC_uncut_pen"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 4 | AC_symb_display_option = 5 then
	hideparameter "gs_symb_show_projection_to"
	lock "gs_symb_show_projection_to"
endif

if AC_symb_display_option = 3 | AC_symb_display_option = 5 then
	hideparameter "AC_cut_linetype", "AC_uncut_pen"
	lock "AC_cut_linetype", "AC_uncut_pen"
endif

if AC_symb_display_option = 1 | AC_symb_display_option = 3 | AC_symb_display_option = 4 then
	hideparameter "AC_overhead_linetype", "AC_overhead_pen"
	lock "AC_overhead_linetype", "AC_overhead_pen"
endif

if AC_symb_display_option = 4 then
	hideparameter "AC_cut_linetype"
	lock "AC_cut_linetype"
endif

if AC_symb_display_option = 5 then
	hideparameter "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock "gs_fill_type", "gs_fill_pen", "gs_back_pen"
endif

! ==============================================================================

values{2} "gs_detlevel_3D_m"	2, `Детальный`,
								0, `Откл.`


if gs_detlevel_3D_m = 0 then
	lock "gs_shadow"
	hideparameter "gs_shadow"
endif

values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

values{2} "iRoofConstrSymbType" 	SYMBTYPE_WITHAXIS, `с Осью`,
									SYMBTYPE_WITHOUTAXIS, `без Оси`,
									SYMBTYPE_ONLYAXIS, `только Ось`

if iRoofConstrSymbType = SYMBTYPE_WITHOUTAXIS then
	lock "gs_axis_line", "gs_axis_line_pen"
endif

if iRoofConstrSymbType = SYMBTYPE_ONLYAXIS then
	lock	"gs_cont_pen", "gs_fill_type", "gs_fill_pen", "gs_back_pen", "gs_ShowMoreHtps2D"
	hideparameter	"gs_cont_pen", "gs_fill_type", "gs_fill_pen", "gs_back_pen", "gs_ShowMoreHtps2D"

	lock	"gs_symb_display_option" ,"gs_symb_show_projection_to",
			"AC_cut_linetype", "AC_uncut_pen", "AC_overhead_linetype", "AC_overhead_pen"
	hideparameter	"gs_symb_display_option" ,"gs_symb_show_projection_to",
					"AC_cut_linetype", "AC_uncut_pen", "AC_overhead_linetype", "AC_overhead_pen"
endif


values "h"			  range(0,)
values "neig"		  range(0,90)
values "schneid"	  range(neig, 180+neig)
values "brfu"		  range(0,h/sin(neig)*2)
values "brfi"		  range(0,h/sin(neig)*2)
values "brfu2"		  range(0,h/sin(neig))
values "brfi2"		  range(0,h/sin(neig))
values "ueb"		  range[0,)
values "edgeOverhang" range[0,)
values "gs_struc_length" range [0,)

IF gs_cont_pen= 0 then parameters gs_cont_pen=2
IF gs_fill_pen=0 then parameters gs_fill_pen=91

cutedge = TAN(neig) * h
cutLenght	= cutedge * COS(neig)

parameters A=gs_struc_length+edgeOverhang+brfu/2 + cutLenght
lock "A"
parameters LumberSize=A/cos(neig)
lock "LumberSize"
parameters purlinsOutDist=gs_struc_length-brfi/2+brfu/2
lock "purlinsOutDist"

if GLOB_MODPAR_NAME = "schneid" or GLOB_MODPAR_NAME = "ueb" then
	edgeOverhang = ueb + h*cos(180-schneid)
	parameters edgeOverhang =edgeOverhang
endif

if GLOB_MODPAR_NAME = "edgeOverhang" then
	schneid = abs(acs((edgeOverhang-ueb)/h)-180)
	parameters schneid = schneid
endif

if GLOB_MODPAR_NAME = "brfu"	then parameters brfu2=brfu/2
if GLOB_MODPAR_NAME = "brfu2"	then parameters brfu=brfu2*2

if GLOB_MODPAR_NAME = "brfi"	then parameters brfi2=brfi/2
if GLOB_MODPAR_NAME = "brfi2"	then parameters brfi=brfi2*2

IF GLOB_MODPAR_NAME = "ueb"		then parameters ueb2=ueb/COS(neig)
IF GLOB_MODPAR_NAME = "ueb2"	then parameters ueb=ueb2*COS(neig)

if GLOB_MODPAR_NAME = "B"		then parameters B2=B/2
if GLOB_MODPAR_NAME = "B2"		then parameters B=B2*2


! ==============================================================================
! Text Label
! ==============================================================================

MM_IN_A_DOT = 25.4 / 72

if not(gs_text_show) then
	lock	"gs_text_font_pen", "fontType",
			"gs_text_font_size_mm", "gs_text_font_size_pt",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"gs_text_pos_x", "gs_text_pos_y", "typeTextRotation"
	hideparameter	"gs_text_font_pen", "fontType",
					"gs_text_font_size_mm", "gs_text_font_size_pt",
					"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
					"gs_text_pos_x", "gs_text_pos_y", "typeTextRotation"
else
	w_units_form = ""
	rrr = request("Working_length_unit", "", w_units_form)
	bMetricDimension = strstr(w_units_form, "m")		! m, cm, mm

	values "gs_text_font_size_mm"	range(0,]
	values "gs_text_font_size_pt"	range(0,]

	if GLOB_MODPAR_NAME = "gs_text_font_size_pt" & gs_text_font_size_pt > EPS then
		parameters gs_text_font_size_mm = gs_text_font_size_pt * MM_IN_A_DOT
	else
		parameters gs_text_font_size_pt = gs_text_font_size_mm / MM_IN_A_DOT
	endif

	if bMetricDimension then
		lock "gs_text_font_size_pt"
		hideparameter "gs_text_font_size_pt"
	endif

	for i=1 to 255: put i: next i
	values "gs_text_font_pen" get(nsp)
endif

! ==============================================================================
! for GER listing
! ==============================================================================

rm_ProfileWidth		= B
rm_ProfileHeight	= h
parameters	rm_ProfileWidth		= rm_ProfileWidth,
			rm_ProfileHeight	= rm_ProfileHeight


