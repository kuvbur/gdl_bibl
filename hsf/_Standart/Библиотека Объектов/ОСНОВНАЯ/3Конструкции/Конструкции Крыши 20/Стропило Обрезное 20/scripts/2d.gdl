

PAPER_TO_MODEL = GLOB_SCALE / 1000	! Convert paper size to model size

unID		= 1
hLineID		= 2000
textUnID	= 1000

! ==============================================================================
! 2D Degradation
! ==============================================================================

if GLOB_CONTEXT < 20 then
	bShowText	= 1
	bShowElem	= 1
	bShowAxis	= 1
else
	GMN = gs_GLOB_MODPAR_NAME
	bShowText	= (GMN = "gs_text_pos_x" | GMN = "gs_text_pos_y")
	bShowElem	= not(bShowText)
	bShowAxis	= not(bShowText)
endif

! --- Oriented View ------------------------------ [

angleViewRot = 0
if gs_text_show and bShowText then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]

! ==============================================================================
! Hotspots
! ==============================================================================

hotspot2 0,0, unID: unID=unID+1		! Placing Hotspot

hotspot2  0, 0, unID,	A, 1+256	! Axis length
hotspot2 -1, 0, unID+1,	A, 3
hotspot2  A, 0, unID+2,	A, 2
unID = unID + 3

hotspot2 A/2, 0, unID: unID=unID+1	! Center

! ==============================================================================
! Symbolic 2D
! ==============================================================================

pen gs_cont_pen
fill gs_fill_type

if bShowElem & AC_symb_display_option = 3 & iRoofConstrSymbType <> SYMBTYPE_ONLYAXIS then

! --- Cut Plane = Vertical -------------------------------------------------------

	if ssch1_m = CUTTING_PLANE_VERT then
		xm1 = -h * sin(neig)
		if abs(schneid1 - (neig + 90)) > EPS then	! schneid1 <> (neig + 90)
			xm1 = xm1 + h / tan(schneid1 - neig) * cos(neig)
		endif

		if schneid1 < 90 then
			line2	 xm1,-B/2,xm1,B/2
			hotspot2 xm1,-0, unID: unID=unID+1
		endif

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			min(0,xm1),	-B/2, 1,
			min(0,xm1),	 B/2, 1,
			A/2,		 B/2, 0,
			A/2,		-B/2, 1
	endif

	if ssch2_m = CUTTING_PLANE_VERT then
		xm2 = -h * sin(neig)
		if abs(schneid2 - (neig + 90)) > EPS then		! schneid <> neig + 90
			xm2 = xm2 + h / tan(schneid2 - neig) * cos(neig)
		endif

		if schneid2 > 90 then
			line2	 A+xm2,-B/2, A+xm2,B/2
			hotspot2 A+xm2,0, unID: unID=unID+1
		endif

		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			max(A,A+xm2),	-B/2, 1,
			max(A,A+xm2),	 B/2, 1,
			A/2,			 B/2, 0,
			A/2,			-B/2, 1
	endif

! --- Cut Plane = Horizontal -------------------------------------------------------

	if ssch1_m = CUTTING_PLANE_HOR or  ssch1_m = CUTTING_PLANE_CENTRAL then

		M1x = (B/2/tan(schneid1))
		L1XY = M1x/cos(schneid1)

		rot2 180
		hotspot2 0,						0,						unID,	schneid1, 6
		hotspot2 L1XY,					0,						unID+1,	schneid1, 4+128
		hotspot2 cos(schneid1) * L1XY,	sin(schneid1) * L1XY,	unID+2,	schneid1, 5
		unID = unID + 3
		del 1

		if ssch1_m = CUTTING_PLANE_HOR then
			poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-M1x,	-B/2, 1,
				 M1x,	 B/2, 1,
				 A/2,	 B/2, 0,
				 A/2,	-B/2, 1,
				-M1x,	-B/2,-1

			hotspot2 0,						0,						unID,	schneid1, 6
			hotspot2 L1XY,					0,						unID+1,	schneid1, 4+128
			hotspot2 cos(schneid1) * L1XY,	sin(schneid1) * L1XY,	unID+2,	schneid1, 5
			unID = unID + 3
		else
			poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-M1x,	-B/2, 1,
				 0,		 0,   1,
				-M1x,	 B/2, 1,
				 A/2,	 B/2, 0,
				 A/2,	-B/2, 1,
				-M1x,	-B/2,-1

			hotspot2 0,							0,							unID,	peak_schneid1, 6, schneid1
			hotspot2 L1XY,						0,							unID+1,	peak_schneid1, 4+128, schneid1
			hotspot2 cos(peak_schneid1) * L1XY,	sin(peak_schneid1) * L1XY,	unID+2,	peak_schneid1, 5, schneid1
			unID = unID + 3
		endif
	endif

	if ssch2_m = CUTTING_PLANE_HOR or ssch2_m = CUTTING_PLANE_CENTRAL then

		M2x = (B/2/tan(schneid2))
		L2XY = M2x/cos(schneid2)

		add2 A,0
		rot2 180
		hotspot2 0,						0,						unID,	schneid2, 6
		hotspot2 L2XY,					0,						unID+1,	schneid2, 4+128
		hotspot2 cos(schneid2) * L2XY,	sin(schneid2) * L2XY,	unID+2,	schneid2, 5
		unID = unID + 3
		del 2

		fill gs_fill_type
		if ssch2_m = CUTTING_PLANE_HOR then
			poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				A-M2x,	-B/2, 1,
				A+M2x,	 B/2, 1,
				A/2,	 B/2, 0,
				A/2,	-B/2, 1,
				A-M2x,	-B/2,-1

			add2 A,0
			hotspot2 0,						0,						unID,	schneid2, 6
			hotspot2 L2XY,					0,						unID+1,	schneid2, 4+128
			hotspot2 cos(schneid2) * L2XY,	sin(schneid2) * L2XY,	unID+2,	schneid2, 5
			unID = unID + 3
			del 1
		else
			poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
				A-M2x,	-B/2,	1,
				A,		 0,		1,
				A-M2x,	 B/2,	1,
				A/2,	 B/2,	0,
				A/2,	-B/2,	1,
				A-M2x,	-B/2,	-1

			add2 A,0
			hotspot2 0,							0,							unID,	peak_schneid2, 6, schneid2
			hotspot2 L2XY,						0,							unID+1,	peak_schneid2, 4+128, schneid2
			hotspot2 cos(peak_schneid2) * L2XY,	sin(peak_schneid2) * L2XY,	unID+2,	peak_schneid2, 5, schneid2
			unID = unID + 3
			del 1
		endif
	endif
endif


! ==============================================================================
! More Hotspots
! ==============================================================================

if gs_ShowMoreHtps2D & iRoofConstrSymbType <> SYMBTYPE_ONLYAXIS then
	if ssch1_m = CUTTING_PLANE_VERT then
		! --- Profile Thickness ---

		hotspot2 min(0,xm1), 0,		unID,	B2, 1+128, B
		hotspot2 min(0,xm1),-B2,	unID+1,	B2, 2, B
		hotspot2 min(0,xm1), 1,		unID+2,	B2, 3, B
		unID = unID + 3

		hotspot2 min(0,xm1),  0,	unID,	B2, 1+128, B
		hotspot2 min(0,xm1),  B2,	unID+1,	B2, 2, B
		hotspot2 min(0,xm1), -1,	unID+2,	B2, 3, B
		unID = unID + 3

!		hotspot2 min(0,xm1),-B/2, unID: unID=unID+1
!		hotspot2 min(0,xm1), B/2, unID: unID=unID+1
	endif
	if ssch2_m = CUTTING_PLANE_VERT then
		! --- Profile Thickness ---

		hotspot2 max(A,A+xm2), 0,	unID,	B2, 1+128, B
		hotspot2 max(A,A+xm2),-B2,	unID+1,	B2, 2, B
		hotspot2 max(A,A+xm2), 1,	unID+2,	B2, 3, B
		unID = unID + 3

		hotspot2 max(A,A+xm2),  0,	unID,	B2, 1+128, B
		hotspot2 max(A,A+xm2),  B2,	unID+1,	B2, 2, B
		hotspot2 max(A,A+xm2), -1,	unID+2,	B2, 3, B
		unID = unID + 3

!		hotspot2 max(A,A+xm2),-B/2,	unID: unID=unID+1
!		hotspot2 max(A,A+xm2), B/2,	unID: unID=unID+1
	endif
endif


! ==============================================================================
! 3D based floor plan symbol
! ==============================================================================

if bShowElem & AC_symb_display_option <> 3 & iRoofConstrSymbType <> SYMBTYPE_ONLYAXIS then

	gs_cutplane_height	= GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1]
	gs_top_range_height	= GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[2]
	if AC_symb_show_projection_to = 1 then
		gs_bottom_range_height = GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[3]	! To Floor Plan Range
	else
		gs_bottom_range_height = GLOB_CUTPLANES_INFO[4]	! Absolute Display Limit
	endif

	doCutplane = (AC_symb_display_option = 1 | AC_symb_display_option = 2)


! View Edges -------------------------------------------------------------------

	pen AC_uncut_pen
	line_type 1

	if AC_symb_display_option = 5 then
		pen AC_overhead_pen
		line_type AC_overhead_linetype
	endif

	project2{3} 3,270,2 + 32 * (AC_symb_display_option = 5 | not(gs_use_3D_view_attributes)), 8,
		parameters	gs_2D_projection		= doCutplane,
					gs_2D_projection_type	= 1,
					gs_cutplane_height		= gs_cutplane_height,
					gs_top_range_height		= gs_top_range_height,
					gs_bottom_range_height	= gs_bottom_range_height


	if AC_symb_display_option <> 5 then

! View Polygons ----------------------------------------------------------------

		pen gs_fill_pen
		fill gs_fill_type

		project2{3} 3,270,3+32+1024+2048, 4,
						gs_back_pen,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 2,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif


	if AC_symb_display_option = 2 then

! Overhead View ----------------------------------------------------------------

		pen AC_overhead_pen
		line_type AC_overhead_linetype

		project2{3} 3,270,2+32, 2+8,
			parameters	gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 3,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif

	if doCutplane then

! Cut Lines --------------------------------------------------------------------

		pen SYMB_SECT_PEN
		line_type AC_cut_linetype
		line_property 2

		project2{3} 3,270,2+16+32+256+4096, 2,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 4,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height


! Cut Polygons -----------------------------------------------------------------

		pen SYMB_FILL_PEN
		fill SYMB_FILL

		project2{3} 3,270,3+32+512, 1,
						SYMB_FBGD_PEN,
			parameters	gs_2D_projection		= 1,
						gs_2D_projection_type	= 5,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif
endif


! ==============================================================================
! Axis line
! ==============================================================================

if bShowAxis & iRoofConstrSymbType <> SYMBTYPE_WITHOUTAXIS then
	line_type	gs_axis_line
	pen			gs_axis_line_pen

	line2	 A,0, 0,0
	hotline2 A,0, 0,0, hLineID : hLineID = hLineID + 1

	line_type	1
	pen			gs_cont_pen
else
	hLineID = hLineID + 1
endif


! ==============================================================================
! Text
! ==============================================================================

if gs_text_show then
	unID = textUnID
	nTrans = 0

	add2 A/2,0
	nTrans = nTrans + 1

	hotspot2 gs_text_pos_x,	 0,				UnID,	gs_text_pos_y, 1+128
	hotspot2 gs_text_pos_x,	-1,				UnID+1,	gs_text_pos_y, 3
	hotspot2 gs_text_pos_x,	 gs_text_pos_y,	UnID+2,	gs_text_pos_y, 2
	unID = unID + 3

	hotspot2  0,			gs_text_pos_y,	UnID,	gs_text_pos_x, 1+128
	hotspot2 -1,			gs_text_pos_y,	UnID+1,	gs_text_pos_x, 3
	hotspot2  gs_text_pos_x,gs_text_pos_y,	UnID+2,	gs_text_pos_x, 2
	unID = unID + 3


	if bShowText then

		define style textStyle	fontType, gs_text_font_size_mm, 5,
								gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
		define style{2} fracStyle  fontType, gs_text_font_size_mm, 32+(gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline)	! with Superscript

		style textStyle
		pen gs_text_font_pen

		nnn = REQUEST ("Linear_dimension", "", format_string)
		txt = str{2}(format_string, LumberSize, frac)

		if frac <> "" then
			PARAGRAPH "paragraph" 1, 0, 0, 0, 1, 0
				pen gs_text_font_pen
				set style textStyle
				""+ txt
				pen gs_text_font_pen
				set style fracStyle
				""+ frac
			ENDPARAGRAPH
				TEXTBLOCK "textblock" 0, 5, 0, 1 ,1, PAPER_TO_MODEL,"paragraph"
		endif

		add2 gs_text_pos_x, gs_text_pos_y
		nTrans = nTrans + 1

		! Text Rotation - Readable / Always Horizontal / Align with Symbol
		! ----------------------------------------------------------------------
		if typeTextRotation_m = 2 then								! Readable
			rot2 180 * (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps))	! 1/0
			nTrans = nTrans + 1
		else
			if typeTextRotation_m = 1 then							! Horizontal
				rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED))	! 1/-1
				nTrans = nTrans + 1
			endif
		endif
		! ----------------------------------------------------------------------

		if GLOB_CONTEXT = 22 then
			if frac <> "" then
				n = REQUEST ("TEXTBLOCK_INFO","textblock",stringWidth,height)
			else
				stringWidth = stw(txt)
			endif
			stringWidth = stringWidth * PAPER_TO_MODEL
			dummy = request("Height_of_style", textStyle, hos, dsc, ldg)	! height of style, descent, leading

			rect2 -stringWidth/2, (dsc - hos/2) * PAPER_TO_MODEL,
				   stringWidth/2, (hos/2 - ldg) * PAPER_TO_MODEL
		else
			if 	frac <> "" then
				richtext2 0,0, "textblock"
			else
				text2 0,0, txt
			endif
		endif
	endif
	del nTrans
endif
