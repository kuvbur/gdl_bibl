
dim stCuttingPlane[3]
	stCuttingPlane[1] = `Вертикальный`
	stCuttingPlane[2] = `Горизонтальный`
	stCuttingPlane[3] = `Центральный Скос`

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT = -1
_idxTab = 1
TABID_DIMENSIONS = _idxTab : _idxTab = _idxTab + 1
TABID_REPRES = _idxTab : _idxTab = _idxTab + 1
TABID_TEXT = _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS = _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Профиля`
ui_current_page gs_ui_current_page

ui_page TABID_DIMENSIONS, TABID_ROOT, `Размеры`, "uiTab_Dimension_flat(1)"
gosub "DimensionsPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_TEXT, TABID_ROOT, `Текст`, "uiTab_Text_flat(1)"
gosub "TextPage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

"DimensionsPage":
	x1 = 0
	x2 = 120
	x3 = 210
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 360
	x7 = 408
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	
	ui_style 0, 0
	
	ui_outfield	`Плоскость Сечения 1` ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_infield{2} "ssch1", 	 x2,   yCurrent, 	x3 - x2, infieldHeight,
				8, "", 3, 1,
				75, 15, 75, 15,
				1, stCuttingPlane[1],
				2, stCuttingPlane[2],
				3, stCuttingPlane[3]

	ui_outfield	`Наклон Ската` ,x4,    yCurrent+4, 	x5 - x4,	outfieldHeight	
	ui_infield "neig", 	 x6,   yCurrent, 	xEnd - x6, infieldHeight

	yCurrent = yCurrent + dy

	ui_outfield	`Плоскость Сечения 2` ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
	ui_infield{2} "ssch2", 	 x2,   yCurrent, 	x3 - x2, infieldHeight,
				8, "", 3, 1,
				75, 15, 75, 15,
				1, stCuttingPlane[1],
				2, stCuttingPlane[2],
				3, stCuttingPlane[3]

	yCurrent = yCurrent + dy


	ui_separator 0, yCurrent, xEnd, yCurrent

	pageStartY = 5

	xPict = 150			! start Position X Coordinate
	y1 = yCurrent + 20	! start Position Y Coordinate
	Gap = 2
	PictX1 = 60
	PictX2 = 92
	PictX = 152
	PictY1 = 110
	PictY = 150
	CellX = 70		! width of cell (numerical infield box)
	CellY = 18		! height of 1.cell (numerical infield box)
	xText = 30		! start Position of outfield text X Coordinate
	TextX = 180		! width of outfield box
	TextCellX = 120 ! width of Text infield box
	
	i = 1		! ssch1_m = CUTTING_PLANE_VERT
	ii = 4		! ssch2_m = CUTTING_PLANE_VERT
	if ssch1_m = CUTTING_PLANE_HOR then i = 2
	if ssch1_m = CUTTING_PLANE_CENTRAL then i = 3
	if ssch2_m = CUTTING_PLANE_HOR then ii = 5
	if ssch2_m = CUTTING_PLANE_CENTRAL then ii = 6
	
	ui_pict	i,	xPict,	        y1+PictY-PictY1,	PictX1,	PictY1, 1
	ui_pict	ii,	xPict+PictX1,	y1,	PictX2,	PictY,  1
	
	ui_infield	"h",	xPict+PictX+Gap*2,	y1-5, CellX,	CellY ui_tooltip `Высота Профиля`
	ui_infield	"b",	xPict+PictX+Gap*2,	y1+CellY-1, CellX-18,	CellY ui_tooltip `Толщина Профиля`
	if ssch1_m = CUTTING_PLANE_VERT then
		ui_infield	"schneid1",	xPict-CellX-Gap*2,	y1+100, CellX,	CellY ui_tooltip `Угол Подрезки 1`
	else
		ui_infield	"schneid1",	xPict-CellX-Gap*2,	y1+PictY1+3*Gap, CellX,	CellY ui_tooltip `Угол Подрезки 1`
	endif
	if ssch2_m = CUTTING_PLANE_VERT then
		ui_infield	"schneid2",	xPict+PictX+Gap*2,	y1+CellY*2+3, CellX,	CellY ui_tooltip `Угол Подрезки 2`
	else
		ui_infield	"schneid2",	xPict+PictX+Gap*2,	y1+PictY1+3*Gap, CellX,	CellY ui_tooltip `Угол Подрезки 2`
	endif
	
	ui_infield	"gs_struc_length",	xPict+PictX+Gap*2,	y1+CellY*3+7, CellX,	CellY ui_tooltip `Длина Элемента`

	ui_infield	"a",	xPict+35,	y1+PictY+Gap, CellX-18,	CellY ui_tooltip `Длина по Горизонтали`
	ui_outfield	str("%.0", LumberSize*1000),	xPict+PictX1-CellX/2-Gap,	y1+(PictY-PictY1)-CellY-1, CellX/2,	CellY ui_tooltip `Размер Пиломатериала`

return


"AppearancePage":
	dim st2DParamNames[][]
		st2DParamNames[1][1] = "iRoofConstrSymbType" 		: st2DParamNames[1][2] = "ui_list_detailLevel2D_flat(1)"
		st2DParamNames[2][1] = "gs_axis_line" 				: st2DParamNames[2][2] = "ui_list_axisLineType_flat(1)"
		st2DParamNames[3][1] = "gs_axis_line_pen" 			: st2DParamNames[3][2] = "ui_list_axisLinePen_flat(1)"
		st2DParamNames[4][1] = "gs_ShowMoreHtps2D" 			: st2DParamNames[4][2] = "ui_list_extraHS_flat(1)"
		st2DParamNames[5][1] = "gs_cont_pen" 				: st2DParamNames[5][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[6][1] = "gs_symb_display_option" 	: st2DParamNames[6][2] = "ui_list_detailLevel2D_flat(1)"
		st2DParamNames[7][1] = "gs_use_3D_view_attributes" 	: st2DParamNames[7][2] = ""
		st2DParamNames[8][1] = "gs_symb_show_projection_to" : st2DParamNames[8][2] = ""
		st2DParamNames[9][1] = "gs_fill_type" 				: st2DParamNames[9][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[10][1] = "gs_fill_pen" 				: st2DParamNames[10][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[11][1] = "gs_back_pen" 				: st2DParamNames[11][2] = "ui_list_fillBgPen_flat(1)"
		st2DParamNames[12][1] = "AC_cut_linetype"		 	: st2DParamNames[12][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[13][1] = "AC_uncut_pen" 				: st2DParamNames[13][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[14][1] = "AC_overhead_linetype" 		: st2DParamNames[14][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[15][1] = "AC_overhead_pen" 			: st2DParamNames[15][2] = "ui_list_contourPen_flat(1)"




	dim st3DParamNames[2][2]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_shadow"		: st3DParamNames[2][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "mat1"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames


	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	file_dependence "ui_list_axis2D_flat(1)"
	file_dependence "ui_list_axisLineType_flat(1)"
	file_dependence "ui_list_axisLinePen_flat(1)"
	file_dependence "ui_list_extraHS_flat(1)"

return

"TextPage":
	x1 = 0
	x2 = 171
	x3 = 261
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	penInfieldHeight 	= 25
	penInfieldLenght 	= 33
	fillTypeInfLenght 	= 200
	fillTypeInfHeight	= 25
	
	pageStartY			= 0
	dy 					= 24
	yEnd 				= 266
	yCurrent 			= pageStartY + 1

! ------------------------------------------------------------------------------

	call "ui_structure_SymbolText" parameters all	pageStartY		= pageStartY

return

"descriptionPage":

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
