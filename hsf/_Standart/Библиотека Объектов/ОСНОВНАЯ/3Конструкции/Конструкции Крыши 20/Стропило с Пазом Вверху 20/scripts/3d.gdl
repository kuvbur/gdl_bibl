
! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		del 1
	endif
endif

! ==============================================================================

if not(gs_2D_projection) & gs_detlevel_3D_m = 0 then goto 100	! END

if	not(gs_shadow)	then shadow off
if		gs_shadow	then shadow on

pen gs_cont_pen
material mat1


ADDx -gs_struc_length
if ABS(schneid1-90)<EPS then		!!schneid1=90
	um=0
else
	um=abs(b/tan(schneid1)/2)
endif

rotz schneid1
cutplane -90
del 1


rotx 90
addz -b/2
addy -gs_struc_length*tan(neig)

base

PRISM 6,b,
	-um,-um*tan(neig),
	gs_struc_length-brfi/2,(gs_struc_length-brfi/2)*tan(neig),
	gs_struc_length-brfi/2,gs_struc_length*tan(neig),
	gs_struc_length,gs_struc_length*tan(neig),
	gs_struc_length,gs_struc_length*tan(neig)+h/cos(neig),
	-um,-um*tan(neig)+h/cos(neig)


vert 0, 0, 0

vert A,A*tan(neig),0
vert -A*tan(neig),A,0
vert 0, 0, 1

coor 2, -1, -2, -3, -4
body 1

cutend
DEL TOP


!!! *** 3D HOTSPOTs / Editable
Unid=1

!!! *** Roof Pitch

L1=(gs_struc_length)/2
ROTy 180
HOTSPOT 0,0,0,unID,neig,6 : unID=unID+1
HOTSPOT L1,0,0,unID,neig,4+128 : unID=unID+1
HOTSPOT 0,-1,0,unID,neig,7 : unID=unID+1
HOTSPOT L1*COS(neig),0,L1*SIN(neig),unID,neig,5 : unID=unID+1
DEL 1

!!! *** Length

!ADDz h/cos(neig)
!ROTy -neig
add  0, 0, -(gs_struc_length)*tan(neig)
HOTSPOT 0, 0, 0, unID, gs_struc_length, 1+128, gs_struc_length: unID=unID+1
HOTSPOT 1, 0, 0, unID, gs_struc_length, 3, gs_struc_length: unID=unID+1
HOTSPOT -gs_struc_length, 0, 0, unID, gs_struc_length, 2, gs_struc_length: unID=unID+1
del 1

!!! *** Height

HOTSPOT 0,0,0,unID, ZZYZX,1+256 : unID=unID+1
HOTSPOT 0,0,1,unID, ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,-ZZYZX,unID, ZZYZX,2+128 : unID=unID+1

ADDx -A
HOTSPOT 0,0,0,unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT 0,0,1,unID, ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,-ZZYZX,unID, ZZYZX,2 : unID=unID+1
DEL 1

!! *** Width of Ridge Purlin

HOTSPOT 0, 0, 0, unID, brfi2, 1+128, brfi : unID=unID+1
HOTSPOT 1, 0, 0, unID, brfi2, 3, brfi : unID=unID+1
HOTSPOT -brfi2, 0, 0, unID, brfi2, 2, brfi : unID=unID+1

!!! *** Profile Height

HOTSPOT 0, 0, 0, unID,  H, 1+128 : unID=unID+1
HOTSPOT -H*sin(neig), 0, H*cos(neig), unID, H, 2 : unID=unID+1
HOTSPOT H*sin(neig), 0, -H*cos(neig), unID,  H, 3 : unID=unID+1

!!! *** 3D HOTSPOTs / Not Editable

!!!!! Hotspots in 3D
Hz=H/cos(neig)

ADDx -gs_struc_length

!!! Hotspots on Top
Hotspot gs_struc_length,b/2,0, Unid	:Unid=Unid+1
Hotspot gs_struc_length,-b/2,0,Unid	:Unid=Unid+1

Hotspot gs_struc_length,b/2,Hz, Unid	:Unid=Unid+1
Hotspot gs_struc_length,-b/2,Hz,Unid	:Unid=Unid+1

!!!!! Hotspots on bottom
Ridge_x=B/2/tan(schneid1)
Ridge_z=Ridge_x*tan(neig)
Hotspot Ridge_x,B/2,-gs_struc_length*tan(neig)+Ridge_z, Unid	:Unid=Unid+1	!! on axis
Hotspot Ridge_x,B/2,-gs_struc_length*tan(neig)+Hz+Ridge_z, Unid	:Unid=Unid+1	!! on axis

Hotspot -Ridge_x,-B/2,-gs_struc_length*tan(neig)-Ridge_z, Unid	:Unid=Unid+1	!! on axis
Hotspot -Ridge_x,-B/2,-gs_struc_length*tan(neig)+Hz-Ridge_z, Unid	:Unid=Unid+1	!! on axis
DEL top


100:
! ==============================================================================

if gs_2D_projection then
	if AC_symb_show_projection_to = 1 | (AC_symb_show_projection_to = 2 & gs_bottom_range_height < gs_cutplane_height) then cutend
	if gs_2D_projection_type <> 6 then cutend
	if AC_symb_show_projection_to <> 3 then cutend
endif

END
