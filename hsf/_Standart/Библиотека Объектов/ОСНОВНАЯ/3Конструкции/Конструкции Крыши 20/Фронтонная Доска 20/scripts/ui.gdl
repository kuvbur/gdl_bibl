
TABID_ROOT			= -1
TABID_STYLE			= 1
TABID_DIMENSIONS	= 2
TABID_SURFACES		= 3
TABID_DESCR			= 4


UI_DIALOG `Параметры Фронтонной Доски`
UI_CURRENT_PAGE gs_ui_current_page

gosub "style_page"
gosub "dimensions_page"
gosub "surfaces_page"
gosub "description_page"

!================================================================================
	end
!================================================================================


!================================================================================
"style_page":
!================================================================================
	pageStartY		= 0
	dy				= 24
	inHeight		= 19
	outHeight		= 15

	posY			= inHeight + 1

	ui_page TABID_STYLE, TABID_ROOT, `Стиль`, "uiTab_Style_flat(1)"

	ui_infield{3} "bIsraft", 0, posY - inHeight, 210, inHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Верхняя Доска`, 0,
					"", `Верхняя Доска`, 1

	posY = pageStartY + dy
	ui_infield{3} type, 0, posY, 444, 265 - posY,
					1, 1, 3, 1,			! 1 	Bargeboard_OB_Style_MAT.png
					105, 125, 90, 90,
					1,	"",	CUT_VERT,
					3,	"",	CUT_ANGLE,
					2,	"",	CUT_HOR \
					ui_tooltip `Тип Подрезки`
return


!================================================================================
"dimensions_page":
!================================================================================
	pageStartY		= 0
	dy				= 24
	inHeight		= 19
	inWidth			= 90
	inWidthSmall	= 60
	outHeight		= 15
	picMargin		= 4
	picX			= 129
	picW			= 188
	picY			= 71
	picH			= 143
	picLeft			= picX - picMargin
	picRight		= picX + picW + picMargin
	picTop			= picY - picMargin
	picBottom		= picY + picH + picMargin + inHeight


	posY			= inHeight + 1

	ui_page TABID_DIMENSIONS, TABID_ROOT, `Размеры`, "uiTab_Dimension_flat(1)"

	ui_outfield `Количество Досок`, 0, posY - outHeight, 155, outHeight
	ui_infield "nBoard", 158, posY - inHeight, inWidthSmall, inHeight					ui_tooltip `Количество Досок`

	ui_outfield `Угол Крыши`, 231, posY - outHeight, 155, outHeight
	ui_infield "rAng", 385, posY - inHeight, inWidthSmall, inHeight						ui_tooltip `Угол Крыши`

	ui_separator 0, dy, 444, dy

	picID = 1 + not(bIsraft) * 3 + type		!2 	Bargeboard_OB_Dimensions_PIC1.png
											!7 	Bargeboard_OB_Dimensions_PIC6.png
	ui_pict picID, picX, picY, picW, picH, 1

	posY = picY + 58
	ui_infield "length", picLeft - inWidth, posY - inHeight, inWidth, inHeight			ui_tooltip `Длина по Диагонали`

	if bIsraft then
		posY = picY + 96
		ui_infield "isrOH", picLeft - inWidth, posY - inHeight, inWidth, inHeight		ui_tooltip `Свес Верхней Доски`
		posY = picTop
		ui_infield "isrW", picX + 62, posY - inHeight, inWidth, inHeight				ui_tooltip `Ширина Верхней Доски`
		ui_infield "isrTh", picX + 62 + inWidth + 4, posY - inHeight, inWidth, inHeight	ui_tooltip `Толщина Верхней Доски`
	endif

	posY = picY + dy
	ui_infield "overlap", picRight, posY - inHeight, inWidth, inHeight					ui_tooltip `Смещение`
	posY = posY + dy
	ui_infield "boardW", picRight, posY - inHeight, inWidth, inHeight					ui_tooltip `Ширина Доски`
	posY = posY + dy
	ui_infield "boardTh", picRight, posY - inHeight, inWidth, inHeight					ui_tooltip `Толщина Доски`

	posY = picBottom
	if type = CUT_HOR then
		ui_infield "cutH", picX + 10, posY - inHeight, inWidth, inHeight				ui_tooltip `Размер Горизонтальной Подрезки`
		posY = picY + 140
		ui_infield "cutV", picLeft - inWidth, posY - inHeight, inWidth, inHeight		ui_tooltip `Размер Вертикальной Подрезки`
	endif
	if type = CUT_ANGLE then
		ui_infield "cutback", picX - 39, posY - inHeight, inWidth, inHeight				ui_tooltip `Отступ`
		ui_infield "cutAng", picX - 39 + inWidth + 4, posY - inHeight, inWidthSmall, inHeight	ui_tooltip `Угол Подрезки`
	endif


return


!================================================================================
"surfaces_page":
!================================================================================
	pageStartY				= 0

	ui_page TABID_SURFACES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_3DRepr_flat(1)"

	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

	paramNameReprArray 	[_idx] = "gs_2D_representation"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""
	_idx = _idx + 1

	paramNameReprArray 	[_idx] = "gs_cont_pen"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_fill_type"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_fill_pen"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_back_pen"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
	_idx = _idx + 1
	if bIsraft then
		paramNameReprArray 	[_idx] = "hiddenline"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contLineType_flat(1)"
		_idx = _idx + 1
	endif

	paramNameReprArray 	[_idx] = "gs_3D_representation"
	childFlagReprArray 	[_idx] = 0
	iconNameReprArray 	[_idx] = ""
	_idx = _idx + 1
	paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
	childFlagReprArray 	[_idx] = 1
	iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"
	_idx = _idx + 1
	if gs_detlevel_3D_m # DETLEV3D_OFF then
		paramNameReprArray 	[_idx] = "gs_shadow"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
		_idx = _idx + 1

		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
	endif
	
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= 1,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray,
													pageStartY		= pageStartY

return


!================================================================================
"description_page":
!================================================================================
	pageStartY				= 0

	ui_page TABID_DESCR, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCR,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
