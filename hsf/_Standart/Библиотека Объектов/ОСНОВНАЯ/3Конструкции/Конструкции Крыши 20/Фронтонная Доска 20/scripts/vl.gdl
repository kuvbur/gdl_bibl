h_wood = max(B, boardTh)
b_wood = min(B, boardTh)
c_wood = round_int(length*200)/200
parameters h_wood = h_wood, b_wood = b_wood, c_wood = c_wood
call "Общий шаблон дерево" parameters all

hideparameter all "A", "B", "ZZYZX"

! --- Main Sizes ---------------------------------------------------------------

values "A"		range (0, )
values "length"	range (0, )
values "zzyzx"	range (0, )
values "rAng"   range (0, 90)
values "nBoard" range [2, )

parameters B     = bIsraft * isrW + not(bIsraft) * nBoard * boardTh
lock "B"

if GLOB_MODPAR_NAME = "rAng" then		!A fixed
	zzyzx = A * tanrAng
	length = A / cosrAng
	parameters	zzyzx = zzyzx,
				length = length
endif

if GLOB_MODPAR_NAME = "A" then			!rAng fixed
	length = A / cosrAng
	zzyzx = A * tanrAng
	parameters	length = length,
				zzyzx = zzyzx
endif

if GLOB_MODPAR_NAME = "length" then		!rAng fixed
	zzyzx = length * sinrAng
	A = length * cosrAng
	parameters	A = A,
				zzyzx = zzyzx
endif

if GLOB_MODPAR_NAME = "ZZYZX" then		!A fixed
	rAng = atn(zzyzx / A)
	length = sqr(A ^ 2  + zzyzx ^ 2)

	parameters	rAng = rAng,
				length = length
endif

if GLOB_MODPAR_NAME = "ZZYZX" | GLOB_MODPAR_NAME = "length" | GLOB_MODPAR_NAME = "A" | GLOB_MODPAR_NAME = "rAng" | GLOB_MODPAR_NAME = "type" then
	ac_toplevel = zzyzx
	ac_bottomlevel = 0
	parameters	ac_toplevel = ac_toplevel,
				ac_bottomlevel = ac_bottomlevel
endif

! --- Settings ----------------------------------------------------------------

values{2} "gs_detlevel_3D_m" 	DETLEV3D_DETAILED,	`Детальный`,
								DETLEV3D_OFF,		`Откл.`

if gs_detlevel_3D_m = DETLEV3D_OFF then
	lock "gs_shadow", "gs_mat"
endif


! --- Israft -------------------------------------------------------------------

if bIsraft = 0 then lock "isrW", "isrTh", "isrOH", "hiddenLine"

values "isrW" range [isrTh, )
values "isrTh" range (0, )
values "isrOH" range [0, )


! --- type 1 - cutAng, cutback -------------------------------------------------

values{2} "type" 	CUT_VERT,	`Тип 1`,
					CUT_ANGLE,	`Тип 2`,
					CUT_HOR,	`Тип 3`

if type # CUT_ANGLE then
	lock "cutAng", "cutback"
endif

values "cutAng"  range [0, 90]
values "cutback" range [0, A ]


! --- type 2 - cutV, cutH ------------------------------------------------------

if type # CUT_HOR then
	lock "cutV", "cutH"
endif

values "cutV" range [0, (boardW - overlap) / cos(rAng)]
values "cutH" range [0, ((boardW - overlap) / cos(rAng)) / tan(rAng)]

if GLOB_MODPAR_NAME = "cutH" then
	parameters cutV = (boardW - overlap) / cos(rAng) - cutH * tan(rAng)
else
	parameters cutH = ((boardW - overlap) / cos(rAng) - cutV) / tan(rAng)
endif


