
unID = 1

if GLOB_CH_STORY_DIST < -EPS  then
	gs_cont_line=dash_line
	gs_fill_type=65
	gs_back_pen=0
	gs_section_landing_m = 1		! without Spine Beam
endif

! Show Lines at Front and Back Joint

if bottom_line then
	mask_bottomLine=1
else
	mask_bottomLine=0
endif

if top_line then
	mask_topLine=1
else
	mask_topLine=0
endif


PEN gs_cont_pen
FILL gs_fill_type

SET LINE_TYPE gs_cont_line

!======================================================================
! Straight
!======================================================================

if gs_shape_landing_m = 1 then	! Staright

	HOTSPOT2 0,fjl, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT2 -1,fjl, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl,fjl, unID, flwl, 2 : unID=unID+1

	HOTSPOT2 0,fjl+fy, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT2 -1,fjl+fy, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl,fjl+fy, unID, flwl, 2 : unID=unID+1

	HOTSPOT2 0,fjl, unID, fy, 1+256 : unID=unID+1
	HOTSPOT2 0,-1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 0,fjl+fy, unID, fy, 2 : unID=unID+1

	HOTSPOT2 flwl,fjl, unID, fy, 1+256 : unID=unID+1
	HOTSPOT2 flwl,-1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 flwl,fjl+fy, unID, fy, 2 : unID=unID+1

	! Concrete
	!======================================================================

	if gs_structure_landing_m =	2 then	! Concrete
		GROUP "landing"
		POLY2_b	5, 1+2+64,	gs_fill_pen,  gs_back_pen,
					0,		0+fjl,		1,
					0,		fjl+fy,		1,
					flwl,	fjl+fy,		1,
					flwl,	0+fjl,		1,
					0,		0+fjl,		1
		ENDGROUP

		GROUP "joint"
		if fj then
			SET LINE_TYPE dash_line
				LINE2 	0,0,flwl,0
				LINE2 	0,0,0,fjl
				LINE2 	flwl,0,flwl,fjl
			SET LINE_TYPE gs_cont_line

				HOTSPOT2 0,0, unID, fjl, 1+256 : unID=unID+1
				HOTSPOT2 0, -1, unID, fjl, 3 : unID=unID+1
				HOTSPOT2 0, fjl, unID, fjl, 2 : unID=unID+1

				HOTSPOT2 A,0, unID, fjl, 1+256 : unID=unID+1
				HOTSPOT2 A, -1, unID, fjl, 3 : unID=unID+1
				HOTSPOT2 A, fjl, unID, fjl, 2 : unID=unID+1
		endif


		if bj then
			SET LINE_TYPE dash_line
				LINE2 	0,fjl+bjl+fy,flwl,fjl+bjl+fy
				LINE2 	0,fjl+fy,0,fjl+bjl+fy
				LINE2 	flwl,fjl+fy,flwl,fjl+bjl+fy
			SET LINE_TYPE gs_cont_line

				HOTSPOT2 0,fjl+fy, unID, bjl, 1+256 : unID=unID+1
				HOTSPOT2 0, fjl+fy-1, unID, bjl, 3 : unID=unID+1
				HOTSPOT2 0, fjl+fy+bjl, unID, bjl, 2 : unID=unID+1

				HOTSPOT2 A,fjl+fy, unID, bjl, 1+256 : unID=unID+1
				HOTSPOT2 A, fjl+fy-1, unID, bjl, 3 : unID=unID+1
				HOTSPOT2 A, fjl+fy+bjl, unID, bjl, 2 : unID=unID+1
		endif

		ENDGROUP

		PLACEGROUP "landing"
		if fj or bj then PLACEGROUP "joint"

	else

	! Monolith
	!======================================================================
		if topTreadLevel_m = 1 then		! Top Tread at Floor Level

			POLY2_b	5, 1+2+64,	gs_fill_pen,  gs_back_pen,
						0,		0,		1,
						0,		fy,		mask_bottomLine,
						flwl,	fy,		1,
						flwl,	0,		mask_topLine,
						0,		0,		1
		else

			if gs_style_tread_connecting_m = 2 then	! Rounded
				temp_nosing= -cover*covthick
			else
				temp_nosing= -(ns*cover+covery*covthicky)	! Normal
			endif

			SET LINE_TYPE gs_cont_line
			POLY2_b	5, 1+2+64,	gs_fill_pen,  gs_back_pen,
						0,		temp_nosing,		1,
						0,		fy,		1,
						flwl,	fy,		1,
						flwl,	temp_nosing,		1,
						0,		temp_nosing,		1

			if cover=1 or covery=1 then
				SET LINE_TYPE dash_line
				LINE2 0,0,flwl,0
				SET LINE_TYPE gs_cont_line

				HOTSPOT2 0,temp_nosing,unID : unID=unID+1
				HOTSPOT2 flwl,temp_nosing,unID : unID=unID+1

			endif
		endif
	endif

	! Section
	!======================================================================

	if gs_section_landing_m = 2 and gs_detlevel_2D_m = 2 then	! With Spine Beam, Detailed
		GROUP "tube"
		SET LINE_TYPE dash_line
			LINE2 flwl/2-beamx/2, beam_dist,flwl/2-beamx/2,fjl+fy+bjl
			LINE2 flwl/2+beamx/2, beam_dist,flwl/2+beamx/2,fjl+fy+bjl
		SET LINE_TYPE gs_cont_line


		if topTreadLevel_m = 2 then		! Top Tread below Floor Level

			HOTSPOT2 flwl/2,0, unID, beam_dist, 1+128 : unID=unID+1
			HOTSPOT2 flwl/2,-1, unID, beam_dist, 3 : unID=unID+1
			HOTSPOT2 flwl/2,beam_dist, unID, beam_dist, 2 : unID=unID+1

		else
			HOTSPOT2	flwl/2,beam_dist,unID : unID=unID+1
		endif

		HOTSPOT2	flwl/2,fjl+bjl+fy,unID : unID=unID+1

		HOTSPOT2	flwl/2+beamx/2,fjl+bjl+fy,unID : unID=unID+1
		HOTSPOT2	flwl/2+beamx/2,beam_dist,unID : unID=unID+1

		HOTSPOT2	flwl/2-beamx/2,fjl+bjl+fy,unID : unID=unID+1
		HOTSPOT2	flwl/2-beamx/2,beam_dist,unID : unID=unID+1

		ENDGROUP
	endif

	if gs_section_landing_m = 2 and gs_detlevel_2D_m = 2 then PLACEGROUP "tube"	! With Spine Beam, Detailed

endif


!======================================================================
! L-Shape
!======================================================================

if gs_shape_landing_m = 2 then	! L-Shape

		fxx=flwl+fx

		!!! *** Middle-bottom
		HOTSPOT2 0,0, unID, flwl, 1+256 : unID=unID+1
		HOTSPOT2 -1, 0, unID, flwl, 3 : unID=unID+1
		HOTSPOT2 flwl,0, unID, flwl, 2 : unID=unID+1

		!!! *** Left-top
		HOTSPOT2 0,yl, unID, fy, 1+128 : unID=unID+1
		HOTSPOT2 0, -1, unID, fy, 3 : unID=unID+1
		HOTSPOT2 0,fy+yl, unID, fy, 2 : unID=unID+1

		!!!!!!!!! MIDDLE
		HOTSPOT2 0,yl, unID, flwl, 1+128 : unID=unID+1
		HOTSPOT2 -1, yl, unID, flwl, 3 : unID=unID+1
		HOTSPOT2 flwl, yl, unID, flwl, 2 : unID=unID+1

		HOTSPOT2 flwl,0, unID, yl, 1+256 : unID=unID+1
		HOTSPOT2 flwl,-1, unID, yl, 3 : unID=unID+1
		HOTSPOT2 flwl,yl, unID, yl, 2 : unID=unID+1

		!!! *** Right-bottom
		HOTSPOT2 flwl,yl, unID, fx, 1+128 : unID=unID+1
		HOTSPOT2 flwl-1,yl, unID, fx, 3 : unID=unID+1
		HOTSPOT2 flwl+fx,yl, unID, fx, 2 : unID=unID+1

		!!! *** Right-top
		HOTSPOT2 flwl,yl+fy, unID, fx, 1+128 : unID=unID+1
		HOTSPOT2 flwl-1,yl+fy, unID, fx, 3 : unID=unID+1
		HOTSPOT2 flwl+fx,yl+fy, unID, fx, 2 : unID=unID+1

		HOTSPOT2 fxx,yl, unID, fy, 1+256 : unID=unID+1
		HOTSPOT2 fxx,yl-1, unID, fy, 3 : unID=unID+1
		HOTSPOT2 fxx,yl+fy, unID, fy, 2 : unID=unID+1

	if gs_structure_landing_m =	2 then	! Concrete
		GROUP "L-landing"
				POLY2_b	7, 1+2+64,	gs_fill_pen,gs_back_pen,
						0,		0+fjl,		1,
						0,		yl+fy,	1,
						flwl+fx-bjl,yl+fy,	1,
						flwl+fx-bjl,	yl,	1,
						flwl,		yl,	1,
						flwl,		0+fjl,	1,
						0,			0+fjl,	1
		ENDGROUP

		GROUP "L-joint"
			if fj then
				SET LINE_TYPE dash_line
					LINE2 0,0,0,fjl
					LINE2 0,0,flwl,0
					LINE2 flwl,0,flwl,fjl
				SET LINE_TYPE gs_cont_line


					HOTSPOT2 0,0, unID, fjl, 1+128 : unID=unID+1
					HOTSPOT2 0, -1, unID, fjl, 3 : unID=unID+1
					HOTSPOT2 0, fjl, unID, fjl, 2 : unID=unID+1

					HOTSPOT2 flwl,0, unID, fjl, 1+128 : unID=unID+1
					HOTSPOT2 flwl, -1, unID, fjl, 3 : unID=unID+1
					HOTSPOT2 flwl, fjl, unID, fjl, 2 : unID=unID+1
			endif

			if bj then
				SET LINE_TYPE dash_line
					LINE2 A,B,A-bjl,B
					LINE2 A,B-fy,A,B
					LINE2 A,B-fy,A-bjl,B-fy
				SET LINE_TYPE gs_cont_line

					HOTSPOT2 A,B, unID, bjl, 1+128 : unID=unID+1
					HOTSPOT2 A+1,B, unID, bjl, 3 : unID=unID+1
					HOTSPOT2 A-bjl,B, unID, bjl, 2 : unID=unID+1

					HOTSPOT2 A,B-fy, unID, bjl, 1+128 : unID=unID+1
					HOTSPOT2 A+1,B-fy, unID, bjl, 3 : unID=unID+1
					HOTSPOT2 A-bjl,B-fy, unID, bjl, 2 : unID=unID+1
			endif
		ENDGROUP

		PLACEGROUP "L-landing"
		if fj or bj then PLACEGROUP "L-joint"

	else		!!! Stair Type = Monolith

		if topTreadLevel_m = 1 then			! Top Tread at Floor Level

				POLY2_b	7, 1+2+64,	gs_fill_pen,gs_back_pen,
						0,		0,		1,
						0,		yl+fy,	1,
						flwl+fx,yl+fy,	mask_bottomLine,
						flwl+fx,	yl,	1,
						flwl,		yl,	1,
						flwl,		0,	mask_topLine,
						0,			0,	1
		else

				if gs_style_tread_connecting_m = 2 then		! Rounded
					temp_nosing = -cover*covthick
				else
					temp_nosing = -(ns*cover+covery*covthicky)
				endif

				SET LINE_TYPE gs_cont_line
				POLY2_b	7, 1+2+64,	gs_fill_pen,gs_back_pen,
						0,		temp_nosing,		1,
						0,		yl+fy,	1,
						flwl+fx,yl+fy,	1,
						flwl+fx,	yl,	1,
						flwl,		yl,	1,
						flwl,		temp_nosing,	1,
						0,			temp_nosing,	1

				if cover=1 or covery=1 then
					SET LINE_TYPE dash_line
					LINE2 0,0,flwl,0
					SET LINE_TYPE gs_cont_line

					HOTSPOT2 0,temp_nosing,unID : unID=unID+1
					HOTSPOT2 flwl,temp_nosing,unID : unID=unID+1

				endif
		endif
	endif
	if gs_section_landing_m = 2 and gs_detlevel_2D_m = 2 then		! With Spine Beam, Detailed

		arany=(fy/flwl)
		ky= fy*(flwl/2+beamx/2)/flwl
		ny= fy*(flwl/2-beamx/2)/flwl

		SET LINE_TYPE dash_line

		LINE2 0, yl+fy, flwl, yl

		GROUP "L-tube1"
			!!! *** fy-tube

			LINE2 flwl/2-beamx/2,beam_dist,flwl/2-beamx/2,yl+ky
			LINE2 flwl/2+beamx/2,beam_dist,flwl/2+beamx/2,yl+ny

		if topTreadLevel_m = 2 then			! Top Tread below Floor Level

			HOTSPOT2 flwl/2,0, unID, beam_dist, 1+128 : unID=unID+1
			HOTSPOT2 flwl/2,-1, unID, beam_dist, 3 : unID=unID+1
			HOTSPOT2 flwl/2,beam_dist, unID, beam_dist, 2 : unID=unID+1

		else
			HOTSPOT2	flwl/2,beam_dist,unID : unID=unID+1
		endif

			HOTSPOT2	flwl/2+beamx/2,beam_dist, unID : unID=unID+1
			HOTSPOT2	flwl/2-beamx/2,beam_dist, unID : unID=unID+1
			HOTSPOT2	flwl/2,yl+fy/2, unID : unID=unID+1

		ENDGROUP

		GROUP "L-tube2"
			!!! *** fy-tube

			LINE2 flwl/2-beamx/2,yl+ky,flwl+fx,yl+fy/2+beamx/2*arany
			LINE2 flwl/2+beamx/2,yl+ny,flwl+fx,yl+fy/2-beamx/2*arany


			HOTSPOT2	flwl+fx,yl+fy/2+beamx/2*arany, unID : unID=unID+1
			HOTSPOT2	flwl+fx,yl+fy/2-beamx/2*arany, unID : unID=unID+1
			HOTSPOT2	flwl+fx,yl+fy/2, unID : unID=unID+1

		ENDGROUP

		SET LINE_TYPE gs_cont_line

	endif


	if gs_section_landing_m = 2 and gs_detlevel_2D_m = 2 then		! With Spine Beam, Detailed
		PLACEGROUP "L-tube1"
		PLACEGROUP "L-tube2"
	endif
endif

!======================================================================
! U-Shape
!======================================================================

if gs_shape_landing_m = 3 then	! U-Shape

	fxx=flwl+fx

	!!! *** Middle-Left-Bottom
	HOTSPOT2 0,0, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT2 -1, 0, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl, 0, unID, flwl, 2 : unID=unID+1

	!!! *** Left-top
	HOTSPOT2 0,yl, unID, fy, 1+128 : unID=unID+1
	HOTSPOT2 0, -1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 0,fy+yl, unID, fy, 2 : unID=unID+1

	!!! *** Middle-Left-Top
	HOTSPOT2 0,yl, unID, flwl, 1+128 : unID=unID+1
	HOTSPOT2 -1, yl, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl, yl, unID, flwl, 2 : unID=unID+1

	HOTSPOT2 flwl,0, unID, yl, 1+256 : unID=unID+1
	HOTSPOT2 flwl,-1, unID, yl, 3 : unID=unID+1
	HOTSPOT2 flwl,yl, unID, yl, 2 : unID=unID+1

	!! *** Right-Bottom
	HOTSPOT2 fxx,yl-yr, unID, flwr, 1+256 : unID=unID+1
	HOTSPOT2 fxx-1,yl-yr, unID, flwr, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl-yr, unID, flwr, 2 : unID=unID+1

	HOTSPOT2 fxx+flwr,yl, unID, yr, 1+128 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl+1, unID, yr, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl-yr, unID, yr, 2 : unID=unID+1

	!!! *** Middle-Right-Top
	HOTSPOT2 fxx,yl, unID, yr, 1+256 : unID=unID+1
	HOTSPOT2 fxx,yl+1, unID, yr, 3 : unID=unID+1
	HOTSPOT2 fxx,yl-yr, unID, yr, 2 : unID=unID+1

	HOTSPOT2 flwl,yl, unID, fx, 1+128 : unID=unID+1
	HOTSPOT2 flwl-1,yl, unID, fx, 3 : unID=unID+1
	HOTSPOT2 flwl+fx,yl, unID, fx, 2 : unID=unID+1

	!! *** Right-top
	HOTSPOT2 fxx,yl+fy, unID, flwr, 1+128 : unID=unID+1
	HOTSPOT2 fxx-1,yl+fy, unID, flwr, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl+fy, unID, flwr, 2 : unID=unID+1

	HOTSPOT2 fxx+flwr,yl, unID, fy, 1+128 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl-1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl+fy, unID, fy, 2 : unID=unID+1

	rry=yl-yr

if gs_structure_landing_m =	2 then	! Concrete
	GROUP "U-landing"

		POLY2_b		9, 1+2+64,		gs_fill_pen,gs_back_pen,
					0,				0+fjl,	1,
					0,				yl+fy,	1,
					flwl+fx+flwr,	yl+fy,	1,
					flwl+fx+flwr,	rry+bjl,	1,
					flwl+fx,		rry+bjl,	1,
					flwl+fx,		yl,		1,
					flwl,			yl,		1,
					flwl,			0+fjl,		1,
					0,				0+fjl,		1

	ENDGROUP

	GROUP "U-joint"
		if fj then
			SET LINE_TYPE dash_line
				LINE2 0,0,0,fjl
				LINE2 0,0,flwl,0
				LINE2 flwl,0,flwl,fjl
			SET LINE_TYPE gs_cont_line


				HOTSPOT2 0,0, unID, fjl, 1+128 : unID=unID+1
				HOTSPOT2 0, -1, unID, fjl, 3 : unID=unID+1
				HOTSPOT2 0, fjl, unID, fjl, 2 : unID=unID+1

				HOTSPOT2 flwl,0, unID, fjl, 1+128 : unID=unID+1
				HOTSPOT2 flwl, -1, unID, fjl, 3 : unID=unID+1
				HOTSPOT2 flwl, fjl, unID, fjl, 2 : unID=unID+1
		endif

		if bj then
			SET LINE_TYPE dash_line
				LINE2 A-flwr,yl-yr+bjl,A-flwr,yl-yr
				LINE2 A-flwr,yl-yr,A,yl-yr
				LINE2 A,yl-yr+bjl,A,yl-yr
			SET LINE_TYPE gs_cont_line

				HOTSPOT2 A-flwr,yl-yr, unID, bjl, 1+128 : unID=unID+1
				HOTSPOT2 A-flwr, yl-yr-1, unID, bjl, 3 : unID=unID+1
				HOTSPOT2 A-flwr, yl-yr+bjl, unID,bjl, 2 : unID=unID+1

				HOTSPOT2 A,yl-yr, unID, bjl, 1+128 : unID=unID+1
				HOTSPOT2 A, yl-yr-1, unID, bjl, 3 : unID=unID+1
				HOTSPOT2 A, yl-yr+bjl, unID, bjl, 2 : unID=unID+1
		endif
	ENDGROUP
	PLACEGROUP "U-landing"
	if fj or bj then PLACEGROUP "U-joint"


	else		!!! Stair Type = Monolith

		if topTreadLevel_m = 1 then			! Top Tread at Floor Level

		POLY2_b		9, 1+2+64,		gs_fill_pen,gs_back_pen,
					0,				0,	1,
					0,				yl+fy,	1,
					flwl+fx+flwr,	yl+fy,	1,
					flwl+fx+flwr,	rry,	mask_bottomLine,
					flwl+fx,		rry,	1,
					flwl+fx,		yl,		1,
					flwl,			yl,		1,
					flwl,			0,		mask_topLine,
					0,				0,		1
		else

				if gs_style_tread_connecting_m = 2 then		! Rounded
					temp_nosing= -cover*covthick
				else
					temp_nosing= -(ns*cover+covery*covthicky)
				endif

				SET LINE_TYPE gs_cont_line
		POLY2_b		9, 1+2+64,		gs_fill_pen,gs_back_pen,
					0,				temp_nosing,	1,
					0,				yl+fy,	1,
					flwl+fx+flwr,	yl+fy,	1,
					flwl+fx+flwr,	rry,	1,
					flwl+fx,		rry,	1,
					flwl+fx,		yl,		1,
					flwl,			yl,		1,
					flwl,			temp_nosing,		1,
					0,				temp_nosing,		1
				if cover=1 or covery=1 then
					SET LINE_TYPE dash_line
					LINE2 0,0,flwl,0
					SET LINE_TYPE gs_cont_line

					HOTSPOT2 0,temp_nosing,unID : unID=unID+1
					HOTSPOT2 flwl,temp_nosing,unID : unID=unID+1

				endif
	endif
	endif

	if gs_section_landing_m = 2 and gs_detlevel_2D_m = 2 then		! With Spine Beam, Detailed

		arany=flwr/flwl

		ky= fy*(flwl/2+beamx/2)/flwl
		ny= fy*(flwl/2-beamx/2)/flwl

		kx=flwr*(fy/2+beamx/2*(fy/flwl))/fy
		nx=flwr*(fy/2-beamx/2*(fy/flwl))/fy

		GROUP "U-tube1"
		   !!! *** flwl-tube

			SET LINE_TYPE dash_line

			LINE2 0, yl+fy, flwl, yl
			LINE2 flwl+fx,yl,flwl+fx+flwr,yl+fy

			LINE2 flwl/2-beamx/2,beam_dist,flwl/2-beamx/2,yl+ky
			LINE2 flwl/2+beamx/2,beam_dist,flwl/2+beamx/2,yl+ny

		if topTreadLevel_m = 2 then		! Top Tread below Floor Level

			HOTSPOT2 flwl/2,0, unID, beam_dist, 1+128 : unID=unID+1
			HOTSPOT2 flwl/2,-1, unID, beam_dist, 3 : unID=unID+1
			HOTSPOT2 flwl/2,beam_dist, unID, beam_dist, 2 : unID=unID+1

		else
			HOTSPOT2	flwl/2,beam_dist,unID : unID=unID+1
		endif

			HOTSPOT2	flwl/2+beamx/2,beam_dist,unID : unID=unID+1
			HOTSPOT2	flwl/2-beamx/2,beam_dist,unID : unID=unID+1
			HOTSPOT2	flwl/2,yl+fy/2,unID : unID=unID+1

		ENDGROUP

		GROUP "U-tube2"
			!!! *** fy-tube

			LINE2 flwl/2-beamx/2,yl+ky,flwl+fx+kx,yl+ky
			LINE2 flwl/2+beamx/2,yl+ny,flwl+fx+nx,yl+ny

		ENDGROUP

		GROUP "U-tube3"
			LINE2 flwl+fx+kx,yl+ky,flwl+fx+flwr/2+beamx/2*arany,rry
			LINE2 flwl+fx+nx,yl+ny,flwl+fx+flwr/2-beamx/2*arany,rry

			HOTSPOT2	flwl+fx+flwr/2+beamx/2*arany,rry,unID : unID=unID+1
			HOTSPOT2	flwl+fx+flwr/2-beamx/2*arany,rry,unID : unID=unID+1
			HOTSPOT2	flwl+fx+flwr/2,rry,unID : unID=unID+1
			HOTSPOT2	flwl+fx+flwr/2,yl+fy/2,unID : unID=unID+1

			SET LINE_TYPE gs_cont_line

		ENDGROUP


	endif


	if gs_section_landing_m = 2 and gs_detlevel_2D_m = 2 then		! With Spine Beam, Detailed
		PLACEGROUP "U-tube1"
		PLACEGROUP "U-tube2"
		PLACEGROUP "U-tube2"
	endif

 endif
end
