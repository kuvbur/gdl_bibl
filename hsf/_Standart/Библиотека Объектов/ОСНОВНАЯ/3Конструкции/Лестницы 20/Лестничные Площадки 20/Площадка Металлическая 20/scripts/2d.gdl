
! --- 2D Representation on Stories ---------------------------------------------

! Stories down the story, where the object is placed on
if GLOB_CH_STORY_DIST < -EPS  then
	gs_cont_line=dash_line
	gs_fill_type=65
	gs_back_pen=0
	gs_type_beam_m = 1		! None
endif


PEN gs_cont_pen
FILL gs_fill_type

SET LINE_TYPE gs_cont_line

unID=1
! ==============================================================================
! Simple
! ==============================================================================

HOTSPOT2 0,0,unID : unID=unID+1
if gs_shape_landing_m = 1 then  ! Straight


	HOTSPOT2 0,fy, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT2 -1,fy, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl,fy, unID, flwl, 2 : unID=unID+1

	HOTSPOT2 0,0, unID, fy, 1+256 : unID=unID+1
	HOTSPOT2 0,-1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 0,fy, unID, fy, 2 : unID=unID+1

	HOTSPOT2 flwl,0, unID, fy, 1+256 : unID=unID+1
	HOTSPOT2 flwl,-1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 flwl,fy, unID, fy, 2 : unID=unID+1

	HOTSPOT2 0,0, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT2 -1,0, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl,0, unID, flwl, 2 : unID=unID+1

! Call Beam Macros

	if gs_type_beam_m <> 5 then		! None

		ROT2 90

		if gs_position_beam_m = 2 then		! Double
			xx=2
		else
			xx=1
		endif

		for k=1 to xx

			if gs_position_beam_m = 1 then		! Single
				ADD2 0,-flwl/2
			else
				ADD2 0,-boff
			endif

			SET LINE_TYPE al
			LINE2 0,0,fy,0
			HOTSPOT2 0,0,unID : unID=unID+1
			HOTSPOT2 fy,0,unID : unID=unID+1
			if schema=0 then
				SET LINE_TYPE dash_line
				LINE2 0,-v/2/1000,fy,-v/2/1000
				LINE2 0,v/2/1000,fy,v/2/1000
			endif

			DEL 1

			ADD2 0,-flwl+2*boff
		next k
		DEL xx

		DEL 1
	endif

	SET LINE_TYPE gs_cont_line
	POLY2_b 5, 1+2+64,gs_fill_pen,  gs_back_pen,
		0,0,1,
		flwl,0,1,
		flwl,fy,1,
		0,fy,1,
		0,0,-1
endif

! ==============================================================================
! L-Shape
! ==============================================================================
if gs_shape_landing_m = 2 then  ! L-Shape


	fxx=flwl+fx

	! Left-top
	HOTSPOT2 0,yl, unID, fy, 1+128 : unID=unID+1
	HOTSPOT2 0, -1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 0,fy+yl, unID, fy, 2 : unID=unID+1

	! MIDDLE
	HOTSPOT2 0,yl, unID, flwl, 1+128 : unID=unID+1
	HOTSPOT2 -1, yl, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl, yl, unID, flwl, 2 : unID=unID+1

	HOTSPOT2 flwl,0, unID, yl, 1+256 : unID=unID+1
	HOTSPOT2 flwl,-1, unID, yl, 3 : unID=unID+1
	HOTSPOT2 flwl,yl, unID, yl, 2 : unID=unID+1

	! Right-bottom
	HOTSPOT2 flwl,yl, unID, fx, 1+128 : unID=unID+1
	HOTSPOT2 flwl-1,yl, unID, fx, 3 : unID=unID+1
	HOTSPOT2 flwl+fx,yl, unID, fx, 2 : unID=unID+1

	! Right-top
	HOTSPOT2 flwl,yl+fy, unID, fx, 1+128 : unID=unID+1
	HOTSPOT2 flwl-1,yl+fy, unID, fx, 3 : unID=unID+1
	HOTSPOT2 flwl+fx,yl+fy, unID, fx, 2 : unID=unID+1

	HOTSPOT2 fxx,yl, unID, fy, 1+256 : unID=unID+1
	HOTSPOT2 fxx,yl-1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 fxx,yl+fy, unID, fy, 2 : unID=unID+1

	! Middle-bottom
	HOTSPOT2 0,0, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT2 -1, 0, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl,0, unID, flwl, 2 : unID=unID+1

	if gs_type_beam_m <> 5 then		! None
		if gs_position_beam_m = 1 then		! Single
			ROT2 90
			ADD2 0,-flwl/2
				SET LINE_TYPE al
					LINE2 0,0,yl+fy/2,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yl+fy/2,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yl+fy/2-v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yl+fy/2+v/2/1000,v/2/1000
				endif
			DEL 2

			ADD2 0,yl+fy/2
			ADD2 flwl/2, 0
				SET LINE_TYPE al
					LINE2 0,0,fx+flwl/2,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 fx+flwl/2,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 v/2/1000,-v/2/1000,fx+flwl/2,-v/2/1000
					LINE2 -v/2/1000,v/2/1000,fx+flwl/2,v/2/1000
				endif
			DEL 2
		else
			ROT2 90
			ADD2 0,-flwl+boff
				SET LINE_TYPE al
					LINE2 0,0,yl+boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yl+boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yl+boff-v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yl+boff+v/2/1000,v/2/1000
				endif
			DEL 2

			ADD2 0,yl+boff
			ADD2 flwl-boff, 0
				SET LINE_TYPE al
					LINE2 0,0,fx+boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 fx+boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 v/2/1000,-v/2/1000,fx+boff,-v/2/1000
					LINE2 -v/2/1000,v/2/1000,fx+boff,v/2/1000
				endif
			DEL 2

			ROT2 90
			ADD2 0,-boff
				SET LINE_TYPE al
					LINE2 0,0,yl+fy-boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yl+fy-boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yl+fy-boff-v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yl+fy-boff+v/2/1000,v/2/1000
				endif
			DEL 2

			ADD2 0,yl+fy-boff
			ADD2 boff, 0
				SET LINE_TYPE al
					LINE2 0,0,fx+flwl-boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 fx+flwl-boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 v/2/1000,-v/2/1000,fx+flwl-boff,-v/2/1000
					LINE2 -v/2/1000,v/2/1000,fx+flwl-boff,v/2/1000
				endif
			DEL 2
		endif
	endif

	SET LINE_TYPE gs_cont_line
	POLY2_b 7, 1+2+64,gs_fill_pen,  gs_back_pen,
		0,0,1,
		0,fy+yl,1,
		flwl+fx, fy+yl,1,
		flwl+fx, yl,1,
		flwl, yl,1,
		flwl,0,1,
		0,0,-1

endif

! ==============================================================================
! U-Shape
! ==============================================================================
if gs_shape_landing_m = 3 then  ! U-Shape

	fxx=flwl+fx

	! Left-top
	HOTSPOT2 0,yl, unID, fy, 1+128 : unID=unID+1
	HOTSPOT2 0, -1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 0,fy+yl, unID, fy, 2 : unID=unID+1

	! Middle-Left-Top
	HOTSPOT2 0,yl, unID, flwl, 1+128 : unID=unID+1
	HOTSPOT2 -1, yl, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl, yl, unID, flwl, 2 : unID=unID+1

	HOTSPOT2 flwl,0, unID, yl, 1+256 : unID=unID+1
	HOTSPOT2 flwl,-1, unID, yl, 3 : unID=unID+1
	HOTSPOT2 flwl,yl, unID, yl, 2 : unID=unID+1

	! Middle-Left-Bottom
	HOTSPOT2 0,0, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT2 -1, 0, unID, flwl, 3 : unID=unID+1
	HOTSPOT2 flwl, 0, unID, flwl, 2 : unID=unID+1

	! Right-Bottom
	HOTSPOT2 fxx,yl-yr, unID, flwr, 1+256 : unID=unID+1
	HOTSPOT2 fxx-1,yl-yr, unID, flwr, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl-yr, unID, flwr, 2 : unID=unID+1

	HOTSPOT2 fxx+flwr,yl, unID, yr, 1+128 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl+1, unID, yr, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl-yr, unID, yr, 2 : unID=unID+1

	! Middle-Right-Top
	HOTSPOT2 fxx,yl, unID, yr, 1+256 : unID=unID+1
	HOTSPOT2 fxx,yl+1, unID, yr, 3 : unID=unID+1
	HOTSPOT2 fxx,yl-yr, unID, yr, 2 : unID=unID+1

	HOTSPOT2 flwl,yl, unID, fx, 1+128 : unID=unID+1
	HOTSPOT2 flwl-1,yl, unID, fx, 3 : unID=unID+1
	HOTSPOT2 flwl+fx,yl, unID, fx, 2 : unID=unID+1

	! Right-top
	HOTSPOT2 fxx,yl+fy, unID, flwr, 1+128 : unID=unID+1
	HOTSPOT2 fxx-1,yl+fy, unID, flwr, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl+fy, unID, flwr, 2 : unID=unID+1

	HOTSPOT2 fxx+flwr,yl, unID, fy, 1+128 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl-1, unID, fy, 3 : unID=unID+1
	HOTSPOT2 fxx+flwr,yl+fy, unID, fy, 2 : unID=unID+1

	if gs_type_beam_m <> 5 then		! None
	SET LINE_TYPE dash_line
		if gs_position_beam_m = 1 then		! Single
			ROT2 90
			ADD2 0,-flwl/2
				SET LINE_TYPE al
					LINE2 0,0,yl+fy/2,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yl+fy/2,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,v/2/1000,yl+fy/2+v/2/1000,v/2/1000
					LINE2 0,-v/2/1000,yl+fy/2-v/2/1000,-v/2/1000
				endif
			DEL 2

			ADD2 0,yl+fy/2
			ADD2 flwl/2,0
				SET LINE_TYPE al
					LINE2 0,0,fx+flwl/2+flwr/2,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 fx+flwl/2+flwr/2,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 -v/2/1000,v/2/1000,fx+flwl/2+flwr/2+v/2/1000,v/2/1000
					LINE2 v/2/1000,-v/2/1000,fx+flwl/2+flwr/2-v/2/1000,-v/2/1000
				endif
			DEL 2

			ROT2 90
			ADD2 0,-1*(flwl+fx+flwr/2)
			ADD2 yl-yr,0
				SET LINE_TYPE al
					LINE2 0,0,yr+fy/2,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yr+fy/2,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yr+fy/2+v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yr+fy/2-v/2/1000,v/2/1000
				endif
			DEL 3
		else
			ROT2 90
			ADD2 0,-flwl+boff
				SET LINE_TYPE al
					LINE2 0,0,yl+boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yl+boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yl+boff-v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yl+boff+v/2/1000,v/2/1000
				endif
			DEL 2

			ADD2 0,yl+boff
			ADD2 flwl-boff,0
				SET LINE_TYPE al
					LINE2 0,0,fx+2*boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 fx+2*boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 -v/2/1000,v/2/1000,fx+2*boff+v/2/1000,v/2/1000
					LINE2 v/2/1000,-v/2/1000,fx+2*boff-v/2/1000,-v/2/1000
				endif
			DEL 2

			ROT2 90
			ADD2 yl-yr,0
			ADD2 0,-1*(flwl+fx+boff)
				SET LINE_TYPE al
					LINE2 0,0,yr+boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yr+boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yr+boff+v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yr+boff-v/2/1000,v/2/1000
				endif
			DEL 3

			ROT2 90
			ADD2 0,-boff
				SET LINE_TYPE al
					LINE2 0,0,yl+fy-boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yl+fy-boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yl+fy-boff-v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yl+fy-boff+v/2/1000,v/2/1000
				endif
			DEL 2

			ADD2 0,yl+fy-boff
			ADD2 boff,0
				SET LINE_TYPE al
					LINE2 0,0,fx+flwl+flwr-2*boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 fx+flwl+flwr-2*boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 -v/2/1000,v/2/1000,fx+flwl+flwr-2*boff+v/2/1000,v/2/1000
					LINE2 v/2/1000,-v/2/1000,fx+flwl+flwr-2*boff-v/2/1000,-v/2/1000
				endif
			DEL 2

			ROT2 90
			ADD2 yl-yr,0
			ADD2 0,-1*(flwl+fx+flwr-boff)
				SET LINE_TYPE al
					LINE2 0,0,yr+fy-boff,0
					HOTSPOT2 0,0,unID : unID=unID+1
					HOTSPOT2 yr+fy-boff,0,unID : unID=unID+1
				if schema=0 then
				SET LINE_TYPE dash_line
					LINE2 0,-v/2/1000,yr+fy-boff+v/2/1000,-v/2/1000
					LINE2 0,v/2/1000,yr+fy-boff-v/2/1000,v/2/1000
				endif
			DEL 3
		endif
	endif

	SET LINE_TYPE gs_cont_line
	POLY2_b 9, 1+2+64,gs_fill_pen,  gs_back_pen,
			0,0,1,
			0, yl+fy,1,
			flwl+fx+flwr, fy+yl, 1,
			flwl+fx+flwr, yl-yr,1,
			flwl+fx, yl-yr,1,
			flwl+fx, yl,1,
			flwl, yl,1,
			flwl, 0,1,
			0,0,-1
endif

end
