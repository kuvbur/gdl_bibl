
if gs_detlevel_3D_m = 0 then end

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then 		! for Simple Model and in 3D Windoow
	gs_detlevel_3D_m = 1		! Simple
endif

if not(gs_shadow) then
	SHADOW OFF
else
	SHADOW ON
endif

pen gs_cont_pen

unID = 1

sh = h/1000


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!  Simple  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	dist_schneid1=((h/1000)/tan(schneid1))
	dist_schneid2=((h/1000)/tan(180-schneid2))

	if schneid1 >= 90 then
	NN1=0
	else
	NN1=1
	endif

	if schneid2 <= 90 then
	NN2=0
	else
	NN2=1
	endif

	if schneid2 >= 90 then
	NN3=0
	else
	NN3=1
	endif

if gs_shape_landing_m = 1 then  ! Straight

	HOTSPOT 0,fy, 0, unID, flwl, 1+256: unID=unID+1
	HOTSPOT -1,fy, 0, unID, flwl, 3: unID=unID+1
	HOTSPOT flwl,fy, 0, unID, flwl, 2: unID=unID+1

	HOTSPOT 0,0, 0, unID, fy, 1+256: unID=unID+1
	HOTSPOT 0,-1, 0, unID, fy, 3: unID=unID+1
	HOTSPOT 0,fy, 0, unID, fy, 2: unID=unID+1

	HOTSPOT flwl,0, 0, unID, fy, 1+256: unID=unID+1
	HOTSPOT flwl,-1, 0, unID, fy, 3: unID=unID+1
	HOTSPOT flwl,fy, 0, unID, fy, 2: unID=unID+1

	HOTSPOT 0,0, 0, unID,  flwl, 1+256 : unID=unID+1
	HOTSPOT -1,0, 0, unID,  flwl, 3 : unID=unID+1
	HOTSPOT flwl,0, 0, unID,  flwl, 2 : unID=unID+1

	!!! Landing Thickness
	HOTSPOT 0,0, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT 0,0, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT 0,0, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl,0, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl,0, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl,0, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl,fy, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl,fy, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl,fy, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT 0,fy, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT 0,fy, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT 0,fy, -lth, unID, lth, 2 : unID=unID+1

	addz -lth

	ROTx schneid1
	CUTPLANE
	DEL 1

	ADDy fy
	ROTx schneid2
	CUTPLANE 1,1,0,1
	DEL 2

!!!!!!!!!!!!!!!Call Beam Macros
ADDz -(sh)
SECT_FILL typeFillStringer, penBackFillStringer, penFillStringer, penLineStringer

if gs_type_beam_m <> 5 then		! None

	ROTz 90

	if gs_position_beam_m = 2 then		! Double
	xx=2
	else
	xx=1
	endif

	FOR k=1 to xx

		if gs_position_beam_m = 1 then		! Single
			ADDy -flwl/2
			else
			ADDy -boff
			endif

			ADDx -((h/1000)/tan(schneid1))*NN1

			temp_a = fy+((h/1000)/tan(schneid1))*NN1+((h/1000)/tan(180-schneid2))*NN2
			temp_schneid1 = 90
			temp_schneid2 = 90
			gosub "macro_calling_rot"

			HOTSPOT 0,0, h/1000,unID: unID=unID+1
			HOTSPOT fy,0, h/1000,unID: unID=unID+1

			DEL 1
			DEL 1

			MUL 1,-1,1
			ADDy 2*flwl-2*boff

	ADDy -flwl+2*boff
	NEXT k
	DEL 3*xx



	DEL 1
endif
DEL 1

CUTEND
CUTEND

ADDx flwl
if gs_type_beam_m <> 5 then		! None

	if gs_position_beam_m = 2 then		! Double
	xx=2
	else
	xx=1
	endif

	FOR k=1 to xx
		if gs_position_beam_m = 1 then		! Single
			ADDx -flwl/2
			else
			ADDx -boff

			endif

				HOTSPOT 		0,	-0.1,	0, 	unID, schneid1, 4+128: unID=unID+1
				HOTSPOT 		0,	0,	0, 			unID, schneid1, 6: unID=unID+1
				HOTSPOT 		0,	-dist_schneid1,	-h/1000, unID, schneid1, 5: unID=unID+1

			ADDy fy
				HOTSPOT 		0,	-0.1,	0, 	unID, schneid2, 4+128: unID=unID+1
				HOTSPOT 		0,	0,	0, 			unID, schneid2, 6: unID=unID+1
				HOTSPOT 		0,	dist_schneid2,	-h/1000, unID, schneid2, 5: unID=unID+1
			DEL 1

			DEL 1
	ADDx -flwl+2*boff
	NEXT k
	DEL xx

endif

DEL 1
!!!!!!!!!!!!!!! End of Call Beam Macros


	MATERIAL matl
	SECT_FILL typeFillTread, penBackFillTread, penFillTread, penLineTread

	PRISM 5, lth,	!!!!LANDING SLAB
		0,0,
		flwl,0,
		flwl,fy,
		0,fy,
		0,0
endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!  L-Shape  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_shape_landing_m = 2 then  ! L-Shape

	fxx=flwl+fx

	!!! *** Left-top
	HOTSPOT 0,yl,0, unID, fy, 1+128 : unID=unID+1
	HOTSPOT 0, -1,0, unID, fy, 3 : unID=unID+1
	HOTSPOT 0,fy+yl,0, unID, fy, 2 : unID=unID+1

	!!!!!!!!! MIDDLE
	HOTSPOT 0,yl,0, unID, flwl, 1+128 : unID=unID+1
	HOTSPOT -1, yl,0, unID, flwl, 3 : unID=unID+1
	HOTSPOT flwl, yl,0, unID, flwl, 2 : unID=unID+1

	HOTSPOT flwl,0,0, unID, yl, 1+256 : unID=unID+1	!!Middle
	HOTSPOT flwl,-1,0, unID, yl, 3 : unID=unID+1
	HOTSPOT flwl,yl,0, unID, yl, 2 : unID=unID+1

	!!! *** Right-bottom
	HOTSPOT flwl,yl,0, unID, fx, 1+128 : unID=unID+1
	HOTSPOT flwl-1,yl,0, unID, fx, 3 : unID=unID+1
	HOTSPOT flwl+fx,yl,0, unID, fx, 2 : unID=unID+1

	!!! *** Right-top
	HOTSPOT flwl,yl+fy,0, unID, fx, 1+128 : unID=unID+1
	HOTSPOT flwl-1,yl+fy,0, unID, fx, 3 : unID=unID+1
	HOTSPOT flwl+fx,yl+fy,0, unID, fx, 2 : unID=unID+1

	HOTSPOT fxx,yl, 0,unID, fy, 1+256 : unID=unID+1
	HOTSPOT fxx,yl-1,0, unID, fy, 3 : unID=unID+1
	HOTSPOT fxx,yl+fy,0, unID, fy, 2 : unID=unID+1

	!!! *** Middle-bottom
	HOTSPOT 0,0,0, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT -1, 0, 0,unID, flwl, 3 : unID=unID+1
	HOTSPOT flwl,0,0, unID, flwl, 2 : unID=unID+1

	!!! Landing Thickness
	HOTSPOT 0,0, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT 0,0, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT 0,0, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl,0, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl,0, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl,0, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl,yl, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl,yl, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl,yl, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl+fx,yl, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl+fx,yl, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl+fx,yl, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl+fx,yl+fy, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl+fx,yl+fy, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl+fx,yl+fy, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT 0,yl+fy, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT 0,yl+fy, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT 0,yl+fy, -lth, unID, lth, 2 : unID=unID+1

	addz -lth
	ADDx flwl+fx
	ROTy 180-schneid2
	CUTPLANE
	DEL 2

	ROTx schneid1
	CUTPLANE
	DEL 1

	ADDz -(sh)
	SECT_FILL typeFillStringer, penBackFillStringer, penFillStringer, penLineStringer

	if gs_type_beam_m <> 5 then		! None
		if gs_position_beam_m = 1 then		! Single

			ROTz 90
			ADDy -flwl/2

			ADDx -dist_schneid1*NN1
			temp_a = yl+fy/2+dist_schneid1*NN1
			temp_schneid1 = 90
			temp_schneid2 = 45
			gosub "macro_calling"
			DEL 1
			DEL 2

			ADDy yl+fy/2
			ADDx flwl/2
			temp_a = fx+flwl/2+dist_schneid2*NN2
			temp_schneid1 = 135
			temp_schneid2 = 90
			gosub "macro_calling"

			DEL 2

		else
			ya=fy*(flwl-boff)/flwl
			yb=fy*(boff)/flwl


			ROTz 90
			ADDy -flwl+boff
			ADDx -dist_schneid1*NN1
			MUL 1,-1,1
			temp_a = yl+boff+dist_schneid1*NN1
			temp_schneid1 = 90
			temp_schneid2 = 135
			gosub "macro_calling"

			DEL 1
			DEL 1
			DEL 2

			ADDy yl+boff
			ADDx flwl-boff
			MUL 1,-1,1
			temp_a = fx+boff+dist_schneid2*NN2
			temp_schneid1 = 45
			temp_schneid2 = 90
			gosub "macro_calling"

			DEL 1
			DEL 2

			ROTz 90
			ADDy -boff
			ADDx -dist_schneid1*NN1
			temp_a = yl+fy-boff+dist_schneid1*NN1
			temp_schneid1 = 90
			temp_schneid2 = 45
			gosub "macro_calling"

			DEL 1
			DEL 2

			ADDy yl+fy-boff
			ADDx boff
			temp_a = fx+flwl-boff+dist_schneid2*NN2
			temp_schneid1 = 135
			temp_schneid2 = 90
			gosub "macro_calling"

			DEL 2
		endif
	endif
DEL 1

CUTEND
CUTEND


ADDx flwl
if gs_type_beam_m <> 5 then		! None

	if gs_position_beam_m = 2 then		! Double
	xx=2
	else
	xx=1
	endif

	FOR k=1 to xx
			if gs_position_beam_m = 1 then		! Single
				ADDx -flwl/2
				else
				ADDx -boff
			endif

				HOTSPOT 		0,	-0.1,	0, 	unID, schneid1, 4+128: unID=unID+1
				HOTSPOT 		0,	0,	0, 			unID, schneid1, 6: unID=unID+1
				HOTSPOT 		0,	-dist_schneid1,	-h/1000, unID, schneid1, 5: unID=unID+1
			DEL 1
	ADDx -flwl+2*boff
	NEXT k
	DEL xx
endif
DEL 1

ADDy yl+fy
if gs_type_beam_m <> 5 then		! None

	if gs_position_beam_m = 2 then		! Double
	xx=2
	else
	xx=1
	endif

	FOR k=1 to xx
			if gs_position_beam_m = 1 then		! Single
				ADDy -fy/2
				else
				ADDy -boff
			endif

				ADDx flwl+fx
					ROTZ -90
						HOTSPOT 		0,	-0.1,	0, 	unID, schneid2, 4+128: unID=unID+1
						HOTSPOT 		0,	0,	0, 			unID, schneid2, 6: unID=unID+1
						HOTSPOT 		0,	dist_schneid2,	-h/1000, unID, schneid2, 5: unID=unID+1
					DEL 1
				DEL 1
			DEL 1
	ADDy -fy+2*boff
	NEXT k
	DEL xx
endif
DEL 1
SECT_FILL typeFillTread, penBackFillTread, penFillTread, penLineTread

	MATERIAL matl
	PRISM 7, lth,
		0,0,
		0,fy+yl,
		flwl+fx, fy+yl,
		flwl+fx, yl,
		flwl, yl,
		flwl,0,
		0,0

endif


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!  U-Shape  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
if gs_shape_landing_m = 3 then  ! U-Shape

	fxx=flwl+fx

	!!! *** Left-top
	HOTSPOT 0,yl,0, unID, fy, 1+128 : unID=unID+1
	HOTSPOT 0, -1,0, unID, fy, 3 : unID=unID+1
	HOTSPOT 0,fy+yl,0, unID, fy, 2 : unID=unID+1

	!!! *** Middle-Left-Top
	HOTSPOT 0,yl,0, unID, flwl, 1+128 : unID=unID+1
	HOTSPOT -1, yl,0, unID, flwl, 3 : unID=unID+1
	HOTSPOT flwl, yl,0, unID, flwl, 2 : unID=unID+1

	HOTSPOT flwl,0,0, unID, yl, 1+256 : unID=unID+1
	HOTSPOT flwl,-1,0, unID, yl, 3 : unID=unID+1
	HOTSPOT flwl,yl,0, unID, yl, 2 : unID=unID+1

	!! *** Middle-Left-Bottom
	HOTSPOT 0,0,0, unID, flwl, 1+256 : unID=unID+1
	HOTSPOT -1, 0,0, unID, flwl, 3 : unID=unID+1
	HOTSPOT flwl, 0,0, unID, flwl, 2 : unID=unID+1

	!! *** Right-Bottom
	HOTSPOT fxx,yl-yr,0, unID, flwr, 1+256 : unID=unID+1
	HOTSPOT fxx-1,yl-yr,0, unID, flwr, 3 : unID=unID+1
	HOTSPOT fxx+flwr,yl-yr,0, unID, flwr, 2 : unID=unID+1

	HOTSPOT fxx+flwr,yl,0, unID, yr, 1+128 : unID=unID+1
	HOTSPOT fxx+flwr,yl+1,0, unID, yr, 3 : unID=unID+1
	HOTSPOT fxx+flwr,yl-yr,0, unID, yr, 2 : unID=unID+1

	!!! *** Middle-Right-Top
	HOTSPOT fxx,yl,0, unID, yr, 1+256 : unID=unID+1
	HOTSPOT fxx,yl+1,0, unID, yr, 3 : unID=unID+1
	HOTSPOT fxx,yl-yr,0, unID, yr, 2 : unID=unID+1

	HOTSPOT flwl,yl,0, unID, fx, 1+128 : unID=unID+1
	HOTSPOT flwl-1,yl,0, unID, fx, 3 : unID=unID+1
	HOTSPOT flwl+fx,yl,0, unID, fx, 2 : unID=unID+1

	!! *** Right-top
	HOTSPOT fxx,yl+fy,0, unID, flwr, 1+128 : unID=unID+1
	HOTSPOT fxx-1,yl+fy,0, unID, flwr, 3 : unID=unID+1
	HOTSPOT fxx+flwr,yl+fy,0, unID, flwr, 2 : unID=unID+1

	HOTSPOT fxx+flwr,yl,0, unID, fy, 1+128 : unID=unID+1
	HOTSPOT fxx+flwr,yl-1,0, unID, fy, 3 : unID=unID+1
	HOTSPOT fxx+flwr,yl+fy,0, unID, fy, 2 : unID=unID+1

	!!! Landing Thickness
	HOTSPOT 0,0, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT 0,0, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT 0,0, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl,0, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl,0, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl,0, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl,yl, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl,yl, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl,yl, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl+fx,yl, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl+fx,yl, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl+fx,yl, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl+fx,yl-yr, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl+fx,yl-yr, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl+fx,yl-yr, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl+fx+flwr,yl-yr, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl+fx+flwr,yl-yr, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl+fx+flwr,yl-yr, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT flwl+fx+flwr,yl+fy, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT flwl+fx+flwr,yl+fy, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT flwl+fx+flwr,yl+fy, -lth, unID, lth, 2 : unID=unID+1

	HOTSPOT 0,yl+fy, 0, unID, lth, 1+128 : unID=unID+1
	HOTSPOT 0,yl+fy, 1, unID, lth, 3 : unID=unID+1
	HOTSPOT 0,yl+fy, -lth, unID, lth, 2 : unID=unID+1

	addz -lth
	ROTx schneid1
	CUTPLANE
	DEL 1

	ADDz -(sh)
	SECT_FILL typeFillStringer, penBackFillStringer, penFillStringer, penLineStringer

	if gs_type_beam_m <> 5 then		! None
		if gs_position_beam_m = 1 then		! Single
			ROTz 90
			ADDy -flwl/2
			ADDx -dist_schneid1*NN1
			temp_a = yl+fy/2+dist_schneid1*NN1
			temp_schneid1 = 90
			temp_schneid2 = 45
			gosub "macro_calling"
			DEL 1
			HOTSPOT 0,0, sh, unID  : unID=unID+1
			DEL 2

			ADDy yl+fy/2
			ADDx flwl/2
			temp_a = fx+flwl/2+flwr/2
			temp_schneid1 = 135
			temp_schneid2 = 45
			gosub "macro_calling"

			DEL 2
		else
			ya=fy*(flwl-boff)/flwl
			yb=fy*(boff)/flwl

			ROTz 90
			ADDy -flwl+boff
			ADDx -dist_schneid1*NN1
			MUL 1,-1,1
			temp_a = yl+boff+dist_schneid1*NN1
			temp_schneid1 = 90
			temp_schneid2 = 135
			gosub "macro_calling"
			DEL 1
			DEL 1
				HOTSPOT 0,0, sh, unID  : unID=unID+1
			DEL 2

			ADDy yl+boff
			ADDx flwl-boff
			MUL 1,-1,1
			temp_a = fx+2*boff
			temp_schneid1 = 45
			temp_schneid2 = 135
			gosub "macro_calling"
			DEL 1
			DEL 2

			ROTz 90
			ADDy -boff
			ADDx -dist_schneid1*NN1
			temp_a = yl+fy-boff+dist_schneid1*NN1
			temp_schneid1 = 90
			temp_schneid2 = 45
			gosub "macro_calling"

			DEL 1
				HOTSPOT 0,0, sh, unID  : unID=unID+1
			DEL 2

			ADDy yl+fy-boff
			ADDx boff
			temp_a = fx+flwl+flwr-2*boff
			temp_schneid1 = 135
			temp_schneid2 = 45
			gosub "macro_calling"

			DEL 2

		endif
	endif
	DEL 1
	CUTEND



	ROTx schneid2
	CUTPLANE
	DEL 1

	ADDz -(sh)
	if gs_type_beam_m <> 5 then		! None
		if gs_position_beam_m = 1 then		! Single
			ROTz 90
			ADDy -1*(flwl+fx+flwr/2)
			ADDx yl-yr+dist_schneid2*NN3
			MUL 1,-1,1
			temp_a = yr+fy/2-dist_schneid2*NN3
			temp_schneid1 = 90
			temp_schneid2 = 45
			gosub "macro_calling"
			DEL 1
			HOTSPOT 0,0, sh, unID : unID=unID+1

			DEL 3
		else
			ya=fy*(flwl-boff)/flwl
			yb=fy*(boff)/flwl

			ROTz 90

			ADDy -1*(flwl+fx+flwr-boff)
			ADDx yl-yr+dist_schneid2*NN3
			MUL 1,-1,1
			temp_a = yr+fy-boff-dist_schneid2*NN3
			temp_schneid1 = 90
			temp_schneid2 = 45
			gosub "macro_calling"
			DEL 1
			HOTSPOT 0,0, sh, unID : unID=unID+1
			DEL 2


			ADDy -1*(flwl+fx+boff)
			ADDx yl-yr+dist_schneid2*NN3
			MUL 1,1,1
			temp_a = yr+boff-dist_schneid2*NN3
			temp_schneid1 = 90
			temp_schneid2 = 135
			gosub "macro_calling"
			DEL 1

			HOTSPOT 0,0, sh, unID : unID=unID+1
			DEL 3

		endif
	endif
	DEL 1
	CUTEND


	ADDx flwl
	if gs_type_beam_m <> 5 then		! None

		if gs_position_beam_m = 2 then		! Double
		xx=2
		else
		xx=1
		endif

		FOR k=1 to xx
				if gs_position_beam_m = 1 then		! Single
				ADDx -flwl/2
				else
				ADDx -boff

				endif

					HOTSPOT 		0,	-0.1,	0, 	unID, schneid1, 4+128: unID=unID+1
					HOTSPOT 		0,	0,	0, 			unID, schneid1, 6: unID=unID+1
					HOTSPOT 		0,	-dist_schneid1,	-h/1000, unID, schneid1, 5: unID=unID+1

				DEL 1
		ADDx -flwl+2*boff
		NEXT k
		DEL xx
	endif
	DEL 1


	ADDx flwl+fx+flwr
	if gs_type_beam_m <> 5 then		! None

		if gs_position_beam_m = 2 then		! Double
		xx=2
		else
		xx=1
		endif

		FOR k=1 to xx
				if gs_position_beam_m = 1 then		! Single
				ADDx -flwr/2
				else
				ADDx -boff

				endif

					HOTSPOT 		0,	-0.1,	0, 	unID, schneid2, 4+128: unID=unID+1
					HOTSPOT 		0,	0,	0, 			unID, schneid2, 6: unID=unID+1
					HOTSPOT 		0,	dist_schneid2,	-h/1000, unID, schneid2, 5: unID=unID+1

				DEL 1
		ADDx -flwr+2*boff
		NEXT k
		DEL xx
	endif
	DEL 1

	SECT_FILL typeFillTread, penBackFillTread, penFillTread, penLineTread

	!!! Landing Covering
	MATERIAL matl
	PRISM 9, lth,
		0,0,
		0, yl+fy,
		flwl+fx+flwr, fy+yl,
		flwl+fx+flwr, yl-yr,
		flwl+fx, yl-yr,
		flwl+fx, yl,
		flwl, yl,
		flwl, 0,
		0,0

endif
end

"macro_calling":
	if gs_type_beam_m = 1 then		! I-Beam
		CALL "I-Beam Inclined" PARAMETERS\
			a = temp_a,
			h = h, v = v, s = s, t = t, kip_m = 1, ssch1_m = 2,
			schneid1 = temp_schneid1,
			spitz1 = 0, ssch2_m = 2,
			schneid2 = temp_schneid2,
			spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
	else
		if gs_type_beam_m = 2 then		! if-Beam
			CALL "IF Beam Inclined" PARAMETERS\
				a = temp_a,
				h = h, v = v, s = s, t = t, kip_m = 1, ssch1_m = 2,
				schneid1 = temp_schneid1,
				spitz1 = 0, ssch2_m = 2,
				schneid2 = temp_schneid2,
				spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
		else
			if gs_type_beam_m = 3 then		! IS-Beam
				CALL "IS Beam Inclined" PARAMETERS\
					a = temp_a,
					h = h, v = v, s = s, t = t, kip_m = 1, ssch1_m = 2,
					schneid1 = temp_schneid1,
					spitz1 = 0, ssch2_m = 2,
					schneid2 = temp_schneid2,
					spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
			else
				if gs_type_beam_m = 4 then		! U-Beam
					CALL "U-Beam Inclined" PARAMETERS\
						a = temp_a,
						h = h, v = v, s = s, t = t, kip_m = 1, ssch1_m = 2,
						schneid1 = temp_schneid1,
						spitz1 = 0, ssch2_m = 2,
						schneid2 = temp_schneid2,
						spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
				endif
			endif
		endif
	endif
	return

"macro_calling_rot":
	if gs_type_beam_m = 1 then		! I-Beam
		CALL "I-Beam Inclined" PARAMETERS\
			a = temp_a,
			h = h, v = v, s = s, t = t, rotation = 0, kip_m = 1, ssch1_m = 2,
			schneid1 = temp_schneid1,
			spitz1 = 0, ssch2_m = 2,
			schneid2 = temp_schneid2,
			spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
	else
		if gs_type_beam_m = 2 then		! if-Beam
			CALL "IF Beam Inclined" PARAMETERS\
				a = temp_a,
				h = h, v = v, s = s, t = t, rotation = 0, kip_m = 1, ssch1_m = 2,
				schneid1 = temp_schneid1,
				spitz1 = 0, ssch2_m = 2,
				schneid2 = temp_schneid2,
				spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
		else
			if gs_type_beam_m = 3 then		! IS-Beam
				CALL "IS Beam Inclined" PARAMETERS\
					a = temp_a,
					h = h, v = v, s = s, t = t, rotation = 0, kip_m = 1, ssch1_m = 2,
					schneid1 = temp_schneid1,
					spitz1 = 0, ssch2_m = 2,
					schneid2 = temp_schneid2,
					spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
			else
				if gs_type_beam_m = 4 then		! U-Beam
					CALL "U-Beam Inclined" PARAMETERS\
						a = temp_a,
						h = h, v = v, s = s, t = t, rotation = 0, kip_m = 1, ssch1_m = 2,
						schneid1 = temp_schneid1,
						spitz1 = 0, ssch2_m = 2,
						schneid2 = temp_schneid2,
						spitz2 = 0, schema = schema, gs_cont_pen = gs_cont_pen, mat1 = mat1
				endif
			endif
		endif
	endif
	return
