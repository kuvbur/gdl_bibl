
IF gs_detlevel_3D_m = DLEV3D_OFF THEN END
IF gs_shadow=0 THEN SHADOW OFF
RESOL gs_resol

PEN gs_cont_pen
material mat_ladder


! --- HOTSPOTS -----------------------------------------------------------------
unID=1
HOTSPOT -a/2,0,0,unID   	:unID=unID+1
HOTSPOT a/2,0,0,unID   		:unID=unID+1
HOTSPOT 0,-7",zzyzx,unID   :unID=unID+1

! ---  EDITABLE HOTSPOTS ---

HOTSPOT -a/2,0,zzyzx,unID,conn_oh,1+128   		:unID=unID+1
HOTSPOT -a/2,0,zzyzx+0.1,unID,conn_oh,3   		:unID=unID+1
HOTSPOT -a/2,0,zzyzx-conn_oh,unID,conn_oh,2   	:unID=unID+1

HOTSPOT a/2,0,zzyzx,unID,conn_oh,1+128   		:unID=unID+1
HOTSPOT a/2,0,zzyzx+0.1,unID,conn_oh,3   		:unID=unID+1
HOTSPOT a/2,0,zzyzx-conn_oh,unID,conn_oh,2   	:unID=unID+1

IF iHeadConnType = HEADCONNTYPE_PARAPET THEN
	HOTSPOT -a/2-s_corr,-WallDist,zzyzx,unID,sr_oh,1+128   		:unID=unID+1
	HOTSPOT -a/2-s_corr,-WallDist,zzyzx-0.1,unID,sr_oh,3   		:unID=unID+1
	HOTSPOT -a/2-s_corr,-WallDist,zzyzx+sr_oh,unID,sr_oh,2   		:unID=unID+1

	HOTSPOT a/2+s_corr,-WallDist,zzyzx,unID,sr_oh,1+128   		:unID=unID+1
	HOTSPOT a/2+s_corr,-WallDist,zzyzx-0.1,unID,sr_oh,3   		:unID=unID+1
	HOTSPOT a/2+s_corr,-WallDist,zzyzx+sr_oh,unID,sr_oh,2   		:unID=unID+1
ENDIF

IF iHeadConnType = HEADCONNTYPE_PLATFORM THEN
	HOTSPOT -a/2,wallThickness/2,zzyzx+(wallThickness+14")/2,unID,sr_oh,1+128   		:unID=unID+1
	HOTSPOT -a/2,wallThickness/2,zzyzx+(wallThickness+14")/2-0.1,unID,sr_oh,3   		:unID=unID+1
	HOTSPOT -a/2,wallThickness/2,zzyzx+(wallThickness+14")/2+sr_oh,unID,sr_oh,2   		:unID=unID+1

	HOTSPOT a/2,wallThickness/2,zzyzx+(wallThickness+14")/2,unID,sr_oh,1+128   		:unID=unID+1
	HOTSPOT a/2,wallThickness/2,zzyzx+(wallThickness+14")/2-0.1,unID,sr_oh,3   		:unID=unID+1
	HOTSPOT a/2,wallThickness/2,zzyzx+(wallThickness+14")/2+sr_oh,unID,sr_oh,2   		:unID=unID+1

	HOTSPOT -a/2,wallThickness,zzyzx,unID,conn_oh,1+128   		:unID=unID+1
	HOTSPOT -a/2,wallThickness,zzyzx+0.1,unID,conn_oh,3   		:unID=unID+1
	HOTSPOT -a/2,wallThickness,zzyzx-conn_oh,unID,conn_oh,2   	:unID=unID+1

	HOTSPOT a/2,wallThickness,zzyzx,unID,conn_oh,1+128   		:unID=unID+1
	HOTSPOT a/2,wallThickness,zzyzx+0.1,unID,conn_oh,3   		:unID=unID+1
	HOTSPOT a/2,wallThickness,zzyzx-conn_oh,unID,conn_oh,2   	:unID=unID+1

	HOTSPOT 0,0,zzyzx,unID,wallThickness,1+128  		:unID=unID+1
	HOTSPOT 0,-0.1,zzyzx,unID,wallThickness,3   		:unID=unID+1
	HOTSPOT 0,wallThickness,zzyzx,unID,wallThickness,2   			:unID=unID+1
ENDIF

! --- SIDES --------------------------------------------------------------------

IF iHeadConnType = HEADCONNTYPE_PARAPET THEN
	FOR i=1 to 2
		ADD -a/2+sr_w,-WallDist-sr_d/2,0
		 TUBE 5,6,1+2+16,
			0,0,0,
			sr_d,0,0,
			sr_d,sr_w,0,
			0,sr_w,0,
			0,0,-1,

			0,0,0,0,
			0,0,4",0,
			0,0,zzyzx,0,
			-s_corr,0,zzyzx+s_corr,0,
			-s_corr,0,zzyzx+sr_oh-sr_d/2,0,
			-s_corr,0,zzyzx+sr_oh-sr_d/2+0.1,0

		 ADD -s_corr-sr_w,0,zzyzx+sr_oh-sr_d/2
		 ROTy 90
		 PRISM_ 4,sr_w,
			0,sr_d/2,979,
			0,sr_d,677,
			sr_d/2,180,4077,
			0,0,8
		 DEL 2
		DEL 1
		MULx -1
	NEXT i
	DEL 2
ENDIF

IF iHeadConnType = HEADCONNTYPE_PLATFORM THEN
	ADDx -a/2+sr_w
	ROTx 90
	ROTy -90
	PRISM_ 8,sr_w,
		WallDist-sr_d/2,4",15,
		WallDist+sr_d/2,4",15,
		WallDist+sr_d/2,zzyzx+sr_oh,79,
		-wallThickness-WallDist-sr_d/2,zzyzx+sr_oh,1079,
		-wallThickness-WallDist-sr_d/2,zzyzx-conn_oh-sr_d-2",15,
		-wallThickness-WallDist+sr_d/2,zzyzx-conn_oh-sr_d-2",15,
		-wallThickness-WallDist+sr_d/2,zzyzx+sr_oh,79,
		WallDist-sr_d/2,zzyzx+sr_oh,1079
	ADDz -a+sr_w
	PRISM_ 8,sr_w,
		WallDist-sr_d/2,4",15,
		WallDist+sr_d/2,4",15,
		WallDist+sr_d/2,zzyzx+sr_oh,79,
		-wallThickness-WallDist-sr_d/2,zzyzx+sr_oh,1079,
		-wallThickness-WallDist-sr_d/2,zzyzx-conn_oh-sr_d-2",15,
		-wallThickness-WallDist+sr_d/2,zzyzx-conn_oh-sr_d-2",15,
		-wallThickness-WallDist+sr_d/2,zzyzx+sr_oh,79,
		WallDist-sr_d/2,zzyzx+sr_oh,1079
	DEL 4

	ADD -a/2-sr_w,wallThickness,6"+n_con*dY_c
	MULy -1
	PRISM_ 6,sr_d,
		0,-h_w,15,
		0,-WallDist-sr_d/2,15,
		h_w,-WallDist-sr_d/2,15,
		h_w,0,15,
		-f_w,0,15,
		-f_w,-h_w,15
	DEL 2

	ADD a/2+sr_w,wallThickness,6"+n_con*dY_c
	MULy -1
	MULx -1
	PRISM_ 6,sr_d,
		0,-h_w,15,
		0,-WallDist-sr_d/2,15,
		h_w,-WallDist-sr_d/2,15,
		h_w,0,15,
		-f_w,0,15,
		-f_w,-h_w,15
	DEL 3
ENDIF

! --- FOOT Connection ----------------------------------------------------------

IF iFootConnType = FOOTCONNTYPE_FLOOR THEN  !!! TO the FLOOR
	ADD -a/2+sr_w,-WallDist+sr_d/2,0
	ROTx 90
	PRISM_ 6,sr_d,
		0,h_w,15,
		0,6",15,
		h_w,6",15,
		h_w,0,15,
		-4",0,15,
		-4",h_w,15
	ADDx a-2*sr_W
	MULx -1
	PRISM_ 6,sr_d,
		0,h_w,15,
		0,6",15,
		h_w,6",15,
		h_w,0,15,
		-4",0,15,
		-4",h_w,15
	DEL 4
ENDIF

IF iFootConnType = FOOTCONNTYPE_WALL THEN !!! TO the WALL
	ADD -a/2-sr_w,0,6"
	PRISM_ 6,sr_d,
		0,-h_w,15,
		0,-WallDist-sr_d/2,15,
		h_w,-WallDist-sr_d/2,15,
		h_w,0,15,
		-f_w,0,15,
		-f_w,-h_w,15
	ADDx a+2*sr_w
	MULx -1
	PRISM_ 6,sr_d,
		0,-h_w,15,
		0,-WallDist-sr_d/2,15,
		h_w,-WallDist-sr_d/2,15,
		h_w,0,15,
		-f_w,0,15,
		-f_w,-h_w,15
	DEL 3
ENDIF

! --- Connections --------------------------------------------------------------

ADDz 6"+dY_c
FOR f=1 to n_con
	ADD -a/2-sr_w,0,(f-1)*dY_c
	PRISM_ 6,sr_d,
		0,-h_w,15,
		0,-WallDist-sr_d/2,15,
		h_w,-WallDist-sr_d/2,15,
		h_w,0,15,
		-f_w,0,15,
		-f_w,-h_w,15
	ADDx a+2*sr_w
	MULx -1
	PRISM_ 6,sr_d,
		0,-h_w,15,
		0,-WallDist-sr_d/2,15,
		h_w,-WallDist-sr_d/2,15,
		h_w,0,15,
		-f_w,0,15,
		-f_w,-h_w,15
	DEL 3
NEXT f
DEL 1

! --- STEPS --------------------------------------------------------------------

ADD 0,-WallDist-rw/2, stepp_dis-rw
FOR f=1 to n_foot+1
	ADD -a/2+sr_w,0,(f-1)*dY
	BLOCK a-2*sr_w,rw,rw
	DEL 1
NEXT f
DEL 1

! --- LANDING PLATFORM ---------------------------------------------------------

IF iHeadConnType = HEADCONNTYPE_PLATFORM THEN
	ADDz zzyzx+0.05
	PRISM_ 10,0.05,
		-a/2+sr_w,-WallDist,15,
		a/2-sr_w,-WallDist,15,
		a/2-sr_w,wallThickness+WallDist,15,
		-a/2+sr_w,wallThickness+WallDist,15,
		-a/2+sr_w,-WallDist,-1,

		-a/2+sr_w+1",-WallDist+1",15,
		a/2-sr_w-1",-WallDist+1",15,
		a/2-sr_w-1",wallThickness+WallDist-1",15,
		-a/2+sr_w+1",wallThickness+WallDist-1",15,
		-a/2+sr_w+1",-WallDist+1",-1

	ROTx -90
	n_p = INT((wallThickness+2*WallDist-2")/0.05)
	n_p2= INT((a-2")/0.05)

	ADDz -WallDist
	FOR p=1 to n_p
		ADDz p*0.05
		POLY 4,
			-a/2+sr_w+1",0,
			a/2-sr_w-1",0,
			a/2-sr_w-1",-0.05,
			-a/2+sr_w+1",-0.05
		DEL 1
	NEXT p

	ROTy -90
	ADDz -a/2+sr_w-1"
	FOR p2=1 to n_p2+1
		ADDz p2*0.05
		POLY 4,
			1",0,
			wallThickness+2*WallDist-1",0,
			wallThickness+2*WallDist-1",-0.05,
			1",-0.05
		DEL 1
	NEXT p2
	DEL 2
	DEL 2
ENDIF
DEL TOP
