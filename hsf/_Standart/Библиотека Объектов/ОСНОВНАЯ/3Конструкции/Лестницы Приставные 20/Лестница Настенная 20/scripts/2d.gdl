

cutplaneHeight	= GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1]


PEN gs_cont_pen
FILL gs_fill_type

! --- CHECK for VISIBILITY ---
bInConcrete = 0
if cutplaneHeight < ac_toplevel | not(bStorySensitive) then bInConcrete = 1


!  HOTSPOTS

unID=1
HOTSPOT2 0,0,unID   : unID=unID+1

HOTSPOT2 0,0,unID,temp_RR,1+128, A:unID=unID+1
HOTSPOT2 -a/2,0,unID,temp_RR,2, A:unID=unID+1
HOTSPOT2 0,-1,unID,temp_RR,3, A:unID=unID+1

HOTSPOT2 0,0,unID,temp_RR,1+128, A:unID=unID+1
HOTSPOT2 a/2,0,unID,temp_RR,2, A:unID=unID+1
HOTSPOT2 0,1,unID,temp_RR,3, A:unID=unID+1

HOTSPOT2 0,-B-rw/2,unID   : unID=unID+1

unID = 10
IF iLadderType = LADDERTYPE_FLATBAR THEN
	HOTSPOT2 -a/2-h_w,-B-sr_d/2,unID   : unID=unID+1
	HOTSPOT2 a/2+h_w,-B-sr_d/2,unID   : unID=unID+1
ENDIF

unID = 15
IF iLadderType = LADDERTYPE_LSTEEL THEN
	HOTSPOT2 -a/2-f_w,-B-sr_d/2,unID   : unID=unID+1
	HOTSPOT2 a/2+f_w,-B-sr_d/2,unID   : unID=unID+1
ENDIF

unID = 20
IF iLadderType = LADDERTYPE_CONCRETE & bInConcrete THEN
	HOTSPOT2 -a/2,st,unID   : unID=unID+1
	HOTSPOT2 a/2,st,unID   : unID=unID+1
ENDIF


! SIDEs

IF iLadderType = LADDERTYPE_FLATBAR THEN
	POLY2_B 6, 1+2+4+64,gs_fill_pen, gs_back_pen,
		-a/2-f_w,0,1,
		-a/2-f_w,-h_w,1,
		-a/2-h_w,-h_w,1,
		-a/2-h_w,-B-sr_d/2,1,
		-a/2,-B-sr_d/2,1,
		-a/2,0,1

	POLY2_B 4, 1+2+4+64,gs_fill_pen, gs_back_pen,
		-a/2,-B+sr_d/2,1,
		-a/2+h_w,-B+sr_d/2,1,
		-a/2+h_w,-B-sr_d/2,1,
		-a/2,-B-sr_d/2,1

	POLY2_B 6, 1+2+4+64,gs_fill_pen, gs_back_pen,
		a/2+f_w,0,1,
		a/2+f_w,-h_w,1,
		a/2+h_w,-h_w,1,
		a/2+h_w,-B-sr_d/2,1,
		a/2,-B-sr_d/2,1,
		a/2,0,1

	POLY2_B 4, 1+2+4+64,gs_fill_pen, gs_back_pen,
		a/2,-B+sr_d/2,1,
		a/2-h_w,-B+sr_d/2,1,
		a/2-h_w,-B-sr_d/2,1,
		a/2,-B-sr_d/2,1
ENDIF

IF iLadderType = LADDERTYPE_LSTEEL THEN
	POLY2_B 6, 1+2+4+64,gs_fill_pen, gs_back_pen,
		-a/2-f_w,0,1,
		-a/2-f_w,-h_w,1,
		-a/2-h_w,-h_w,1,
		-a/2-h_w,-B-sr_d/2+h_w,1,
		-a/2,-B-sr_d/2+h_w,1,
		-a/2,0,1

	POLY2_B 6, 1+2+4+64,gs_fill_pen, gs_back_pen,
		-a/2,-B+sr_d/2,1,
		-a/2+h_w,-B+sr_d/2,1,
		-a/2+h_w,-B-sr_d/2,1,
		-a/2-sr_w,-B-sr_d/2,1,
		-a/2-sr_w,-B-sr_d/2+h_w,1,
		-a/2,-B-sr_d/2+h_w,1

	POLY2_B 6, 1+2+4+64,gs_fill_pen, gs_back_pen,
		a/2+f_w,0,1,
		a/2+f_w,-h_w,1,
		a/2+h_w,-h_w,1,
		a/2+h_w,-B-sr_d/2+h_w,1,
		a/2,-B-sr_d/2+h_w,1,
		a/2,0,1

	POLY2_B 6, 1+2+4+64,gs_fill_pen, gs_back_pen,
		a/2,-B+sr_d/2,1,
		a/2-h_w,-B+sr_d/2,1,
		a/2-h_w,-B-sr_d/2,1,
		a/2+sr_w,-B-sr_d/2,1,
		a/2+sr_w,-B-sr_d/2+h_w,1,
		a/2,-B-sr_d/2+h_w,1
ENDIF

! STEPs

IF iLadderType = LADDERTYPE_FLATBAR or iLadderType = LADDERTYPE_LSTEEL THEN
	POLY2_B 4, 1+2+4+64,gs_fill_pen, gs_back_pen,
		-a/2+h_w,-B+rw/2,1,
		-a/2+h_w,-B-rw/2,1,
		a/2-h_w,-B-rw/2,1,
		a/2-h_w,-B+rw/2,1
ENDIF

IF iLadderType = LADDERTYPE_CONCRETE THEN
	rBend = min(2", a/2-rw*1.1)
	POLY2_B 12, 1+2+4+64,gs_fill_pen, gs_back_pen,
		-a/2,         0,1,
		-a/2+rw,      0,1,
		-a/2+rw,      -B+rBend+rw/2,1,
		-a/2+rw+rBend,-B+rw/2,1001,
		a/2-rw-rBend, -B+rw/2,1,
		a/2-rw,       -B+rBend+rw/2,1001,
		a/2-rw,       0,1,
		a/2,          0,1,
		a/2,          -B+rBend+rw/2,1,
		a/2-rw-rBend, -B-rw/2,1001,
		-a/2+rBend+rw,-B-rw/2,1,
		-a/2,         -B+rBend+rw/2,1001

	if bInConcrete then
		LINE_TYPE dash_line
		POLY2_B 4, 1+4+64,gs_fill_pen, gs_back_pen,
			-a/2,0,1,
			-a/2+rw,0,1,
			-a/2+rw,st,1,
			-a/2,st,1

		POLY2_B 4, 1+4+64,gs_fill_pen, gs_back_pen,
			a/2,0,1,
			a/2-rw,0,1,
			a/2-rw,st,1,
			a/2,st,1
	endif
ENDIF


