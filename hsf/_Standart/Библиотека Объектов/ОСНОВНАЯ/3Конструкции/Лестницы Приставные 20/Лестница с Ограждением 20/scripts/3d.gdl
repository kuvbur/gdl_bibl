

IF gs_detlevel_3D_m = DLEV3D_OFF THEN END
IF gs_shadow=0 THEN SHADOW OFF
RESOL gs_resol

PEN gs_cont_pen
MATERIAL mat_ladder

! --- HOTSPOTS ---

UnID=1
HOTSPOT a/2-4",0,0,UnID 		:UnID=UnID+1
HOTSPOT -a/2+4",0,0,UnID 		:UnID=UnID+1
UnID=UnID+1	! because a/2-4",0,zzyzx hotspot became editable, with the same fix ID (3)
UnID=UnID+1	! because -a/2+4",0,zzyzx hotspot became editable, with the same fix ID (4)

! --- EDITABLE HOTSPOTS ---

HOTSPOT -a/2+4",-7",zzyzx,UnID,sr_oh,1+128 		:UnID=UnID+1
HOTSPOT -a/2+4",-7",zzyzx-0.1,UnID,sr_oh,3 		:UnID=UnID+1
HOTSPOT -a/2+4",-7",zzyzx+sr_oh,UnID,sr_oh,2 	:UnID=UnID+1

HOTSPOT a/2-4",-7",zzyzx,UnID,sr_oh,1+128 	:UnID=UnID+1
HOTSPOT a/2-4",-7",zzyzx-0.1,UnID,sr_oh,3 	:UnID=UnID+1
HOTSPOT a/2-4",-7",zzyzx+sr_oh,UnID,sr_oh,2 :UnID=UnID+1

HOTSPOT 0,0,zzyzx,UnID,lnd_w,1+128 			:UnID=UnID+1
HOTSPOT 0,-0.1,zzyzx,UnID,lnd_w,3 			:UnID=UnID+1
HOTSPOT 0,lnd_w,zzyzx,UnID,lnd_w,2 			:UnID=UnID+1

HOTSPOT -a/2+4"-sr_w/2,-7"+sr_d/2,zzyzx+sr_oh,UnID,tr_oh,1+128 	:UnID=UnID+1
HOTSPOT -a/2+4"-sr_w/2,-7"+sr_d/2-0.1,zzyzx+sr_oh,UnID,tr_oh,3 	:UnID=UnID+1
HOTSPOT -a/2+4"-sr_w/2,-7"+sr_d/2+tr_oh,zzyzx+sr_oh,UnID,tr_oh,2 	:UnID=UnID+1

HOTSPOT a/2-4"+sr_w/2,-7"+sr_d/2,zzyzx+sr_oh,UnID,tr_oh,1+128 	:UnID=UnID+1
HOTSPOT a/2-4"+sr_w/2,-7"+sr_d/2-0.1,zzyzx+sr_oh,UnID,tr_oh,3 	:UnID=UnID+1
HOTSPOT a/2-4"+sr_w/2,-7"+sr_d/2+tr_oh,zzyzx+sr_oh,UnID,tr_oh,2 	:UnID=UnID+1

!using fix unID, because there were fix hotspots before and their unID was 3 and 4

hotspot  a/2-4",0,0,		unID, 	zzyzx, 1+128	:UnID=UnID+1
hotspot  a/2-4",0,zzyzx,	3,		zzyzx, 2		! Fix ID (3)
hotspot  a/2-4",0,-0.1,		unID, 	zzyzx, 3		:UnID=UnID+1

hotspot  -a/2+4",0,0,		unID, 	zzyzx, 1+128	:UnID=UnID+1
hotspot  -a/2+4",0,zzyzx,	4, 		zzyzx, 2		! Fix ID (4)
hotspot  -a/2+4",0,-0.1,	unID, 	zzyzx, 3		:UnID=UnID+1



! --- SIDE RAIL ---

ADDz 4"
PRISM_ 4,zzyzx-4"+sr_oh,
	a/2-4",-7"+sr_d/2,15,
	a/2-4"+sr_w,-7"+sr_d/2,15,
	a/2-4"+sr_w,-7"-sr_d/2,15,
	a/2-4",-7"-sr_d/2,15

PRISM_ 4,zzyzx-4"+sr_oh,
	-a/2+4",-7"+sr_d/2,15,
	-a/2+4"-sr_w,-7"+sr_d/2,15,
	-a/2+4"-sr_w,-7"-sr_d/2,15,
	-a/2+4",-7"-sr_d/2,15
DEL 1


! --- FOOT Connection ---

IF iFootConnType = FOOTCONNTYPE_FLOOR THEN  !!! TO the FLOOR
	ADD -a/2+4",-7"+sr_d/2,0
	ROTx 90
	PRISM_ 6,sr_d,
		0,h_w,15,
		0,6",15,
		h_w,6",15,
		h_w,0,15,
		-4",0,15,
		-4",h_w,15
	ADDx a-8"
	MULx -1
	PRISM_ 6,sr_d,
		0,h_w,15,
		0,6",15,
		h_w,6",15,
		h_w,0,15,
		-4",0,15,
		-4",h_w,15
	DEL 4
ENDIF

IF iFootConnType = FOOTCONNTYPE_WALL THEN !!! TO the WALL
	ADD -a/2+4",0,6"
	PRISM_ 6,sr_d,
		0,0,15,
		0,-7"-sr_d/2,15,
		h_w,-7"-sr_d/2,15,
		h_w,-h_w,15,
		f_w,-h_w,15,
		f_w,0,15
	ADDx a-8"
	MULx -1
	PRISM_ 6,sr_d,
		0,0,15,
		0,-7"-sr_d/2,15,
		h_w,-7"-sr_d/2,15,
		h_w,-h_w,15,
		f_w,-h_w,15,
		f_w,0,15
	DEL 3
ENDIF


! --- STEPS ---

ADD -a/2+4",-7"-sr_d/4,0
FOR s=1 to n_s
	ADDz s*dz-rw-0.001
	 BLOCK a-8",sr_d/2,rw
	DEL 1
next s
DEL 1

if zzyzx>=saftyLimit then

! --- VERTICAL CAGEs ---

	addz sr_bl
	prism_ 4,zzyzx-sr_bl+sr_oh,
		-a/2+h_w,-13",15,
		-a/2+h_w,-13"-vc_d,15,
		-a/2+h_w+vc_w,-13"-vc_d,15,
		-a/2+h_w+vc_w,-13",15

	prism_ 4,zzyzx-sr_bl+sr_oh,
		a/2-h_w,-13",15,
		a/2-h_w,-13"-vc_d,15,
		a/2-h_w-vc_w,-13"-vc_d,15,
		a/2-h_w-vc_w,-13",15

	prism_ 4,zzyzx-sr_bl+sr_oh,
		-a/2+h_w,-19",15,
		-a/2+h_w,-19"-vc_d,15,
		-a/2+h_w+vc_w,-19"-vc_d,15,
		-a/2+h_w+vc_w,-19",15

	prism_ 4,zzyzx-sr_bl+sr_oh,
		a/2-h_w,-19",15,
		a/2-h_w,-19"-vc_d,15,
		a/2-h_w-vc_w,-19"-vc_d,15,
		a/2-h_w-vc_w,-19",15
	del 1

	addy -2'1/4"-a/2
	tube 4,5,1+2+16+32,
		-vc_d/2,0,0,
		-vc_d/2,vc_w,0,
		vc_d/2,vc_w,0,
		vc_d/2,0,0,

		0,0,0,0,
		0,0,sr_bl,0,
		0,5",z_temp,0,
		0,5",zzyzx+sr_oh,0,
		0,5",zzyzx+sr_oh+0.1,0
	DEL 1

	addy -2'1/2"
	rotz 55
	addy -a/2+vc_w
	tube 4,5,1+2+16+32,
		-vc_d/2,0,0,
		-vc_d/2,vc_w,0,
		vc_d/2,vc_w,0,
		vc_d/2,0,0,

		0,0,0,0,
		0,0,sr_bl,0,
		0,dL,z_temp,alfa,
		0,dL,zzyzx+sr_oh,alfa,
		0,dL,zzyzx+sr_oh+0.1,alfa
	del 3

	addy -2'
	mulx -1
	rotz 55
	addy -a/2
	tube 4,5,1+2+16+32,
		-vc_d/2,0,0,
		-vc_d/2,vc_w,0,
		vc_d/2,vc_w,0,
		vc_d/2,0,0,

		0,0,0,0,
		0,0,sr_bl,0,
		0,dL,z_temp,alfa,
		0,dL,zzyzx+sr_oh,alfa,
		0,dL,zzyzx+sr_oh+0.1,alfa
	del 4


! --- HOOP ---

	addz sr_bl
	prism_ 20,2",
		a/2-4"-f_w,0,15,
		a/2-4",0,15,
		a/2-4",-7"-sr_d/2,15,
		a/2,-12",15,
		a/2,-3'2"+1'1/2",79,
		-a/2,-3'2"+1'1/2",1079,
		-a/2,-12",15,
		-a/2+4",-7"-sr_d/2,15,
		-a/2+4",0,79,
		-a/2+4"+f_w,0,15,

		-a/2+4"+f_w,-h_w,15,
		-a/2+4"+h_w,-h_w,15,
		-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
		-a/2+h_w,-12"-h_w/2,15,
		-a/2+h_w,-3'2"+1'1/2",79,
		a/2-h_w,-3'2"+1'1/2",1079,
		a/2-h_w,-12"-h_w/2,15,
		a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
		a/2-4"-h_w,-h_w,15,
		a/2-4"-f_w,-h_w,15

	addz 4'
	for i=1 to n_hoop
		addz (i-1)*h_dis
		prism_ 20,2",
			a/2-4"-f_w,0,15,
			a/2-4",0,15,
			a/2-4",-7"-sr_d/2,15,
			a/2,-12",15,
			a/2,-2'9"+1'1/2",79,
			-a/2,-2'9"+1'1/2",1079,
			-a/2,-12",15,
			-a/2+4",-7"-sr_d/2,15,
			-a/2+4",0,79,
			-a/2+4"+f_w,0,15,

			-a/2+4"+f_w,-h_w,15,
			-a/2+4"+h_w,-h_w,15,
			-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
			-a/2+h_w,-12"-h_w/2,15,
			-a/2+h_w,-2'9"+1'1/2",79,
			a/2-h_w,-2'9"+1'1/2",1079,
			a/2-h_w,-12"-h_w/2,15,
			a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
			a/2-4"-h_w,-h_w,15,
			a/2-4"-f_w,-h_w,15
		del 1
	next i
	del 2

	addz zzyzx-2"
	prism_ 20,2",
		a/2-4"-f_w,0,15,
		a/2-4",0,15,
		a/2-4",-7"-sr_d/2,15,
		a/2,-12",15,
		a/2,-2'9"+1'1/2",79,
		-a/2,-2'9"+1'1/2",1079,
		-a/2,-12",15,
		-a/2+4",-7"-sr_d/2,15,
		-a/2+4",0,79,
		-a/2+4"+f_w,0,15,

		-a/2+4"+f_w,-h_w,15,
		-a/2+4"+h_w,-h_w,15,
		-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
		-a/2+h_w,-12"-h_w/2,15,
		-a/2+h_w,-2'9"+1'1/2",79,
		a/2-h_w,-2'9"+1'1/2",1079,
		a/2-h_w,-12"-h_w/2,15,
		a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
		a/2-4"-h_w,-h_w,15,
		a/2-4"-f_w,-h_w,15


	! --- RAIL on the TOP ---

	addz sr_oh
	prism_ 16,2",
		a/2-4",-7"+sr_d/2,15,
		a/2-4",-7"-sr_d/2,15,
		a/2,-12",15,
		a/2,-2'9"+1'1/2",79,
		-a/2,-2'9"+1'1/2",1079,
		-a/2,-12",15,
		-a/2+4",-7"-sr_d/2,15,
		-a/2+4",-7"+sr_d/2,15,

		-a/2+4"+h_w,-7"+sr_d/2,15,
		-a/2+4"+h_w,-7"-sr_d/2-h_w/2,15,
		-a/2+h_w,-12"-h_w/2,15,
		-a/2+h_w,-2'9"+1'1/2",79,
		a/2-h_w,-2'9"+1'1/2",1079,
		a/2-h_w,-12"-h_w/2,15,
		a/2-4"-h_w,-7"-sr_d/2-h_w/2,15,
		a/2-4"-h_w,-7"+sr_d/2,15
	del 2
else
	addz sr_bl

	addx -a/2+4"
	prism_ 6, 2",
		0,0,15,
		0,-7"-sr_d/2,15,
		h_w,-7"-sr_d/2,15,
		h_w,-h_w,15,
		f_w,-h_w,15,
		f_w,0,15
	addx a-8"
	mulx -1
	prism_ 6, 2",
		0,0,15,
		0,-7"-sr_d/2,15,
		h_w,-7"-sr_d/2,15,
		h_w,-h_w,15,
		f_w,-h_w,15,
		f_w,0,15
	del 3

	addz 4'
	for i=1 to n_hoop
		addz (i-1)*4'
		addx -a/2+4"
		prism_ 6, 2",
			0,0,15,
			0,-7"-sr_d/2,15,
			h_w,-7"-sr_d/2,15,
			h_w,-h_w,15,
			f_w,-h_w,15,
			f_w,0,15
		addx a-8"
		mulx -1
		prism_ 6, 2",
			0,0,15,
			0,-7"-sr_d/2,15,
			h_w,-7"-sr_d/2,15,
			h_w,-h_w,15,
			f_w,-h_w,15,
			f_w,0,15
		del 4
	next i
	del 2

	addz zzyzx-2"
	addx -a/2+4"
	prism_ 6, 2",
		0,0,15,
		0,-7"-sr_d/2,15,
		h_w,-7"-sr_d/2,15,
		h_w,-h_w,15,
		f_w,-h_w,15,
		f_w,0,15
	addx a-8"
	mulx -1
	prism_ 6, 2",
		0,0,15,
		0,-7"-sr_d/2,15,
		h_w,-7"-sr_d/2,15,
		h_w,-h_w,15,
		f_w,-h_w,15,
		f_w,0,15
	del 4
endif

IF top_rail THEN
	ADD a/2-4",-7"+sr_d/2,zzyzx
	ROTx 90
	ROTy 90
	 PRISM_ 8,sr_w,
		0,0.3,15,
		tr_oh,0.3,15,
		tr_oh,sr_oh,15,
		0,sr_oh,15,
		0,sr_oh-sr_w,15,
		tr_oh-sr_w,sr_oh-sr_w,15,
		tr_oh-sr_w,0.3+sr_w,15,
		0,0.3+sr_w,15

	IF sr_oh-0.3>0.4 THEN
	 ADDy 0.3+(sr_oh-0.4)/2
	  BLOCK  tr_oh-sr_w,sr_w,sr_w
	 DEL 1
	ENDIF

	ADDz -a+4"+sr_w
	 PRISM_ 8,sr_w,
		0,0.3,15,
		tr_oh,0.3,15,
		tr_oh,sr_oh,15,
		0,sr_oh,15,
		0,sr_oh-sr_w,15,
		tr_oh-sr_w,sr_oh-sr_w,15,
		tr_oh-sr_w,0.3+sr_w,15,
		0,0.3+sr_w,15

	IF sr_oh-0.3>0.3 THEN
	 ADDy 0.3+(sr_oh-0.3)/2
	  BLOCK  tr_oh-sr_w,sr_w,sr_w
	 DEL 1
	ENDIF
	DEL 4
ENDIF


! --- LANDING Platform ---

IF landing THEN
	ADDz zzyzx-0.05
	PRISM_ 10,0.05,
		-a/2+4",0,15,
		a/2-4",0,15,
		a/2-4",lnd_w,15,
		-a/2+4",lnd_w,15,
		-a/2+4",0,-1,

		-a/2+5",1",15,
		a/2-5",1",15,
		a/2-5",lnd_w-1",15,
		-a/2+5",lnd_w-1",15,
		-a/2+5",1",-1

	ROTx -90
	n_p = INT((lnd_w-2")/0.05)
	n_p2= INT((a-8"-2")/0.05)

	FOR p=1 to n_p+1
		ADDz p*0.05
		POLY 4,
			-a/2+5",0,
			a/2-5",0,
			a/2-5",-0.05,
			-a/2+5",-0.05
		DEL 1
	NEXT p

	ROTy -90
	ADDz -a/2+4"
	FOR p2=1 to n_p2+1
		ADDz p2*0.05
		POLY 4,
			1",0,
			lnd_w-1",0,
			lnd_w-1",-0.05,
			1",-0.05
		DEL 1
	NEXT p2
	DEL 2
	DEL 2
ENDIF

