
IF gs_detlevel_3D_m = DLEV3D_SIMPLE THEN iTreed = 1	! Simple
IF gs_detlevel_3D_m = DLEV3D_DETAILED THEN iTreed = 2	! Detailed

IF gs_detlevel_3D_m = DLEV3D_OFF THEN end
IF gs_detlevel_3D_m = DLEV3D_SIMPLE THEN gs_resol = 4

IF (GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40) THEN gs_detlevel_3D_m = DLEV3D_SIMPLE	!!Simple

IF gs_shadow=0 THEN SHADOW OFF

PEN gs_cont_pen
MATERIAL mat_ladder

ADD -ladderLength, -gs_ladder_width/2, 0

!!!!************************
!!!!************************  HOTSPOTS
!!!!************************
unID=1
!HOTSPOT 0,0,0,unID    		:unID=unID+1


!!!!************************
!!!!************************  EDITABLE HOTSPOTS
!!!!************************!


HOTSPOT 0,	 gs_ladder_width/2,0,unID,gs_ladder_width,1	: unID=unID+1
HOTSPOT 0,	 gs_ladder_width+1,0,unID,gs_ladder_width,3	: unID=unID+1
HOTSPOT 0,	-gs_ladder_width/2,0,unID,gs_ladder_width,2	: unID=unID+1


HOTSPOT ladderLength,	gs_ladder_width/2,	zzyzx,unID,B,1	: unID=unID+1
HOTSPOT ladderLength,	gs_ladder_width/2+1,zzyzx,unID,B,3	: unID=unID+1
HOTSPOT ladderLength,	gs_ladder_width/2-B,zzyzx,unID,B,2	: unID=unID+1

HOTSPOT A,	gs_ladder_width/2,	zzyzx,unID,B,1	: unID=unID+1
HOTSPOT A,	gs_ladder_width/2+1,zzyzx,unID,B,3	: unID=unID+1
HOTSPOT A,	gs_ladder_width/2-B,zzyzx,unID,B,2	: unID=unID+1

HOTSPOT ladderLength,0,0,unID,zzyzx,1+128      :unID=unID+1
HOTSPOT ladderLength,0,-0.1,unID,zzyzx,3       :unID=unID+1
HOTSPOT ladderLength,0,zzyzx,unID,zzyzx,2      :unID=unID+1


IF hr_p THEN
	ADDz zzyzx
	 HOTSPOT A+hr_d/2,-B+gs_ladder_width/2-hr_d/2,0,unID,hr_h,1+128    :unID=unID+1
	 HOTSPOT A+hr_d/2,-B+gs_ladder_width/2-hr_d/2,-0.1,unID,hr_h,3     :unID=unID+1
	 HOTSPOT A+hr_d/2,-B+gs_ladder_width/2-hr_d/2,hr_h,unID,hr_h,2   :unID=unID+1
	DEL 1
ENDIF

ADDz dY
 IF iHandrailPos = HANDRAILPOS_RIGHT or iHandrailPos = HANDRAILPOS_BOTH THEN
	HOTSPOT 2",-gs_ladder_width/2-hr_d/2,0,unID,hr_h,1+128    :unID=unID+1
	HOTSPOT 2",-gs_ladder_width/2-hr_d/2,-0.1,unID,hr_h,3     :unID=unID+1
	HOTSPOT 2",-gs_ladder_width/2-hr_d/2,hr_h,unID,hr_h,2   :unID=unID+1
 ENDIF

 IF iHandrailPos = HANDRAILPOS_LEFT or iHandrailPos = HANDRAILPOS_BOTH THEN
	HOTSPOT 2", gs_ladder_width/2-hr_d,0,unID,hr_h,1+128    :unID=unID+1
	HOTSPOT 2", gs_ladder_width/2-hr_d,-0.1,unID,hr_h,3     :unID=unID+1
	HOTSPOT 2", gs_ladder_width/2-hr_d,hr_h,unID,hr_h,2   :unID=unID+1
 ENDIF
DEL 1

hotspot ladderLength,						gs_ladder_width/2-B, ZZYZX, unID, gs_platform_length, 1+256	: unID=unID+1
hotspot ladderLength-1,						gs_ladder_width/2-B, ZZYZX, unID, gs_platform_length, 3		: unID=unID+1
hotspot ladderLength+gs_platform_length,	gs_ladder_width/2-B, ZZYZX, unID, gs_platform_length, 2		: unID=unID+1


!!!!************************
!!!!************************  SIDE RAILs
!!!!************************

ADDy -gs_ladder_width/2
ROTz 90
FOR i=1 to 2

	!!!!!!!****************************** ACCESSORIES
	IF iTreed = 2 THEN	! Detailed
		if i=2 THEN
			MULx -1
			ADDx -sr_t
		ENDIF
		MULx -1
		ROTx 90
		ADDz 1"

		PRISM_ 6,4",
			0,0,15,
			f_w,0,15,
			f_w,f_t,15,
			f_t,f_t,15,
			f_t,f_d,15,
			0,f_d,15
		IF i=2 THEN DEL 2

		DEL 3
	ENDIF


	 CUTPLANE 180
	ROTx 90
	 CUTPLANE 180
	DEL 1

	ADD 0,-ladderLength,zzyzx
	 CUTPLANE 90
	DEL 1

	ADDz zzyzx
	 CUTPLANE
	DEL 1

	ADDy -6"
	ROTx 90-alfa
	ADDz -6"*sin(90-alfa)
	PRISM_ 8,zzyzx/sin(alfa),
		0,0,15,
		sr_d,0,15,
		sr_d,sr_t,15,
		sr_t,sr_t,15,
		sr_t,sr_w-sr_t,15,
		sr_d,sr_w-sr_t,15,
		sr_d,sr_w,15,
		0,sr_w,15
	DEL 3
	 CUTEND
	 CUTEND
	 CUTEND
	 CUTEND

	ADDx gs_ladder_width
	MULx -1
NEXT i
DEL 4
DEL 2

!!!!************************
!!!!************************  STEPS
!!!!************************


GROUP "LadderStep"
	!!!!!!!****************************** SIMPLE STEPS
	IF iTreed = 1 THEN
	  PRISM_ 5,st_h,
		-gs_ladder_width/2+sr_t,	0,		15,
		-gs_ladder_width/2+sr_t,	st_w,	15,
		 gs_ladder_width/2-sr_t,	st_w,	15,
		 gs_ladder_width/2-sr_t,	0,		15,
		-gs_ladder_width/2+sr_t,	0,		-1
	ENDIF

	!!!!!!!****************************** DETAILED STEPS
	IF iTreed = 2 THEN
	  PRISM_ 10,st_h,
		-gs_ladder_width/2+sr_t,	0,		15,
		-gs_ladder_width/2+sr_t,	st_w,	15,
		 gs_ladder_width/2-sr_t,	st_w,	15,
		 gs_ladder_width/2-sr_t,	0,		15,
		-gs_ladder_width/2+sr_t,	0,		-1,

		-gs_ladder_width/2+sr_t+st_t,	st_t,		15,
		-gs_ladder_width/2+sr_t+st_t,	st_w-st_t,	15,
		 gs_ladder_width/2-sr_t-st_t,	st_w-st_t,	15,
		 gs_ladder_width/2-sr_t-st_t,	st_t,		15,
		-gs_ladder_width/2+sr_t+st_t,	st_t,		-1

	  ROTx 90
	  ADDz -st_t
	  FOR p=1 to npY-1
		ADDz -dpY
		 POLY 4,
			-gs_ladder_width/2+sr_t+st_t,	0,
			-gs_ladder_width/2+sr_t+st_t,	st_h,
			 gs_ladder_width/2-sr_t-st_t,	st_h,
			 gs_ladder_width/2-sr_t-st_t,	0
	  NEXT p
	  DEL 2 + (npY-1)

	  ROTz 90
	  ROTx 90
	  ADDz -gs_ladder_width/2+sr_t+st_t
	  FOR p=1 to npX-1
		ADDz dpX
		 POLY 4,
			st_t,		0,
			st_t,		st_h,
			st_w-st_t,	st_h,
			st_w-st_t,	0
	  NEXT p
	  DEL 3 + (npX-1)
	ENDIF
ENDGROUP

ROTz -90
ADD 0, ladderLength - n_st * dX, -st_h

FOR s=1 TO n_st - 1
	ADD 0,dX,dY
	PLACEGROUP "LadderStep"
NEXT s
DEL 2 + n_st - 1


!!!!************************
!!!!************************  HANDRAIL
!!!!************************

RESOL gs_resol

halfD = hr_d/2

IF iHandrailPos = HANDRAILPOS_LEFT or iHandrailPos = HANDRAILPOS_BOTH THEN
	ADD 2",gs_ladder_width/2-hr_d,0

	TUBE 2,4,1+2+16+32,
		0,0,901,
		halfD,360,4001,

		0-dX,0,hr_h,0,
		0,0,hr_h+dY,0,

		(n_st-1) * dX,	0,zzyzx+hr_h,0,
		n_st * dX,	0,zzyzx+hr_h+dY,0

	DEL 1
ENDIF

IF iHandrailPos = HANDRAILPOS_RIGHT or iHandrailPos = HANDRAILPOS_BOTH THEN

	numPosts = CEIL(((n_st-1) * dX) / postDist)
	postRealDist = ((n_st-1) * dX) / numPosts

	ADD 2",-gs_ladder_width/2-hr_d/2,0

	PUT	0,0,0,0,
		0,0,2",0,
		0,0,hr_h+dY,0,
		(n_st-1) * dX,	0,zzyzx+hr_h,0

	IF ABS(B-gs_ladder_width) < EPS THEN
		IF hr_p THEN
			PUT	ladderLength-2"-halfD,0,zzyzx+hr_h,0,
				ladderLength+1,0,zzyzx+hr_h,0
		ELSE
			PUT	ladderLength-2"-halfD+dX,0,zzyzx+hr_h+dY,0
		ENDIF
	ELSE
		IF hr_p THEN
			PUT	ladderLength-2"-halfD,0,zzyzx+hr_h,0,
				ladderLength,-1,zzyzx+hr_h,0
		ELSE
			PUT	ladderLength-2"-halfD+dX,0,zzyzx+hr_h+dY,0
		ENDIF
	ENDIF

	TUBE 2,NSP/4,1+2+16+32,
		0,0,901,
		halfD,360,4001,
		GET(NSP)

	ADDz hr_h+dY
	FOR i=1 TO numPosts

		ADDz -hr_h/2
		 ARMC halfD, halfD, postRealDist/COS(alfa), 0, 0, 90-alfa
		DEL 1

		ADD postRealDist,0,postRealDist*TAN(alfa)

		ROTz 180
		ROTy 90+alfa
		 ARMC halfD, halfD, hr_h+dY, 0, 0, 90-alfa
		DEL 2

	NEXT i
	DEL 2 + numPosts


!!!!!!!*********************  Accessories (only in detailed mode)
	IF iTreed = 2 THEN	! Detailed
		showLeft = 1
		showRight = 1

		ADD 2",-gs_ladder_width/2,2"
		MULy -1
		FOR i=1 TO numPosts+1
			GOSUB 200
			ADD postRealDist, 0, postRealDist*TAN(alfa)
		NEXT i
		DEL 2 + (numPosts+1)
	ENDIF
ENDIF



!!!!************************
!!!!************************  PLATFORM
!!!!************************

ROTz -90
ADD 0, ladderLength, ZZYZX-pl_h

BODY -1

! --- Platform Frame
ADD -gs_ladder_width/2, 0, -plf_t
TUBE 8,7, 1+2+16+32+64,
	plf_t,0, 0,
	plf_t,-plf_h+plf_t, 0,
	0,-plf_h+plf_t, 0,
	0,plf_h, 0,
	plf_t, plf_h, 0,
	plf_t, plf_t, 0,
	plf_w, plf_t, 0,
	plf_w, 0, 0,

	0,1,0, 0,
	0,0,0, 0,
	B, 0,0, 0,
	B, gs_platform_length,0, 0,
	0, gs_platform_length,0, 0,
	0,0,0, 0,
	1,0,0, 0

! --- Platform Support
ADDz -B
ROTy 45
ROTz 90
IF iTreed = 1 THEN
	GOSUB 100
	ADDx gs_platform_length - plf_w
	GOSUB 100
	DEL 1
ENDIF
IF iTreed = 2 THEN
	GOSUB 101
	ADDx gs_platform_length - plf_w
	GOSUB 101
	DEL 1
ENDIF
DEL 3
DEL 1

!!!!!!!****************************** SIMPLE PLATFORM
IF iTreed = 1 THEN
  PRISM_ 5,pl_h,
	-gs_ladder_width/2+plf_t,	plf_t,15,
	-gs_ladder_width/2+plf_t,	gs_platform_length-plf_t,15,
	-gs_ladder_width/2+B-plf_t,	gs_platform_length-plf_t,15,
	-gs_ladder_width/2+B-plf_t,	plf_t,15,
	-gs_ladder_width/2+plf_t,	plf_t,-1
ENDIF

!!!!!!!****************************** DETAILED PLATFORM
IF iTreed = 2 THEN
  PRISM_ 10,pl_h,
	-gs_ladder_width/2+plf_t,	plf_t,15,
	-gs_ladder_width/2+plf_t,	gs_platform_length-plf_t,15,
	-gs_ladder_width/2+B-plf_t,	gs_platform_length-plf_t,15,
	-gs_ladder_width/2+B-plf_t,	plf_t,15,
	-gs_ladder_width/2+plf_t,	plf_t,-1,

	-gs_ladder_width/2+plf_t+st_t,		plf_t+st_t,15,
	-gs_ladder_width/2+plf_t+st_t,		gs_platform_length-plf_t-st_t,15,
	-gs_ladder_width/2+B-plf_t-st_t,	gs_platform_length-plf_t-st_t,15,
	-gs_ladder_width/2+B-plf_t-st_t,	plf_t+st_t,15,
	-gs_ladder_width/2+plf_t+st_t,		plf_t+st_t,-1

	  ROTx 90
	  ADDz -plf_t-st_t
	  FOR p=1 to npYP-1
		ADDz -dpYP
		 POLY 4,
			-gs_ladder_width/2+plf_t+st_t,0,
			-gs_ladder_width/2+plf_t+st_t,st_h,
			-gs_ladder_width/2+B-plf_t-st_t,st_h,
			-gs_ladder_width/2+B-plf_t-st_t,0
	  NEXT p
	  DEL 2 + (npYP-1)

	  ROTz 90
	  ROTx 90
	  ADDz -gs_ladder_width/2+plf_t+st_t
	  FOR p=1 to npXP-1
		ADDz dpXP
		 POLY 4,
			plf_t+st_t,						0,
			plf_t+st_t,						st_h,
			gs_platform_length-plf_t-st_t,	st_h,
			gs_platform_length-plf_t-st_t,	0
	  NEXT p
	  DEL 3 + (npXP-1)

ENDIF

DEL 2

!!!!************************
!!!!************************  PLATFORM RAIL
!!!!************************

IF hr_p THEN
	halfD = hr_d/2

	ADD ladderLength, gs_ladder_width/2, ZZYZX

	IF iHandrailPos = HANDRAILPOS_RIGHT or iHandrailPos = HANDRAILPOS_BOTH THEN
		PUT	-halfD-1,	-gs_ladder_width-halfD, hr_h,	0,
			-halfD,		-gs_ladder_width-halfD,	hr_h,	0,
			-halfD,		-B-halfD,				hr_h,	0
	ELSE
		IF B-gs_ladder_width > 0.1 THEN
			PUT	-halfD,		-gs_ladder_width-halfD,	-1,		0,
				-halfD,		-gs_ladder_width-halfD,	-plf_w,	0,
				-halfD,		-gs_ladder_width-halfD,	 hr_h,	0,
				-halfD,		-B-halfD,				 hr_h,	0
		ELSE
			PUT -halfD-1,	-B-halfD,	hr_h,	0,
				-halfD,		-B-halfD,	hr_h,	0
		ENDIF
	ENDIF


	PUT	gs_platform_length+halfD,-B-halfD,hr_h,0,
		gs_platform_length+halfD,0,hr_h,0,
		gs_platform_length+halfD,1,hr_h,0


	TUBE 2,NSP/4,1+2+16+32,
		0,0,901,
		halfD,360,4001,
		GET(NSP)


	ADD plf_w/2, -B-halfD,hr_h
	ROTy 90

	numPosts = CEIL((gs_platform_length - plf_w) / postDist)
	postRealDist = (gs_platform_length - plf_w) / numPosts

	FOR i = 1 TO numPosts + 1
		IF i > 1 OR ABS(B-gs_ladder_width > EPS) OR (iHandrailPos = HANDRAILPOS_LEFT or iHandrailPos = HANDRAILPOS_NONE) THEN
			ARMC halfD, halfD, hr_h+plf_w, 0, 0, 90

			IF i>1 THEN
				ADDx hr_h/2
				ROTy 90

				if i = 2 & not(_bShowRightRailOnCorner) then
					ARMC halfD, halfD, postRealDist + (ladderLength - (n_st - 1) * dX - 2") + plf_w / 2, 0, 0, 90
				else
					ARMC halfD, halfD, postRealDist, 0, 0, 90
				endif

				DEL 2
			ENDIF

			IF iTreed = 2 THEN	! Detailed
				ADD hr_h+plf_w,halfD,0
				ROTy -90
				ROTz 180
				showLeft = NOT(i=1)
				showRight = NOT(i=numPosts + 1)
				GOSUB 200
				DEL 3
			ENDIF
		ENDIF
		ADDz postRealDist
	NEXT i

	DEL 2 + numPosts + 1

	ADD gs_platform_length - plf_w/2, -B-halfD, hr_h/2
	ROTz 90
	ARMC halfD, halfD, B+halfD, 0, 0, 90
	DEL 2

	DEL 1
ENDIF

DEL TOP
END !!!!! END !!!!! END !!!!! END !!!!! END !!!!! END !!!!! END



100:	! Platform Support - Simple
	CUTPLANE -135
	SPRISM_ mat_ladder, mat_ladder, mat_ladder,
		4, 0,0, 1,0, SQR(2)*B, 45,
		plf_t,0, 15,
		plf_w-plf_t,0, 15,
		plf_w-plf_t,plf_w, 15,
		plf_t,plf_w, 15
	CUTEND
RETURN

101:	! Platform Support - Detailed

	ROTx 45
	ROTy 90
	ROTz 180
	ADDy -B+plf_h-plf_t
	PRISM_ 6,plf_w,
		0,0,15,
		f_w,0,15,
		f_w,f_t,15,
		f_t,f_t,15,
		f_t,f_d,15,
		0,f_d,15
	DEL 4

	ADDz SQR(2)*plf_t
	CUTPLANE -135
	DEL 1
	SPRISM_ mat_ladder, mat_ladder, mat_ladder,
		4, 0,0, 1,0, SQR(2)*B, 45,
		plf_t,0, 15,
		plf_w-plf_t,0, 15,
		plf_w-plf_t,plf_w, 15,
		plf_t,plf_w, 15
	CUTEND

	ROTx 45
	ADD -plf_w,0,0
		BLOCK 3*plf_w, SQR(2)*plf_w+plf_t, plf_t
	DEL 2
RETURN


200:	! Rail Support
	IF showRight THEN
		PUT	-2",	0,		15,
			-2",	f_t,	15
	ELSE
		PUT	-halfD-f_t,	0,		15
	ENDIF
	PUT	-halfD-f_t,	f_t,	15,
		-halfD-f_t,	halfD,	79,
		 halfD+f_t,	halfD,	1079,
		 halfD+f_t,	f_t,	15
	IF showLeft THEN
		PUT 2",		f_t,	15,
			2",		0,		15
	ELSE
		PUT halfD+f_t, 0,		15
	ENDIF
	PUT	 halfD,		0,		15,
		 halfD,		halfD,	79,
		-halfD,		halfD,	1079,
		-halfD,		0,		15

	PRISM_ NSP/3, 2", GET(NSP)
RETURN


