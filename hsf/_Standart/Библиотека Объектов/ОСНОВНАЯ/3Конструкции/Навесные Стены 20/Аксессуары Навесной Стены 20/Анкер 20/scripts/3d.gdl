
unID = 1
if not (gs_shadow) then shadow off

pen gs_cont_pen
material gs_material_anchor

sect_fill gs_sect_fill, gs_sect_bg_pen, gs_sect_fill_pen, gs_sect_cont_pen

if AC_globalZDir[1][3] < 0 then mulz -1

if AC_accessoryFlipped then
	addy AC_frameWidthBack
else
	addy AC_frameWidthFront
endif

hotspot 0, 				0, 0, unID, anchorPosition, 1 + 128	: unID = unID + 1
hotspot -1, 			0, 0, unID, anchorPosition, 3		: unID = unID + 1
hotspot anchorPosition,	0, 0, unID, anchorPosition, 2		: unID = unID + 1

del 1

if iAnchorPlace = 1 then
	put -AC_width / 2, 0, 15,
		 AC_width / 2, 0, 15
endif

if iAnchorPlace = 1 then
	if iAnchorType = 1 then
		put AC_width / 2, AC_height, 15

		if bEndPlate then
			put -AC_width / 2, AC_height, 15,
				-AC_width / 2, AC_height - anchorThickness, 15,
				 AC_width / 2 - anchorThickness, AC_height - anchorThickness, 15
		else
			put AC_width / 2 - anchorThickness, AC_height, 15
		endif

		put  AC_width / 2 - anchorThickness, anchorThickness, 15,
			-AC_width / 2, anchorThickness, 15
	endif

	if iAnchorType = 2 then
		put AC_width / 2, anchorThickness, 15,
			anchorThickness/2, anchorThickness, 15

		if bEndPlate then
			put anchorThickness/2, AC_height - anchorThickness, 15,
				 AC_width / 2, AC_height - anchorThickness, 15,
				 AC_width / 2, AC_height, 15,
				-AC_width / 2, AC_height, 15,
				-AC_width / 2, AC_height - anchorThickness, 15,
				-anchorThickness/2, AC_height - anchorThickness, 15
		else
			put	 anchorThickness/2, AC_height, 15,
				-anchorThickness/2, AC_height, 15
		endif

		put -anchorThickness/2, anchorThickness, 15,
			-AC_width / 2, anchorThickness, 15
	endif
else
	if iAnchorPlace = 2 then addz AC_frameWidthLeft
	if iAnchorPlace = 3 then addz -AC_frameWidthRight

	addx anchorPosition
	hotspot 0, 				0, 0, unID, lAnchorOffset, 1 + 128	: unId = unID + 1
	hotspot 0, 			-1, 0, unID, lAnchorOffset, 3		: unId = unID + 1
	hotspot 0,	lAnchorOffset, 0, unID, lAnchorOffset, 2		: unId = unID + 1
	del 2

	addy lAnchorOffset
	if iAnchorPlace = 2 then
		put -AC_frameWidthLeft, 0, 15,
			-AC_frameWidthLeft - anchorThickness, 0, 15

		if bEndPlate then
				if abs(anchorRot) < eps then
					put -AC_frameWidthLeft - anchorThickness, AC_height, 15,
						-AC_frameWidthLeft - anchorThickness + AC_width , AC_height, 15,
						-AC_frameWidthLeft - anchorThickness + AC_width, AC_height - anchorThickness, 15,
						-AC_frameWidthLeft, AC_height - anchorThickness, 15
				else
					put -AC_frameWidthLeft - anchorThickness, AC_height - anchorThickness, 15,
						-AC_frameWidthLeft - AC_width , AC_height - anchorThickness, 15,
						-AC_frameWidthLeft - AC_width, AC_height, 15,
						-AC_frameWidthLeft, AC_height, 15
				endif
		else
			put -AC_frameWidthLeft - anchorThickness, AC_height, 15,
				-AC_frameWidthLeft, AC_height, 15
		endif
	endif

	if iAnchorPlace = 3 then
		put AC_frameWidthRight, 0, 15,
			AC_frameWidthRight + anchorThickness, 0, 15

		if bEndPlate then
			if abs(anchorRot) < eps then
				put AC_frameWidthRight + anchorThickness, AC_height - anchorThickness, 15,
					AC_frameWidthRight + AC_width , AC_height - anchorThickness, 15,
					AC_frameWidthRight + AC_width, AC_height, 15,
					AC_frameWidthRight, AC_height, 15
			else
				put AC_frameWidthRight + anchorThickness, AC_height, 15,
					AC_frameWidthRight + anchorThickness - AC_width , AC_height, 15,
					AC_frameWidthRight + anchorThickness - AC_width, AC_height - anchorThickness, 15,
					AC_frameWidthRight, AC_height - anchorThickness, 15
			endif
		else
			put AC_frameWidthRight + anchorThickness, AC_height, 15,
				AC_frameWidthRight, AC_height, 15
		endif
	endif
endif


roty 90

if iAnchorPlace = 1 then
	if AC_accessoryFlipped then
		addy AC_frameWidthBack
	else
		addy AC_frameWidthFront
	endif
endif

addz anchorPosition
if iAnchorPlace = 1 then roty -anchorRot
addz -anchorHeight / 2

prism_ nsp/3, anchorHeight,
	get (nsp)

del top

end
