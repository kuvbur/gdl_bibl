
targetGUID = TO_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

gosub "repairoffsethorizontal_BWM"
gosub "change_armparams_BWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid targetGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! Repair geometry with horizontal offset & vertical rotation
! ==============================================================================
"repairoffsethorizontal_BWM":
	if targetGUID # "" then

		!very faulty in 19
		if abs(gs_offset_horizontal) > EPS & (abs(gs_rotation_vertical) + EPS) % 360 > 2 * EPS then
			targetGUID = ""
		endif
	endif
return


! ==============================================================================
! Change Console, Arm and Frame parameters
! ==============================================================================
"change_armparams_BWM":
	if targetGUID # "" then

		newparameter "gs_nConsol_left", "Integer"
		newparameter "gs_nConsol_right", "Integer"
		newparameter "gs_casing_left", "Boolean"
		newparameter "gs_casing_right", "Boolean"
		newparameter "gs_gap_thickness", "Length"

		!casing is off
		gs_casing_left = 0
		gs_casing_right = 0

		!set nConsol parameters
		if not(bArmLeft) then
			gs_nConsol_left = 0
		else
			if abs(gs_deltaA1) < EPS then
				gs_nConsol_left = 1
			else
				gs_nConsol_left = 2
			endif
		endif

		if not(bArmRight) then
			gs_nConsol_right = 0
		else
			if abs(gs_deltaA2) < EPS then
				gs_nConsol_right = 1
			else
				gs_nConsol_right = 2
			endif
		endif

		!transfer offset to gap parameter if possible
		if gs_nConsol_left = 2 then
			if	(gs_nConsol_right = 0 & abs(gs_deltaF2) < EPS) |\					!only zero offset allowed on other side
				(gs_nConsol_right = 1 & abs(gs_deltaA2) < EPS) |\					!only zero offset allowed on other side
				(gs_nConsol_right = 2 & abs(gs_deltaA1 - gs_deltaA2) < EPS) then	!only same offset allowed on other side

				gs_gap_thickness = gs_deltaA1
			else
				targetGUID = ""
			endif
		else
			if	(gs_nConsol_left = 0 & abs(gs_deltaF1) < EPS) |\					!only zero offset allowed on this side
				(gs_nConsol_left = 1 & abs(gs_deltaA1) < EPS) then					!only zero offset allowed on this side

				gs_gap_thickness = 0
			else
				targetGUID = ""
			endif
		endif
		if gs_nConsol_right = 2 then
			if	(gs_nConsol_left = 0 & abs(gs_deltaF1) < EPS) |\					!only zero offset allowed on other side
				(gs_nConsol_left = 1 & abs(gs_deltaA1) < EPS) |\					!only zero offset allowed on other side
				(gs_nConsol_left = 2 & abs(gs_deltaA1 - gs_deltaA2) < EPS) then		!only same offset allowed on other side

				gs_gap_thickness = gs_deltaA2
			else
				targetGUID = ""
			endif
		else
			if	(gs_nConsol_right = 0 & abs(gs_deltaF2) < EPS) |\					!only zero offset allowed on this side
				(gs_nConsol_right = 1 & abs(gs_deltaA2) < EPS) then					!only zero offset allowed on this side

				gs_gap_thickness = 0
			else
				targetGUID = ""
			endif
		endif


		if targetGUID # "" then
			parameters	gs_nConsol_left = gs_nConsol_left,
						gs_nConsol_right = gs_nConsol_right,
						gs_casing_left = gs_casing_left,
						gs_casing_right = gs_casing_right,
						gs_gap_thickness = gs_gap_thickness
		endif

	endif
return
