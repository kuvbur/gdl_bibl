
![#COMPATIBILITY] - START

if iLouverMethod = -1 then
	bDefaultElem = (GLOB_INTGUID = "" | GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")

	if bDefaultElem then
		iLouverMethod = LOUVER_ENDS
	endif
endif

gosub "GetLouverMethod"


![#COMPATIBILITY] - END

call "FM_types" parameters all

values "gs_detlevel_3d" `Детальный`, `Простой`
values "gs_detlevel_3d_m" 2, 1
values "gs_resol" range [4,)

if GLOB_MODPAR_NAME = "gs_detlevel_3d" then
	if gs_detlevel_3d = `Простой` then gs_detlevel_3d_m = 1
	if gs_detlevel_3d = `Детальный` then gs_detlevel_3d_m = 2
	parameters gs_detlevel_3d_m = gs_detlevel_3d_m
else
	if gs_detlevel_3d_m = 1 then gs_detlevel_3d = `Простой`
	if gs_detlevel_3d_m = 2 then gs_detlevel_3d = `Детальный`
	parameters gs_detlevel_3d = gs_detlevel_3d
endif

if gs_detlevel_3d_m < 2 then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

values "gs_rod_radius" range [0, AC_height/2]

if not (gs_rod) then hideparameter "gs_rod_radius"

if bRounded then
	bEndFrame = 0
	parameters bEndFrame = bEndFrame
	lock "bEndFrame"
endif

DIM stLouverMethod[3]
	stLouverMethod[1] = `между Концами`
	stLouverMethod[2] = `по Количеству`
	stLouverMethod[3] = `по Расстоянию`

values "gs_louver_method" stLouverMethod
values "iLouverMethod" 1,2,3

if GLOB_MODPAR_NAME = "gs_louver_method" then
	if gs_louver_method = stLouverMethod[1] then iLouverMethod = LOUVER_ENDS
	if gs_louver_method = stLouverMethod[2] then iLouverMethod = LOUVER_BY_NUM
	if gs_louver_method = stLouverMethod[3] then iLouverMethod = LOUVER_BY_DIST
	parameters iLouverMethod = iLouverMethod
else
	if iLouverMethod = LOUVER_ENDS then gs_louver_method = stLouverMethod[1]
	if iLouverMethod = LOUVER_BY_NUM then gs_louver_method = stLouverMethod[2]
	if iLouverMethod = LOUVER_BY_DIST then gs_louver_method = stLouverMethod[3]
	parameters gs_louver_method = gs_louver_method
endif

if iLouverMethod = LOUVER_BY_NUM or iLouverMethod = LOUVER_ENDS then
	lock "gs_lamelles_axis_dist"
	hideparameter "gs_lamelles_axis_dist"
endif
if iLouverMethod = LOUVER_BY_DIST then
	lock "gs_lamelles"
	hideparameter "gs_lamelles"
endif
if iLouverMethod = LOUVER_ENDS then
	bAutoLouverWidth = 1
	parameters bAutoLouverWidth = bAutoLouverWidth
	lock "bAutoLouverWidth"
	hideparameter "bAutoLouverWidth"
endif
if bAutoLouverWidth then
	lock "gs_lamelles_width"
	hideparameter "gs_lamelles_width"
endif
values "gs_lamelles" range [0,)
values "gs_lamelles_angle" range [-90, 90]
values "gs_lamelles_width" range (0, maxLamelleDist]
values "gs_lamelles_axis_dist" range (0, freeLength]

if not(bArmLeft | bArmRight) then
	lock "gs_arm_thickness", "AC_width", "AC_height", "bRounded"
endif
if not(bArmLeft) then
	lock "gs_deltaA1"
else
	lock "gs_deltaF1"
endif
if not(bArmRight) then
	lock "gs_deltaA2"
else
	lock "gs_deltaF2"
endif
