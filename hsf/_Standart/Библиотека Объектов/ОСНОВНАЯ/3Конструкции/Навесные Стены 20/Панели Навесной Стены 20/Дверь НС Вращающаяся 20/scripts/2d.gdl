
![#COMPATIBILITY] - START

if iLeafType = -1 then
	gosub "LeafCompatibility"
else
	panelThk = sashThk
endif
![#COMPATIBILITY] - END

unID = 1

tranWidth = 0
tranX = 0
call "CW Transformation" parameters A = A, AC_PanelCoords = AC_PanelCoords, ac_originIsFrameCenter = ac_originIsFrameCenter returned_parameters tranWidth, tranX
mul2 tranWidth, 1
add2 -tranX, 0


! =============================================================================
! Request globals for 2D (MVO Detail Level, MVO Reveal, Hotspots for IS)
! =============================================================================

iMVODetlevel2D			= 0
bMVOCasing2D			= 0
bShowAddHotspots		= 0
bMVODetlevelReveal		= 0
bShowAddHotspotsForIS	= 0

bGetMVOValueDlev2D		= (gs_detlevel_2d_m = 1)
bGetMVOValueCasing2D	= 0
bGetMVOValueDlevReveal	= 0
bGetHotspotsForIS		= 0

if (bGetMVOValueDlev2D | bGetMVOValueDlevReveal | bGetHotspotsForIS) & GLOB_SCRIPT_TYPE = 2 then
	call "GetDWSymbSettings" parameters	bWindow					= 0,
										bGetMVOValueDlev2D		= bGetMVOValueDlev2D,
										bGetMVOValueCasing2D	= bGetMVOValueCasing2D,
										bGetMVOValueDlevReveal	= bGetMVOValueDlevReveal,
										bGetHotspotsForIS		= bGetHotspotsForIS,
					returned_parameters iMVODetlevel2D,
										bMVOCasing2D,
										bMVODetlevelReveal,
										bShowAddHotspotsForIS
endif


! =============================================================================
! 2D detail level settings
! =============================================================================

if bGetMVOValueDlev2D then																! by MVO
	iLoD2D	= SYMBD_D_2LINELEAFOP_GLASS
	if iMVODetlevel2D < SYMBD_D_2LINELEAFOP_GLASS then		iLoD2D = SYMBD_M_1LINELEAF_2LINE
	if iMVODetlevel2D < SYMBD_M_1LINELEAF_2LINE then		iLoD2D = SYMBD_S_1LINE

	if GLOB_PREVIEW_MODE = 3 then				! in case of 2D-3D Favorite Preview
		iLoD2D = SYMBD_D_2LINELEAFOP_GLASS
	endif
else
	iLoD2D = SYMBD_OFF																! Off
	if gs_detlevel_2d_m = 50 then	iLoD2D = SYMBD_D_2LINELEAFOP_GLASS		! 50
	if gs_detlevel_2d_m = 100 then	iLoD2D = SYMBD_M_1LINELEAF_2LINE		! 100
	if gs_detlevel_2d_m = 200 then	iLoD2D = SYMBD_S_1LINE					! 200

	if gs_detlevel_2d_m < 0 then
		iLoD2D	= SYMBD_S_1LINE
		if GLOB_SCALE <= 150 and GLOB_SCALE > 75 then	iLoD2D = SYMBD_M_1LINELEAF_2LINE
		if GLOB_SCALE <= 75 then						iLoD2D = SYMBD_D_2LINELEAFOP_GLASS
	endif
endif

if iLoD2D = SYMBD_OFF then end


! --- setting center of door ---------------------------------------------------

add2 (leftSide_X + rightSide_X) / 2, offsetPanel


! --- defining dimensions ------------------------------------------------------

tempX_frame_left  = -doorWidth / 2
tempX_frame_right =  doorWidth / 2

tempX_sash_left  = tempX_frame_left  + sashWidth
tempX_sash_right = tempX_frame_right - sashWidth

doorWidth = tempX_frame_right - tempX_frame_left


! --- Door Symbol --------------------------------------------------------------

dx = frameWidth / 2

if iLoD2D = SYMBD_D_2LINELEAFOP_GLASS then
	fill fillFrame
	pen penFrame

	poly2_b 5, 1+2+4+32, fillPenFrame, fillPenBgFrame,
		0,										0,										901+32,
		cos(45) * doorWidth/2,					sin(45) * doorWidth/2,					1+32,
		cos(-45) * doorWidth/2,					sin(-45) * doorWidth/2,					3001+32,
		cos(-45) * (doorWidth/2 - frameThk),	sin(-45) * (doorWidth/2 - frameThk),	1+32,
		cos(45) * (doorWidth/2 - frameThk),		sin(45) * (doorWidth/2 - frameThk),		3001+32

	poly2_b 5, 1+2+4+32, fillPenFrame, fillPenBgFrame,   ! left
		0,										0,										901+32,
		cos(135) * doorWidth/2,					sin(135) * doorWidth/2,					1+32,
		cos(225) * doorWidth/2,					sin(225) * doorWidth/2,					3001+32,
		cos(225) * (doorWidth/2 - frameThk),	sin(225) * (doorWidth/2 - frameThk),	1+32,
		cos(135) * (doorWidth/2 - frameThk),	sin(135) * (doorWidth/2 - frameThk),	3001+32

	circle2 0,	0,	radiusMiddleColumn

	rot2 -45

	fill fillSash
	pen penSash
	for i=1 to 4		! 1-2 right, 3-4 left
		line2 0,	doorWidth/2 - frameThk,	0,	doorWidth/2

			poly2_b 5, 1+2+4+32, fillPenSash, fillPenBgSash,
				radiusMiddleColumn,		-panelThk/2,	1+32,
				radiusMiddleColumn,		 panelThk/2,	1+32,
				doorWidth/2-frameThk,	 panelThk/2,	1+32,
				doorWidth/2-frameThk,	-panelThk/2,	1+32,
				radiusMiddleColumn,		-panelThk/2,	-1

		rot2 90
	next i
else
	pen penFrame
	arc2 0, 0, doorWidth/2, -45, 45
	arc2 0, 0, doorWidth/2, 135, 225 ! left
	pen penSash
	line2 -doorWidth/2/1.41,	-doorWidth/2/1.41,	doorWidth/2/1.41,	 doorWidth/2/1.41
	line2 -doorWidth/2/1.41,	 doorWidth/2/1.41,	doorWidth/2/1.41,	-doorWidth/2/1.41
endif

del 1

end
