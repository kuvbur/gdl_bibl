
if gs_cust_panel = "" then			! just a plane panel in case of no custom panel named
	addz -AC_ClampWidth / 2 + offsetPanel
	for i = 1 to nCorners_in
		put AC_clampFreeRegionCoords [i][1], AC_clampFreeRegionCoords [i][2], 15
	next i

	material matPanel
	prism_ nCorners_in, AC_Clampwidth,
			get (nsp)
	del 1
else
	addx (leftSide_X + rightSide_X) / 2
	addy (downSide_Y + upperSide_Y) / 2

	unID = 1
	r = min (leftSide_X + rightSide_X, downSide_Y + upperSide_Y) / 4
	hotspot 0, 							0, 							0, unID, gs_updirection, 6 : unID = unID +1 !center of angle
	hotspot 0, 							r,							0, unID, gs_updirection, 4+128 : unID = unID +1 !base hotspot
	hotspot r * sin (gs_updirection), 	r * cos (gs_updirection), 	0, unID, gs_updirection, 5 : unID = unID +1 !moving
	hotspot 0, 							0, 							-1, unID, gs_updirection, 7 : unID = unID +1 !center of angle


	rotz -gs_updirection

	if abs (gs_updirection) < eps then
		addx -(leftSide_X + rightSide_X) / 2 + leftSide_X
		addy -(downSide_Y + upperSide_Y) / 2 + downSide_Y
		panelWidth = rightSide_X - leftSide_X
		panelHeight = upperSide_Y - downSide_Y
	endif
	if abs (gs_updirection - 90) < eps then
		addx -(downSide_Y + upperSide_Y) / 2 + downSide_Y
		addy -(leftSide_X + rightSide_X) / 2 + leftSide_X
		panelHeight = rightSide_X - leftSide_X
		panelWidth = upperSide_Y - downSide_Y
	endif
	if abs (gs_updirection - 180) < eps then
		addx -(leftSide_X + rightSide_X) / 2 + leftSide_X
		addy -(downSide_Y + upperSide_Y) / 2 + downSide_Y
		panelWidth = rightSide_X - leftSide_X
		panelHeight = upperSide_Y - downSide_Y
	endif
	if abs (gs_updirection - 270) < eps then
		addx -(downSide_Y + upperSide_Y) / 2 + downSide_Y
		addy -(leftSide_X + rightSide_X) / 2 + leftSide_X
		panelHeight = rightSide_X - leftSide_X
		panelWidth = upperSide_Y - downSide_Y
	endif

	addz -panelThk + AC_ClampWidth / 2 + offsetPanel

	sect_fill gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen

	if bFlipped then
		addz panelThk
		mulz -1
	endif

	call "CW_Panel_Collection" parameters a = panelWidth,
							b 				= panelHeight,
							zzyzx			= panelThk,
							gs_ptype		= gs_cust_panel
endif


