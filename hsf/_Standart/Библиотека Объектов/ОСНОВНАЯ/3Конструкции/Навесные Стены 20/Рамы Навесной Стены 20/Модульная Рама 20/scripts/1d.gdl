
EPS = 0.0001

if GLOB_SCRIPT_TYPE = 7 | GLOB_SCRIPT_TYPE = 8 then goto "MasterEnd"


dim st3dDetLevelValues[3]
	st3dDetLevelValues[1] = `Детальный`
	st3dDetLevelValues[2] = `Простой`
	st3dDetLevelValues[3] = `Откл.`

dim stCWFrameType[4]
	stCWFrameType[1] = `Тип 1`
	stCWFrameType[2] = `Тип 2`
	stCWFrameType[3] = `Тип 3`
	stCWFrameType[4] = `Специальная Рама`

dim stCapType[4]
	stCapType[1]	= `Тип 1`
	stCapType[2]	= `Тип 2`
	stCapType[3]	= `Тип 3`
	stCapType[4]	= `Специальная Насадка`

dim stCutModeColValues[2]
	stCutModeColValues[1] = `Вдоль Оси X`
	stCutModeColValues[2] = `Вдоль Оси Y`


! gs_detlevel_3D_m
DETLEV_OFF		= 0
DETLEV_SIMPLE	= 1
DETLEV_DETAILED	= 2

! gs_frameType
FRAMETYPE_TYPE1		= 1
FRAMETYPE_TYPE2		= 2
FRAMETYPE_TYPE3		= 3
FRAMETYPE_CUSTOM	= 0

! gs_capType
CAPTYPE_TYPE1	= 1
CAPTYPE_TYPE2	= 2
CAPTYPE_TYPE3	= 3
CAPTYPE_CUSTOM	= 0

! iCutDirTop, iCutDirBottom
CUTMODE_ALONG_X	= 1
CUTMODE_ALONG_Y	= 2


! =========Set the Dimensions of frame

FRAME_WIDTH = gs_originOffsetX + gs_rightOffset	!A
FRAME_DEPTH = gs_originOffsetY + gs_frontOffset	!B
A = gs_originOffsetX + gs_rightOffset
B = gs_originOffsetY + gs_frontOffset

NEG_WIDTH_SEG = gs_originOffsetX
POZ_WIDTH_SEG = gs_rightOffset
NEG_DEPTH_SEG = gs_originOffsetY
POZ_DEPTH_SEG = gs_frontOffset

if AC_clampDepth > A/2 - EPS then
	AC_clampDepth = A/2 - EPS
endif

!Boundary outside
if ABS(gs_originOffsetX) < EPS then tempXorigo = gs_rightOffset/2

!Boundary inside
if ABS(gs_rightOffset) < EPS then tempXorigo = -gs_originOffsetX/2

!Boundary center
if ABS(gs_rightOffset) > EPS AND ABS(gs_originOffsetX) > EPS then tempXorigo = 0

! =========Set the Angle of panels
VP = 0
for i = 1 to ac_nConnectingPanels

	if ABS(AC_ClampVector[i][1]) > EPS OR ABS(AC_ClampVector[i][2]) > EPS then

		VP = VP + 1
		if abs(AC_ClampVector[i][1]) < EPS then
			if AC_ClampVector[i][2] < 0 then
				gs_frameAngles[VP] = 90
			else
				gs_frameAngles[VP] = 270
			endif
		else
			if AC_ClampVector[i][1] > 0 then
				if atn(AC_ClampVector[i][2]/AC_ClampVector[i][1]) >= 0 then
					gs_frameAngles[VP] = atn(AC_ClampVector[i][2]/AC_ClampVector[i][1])
				else
					gs_frameAngles[VP] = 360 + atn(AC_ClampVector[i][2]/AC_ClampVector[i][1])
				endif
			else
				if atn(AC_ClampVector[i][2]/AC_ClampVector[i][1]) >= 0 then
					gs_frameAngles[VP] = 180 + atn(AC_ClampVector[i][2]/AC_ClampVector[i][1])
				else
					gs_frameAngles[VP] = 180 + atn(AC_ClampVector[i][2]/AC_ClampVector[i][1])
				endif
			endif
		endif
		PARAMETERS gs_frameAngles[VP] = gs_frameAngles[VP]

		gs_frameDistance[i] = sqr(AC_ClampVector[i][1]*AC_ClampVector[i][1] + AC_ClampVector[i][2]*AC_ClampVector[i][2])
	endif
next i
! =========Set the Angle of panels


! =========Ordering the Angles to drawing
for i = 1 to VP
	for j = 1 to i-1
		if gs_frameAngles[j] > gs_frameAngles[j+1] then
			TEMP = gs_frameAngles[j]
			gs_frameAngles[j] = gs_frameAngles[j+1]
			gs_frameAngles[j+1] = TEMP
		endif
	next j
next i

!=========Ordering the Angles to drawing

"MasterEnd":

