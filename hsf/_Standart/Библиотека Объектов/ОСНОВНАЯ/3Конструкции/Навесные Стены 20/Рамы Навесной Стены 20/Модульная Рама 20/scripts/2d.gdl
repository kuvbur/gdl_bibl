

pen gs_cont_pen
fill gs_fill_type

! =========Shape generation
! =========CORE
poly2_B 4, 1+2+4+32,gs_fill_pen,gs_back_pen,
	POZ_WIDTH_SEG-AC_clampDepth,-(AC_clampWidth/2)-gs_gasketIN,1,
	POZ_WIDTH_SEG-AC_clampDepth,(AC_clampWidth/2)+gs_gasketOUT,1,
	-NEG_WIDTH_SEG+AC_clampDepth,(AC_clampWidth/2)+gs_gasketOUT,1,
	-NEG_WIDTH_SEG+AC_clampDepth,-(AC_clampWidth/2)-gs_gasketIN,1

if gs_gasketIN > 0 then
	if gs_detlevel_3D_m = DETLEV_DETAILED then
		poly2_B 4, 1+2+4+32,gs_fill_pen,gs_back_pen,
			POZ_WIDTH_SEG,-(AC_clampWidth/2)-gs_gasketIN,1,
			POZ_WIDTH_SEG,-(AC_clampWidth/2),1,
			POZ_WIDTH_SEG-AC_clampDepth,-(AC_clampWidth/2),1,
			POZ_WIDTH_SEG-AC_clampDepth,-(AC_clampWidth/2)-gs_gasketIN,1

		poly2_B 4, 1+2+4+32,gs_fill_pen,gs_back_pen,
			-NEG_WIDTH_SEG,-(AC_clampWidth/2)-gs_gasketIN,1,
			-NEG_WIDTH_SEG,-(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG+AC_clampDepth,-(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG+AC_clampDepth,-(AC_clampWidth/2)-gs_gasketIN,1
	endif

	if gs_detlevel_3D_m = DETLEV_SIMPLE then
		poly2_B 4, 1+2+4+32,gs_fill_pen,gs_back_pen,
			POZ_WIDTH_SEG,-(AC_clampWidth/2)-gs_gasketIN,1,
			POZ_WIDTH_SEG,-(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG,-(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG,-(AC_clampWidth/2)-gs_gasketIN,1
	endif

endif

if gs_gasketOUT > 0 then
	if gs_detlevel_3D_m = DETLEV_DETAILED then
		poly2_B 4, 1+2+4+32,gs_fill_pen,gs_back_pen,
			POZ_WIDTH_SEG,(AC_clampWidth/2)+gs_gasketOUT,1,
			POZ_WIDTH_SEG,(AC_clampWidth/2),1,
			POZ_WIDTH_SEG-AC_clampDepth,(AC_clampWidth/2),1,
			POZ_WIDTH_SEG-AC_clampDepth,(AC_clampWidth/2)+gs_gasketOUT,1

		poly2_B 4, 1+2+4+32,gs_fill_pen,gs_back_pen,
			-NEG_WIDTH_SEG,(AC_clampWidth/2)+gs_gasketOUT,1,
			-NEG_WIDTH_SEG,(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG+AC_clampDepth,(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG+AC_clampDepth,(AC_clampWidth/2)+gs_gasketOUT,1
	endif

	if gs_detlevel_3D_m = DETLEV_SIMPLE then
		poly2_B 4, 1+2+4+32,gs_fill_pen,gs_back_pen,
			POZ_WIDTH_SEG,(AC_clampWidth/2)+gs_gasketOUT,1,
			POZ_WIDTH_SEG,(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG,(AC_clampWidth/2),1,
			-NEG_WIDTH_SEG,(AC_clampWidth/2)+gs_gasketOUT,1
	endif

endif
! =========CORE


! =========FRAME
if gs_frameType = FRAMETYPE_CUSTOM then

	!Boundary outside
	if ABS(gs_originOffsetX) < EPS then add2 0,-NEG_DEPTH_SEG

	!Boundary inside
	if ABS(gs_rightOffset) < EPS then add2 -NEG_WIDTH_SEG,-NEG_DEPTH_SEG

	!Boundary center
	if ABS(gs_rightOffset) > EPS AND ABS(gs_originOffsetX) > EPS then add2 -POZ_WIDTH_SEG,-NEG_DEPTH_SEG

	CALL "CW_Frame_Collection" PARAMETERS	a=POZ_WIDTH_SEG+NEG_WIDTH_SEG,
											b=NEG_DEPTH_SEG-(AC_clampWidth/2)-gs_gasketIN,
											zzyzx=modz,
											gs_ptype=gs_CustomFrameName
	del 1

else
	if gs_frameType = FRAMETYPE_TYPE1 then
		put POZ_WIDTH_SEG, -NEG_DEPTH_SEG,1
		put POZ_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN,1
		put -NEG_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN,1
		put -NEG_WIDTH_SEG, -NEG_DEPTH_SEG,1
	endif

	if gs_frameType = FRAMETYPE_TYPE2 then
		put tempXorigo+0.004, -NEG_DEPTH_SEG,1
		put tempXorigo+0.004, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put POZ_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put POZ_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN,1
		put -NEG_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN,1
		put -NEG_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put tempXorigo-0.004, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put tempXorigo-0.004, -NEG_DEPTH_SEG,15
	endif

	if gs_frameType = FRAMETYPE_TYPE3 then
		put POZ_WIDTH_SEG, -NEG_DEPTH_SEG,1
		put POZ_WIDTH_SEG, -NEG_DEPTH_SEG+0.008,1
		put tempXorigo+0.004, -NEG_DEPTH_SEG+0.008,1
		put tempXorigo+0.004, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put POZ_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put POZ_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN,1
		put -NEG_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN,1
		put -NEG_WIDTH_SEG, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put tempXorigo-0.004, 0-(AC_clampWidth/2)-gs_gasketIN-0.008,1
		put tempXorigo-0.004, -NEG_DEPTH_SEG+0.008,1
		put -NEG_WIDTH_SEG, -NEG_DEPTH_SEG+0.008,1
		put -NEG_WIDTH_SEG, -NEG_DEPTH_SEG,1
	endif

	poly2_B NSP/3, 1+2+4+32,gs_fill_pen,gs_back_pen,
		GET (NSP)
endif
! =========FRAME

! =========CAP
if gs_capType = CAPTYPE_CUSTOM then

	!Boundary outside
	if ABS(gs_originOffsetX) < EPS then add2 0,AC_clampWidth/2+gs_gasketOUT

	!Boundary inside
	if ABS(gs_rightOffset) < EPS then add2 -NEG_WIDTH_SEG,AC_clampWidth/2+gs_gasketOUT

	!Boundary center
	if ABS(gs_rightOffset) > EPS AND ABS(gs_originOffsetX) > EPS then add2 -POZ_WIDTH_SEG,AC_clampWidth/2+gs_gasketOUT

	CALL "CW_Cap_Collection" PARAMETERS	a=POZ_WIDTH_SEG+NEG_WIDTH_SEG,
											b=POZ_DEPTH_SEG-(AC_clampWidth/2)-gs_gasketOUT,
											zzyzx=modz,
											gs_ptype=gs_CustomCapName

	del 1

else
	if gs_capType = CAPTYPE_TYPE1 then
		put POZ_WIDTH_SEG, POZ_DEPTH_SEG,1
		put POZ_WIDTH_SEG, 0+(AC_clampWidth/2)+gs_gasketOUT,1
		put -NEG_WIDTH_SEG, 0+(AC_clampWidth/2)+gs_gasketOUT,1
		put -NEG_WIDTH_SEG, POZ_DEPTH_SEG,1
	endif

	if gs_capType = CAPTYPE_TYPE2 then
		put tempXorigo, POZ_DEPTH_SEG,1
		put POZ_WIDTH_SEG, 0+(AC_clampWidth/2)+gs_gasketOUT,1
		put -NEG_WIDTH_SEG, 0+(AC_clampWidth/2)+gs_gasketOUT,1
	endif

	if gs_capType = CAPTYPE_TYPE3 then
		put POZ_WIDTH_SEG, 0+(AC_clampWidth/2)+gs_gasketOUT,1
		put tempXorigo, 0+(AC_clampWidth/2)+gs_gasketOUT,900
		put -NEG_WIDTH_SEG, 0+(AC_clampWidth/2)+gs_gasketOUT,3001
	endif

	poly2_B NSP/3, 1+2+4+32,gs_fill_pen,gs_back_pen,
		GET (NSP)
endif
! =========CAP
! ========Shape generation

END
