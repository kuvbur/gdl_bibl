
if gs_detlevel_3D_m = DLEV3D_OFF then END

if gs_shadow then
	SHADOW ON
else
	SHADOW OFF
endif

cutedgestatus	= 1
if GLOB_CONTEXT = 5 or GLOB_CONTEXT = 6 then
	cutedgestatus	= 2
endif
!!!=============
!!!========DEBUG
!!!if gs_debug then
!!!	if gs_draw_body then
!!!	print "gs_rightOffset",gs_rightOffset, "\n", "gs_originOffsetX", gs_originOffsetX
!!!	addz gs_spider_height
!!!	brick 0.5,0.02,0.02
!!!	addx 0.51
!!!	text 0.02,0,"X"
!!!	del 1
!!!
!!!	brick 0.02,0.5,0.02
!!!	addy 0.51
!!!	text 0.02,0,"Y"
!!!	del 1
!!!
!!!	brick 0.02,0.02,0.5
!!!	del 1
!!!	endif
!!!	if gs_print_gotparams then \
!!!		print "Top plane:",AC_topConnPlane[1],AC_topConnPlane[2],AC_topConnPlane[3],AC_topConnPlane[4],"\n",\
!!!			"Bottom plane:",AC_bottomConnPlane[1],AC_bottomConnPlane[2],AC_bottomConnPlane[3],AC_bottomConnPlane[4]
!!!!		print gs_frameAngles[i], ">",OutAngles[i*2-1],InAngles[i*2-1],InAngles[i*2],OutAngles[i*2],"<",VP,"-",ac_nConnectingPanels
!!!		print "1:",AC_clampvector[1][1],AC_clampvector[1][2],"2:",AC_clampvector[2][1],AC_clampvector[2][2]
!!!endif
!!!========DEBUG
!!!=============

material gs_material
RESOL gs_resol
PEN gs_cont_pen
if buseSectionPens then
	sect_fill gs_sect_fill, gs_sect_bg_pen, gs_sect_fill_pen, gs_sect_cont_pen
endif

!!!==================================
!!!=========Cutplanes - Start Cutting
modz = zzyzx
IF GS_Cutting Then
!!!=========User  Cuts
	AB = sqr(FRAME_DEPTH^2 + FRAME_WIDTH^2)

	!!!-------------------- Top Cutting
	If ABS(gs_cutang1) > EPS OR ABS(gs_cutang2) > EPS Then
		modz = modz+tan(60)* AB/2

		Rotz gs_cutdir_top
		addz zzyzx
		rotx 90

		IF ABS(gs_cutdir_top) < EPS Then
			HPx = FRAME_WIDTH
			HPy = FRAME_DEPTH
		Else
			HPx = FRAME_DEPTH
			HPy = FRAME_WIDTH
		Endif

		cutpoly 5,
				-AB/2,tan(60)* AB /2,
				AB/2,tan(60)* AB/2,
				AB/2, tan(gs_cutang1)* AB/2,
				0,0,
				-AB/2, tan(gs_cutang2)* AB/2

		del 3
	Endif

	!!!-------------------- Bottom Cutting
	If ABS(gs_cutang3) > EPS OR ABS(gs_cutang4) > EPS Then
		modz = modz+tan(60)* AB/2

		Rotz gs_cutdir_bot
		rotx 90

		IF ABS(gs_cutdir_bot) < EPS Then
			HPx = FRAME_WIDTH
			HPy = FRAME_DEPTH
		Else
			HPx = FRAME_DEPTH
			HPy = FRAME_WIDTH
		Endif

		cutpoly 5,
				-AB/2,-tan(60)* AB/2,
				AB/2,-tan(60)* AB/2,
				AB/2, tan(gs_cutang3)* AB/2,
				0,0,
				-AB/2, tan(gs_cutang4)* AB/2

		del 2
		addz -tan(60)*AB/2
	Endif
else
!!!=========Auto Cuts

	! --- Auto Cutting at Top ---
	if ABS(AC_topConnPlane[3]) > EPS then
		tempwidthX = modz + A * ABS(TAN(ATN(AC_topConnPlane[1]/AC_topConnPlane[3])))
		tempwidthY = modz + B * ABS(TAN(ATN(AC_topConnPlane[2]/AC_topConnPlane[3])))
		modz = modz + max(tempwidthX, tempwidthY)
	endif

	if ABS(AC_topConnPlane[3]) > EPS then
		roty ATN(AC_topConnPlane[1]/AC_topConnPlane[3])
		rotx -ATN(AC_topConnPlane[2]/AC_topConnPlane[3])
	endif

	addz AC_topConnPlane[4]

	if gs_draw_body then plane 4, -b,b,0,b,b,0,b,-b,0,-b,-b,0

	cutplane 1,1,0,1,cutedgestatus

	if ABS(AC_topConnPlane[3]) > EPS then del 2

	del 1

	! --- Auto Cutting at bottom ---
	if ABS(AC_bottomConnPlane[3]) > EPS then
		tempwidthX = modz + A * ABS(TAN(ATN(AC_bottomConnPlane[1]/AC_bottomConnPlane[3])))
		tempwidthY = modz + B * ABS(TAN(ATN(AC_bottomConnPlane[2]/AC_bottomConnPlane[3])))
		modz = modz + max(tempwidthX, tempwidthY)
	endif

	if ABS(AC_bottomConnPlane[3]) > EPS then
		roty ATN(AC_bottomConnPlane[1]/AC_bottomConnPlane[3])
		rotx -ATN(AC_bottomConnPlane[2]/AC_bottomConnPlane[3])
	endif

	addz -AC_bottomConnPlane[4]

	if gs_draw_body then plane 4, -b,b,0,b,b,0,b,-b,0,-b,-b,0

	rotx 180
	cutplane 1,1,0,1,cutedgestatus
	del 1

	if ABS(AC_bottomConnPlane[3]) > EPS then del 2

	del 1

	if ABS(AC_bottomConnPlane[3]) > EPS then
		addz - max(A * ABS(TAN(ATN(AC_bottomConnPlane[1]/AC_bottomConnPlane[3]))), B * ABS(TAN(ATN(AC_bottomConnPlane[2]/AC_bottomConnPlane[3]))) )
	endif

endif
!!!=========Cutplanes - Start Cutting
!!!==================================


!!!================
!!!=========Drawing

	if slot1 then gosub 10	!Ritght socket
	if slot2  then gosub 20 !Front socket
	if slot3 then gosub 30	!Left socket
	if slot4  then gosub 40 !Front socket

	if slot5 then gosub 50	!45 socket
	if slot6 then gosub 60	!135 socket
	if slot7 then gosub 70	!225 socket
	if slot8 then gosub 80	!315 socket

	base
if gs_frameType = 2 then
		prism_ 6, modz,
			0, -NEG_DEPTH_SEG-POZ_WIDTH_SEG,15,
			POZ_WIDTH_SEG, -NEG_DEPTH_SEG,15,
			POZ_WIDTH_SEG, POZ_DEPTH_SEG,15,
			0, POZ_DEPTH_SEG+POZ_WIDTH_SEG,15,
			-NEG_WIDTH_SEG, POZ_DEPTH_SEG,15,
			-NEG_WIDTH_SEG, -NEG_DEPTH_SEG,15
endif


	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1


	if slot1 then cutend
	if slot2 then cutend
	if slot3 then cutend
	if slot4 then cutend

	if slot5 then cutend
	if slot6 then cutend
	if slot7 then cutend
	if slot8 then cutend

!!!=========Drawing
!!!================


!!!================================
!!!=========Cutplanes - End Cutting
if GS_Cutting then
	if ABS(gs_cutang1) > EPS OR ABS(gs_cutang2) > EPS then cutend

	if ABS(gs_cutang3) > EPS OR ABS(gs_cutang4) > EPS then
		del 1
		cutend
	endif
else
	cutend
	cutend
	if ABS(AC_bottomConnPlane[1]) > EPS then
		del 1
	endif
endif
!!!=========Cutplanes - End Cutting
!!!================================

END


!!!=========================================
!!!============	SLOTS ======================
!!!=========================================

10: !Ritght socket

	cutpoly 5,
		POZ_WIDTH_SEG, -ac_clampWidth/2,
		POZ_WIDTH_SEG-ac_clampDepth, -ac_clampWidth/2,
		POZ_WIDTH_SEG-ac_clampDepth, ac_clampWidth/2,
		POZ_WIDTH_SEG, ac_clampWidth/2,
		POZ_WIDTH_SEG, -ac_clampWidth/2

RETURN

20: !Front socket

if ABS(gs_originOffsetX) < EPS then !Boundary outside

	cutpoly 5,
		gs_rightOffset/2+ac_clampWidth/2, POZ_DEPTH_SEG,
		gs_rightOffset/2+ac_clampWidth/2, POZ_DEPTH_SEG-ac_clampDepth,
		gs_rightOffset/2-ac_clampWidth/2, POZ_DEPTH_SEG-ac_clampDepth,
		gs_rightOffset/2-ac_clampWidth/2, POZ_DEPTH_SEG,
		gs_rightOffset/2+ac_clampWidth/2, POZ_DEPTH_SEG

endif

if ABS(gs_rightOffset) < EPS then !Boundary inside

	cutpoly 5,
		-gs_originOffsetX/2+ac_clampWidth/2, POZ_DEPTH_SEG,
		-gs_originOffsetX/2+ac_clampWidth/2, POZ_DEPTH_SEG-ac_clampDepth,
		-gs_originOffsetX/2-ac_clampWidth/2, POZ_DEPTH_SEG-ac_clampDepth,
		-gs_originOffsetX/2-ac_clampWidth/2, POZ_DEPTH_SEG,
		-gs_originOffsetX/2+ac_clampWidth/2, POZ_DEPTH_SEG

endif

if ABS(gs_rightOffset) > EPS AND ABS(gs_originOffsetX) > EPS then

	cutpoly 5,
		ac_clampWidth/2, POZ_DEPTH_SEG,
		ac_clampWidth/2, POZ_DEPTH_SEG-ac_clampDepth,
		-ac_clampWidth/2, POZ_DEPTH_SEG-ac_clampDepth,
		-ac_clampWidth/2, POZ_DEPTH_SEG,
		ac_clampWidth/2, POZ_DEPTH_SEG

endif

RETURN

30: !Left socket

	cutpoly 5,
		-NEG_WIDTH_SEG, ac_clampWidth/2,
		-NEG_WIDTH_SEG+ac_clampDepth, ac_clampWidth/2,
		-NEG_WIDTH_SEG+ac_clampDepth, -ac_clampWidth/2,
		-NEG_WIDTH_SEG, -ac_clampWidth/2,
		-NEG_WIDTH_SEG, ac_clampWidth/2

RETURN

40: !Back socket

if ABS(gs_originOffsetX) < EPS then !Boundary outside

	cutpoly 5,
		gs_rightOffset/2-ac_clampWidth/2, -NEG_DEPTH_SEG,
		gs_rightOffset/2-ac_clampWidth/2, -NEG_DEPTH_SEG+ac_clampDepth,
		gs_rightOffset/2+ac_clampWidth/2, -NEG_DEPTH_SEG+ac_clampDepth,
		gs_rightOffset/2+ac_clampWidth/2, -NEG_DEPTH_SEG,
		gs_rightOffset/2-ac_clampWidth/2, -NEG_DEPTH_SEG

endif

if ABS(gs_rightOffset) < EPS then !Boundary inside

	cutpoly 5,
		-gs_originOffsetX/2-ac_clampWidth/2, -NEG_DEPTH_SEG,
		-gs_originOffsetX/2-ac_clampWidth/2, -NEG_DEPTH_SEG+ac_clampDepth,
		-gs_originOffsetX/2+ac_clampWidth/2, -NEG_DEPTH_SEG+ac_clampDepth,
		-gs_originOffsetX/2+ac_clampWidth/2, -NEG_DEPTH_SEG,
		-gs_originOffsetX/2-ac_clampWidth/2, -NEG_DEPTH_SEG

endif

if ABS(gs_rightOffset) > EPS AND ABS(gs_originOffsetX) > EPS then

	cutpoly 5,
		-ac_clampWidth/2, -NEG_DEPTH_SEG,
		-ac_clampWidth/2, -NEG_DEPTH_SEG+ac_clampDepth,
		ac_clampWidth/2, -NEG_DEPTH_SEG+ac_clampDepth,
		ac_clampWidth/2, -NEG_DEPTH_SEG,
		-ac_clampWidth/2, -NEG_DEPTH_SEG

endif

RETURN


50: !45 socket

	slot5depth = POZ_WIDTH_SEG - ac_clampDepth
	rotz slot5Angle

	cutpoly 5,
		slot5depth,-ac_clampWidth/2,
		slot5depth,ac_clampWidth/2,
		slot5depth+2*A,ac_clampWidth/2,
		slot5depth+2*A,-ac_clampWidth/2,
		slot5depth,-ac_clampWidth/2

	del 1

RETURN

60: !135 socket

	slot6depth = NEG_WIDTH_SEG - ac_clampDepth
	rotz slot6Angle

	cutpoly 5,
		slot6depth,-ac_clampWidth/2,
		slot6depth,ac_clampWidth/2,
		slot6depth+2*A,ac_clampWidth/2,
		slot6depth+2*A,-ac_clampWidth/2,
		slot6depth,-ac_clampWidth/2

	del 1

RETURN


70: !225 socket

	slot7depth = NEG_WIDTH_SEG - ac_clampDepth
	rotz slot7Angle

	cutpoly 5,
		slot7depth,-ac_clampWidth/2,
		slot7depth,ac_clampWidth/2,
		slot7depth+SEGMENT_MAX*2,ac_clampWidth/2,
		slot7depth+SEGMENT_MAX*2,-ac_clampWidth/2,
		slot7depth,-ac_clampWidth/2

	del 1

RETURN


80: !315 socket

	slot8depth = NEG_WIDTH_SEG - ac_clampDepth
	rotz slot8Angle

	cutpoly 5,
		slot8depth,-ac_clampWidth/2,
		slot8depth,ac_clampWidth/2,
		slot8depth+SEGMENT_MAX*2,ac_clampWidth/2,
		slot8depth+SEGMENT_MAX*2,-ac_clampWidth/2,
		slot8depth,-ac_clampWidth/2

	del 1

RETURN

