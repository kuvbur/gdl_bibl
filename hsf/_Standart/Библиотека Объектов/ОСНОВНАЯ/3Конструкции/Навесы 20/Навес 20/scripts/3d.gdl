
if gs_detlevel_3D_m = DETLEVEL_OFF then end

if gs_shadow then
	shadow on
else
	shadow off
endif

RESOL gs_resol
PEN gs_cont_pen


MUL 1,-1,1

! --- HOTSPOT EDITING A ---

unID=1
hotspot 0, 0, 0, unID, A, 1

unID=unID+1
hotspot A, 0, 0, unID, A, 2

unID=unID+1
hotspot 0, 0, 0, unID, A, 3


! --- HOTSPOT EDITING gs_awbox_x ---

unID=unID+1
hotspot 0, 0, gs_awbox_y, unID, gs_awbox_x, 1+128

unID=unID+1
hotspot 0, gs_awbox_x, gs_awbox_y, unID, gs_awbox_x, 2

unID=unID+1
hotspot 0, 0, gs_awbox_y, unID, gs_awbox_x, 3


! --- HOTSPOT EDITING projection ---

unID=unID+1
hotspot a/2, gs_awbox_x, gs_awbox_y, unID, gs_awning_size,1+128

unID=unID+1
hotspot a/2, gs_awbox_x+gs_awning_size*(cos(gs_alpha)), gs_awbox_y-gs_awning_size*(sin(gs_alpha)), unID, gs_awning_size, 2

unID=unID+1
hotspot a/2, gs_awbox_x, gs_awbox_y, unID, gs_awning_size, 3


! --- HOTSPOT EDITING pitch ---
!base
unID=unID+1
hotspot 0.1, gs_awbox_x+gs_awning_size, gs_awbox_y,unID,gs_alpha,4+128
!moving
unID=unID+1
hotspot 0.1, gs_awbox_x+gs_awning_size*(cos(gs_alpha)), gs_awbox_y-gs_awning_size*(sin(gs_alpha)),unID,gs_alpha, 5
!center
unID=unID+1
hotspot 0.1, gs_awbox_x, gs_awbox_y,unID,gs_alpha, 6
!ref
unID=unID+1
hotspot 0.1, -(gs_awbox_x+gs_awning_size*(cos(gs_alpha))), -(gs_awbox_y-gs_awning_size*(sin(gs_alpha))),unID,gs_alpha, 7


! --- HOTSPOT EDITING pitch ---
!base
unID=unID+1
hotspot a-0.1, gs_awbox_x+gs_awning_size, gs_awbox_y,unID,gs_alpha,4+128
!moving
unID=unID+1
hotspot a-0.1, gs_awbox_x+gs_awning_size*(cos(gs_alpha)), gs_awbox_y-gs_awning_size*(sin(gs_alpha)),unID,gs_alpha, 5
!center
unID=unID+1
hotspot a-0.1, gs_awbox_x, gs_awbox_y,unID,gs_alpha, 6
!ref
unID=unID+1
hotspot a-0.1, -(gs_awbox_x+gs_awning_size*(cos(gs_alpha))), -(gs_awbox_y-gs_awning_size*(sin(gs_alpha))),unID,gs_alpha, 7


! --- HOTSPOT EDITING overhang ---

unID=unID+1
hotspot a/2, gs_awbox_x+gs_awning_size*(cos(gs_alpha)), gs_awbox_y-gs_awning_size*(sin(gs_alpha))-gs_ra_aw,unID,gs_awning_oversize,1+128

unID=unID+1
hotspot a/2, gs_awbox_x+gs_awning_size*(cos(gs_alpha)), gs_awbox_y-gs_awning_size*(sin(gs_alpha))-gs_ra_aw-gs_awning_oversize,unID,gs_awning_oversize, 2

unID=unID+1
hotspot a/2, gs_awbox_x+gs_awning_size*(cos(gs_alpha)), gs_awbox_y-gs_awning_size*(sin(gs_alpha)),unID,gs_awning_oversize, 3




size_x= gs_awning_size*(cos(gs_alpha))
size_y= gs_awning_size*(sin(gs_alpha))

ax=gs_rf_aw*cos(90-gs_alpha)
ay=gs_rf_aw*sin(90-gs_alpha)
bx=(gs_aw_weight/2)+(gs_awning_size*cos(gs_alpha))
by=-gs_ra_aw+(gs_awning_size*sin(gs_alpha))

lenght_kar=0.5*sqr((gs_awning_lenght-gs_ra_aw-gs_rf_aw-gs_awbox_x)**2+gs_awbox_x**2)
lenght_size=(sqr((size_x-gs_ra_aw)**2+(size_y-gs_awbox_y)**2))

beta=atn((by+ay)/(bx-ax))

delta=atn((size_y-gs_awbox_y)/(size_x-gs_ra_aw))


! ponyva

material gs_mat_aw

length= a-0.2

add 0.05, gs_awbox_x, gs_awbox_y

gs_r_aw=gs_rf_aw

gosub 100

del 1

add 0.1, gs_awbox_x, gs_awbox_y

if beta<0 then
	rotx 180-beta
else
	rotx -beta
endif

add 0, 0, gs_rf_aw-gs_aw_weight

block a-0.2, (sqr(((bx-(gs_aw_weight/2)-ax)**2)+((by+ay)**2))), gs_aw_weight

del 3


if gs_aw_typ_m = AW_STRAIGHT then

	add 0.1, gs_awbox_x+size_x, gs_awbox_y-size_y+gs_ra_aw

	rotx -90

	addz -gs_aw_weight/2

	block a-0.2, gs_ra_aw*2+gs_awning_oversize, gs_aw_weight

	del 3

else

	hullam=int(((a-0.2)/0.3))*2

	hull=(a-0.2)/hullam

	add 0.1, gs_awbox_x+size_x, gs_awbox_y-size_y+gs_ra_aw

	rotx -90

	addz -gs_aw_weight/2

	prism_ 4, gs_aw_weight,
		0, 0, 8,
		hull/2, 0, 8,
		hull/2, gs_ra_aw*2+gs_awning_oversize-hull/2, 13,
		0, gs_ra_aw*2+gs_awning_oversize, 1013

	addx hull/2

	for jj=1 to hullam/2-1

		prism_ 5, gs_aw_weight,
		0, 0, 8,
		hull*2, 0, 8,
		hull*2, gs_ra_aw*2+gs_awning_oversize-hull/2, 15,
		hull, gs_ra_aw*2+gs_awning_oversize-hull/2, 1015,
		0, gs_ra_aw*2+gs_awning_oversize-hull/2, 1008

		addx hull*2

	next jj

		prism_ 6, gs_aw_weight,
		0, 0, 8,
		3*hull/2, 0, 13,
		3*hull/2, gs_ra_aw*2+gs_awning_oversize, 13,
		3*hull/2, gs_ra_aw*2+gs_awning_oversize-hull/2, 915,
		0, gs_ra_aw*2+gs_awning_oversize-hull/2, 3015,
		0, gs_ra_aw*2+gs_awning_oversize-hull/2, 1008

	del hullam/2-1

	del 1

	del 2

	del 1

endif


! feszitorud

material gs_mat_metal

length= a-0.10

add 0.05, gs_awbox_x+size_x, gs_awbox_y-size_y

gs_r_aw=gs_ra_aw

gosub 110

del 1

! rogzites

addx bb
addz -box_y/2

block a-2*bb, box_x, box_y

del 2

! konyokok

	add a-0.1, box_x+gs_arm_size/2, gs_arm_size/2

	if delta<-85  then
		rotx 180-delta
	else
		rotx -delta
	endif

	addz -gs_arm_size/2

	if ((lenght_size)/(lenght_kar*2))>1 then
		rotz 0
	else
		rotz acs((lenght_size)/(lenght_kar*2))
	endif

	gosub 200

	del 1

	addx -a+0.2

	if ((lenght_size)/(lenght_kar*2))>1 then
		rotz 0
	else
		rotz -acs((lenght_size)/(lenght_kar*2))
	endif

	gosub 200

	addy lenght_kar

	if ((lenght_size)/(lenght_kar*2))>1 then
		rotz 0
	else
		rotz 2*acs((lenght_size)/(lenght_kar*2))
	endif

	gosub 200

	del 2

	del 2

	if ((lenght_size)/(lenght_kar*2))>1 then
		rotz 0
	else
		rotz acs((lenght_size)/(lenght_kar*2))
	endif

	addy lenght_kar

	if ((lenght_size)/(lenght_kar*2))>1 then
		rotz 0
	else
		rotz -2*acs((lenght_size)/(lenght_kar*2))
	endif

	gosub 200

	del 2

	del 4


material gs_mat_ved

! vedopanelek

	addz -gs_arm_size*2

	gosub 300

	addx a-bb

	gosub 300

	del 2

if gs_ved=1 then

	roty -90

	addz -a+0.05

	addx gs_awbox_y+gs_rf_aw

	prism_ 6, a-0.1,
		0.08, 0,  15,
		0.08, gs_rf_aw/2+gs_awbox_x,  15,
		0, gs_rf_aw/2+gs_awbox_x+0.08/(tan(23)),  15,
		0.04*cos(23), gs_rf_aw/2+gs_awbox_x+0.08/(tan(23))+0.04*sin(23),  15,
		0.12, gs_rf_aw/2+gs_awbox_x+0.04*tan(23),  15,
		0.12, 0,  15

	del 3

endif

DEL 1
end





100:

! ponyvatekercs

material gs_mat_metal

REVOLVE 2, 360, 1+2,
	0, gs_r_aw/2, 1,
	length+0.1, gs_r_aw/2, 1

addx 0.05

material gs_mat_aw

REVOLVE 2, 360, 1+2,
	0, gs_r_aw, 1,
	length, gs_r_aw, 1

del 1

return

110:

! tekercs

roty 90

cylind  length, gs_r_aw

del 1

return


200:

! konyokkar

if gs_arm_typ_m = ARM_RECT then

	rotx -90

	prism_ 4, lenght_kar,
		-gs_arm_size/2, -gs_arm_size/2, 15,
		-gs_arm_size/2,  gs_arm_size/2, 15,
		 gs_arm_size/2,  gs_arm_size/2, 15,
		 gs_arm_size/2, -gs_arm_size/2, 15

	del 1

else

	rotx -90

cylind  lenght_kar, gs_arm_size/2

	del 1

endif

return

300:

! vedopanel

rotz 90

rotx 90

prism_ 6, bb,
	0, 0, 15,
	0, gs_awbox_y+gs_arm_size*2, 15,
	gs_awbox_x-gs_rf_aw-gs_arm_size/2, gs_awbox_y+gs_arm_size*2, 13,
	gs_awbox_x, gs_awbox_y+gs_arm_size*2, 915,
	0, -270, 4015,
	gs_awbox_x, 0, 15

del 2

return
