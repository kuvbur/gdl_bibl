
!! Solar Shade Curved 01


PEN gs_cont_pen
FILL gs_fill_type


mul2 -1,1
add2 -rad_f-fw/2,fw/2

rad_r=rad_r-fw/2
ax=(rad_f-rad_r)*cos(tang)
rad_f=rad_r+ax


hotspot2 0,0,0,alfa,6+512
hotspot2 0,0,3,alfa,6+512


o1=(((fw/2)*180)/PI)/(rad_r)
o2=(((fw/2)*180)/PI)/(rad_f)

! *** Rear
if rear then
i=0
if iSolarFrameType = FRAMETYPE_CURVED then tc = 5 else tc = alfa_step
PUT rad_r*COS(alfa2+o1),rad_r*SIN(alfa2+o1),1
for i=o1 to alfa step tc
 PUT rad_r*COS(alfa2+i),rad_r*SIN(alfa2+i),1
next i
PUT rad_r*COS(alfa2+alfa-o1),rad_r*SIN(alfa2+alfa-o1),1
for i=alfa-o1 to 0 step -tc
 PUT (rad_r+fw)*COS(alfa2+i),((rad_r+fw))*SIN(alfa2+i),1
next i
PUT (rad_r+fw)*COS(alfa2+o1),((rad_r+fw))*SIN(alfa2+o1),1
PUT rad_r*COS(alfa2+o1),rad_r*SIN(alfa2+o1),-1
POLY2_B NSP/3, 1+2+4+64,gs_fill_pen, gs_back_pen,GET (NSP)
endif

! *** Front
if front then
i=0
if iSolarFrameType = FRAMETYPE_CURVED then tc = 5 else tc = alfa_step
PUT rad_f*COS(alfa2+o2),rad_f*SIN(alfa2+o2),1
for i=o2 to alfa step tc
 PUT rad_f*COS(alfa2+i),rad_f*SIN(alfa2+i),1
next i
PUT rad_f*COS(alfa2+alfa-o2),rad_f*SIN(alfa2+alfa-o2),1
for i=alfa-o2 to o2 step -tc
 PUT (rad_f+fw)*COS(alfa2+i),((rad_f+fw))*SIN(alfa2+i),1
next i
PUT (rad_f+fw)*COS(alfa2+o2),((rad_f+fw))*SIN(alfa2+o2),1
PUT rad_f*COS(alfa2+o2),rad_f*SIN(alfa2+o2),-1
POLY2_B NSP/3, 1+2+4+64,gs_fill_pen, gs_back_pen,GET (NSP)
endif

! *** Arms
for i=alfa_step to alfa-alfa_step step alfa_step
	x1jsp=(rad_r)*COS(alfa2+i)
	y1jsp=(rad_r)*SIN(alfa2+i)
	x1bsp=((rad_f+fw))*COS(alfa2+i)
	y1bsp=((rad_f+fw))*SIN(alfa2+i)
	x1=((rad_r+fw))*COS(alfa2+i+o1)
	y1=((rad_r+fw))*SIN(alfa2+i+o1)
	x2=((rad_r+fw))*COS(alfa2+i-o1)
	y2=((rad_r+fw))*SIN(alfa2+i-o1)
	x3=((rad_f))*COS(alfa2+i+o2)
	y3=((rad_f))*SIN(alfa2+i+o2)
	x4=((rad_f))*COS(alfa2+i-o2)
	y4=((rad_f))*SIN(alfa2+i-o2)

	POLY2_B 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
	 x1,y1,1,
	 x2,y2,1,
	 x4,y4,1,
	 x3,y3,1,
	 x1,y1,-1

	hotspot2 x1jsp,y1jsp
	hotspot2 x1bsp,y1bsp
next i

! *** Axes
if tang<>90 and tang<>-90 then
DEFINE LINE_TYPE "v1" .2,5,.005,.002,.0,.002,.005
SET LINE_TYPE v1
for k=0 to arms
 it=k*alfa_step
 for j=1 to numl
  omega=(((fw)*180)/PI)/(rad_r+(j*lax))
  dx=((j*lax)-(ldepth/2)+(fw/2))*cos(tang)
  x1=( rad_r+dx ) * COS(alfa2+it+omega)
  y1=( rad_r+dx ) * SIN(alfa2+it+omega)
  x2=( rad_r+dx ) * COS(alfa2+it+alfa_step-omega)
  y2=( rad_r+dx ) * SIN(alfa2+it+alfa_step-omega)
  LINE2 x1,y1,x2,y2
 next j
next k
SET LINE_TYPE 0
endif

! *** Right
if lf then
x1o=((rad_f+fw))*COS(alfa2-o2)
y1o=((rad_f+fw))*SIN(alfa2-o2)
x2o=(rad_r)*COS(alfa2-o1)
y2o=((rad_r+fw))*SIN(alfa2-o1)
x1=(rad_r)*COS(alfa2+o1)
y1=((rad_r+fw))*SIN(alfa2+o1)
x2=(rad_r)*COS(alfa2-o1)
y2=((rad_r+fw))*SIN(alfa2-o1)
x3=(rad_f+fw)*COS(alfa2+o2)
y3=((rad_f+fw))*SIN(alfa2+o2)
x4=(rad_f+fw)*COS(alfa2-o2)
y4=((rad_f+fw))*SIN(alfa2-o2)

POLY2_B 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
 x1,y1,1,
 x2,y2,1,
 x4,y4,1,
 x3,y3,1,
 x1,y1,-1

hotspot2 x1o,y1o,1,alfa,4+256+512
hotspot2 x2o,y2o,4,alfa,4+256+512
endif


! *** Left
if rf then
x1o=((rad_f+fw))*COS(alfa2+alfa+o2)
y1o=((rad_f+fw))*SIN(alfa2+alfa+o2)
x2o=(rad_r)*COS(alfa2+alfa+o1)
y2o=((rad_r+fw))*SIN(alfa2+alfa+o1)
x1=(rad_r)*COS(alfa2+o1+alfa)
if alfa=180 then
 y1=((rad_r+fw))*SIN(alfa2+o1+alfa)
 y2=((rad_r+fw))*SIN(alfa2-o1+alfa)
 y2o=((rad_r+fw))*SIN(alfa2+alfa+o1)
else
 y1=(rad_r)*SIN(alfa2+o1+alfa)
 y2=(rad_r)*SIN(alfa2-o1+alfa)
 y2o=(rad_r)*SIN(alfa2+alfa+o1)
endif
x2=(rad_r)*COS(alfa2-o1+alfa)
x3=((rad_f+fw))*COS(alfa2+o2+alfa)
y3=((rad_f+fw))*SIN(alfa2+o2+alfa)
x4=((rad_f+fw))*COS(alfa2-o2+alfa)
y4=((rad_f+fw))*SIN(alfa2-o2+alfa)

POLY2_B 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
 x1,y1,1,
 x2,y2,1,
 x4,y4,1,
 x3,y3,1,
 x1,y1,-1

hotspot2 x1o,y1o,2,alfa,5+512
hotspot2 x2o,y2o,5,alfa,5+512
endif

1000:
del top
end


