
!! Solar Shade Straight

if gs_shadow=1 then SHADOW ON
if gs_shadow=0 then SHADOW OFF

IF gs_detlevel_3D_m = DLEV3D_OFF THEN
 GOTO 1000
ELSE
 addy -fw/2
 addz -fh/2
 ROTX tang
 GOSUB 10
 GOSUB 20
 GOTO 1000
ENDIF

!! *** Frames
10:
SET MATERIAL framemat

 IF lframe THEN
  DEL NTR()-3 : ADDY fw : BLOCK fw,B-2*fw,fh
 ENDIF
 IF rframe THEN
  DEL NTR()-3 : ADDY fw : ADDX A-fw : BLOCK fw,B-2*fw,fh
 ENDIF
 IF fframe THEN
  DEL NTR()-3 : BLOCK A,fw,fh
 ENDIF
 IF rrframe THEN
  DEL NTR()-3 : ADDY B-fw : BLOCK A,fw,fh
 ENDIF

 xx= ( A - ( fw ) )/(arms+1)

 IF arms>0 THEN
  DEL NTR()-3
   ADDY fw
  FOR i=1 TO arms
   ADDX xx
   BLOCK fw,B-2*fw,fh
  NEXT i
 ENDIF
RETURN


! *** Louvres
20:
set material lomat
if iLouvrePosition = LOUVRE_HORIZONTAL then
 del ntr()-3
 addy (-ldepth/2)+fw
 for j=0 to arms
  del ntr()-4
  addx fw
  addx j*xx
  addz fh/2
  for i=1 to numl
   dl=xx-fw
   addy lax
   gosub 100
  next i
 next j
endif

if iLouvrePosition = LOUVRE_VERTICAL then
 dl=B-2*fw
 for j=0 to arms
  del ntr()-3
  addz fh/2
  addy fw
  addx (j*xx)-ldepth/2
  for i=1 to numl
   addx lax
   gosub 100
  next i
 next j
endif
return

! *** Actual louvre
100:
IF iLouvrePosition = LOUVRE_HORIZONTAL THEN
 ROTY 90 : ROTZ ang
ENDIF
IF iLouvrePosition = LOUVRE_VERTICAL THEN
 ROTX -90 : ROTZ 90-ang
ENDIF
 IF gs_detlevel_3D_m = DLEV3D_SIMPLE THEN
 EXTRUDE 4,0,0,dl,1+2+4+16+32,
   0-(ldepth/2),lthick/2,0,
   ldepth/2,lthick/2,0,
   ldepth/2,-(lthick/2),0,
   0-(ldepth/2),0-(lthick/2),0
 ENDIF
 IF gs_detlevel_3D_m = DLEV3D_DETAILED THEN
  IF iLouvreStyle = LOUVRESTYLE_1 THEN
   EXTRUDE 4,0,0,dl,1+2+4+16+32,
	0-(ldepth/2),0,1,
	0,lthick/2,1,
	ldepth/2,0,1,
	0,0-(lthick/2),1
  ENDIF
  IF iLouvreStyle = LOUVRESTYLE_2 THEN
   EXTRUDE 4,0,0,dl,1+2+4+16+32,
	0-(ldepth/2),-(lthick/2),1,
	-ldepth/9,lthick/2,1,
	ldepth/2,lthick/2,1,
	ldepth/9,0-(lthick/2),1
  ENDIF
  IF iLouvreStyle = LOUVRESTYLE_3 THEN
   EXTRUDE 10,0,0,dl,1+2+4+16+32,
	0-(ldepth/2),0,1,
	-ldepth/3,lthick/3,1,
	-ldepth/6,lthick/2,1,
	ldepth/6,lthick/2,1,
	ldepth/3,lthick/3,1,
	ldepth/2,0,1,
	ldepth/3,-lthick/3,1,
	ldepth/6,-lthick/2,1,
	-ldepth/6,-lthick/2,1,
	-ldepth/3,-lthick/3,1
  ENDIF
 ENDIF
DEL 2
RETURN

!! *** End
1000:
DEL TOP
END
