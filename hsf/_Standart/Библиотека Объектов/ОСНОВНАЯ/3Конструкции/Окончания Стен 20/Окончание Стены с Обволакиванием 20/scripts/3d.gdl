
!<TZs>

if _bShowWrappedEnd then

	if not(b3DRepresentation) then
	end
	endif

	_useWallMat = ( matCap = WALL_MAT_A |\
					matCap = WALL_MAT_B |\
					matCap = WALL_MAT_EDGE)

	elevation = 0
	if AC_fit_to_wall_height then
		ZZYZX = WALL_HEIGHT
		elevation = -GLOB_ELEVATION / sin (ac_wall_slant_angle1)
	endif

	dim trafo_array[][]

	rotz -90
	numTrafo = 1
	trafo_array[numTrafo][1] = TRAFO_ROT_Z
	trafo_array[numTrafo][2] = -90

	addz elevation
	numTrafo = numTrafo + 1
	trafo_array[numTrafo][1] = TRAFO_ADD_Z
	trafo_array[numTrafo][2] = elevation

	pen gs_wallEndPen_3D

	if abs(wallIncl) > EPS or abs(ac_wall_slant_angle1 - ac_wall_slant_angle2) > EPS then
	trapezoidCoreSkin = 0
	for j = 1 to WALL_SKINS_NUMBER
		if abs(WALL_SKINS_PARAMS [j][SKIN_THICKEN_TRAPEZOID] - 1) < EPS then
			trapezoidCoreSkin = j
		endif
	next j
	if not(SYMB_MIRRORED) then
		if 1 = trapezoidCoreSkin then
			firstTrapType = 1
			midTrapType = 2
			secTrapType = 2
		else
			if WALL_SKINS_NUMBER = trapezoidCoreSkin then
				firstTrapType = 0
				midTrapType = 0
				secTrapType = 1
			else
				firstTrapType = 0
				midTrapType = 1
				secTrapType = 2
			endif
		endif
	else
		if 1 = trapezoidCoreSkin then
			firstTrapType = 1
			midTrapType = 0
			secTrapType = 0
		else
			if WALL_SKINS_NUMBER = trapezoidCoreSkin then
				firstTrapType = 2
				midTrapType = 2
				secTrapType = 1
			else
				firstTrapType = 2
				midTrapType = 1
				secTrapType = 0
			endif
		endif
	endif
	else
	firstTrapType = 0
	midTrapType = 0
	secTrapType = 0
	endif

	bDoCut = 1
	for i = 1 to 2
	! bottom arm
	if thickFirst > eps then
		! bottom corner
		if thickCap > eps then
			if length > eps then
				mask2 = 0
				mask3 = 0
			else
				mask2 = 1 + 2
				mask3 = 2
			endif
			call "SkinRect" PARAMETERS trafo_array = trafo_array, numTrafo = numTrafo, gs_useWallMat = _useWallMat,
					startX = 0, startY = 0,
					endX = thickCap, endY = thickFirst,
					innerRadius = r0, incAngle = wallIncl,
					trapType = firstTrapType,
					ac_wall_slant_angle1 = ac_wall_slant_angle1, ac_wall_slant_angle2 = ac_wall_slant_angle2,
					bDoCut = bDoCut, height = ZZYZX,
					edge1 = 1 + 2 + 8, edge2 = 4 + 8 + mask2, edge3 = 1 + 8 + mask3, edge4 = 1 + 2 + 8,
					matLeft = matCap, matRight = matCap, matVert = matCap, matHoriz = matCap
			mask1 = 0
			mask4 = 0
		else
			mask1 = 2
			mask4 = 1 + 2 + 4
		endif

		! bottom arm
		if length > thickCap then
			call "SkinRect" PARAMETERS trafo_array = trafo_array, numTrafo = numTrafo, gs_useWallMat = _useWallMat,
					startX = thickCap, startY = 0,
					endX = length, endY = thickFirst,
					innerRadius = r0, incAngle = wallIncl,
					trapType = firstTrapType,
					ac_wall_slant_angle1 = ac_wall_slant_angle1, ac_wall_slant_angle2 = ac_wall_slant_angle2,
					bDoCut = bDoCut, height = ZZYZX,
					edge1 = 1 + 4 + 8 + mask1, edge2 = 1 + 2 + 4 + 8, edge3 = 1 + 2 + 4 + 8, edge4 = 8 + mask4,
					matLeft = matCap, matRight = matCap, matVert = matCap, matHoriz = matCap
		endif
	endif

	! upper arm
	if thickSecond > eps then
		! upper corner
		if thickCap > eps then
			if length > eps then
				mask2 = 0
				mask3 = 0
			else
				mask2 = 2 + 4
				mask3 = 2
			endif
			call "SkinRect" PARAMETERS trafo_array = trafo_array, numTrafo = numTrafo, gs_useWallMat = _useWallMat,
					startX = 0, startY = thickSecFromBeg,
					endX = thickCap, endY = WALL_THICKNESS,
					innerRadius = r0, incAngle = wallIncl,
					trapType = secTrapType,
					ac_wall_slant_angle1 = ac_wall_slant_angle1, ac_wall_slant_angle2 = ac_wall_slant_angle2,
					bDoCut = bDoCut, height = ZZYZX,
					edge1 = 6 + 8, edge2 = 1 + 8 + mask2, edge3 = 4 + 8 + mask3, edge4 = 6 + 8,
					matLeft = matCap, matRight = matCap, matVert = matCap, matHoriz = matCap
			mask1 = 0
			mask4 = 0
		else
			mask1 = 2
			mask4 = 1 + 2 + 4
		endif

		! upper arm
		if length > thickCap then
			call "SkinRect" PARAMETERS trafo_array = trafo_array, numTrafo = numTrafo, gs_useWallMat = _useWallMat,
					startX = thickCap, startY = thickSecFromBeg,
					endX = length, endY = WALL_THICKNESS,
					innerRadius = r0, incAngle = wallIncl,
					trapType = secTrapType,
					ac_wall_slant_angle1 = ac_wall_slant_angle1, ac_wall_slant_angle2 = ac_wall_slant_angle2,
					bDoCut = bDoCut, height = ZZYZX,
					edge1 = 1 + 4 + 8 + mask1, edge2 = 1 + 2 + 4 + 8, edge3 = 1 + 2 + 4 + 8, edge4 = 8 + mask4,
					matLeft = matCap, matRight = matCap, matVert = matCap, matHoriz = matCap
		endif
	endif

	! middle part
	if thickCap > eps then
		mask = 0
		if thickFirst < eps then
			mask = mask + 1
		endif
		if thickSecond < eps then
			mask = mask + 4
		endif
		if not ((abs(wallIncl) < EPS) and (abs(WALL_THICKNESS - thickFirst - thickSecond) < EPS)) then
			call "SkinRect" PARAMETERS trafo_array = trafo_array, numTrafo = numTrafo, gs_useWallMat = _useWallMat,
					startX = 0, startY = thickFirst,
					endX = thickCap, endY = thickSecFromBeg,
					innerRadius = r0, incAngle = wallIncl,
					trapType = midTrapType,
					ac_wall_slant_angle1 = ac_wall_slant_angle1, ac_wall_slant_angle2 = ac_wall_slant_angle2,
					bDoCut = bDoCut, height = ZZYZX,
					edge1 = 2 + 8 + mask, edge2 = 2 + 8 + mask, edge3 = 2 + 8 + mask, edge4 = 2 + 8 + mask,
					matLeft = matCap, matRight = matCap, matVert = matCap, matHoriz = matCap
		endif
	endif

	bDoCut = 0
	next i

	! --- replace AC hotspots at the ends ---
	call "Resize Wall End" PARAMETERS ZZYZX = ZZYZX,
		GS_HotspotUnIDBegin = 20000,
		cutWidthAngle = cutWidthAngle, AC_CutWidth = AC_CutWidth,
		r0 = r0, wallIncl = wallIncl, onWallBeg = onWallBeg

	del 2
endif
