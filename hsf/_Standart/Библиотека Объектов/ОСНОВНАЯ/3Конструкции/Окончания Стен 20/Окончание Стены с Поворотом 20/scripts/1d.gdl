
EPS = 0.001

! const values for parameter: trafo_array
TRAFO_ADD_X = 1
TRAFO_ADD_Y = 2
TRAFO_ADD_Z = 3
TRAFO_ROT_X = 4
TRAFO_ROT_Y = 5
TRAFO_ROT_Z = 6
TRAFO_MUL_X = 7
TRAFO_MUL_Y = 8
TRAFO_MUL_Z = 9

SKIN_FILL				=  1
SKIN_THICKNESS			=  2
SKIN_CONT_PEN			=  3	! Obsolete (old contour pen)
SKIN_FILL_PEN			=  4
SKIN_FILL_BACK_PEN		=  5
SKIN_CORE_STATUS		=  6
SKIN_UPPER_LINE_PEN		=  7
SKIN_UPPER_LINE_TYPE	=  8
SKIN_LOWER_LINE_PEN		=  9
SKIN_LOWER_LINE_TYPE	= 10
SKIN_END_FACE_PEN		= 11
SKIN_FILL_ORIENTATION	= 12	! 0 - global, 1 - local
SKIN_TYPE				= 13	! 0 - cut, 1 - below cutplane, 2 - above cutplane (all skin types are 0 for simple walls)
!	For D/W in complex walls on the floor plan this variable contains the data of all cut skins, for wall ends on the floor plan the data of all skins.
!	For D/W and wall ends in the 3D window contains the data of the skins actually cut by the D/W or wall end
SKIN_END_LINE_TYPE		= 14
SKIN_FINISH_STATUS		= 15
SKIN_FILL_FIT_TO_SKIN	= 16
SKIN_THICKEN_TRAPEZOID	= 17

nExtendedSkin = nExtendedSkins + gs_show_extendedSkin_2 + gs_show_extendedSkin_3 + gs_show_extendedSkin_4

if gs_cont_pen < EPS then gs_cont_pen = 1

turnLast = 0
if bTurnRefSide and SYMB_MIRRORED or not(bTurnRefSide) and not(SYMB_MIRRORED) then
	turnLast = 1
endif

isComposite = 0
if WALL_COMPS_NAME <> "" and WALL_SKINS_NUMBER > 1 then
	isComposite = 1
endif

yWallStart = 0
yWallEnd = WALL_THICKNESS

iOnlyCutSkin = 0
iLeftmost = 1
iRightmost = WALL_SKINS_NUMBER
if isComposite then
	nSkins = WALL_SKINS_NUMBER
	bBefore = 1
	for i = 1 to WALL_SKINS_NUMBER
		if WALL_SKINS_PARAMS[i][SKIN_TYPE] < 1 then
			iOnlyCutSkin = i
			bBefore = 0
		else
			nSkins = nSkins - 1
		endif
		if bBefore then
			yWallStart = yWallStart + WALL_SKINS_PARAMS[i][SKIN_THICKNESS]
			iLeftmost = iLeftmost + 1
		endif
	next i

	if iOnlyCutSkin = 0 then end	! No cut skins found

	if nSkins <= 1 then
		isComposite = false
		yWallEnd = yWallStart + WALL_SKINS_PARAMS[iOnlyCutSkin][SKIN_THICKNESS]
		WALL_FILL = WALL_SKINS_PARAMS[iOnlyCutSkin][SKIN_FILL]
		iRightmost = iOnlyCutSkin
	else
		bBefore = 1
		for i = 0 to WALL_SKINS_NUMBER-1
			if WALL_SKINS_PARAMS[WALL_SKINS_NUMBER-i][SKIN_TYPE] < 1 then
				bBefore = 0
			endif
			if bBefore then
				yWallEnd = yWallEnd - WALL_SKINS_PARAMS[WALL_SKINS_NUMBER-i][SKIN_THICKNESS]
				iRightmost = iRightmost - 1
			endif
		next i
	endif

	if nTurnSkins > iRightmost - iLeftmost then
		nTurnSkins = iRightmost - iLeftmost
	endif

	if nTurnSkins + nExtendedSkin > (iRightmost - iLeftmost + 1) then
		if nTurnSkins > nExtendedSkin then
			nTurnSkins = (iRightmost - iLeftmost + 1) - nExtendedSkin
		else
			nExtendedSkin = nTurnSkins - 1
		endif
	endif

	if not(turnLast) then
		thickCap = 0
		for i = 1 to nTurnSkins
			thickCap = thickCap + WALL_SKINS_PARAMS[iLeftmost-1+i][SKIN_THICKNESS]
		next i
	else
		thickCap = 0
		for i = 1 to nTurnSkins
			thickCap = thickCap + WALL_SKINS_PARAMS[iRightmost+1-i][SKIN_THICKNESS]
		next i
	endif

else
	thickCap = 0
endif

length = thickCap

! --- wall geometry ---
onWallBeg = (abs(WIDO_POSITION) < EPS)

wallIncl = WALL_INCL
maxWallThickness = WALL_START_THICKNESS
if maxWallThickness < WALL_END_THICKNESS then maxWallThickness = WALL_END_THICKNESS

if abs(maxWallThickness - WALL_END_THICKNESS) < EPS then
	if (abs(maxWallThickness - WALL_THICKNESS) < EPS & not(SYMB_MIRRORED)) | \
		(abs(maxWallThickness - WALL_THICKNESS) > EPS & SYMB_MIRRORED) then
		wallIncl = -WALL_INCL
	endif
else
	if (abs(maxWallThickness - WALL_THICKNESS) < EPS & SYMB_MIRRORED) | \
		(abs(maxWallThickness - WALL_THICKNESS) > EPS & not(SYMB_MIRRORED)) then
		wallIncl = -WALL_INCL
	endif
endif

isCurved = 0
r0 = 0
if abs(WIDO_ORIG_DIST) > EPS then
	isCurved = 1
	if WIDO_ORIG_DIST > 0 then
		r0 = abs(WIDO_ORIG_DIST) + yWallStart - WALL_THICKNESS
	else
		r0 = abs(WIDO_ORIG_DIST) - yWallStart
	endif
	alpha = (length / (r0 + WALL_THICKNESS/2)) / PI * 180
endif

! ==============================================================================
! Partial Stucture Display related calculations
! ==============================================================================

! Partial Stucture Display defines:
STRUCTURE_ENTIRE_STRUCTURE	= 0
STRUCTURE_CORE_ONLY			= 1
STRUCTURE_WITHOUT_FINISHES	= 2

