
! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT = -1
_idxTab = 1
TABID_PROF_TYPES    = _idxTab : _idxTab = _idxTab + 1
TABID_CUT_PLANES    = _idxTab : _idxTab = _idxTab + 1
TABID_REPRES        = _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS  = _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Профиля`
ui_current_page gs_ui_current_page

ui_page TABID_PROF_TYPES, TABID_ROOT, `Стиль`, "uiTab_Style_flat(1)"
gosub "ProfTypesPage"

ui_page TABID_CUT_PLANES, TABID_ROOT, `Секущие Плоскости`, "uiTab_CuttingPlanes_flat(1)"
gosub "CutPlanesPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

"ProfTypesPage":
	pageStartY = 5
	rowy = 18
	dy = 20
	_dy = 30
	infHeight = 19
	
	x1 = 15
	cx = 80
	x2 = x1 + cx
	x3 = x2 + 150
	xEnd = 200
	y = pageStartY + 15
	
	ui_outfield `Профили`, x1, y, cx, 15
	y = y + _dy
	
	ui_infield{3} "iProfiles", x1, y, xEnd-2*x1, 50,
					2, 1,									! (1) "ui_sheet_profile_types.png"
					5, 5,
					160, 45, 160, 44,
					1, "", 1,
					2, "", 2,
					3, "", 3,
					4, "", 4,
					5, "", 5	\
					ui_tooltip `Профиль`
	
	y = y + _dy + 50

	dim _stPicIdx[]
	for _i = 1 to vardim1(profileDefaults)
		_stPicIdx[_i] = ""
	next _i

	ui_outfield `Размер`, x1, y, cx, rowy
	ui_infield{4} "size", xEnd-cx-x1, y-4, cx, infHeight,
			8,	"",
			1,	1,
			cx, rowy, cx, rowy,
			_stPicIdx, profileDefaults, profileDefaults,
			"", `Специальный`, `Специальный`

	y = y + _dy

	ui_outfield `Толщина`, x1, y, cx, 15
	ui_infield "thickness", xEnd-cx-x1, y-4, cx, infHeight 	ui_tooltip `Толщина`
	y = y + _dy

	ui_separator xEnd, pageStartY + 9, xEnd, pageStartY + 217


	x0 = xEnd+10	! Picture
	x1 = 255
	xEnd = 445
	cx = 60
	y = pageStartY + 15

	if iProfiles <> 5 then
		ui_infield "width_upper", x1, y, cx, infHeight 	ui_tooltip `Ширина Волны Вверху`
	endif
	y = y + _dy
	
	if iProfiles = 1 or iProfiles = 2 then
		ui_pict 12, x0+75-33, y, 65, 9, 1		! (12) "ui_arrow_length_65.png"
	endif
	if iProfiles = 3 then
		ui_pict 13, x0+75-35, y, 70, 9, 1		! (13) "ui_arrow_length_70.png"
	endif
	if iProfiles = 4 then
		ui_pict 14, x0+75-40, y, 80, 9, 1		! (14) "ui_arrow_length_80.png"
	endif
	y = y + 10


	! ------- Profile Picture
	if iProfiles = 1 then ui_pict 7, x0, y, 150, 58, 1		! (7) "ui_sheet_profile_1.png"
	if iProfiles = 2 then ui_pict 8, x0, y, 150, 58, 1		! (8) "ui_sheet_profile_2.png"
	if iProfiles = 3 then ui_pict 9, x0, y, 150, 58, 1		! (9) "ui_sheet_profile_3.png"
	if iProfiles = 4 then ui_pict 10, x0, y, 150, 58, 1		! (10) "ui_sheet_profile_4.png"
	if iProfiles = 5 then ui_pict 11, x0, y, 150, 58, 1		! (11) "ui_sheet_profile_5.png"
	y = y + 60


	if iProfiles = 1 then
		ui_pict 15, x0+75-45, y, 90, 9, 1		! (15) "ui_arrow_length_90.png"
	endif
	if iProfiles = 2 then
		ui_pict 16, x0+75-13, y, 26, 9, 1		! (16) "ui_arrow_length_26.png"
	endif
	if iProfiles = 3 then
		ui_pict 15, x0+75-45, y, 90, 9, 1		! (15) "ui_arrow_length_90.png"
	endif
	if iProfiles = 4 then
		ui_pict 15, x0+75-45, y, 90, 9, 1		! (15) "ui_arrow_length_90.png"
	endif
	y = y + 20
	
	if iProfiles <> 5 then
		ui_infield "width_bottom", x1, y, cx, infHeight 	ui_tooltip `Ширина Волны Внизу`
	endif
	
	y = y + _dy

	if iProfiles <> 5 then
		ui_pict 17, x0+9, y-50, 20, 61, 1		! (17) "ui_arrow1_left.png"
		ui_pict 18, x0+121, y-50, 20, 61, 1		! (18) "ui_arrow1_right.png"
		ui_pict 19, x0+9+20, y+4, 92, 1, 1		! (19) "ui_arrow_middle.png"
	else
		ui_pict 17, x0+19, y-51, 20, 61, 1		! (17) "ui_arrow1_left.png"
		ui_pict 18, x0+112, y-51, 20, 61, 1		! (18) "ui_arrow1_right.png"
		ui_pict 19, x0+19+20, y+3, 73, 1, 1		! (19) "ui_arrow_middle.png"
	endif
	y = y + 18
	
	ui_infield "waveLength", x1, y, cx, infHeight 	ui_tooltip `Длина Волны`
	
	
	y = pageStartY + 68
	if iProfiles = 1 then
		ui_pict 20, x0+150+1, y, 9, 42, 1			! (20) "ui_arrow_height_42.png"
		y = y + 10
	endif
	if iProfiles = 2 then
		ui_pict 21, x0+150+1, y-11, 9, 53, 1		! (21) "ui_arrow_height_53.png"
		y = y + 10
	endif
	if iProfiles = 3 then
		ui_pict 20, x0+150+1, y, 9, 42, 1			! (20) "ui_arrow_height_42.png"
		y = y + 10
	endif
	if iProfiles = 4 then
		ui_pict 22, x0+150+1, y-1, 9, 45, 1			! (22) "ui_arrow_height_45.png"
		y = y + 10
	endif
	if iProfiles = 5 then
		ui_pict 20, x0+150+1, y, 9, 42, 1			! (20) "ui_arrow_height_42.png"
		y = y + 10
	endif
	
	ui_infield "waveHeight", xEnd-60, y, 60, infHeight 	ui_tooltip `Высота Волны`
return

"CutPlanesPage":
	pageStartY = 5
	rowy = 18
	dy = 20
	x1 = 15
	x2 = x1 + 30
	x3 = x2 + 170
	y = pageStartY + 3

	_flagCuttingTexts = 4 *(abs(rotation) < EPS | abs(rotation-90) < EPS)
	
	ui_style 0,1
	ui_outfield `Плоскости Среза - Вид Сбоку`, x1, y, x3-x2, 15
	ui_style 0, 0
	y = y + dy
	
	ui_outfield `Срез 1 - Вверху`, x1, y, x3-x2, rowy, _flagCuttingTexts
	y = y + dy
	ui_infield{3} "iTopCut", x1, y, x3-x2, rowy,
			8, "", 0, 0, 0, 0, 0, 0,
			"", stCuttingTypes[1], CUT_VERTICAL,
			"", stCuttingTypes[2], CUT_PERPENDICULAR \
			ui_tooltip `Плоскость Среза Вверху`
	y = y + dy + 3
	
	
	ui_infield "length", x1+10, y+15, 90, infHeight		ui_tooltip `Длина`

	if iBottomCut = 1 then
		ui_pict 2, x1-1, y+51, 71, 61, 1		! (2) "ui_sheet_cut_vert_bottom.png"
	else
		ui_pict 3, x1-1, y+51, 71, 61, 1 		! (3) "ui_sheet_cut_perp_bottom.png"
	endif
	
	ui_pict 23, x1+70, y+39, 32, 42, 1 		! (23) "ui_sheet_cut_mid.png"
	
	if iTopCut = 1 then
		ui_pict 4, x1+102, y+4, 71, 61, 1		! (4) "ui_sheet_cut_vert_top.png"
	else
		ui_pict 5, x1+102, y+4, 71, 61, 1		! (5) "ui_sheet_cut_perp_top.png"
	endif
	
	
	ui_infield "rotation", x1+72, y+90, 60, infHeight		ui_tooltip `Наклон`
	y = y + 123
	
	ui_outfield `Срез 2 - Внизу`, x1, y, x3-x2, 15, _flagCuttingTexts
	y = y + dy
	
	ui_infield{3} "iBottomCut", x1, y, x3-x2, infHeight,
			8, "", 0, 0, 0, 0, 0, 0,
			"", stCuttingTypes[1], CUT_VERTICAL,
			"", stCuttingTypes[2], CUT_PERPENDICULAR \
			ui_tooltip `Плоскость Среза Вверху`
	y = y + dy
	
	
	ui_separator x1+185, 0, x1+185, 263
	
	
	x4 = x1 + 200
	x5 = x4 + 10
	x6 = x5 + 115
	x7 = x6 + 40
	y = pageStartY + 3
	
	ui_style 0,1
	ui_outfield `Плоскости Среза - Вид Сверху`, x4, y, x3-x2, 15
	ui_style 0, 0
	y = y + 2*dy
	
	ui_infield "cut_width_1", x5, y, 90, rowy		ui_tooltip `Ширина Среза Угла 1`
	ui_infield "cut_width_2", x6, y, 90, rowy		ui_tooltip `Ширина Среза Угла 2`
	y = y + dy + 12
	
	ui_infield "angCut1", x4, y, 60, rowy		ui_tooltip `Угол Среза Угла 1 Вверху Слева`
	ui_infield "angCut2", x7, y, 60, rowy		ui_tooltip `Угол Среза Угла 2 Вверху Справа`
	y = y + 4.5*dy
	
	ui_infield "angCut3", x4, y, 60, rowy		ui_tooltip `Угол Среза Угла 3 Внизу Слева`
	ui_infield "angCut4", x7, y, 60, rowy		ui_tooltip `Угол Среза Угла 4 Внизу Справа`
	y = y + 2.5*dy
	
	
	ui_infield "cut_width_3", x5, y-5, 90, rowy		ui_tooltip `Ширина Среза Угла 3`
	ui_infield "cut_width_4", x6, y-5, 90, rowy		ui_tooltip `Ширина Среза Угла 4`
	
	
	y = pageStartY + 52
	ui_pict 6, x4 + 62, y + 11, 100, 145, 1		! (6) "ui_sheet_cut_topview.png"
return

"AppearancePage":
	dim st2DParamNames[6][2]
		st2DParamNames[1][1] = "gs_detlevel_2D_m" : st2DParamNames[1][2] = "ui_list_detailLevel2D_flat(1)"
		st2DParamNames[2][1] = "gs_line_type" 	: st2DParamNames[2][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[3][1] = "gs_cont_pen" 	: st2DParamNames[3][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[4][1] = "gs_fill_type" 	: st2DParamNames[4][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[5][1] = "gs_fill_pen" 	: st2DParamNames[5][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[6][1] = "gs_back_pen" 	: st2DParamNames[6][2] = "ui_list_fillBgPen_flat(1)"

	dim st3DParamNames[3][2]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_resol"			: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
		st3DParamNames[3][1] = "gs_shadow"			: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "mat1"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

return

"descriptionPage":

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return



