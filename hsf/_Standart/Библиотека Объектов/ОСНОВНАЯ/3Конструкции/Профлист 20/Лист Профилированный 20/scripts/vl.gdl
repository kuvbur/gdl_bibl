
hideparameter all "A", "B", "Zzyzx" 

call "FM_types" parameters all

values "gs_resol" range[3,)

!=============================================================================
! Profile types and sizes
!=============================================================================

values{2} "iProfiles"	1, stProfileTypes[1],
						2, stProfileTypes[2],
						3, stProfileTypes[3],
						4, stProfileTypes[4],
						5, stProfileTypes[5]

values "size" profileDefaults, `Специальный`

if GLOB_MODPAR_NAME = "iProfiles" then
	if iProfiles = 1 then size = profileDefaults[1]
	if iProfiles = 2 then size = profileDefaults[1]
	if iProfiles = 3 then size = profileDefaults[3]
	if iProfiles = 4 then size = profileDefaults[1]
	if iProfiles = 5 then size = profileDefaults[3]
	parameters size = size
endif

if GLOB_MODPAR_NAME = "size" or GLOB_MODPAR_NAME = "iProfiles" then
	for i = 1 to vardim1(enabledDefaults)
		if enabledDefaults[i] > 0 then
			if size = profileDefaults[enabledDefaults[i]] then
				waveHeight = profileSizes[i][1]
				waveLength = profileSizes[i][2]
				parameters waveHeight = waveHeight, waveLength = waveLength

				i = vardim1(enabledDefaults)		! break
			endif
		endif
	next i
else
	actSize = str(sizeFormat, waveHeight) + strSeparatorCharacter + str(sizeFormat, waveLength)
	bCustom = 1
	for i = 1 to vardim1(profileDefaults)
		if actSize = profileDefaults[i] then bCustom = 0
	next i

	if bCustom then
		size = `Специальный`
	else
		size = actSize
	endif
	parameters size = size
endif

values "thickness" range (0, )


!=============================================================================
! Profile wave upper and bottom defaults, ranges
!=============================================================================

if GLOB_MODPAR_NAME = "iProfiles" or GLOB_MODPAR_NAME = "size" then
	if iProfiles = 1 then
		width_upper = 0.1225
		width_bottom = 0.1875
	endif

	if iProfiles = 2 then
		width_upper = 0.0625
		width_bottom = 0.0285
	endif

	if iProfiles = 3 then
		if size = profileDefaults[1] then
			width_upper = waveLength-0.0270833
			width_bottom = waveLength-0.018396
		else
			if size = profileDefaults[2] then
				width_upper = waveLength-0.0425
				width_bottom = waveLength-0.02545
			else
				if size = profileDefaults[3] then
					width_upper = waveLength-0.04333
					width_bottom = waveLength-0.02943
				else
					if size = profileDefaults[4] then
						width_upper = waveLength-0.0635
						width_bottom = waveLength-0.04445
					else
						if size = profileDefaults[5] then
							width_upper = waveLength-0.06985
							width_bottom = waveLength-0.0381
						else
							width_upper = waveLength-0.04333
							width_bottom = waveLength-0.02943
						endif
					endif
				endif
			endif
		endif
	endif

	if iProfiles = 4 then
		if size = profileDefaults[1] then
			width_upper = waveLength-0.063
			width_bottom = waveLength-0.0428
		else
			width_upper = waveLength-0.04333
			width_bottom = waveLength-0.02943
		endif
	endif

	parameters width_upper = width_upper, width_bottom = width_bottom
endif


!--- Profile 1 ---

if iProfiles = 1 then
	values "width_bottom" range [0.026, waveLength - 0.026]
	values "width_upper" range [0.026, waveLength - 0.026]

	if GLOB_MODPAR_NAME = "width_upper" then
		if width_bottom < width_upper then
			width_bottom = width_upper
		endif
		if waveLength - 0.026 < width_upper then
			waveLength = width_upper + 0.026
		endif
	else
		if GLOB_MODPAR_NAME = "width_bottom" then
			if width_upper > width_bottom then
				width_upper = width_bottom
			endif
			if waveLength - 0.026 < width_bottom then
				waveLength = width_bottom + 0.026
			endif
		else
			if width_bottom > waveLength - 0.026 then
				width_bottom = waveLength - 0.026
			endif
			if width_upper > width_bottom then
				width_upper = width_bottom
			endif
		endif
	endif
	
	parameters 	width_bottom = width_bottom,
				width_upper = width_upper,
				waveLength = waveLength
endif

!--- Profile 2 ---

if iProfiles = 2 then
	values "width_bottom" range (0, width_upper]
	values "width_upper" range [width_bottom, waveLength)
endif

!--- Profile 3, Profile 4 ---

if iProfiles = 3 or iProfiles = 4 then
	values "width_bottom" range [width_upper, waveLength)
	values "width_upper" range (0, width_bottom]
endif

!--- Profile 5 ---

if iProfiles = 5 then
	lock "width_upper", "width_bottom"
	hideparameter "width_upper", "width_bottom"

	if waveLength < waveHeight * 2 then
		waveLength = waveHeight * 2
		parameters waveLength = waveLength
	endif
endif


!=============================================================================
! Detail Levels
!=============================================================================

values{2} "gs_detlevel_3D_m"	DETLEVEL_3D_SIMPLE, stDetlevel3DValues[1],
								DETLEVEL_3D_DETAILED, stDetlevel3DValues[2],
								DETLEVEL_3D_OFF, stDetlevel3DValues[3]

values{2} "gs_detlevel_2D_m"	DETLEVEL_2D_CONTOUR, stDetlevel2DValues[1],
								DETLEVEL_2D_SIMPLE, stDetlevel2DValues[2],
								DETLEVEL_2D_DETAILED, stDetlevel2DValues[3]

!=============================================================================
! Sheet cutting
!=============================================================================

values{2} "iTopCut"		1, stCuttingTypes[1],
						2, stCuttingTypes[2]

values{2} "iBottomCut"	1, stCuttingTypes[1],
						2, stCuttingTypes[2]


!=============================================================================
! Cut parameters
!=============================================================================

values "rotation" range [-90,90]

if abs(rotation)<eps or abs(rotation-90) < eps then
	lock "iTopCut", "iBottomCut"
endif

if abs(angCut1)<eps then lock "cut_width_1"
if abs(angCut2)<eps then lock "cut_width_2"
if abs(angCut3)<eps then lock "cut_width_3"
if abs(angCut4)<eps then lock "cut_width_4"

values "angCut1" range [0, 90)
values "angCut2" range [0, 90)
values "angCut3" range [0, 90)
values "angCut4" range [0, 90)

values "cut_width_1" range [0, width]
values "cut_width_2" range [0, width-cut_width_1*(angCut1>0)]
values "cut_width_3" range [0, width]
values "cut_width_4" range [0, width-cut_width_3*(angCut3>0)]

if iProfiles = 1 then
	values "waveLength" range [0.052, )
	values "waveHeight" range (0, )
else
	if iProfiles = 5 then
		values "waveLength" range [waveHeight * 2, )
		values "waveHeight" range (0, )
	else
		values "waveLength" range (0, )
		values "waveHeight" range (0, )
	endif
endif

values "width" 	range (0, )
values "length" range (0, )
values "A2" 	range (0, )


! --- Sizes ---------------------------------------------------------------------

if abs(rotation-90) < eps or abs(rotation+90) < eps then
	A = waveHeight
	parameters A = A
else
	if GLOB_MODPAR_NAME = "A" then
		length = abs(A/cos(rotation) + waveHeight*abs(tan(rotation)))
		parameters length = length
	else
		A = abs((length-waveHeight*abs(tan(rotation))) * cos(rotation))
		parameters A = A
	endif
endif

if GLOB_MODPAR_NAME = "B" then
	width = B
	parameters width = width
else
	B = width
	parameters B = B
endif

if abs(rotation) < eps then
	parameters ZZYZX = waveHeight
	lock "ZZYZX"
else
	if GLOB_MODPAR_NAME = "ZZYZX" then
		length = abs((ZZYZX - waveHeight*cos(rotation)) / sin(rotation))
		parameters length = length
	else
		ZZYZX = abs(length*sin(rotation) + waveHeight*cos(rotation))
		parameters ZZYZX = ZZYZX
	endif
endif

if abs(rotation-90) < eps or abs(rotation+90) < eps then
	A2 = waveHeight
	parameters A2 = A2
else
	if GLOB_MODPAR_NAME = "A2" then
		length = A2/cos(rotation)
		parameters length = length
	else
		A2 = length * cos(rotation)
		parameters A2 = A2
	endif
endif

