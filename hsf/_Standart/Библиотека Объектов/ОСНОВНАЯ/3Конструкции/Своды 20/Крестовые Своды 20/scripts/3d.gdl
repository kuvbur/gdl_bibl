
shell_angle=180
angle_1=0

if gs_detlevel_3D_m = DLEV3D_OFF then End
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 12

if gs_shadow then
	shadow on
else
	shadow off
endif

if GLOB_CONTEXT<20 or GLOB_CONTEXT>40 then
	resol gs_resol
else
	resol 12
endif

PEN gs_cont_pen

SECT_FILL	gs_sectfill, gs_sectfillbkg, gs_sectfillpn, gs_sectcont

MATERIAL gs_prim_mat


rotz 90
rotx 90

unID=1

!----------------------!
! Shell Length Editing !
!----------------------!

hotspot -radius_1,0,0,unId,a,1+256 : unID=unID+1
hotspot -radius_1,0,a,unId,a,2 : unID=unID+1
hotspot -radius_1,0,-1,unId,a,3 : unID=unID+1

hotspot radius_1,0,0,unId,a,1+256 : unID=unID+1
hotspot radius_1,0,a,unId,a,2 : unID=unID+1
hotspot radius_1,0,-1,unId,a,3 : unID=unID+1

hotspot 0,radius_1,0,unId,a,1+256 : unID=unID+1
hotspot 0,radius_1,a,unId,a,2 : unID=unID+1
hotspot 0,radius_1,-1,unId,a,3 : unID=unID+1

!-------------------------!
! Shell Thickness Editing !
!-------------------------!

hotspot -radius_1,0,0,unID,shell_thk,1+128 : unID=unID+1
hotspot -radius_1+shell_thk,0,0,unID,shell_thk,2 : unID=unID+1
hotspot -radius_1+shell_thk-1,0,0,unID,shell_thk,3 : unID=unID+1

hotspot -radius_1,0,a,unID,shell_thk,1+128 : unID=unID+1
hotspot -radius_1+shell_thk,0,a,unID,shell_thk,2 : unID=unID+1
hotspot -radius_1+shell_thk-1,0,a,unID,shell_thk,3 : unID=unID+1

hotspot radius_1,0,0,unID,shell_thk,1+128 : unID=unID+1
hotspot radius_1-shell_thk,0,0,unID,shell_thk,2 : unID=unID+1
hotspot radius_1-shell_thk+1,0,0,unID,shell_thk,3 : unID=unID+1

hotspot radius_1,0,a,unID,shell_thk,1+128 : unID=unID+1
hotspot radius_1-shell_thk,0,a,unID,shell_thk,2 : unID=unID+1
hotspot radius_1-shell_thk+1,0,a,unID,shell_thk,3 : unID=unID+1


!!---------------------!!
!! Shell Width Editing !!
!!---------------------!!

hotspot 0, 0, 0,unID,radius_1,1+128 : unID=unID+1
hotspot -radius_1, 0, 0,unID,radius_1,2 : unID=unID+1
hotspot radius_1, 0,0, unID,radius_1,3 : unID=unID+1

hotspot 0, 0, a, unID, radius_1,1+128 : unID=unID+1
hotspot -radius_1, 0, a,unID,radius_1,2 : unID=unID+1
hotspot radius_1, 0, a, unID,radius_1,3 : unID=unID+1

hotspot 0, 0, 0, unID,radius_1,1+128 : unID=unID+1
hotspot radius_1, 0, 0, unID,radius_1,2 : unID=unID+1
hotspot -radius_1, 0, 0, unID,radius_1,3 : unID=unID+1

hotspot 0, 0, a, unID,radius_1,1+128 : unID=unID+1
hotspot radius_1, 0, a, unID,radius_1,2 : unID=unID+1
hotspot -radius_1, 0, a, unID,radius_1,3 : unID=unID+1


del 2

!!******************************** Cutting 1

if gs_f_type_m = LOCATION_BOTH_SIDES then
	infin=0
else
	infin=1
endif

rotx 90
addx gs_firstaxis

for i=1 to gs_f_num



	cutpolya 6, 1, infin,
			-gs_radius_2+gs_shell_thk2+0.001, 0, 15,
			-gs_radius_2+gs_shell_thk2, 0, 79,
			0, 0, 979,
			gs_radius_2-gs_shell_thk2,-180, 4079,
			gs_radius_2-gs_shell_thk2-0.001, 0, 15,
			-gs_radius_2+gs_shell_thk2+0.001, 0, 15

	addx gs_ax_length

next i

del gs_f_num

del 2


!!********************************

rotz 90
rotx 90


if shell_thk>0 then

	prism_	13, a,
			-radius_1+shell_thk, 0, 15,
			-radius_1+shell_thk-0.001, 0, 15,
			-radius_1+0.001, 0, 15,
			-radius_1, 0, 79,
			0, 0, 979,
			radius_1,-180, 4079,
			radius_1-0.001, 0, 15,
			radius_1-shell_thk+0.001, 0, 15,
			radius_1-shell_thk, 0, 15,
			radius_1-shell_thk, 0, 79,
			0, 0, 979,
			-radius_1+shell_thk, 180, 4079,
			-radius_1+shell_thk, 0, 15

else

	extrude	7, 0,0,a, 16+32,
			-(radius_1-shell_thk), 0, 1,
			-radius_1, 0, 1,
			0, 0, 901,
			radius_1,-shell_angle, 4001,
			(radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle), 1,
			0, 0, 901,
			-(radius_1-shell_thk), shell_angle, 4001
endif

del 2

!!*********

for i=1 to gs_f_num
	cutend
next i


!!*********************************		Cutting 2

rotz 90
rotx 90

cutpolya	6, 1, 0,
		-radius_1+0.001, 0, 15,
		-radius_1, 0, 79,
		0, 0, 979,
		radius_1,-180, 4079,
		radius_1-0.001, 0, 15,
		-radius_1+0.001, 0, 15

del 2

!!**********************************


rotx 90
addx gs_firstaxis

sid=1
if gs_f_type_m = LOCATION_BOTH_SIDES then sid=2

for k=1 to sid

	for hp=1 to gs_f_num

		!!********* Hotspot editing - length

		hotspot	-gs_radius_2,0,radius_1,unID,gs_f_length,1+128 : unID=unID+1
		hotspot	-gs_radius_2,0,gs_f_length+radius_1,unID,gs_f_length,2 : unID=unID+1
		hotspot	-gs_radius_2,0,0,unID,gs_f_length,3 : unID=unID+1

		hotspot	 gs_radius_2,0,radius_1,unID,gs_f_length,1+128 : unID=unID+1
		hotspot	 gs_radius_2,0,gs_f_length+radius_1,unID,gs_f_length,2 : unID=unID+1
		hotspot	 gs_radius_2,0,0,unID,gs_f_length,3 : unID=unID+1

		!!********* Hotspot editing - thickness

		hotspot	-gs_radius_2,0,gs_f_length+radius_1,unID,gs_shell_thk2,1+128 : unID=unID+1
		hotspot	-gs_radius_2+gs_shell_thk2,0,gs_f_length+radius_1,unID,gs_shell_thk2,2 : unID=unID+1
		hotspot	-gs_radius_2-1,0,gs_f_length+radius_1,unID,gs_shell_thk2,3 : unID=unID+1

		hotspot	 gs_radius_2,0,gs_f_length+radius_1,unID,gs_shell_thk2,1+128 : unID=unID+1
		hotspot	 gs_radius_2-gs_shell_thk2,0,gs_f_length+radius_1,unID,gs_shell_thk2,2 : unID=unID+1
		hotspot	 gs_radius_2+1,0,gs_f_length+radius_1,unID,gs_shell_thk2,3 : unID=unID+1

		!!****** Hotspot editing - width

		hotspot	 0,0,gs_f_length+radius_1,unID,gs_radius_2,1+128 : unID=unID+1
		hotspot	-gs_radius_2,0,gs_f_length+radius_1,unID,gs_radius_2,2 : unID=unID+1
		hotspot	 1,0,gs_f_length+radius_1,unID,gs_radius_2,3 : unID=unID+1

		hotspot	 0,0,gs_f_length+radius_1,unID,gs_radius_2,1+128 : unID=unID+1
		hotspot	 gs_radius_2,0,gs_f_length+radius_1,unID,gs_radius_2,2 : unID=unID+1
		hotspot	 -1,0,gs_f_length+radius_1,unID,gs_radius_2,3 : unID=unID+1


		!!****** Hotspot editing - axes

		if hp < gs_f_num+1 then
			if hp<2 then
				hotspot gs_ax_length, gs_radius_2,gs_f_length+radius_1,unId,gs_ax_length,1+128 : unID=unID+1
				hotspot 0, gs_radius_2,gs_f_length+radius_1,unId,gs_ax_length,2 : unID=unID+1
				hotspot a+1, gs_radius_2,gs_f_length+radius_1,unId,gs_ax_length,3 : unID=unID+1
			else
				hotspot -gs_ax_length, gs_radius_2,gs_f_length+radius_1,unId,gs_ax_length,1+128 : unID=unID+1
				hotspot 0, gs_radius_2,gs_f_length+radius_1,unId,gs_ax_length,2 : unID=unID+1
				hotspot -2*gs_ax_length, gs_radius_2,gs_f_length+radius_1,unId,gs_ax_length,3 : unID=unID+1
			endif
		endif

		addx gs_ax_length

	next hp

	del gs_f_num

	if gs_shell_thk2>0 then

		for i=1 to gs_f_num

			prism_	13, gs_f_length+radius_1,
					-gs_radius_2+gs_shell_thk2, 0, 15,
					-gs_radius_2+gs_shell_thk2-0.001, 0, 15,
					-gs_radius_2+0.001, 0, 15,
					-gs_radius_2, 0, 79,
					0, 0, 979,
					gs_radius_2,-180, 4079,
					gs_radius_2-0.001, 0, 15,
					gs_radius_2-gs_shell_thk2+0.001, 0, 15,
					gs_radius_2-gs_shell_thk2, 0, 15,
					gs_radius_2-gs_shell_thk2, 0, 79,
					0, 0, 979,
					-gs_radius_2+gs_shell_thk2, 180, 4079,
					-gs_radius_2+gs_shell_thk2, 0, 15

			addx gs_ax_length

		next i

		del gs_f_num

	else

		for j=1 to gs_f_num

			extrude	3, 0,0,gs_f_length+radius_1, 16+32,
					-gs_radius_2, 0, 1,
					0, 0, 901,
					gs_radius_2, -180, 4000


			addx gs_ax_length

		next j

		del gs_f_num

	endif

	mulz -1

next k

del sid

del 2

!!***********

cutend

!!***********


