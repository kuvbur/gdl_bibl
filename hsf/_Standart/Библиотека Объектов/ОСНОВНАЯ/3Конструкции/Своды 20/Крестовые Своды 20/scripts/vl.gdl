
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all


values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_SIMPLE, `Простой`,
								DLEV3D_OFF, `Откл.`

if GLOB_MODPAR_NAME = "gs_f_type" then
	gs_f_type_m = LOCATION_ONE_SIDE
	if gs_f_type = stLocation[2] then gs_f_type_m = LOCATION_BOTH_SIDES
	parameters gs_f_type_m = gs_f_type_m
else
	gs_f_type = stLocation[1]		! One Sides
	if gs_f_type_m = LOCATION_BOTH_SIDES then gs_f_type = stLocation[2]
	parameters gs_f_type = gs_f_type
endif

values "gs_f_type" stLocation
values "gs_f_type_m" LOCATION_ONE_SIDE, LOCATION_BOTH_SIDES

if gs_cont_pen = 0 then parameters gs_cont_pen = 89
if gs_fill_pen = 0 then parameters gs_fill_pen = 19
if gs_axis_pen = 0 then parameters gs_axis_pen = 69
if gs_sectcont = 0 then parameters sectcont = 29
if gs_sectfillpn = 0 then parameters gs_sectfillpn = 109

If gs_resol < 3 then parameters gs_resol = 3
if gs_detlevel_3D_m = DLEV3D_SIMPLE then lock "gs_resol"

parameters B = 2 * radius_1
parameters ZZYZX = radius_1
lock "ZZYZX", "B"

if gs_axis = 0 then hideparameter "gs_axis_line", "gs_axis_pen"
if gs_f_simm = 1 then hideparameter "gs_firstaxis"


values "radius_1" range (0, ]
values "shell_thk" range [0, radius_1]
values "gs_f_length" range [0, ]
values "gs_shell_thk2" range [0, gs_radius_2]
values "A" range [gs_firstaxis * not(gs_f_simm) + gs_radius_2 + gs_radius_2 * gs_f_simm, ]

if gs_f_num = 1 then
	values "gs_radius_2" range (0, min(A / 2, radius_1)]
else
	values "gs_radius_2" range (0, min(gs_ax_length / 2, radius_1)]
endif
parameters gs_b2 = gs_radius_2 * 2
lock "gs_b2"

nMaxIV = int((A - gs_firstaxis * not(gs_f_simm) - gs_radius_2 - gs_radius_2 * gs_f_simm) / gs_ax_length) + 1
values "gs_f_num" range [1, nMaxIV]

if GLOB_MODPAR_NAME = "gs_f_num" | GLOB_MODPAR_NAME = "A" then

	if gs_f_num > nMaxIV then
		parameters gs_f_num = nMaxIV
	endif

endif

if GLOB_MODPAR_NAME <> "A" & GLOB_MODPAR_NAME <> "gs_f_num" & not(gs_f_simm) then

	values "gs_firstaxis" range [gs_radius_2, A - gs_ax_length * (gs_f_num - 1) - gs_radius_2]

endif

if (GLOB_MODPAR_NAME = "gs_f_simm" | GLOB_MODPAR_NAME = "gs_ax_length" | \
	GLOB_MODPAR_NAME = "gs_f_num" | GLOB_MODPAR_NAME = "A") & gs_f_simm then

		parameters gs_firstaxis = (A - gs_ax_length * (gs_f_num - 1)) / 2

endif

if gs_f_num = 1 then
	parameters gs_ax_length = gs_radius_2 * 2
	lock "gs_ax_length"
else
	if GLOB_MODPAR_NAME <> "A" & GLOB_MODPAR_NAME <> "gs_f_num" then
		values "gs_ax_length" range [gs_radius_2 * 2, (A - gs_firstaxis * not(gs_f_simm) - gs_radius_2 - gs_radius_2 * gs_f_simm) / (gs_f_num - 1)]
	endif
endif

