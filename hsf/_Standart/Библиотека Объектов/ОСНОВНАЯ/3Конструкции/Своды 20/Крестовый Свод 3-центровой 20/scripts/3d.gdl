

!! Shell

if gs_detlevel_3D_m = DLEV3D_OFF then End
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 18

if gs_shadow then
	shadow on
else
	shadow off
endif

if GLOB_CONTEXT<20 or GLOB_CONTEXT>40 then
	resol gs_resol
else
	resol 12
endif

PEN gs_cont_pen

SECT_FILL	gs_sectfill, gs_sectfillbkg, gs_sectfillpn, gs_sectcont

MATERIAL gs_prim_mat


rotz 90
rotx 90

unID=1

!----------------------!
! Shell Length Editing !
!----------------------!

hotspot -b/2,0,0,unId,a,1+256 : unID=unID+1
hotspot -b/2,0,a,unId,a,2 : unID=unID+1
hotspot -b/2,0,-1,unId,a,3 : unID=unID+1

hotspot b/2,0,0,unId,a,1+256 : unID=unID+1
hotspot b/2,0,a,unId,a,2 : unID=unID+1
hotspot b/2,0,-1,unId,a,3 : unID=unID+1

hotspot 0,gs_h2,0,unId,a,1+256 : unID=unID+1
hotspot 0,gs_h2,a,unId,a,2 : unID=unID+1
hotspot 0,gs_h2,-1,unId,a,3 : unID=unID+1

!-------------------------!
! Shell Thickness Editing !
!-------------------------!

hotspot -b/2,0,0,unID,shell_thk,1+128 : unID=unID+1
hotspot -b/2+shell_thk,0,0,unID,shell_thk,2 : unID=unID+1
hotspot -b/2+shell_thk-1,0,0,unID,shell_thk,3 : unID=unID+1

hotspot -b/2,0,a,unID,shell_thk,1+128 : unID=unID+1
hotspot -b/2+shell_thk,0,a,unID,shell_thk,2 : unID=unID+1
hotspot -b/2+shell_thk-1,0,a,unID,shell_thk,3 : unID=unID+1

hotspot b/2,0,0,unID,shell_thk,1+128 : unID=unID+1
hotspot b/2-shell_thk,0,0,unID,shell_thk,2 : unID=unID+1
hotspot b/2-shell_thk+1,0,0,unID,shell_thk,3 : unID=unID+1

hotspot b/2,0,a,unID,shell_thk,1+128 : unID=unID+1
hotspot b/2-shell_thk,0,a,unID,shell_thk,2 : unID=unID+1
hotspot b/2-shell_thk+1,0,a,unID,shell_thk,3 : unID=unID+1


!!---------------------!!
!! Shell Width Editing !!
!!---------------------!!

hotspot	 0,0,0,unID,half_b,1+128 : unID=unID+1
hotspot	-b/2,0,0,unID,half_b,3 : unID=unID+1
hotspot	 b/2,0,0,unID,half_b,2 : unID=unID+1

hotspot  0,0,0,unID,half_b,1+128 : unID=unID+1
hotspot  b/2,0,0,unID,half_b,3 : unID=unID+1
hotspot -b/2,0,0,unID,half_b,2 : unID=unID+1

hotspot	 0,0,a,unID,half_b,1+128 : unID=unID+1
hotspot	-b/2,0,a,unID,half_b,3 : unID=unID+1
hotspot	 b/2,0,a,unID,half_b,2 : unID=unID+1

hotspot  0,0,a,unID,half_b,1+128 : unID=unID+1
hotspot  b/2,0,a,unID,half_b,3 : unID=unID+1
hotspot -b/2,0,a,unID,half_b,2 : unID=unID+1

!!!*****************



del 2

!!!******************************** Cutting 1

if gs_f_type_m = LOCATION_BOTH_SIDES then
	infin=0
else
	infin=1
endif

rotx 90
addx gs_firstaxis

for i=1 to gs_f_num

	cutpolya 8, 1, infin,
		   -gs_b2/2+gs_shell_thk2, 0, 79,
			0,1,879,
		   -(rr1_2-gs_shell_thk2)*sin(alpha2),(rr2_2-gs_shell_thk2)*cos(alpha2),1079,
			0,gs_h2-gs_shell_thk2,1079,
			(rr1_2-gs_shell_thk2)*sin(alpha2),(rr2_2-gs_shell_thk2)*cos(alpha2),1079,
			gs_b2/2-gs_shell_thk2, 0, 1015,
			gs_b2/2-gs_shell_thk2, 0, 15,
		   -gs_b2/2+gs_shell_thk2, 0, 15

	addx gs_ax_length

next i

del gs_f_num

del 2


!!********************************

rotz 90
rotx 90


if shell_thk>0 then

	prism_ 18, a,
		b/2-shell_thk, 0, 15,
		b/2-shell_thk+0.0001, 0, 15,
		b/2-0.0001, 0, 15,
		b/2, 0, 79,
		0,1,879,
		rr1*sin(alpha),rr2*cos(alpha),1079,
		0,gs_h1,1079,
	   -rr1*sin(alpha),rr2*cos(alpha),1079,
	   -b/2, 0, 1015,
	   -b/2+0.0001, 0, 15,
	   -b/2+shell_thk-0.0001, 0, 15,
	   -b/2+shell_thk, 0, 79,
		0,1,879,
	   -(rr1-shell_thk)*sin(alpha),(rr2-shell_thk)*cos(alpha),1079,
		0,gs_h1-shell_thk,1079,
		(rr1-shell_thk)*sin(alpha),(rr2-shell_thk)*cos(alpha),1079,
		b/2-shell_thk, 0, 1015,
		b/2-shell_thk, 0, 15

else

	extrude	6, 0,0,a, 16+32,
		b/2, 0, 1,
		0,1,801,
		rr1*sin(alpha),rr2*cos(alpha),1001,
		0,gs_h1,1001,
	   -rr1*sin(alpha),rr2*cos(alpha),1001,
	   -b/2, 0, 1000

endif

del 2

!!!*********

for i=1 to gs_f_num
	cutend
next i


!!!*********************************		Cutting 2

rotz 90
rotx 90

cutpolya	7, 1, 0,
		b/2, 0, 79,
		0,1,879,
		rr1*sin(alpha),rr2*cos(alpha),1079,
		0,gs_h1,1079,
	   -rr1*sin(alpha),rr2*cos(alpha),1079,
	   -b/2, 0, 1079,
		b/2, 0, 15

del 2

!!!**********************************


rotx 90
addx gs_firstaxis

sid=1
if gs_f_type_m = LOCATION_BOTH_SIDES then sid=2

for k=1 to sid

	for hp=1 to gs_f_num

		!!********* Hotspot editing - length

		hotspot	-gs_b2/2,0,b/2,unID,gs_f_length,1+128 : unID=unID+1
		hotspot	-gs_b2/2,0,gs_f_length+b/2,unID,gs_f_length,2 : unID=unID+1
		hotspot	-gs_b2/2,0,0,unID,gs_f_length,3 : unID=unID+1

		hotspot	 gs_b2/2,0,b/2,unID,gs_f_length,1+128 : unID=unID+1
		hotspot	 gs_b2/2,0,gs_f_length+b/2,unID,gs_f_length,2 : unID=unID+1
		hotspot	 gs_b2/2,0,0,unID,gs_f_length,3 : unID=unID+1

		!!********* Hotspot editing - thickness

		hotspot	-gs_b2/2,0,gs_f_length+b/2,unID,gs_shell_thk2,1+128 : unID=unID+1
		hotspot	-gs_b2/2+gs_shell_thk2,0,gs_f_length+b/2,unID,gs_shell_thk2,2 : unID=unID+1
		hotspot	-gs_b2/2-1,0,gs_f_length+b/2,unID,gs_shell_thk2,3 : unID=unID+1

		hotspot	 gs_b2/2,0,gs_f_length+b/2,unID,gs_shell_thk2,1+128 : unID=unID+1
		hotspot	 gs_b2/2-gs_shell_thk2,0,gs_f_length+b/2,unID,gs_shell_thk2,2 : unID=unID+1
		hotspot	 gs_b2/2+1,0,gs_f_length+b/2,unID,gs_shell_thk2,3 : unID=unID+1

		!!****** Hotspot editing - width

		hotspot	 0,0,gs_f_length+b/2,unID,half_b2,1+128 : unID=unID+1
		hotspot	-gs_b2/2,0,gs_f_length+b/2,unID,half_b2,2 : unID=unID+1
		hotspot	 1,0,gs_f_length+b/2,unID,half_b2,3 : unID=unID+1

		hotspot	 0,0,gs_f_length+b/2,unID,half_b2,1+128 : unID=unID+1
		hotspot	 gs_b2/2,0,gs_f_length+b/2,unID,half_b2,2 : unID=unID+1
		hotspot	 -1,0,gs_f_length+b/2,unID,half_b2,3 : unID=unID+1

		!!****** Hotspot editing - axes

		if hp < gs_f_num+1 then
			if hp<2 then
				hotspot gs_ax_length, gs_h2,gs_f_length+b/2,unId,gs_ax_length,1+128 : unID=unID+1
				hotspot 0, gs_h2,gs_f_length+b/2,unId,gs_ax_length,2 : unID=unID+1
				hotspot a, gs_h2,gs_f_length+b/2,unId,gs_ax_length,3 : unID=unID+1
			else
				hotspot -gs_ax_length, gs_h2,gs_f_length+b/2,unId,gs_ax_length,1+128 : unID=unID+1
				hotspot 0, gs_h2,gs_f_length+b/2,unId,gs_ax_length,2 : unID=unID+1
				hotspot -2*gs_ax_length, gs_h2,gs_f_length+b/2,unId,gs_ax_length,3 : unID=unID+1
			endif
		endif

		addx gs_ax_length

	next hp

	del gs_f_num

	if gs_shell_thk2>0 then

		for i=1 to gs_f_num

			prism_ 18, gs_f_length+b/2,
				gs_b2/2-gs_shell_thk2, 0, 15,
				gs_b2/2-gs_shell_thk2+0.0001, 0, 15,
				gs_b2/2-0.0001, 0, 15,
				gs_b2/2, 0, 79,
				0,1,879,
				rr1_2*sin(alpha2),rr2_2*cos(alpha2),1079,
				0,gs_h2,1079,
			   -rr1_2*sin(alpha2),rr2_2*cos(alpha2),1079,
			   -gs_b2/2, 0, 1015,
			   -gs_b2/2+0.0001, 0, 15,
			   -gs_b2/2+gs_shell_thk2-0.0001, 0, 15,
			   -gs_b2/2+gs_shell_thk2, 0, 79,
				0,1,879,
			   -(rr1_2-gs_shell_thk2)*sin(alpha2),(rr2_2-gs_shell_thk2)*cos(alpha2),1079,
				0,gs_h2-gs_shell_thk2,1079,
				(rr1_2-gs_shell_thk2)*sin(alpha2),(rr2_2-gs_shell_thk2)*cos(alpha2),1079,
				gs_b2/2-gs_shell_thk2, 0, 1015,
				gs_b2/2-gs_shell_thk2, 0, 15

			addx gs_ax_length

		next i

		del gs_f_num

	else

		for j=1 to gs_f_num

			extrude	6, 0,0,gs_f_length+radius_1, 16+32,
				gs_b2/2, 0, 1,
				0,1,801,
				rr1_2*sin(alpha2),rr2_2*cos(alpha2),1001,
				0,gs_h2,1001,
			   -rr1_2*sin(alpha2),rr2_2*cos(alpha2),1001,
			   -gs_b2/2, 0, 1000


			addx gs_ax_length

		next j

		del gs_f_num

	endif

	mulz -1

next k

del sid

del 2

!!!***********

cutend

!!!***********


