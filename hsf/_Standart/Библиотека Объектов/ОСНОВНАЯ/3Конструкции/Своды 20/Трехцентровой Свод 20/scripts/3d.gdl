
!! Shell

if gs_detlevel_3D_m = DLEV3D_OFF then End
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 12

if gs_shadow then
	shadow on
else
	shadow off
endif

if GLOB_CONTEXT<20 or GLOB_CONTEXT>40 then
	resol gs_resol
else
	resol 12
endif

PEN gs_cont_pen

SECT_FILL	gs_sectfill, gs_sectfillbkg, gs_sectfillpn, gs_sectcont

MATERIAL gs_prim_mat


if gs_wallcut=1 then

	addy -b/2+shell_thk
	rotx 90
	cutplane
	del 2

	addy b/2-shell_thk
	rotx -90
	cutplane
	del 2


endif

rotz 90
rotx 90

unID=1

!----------------------!
! Shell Length Editing !
!----------------------!

hotspot	-b/2,0,0,unID,a,1+256 : unID=unID+1
hotspot	-b/2,0,-1,unID,a,3 : unID=unID+1
hotspot	-b/2,0,a,unID,a,2 : unID=unID+1

hotspot b/2,0,0,unID,a,1+256 : unID=unID+1
hotspot b/2,0,-1,unID,a,3 : unID=unID+1
hotspot b/2,0,a,unID,a,2 : unID=unID+1

!-------------------------!
! Shell Thickness Editing !
!-------------------------!

hotspot	-b/2,0,a,unID,shell_thk,1+128 : unID=unID+1
hotspot	-b/2-1,0,a,unID,shell_thk,3 : unID=unID+1
hotspot	-b/2+shell_thk,0,a,unID,shell_thk,2 : unID=unID+1

hotspot	b/2,0,a,unID,shell_thk,1+128 : unID=unID+1
hotspot	b/2+1,0,a,unID,shell_thk,3 : unID=unID+1
hotspot	b/2-shell_thk,0,a,unID,shell_thk,2 : unID=unID+1

hotspot	-b/2,0,0,unID,shell_thk,1+128 : unID=unID+1
hotspot	-b/2-1,0,0,unID,shell_thk,3 : unID=unID+1
hotspot	-b/2+shell_thk,0,0,unID,shell_thk,2 : unID=unID+1

hotspot	b/2,0,0,unID,shell_thk,1+128 : unID=unID+1
hotspot	b/2+1,0,0,unID,shell_thk,3 : unID=unID+1
hotspot	b/2-shell_thk,0,0,unID,shell_thk,2 : unID=unID+1

!----------------------!
! Shell Width Editing !
!----------------------!

hotspot	 0,0,0,unID,half_b,1+128 : unID=unID+1
hotspot	-b/2,0,0,unID,half_b,3 : unID=unID+1
hotspot	 b/2,0,0,unID,half_b,2 : unID=unID+1

hotspot  0,0,0,unID,half_b,1+128 : unID=unID+1
hotspot  b/2,0,0,unID,half_b,3 : unID=unID+1
hotspot -b/2,0,0,unID,half_b,2 : unID=unID+1

hotspot	 0,0,a,unID,half_b,1+128 : unID=unID+1
hotspot	-b/2,0,a,unID,half_b,3 : unID=unID+1
hotspot	 b/2,0,a,unID,half_b,2 : unID=unID+1

hotspot  0,0,a,unID,half_b,1+128 : unID=unID+1
hotspot  b/2,0,a,unID,half_b,3 : unID=unID+1
hotspot -b/2,0,a,unID,half_b,2 : unID=unID+1

!!!*****************


if shell_thk>0 then

	prism_ 18, a,
		b/2-shell_thk, 0, 15,
		b/2-shell_thk+0.0001, 0, 15,
		b/2-0.0001, 0, 15,
		b/2, 0, 79,
		0,1,879,
		rr1*sin(alpha),rr2*cos(alpha),1079,
		0,gs_h1,1079,
	   -rr1*sin(alpha),rr2*cos(alpha),1079,
	   -b/2, 0, 1015,
	   -b/2+0.0001, 0, 15,
	   -b/2+shell_thk-0.0001, 0, 15,
	   -b/2+shell_thk, 0, 79,
		0,1,879,
	   -(rr1-shell_thk)*sin(alpha),(rr2-shell_thk)*cos(alpha),1079,
		0,gs_h1-shell_thk,1079,
		(rr1-shell_thk)*sin(alpha),(rr2-shell_thk)*cos(alpha),1079,
		b/2-shell_thk, 0, 1015,
		b/2-shell_thk, 0, 15

else

	extrude	6, 0,0,a, 16+32,
		b/2, 0, 1,
		0,1,801,
		rr1*sin(alpha),rr2*cos(alpha),1001,
		0,gs_h1,1001,
	   -rr1*sin(alpha),rr2*cos(alpha),1001,
	   -b/2, 0, 1000

endif

del 2

if gs_wallcut=1 then

	cutend
	cutend

endif


