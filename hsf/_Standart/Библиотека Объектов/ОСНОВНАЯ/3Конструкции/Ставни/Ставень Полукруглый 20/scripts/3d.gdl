
!! Shutter Half Round.gsm


IF gs_detlevel_3D_m = DLEV3D_OFF THEN END

if gs_shadow=1 then SHADOW ON
if gs_shadow=0 then SHADOW OFF

PEN gs_cont_pen
RESOL gs_resol


count=0

10:

if iShutterType = SHUTTERTYPE_DOUBLE then addx -gs_ww/2-A


MATERIAL mat1

ADD A/2,fd/2,0

IF gs_detlevel_3D_m = DLEV3D_SIMPLE THEN
	IF zzyzx<a-2*fw THEN zzyzx=a-2*fw
	h=zzyzx-(a-fw)

	ROTX 90
	ADDZ -fd/2

	PRISM_ 7,  fd,
		-a/2,0,15,
		 a/2,0,15,
		 a/2,h,79,
		 0,1,800,
		-a/2+fw,zzyzx,1079,
		-a/2,zzyzx,15,
		-a/2,0,-1

	DEL 2

	if iShutterType = SHUTTERTYPE_DOUBLE and count = 0 then
		del 1
		count=1
		mulx -1
		addx -gs_ww
		goto 10
	else
		END
	endif

END
ENDIF


ROTX 90
ADDZ -fd/2

!Parameter definition, restriction

IF zzyzx<a-2*fw THEN zzyzx=a-2*fw
h=zzyzx-(a-fw)



!! FRAME !!

MATERIAL mat1

if iShutterStyle = SHUTTERSTYLE_1 or iShutterStyle = SHUTTERSTYLE_3 then

	PRISM_ 13,  fd,
		-a/2,0,15,
		 a/2,0,15,
		 a/2,h,79,
		 0,1,800,
		-a/2+fw,zzyzx,1079,
		-a/2,zzyzx,15,
		-a/2,0,-1,

		-a/2+fw,fw,15,
		 a/2-fw,fw,15,
		 a/2-fw,h,79,
		 0,1,800,
		-a/2+fw,zzyzx-fw,1015,
		-a/2+fw,fw,-1

else

	PRISM_ 18, fd,
		-a/2,0,15,
		 a/2,0,15,
		 a/2,h,79,
		 0,1,800,
		-a/2+fw,zzyzx,1079,
		-a/2,zzyzx,15,
		-a/2,0,-1,

		-a/2+fw,fw,15,
		 a/2-fw,fw,15,
		 a/2-fw,h-fw,15,
		-a/2+fw,h-fw,15,
		-a/2+fw,fw,-1,

		-a/2+fw,h,15,
		 a/2-fw-0.00001,h,15,
		 a/2-fw,h,79,
		 0,1,800,
		-a/2+fw,zzyzx-fw,1015,
		-a/2+fw,h,-1

endif



!! LOUVERS OR PANEL!!

MATERIAL mat2

IF iShutterStyle = SHUTTERSTYLE_3 THEN

	ADDZ (fd-shth)/2

	PRISM_ 6, shth,
		-a/2+fw,fw,15,
		 a/2-fw,fw,15,
		 a/2-fw,h,79,
		 0,1,800,
		-a/2+fw,zzyzx-fw,1015,
		-a/2+fw,fw,-1

	del 1

ELSE

	IF iShutterStyle = SHUTTERSTYLE_1 THEN

		!*** Cutplanes at curve

		alpha=360/gs_resol
		dev=(a-2*fw)-(a-2*fw)*cos(alpha/2)

		ADD -a/2+fw,h,0
		ROTZ alpha/2

		FOR i=1 TO gs_resol/4 STEP 1

			ADDX a-2*fw-dev
			CUTPLANE 0,1,1,0
			DEL 1

			ROTZ alpha

		NEXT i

		DEL gs_resol/4+2

		!*** Louvers

		d=zzyzx-(2*fw)
		n=int(d/shw)
		dp=d/n

		ADDY fw-dp/2
		ADDZ fd/2

		FOR i=1 TO n STEP 1

			ADDY dp

			ROTX shang
			ADDZ -shth/2

			PRISM_ 5, shth,
				-a/2+fw,  -dp/2,     15,
				-a/2+fw,   dp/2,     15,
				 a/2-fw,   dp/2,     15,
				 a/2-fw,  -dp/2,     15,
				-a/2+fw,  -dp/2,     -1

			DEL 2

		NEXT i

		DEL n+2

		FOR i=1 TO gs_resol/4 STEP 1
			CUTEND
		NEXT i

	else

		IF iShutterStyle = SHUTTERSTYLE_2 THEN

			!**** Cutplanes at curve

			alpha=360/gs_resol
			dev=(a-2*fw)-(a-2*fw)*cos(alpha/2)

			ADD -a/2+fw,h,0
			ROTZ alpha/2
			FOR i=1 TO gs_resol/4 STEP 1
			ADDX a-2*fw-dev
			CUTPLANE 0,1,1,0
			DEL 1
			ROTZ alpha
			NEXT i
			DEL gs_resol/4+2

			!Louvers

			!1st part

			d=zzyzx-h-fw
			n=int(d/shw)
			dp=d/n

			ADDY h-dp/2
			ADDZ fd/2

			FOR i=1 TO n STEP 1
				ADDY dp

				ROTX shang
				ADDZ -shth/2

				PRISM_          5,       shth,
					-a/2+fw,     -dp/2,     15,
					-a/2+fw,    dp/2,     15,
					 a/2-fw,       dp/2,     15,
					 a/2-fw,        -dp/2,     15,
					-a/2+fw,     -dp/2,     -1

				DEL 2
			NEXT i

			DEL n+2


			!2nd part

			d=h-(2*fw)
			n=int(d/shw)
			dp=d/n

			ADDY fw-dp/2
			ADDZ fd/2

			FOR i=1 TO n STEP 1
				ADDY dp

				ROTX shang
				ADDZ -shth/2
				PRISM_          5,       shth,
					-a/2+fw,     -dp/2,     15,
					-a/2+fw,    dp/2,     15,
					 a/2-fw,       dp/2,     15,
					 a/2-fw,        -dp/2,     15,
					-a/2+fw,     -dp/2,     -1
				DEL 2
			NEXT i

			DEL n+2

			FOR i=1 TO gs_resol/4 STEP 1
				CUTEND
			NEXT i

		else

			ADDZ (fd-shth)/2

			PRISM_ 5, shth,
				-a/2+fw,fw,15,
				 a/2-fw,fw,15,
				 a/2-fw,h-fw,15,
				-a/2+fw,h-fw,15,
				-a/2+fw,fw,-1

			PRISM_ 6, shth,
				-a/2+fw,h,15,
				 a/2-fw-0.00001,h,15,
				 a/2-fw,h,79,
				 0,1,800,
				-a/2+fw,zzyzx-fw,1015,
				-a/2+fw,h,-1

			del 1

		endif

	endif

ENDIF


del 3

if iShutterType = SHUTTERTYPE_DOUBLE and count = 0 then
	del 1
	count=1
	mulx -1
	goto 10
else
	END
endif







