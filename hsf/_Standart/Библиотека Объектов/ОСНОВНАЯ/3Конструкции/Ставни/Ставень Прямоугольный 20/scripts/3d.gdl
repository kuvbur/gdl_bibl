
!! Shutter Rectangular.gsm


IF gs_detlevel_3D_m = DLEV3D_OFF THEN END

if gs_shadow=1 then SHADOW ON
if gs_shadow=0 then SHADOW OFF

count=0

10:

if iShutterType = SHUTTERTYPE_DOUBLE then addx -gs_ww/2-A


PEN gs_cont_pen

MATERIAL mat1


!**************************** SIMPLE ***********************
!***********************************************************

IF gs_detlevel_3D_m = DLEV3D_SIMPLE THEN
	for i = 1 to 1 + (iShutterType = SHUTTERTYPE_DOUBLE)
		BLOCK a, fd, zzyzx
		addx a + gs_ww
	next i
	END

ENDIF


ADD a/2,fd/2,0

IF iShutterStyle = SHUTTERSTYLE_4 then
	if 	count=1 then ADDx -2 * a
	gosub "style4_model"
	if 	count=1 then del 1
	goto 80
endif


!**************************** FRAMES ***********************
!***********************************************************


!******************** all styles except style 4

ROTX 90
ADDZ -fd/2

hx=(a-2*fw-vbn*vbw)/(vbn+1)
hy=(zzyzx-2*fw-hbn*hbw)/(hbn+1)

ADD -a/2+fw,fw,0

FOR s=1 TO hbn+1

	FOR p=1 TO vbn+1

		CUTPOLY 4,
			   0,0,
			   hx,0,
			   hx,hy,
			   0,hy

		ADDX (a-2*fw+vbw)/(vbn+1)

	NEXT p

	DEL vbn+1

	ADDY (zzyzx-2*fw+hbw)/(hbn+1)

NEXT s

DEL hbn+1

DEL 1

PRISM_  5,  fd,
	   -a/2,      0,      15,
	   -a/2,  zzyzx,      15,
		a/2,  zzyzx,      15,
		a/2,      0,      15,
	   -a/2,      0,      -1

FOR v=1 to (vbn+1)*(hbn+1)
	CUTEND
NEXT v



!********************* LOUVERS / PANELS ********************
!***********************************************************


MATERIAL mat2

IF iShutterStyle = SHUTTERSTYLE_1 or iShutterStyle = SHUTTERSTYLE_2 or iShutterStyle = SHUTTERSTYLE_3 THEN

	!******************** styles 1, 2, 3

	FOR r=1 TO vbn+1

		FOR t=1 TO hbn+1

			n=INT(hy/shw)
			dp=hy/n

			ADDY fw-dp/2
			ADDZ fd/2

			FOR i=1 TO n STEP 1
				ADDY dp

				ROTX shang
				ADDZ -shth/2
				PRISM_          5,       shth,
					-a/2+fw,     -dp/2,     15,
					-a/2+fw,    dp/2,     15,
					-a/2+fw+hx,       dp/2,     15,
					-a/2+fw+hx,        -dp/2,     15,
					-a/2+fw,     -dp/2,     -1

				DEL 2
			NEXT i

			DEL n+2

			ADDY (zzyzx-2*fw+hbw)/(hbn+1)

		NEXT t

		DEL hbn+1
		ADDX (a-2*fw+vbw)/(vbn+1)

	NEXT r

	DEL vbn+1

	DEL 2  !*** frame transf.

ENDIF


IF iShutterStyle = SHUTTERSTYLE_5 or iShutterStyle = SHUTTERSTYLE_6 or iShutterStyle = SHUTTERSTYLE_7 THEN

	!******************** styles 5, 6, 7

	ADDZ fd/2-shth/2
	ADD -a/2+fw,fw,0

	FOR r=1 TO vbn+1
		FOR t=1 TO hbn+1

			PRISM_ 4, shth,
				   0,0,15,
				   hx,0,15,
				   hx,hy,15,
				   0,hy,15

			ADDY (zzyzx-2*fw+hbw)/(hbn+1)
		NEXT t

		DEL hbn+1

		ADDX (a-2*fw+vbw)/(vbn+1)
	NEXT r

	DEL vbn+1

	DEL 2

	DEL 2  !*** frame transf.

ENDIF

del 1

80:

if iShutterType = SHUTTERTYPE_DOUBLE and count = 0 then
	del 1
	count=1
	mulx -1
	goto 10
else
	END
endif

END

"style4_model":

muly -1
ROTX 90
ADDZ -fd/2

ADDX -a/2

n=INT(a/bw)
dbb=(a-n*bw)/(n-1)       !!! Distance between boards

IF dbb<0.01 THEN
	bw=bw-0.01
	dbb=(a-n*bw)/(n-1)
ENDIF

FOR i=1 TO n

PRISM_ 4,bd,
	0,     0, 15,
	0, zzyzx, 15,
   bw, zzyzx, 15,
   bw,     0, 15

ADDX bw+dbb

NEXT i

DEL n
DEL 1


MATERIAL mat1
ADDZ bd

mx=zzyzx-2*fw-0.1
x=(2*(a-0.5*bw)-SQR(4*(a-0.5*bw)^2-4*(1-(mx^2/fw^2))*((a-0.5*bw)^2+mx^2)))/(2*((1-(mx^2/fw^2))))

PRISM_       4,     bd,
		-a/2+bw*0.25,0.05,15,
		-a/2+bw*0.25,fw+0.05,15,
		a/2-bw*0.25,fw+0.05,15,
		a/2-bw*0.25,0.05,15

PRISM_       4,     bd,
		-a/2+bw*0.25,zzyzx-fw-0.05,15,
		-a/2+bw*0.25,zzyzx-0.05,15,
		a/2-bw*0.25,zzyzx-0.05,15,
		a/2-bw*0.25,zzyzx-fw-0.05,15

PRISM_       4,     bd,
	   -a/2+bw*0.25,fw+0.05,15,
	   a/2-bw*0.25-x,zzyzx-fw-0.05,15,
	   a/2-bw*0.25,zzyzx-fw-0.05,15,
	   -a/2+bw*0.25+x,fw+0.05,15

DEL 1

del 3

return





