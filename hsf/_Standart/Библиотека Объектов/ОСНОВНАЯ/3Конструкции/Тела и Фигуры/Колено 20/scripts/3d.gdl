
unID = 1

if gs_detlevel_3D_m = DLEV3D_OFF and not(gs_2D_projection) then End
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 12

if gs_shadow then
	shadow on
else
	shadow off
endif

if GLOB_CONTEXT<20 or GLOB_CONTEXT>40 then
	resol gs_resol
else
	resol 12
endif

pen gs_cont_pen

!---------------------------------------!
! Rotation around the Y coordinate axis !
!---------------------------------------!

hotspot 0,0,0,unID,angle_3,6 : unID=unID+1
hotspot 1,0,0,unID,angle_3,4+128 : unID=unID+1
hotspot 0,-1,0,unID,angle_3,7 : unID=unID+1
hotspot 0.20*cos(angle_3),0,0.20*sin(angle_3),unID,angle_3,5 : unID=unID+1

roty -angle_3

!---------------------------------------!
! Rotation around the X coordinate axis !
!---------------------------------------!

hotspot 0,0,0,unID,angle_1,6 : unID=unID+1
hotspot 0,1,0,unID,angle_1,4+128 : unID=unID+1
hotspot 1,0,0,unID,angle_1,7 : unID=unID+1
hotspot 0,0.20*cos(angle_1),0.20*sin(angle_1),unID,angle_1,5 : unID=unID+1

rotx -90+angle_1

!------------------!
! Angle of Bending !
!------------------!

hotspot radius_1,0,0,unID,angle_2,6 : unID=unID+1
hotspot 0,0,0,unID,angle_2,4+256 : unID=unID+1
hotspot radius_1,0,1,unID,angle_2,7 : unID=unID+1
hotspot radius_1-radius_1*cos(angle_2),0,radius_1*sin(angle_2),unID,angle_2,5 : unID=unID+1

!--------------------------------------!
! Elbow radius editing at the Begining !
!--------------------------------------!

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot -1,0,0,unID,radius_2,3 : unID=unID+1
hotspot radius_2,0,0,unID,radius_2,2 : unID=unID+1

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot 1,0,0,unID,radius_2,3 : unID=unID+1
hotspot -radius_2,0,0,unID,radius_2,2 : unID=unID+1

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot 0,-1,0,unID,radius_2,3 : unID=unID+1
hotspot 0,radius_2,0,unID,radius_2,2 : unID=unID+1

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot 0,1,0,unID,radius_2,3 : unID=unID+1
hotspot 0,-radius_2,0,unID,radius_2,2 : unID=unID+1

!---------------------------------!
! Elbow radius editing at the End !
!---------------------------------!

addx radius_1
roty angle_2
addx -radius_1

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot -1,0,0,unID,radius_2,3 : unID=unID+1
hotspot radius_2,0,0,unID,radius_2,2 : unID=unID+1

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot 1,0,0,unID,radius_2,3 : unID=unID+1
hotspot -radius_2,0,0,unID,radius_2,2 : unID=unID+1

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot 0,-1,0,unID,radius_2,3 : unID=unID+1
hotspot 0,radius_2,0,unID,radius_2,2 : unID=unID+1

hotspot	0,0,0,unID,radius_2,1+128 : unID=unID+1
hotspot 0,1,0,unID,radius_2,3 : unID=unID+1
hotspot 0,-radius_2,0,unID,radius_2,2 : unID=unID+1

del 3

!----------!
! Geometry !
!----------!

material gs_prim_mat
elbow radius_1,angle_2,radius_2

!-----------------------------------!
! Axis Line
!-----------------------------------!

if gs_axis then
	addx radius_1
	rotx -90

	model wire
	ARC radius_1, 180, 180+angle_2

	del 2
endif
