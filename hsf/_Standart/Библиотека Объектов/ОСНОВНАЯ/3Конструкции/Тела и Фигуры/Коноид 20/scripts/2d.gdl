

pen gs_cont_pen

unID=1
hLineID = 1000
hLineAxisID = 1100

!! Length Editing !!

IF ABS(cos(angle_1)) > EPS THEN
	hotspot2 0,  0,   unID, b_ca, 1+256 ,B: unID=unID+1
	hotspot2 0, -1,   unID, b_ca, 3     ,B: unID=unID+1
	hotspot2 0, b_ca, unID, b_ca, 2     ,B: unID=unID+1
ELSE
	hotspot2 0,  0, unID: unID=unID+3
ENDIF

!! Start Radius Editing !!

IF abs(angle_2 - 90)>=eps Then
	hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
	hotspot2 cos(angle_2),sin(angle_2)*sin(angle_1),unID,radius_1,3 : unID=unID+1
	hotspot2 (-radius_1)*cos(angle_2),(-radius_1)*sin(angle_2)*sin(angle_1),unID,radius_1,2 : unID=unID+1
ELSE
	unID=unID+3
ENDIF

IF cone_angle - 90 >= EPS Then
	! cone_angle+angle_2 <> 90
	hotspot2 0,0,unID,radius_1_ca,1+128,radius_1 : unID=unID+1
	hotspot2 -cos(angle_2-180+cone_angle),-sin(angle_2-180+cone_angle)*sin(angle_1),unID,radius_1_ca,3,radius_1 : unID=unID+1
	hotspot2 radius_1*cos(angle_2-180+cone_angle),+radius_1*sin(angle_2-180+cone_angle)*sin(angle_1),unID,radius_1_ca,2,radius_1 : unID=unID+1
ELSE
	unID=unID+3
ENDIF

!! End Radius Editing !!

IF abs(angle_2 - 90)>=eps Then
	hotspot2 0,b_ca,unID,radius_2,1+128 : unID=unID+1
	hotspot2 cos(angle_2),b_ca*sin(angle_2)*sin(angle_1),unID,radius_2,3 : unID=unID+1
	hotspot2 (-radius_2)*cos(angle_2),b_ca+(-radius_2)*sin(angle_2)*sin(angle_1),unID,radius_2,2 : unID=unID+1
ELSE
	unID=unID+3
ENDIF

IF cone_angle - 90 >= EPS Then
	hotspot2 0,b_ca,unID,radius_2_ca,1+128,radius_2 : unID=unID+1
	hotspot2 -cos(angle_2-180+cone_angle),b_ca*(-sin(angle_2-180+cone_angle)*sin(angle_1)),unID,radius_2_ca,3,radius_2 : unID=unID+1
	hotspot2 radius_2*cos(angle_2-180+cone_angle),b_ca+radius_2*sin(angle_2-180+cone_angle)*sin(angle_1),unID,radius_2_ca,2,radius_2 : unID=unID+1
ELSE
	unID=unID+3
ENDIF

IF angle_1 > 0 OR angle_2 > 0 THEN
	HOTSPOT2 0,0
	PROJECT2{2} 3, 270, 32+3, gs_back_pen, 0, 0, 90
	IF gs_axis THEN
		LINE_TYPE gs_axis_line
		LINE2 0,(0-g)*cos (angle_1),0,(b+g)*cos(angle_1)
		HOTLINE2 0,(0-g)*cos (angle_1),0,(b+g)*cos(angle_1), hLineAxisID
	ENDIF
ELSE

	!!--------------------------!!
	!! Conoid Thickness Editing !!
	!!--------------------------!!

	hotspot2 -radius_1,0,unID,cone_thk_1,1+128 : unID=unID+1
	hotspot2 -radius_1-1,0,unID,cone_thk_1,3 : unID=unID+1
	hotspot2 -radius_1+cone_thk_1,0,unID,cone_thk_1,2 : unID=unID+1

	hotspot2 -radius_2,b,unID,cone_thk_2,1+128 : unID=unID+1
	hotspot2 -radius_2-1,b,unID,cone_thk_2,3 : unID=unID+1
	hotspot2 -radius_2+cone_thk_2,b,unID,cone_thk_2,2 : unID=unID+1

	If abs(cone_angle-90)>=eps Then
		If cone_angle>90 Then
			hotspot2 radius_1_ca,0,unID,cone_thk_1_ca,1+128,cone_thk_1 : unID=unID+1
			hotspot2 radius_1_ca+1,0,unID,cone_thk_1_ca,3,cone_thk_1 : unID=unID+1
			hotspot2 radius_1_ca-cone_thk_1_ca,0,unID,cone_thk_1_ca,2,cone_thk_1 : unID=unID+1

			hotspot2 radius_2_ca,b,unID,cone_thk_2_ca,1+128,cone_thk_2 : unID=unID+1
			hotspot2 radius_2_ca+1,b,unID,cone_thk_2_ca,3,cone_thk_2 : unID=unID+1
			hotspot2 radius_2_ca-cone_thk_2_ca,b,unID,cone_thk_2_ca,2,cone_thk_2 : unID=unID+1
		Else
			hotspot2 radius_1_ca,0,unID,cone_thk_1_ca,1+128,cone_thk_1 : unID=unID+1
			hotspot2 radius_1_ca+1,0,unID,cone_thk_1_ca,3,cone_thk_1 : unID=unID+1
			hotspot2 radius_1_ca-cone_thk_1_ca,0,unID,cone_thk_1_ca,2,cone_thk_1 : unID=unID+1

			hotspot2 radius_2_ca,b,unID,cone_thk_2_ca,1+128,cone_thk_2 : unID=unID+1
			hotspot2 radius_2_ca+1,b,unID,cone_thk_2_ca,3,cone_thk_2 : unID=unID+1
			hotspot2 radius_2_ca-cone_thk_2_ca,b,unID,cone_thk_2_ca,2,cone_thk_2 : unID=unID+1
		EndIf
	EndIf

	!!!!!!!!!!!!!!!!!!!!!! fill

	drawindex 10
	pen gs_cont_pen
	fill gs_fill_type
	poly2_b 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
			-radius_1,0,1,
			max(radius_1*cos(180-cone_angle),(radius_1-cone_thk_1)*cos(180-cone_angle)),0,1,
			max(radius_2*cos(180-cone_angle),(radius_2-cone_thk_2)*cos(180-cone_angle)),b,1,
			-radius_2,b,1,
			-radius_1,0,-1

	hotline2 -radius_1,0,max(radius_1*cos(180-cone_angle),(radius_1-cone_thk_1)*cos(180-cone_angle)),0, hLineID : hLineID = hLineID + 1
	hotline2 max(radius_1*cos(180-cone_angle),(radius_1-cone_thk_1)*cos(180-cone_angle)),0,max(radius_2*cos(180-cone_angle),(radius_2-cone_thk_2)*cos(180-cone_angle)),b, hLineID : hLineID = hLineID + 1
	hotline2 max(radius_2*cos(180-cone_angle),(radius_2-cone_thk_2)*cos(180-cone_angle)),b,-radius_2,b, hLineID : hLineID = hLineID + 1
	hotline2 -radius_2,b,-radius_1,0, hLineID : hLineID = hLineID + 1

	drawindex 20
	If cone_angle>90 Then set line_type gs_DashedLine
	line2  min(radius_1*cos(180-cone_angle),(radius_1-cone_thk_1)*cos(180-cone_angle)), 0, min(radius_2*cos(180-cone_angle),(radius_2-cone_thk_2)*cos(180-cone_angle)), b
	hotline2  min(radius_1*cos(180-cone_angle),(radius_1-cone_thk_1)*cos(180-cone_angle)), 0, min(radius_2*cos(180-cone_angle),(radius_2-cone_thk_2)*cos(180-cone_angle)), b, hLineID : hLineID = hLineID + 1

	set line_type gs_DashedLine
	line2 -radius_1+cone_thk_1, 0, -radius_2+cone_thk_2, b
	hotline2 -radius_1+cone_thk_1, 0, -radius_2+cone_thk_2, b, hLineID : hLineID = hLineID + 1

	pen gs_axis_pen
	!project2 3,270,1

	if gs_axis then
		line_type gs_axis_line
		line2 0,0-g,0,b+g
		hotline2 0,0-g,0,b+g, hLineAxisID
	endif
endif

end

