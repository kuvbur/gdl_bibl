
call "FM_types" parameters all

IF ABS(cone_angle-180) < EPS AND ABS(angle_1) < EPS AND ABS(angle_2) < EPS THEN
	parameters a=2*max(radius_1,radius_2),zzyzx=max(radius_1,radius_2)
	lock "A","ZZYZX"
ELSE
	HIDEPARAMETER "A", "ZZYZX"
ENDIF

values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_SIMPLE, `Простой`,
								DLEV3D_OFF, `Откл.`
values "radius_1" range(0,)
values "radius_2" range(0,)
values "radius_1_ca" range(0,)
values "radius_2_ca" range(0,)
values "gs_resol" range [3,)

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19
if gs_axis_pen=0 then parameters gs_axis_pen=64


if gs_detlevel_3D_m = DLEV3D_SIMPLE then lock "gs_resol"

If GLOB_MODPAR_NAME="radius_1" Then parameters radius_1_ca=radius_1*cos(180-cone_angle)
If GLOB_MODPAR_NAME="radius_1_ca" Then parameters radius_1=radius_1_ca/cos(180-cone_angle)

values "cone_angle" range[10,180]
If GLOB_MODPAR_NAME="cone_angle" Then
	parameters cone_thk_1_ca=cone_thk_1*cos(180-cone_angle), cone_thk_2_ca=cone_thk_2*cos(180-cone_angle)
	parameters radius_1_ca=radius_1*cos(180-cone_angle), radius_2_ca=radius_2*cos(180-cone_angle)
EndIf

if GLOB_MODPAR_NAME="radius_2_ca" then
	parameters radius_2 = radius_2_ca/cos(180-cone_angle)
else
	parameters radius_2_ca = radius_2*cos(180-cone_angle)
endif

if GLOB_MODPAR_NAME = "b_ca" then
	parameters B = b_ca/cos(angle_1)
else
	parameters b_ca = B*cos(angle_1)
endif

values{2} "iConeType" 	CONETYPE_CONSTANSTHK, `Постоянная Толщина`,
						CONETYPE_VARIABLETHK, `Переменная Толщина`
If iConeType = CONETYPE_CONSTANSTHK Then
	values "cone_thk_1" range(0.001,min(radius_1,radius_2)]
	values "cone_thk_2" range(0.001,min(radius_1,radius_2)]
	If cone_angle>=90 Then
		values "cone_thk_1_ca" range(0.001,min(radius_1*cos(180-cone_angle),radius_2*cos(180-cone_angle))]
		values "cone_thk_2_ca" range(0.001,min(radius_1*cos(180-cone_angle),radius_2*cos(180-cone_angle))]
	Else
		values "cone_thk_1_ca" range(max(radius_1*cos(180-cone_angle),radius_2*cos(180-cone_angle)),-0.001]
		values "cone_thk_2_ca" range(max(radius_1*cos(180-cone_angle),radius_2*cos(180-cone_angle)),-0.001]
	EndIf

	If GLOB_MODPAR_NAME="cone_thk_1" Then
		parameters cone_thk_2=cone_thk_1
		parameters cone_thk_1_ca=cone_thk_1*cos(180-cone_angle), cone_thk_2_ca=cone_thk_1*cos(180-cone_angle)
	EndIf
	If GLOB_MODPAR_NAME="cone_thk_1_ca" Then
		parameters cone_thk_2_ca=cone_thk_1_ca
		parameters cone_thk_1=cone_thk_1_ca/cos(180-cone_angle), cone_thk_2=cone_thk_1_ca/cos(180-cone_angle)
	EndIf
	If GLOB_MODPAR_NAME="cone_thk_2" Then
		parameters cone_thk_1=cone_thk_2, cone_thk_1_ca=cone_thk_2_ca
		parameters cone_thk_2_ca=cone_thk_2*cos(180-cone_angle), cone_thk_1_ca=cone_thk_2*cos(180-cone_angle)
	EndIf
	If GLOB_MODPAR_NAME="cone_thk_2_ca" Then
		parameters cone_thk_1_ca=cone_thk_2_ca
		parameters cone_thk_2=cone_thk_2_ca/cos(180-cone_angle), cone_thk_1=cone_thk_2_ca/cos(180-cone_angle)
	EndIf
Else
	values "cone_thk_1" range(0.001,radius_1]
	values "cone_thk_2" range(0.001,radius_2]
	If cone_angle>=90 Then
		values "cone_thk_1_ca" range(0.001,radius_1*cos(180-cone_angle)]
		values "cone_thk_2_ca" range(0.001,radius_2*cos(180-cone_angle)]
	Else
		values "cone_thk_1_ca" range(radius_1*cos(180-cone_angle),-0.001]
		values "cone_thk_2_ca" range(radius_2*cos(180-cone_angle),-0.001]
	EndIf

	If GLOB_MODPAR_NAME="cone_thk_1" Then parameters cone_thk_1_ca=cone_thk_1*cos(180-cone_angle)
	If GLOB_MODPAR_NAME="cone_thk_1_ca" Then parameters cone_thk_1=cone_thk_1_ca/cos(180-cone_angle)

	If GLOB_MODPAR_NAME="cone_thk_2" Then parameters cone_thk_2_ca=cone_thk_2*cos(180-cone_angle)
	If GLOB_MODPAR_NAME="cone_thk_2_ca" Then parameters cone_thk_2=cone_thk_2_ca/cos(180-cone_angle)
EndIf

If GLOB_MODPAR_NAME="iConeType" and iConeType = CONETYPE_CONSTANSTHK Then parameters cone_thk_2=cone_thk_1, cone_thk_2_ca=cone_thk_1_ca

IF iConeType = CONETYPE_CONSTANSTHK THEN
	PARAMETERS cone_thk_2 = cone_thk_1
	LOCK "cone_thk_2"
ENDIF
