

values{2} "iTreeForm"	TREE_FORM_1,	stTreeForms[1],
						TREE_FORM_2, 	stTreeForms[2]

call "FM_types" parameters all

if iTreeForm = TREE_FORM_1 then						!Tree
else
	lock	"bEllipsoid",
			"nGreen",
			"widthGreen"

	hideparameter	"bEllipsoid",
					"nGreen",
					"widthGreen"

	values "heightTrunk" range [0.001 , ZZYZX - 0.01]
endif

if GLOB_MODPAR_NAME = "iTreeForm" then
	if iTreeForm = TREE_FORM_1 then						!Tree
		heightGreen = 6
		heightTrunk = 3
		widthGreen = 4
		diamGreen = 1.8
		parameters 	widthGreen = widthGreen
	else
		heightGreen = 7
		heightTrunk = 1
		diamGreen = 3
	endif
	parameters 	heightGreen = heightGreen,
				heightTrunk = heightTrunk,
				diamGreen = diamGreen
endif

if GLOB_MODPAR_NAME = "ZZYZX" then
	heightTrunk = ZZYZX - heightGreen
	parameters heightTrunk = heightTrunk
else
	ZZYZX = heightGreen + heightTrunk
	parameters ZZYZX = ZZYZX
endif

if iTreeForm = TREE_FORM_1 then						!Tree
	if GLOB_MODPAR_NAME = "A" then
		widthGreen = A
		B = A
		parameters	widthGreen = widthGreen,
					B = B
	else
		if GLOB_MODPAR_NAME = "B" then
			widthGreen = B
			A = B
			parameters	widthGreen = widthGreen,
						A = A
		else
			B = widthGreen 
			A = widthGreen 
			parameters	B = B,
						A = A
		endif
	endif
else
	if GLOB_MODPAR_NAME = "A" then
		diamGreen = A
		B = A
		parameters	diamGreen = diamGreen,
					B = B
	else
		if GLOB_MODPAR_NAME = "B" then
			diamGreen = B
			A = B
			parameters	diamGreen = diamGreen,
						A = A
		else
			B = diamGreen 
			A = diamGreen 
			parameters	B = B,
						A = A
		endif
	endif
endif

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	rrr = REQUEST ("Name_of_macro", "", LPName)
	call "Onorm_Plant" parameters all sLibpartName = strsub(LPName,0,strlen(LPName)-4)
else
	hideparameter "gs_onorm_Title"
endif

