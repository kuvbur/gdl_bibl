
!! Dome


pen gs_cont_pen

unID	= 1
hLineID = 1000

hotspot2 0,0,unID : unID=unID+1

!--------------------!
! Dome angle editing !
!--------------------!
IF ABS(angle)<EPS THEN
	hotspot2 0,0,unID,angle_1,6 : unID=unID+1
	hotspot2 (sphere_rad-dome_thk/2),0,unID,angle_1,4+256 : unID=unID+1
	hotspot2 (sphere_rad-dome_thk/2)*cos(angle_1),(sphere_rad-dome_thk/2)*sin(angle_1),unID,angle_1,5 : unID=unID+1
ENDIF


!!---------------------!!
!! Dome Radius Editing !!
!!---------------------!!

hotspot2 0,0,unID,sphere_rad,1+128 : unID=unID+1
hotspot2 -1,0,unID,sphere_rad,3 : unID=unID+1
hotspot2 sphere_rad*cos(angle),0,unID,sphere_rad,2 : unID=unID+1

IF ABS(angle)<EPS OR ABS(angle-180)<EPS THEN
	if angle_1<>360 then
		rot2 angle_1
		hotspot2 0,0,unID,sphere_rad,1+128 : unID=unID+1
		hotspot2 -1,0,unID,sphere_rad,3 : unID=unID+1
		hotspot2 sphere_rad,0,unID,sphere_rad,2 : unID=unID+1
		del 1
	endif

	if angle_1>180 then
		hotspot2 0,0,unID,sphere_rad,1+128 : unID=unID+1
		hotspot2 1,0,unID,sphere_rad,3 : unID=unID+1
		hotspot2 -sphere_rad,0,unID,sphere_rad,2 : unID=unID+1
	endif
endif

if angle_1>90 then
	hotspot2 0,0,unID,sphere_rad,1+128 : unID=unID+1
	hotspot2 0,-1,unID,sphere_rad,3 : unID=unID+1
	hotspot2 0,sphere_rad,unID,sphere_rad,2 : unID=unID+1
endif

if angle_1>270 then
	hotspot2 0,0,unID,sphere_rad,1+128 : unID=unID+1
	hotspot2 0,1,unID,sphere_rad,3 : unID=unID+1
	hotspot2 0,-sphere_rad,unID,sphere_rad,2 : unID=unID+1
endif


!!------------------------!!
!! Dome Thickness Editing !!
!!------------------------!!

hotspot2 sphere_rad,0,unID,dome_thk,1+128 : unID=unID+1
hotspot2 sphere_rad+1,0,unID,dome_thk,3 : unID=unID+1
hotspot2 (sphere_rad-dome_thk)*cos(angle),0,unID,dome_thk,2 : unID=unID+1

IF ABS(angle)<EPS OR ABS(angle-180)<EPS THEN
	if angle_1<>360 then
		rot2 angle_1
		hotspot2 sphere_rad,0,unID,dome_thk,1+128 : unID=unID+1
		hotspot2 sphere_rad+1,0,unID,dome_thk,3 : unID=unID+1
		hotspot2 sphere_rad-dome_thk,0,unID,dome_thk,2 : unID=unID+1
		del 1
	endif

	if angle_1>180 then
		hotspot2 -sphere_rad,0,unID,dome_thk,1+128 : unID=unID+1
		hotspot2 -sphere_rad-1,0,unID,dome_thk,3 : unID=unID+1
		hotspot2 -sphere_rad+dome_thk,0,unID,dome_thk,2 : unID=unID+1
	endif
ENDIF

if angle_1>90 then
	hotspot2 0,sphere_rad,unID,dome_thk,1+128 : unID=unID+1
	hotspot2 0,sphere_rad+1,unID,dome_thk,3 : unID=unID+1
	hotspot2 0,sphere_rad-dome_thk,unID,dome_thk,2 : unID=unID+1
endif

if angle_1>270 then
	hotspot2 0,-sphere_rad,unID,dome_thk,1+128 : unID=unID+1
	hotspot2 0,-sphere_rad-1,unID,dome_thk,3 : unID=unID+1
	hotspot2 0,-sphere_rad+dome_thk,unID,dome_thk,2 : unID=unID+1
endif

!!----------!!
!! Geometry !!
!!----------!!

fill gs_fill_type
if abs(angle)<eps then
	if angle_1=360 then
		poly2_b	2, 1+2+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				sphere_rad, angle_1, 4001

		hotarc2 0,0,sphere_rad,0,180, hLineID : hLineID = hLineID + 1
		hotarc2 0,0,sphere_rad,180,360, hLineID : hLineID = hLineID + 1

		line2 sphere_rad,0,sphere_rad-2*dome_thk,0
		hotline2 sphere_rad,0,sphere_rad-2*dome_thk,0, hLineID : hLineID = hLineID + 1
		hLineID = hLineID + 3
	else
		hLineID = hLineID + 3
		poly2_b	5, 1+2+64, gs_fill_pen, gs_back_pen,
				0, 0, 1,
				sphere_rad, 0, 1,
				0, 0, 901,
				sphere_rad, angle_1, 4001,
				0, 0, -1

		hotarc2 0,0,sphere_rad,0,angle_1, hLineID : hLineID = hLineID + 1
		hotline2 0,0,sphere_rad,0, hLineID : hLineID = hLineID + 1
		hotline2 0,0,sphere_rad*cos(angle_1), sphere_rad*sin(angle_1), hLineID : hLineID = hLineID + 1
	endif

	line_type gs_DashedLine
	if abs(sphere_rad-dome_thk) > EPS then
		arc2 0,0,(sphere_rad-dome_thk),0,angle_1
		hotarc2 0,0,(sphere_rad-dome_thk),0,angle_1, hLineID : hLineID = hLineID + 1
	else
		hLineID = hLineID + 1
	endif

	if gs_axis then
		pen gs_axis_pen
		line_type gs_axis_line
		arc2 0,0,sphere_rad-dome_thk/2, 0, angle_1
		hotarc2 0,0,sphere_rad-dome_thk/2, 0, angle_1, hLineID : hLineID = hLineID + 1
	else
		hLineID = hLineID + 1
	endif
	pen gs_cont_pen
else
	hLineID = hLineID + 8
	project2{2} 3, 270, 32+3, gs_back_pen, 0, 0, 90

	line_type gs_DashedLine
	mul2 cos(angle),1
	arc2 0,0,(sphere_rad-dome_thk),0,angle_1
	hotarc2 0,0,(sphere_rad-dome_thk),0,angle_1, hLineID : hLineID = hLineID + 1
endif



