
!! Dome

if gs_detlevel_3D_m = DLEV3D_OFF then End
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 18

if gs_shadow then
	shadow on
else
	shadow off
endif

if GLOB_CONTEXT<20 or GLOB_CONTEXT>40 then
	resol gs_resol
else
	resol 12
endif

pen gs_cont_pen

unID=1
hotspot 0,0,0,unID : unID=unID+1

material gs_prim_mat


hotspot 0,0,0,unID,angle,6 : unID=unID+1
hotspot 1,0,0,unID,angle,4+128 : unID=unID+1
hotspot 0,-1,0,unID,angle,7 : unID=unID+1
hotspot 1/2*sphere_rad*cos(angle),0,1/2*sphere_rad*sin(angle),unID,angle,5 : unID=unID+1

roty -angle

!!---------------------!!
!! Dome Radius Editing !!
!!---------------------!!

!* right
hotspot 0,0,0,unID,sphere_rad,1+128 : unID=unID+1
hotspot -1,0,0,unID,sphere_rad,3 : unID=unID+1
hotspot sphere_rad,0,0,unID,sphere_rad,2 : unID=unID+1

!* bottom
if angle_1<>360 then
	rotz angle_1
	hotspot 0,0,0,unID,sphere_rad,1+128 : unID=unID+1
	hotspot -1,0,0,unID,sphere_rad,3 : unID=unID+1
	hotspot sphere_rad,0,0,unID,sphere_rad,2 : unID=unID+1
	del 1
endif

!*upper
if angle_1>90 then
	hotspot 0,0,0,unID,sphere_rad,1+128 : unID=unID+1
	hotspot 0,-1,0,unID,sphere_rad,3 : unID=unID+1
	hotspot 0,sphere_rad,0,unID,sphere_rad,2 : unID=unID+1
endif

!*left
if angle_1>180 then
	hotspot 0,0,0,unID,sphere_rad,1+128 : unID=unID+1
	hotspot 1,0,0,unID,sphere_rad,3 : unID=unID+1
	hotspot -sphere_rad,0,0,unID,sphere_rad,2 : unID=unID+1
endif

if angle_1>270 then
	hotspot 0,0,0,unID,sphere_rad,1+128 : unID=unID+1
	hotspot 0,1,0,unID,sphere_rad,3 : unID=unID+1
	hotspot 0,-sphere_rad,0,unID,sphere_rad,2 : unID=unID+1
endif

hotspot 0,0,0,unID,sphere_rad,1+128 : unID=unID+1
hotspot 0,0,-1,unID,sphere_rad,3 : unID=unID+1
hotspot 0,0,sphere_rad,unID,sphere_rad,2 : unID=unID+1

!!------------------------!!
!! Dome Thickness Editing !!
!!------------------------!!

hotspot sphere_rad,0,0,unID,dome_thk,1+128 : unID=unID+1
hotspot sphere_rad+1,0,0,unID,dome_thk,3 : unID=unID+1
hotspot sphere_rad-dome_thk,0,0,unID,dome_thk,2 : unID=unID+1

if angle_1<>360 then
	rotz angle_1
	hotspot sphere_rad,0,0,unID,dome_thk,1+128 : unID=unID+1
	hotspot sphere_rad+1,0,0,unID,dome_thk,3 : unID=unID+1
	hotspot sphere_rad-dome_thk,0,0,unID,dome_thk,2 : unID=unID+1
	del 1
endif

if angle_1>90 then
	hotspot 0,sphere_rad,0,unID,dome_thk,1+128 : unID=unID+1
	hotspot 0,sphere_rad+1,0,unID,dome_thk,3 : unID=unID+1
	hotspot 0,sphere_rad-dome_thk,0,unID,dome_thk,2 : unID=unID+1
endif

if angle_1>180 then
	hotspot -sphere_rad,0,0,unID,dome_thk,1+128 : unID=unID+1
	hotspot -sphere_rad-1,0,0,unID,dome_thk,3 : unID=unID+1
	hotspot -sphere_rad+dome_thk,0,0,unID,dome_thk,2 : unID=unID+1
endif

if angle_1>270 then
	hotspot 0,-sphere_rad,0,unID,dome_thk,1+128 : unID=unID+1
	hotspot 0,-sphere_rad-1,0,unID,dome_thk,3 : unID=unID+1
	hotspot 0,-sphere_rad+dome_thk,0,unID,dome_thk,2 : unID=unID+1
endif

hotspot 0,0,sphere_rad,unID,dome_thk,1+128 : unID=unID+1
hotspot 0,0,sphere_rad+1,unID,dome_thk,3 : unID=unID+1
hotspot 0,0,sphere_rad-dome_thk,unID,dome_thk,2 : unID=unID+1

!!----------!!
!! Geometry !!
!!----------!!

roty -90

num=int(gs_resol/4)
alfa=0
for i=1 to num
	if i=1 then show_edge=2 else show_edge=1 ! Edge Visible
	put sphere_rad*sin(alfa), sphere_rad*cos(alfa), show_edge
	alfa=90/num*i
next i
put sphere_rad, 0.00001, 2

if dome_thk>0 then
	alfa=90
	show_edge=1

	put sphere_rad-dome_thk, 0.00001, 2
	for i=1 to num
		if i=num then show_edge=2 ! Edge Visible
		alfa=alfa-90/num
		put (sphere_rad-dome_thk)*sin(alfa), (sphere_rad-dome_thk)*cos(alfa), show_edge
	next i
	put 0, sphere_rad, 2
endif

if angle_1=360 then
	mask=2
else
	if dome_thk>0 then
		mask=4+8+16+32
	else
		mask=2+16+32
	endif
endif


resol 120
rotx -90
revolve	nsp/3, angle_1, mask,
		get(nsp)
del 1
del top
END
