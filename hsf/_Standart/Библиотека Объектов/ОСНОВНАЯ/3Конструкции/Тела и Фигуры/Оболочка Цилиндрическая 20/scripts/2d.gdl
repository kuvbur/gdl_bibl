
!! Cylinder Shell

pen gs_cont_pen

unID=1
hLineID = 1000

hotspot2 0,0,unID : unID=unID+1

if angle_1 > 0 or angle_2 > 0 then

	project2{2} 3, 270, 32+3, gs_back_pen, 0, 0, 90

else

	!---------------------!
	! Shell angle editing !
	!---------------------!

	hotspot2 0,0,unID,shell_angle,6+512 : unID=unID+1
	hotspot2 -radius_1+shell_thk/2,0,unID,shell_angle,4+256 : unID=unID+1
	hotspot2 (radius_1-shell_thk/2)*cos(180-shell_angle),(radius_1-shell_thk/2)*sin(shell_angle),unID,shell_angle,5 : unID=unID+1

	!----------------------!
	! Shell radius editing !
	!----------------------!

	hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
	hotspot2 1,0,unID,radius_1,3 : unID=unID+1
	hotspot2 -radius_1,0,unID,radius_1,2 : unID=unID+1

	if shell_angle<>360 then
		rot2 -shell_angle
		hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
		hotspot2 1,0,unID,radius_1,3 : unID=unID+1
		hotspot2 -radius_1,0,unID,radius_1,2 : unID=unID+1
		del 1
	endif

	if shell_angle>90 then
		hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
		hotspot2 0,-1,unID,radius_1,3 : unID=unID+1
		hotspot2 0,radius_1,unID,radius_1,2 : unID=unID+1
	endif

	if shell_angle>180 then
		hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
		hotspot2 -1,0,unID,radius_1,3 : unID=unID+1
		hotspot2 radius_1,0,unID,radius_1,2 : unID=unID+1
	endif

	if shell_angle>270 then
		hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
		hotspot2 0,1,unID,radius_1,3 : unID=unID+1
		hotspot2 0,-radius_1,unID,radius_1,2 : unID=unID+1
	endif

	!-------------------------!
	! Shell Thickness Editing !
	!-------------------------!

	hotspot2 -radius_1,0,unID,shell_thk,1+128 : unID=unID+1
	hotspot2 -radius_1-1,0,unID,shell_thk,3 : unID=unID+1
	hotspot2 -radius_1+shell_thk,0,unID,shell_thk,2 : unID=unID+1

	if shell_angle<>360 then
		rot2 -shell_angle
		hotspot2 -radius_1,0,unID,shell_thk,1+128 : unID=unID+1
		hotspot2 -radius_1-1,0,unID,shell_thk,3 : unID=unID+1
		hotspot2 -radius_1+shell_thk,0,unID,shell_thk,2 : unID=unID+1
		del 1
	endif

	if shell_angle>90 then
		hotspot2 0,radius_1,unID,shell_thk,1+128 : unID=unID+1
		hotspot2 0,radius_1+1,unID,shell_thk,3 : unID=unID+1
		hotspot2 0,radius_1-shell_thk,unID,shell_thk,2 : unID=unID+1
	endif

	if shell_angle>180 then
		hotspot2 radius_1,0,unID,shell_thk,1+128 : unID=unID+1
		hotspot2 radius_1+1,0,unID,shell_thk,3 : unID=unID+1
		hotspot2 radius_1-shell_thk,0,unID,shell_thk,2 : unID=unID+1
	endif

	if shell_angle>270 then
		hotspot2 0,-radius_1,unID,shell_thk,1+128 : unID=unID+1
		hotspot2 0,-radius_1-1,unID,shell_thk,3 : unID=unID+1
		hotspot2 0,-radius_1+shell_thk,unID,shell_thk,2 : unID=unID+1
	endif

	!!----------!!
	!! Geometry !!
	!!----------!!

	fill gs_fill_type
	if shell_angle=360 then
		poly2_b 14, 1+2+4+64, gs_fill_pen, gs_back_pen,
				radius_1, 0.0, 1,
				0.0, 0.0, 900,
				0.0, 180.0, 4001,
			   -radius_1, 0.0, 1,
				0.0, 0.0, 900,
				0.0, 180.0, 4001,
				radius_1, 0.0, -1,
				(radius_1-shell_thk), 0.0, 1,
				0.0, 0.0, 900,
				0.0, 180.0, 4001,
			   -(radius_1-shell_thk), 0.0, 1,
				0.0, 0.0, 900,
				0.0, 180.0, 4001,
				(radius_1-shell_thk), 0.0, -1

			hotarc2 0,0,radius_1-shell_thk, 0, 180, hLineID : hLineID = hLineID + 1
			hotarc2 0,0,radius_1-shell_thk, 180, 360, hLineID : hLineID = hLineID + 1
			hotarc2 0,0,radius_1, 0, 180, hLineID : hLineID = hLineID + 1
			hotarc2 0,0,radius_1, 180, 360, hLineID : hLineID = hLineID + 1
	else
		hLineID = hLineID + 4
		poly2_b	7, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-(radius_1-shell_thk), 0, 1,
				-radius_1, 0, 1,
				0, 0, 901,
				radius_1,-shell_angle, 4001,
				(radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle), 1,
				0, 0, 901,
				-(radius_1-shell_thk), shell_angle, 4001

			hotarc2 0,0,radius_1-shell_thk, 180-shell_angle, 180, hLineID : hLineID = hLineID + 1
			hotarc2 0,0,radius_1, 180-shell_angle, 180, hLineID : hLineID = hLineID + 1
			hotline2 (radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle),
					 (radius_1)*cos(180-shell_angle),(radius_1)*sin(shell_angle), hLineID : hLineID = hLineID + 1
			hotline2 -(radius_1-shell_thk), 0,-radius_1, 0, hLineID : hLineID = hLineID + 1
	endif

	if gs_axis then
		line_type gs_axis_line
		pen gs_axis_pen
		if shell_angle<>360 then
			arc2 0,0,radius_1-shell_thk/2, 180-shell_angle, 180
			hotarc2 0,0,radius_1-shell_thk/2, 180-shell_angle, 180, hLineID : hLineID = hLineID + 1
		else
			hLineID = hLineID + 1
			arc2 0,0,radius_1-shell_thk/2, 0, 180
			arc2 0,0,radius_1-shell_thk/2, 180, 360
			hotarc2 0,0,radius_1-shell_thk/2, 0, 180, hLineID : hLineID = hLineID + 1
			hotarc2 0,0,radius_1-shell_thk/2, 180, 360, hLineID : hLineID = hLineID + 1
		endif
	endif
endif
end
