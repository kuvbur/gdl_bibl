
pen gs_cont_pen

unID	= 1
hLineID = 1000

hotspot2 0,0,unID : unID=unID+1

!--------------------!
! Top Center Editing !
!--------------------!

if iEditMode = EDITMODE_COORDINATES then
	hotspot2 0,			dist_y,	unID,	dist_x,	1+128	: unID = unID + 1
	hotspot2 -1,		dist_y,	unID,	dist_x,	3		: unID = unID + 1
	hotspot2 dist_x,	dist_y,	unID,	dist_x,	2		: unID = unID + 1

	hotspot2 dist_x,	0,		unID,	dist_y,	1+128	: unID = unID + 1
	hotspot2 dist_x,	-1,		unID,	dist_y,	3		: unID = unID + 1
	hotspot2 dist_x,	dist_y,	unID,	dist_y,	2		: unID = unID + 1
else
	hotspot2 dist_x,	0,		unID : unID = unID + 1
endif

unID = 10
if iEndMode = ENDMODE_HORIZONTAL then
	angle=0
	for i=1 to edge_num
		hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
		hotspot2 -cos(angle),-sin(angle),unID,radius_1,3 : unID=unID+1
		hotspot2 radius_1*cos(angle),radius_1*sin(angle),unID,radius_1,2 : unID=unID+1

		angle	= i*360/edge_num
	next i
else
	rot2 beta
	angle	= 0
	for i=1 to edge_num
		hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
		hotspot2 -(cos(angle)*cos(90-gamma)),-sin(angle),unID,radius_1,3 : unID=unID+1
		hotspot2 radius_1*cos(angle)*cos(90-gamma),radius_1*sin(angle),unID,radius_1,2 : unID=unID+1
		angle=i*360/edge_num
	next i
	del 1
endif

unID = 110

if ABS(dist_x) < EPS & ABS(dist_y) < EPS then
	hotspot2 0,0,unID : unID=unID+1

	angle	= 0
	for i=1 to edge_num
		put radius_1*cos(angle), radius_1*sin(angle), 1
		hotline2 radius_1*cos(angle), radius_1*sin(angle), radius_1*cos(i*360/edge_num), radius_1*sin(i*360/edge_num), hLineID
		hLineID = hLineID + 1
		angle=i*360/edge_num
	next i

	fill gs_fill_type
	poly2_b	edge_num, 1+2+4+64, gs_fill_pen, gs_back_pen,
			get(nsp)
	angle	= 0
	for i=1 to edge_num
		line2 0,0,radius_1*cos(angle), radius_1*sin(angle)
		hotline2 0,0,radius_1*cos(angle), radius_1*sin(angle), hLineID
		hLineID = hLineID + 1
		angle=i*360/edge_num
	next i
else
	fill gs_fill_type
	project2{2} 3, 270, 32+3, gs_back_pen, 0, 0, 90
endif
