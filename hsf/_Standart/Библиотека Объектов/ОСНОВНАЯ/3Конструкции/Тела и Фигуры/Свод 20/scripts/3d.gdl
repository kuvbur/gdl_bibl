
!! Shell

if gs_detlevel_3D_m = DLEV3D_OFF then End
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 18

if gs_shadow then
	shadow on
else
	shadow off
endif

if GLOB_CONTEXT<20 or GLOB_CONTEXT>40 then
	resol gs_resol
else
	resol 12
endif

pen gs_cont_pen

rotz 90
rotx 90

unID=1
!---------------------!
! Shell angle editing !
!---------------------!

hotspot 0,0,0,unID,angle_1,6+128 : unID=unID+1
hotspot -radius_1+shell_thk/2,0,0,unID,angle_1,4+128 : unID=unID+1 !!!! +256
hotspot 0,0,-1,unID,angle_1,7 : unID=unID+1
hotspot (radius_1-shell_thk/2)*cos(180-angle_1),(radius_1-shell_thk/2)*sin(angle_1),0,unID,angle_1,5 : unID=unID+1

hotspot 0,0,a,unID,angle_1,6+128 : unID=unID+1
hotspot -radius_1+shell_thk/2,0,a,unID,angle_1,4+128 : unID=unID+1 !!!! +256
hotspot 0,0,a-1,unID,angle_1,7 : unID=unID+1
hotspot (radius_1-shell_thk/2)*cos(180-angle_1),(radius_1-shell_thk/2)*sin(angle_1),a,unID,angle_1,5 : unID=unID+1

rotz -angle_1

!----------------!
! Height Editing !
!----------------!

hotspot	-radius_1,0,0,unID,a,1+256 : unID=unID+1
hotspot	-radius_1,0,-1,unID,a,3 : unID=unID+1
hotspot	-radius_1,0,a,unID,a,2 : unID=unID+1

hotspot	-radius_1+shell_thk,0,0,unID,a,1+256 : unID=unID+1
hotspot	-radius_1+shell_thk,0,-1,unID,a,3 : unID=unID+1
hotspot	-radius_1+shell_thk,0,a,unID,a,2 : unID=unID+1

hotspot (radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle),0,unID,a,1+256 : unID=unID+1
hotspot (radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle),-1,unID,a,3 : unID=unID+1
hotspot (radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle),a,unID,a,2 : unID=unID+1

hotspot radius_1*cos(180-shell_angle),radius_1*sin(shell_angle),0,unID,a,1+256 : unID=unID+1
hotspot radius_1*cos(180-shell_angle),radius_1*sin(shell_angle),-1,unID,a,3 : unID=unID+1
hotspot radius_1*cos(180-shell_angle),radius_1*sin(shell_angle),a,unID,a,2 : unID=unID+1

!---------------------!
! Shell angle editing !
!---------------------!

hotspot 0,0,0,unID,shell_angle,6 : unID=unID+1
hotspot -radius_1+shell_thk/2,0,0,unID,shell_angle,4 : unID=unID+1 !!!! +256
hotspot 0,0,-1,unID,shell_angle,7 : unID=unID+1
hotspot (radius_1-shell_thk/2)*cos(180-shell_angle),(radius_1-shell_thk/2)*sin(shell_angle),0,unID,shell_angle,5 : unID=unID+1

hotspot 0,0,a,unID,shell_angle,6 : unID=unID+1
hotspot -radius_1+shell_thk/2,0,a,unID,shell_angle,4 : unID=unID+1 !!!! +256
hotspot 0,0,a-1,unID,shell_angle,7 : unID=unID+1
hotspot (radius_1-shell_thk/2)*cos(180-shell_angle),(radius_1-shell_thk/2)*sin(shell_angle),a,unID,shell_angle,5 : unID=unID+1

!-------------------------!
! Shell Thickness Editing !
!-------------------------!

hotspot	-radius_1,0,a/2,unID,shell_thk,1+128 : unID=unID+1
hotspot	-radius_1-1,0,a/2,unID,shell_thk,3 : unID=unID+1
hotspot	-radius_1+shell_thk,0,a/2,unID,shell_thk,2 : unID=unID+1

hotspot	radius_1*cos(180-shell_angle),radius_1*sin(shell_angle),a/2,unID,shell_thk,1+128 : unID=unID+1
hotspot	(radius_1+1)*cos(180-shell_angle),(radius_1+1)*sin(shell_angle),a/2,unID,shell_thk,3 : unID=unID+1
hotspot	(radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle),a/2,unID,shell_thk,2 : unID=unID+1

!-------------------------!
! Shell Radius Editing !
!-------------------------!

hotspot	0,0,a/2,unID,radius_1,1+128 : unID=unID+1
hotspot	1,0,a/2,unID,radius_1,3 : unID=unID+1
hotspot	-radius_1,0,a/2,unID,radius_1,2 : unID=unID+1

hotspot	0,0,a/2,unID,radius_1,1+128 : unID=unID+1
hotspot	-1*cos(180-shell_angle),-1*sin(shell_angle),a/2,unID,radius_1,3 : unID=unID+1
hotspot	(radius_1)*cos(180-shell_angle),(radius_1)*sin(shell_angle),a/2,unID,radius_1,2 : unID=unID+1

material gs_prim_mat

if shell_angle>360-0.001 and shell_angle<360+0.001 then
	if shell_thk>0 then
		prism_  14, a,
				radius_1, 0.0, 79,
				0.0, 0.0, 900,
				0.0, 180.0, 4079,
			   -radius_1, 0.0, 79,
				0.0, 0.0, 900,
				0.0, 180.0, 4079,
				radius_1, 0.0, -1,
				(radius_1-shell_thk), 0.0, 79,
				0.0, 0.0, 900,
				0.0, 180.0, 4079,
			   -(radius_1-shell_thk), 0.0, 79,
				0.0, 0.0, 900,
				0.0, 180.0, 4079,
				(radius_1-shell_thk), 0.0, -1
	else
		extrude 7, 0,0,a, 16+32,
				radius_1, 0.0, 1,
				0.0, 0.0, 901,
				0.0, 180.0, 4001,
			   -radius_1, 0.0, 1,
				0.0, 0.0, 901,
				0.0, 180.0, 4001,
				radius_1, 0.0, -1
	endif
else
	if shell_thk>0 then
		prism_	7, a,
				-(radius_1-shell_thk), 0, 79,
				-radius_1, 0, 79,
				0, 0, 979,
				radius_1,-shell_angle, 4079,
				(radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle), 79,
				0, 0, 979,
				-(radius_1-shell_thk), shell_angle, 4079

		for i=1 to 2
			lin_	-radius_1,0,0, -radius_1,0,a
			lin_	-radius_1+shell_thk,0,0, -radius_1+shell_thk,0,a
			rotz -shell_angle
		next i
		del 2
	else
		extrude	7, 0,0,a, 16+32,
				-(radius_1-shell_thk), 0, 1,
				-radius_1, 0, 1,
				0, 0, 901,
				radius_1,-shell_angle, 4001,
				(radius_1-shell_thk)*cos(180-shell_angle),(radius_1-shell_thk)*sin(shell_angle), 1,
				0, 0, 901,
				-(radius_1-shell_thk), shell_angle, 4001
	endif
endif
del top

