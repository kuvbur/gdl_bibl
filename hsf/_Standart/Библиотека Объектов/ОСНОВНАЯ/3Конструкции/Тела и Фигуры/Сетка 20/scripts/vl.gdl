
call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------
values "stStyle" stStyles
values "iStyle"  1, 2

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	parameters stStyle = stStyle
endif

if iStyle = 1 then
	hideparameter "zCoordFrames"
else
	hideparameter "yCoordFrames"
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------
values "gs_detlevel_3D" st_detail3D
values "gs_detlevel_3D_m" 2, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D_m" then
	gs_detlevel_3D = st_detail3D[1]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = st_detail3D[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
else
	gs_detlevel_3D_m = 2	! Detailed
	if gs_detlevel_3D = st_detail3D[2] then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------
values "a" range [2*thickEdge, ]
values "b" range (0, ]
values "ZZYZX" range (0, ]

if GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then
	if iStyle = 1 then
		b = lastSizes[1]
		ZZYZX = lastSizes[2]
	else
		b = lastSizes[2]
		ZZYZX = lastSizes[1]
	endif
	parameters b = b, ZZYZX = ZZYZX
endif

if GLOB_MODPAR_NAME = "B" then
	parameters lastSizes[iStyle] = b
endif
if GLOB_MODPAR_NAME = "ZZYZX" then
	parameters lastSizes[3-iStyle] = ZZYZX
endif

if GLOB_MODPAR_NAME = "iNumFramesA" or GLOB_MODPAR_NAME = "iNumFramesB" or GLOB_MODPAR_NAME = "mVert" then
	parameters xCoordEdges[1] = 0
	parameters yCoordEdges[1] = 0
	parameters zCoordEdges[1] = 0
	parameters xCoordEdges[2] = A
	parameters yCoordEdges[2] = B
	parameters zCoordEdges[2] = ZZYZX
endif

if GLOB_MODPAR_NAME = "A" or GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then
	parameters xCoordEdges[2] = A + xCoordEdges[1]
	xCoordEdges[2] = a + xCoordEdges[1]
endif

if GLOB_MODPAR_NAME = "B"  or GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then
	parameters yCoordEdges[2] = B + yCoordEdges[1]
	yCoordEdges[2] = b + yCoordEdges[1]
endif

if GLOB_MODPAR_NAME = "ZZYZX"  or GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then
	parameters zCoordEdges[2] = ZZYZX + zCoordEdges[1]
	zCoordEdges[2] = ZZYZX + zCoordEdges[1]
endif

if GLOB_MODPAR_NAME = "A" or GLOB_MODPAR_NAME = "iNumFramesA" then

	!!!!!!Croossing Coordinates
	u = (a-thickEdge)/(iNumFramesA-1)

	for i = 1 to iNumFramesA
		parameters xCoordFrames[i] = thickEdge/2+(i-1)*u
	next i
	for i = iNumFramesA + 1 to 20
		parameters xCoordFrames[i] = 0
	next i

endif

if iStyle = 1 then
	if GLOB_MODPAR_NAME = "B" or GLOB_MODPAR_NAME = "iNumFramesB" or GLOB_MODPAR_NAME = "mVert" then

		!!!!!!Croossing Coordinates
		w = (b-thickEdge)/(iNumFramesB-1)

		for i = 1 to iNumFramesB
			parameters yCoordFrames[i] = thickEdge/2+(i-1)*w
			parameters zCoordFrames[i] = thickEdge/2+(i-1)*w
		next i
		for i = iNumFramesB + 1 to 20
			parameters yCoordFrames[i] = 0
			parameters zCoordFrames[i] = 0
		next i

	endif
else
	if GLOB_MODPAR_NAME = "ZZYZX" or GLOB_MODPAR_NAME = "iNumFramesB" or GLOB_MODPAR_NAME = "mVert" then

		!!!!!!Croossing Coordinates
		w = (ZZYZX-thickEdge)/(iNumFramesB-1)

		for i = 1 to iNumFramesB
			parameters yCoordFrames[i] = thickEdge/2+(i-1)*w
			parameters zCoordFrames[i] = thickEdge/2+(i-1)*w
		next i

		for i = iNumFramesB + 1 to 20
			parameters yCoordFrames[i] = 0
			parameters zCoordFrames[i] = 0
		next i

	endif
endif


if iNumFramesA < 2 then iNumFramesA = 2
if iNumFramesB < 2 then iNumFramesB = 2
if mVert < 2 then mVert = 2
parameters iNumFramesA = iNumFramesA, iNumFramesB = iNumFramesB, mVert = mVert

values "thickEdge" thickFrame, custom

if gs_cont_pen = 0 then parameters gs_cont_pen = 2
if gs_fill_pen = 0 then parameters gs_fill_pen = 91

if iStyle = 1 then hideparameter "mVert", "zCoordFrames"
if iStyle = 2 then hideparameter "iNumFramesB", "yCoordFrames"

if iStyle = 1 then
	mVert = iNumFramesB
	parameters mVert = mVert
else
	iNumFramesB = mVert
	parameters iNumFramesB = iNumFramesB
endif

if GLOB_MODPAR_NAME = "yCoordFrames" then
	zCoordFrames = yCoordFrames
	parameters zCoordFrames = zCoordFrames
else
	yCoordFrames = zCoordFrames
	parameters yCoordFrames = yCoordFrames
endif

! -----------------------------------------------------------------------------
! Show / Hide 2D Fills
! -----------------------------------------------------------------------------

if not(gs_bFills) then	! Hide
	hideparameter	"gs_fill_type",
					"gs_fill_pen",
					"gs_back_pen",
					"gs_fillGlass",
					"gs_penGlass_fg",
					"gs_penGlass_bg"

endif

! -----------------------------------------------------------------------------
! mullion parameter checking
! -----------------------------------------------------------------------------

! --- Horizontal Mullions ---

if GLOB_MODPAR_NAME = "xCoordFrames" then
	for ij=2 to iNumFramesA-1
		if xCoordFrames[ij] < xCoordFrames[ij-1] + minmdst then
			parameters xCoordFrames[ij] = xCoordFrames[ij-1] + minmdst
		endif
		if xCoordFrames[ij] > xCoordFrames[ij+1] - minmdst then
			parameters xCoordFrames[ij] = xCoordFrames[ij+1] - minmdst
		endif
	next ij
endif

! --- Vertical Mullions ---

if GLOB_MODPAR_NAME = "yCoordFrames" then
	for kj=2 to iNumFramesB-1
		if yCoordFrames[kj] < yCoordFrames[kj-1] + minmdst then
			parameters yCoordFrames[kj] = yCoordFrames[kj-1] + minmdst
		endif
		if yCoordFrames[kj] > yCoordFrames[kj+1] - minmdst then
			parameters yCoordFrames[kj] = yCoordFrames[kj+1] - minmdst
		endif
	next kj
endif

! -----------------------------------------------------------------------------
! UI
! -----------------------------------------------------------------------------

values "gs_ui_current_page" 1

