
! Tube

pen gs_cont_pen

unID	= 1
hLineID = 1000
hLineAxisID = 1100

hotspot2 0,0,unID : unID=unID+1

!if edit_mode=`Coordinate` then
	hotspot2 0,0,unID,dist_x,1+256 : unID=unID+1
	hotspot2 -1,0,unID,dist_x,3 : unID=unID+1
	hotspot2 dist_x,0,unID,dist_x,2 : unID=unID+1
!else
!	hotspot2 0,0,unID : unID=unID+1
!	hotspot2 dist_x,0,unID : unID=unID+1
!endif

!!----------------!!
!! Radius Editing !!
!!----------------!!

hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
hotspot2 0,-1,unID,radius_1,3 : unID=unID+1
hotspot2 0,radius_1,unID,radius_1,2 : unID=unID+1

hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
hotspot2 0,1,unID,radius_1,3 : unID=unID+1
hotspot2 0,-radius_1,unID,radius_1,2 : unID=unID+1

if abs(angle_1-90)<eps then
	hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
	hotspot2 -1,0,unID,radius_1,3 : unID=unID+1
	hotspot2 radius_1,0,unID,radius_1,2 : unID=unID+1

	hotspot2 0,0,unID,radius_1,1+128 : unID=unID+1
	hotspot2 1,0,unID,radius_1,3 : unID=unID+1
	hotspot2 -radius_1,0,unID,radius_1,2 : unID=unID+1
else
	hotspot2 dist_x,0,unID,radius_1,1+128 : unID=unID+1
	hotspot2 dist_x,-1,unID,radius_1,3 : unID=unID+1
	hotspot2 dist_x,radius_1,unID,radius_1,2 : unID=unID+1

	hotspot2 dist_x,0,unID,radius_1,1+128 : unID=unID+1
	hotspot2 dist_x,1,unID,radius_1,3 : unID=unID+1
	hotspot2 dist_x,-radius_1,unID,radius_1,2 : unID=unID+1
endif

!!-------------------!!
!! Thickness Editing !!
!!-------------------!!

hotspot2 0,radius_1,unID,tube_thk,1+128 : unID=unID+1
hotspot2 0,radius_1+1,unID,tube_thk,3 : unID=unID+1
hotspot2 0,radius_1-tube_thk,unID,tube_thk,2 : unID=unID+1

hotspot2 0,-radius_1,unID,tube_thk,1+128 : unID=unID+1
hotspot2 0,-radius_1-1,unID,tube_thk,3 : unID=unID+1
hotspot2 0,-radius_1+tube_thk,unID,tube_thk,2 : unID=unID+1

if abs(angle_1-90)<eps then
	hotspot2 radius_1,0,unID,tube_thk,1+128 : unID=unID+1
	hotspot2 radius_1+1,0,unID,tube_thk,3 : unID=unID+1
	hotspot2 radius_1-tube_thk,0,unID,tube_thk,2 : unID=unID+1

	hotspot2 -radius_1,0,unID,tube_thk,1+128 : unID=unID+1
	hotspot2 -radius_1-1,0,unID,tube_thk,3 : unID=unID+1
	hotspot2 -radius_1+tube_thk,0,unID,tube_thk,2 : unID=unID+1
else
	hotspot2 dist_x,radius_1,unID,tube_thk,1+128 : unID=unID+1
	hotspot2 dist_x,radius_1+1,unID,tube_thk,3 : unID=unID+1
	hotspot2 dist_x,radius_1-tube_thk,unID,tube_thk,2 : unID=unID+1

	hotspot2 dist_x,-radius_1,unID,tube_thk,1+128 : unID=unID+1
	hotspot2 dist_x,-radius_1-1,unID,tube_thk,3 : unID=unID+1
	hotspot2 dist_x,-radius_1+tube_thk,unID,tube_thk,2 : unID=unID+1
endif


!!----------!!
!! Geometry !!
!!----------!!


fill gs_fill_type

if abs(angle_1)<eps then
	poly2_b	5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, radius_1, 1,
			0, -radius_1, 1,
			dist_x, -radius_1, 1,
			dist_x, radius_1, 1,
			0, radius_1, -1

	hotline2 0, radius_1,0, -radius_1, hLineID : hLineID = hLineID + 1
	hotline2 0, -radius_1,dist_x, -radius_1, hLineID : hLineID = hLineID + 1
	hotline2 dist_x, -radius_1,dist_x, radius_1, hLineID : hLineID = hLineID + 1
	hotline2 dist_x, radius_1,0, radius_1, hLineID : hLineID = hLineID + 1

	line_type gs_DashedLine
	line2 0,radius_1-tube_thk,dist_x,radius_1-tube_thk
	line2 0,-radius_1+tube_thk,dist_x,-radius_1+tube_thk
	hotline2 0,radius_1-tube_thk,dist_x,radius_1-tube_thk, hLineID : hLineID = hLineID + 1
	hotline2 0,-radius_1+tube_thk,dist_x,-radius_1+tube_thk, hLineID : hLineID = hLineID + 1

	if gs_axis then
		pen gs_axis_pen
		line_type gs_axis_line
		line2 0,0,dist_x,0
		hotline2 0,0,dist_x,0, hLineAxisID
	endif
	pen gs_cont_pen

else
	hLineID = hLineID + 6
	if abs(angle_1-90)<eps then
		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				radius_1, 360, 4001

		hotarc2 0,0,radius_1,0,180, hLineID : hLineID = hLineID + 1
		hotarc2 0,0,radius_1,180,360, hLineID : hLineID = hLineID + 1

		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				radius_1-tube_thk, 360, 4001

		hotarc2 0,0,radius_1-tube_thk,0,180, hLineID : hLineID = hLineID + 1
		hotarc2 0,0,radius_1-tube_thk,180,360, hLineID : hLineID = hLineID + 1

	else
		hLineID = hLineID + 4
		mul2 cos(90-angle_1),1
		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				radius_1, 360, 4001

		hotarc2 0,0,radius_1,0,180, hLineID : hLineID = hLineID + 1
		hotarc2 0,0,radius_1,180,360, hLineID : hLineID = hLineID + 1

		del 1

		poly2_b	5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, -radius_1, 1,
				dist_x, -radius_1, 0,
				dist_x, radius_1, 1,
				0, radius_1, 0,
				0, -radius_1, -1

		hotline2 0, -radius_1,dist_x, -radius_1, hLineID : hLineID = hLineID + 1
		hotline2 dist_x, -radius_1,dist_x, radius_1, hLineID : hLineID = hLineID + 1
		hotline2 dist_x, radius_1,0, radius_1, hLineID : hLineID = hLineID + 1
		hotline2 0, radius_1,0, -radius_1, hLineID : hLineID = hLineID + 1

		line_type gs_DashedLine
		line2 0,radius_1-tube_thk,dist_x,radius_1-tube_thk
		line2 0,-radius_1+tube_thk,dist_x,-radius_1+tube_thk
		hotline2 0,radius_1-tube_thk,dist_x,radius_1-tube_thk, hLineID : hLineID = hLineID + 1
		hotline2 0,-radius_1+tube_thk,dist_x,-radius_1+tube_thk, hLineID : hLineID = hLineID + 1

		if gs_axis then
			pen gs_axis_pen
			line_type gs_axis_line
			line2 0,0,dist_x,0
			hotline2 0,0,dist_x,0, hLineAxisID
		endif
		pen gs_cont_pen

		mul2 cos(90-angle_1),1
		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				radius_1-tube_thk, 360, 4001

		hotarc2 0,0,radius_1-tube_thk,0,180, hLineID : hLineID = hLineID + 1
		hotarc2 0,0,radius_1-tube_thk,180,360, hLineID : hLineID = hLineID + 1

		del 1

		add2 dist_x,0
		mul2 cos(90-angle_1),1
		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				radius_1, 360, 4001

		hotarc2 0,0,radius_1,0,180, hLineID : hLineID = hLineID + 1
		hotarc2 0,0,radius_1,180,360, hLineID : hLineID = hLineID + 1

		poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 901,
				radius_1-tube_thk, 360, 4001

		hotarc2 0,0,radius_1-tube_thk,0,180, hLineID : hLineID = hLineID + 1
		hotarc2 0,0,radius_1-tube_thk,180,360, hLineID : hLineID = hLineID + 1

		del 2
	endif
endif
