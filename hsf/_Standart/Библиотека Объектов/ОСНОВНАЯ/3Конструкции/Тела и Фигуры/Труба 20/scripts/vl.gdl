
call "FM_types" parameters all


values "radius_1" range [0, )
values "tube_thk" range [0, radius_1)
values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_SIMPLE, `Простой`,
								DLEV3D_OFF, `Откл.`
values{2} "iTubeEditMode" 	EDITMODE_LENGTH, `Угол+Длина`,
							EDITMODE_COORDINATE, `Координаты`

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19
if gs_axis_pen=0 then parameters gs_axis_pen=64

If gs_resol<3 then parameters gs_resol=3
if gs_detlevel_3D_m = DLEV3D_SIMPLE then lock "gs_resol"


if angle_1<0 then parameters angle_1=0
if angle_1>90 then parameters angle_1=90

if tube_thk>=radius_1 then parameters tube_thk=radius_1
if tube_thk<=0 then parameters tube_thk=0

IF iTubeEditMode = EDITMODE_COORDINATE THEN

	PARAMETERS tube_length=sqr(dist_x^2+zzyzx^2)

	IF abs(dist_x)<eps then
		PARAMETERS angle_1=90
	ELSE
		IF abs(zzyzx)<eps then
			PARAMETERS angle_1=0
		ELSE
			PARAMETERS angle_1=atn(zzyzx/dist_x)
		ENDIF
	ENDIF
	lock "tube_length","angle_1"
ELSE
	IF GLOB_MODPAR_NAME = "tube_length" 	THEN PARAMETERS dist_x=tube_length*cos(angle_1)
	IF GLOB_MODPAR_NAME = "dist_x" 			THEN PARAMETERS tube_length=dist_x/cos(angle_1)

	IF GLOB_MODPAR_NAME = "angle_1" 		THEN PARAMETERS dist_x=tube_length*cos(angle_1)

	PARAMETERS zzyzx=tube_length*sin(angle_1)

	lock "zzyzx"
ENDIF

lock "a","b"

if gs_axis=0 then hideparameter "gs_axis_line"
