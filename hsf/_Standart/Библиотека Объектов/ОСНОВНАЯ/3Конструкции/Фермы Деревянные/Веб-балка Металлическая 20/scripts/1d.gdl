
EPS = 0.00001

! ==============================================================================
! Definitions
! ==============================================================================

!ac_symb_display_option
	DISPLAY_PROJECTED			= 1
	DISPLAY_OVERHEAD_PROJ		= 2
	DISPLAY_SYMBOLIC			= 3
	DISPLAY_OUTLINES			= 4
	DISPLAY_OVERHEAD_ALL		= 5

!ac_symb_show_projection_to
	PROJECTION_FLOOR_PLAN_RANGE	= 1
	PROJECTION_DISPLAY_LIMIT	= 2
	PROJECTION_ENTIRE_ELEMENT	= 3

!================================================================================
! Values
!================================================================================
dim stDisplayOptions[5]
	stDisplayOptions[1]		= `Сечение и Низ`
	stDisplayOptions[2]		= `Сечение и Верх`
	stDisplayOptions[3]		= `Символический Вид`
	stDisplayOptions[4]		= `Только Контуры`
	stDisplayOptions[5]		= `Вся Проекция`

dim stProjectionTypes[3]
	stProjectionTypes[1]	= `Согласно Диапазону Плана Этажа`
	stProjectionTypes[2]	= `Согласно Абсолютному Диапазону`
	stProjectionTypes[3]	= `Весь Элемент`

dim st3dDetLevelValues[3]
	st3dDetLevelValues[1] = `Детальный`
	st3dDetLevelValues[2] = `Простой`
	st3dDetLevelValues[3] = `Откл.`

dim strEndTrimmingValues[2]
	strEndTrimmingValues[1] = `Каркасный`
	strEndTrimmingValues[2] = `Сплошная Стенка`

dim strWebBracingValues[3]
	strWebBracingValues[1] = `Обычной`
	strWebBracingValues[2] = `Ступенчатое`
	strWebBracingValues[3] = `Двойная Плотность`


PS8   = 1
PS9N  = 2
PS10N = 3
PS12N = 4
PS14N = 5
PS16N = 6
WS200 = 7
WS250 = 8
WS300 = 9
WS400 = 10

FRAMED    = 1
SOLID_WEB = 2

NORMAL         = 1
STAGGERED      = 2
DOUBLE_DENSITY = 3

DETLEVEL_3D_DETAILED = 1
DETLEVEL_3D_SIMPLE	 = 2
DETLEVEL_3D_OFF 	 = 0


! --- Joist Values --------------------------------------------------------------

dim enabledJoistDefaults[10]
	enabledJoistDefaults[ 1] = 1
	enabledJoistDefaults[ 2] = 2
	enabledJoistDefaults[ 3] = 3
	enabledJoistDefaults[ 4] = 4
	enabledJoistDefaults[ 5] = 5
	enabledJoistDefaults[ 6] = 6
	enabledJoistDefaults[ 7] = 7
	enabledJoistDefaults[ 8] = 8
	enabledJoistDefaults[ 9] = 9
	enabledJoistDefaults[10] = 10

dim strJoistTypeValues[]
dim iJoistTypeValues[]

j = 1
for i = 1 to vardim1(enabledJoistDefaults)
	if enabledJoistDefaults[i] > 0 then
		strJoistTypeValues[j] = joistNames[i]
		iJoistTypeValues[j] = i
		j = j + 1
	endif
next i


! --- Timber Chord Values -------------------------------------------------------

sizeFormat = "%~.2mm"

dim enabledChordDefaults[4]
	enabledChordDefaults[1] = 1
	enabledChordDefaults[2] = 2
	enabledChordDefaults[3] = 3
	enabledChordDefaults[4] = 4

dim chordProfileDefaults[4]
	chordProfileDefaults[1] = STR(sizeFormat, chordProfileSizes[1][1]) + strSeparatorCharacter + STR(sizeFormat, chordProfileSizes[1][2])
	chordProfileDefaults[2] = STR(sizeFormat, chordProfileSizes[2][1]) + strSeparatorCharacter + STR(sizeFormat, chordProfileSizes[2][2])
	chordProfileDefaults[3] = STR(sizeFormat, chordProfileSizes[3][1]) + strSeparatorCharacter + STR(sizeFormat, chordProfileSizes[3][2])
	chordProfileDefaults[4] = STR(sizeFormat, chordProfileSizes[4][1]) + strSeparatorCharacter + STR(sizeFormat, chordProfileSizes[4][2])



!================================================================================
! Calculations
!================================================================================


! --- Metal Profile Geometry ----------------------------------------------------

! --- Width of half V ---
if iJoistType = PS16N or iJoistType = WS400 then
	w_metal = 0.384
else
	w_metal = 0.300
endif

! --- Width of full V ---
if iWebBracing = NORMAL 		then unit_width = w_metal*2
if iWebBracing = STAGGERED 		then unit_width = w_metal*2
if iWebBracing = DOUBLE_DENSITY then unit_width = (w_metal+0.100)*2

! --- Fillet Radius ---
rf1 = 0.090
rf2 = 0.025

! --- Height
d_chord = joistHeight - chordHeight*2

! --- Chord Contact Part ---
h_connection       = 0.035
chamfer_connection = 0.030
w_connection       = 0.060
h_chamfer          = 0.005

! --- Thicknesses ---
w_profile = 0.010



! --- Additional Pillars --------------------------------------------------------

n_pillars = 0
if A > 4.000 then n_pillars = 1
if A > 8.000 then n_pillars = 2


! --- Metal Profile Array -------------------------------------------------------

n_units = INT(A/unit_width)

max_A = 12.000

! --- Solid Web --------------------------------------------------------------

! --- End Trims Lengths are symmetric

min_trim = 0.100

! --- Limits

ASolidLowerLimit = unit_width+min_trim*2
ASolidUpperLimit = 12.000

if iEndTrimming = SOLID_WEB then

	end_trim_length = (A - n_units*unit_width - n_pillars*chordHeight)/2
	if end_trim_length < min_trim then n_units = n_units-1
	if n_units < 1 then n_units = 1
	end_trim_length = (A - n_units*unit_width - n_pillars*chordHeight)/2

endif


! --- Framed -----------------------------------------------------------------

d_columns = 0.100		! this parameter needed here - causes parameter script crash if initialized only for framed type

if iEndTrimming = FRAMED then
	! --- End Trims Lengths are symmetric

	sum_end_trim_length = (A - n_units*unit_width - n_pillars*chordHeight)
	if sum_end_trim_length < chordHeight then n_units = n_units-1
	if n_units < 1 then n_units = 1
	sum_end_trim_length = (A - n_units*unit_width - n_pillars*chordHeight)


	! --- Symmetric Arrange Of Columns On Left And Right Ends

	! --- Left Side ---

	end_trim_length_left = sum_end_trim_length/2

	if end_trim_length_left < chordHeight then
		end_trim_length_left = chordHeight
		n_columns_left = 1
	else
		n_columns_left = INT((end_trim_length_left-chordHeight)/d_columns) + 1
	endif

	end_trim_length_left = (n_columns_left-1)*d_columns + chordHeight


	! --- Right Side ---

	end_trim_length_right = sum_end_trim_length/2
	n_columns_right = n_columns_left

endif

