
! ==============================================================================
! Floor Plan View
! ==============================================================================
! ----- Display with floorplan cutplane ------------------------------------
gs_cutplane_height	= GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1]
gs_top_range_height	= GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[2]
if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE then
	gs_bottom_range_height = GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[3]	! To Floor Plan Range
else
	gs_bottom_range_height = GLOB_CUTPLANES_INFO[4]	! Absolute Display Limit
endif

doCutplane = (ac_symb_display_option = DISPLAY_PROJECTED | ac_symb_display_option = DISPLAY_OVERHEAD_PROJ)

! ==============================================================================
! Hotspots
! ==============================================================================
hLineID = 1000
unID = 1

hotline2 0,0,a,0, hLineID : hLineID = hLineID + 1
hotline2 a,0,a,b, hLineID : hLineID = hLineID + 1
hotline2 a,b,0,b, hLineID : hLineID = hLineID + 1
hotline2 0,b,0,0, hLineID : hLineID = hLineID + 1

hotspot2 0, b/2, unID, a, 1+256 : unID=unID+1
hotspot2 -1, b/2, unID, a, 3 : unID=unID+1
hotspot2 a, b/2, unID, a, 2 : unID=unID+1

add2 a/2,0
m=zzyzx
r=(a*a)/(8*m)+(m/2)
aa=sqr((r-crh)^2-(r-m+crh)^2)

if ced=0 then
	dd=sqr((r-(crh/2))^2-(r-m+(crh/2))^2)
else
	dd=a/2
endif

dx=dd/wn
for t=-wn+1 to wn-1
	hotspot2 dx*t, b/2, unID : unID=unID+1
next t
del 1

call "Resize_A_B_ZZYZX" parameters all

! ==============================================================================
! Axis
! ==============================================================================
if ac_symb_display_option <> DISPLAY_OUTLINES & gs_axis = 1 then
	line_type gs_axis_line
	pen gs_axis_line_pen

	line2 0,b/2,a,b/2
	hotline2 0,b/2,a,b/2, hLineID : hLineID = hLineID + 1
else
	hLineID = hLineID + 1
endif

! ==============================================================================
! Floor Plan View
! ==============================================================================
pen gs_cont_pen
line_type gs_cont_line

if ac_symb_display_option = DISPLAY_SYMBOLIC then

	rect2 0, 0, a, b

	fill gs_fill_type
	poly2_b 5, 2+64, gs_fill_pen, gs_back_pen,
			0, 0, 0,
			0, b, 1,
			a, b, 1,
			a, 0, 1,
			0, 0, -1
				
	if std then
		add2 0,b
		project2{3} 4, 270, 2, 4+8, 0, 0, 0, 0, parameters gs_detlevel_3D_m = DETLEVEL3D_DETAILED
		del 1

		hotspot2 0,b,unID,zzyzx,1+128:unID=unID+1
		hotspot2 0,-1,unID,zzyzx,3:unID=unID+1
		hotspot2 0,zzyzx+b,unID,zzyzx,2:unID=unID+1

		hotspot2 a,b,unID,zzyzx,1+128:unID=unID+1
		hotspot2 a,-1,unID,zzyzx,3:unID=unID+1
		hotspot2 a,zzyzx+b,unID,zzyzx,2:unID=unID+1
	endif
else

	! ----- View Edges ---------------------------------------------------------
	pen gs_cont_pen
	line_type gs_cont_line

	if ac_symb_display_option = DISPLAY_OVERHEAD_ALL then
		pen gs_overhead_pen
		line_type gs_overhead_linetype
	endif

	project2{3} 3,270,2 + 32 * (ac_symb_display_option = DISPLAY_OVERHEAD_ALL | not(gs_use_3D_view_attributes)), 8,
		parameters	gs_detlevel_3D_m		= DETLEVEL3D_DETAILED,
					gs_2D_projection		= doCutplane,
					gs_2D_projection_type	= 1,
					gs_cutplane_height		= gs_cutplane_height,
					gs_top_range_height		= gs_top_range_height,
					gs_bottom_range_height	= gs_bottom_range_height


	if ac_symb_display_option <> DISPLAY_OVERHEAD_ALL then
		! ----- View Polygons --------------------------------------------------
		pen gs_fill_pen
		fill gs_fill_type

		project2{3} 3,270,3+32+1024+2048, 4,
						gs_back_pen,
			parameters	gs_detlevel_3D_m		= DETLEVEL3D_DETAILED,
						gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 2,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif


	if ac_symb_display_option = DISPLAY_OVERHEAD_PROJ then

		! ----- Overhead View -----------------------------------------------------
		pen gs_overhead_pen
		line_type gs_overhead_linetype

		project2{3} 3,270,2+32, 2+8,
			parameters	gs_detlevel_3D_m		= DETLEVEL3D_DETAILED,
						gs_2D_projection		= doCutplane,
						gs_2D_projection_type	= 3,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif

	if doCutplane then

		! ----- Cut Lines ---------------------------------------------------------
		pen gs_cut_pen
		line_type gs_cut_linetype
		line_property 2

		project2{3} 3,270,2+16+32+256+4096, 2,
			parameters	gs_detlevel_3D_m		= DETLEVEL3D_DETAILED,
						gs_2D_projection		= 1,
						gs_2D_projection_type	= 4,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height


		! ----- Cut Polygons ------------------------------------------------------
		pen gs_cut_fill_pen
		fill gs_cut_fill_type

		project2{3} 3,270,3+32+512, 1,
						gs_cut_back_pen,
			parameters	gs_detlevel_3D_m		= DETLEVEL3D_DETAILED,
						gs_2D_projection		= 1,
						gs_2D_projection_type	= 5,
						gs_cutplane_height		= gs_cutplane_height,
						gs_top_range_height		= gs_top_range_height,
						gs_bottom_range_height	= gs_bottom_range_height
	endif
endif

