
if gs_detlevel_3D_m = DETLEVEL3D_OFF then end

! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

unID=1

GOSUB 1000

addy b

pen gs_pen_3d
resol gs_resol

addx a/2
rotx 90
material gs_truss_mat
m=zzyzx

if ced=0 then
	r=(a*a)/(8*m)+(m/2)
	aa=sqr((r-crh)^2-(r-m+crh)^2)
	 prism_       8,      b,
		-a/2, 0,      13,
		0, -r+m,    913,
		a/2,0,   3013,
		-a/2, 0,      -1 ,
		-aa, crh,      13,
		0, -r+m,    913,
		aa,crh,   3013,
		-aa, crh,      -1
else
	r=(a*a)/(8*(m-crh))+((m-crh)/2)
	aa=sqr((r-crh)^2-(r-m+crh)^2)
	 prism_       10,      b,
		-a/2, 0,      15,
		-a/2,crh,13,
		0, -r+m,    913,
		a/2,crh,   3013,
		a/2,0,15,
		-a/2, 0,      -1 ,
		-aa, crh,      13,
		0, -r+m,    913,
		aa,crh,   3013,
		-aa, crh,      -1

endif


if gs_detlevel_3D_m = DETLEVEL3D_DETAILED then

	dd=sqr((r-(crh/2))^2-(r-m+(crh/2))^2)
	dx=dd/wn


	for t=-wn+1 to wn-1

		x1=(t*dx)-(crh/2)
		x2=(t*dx)+(crh/2)
		y1=sqr((r-crh)^2-x1^2)-(r-m)
		y2=sqr((r-crh)^2-x2^2)-(r-m)

		yz1=sqr(r^2-(x1)^2)-(r-m)
		yz2=sqr(r^2-(x2)^2)-(r-m)


		prism_ 7,b,
			x1,crh,15,
			x1,y1-0.0001,15,
			x1,y1,13,
			0, -r+m,    913,
			x2,y2,3015,
			x2,crh,15,
			x1,crh,-1

		if stp then
			material mat2
			addz b
			prism_ 6,0.005,
				x1,y1-crh,15,
				x1,yz1,13,
				0, -r+m,    913,
				x2,yz2,3015,
				x2,y1-crh,15,
				x1,y1-crh,15

			addz -b-0.005

			prism_ 6,0.005,
				x1,y1-crh,15,
				x1,yz1,13,
				0, -r+m,    913,
				x2,yz2,3015,
				x2,y1-crh,15,
				x1,y1-crh,15
			del 2
		endif

		material gs_truss_mat
	next t

	for z=1 to 2
		for t=-wn+1 to wn-1


			xle=(t*dx)+(crh/2)
			xri=((t+1)*dx)-(crh/2)
			xmi=((t+0.5)*dx)

			ymi=sqr((r-crh)^2-xmi^2)-(r-m)

			alf=atn((ymi-crh)/((dx-crh)/2))

			xx=(crh/2)/sin(alf)
			yy=(crh/2)/cos(alf)



			yle=crh+yy
			con=-xle*tan(alf)+yle-m+r

			aa=1+tan(alf)*tan(alf)
			bb=2*tan(alf)*con
			cc=con*con-(r-crh)*(r-crh)

			xxx=(-bb+sqr(bb*bb-4*aa*cc))/2/aa
			yyy=(xxx-xle)*tan(alf)+yle
			if t=wn-1 then goto 20


			prism_ 7,b,
				xle+xx,crh,15,
				xmi,ymi-yy,15,
				xmi,ymi,15,
				xxx,yyy,15,
				xle,crh+yy,15,
				xle,crh,15,
				xle+xx,crh,-1

			if stp then
				material mat2
				addz b
				prism_ 5,0.005,
					xle-2*crh,0,15,
					xle-2*crh,2.5*crh,15,
					xle+crh,2.5*crh,15,
					xle+crh,0,15,
					xle-2*crh,0,15


				ym1=sqr(r^2-(xmi-crh)^2)-(r-m)
				ym2=sqr(r^2-(xmi+crh)^2)-(r-m)

				prism_ 6,0.005,
					xmi-1*crh,ymi-yy,15,
					xmi-1*crh,ym1,13,
					0, -r+m,    913,
					xmi+1*crh,ym2,3015,
					xmi+1*crh,ymi-yy,15,
					xmi-1*crh,ymi-yy,15

				addz -b-0.005
				prism_ 5,0.005,
					xle-2*crh,0,15,
					xle-2*crh,2.5*crh,15,
					xle+crh,2.5*crh,15,
					xle+crh,0,15,
					xle-2*crh,0,15

				prism_ 6,0.005,
					xmi-1*crh,ymi-yy,15,
					xmi-1*crh,ym1,13,
					0, -r+m,    913,
					xmi+1*crh,ym2,3015,
					xmi+1*crh,ymi-yy,15,
					xmi-1*crh,ymi-yy,15

				del 2
			endif


			material gs_truss_mat
			goto 30

			20: ye=crh
			xe=xle+xx
			cn=-xe*tan(alf)+ye-m+r

			aaa=1+tan(alf)*tan(alf)
			bbb=2*tan(alf)*cn
			ccc=cn*cn-(r-crh)*(r-crh)

			xxxx=(-bbb+sqr(bbb*bbb-4*aaa*ccc))/2/aaa
			yyyy=(xxxx-xe)*tan(alf)+ye

			prism_ 6,b,
				xle+xx,crh,15,
				xxxx,yyyy,15,
				xxx,yyy,15,
				xle,crh+yy,15,
				xle,crh,15,
				xle+xx,crh,-1


			ccz=cn*cn-(r)*(r)
			xxz=(-bbb+sqr(bbb*bbb-4*aaa*ccz))/2/aaa
			yyz=(xxz-xe)*tan(alf)+ye

			cz=con*con-(r)*(r)

			xz=(-bb+sqr(bb*bb-4*aa*cz))/2/aa
			yz=(xz-xle)*tan(alf)+yle

			if stp then
				addz b
				material mat2
				prism_ 6,0.005,
					xxxx-crh*cos(alf),yyyy-crh*sin(alf),15,
					xxz,yyz,13,
					0, -r+m,    913,
					xz,yz,3015,
					xxxx-crh*cos(alf)-crh*sin(alf),yyyy-crh*sin(alf)+crh*cos(alf),15,
					xxxx-crh*cos(alf),yyyy-crh*sin(alf),15


				dd=crh*3
				ale=asn ((dd/2)/r)
				aff=acs((a/2)/r)
				xj=r*cos(aff+2*ale)
				yj=r*sin(aff+2*ale)-r+m

				if ced then
					prism_ 6,0.005,
						a/2,0,15,
						a/2,crh,15,
						0, -r+m,    913,
						xj,yj,3015,
						a/2-dd,0,15,
						a/2,0,15
				else
					prism_ 5,0.005,
						a/2,0,15,
						0, -r+m,    913,
						xj,yj,3015,
						a/2-dd,0,15,
						a/2,0,15
				endif

				addz -b-0.005

				prism_ 6,0.005,
					xxxx-crh*cos(alf),yyyy-crh*sin(alf),15,
					xxz,yyz,13,
					0, -r+m,    913,
					xz,yz,3015,
					xxxx-crh*cos(alf)-crh*sin(alf),yyyy-crh*sin(alf)+crh*cos(alf),15,
					xxxx-crh*cos(alf),yyyy-crh*sin(alf),15

				if ced then
					prism_ 6,0.005,
						a/2,0,15,
						a/2,crh,15,
						0, -r+m,    913,
						xj,yj,3015,
						a/2-dd,0,15,
						a/2,0,15
				else
					prism_ 5,0.005,
						a/2,0,15,
						0, -r+m,    913,
						xj,yj,3015,
						a/2-dd,0,15,
						a/2,0,15
				endif

				del 2
			endif

			material gs_truss_mat
		30: next t
		mulx -1
	next z

	del top

endif

for i = 1 to nProjectionCut
	cutend
next i

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ==============================================================================

1000:

!!---- Hotspot Editing
hotspot a/2,b/2,0,unID,zzyzx,1+128 : unID=unID+1
hotspot a/2,b/2,-1,unID,zzyzx,3 : unID=unID+1
hotspot a/2,b/2,zzyzx,unID,zzyzx,2 : unID=unID+1

hotspot 0,b/2,0,unID,a,1+256 : unID=unID+1
hotspot -1,b/2,0,unID,a,3 : unID=unID+1
hotspot a,b/2,0,unID,a,2 : unID=unID+1

hotspot 0,0,0,unID,a,1+256 : unID=unID+1
hotspot -1,0,0,unID,a,3 : unID=unID+1
hotspot a,0,0,unID,a,2 : unID=unID+1

hotspot 0,b,0,unID,a,1+256 : unID=unID+1
hotspot -1,b,0,unID,a,3 : unID=unID+1
hotspot a,b,0,unID,a,2 : unID=unID+1

hotspot 0,0,0,unID,b,1+256 : unID=unID+1
hotspot 0,-1,0,unID,b,3 : unID=unID+1
hotspot 0,b,0,unID,b,2 : unID=unID+1

hotspot a,0,0,unID,b,1+256 : unID=unID+1
hotspot a,-1,0,unID,b,3 : unID=unID+1
hotspot a,b,0,unID,b,2 : unID=unID+1

return
