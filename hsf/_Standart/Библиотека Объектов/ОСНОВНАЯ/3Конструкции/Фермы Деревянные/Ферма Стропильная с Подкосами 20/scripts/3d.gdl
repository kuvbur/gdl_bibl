
if gs_detlevel_3D_m = DETLEVEL3D_OFF then end

! ==============================================================================
! 2D Display Options
! ==============================================================================

!gs_2D_projection_type:
!	0 - Normal 3D
!	1 - View Edges
!	2 - View Polygons
!	3 - Overhead Edges
!	4 - Cut Lines
!	5 - Cut Polygons
!	6 - Bottom Range Cut Lines
!	7 - Top Range Cut Lines

nProjectionCut = 0
if gs_2D_projection then
	if AC_symb_show_projection_to = PROJECTION_FLOOR_PLAN_RANGE | (AC_symb_show_projection_to = PROJECTION_DISPLAY_LIMIT & gs_bottom_range_height < gs_cutplane_height) then
		addz gs_bottom_range_height
		cutplane{2} 180, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if gs_2D_projection_type <> 6 then
		addz gs_cutplane_height
		cutplane{2} 180 * (gs_2D_projection_type = 3), 1
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
	if AC_symb_show_projection_to <> 3 then
		addz gs_top_range_height
		cutplane{2} 0, 2
		nProjectionCut = nProjectionCut + 1
		del 1
	endif
endif

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

_eaveOverhang = crh / sin(ang)
_A = tieBeamLength + _eaveOverhang*2

unID=1
gosub 1000

pen gs_pen_3d
addy crd
!!!------------------------------------------------


dx = (_A/2 - (crh/2)/sin(ang)) / n
dy11 = zzyzx - (dx*tan(ang))

dy1= dy11  - (crh)/cos(ang) + (crh/2)*tan(ang)
dx1 = (dx*(n+1)) - (crh/2)/sin(ang)
dx12 = (crh/2)/sin(ang)
bet = atn(dy1 / ((dx*(n+1)) - crh/2 - (crh/2)/sin(ang)))
dy = dy1 - (dx - (crh/2))*tan(bet)
!!!------------------------------------------------
dr1 = crh/sin(ang - bet)
dx21 = dr1*cos(ang) + (crh)/sin(ang)
dy21 = dr1*sin(ang)
!!!------------------------------------------------
dy31 = crh/cos(ang)
!!!------------------------------------------------
dy41 = crh/cos(bet)
dx41 = (dy41/2)/tan(bet)
!!!------------------------------------------------
dr2 = crh/cos(90 - ang - bet)
dy51 = dr2*sin(ang)
dx51 = dr2*cos(ang)
!!!------------------------------------------------


material gs_truss_mat
addx -_eaveOverhang
rotx 90

for u=1 to 2
	! Rafter
	if ced then
		put	_eaveOverhang,	_eaveOverhang*tan(ang),	15,
			_A/2,			zzyzx,					15,
			_A/2,			zzyzx - dy31,			15,
			_eaveOverhang,	0,						15
	else
		put	0,				0,				15,
			_A/2,			zzyzx,			15,
			_A/2,			zzyzx - dy31,	15,
			_eaveOverhang,	0,				15
	endif

	prism_ nsp/3, crd,
		get(nsp)

	! Lower Beam
	put	_eaveOverhang,	0,			15,
		dx21,			dy21,		15,
		_A/2,			dy + dy41,	15,
		_A/2,			dy,			15

	prism_ nsp/3, crd,
		get(nsp)

	addx _A
	mulx -1
next u
del 4


if (gs_detlevel_3D_m = DETLEVEL3D_DETAILED) then

	for u=1 to 2

		material gs_truss_mat
		put	(crh/2)/sin(ang)+(n-1)*dx + crh/2,			dy1,			15,
			(crh/2)/sin(ang)+(n-1)*dx + crh/2 + dx51,	dy1 + dy51,		15,
			_A/2,										dy + dy41,		15,
			_A/2 - dx41,								dy  +(dy41/2),	15

		prism_ nsp/3, crd,
			get(nsp)

		for i=1 to n-1

			! Web beams
			!!------------------------------------------
			dy61=((i)*dx-((crh/2)/sin(ang)))*tan(bet)+crh/cos(bet)
			dy62=((i)*dx-((crh/2)/sin(ang)))*tan(ang)
			!!------------------------------------------
			dy71=((i+1)*dx-((crh/2)/sin(ang)))*tan(bet)+crh/cos(bet)-(crh/2)*tan(bet)
			dy72=((i)*dx-((crh/2)/sin(ang)))*tan(ang)+(crh/2)*tan(ang)
			dxr=dx-crh
			dyr=dy72-dy71
			gams1=90-atn(dyr/dxr)
			drs=sqr((dxr*dxr)+(dyr*dyr))
			gams2=asn(crh/drs)

			if gams1-gams2 < 90 then
				gam=90-(gams1-gams2)
			else
				gam=- (gams1-gams2-90)
			endif

			dr3=crh/sin(gam+ang)
			dr4=crh/sin(gam+bet)
			!!-------------------------------------------
			material gs_truss_mat
			prism_ 4,crd,
				dx12+dx*i+crh/2,dy61+(crh/2)*tan(bet),15,
				dx12+dx*i-crh/2,dy61-(crh/2)*tan(bet),15,
				dx12+dx*i-crh/2,dy62-(crh/2)*tan(ang),15,
				dx12+dx*i+crh/2,dy62+(crh/2)*tan(ang),15

			if i < n-1 then
				prism_ 4,crd,
					dx12+dx*i+crh/2,dy72,15,
					dx12+dx*i+crh/2+dr3*cos(ang),dy62+(crh/2)*tan(ang)+dr3*sin(ang),15,
					dx12+dx*(i+1)-crh/2,dy71,15,
					dx12+dx*(i+1)-crh/2-dr4*cos(bet),dy71-dr4*sin(bet),15
			endif

			! Steel plates
			if stp then
				material mat2
				addz -0.005
				prism_ 4,0.005,
					 dx12+dx*i-crh/2,dy62-(crh/2)*tan(ang),15,
					 dx12+dx*i+crh/2+dr3*cos(ang),dy62-(crh/2)*tan(ang),15,
					 dx12+dx*i+crh/2+dr3*cos(ang),dy62+(crh/2)*tan(ang)+dr3*sin(ang)+crh/cos(ang),15,
					 dx12+dx*i-crh/2,dy62-(crh/2)*tan(ang)+crh/cos(ang),15
				prism_ 4,0.005,
					 dx12+dx*i-crh/2,dy61-(crh/2)*tan(bet)-crh/cos(bet),15,
					 dx12+dx*i-crh/2,dy61-(crh/2)*tan(bet)+crh/cos(bet) ,15,
					 dx12+dx*i+crh/2,dy61-(crh/2)*tan(bet)+crh/cos(bet) ,15,
					 dx12+dx*i+crh/2,dy61+(crh/2)*tan(bet)-crh/cos(bet),15
				!!------------------------------------------------------------------------------
				addz 0.005+crd
				prism_ 4,0.005,
					 dx12+dx*i-crh/2,dy62-(crh/2)*tan(ang),15,
					 dx12+dx*i+crh/2+dr3*cos(ang),dy62-(crh/2)*tan(ang),15,
					 dx12+dx*i+crh/2+dr3*cos(ang),dy62+(crh/2)*tan(ang)+dr3*sin(ang)+crh/cos(ang),15,
					 dx12+dx*i-crh/2,dy62-(crh/2)*tan(ang)+crh/cos(ang),15
				prism_ 4,0.005,
					 dx12+dx*i-crh/2,dy61-(crh/2)*tan(bet)-crh/cos(bet),15,
					 dx12+dx*i-crh/2,dy61-(crh/2)*tan(bet)+crh/cos(bet) ,15,
					 dx12+dx*i+crh/2,dy61-(crh/2)*tan(bet)+crh/cos(bet) ,15,
					 dx12+dx*i+crh/2,dy61+(crh/2)*tan(bet)-crh/cos(bet),15
				del 2
			endif

		next i
		addx _A
		mulx -1
	next u

	del 4

	! Steel plates
	if stp then
		addz -0.005
		prism_ 5,0.005,
			_A/2,zzyzx,15,
			_A/2+2*crh*cos(ang),zzyzx-2*crh*sin(ang),15,
			_A/2+2*crh*cos(ang)-crh*sin(ang),zzyzx-2*crh*sin(ang)-crh*cos(ang),15,
			_A/2-2*crh*cos(ang)+crh*sin(ang),zzyzx-2*crh*sin(ang)-crh*cos(ang),15,
			_A/2-2*crh*cos(ang),zzyzx-2*crh*sin(ang),15
		prism_ 4,0.005,
			_A/2-dx41,dy-dx41*tan(bet),15,
			_A/2-dx41,dy+dx41*tan(bet)+dy41,15,
			_A/2+dx41,dy+dx41*tan(bet)+dy41,15,
			_A/2+dx41,dy-dx41*tan(bet),15
		prism_ 4,0.005,
			crh/sin(ang),0,15,
			crh/sin(ang),(crh/sin(ang))*tan(ang),15,
			dx21,dx21*tan(ang),15,
			dx21,(dx21-crh/sin(ang))*tan(bet),15
		prism_ 4,0.005,
			_A-crh/sin(ang),0,15,
			_A-crh/sin(ang),(crh/sin(ang))*tan(ang),15,
			_A-dx21,dx21*tan(ang),15,
			_A-dx21,(dx21-crh/sin(ang))*tan(bet),15
		!!--------------------------------------------------------------
		  addz 0.005+crd
		prism_ 5,0.005,
			_A/2,zzyzx,15,
			_A/2+2*crh*cos(ang),zzyzx-2*crh*sin(ang),15,
			_A/2+2*crh*cos(ang)-crh*sin(ang),zzyzx-2*crh*sin(ang)-crh*cos(ang),15,
			_A/2-2*crh*cos(ang)+crh*sin(ang),zzyzx-2*crh*sin(ang)-crh*cos(ang),15,
			_A/2-2*crh*cos(ang),zzyzx-2*crh*sin(ang),15
		prism_ 4,0.005,
			_A/2-dx41,dy-dx41*tan(bet),15,
			_A/2-dx41,dy+dx41*tan(bet)+dy41,15,
			_A/2+dx41,dy+dx41*tan(bet)+dy41,15,
			_A/2+dx41,dy-dx41*tan(bet),15
		prism_ 4,0.005,
			crh/sin(ang),0,15,
			crh/sin(ang),(crh/sin(ang))*tan(ang),15,
			dx21,dx21*tan(ang),15,
			dx21,(dx21-crh/sin(ang))*tan(bet),15
		prism_ 4,0.005,
			_A-crh/sin(ang),0,15,
			_A-crh/sin(ang),(crh/sin(ang))*tan(ang),15,
			_A-dx21,dx21*tan(ang),15,
			_A-dx21,(dx21-crh/sin(ang))*tan(bet),15
		!!--------------------------------------------------------------
		  del 2

	endif

endif

del 2

for i = 1 to nProjectionCut
	cutend
next i

del top

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ==============================================================================

1000:
	!!---- Hotspot Editing
	hotspot 0,				B/2, 0,unID, tieBeamLength, 1+256	: unID=unID+1
	hotspot -1,				B/2, 0,unID, tieBeamLength, 3		: unID=unID+1
	hotspot tieBeamLength,	B/2, 0,unID, tieBeamLength, 2		: unID=unID+1

	hotspot -eaveOverhang,	0,	0, unID, B, 1+256	: unID=unID+1
	hotspot -eaveOverhang,	-1,	0, unID, B, 3		: unID=unID+1
	hotspot -eaveOverhang,	B,	0, unID, B, 2		: unID=unID+1

	hotspot tieBeamLength + eaveOverhang,	0,	0, unID, B, 1+256	: unID=unID+1
	hotspot tieBeamLength + eaveOverhang,	-1,	0, unID, B, 3		: unID=unID+1
	hotspot tieBeamLength + eaveOverhang,	B,	0, unID, B, 2		: unID=unID+1

	if iDimensionSetMode = DIM_WIDTH_HEIGHT then
		hotspot tieBeamLength/2, B/2, 0,		unID, zzyzx, 1+128	:unID=unID+1
		hotspot tieBeamLength/2, B/2, -1,		unID, zzyzx, 3		:unID=unID+1
		hotspot tieBeamLength/2, B/2, zzyzx,	unID, zzyzx, 2		:unID=unID+1
	endif

	if iDimensionSetMode = DIM_WIDTH_ANGLE | iDimensionSetMode = DIM_WIDTH_PROPORTION then
		hotspot tieBeamLength/2, B/2, zzyzx, unID : unID=unID+1
	endif

	if iDimensionSetMode = DIM_WIDTH_ANGLE then
		rafterLength = sqr((tieBeamLength/2)^2 + (zzyzx - crh/cos(ang))^2)

		hotspot rafterLength/2,				0,	0,							unID, ang, 4+128	: unID=unID+1
		hotspot 0,							0,	0,							unID, ang, 6 		: unID=unID+1
		hotspot (rafterLength/2)*cos(ang),	0,	(rafterLength/2)*sin(ang),	unID, ang, 5		: unID=unID+1
		hotspot 0,							-1,	0,							unID, ang, 7		: unID=unID+1

		hotspot rafterLength/2,				B,		0,							unID, ang, 4+128	: unID=unID+1
		hotspot 0,							B,		0,							unID, ang, 6 		: unID=unID+1
		hotspot (rafterLength/2)*cos(ang),	B,		(rafterLength/2)*sin(ang),	unID, ang, 5		: unID=unID+1
		hotspot 0,							B - 1,	0,							unID, ang, 7		: unID=unID+1

		hotspot tieBeamLength - rafterLength/2,				0,	0,							unID, ang, 4+128	: unID=unID+1
		hotspot tieBeamLength,								0,	0,							unID, ang, 6 		: unID=unID+1
		hotspot tieBeamLength - (rafterLength/2)*cos(ang),	0,	(rafterLength/2)*sin(ang),	unID, ang, 5		: unID=unID+1
		hotspot tieBeamLength,								1,	0,							unID, ang, 7		: unID=unID+1

		hotspot tieBeamLength - rafterLength/2,				B,		0,							unID, ang, 4+128	: unID=unID+1
		hotspot tieBeamLength,								B,		0,							unID, ang, 6 		: unID=unID+1
		hotspot tieBeamLength - (rafterLength/2)*cos(ang),	B,		(rafterLength/2)*sin(ang),	unID, ang, 5		: unID=unID+1
		hotspot tieBeamLength,								B + 1,	0,							unID, ang, 7		: unID=unID+1
	endif

return

