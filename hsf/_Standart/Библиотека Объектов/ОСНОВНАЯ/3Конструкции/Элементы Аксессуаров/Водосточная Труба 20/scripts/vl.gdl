

! -------------------------------------------------------------------------------
! Hide all paremeterlist
! -------------------------------------------------------------------------------
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

! -------------------------------------------------------------------------------
! 3D Detail Level
! -------------------------------------------------------------------------------
values "gs_detlevel_3D" `Детальный`, `Простой`, `Откл.`
values "gs_detlevel_3D_m" DETLEVEL_3D_OFF, DETLEVEL_3D_SIMPLE, DETLEVEL_3D_DETAILED

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DETLEVEL_3D_DETAILED
	if gs_detlevel_3D = `Простой`					then gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE
	if gs_detlevel_3D = `Откл.`						then gs_detlevel_3D_m = DETLEVEL_3D_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = `Детальный`
	if gs_detlevel_3D_m = DETLEVEL_3D_SIMPLE		then gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = DETLEVEL_3D_OFF			then gs_detlevel_3D = `Откл.`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! -------------------------------------------------------------------------------
! Hanger Positioning
! -------------------------------------------------------------------------------
values{2} "gs_gutter_ds_h_positioning",	HANGER_CUSTOM,	stHangerPositioning[1],
										HANGER_UNIFORM, stHangerPositioning[2]

! -------------------------------------------------------------------------------
! Downspout Profile Type
! -------------------------------------------------------------------------------
if LibraryLangCode = "USA" then

	values "gs_gutter_ds_profile" stGutterDsProfiles

	values "gs_gutter_ds_profile_num" PROFILE_ROUND, PROFILE_RECTANGULAR, PROFILE_CORRUGATED

	if gs_gutter_ds_profile = stGutterDsProfiles[1] OR gs_gutter_ds_profile = stGutterDsProfiles[2] OR gs_gutter_ds_profile = stGutterDsProfiles[11] THEN
		parameters gs_gutter_ds_profile_num = PROFILE_ROUND
	endif

	if gs_gutter_ds_profile = stGutterDsProfiles[3] OR gs_gutter_ds_profile = stGutterDsProfiles[4] OR gs_gutter_ds_profile = stGutterDsProfiles[5] OR gs_gutter_ds_profile = stGutterDsProfiles[6] OR gs_gutter_ds_profile = stGutterDsProfiles[12] THEN
		parameters gs_gutter_ds_profile_num = PROFILE_RECTANGULAR
	endif

	if gs_gutter_ds_profile = stGutterDsProfiles[7] OR gs_gutter_ds_profile = stGutterDsProfiles[7] OR gs_gutter_ds_profile = stGutterDsProfiles[8] OR gs_gutter_ds_profile = stGutterDsProfiles[9] OR gs_gutter_ds_profile = stGutterDsProfiles[13] THEN
		parameters gs_gutter_ds_profile_num = PROFILE_CORRUGATED
	endif

else

	values "gs_gutter_ds_profile" `Круглый`,`Прямоугольный`,`Закругленный`
	values "gs_gutter_ds_profile_num" PROFILE_ROUND, PROFILE_RECTANGULAR, PROFILE_CORRUGATED

	if GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
		gs_gutter_ds_profile_num = PROFILE_RECTANGULAR
		if gs_gutter_ds_profile = `Круглый`				then gs_gutter_ds_profile_num = PROFILE_ROUND
		if gs_gutter_ds_profile = `Закругленный`			then gs_gutter_ds_profile_num = PROFILE_CORRUGATED
		parameters gs_gutter_ds_profile_num = gs_gutter_ds_profile_num
	else
		gs_gutter_ds_profile = `Прямоугольный`
		if gs_gutter_ds_profile_num = PROFILE_ROUND			then gs_gutter_ds_profile = `Круглый`
		if gs_gutter_ds_profile_num = PROFILE_CORRUGATED	then gs_gutter_ds_profile = `Закругленный`
		parameters gs_gutter_ds_profile = gs_gutter_ds_profile
	endif

endif

