


ui_dialog `Параметры Углового Желоба`

ui_current_page gs_ui_current_page

TABID_ROOT			= -1
TABID_DIMENSIONS	= 1
TABID_HANGERS		= 2
TABID_DOWNSPOUT		= 3
TABID_REP_2D		= 4
TABID_REP_3D		= 5
TABID_LISTING		= 6

pageStartY 			= 0
textElevInfield		= 3
textElevBoolean		= 2
textElevMaterial	= 2
infieldHeight		= 19
booleanHeight		= 20
attributeHeight		= 25
outfieldHeight		= 15
fieldToPic			= 3
fieldToField		= 5

ui_page TABID_DIMENSIONS, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "dimensionsPage"

ui_page TABID_HANGERS, TABID_ROOT, `Кронштейны`, "uiTab_GutterHangers_flat(1)"
gosub "hangersPage"

ui_page TABID_DOWNSPOUT, TABID_ROOT, `Отверстие Воронки`, "uiTab_DownspoutDownspout_flat(1)"
gosub "downspoutPage"

ui_page TABID_REP_2D, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
gosub "representationPage_2d"

ui_page TABID_REP_3D, TABID_ROOT, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
gosub "representationPage_3d"

ui_page TABID_LISTING, TABID_ROOT, `Параметры Спецификаций`, "uiTab_Descriptions_flat(1)"
gosub "listingPage"

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================

! -----------------------------------------------------------------------------
"dimensionsPage":
! -----------------------------------------------------------------------------

	x0		= 0
	y0		= pageStartY
	dy		= 22

	endX = 444
	endY = 265
	midX = 222

	outX	= 110
	outXs	= 60
	inX		= 90
	inXs	= 60
	inXl	= 120

	sepY	= 85

	picX	= 93
	picY	= 173

	posX 	= x0
	posY	= y0

	ui_outfield `Тип Профиля`, posX, posY + textElevInfield, outX, outfieldHeight

	ui_infield{3}	"iGutterProfile", (endX - inXl) / 2, posY, inXl, 37,
			2, "ui_gutter_profiles_matrix(1)", 9, 1,
			33, 33, 33, 33,
			1,  stGutterProfiles[4], GUTTER_HALFROUND_25,
			2,  stGutterProfiles[5], GUTTER_HALFROUND_3,
			3,  stGutterProfiles[6], GUTTER_KSTYLE_5BY35,
			4,  stGutterProfiles[7], GUTTER_KSTYLE_6BY4,
			5,  stGutterProfiles[8], GUTTER_USTLYE_5BY35,
			6,  stGutterProfiles[9], GUTTER_USTYLE_6BY4,
			7,  stGutterProfiles[1], GUTTER_HALFROUND,
			8,  stGutterProfiles[2], GUTTER_KSTYLE,
			9,  stGutterProfiles[3], GUTTER_USTYLE

	if gs_gutter_p_num = GUTTER_HALFROUND then
		
		ui_pict "ui_gutter_profile_halfround(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		ui_infield "gs_gutter_p_r", endX - inX, posY + 13, inX, infieldHeight ui_tooltip `Радиус Профиля`
		ui_infield "gs_gutter_p_d", endX - inX, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Диаметр Профиля`

	endif

	if gs_gutter_p_num = GUTTER_KSTYLE then

		ui_pict "ui_gutter_profile_kstyle(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		ui_infield "gs_gutter_p_h", endX - inX, posY + 13, inX, infieldHeight ui_tooltip `Высота Профиля`
		ui_infield "gs_gutter_p_w", endX - inX, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Ширина Профиля`

	endif

	if gs_gutter_p_num = GUTTER_USTYLE then

		ui_pict "ui_gutter_profile_ustyle(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		ui_infield "gs_gutter_p_h", endX - inX, posY + 13, inX, infieldHeight ui_tooltip `Высота Профиля`
		ui_infield "gs_gutter_p_w", endX - inX, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Ширина Профиля`

	endif

	posY = posY + 40

	ui_outfield `Угол Соединения`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield "gs_gutter_mitress_ang", (endX - inXl) / 2 + inXl - inX, posY, inX, infieldHeight

	inXl = 170
	posY = posY + dy
	ui_infield{3}	"gs_gutter_guard", posX, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Решетка`, 0,
			"",`Решетка`, 1

	ui_separator 0, sepY, endX, sepY
	ui_separator midX, sepY + 4, midX, endY

	posY = sepY + 5
	ui_style 0, 1
	ui_infield{3}	"gs_gutter_hanger1", posX, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Кронштейны`, 0,
			"",`Кронштейны`, 1

	ui_pict_button ui_function, "", "uiFunctionHangers_flat(1)", midX - 5 - 36, posY-1, 36, 22, -TABID_HANGERS ! Set GLOB_UI_BUTTON_ID


	ui_infield{3}	"gs_gutter_hanger2", midX + 10, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Кронштейны`, 0,
			"",`Кронштейны`, 1
	ui_pict_button ui_function, "", "uiFunctionHangers_flat(1)", endX - 1 - 36, posY-1, 36, 22, -TABID_HANGERS ! Set GLOB_UI_BUTTON_ID


	posY = posY + dy + 2
	ui_infield{3}	"gs_gutter_downspout1", posX, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Отверстие Воронки`, 0,
			"",`Отверстие Воронки`, 1

	ui_pict_button ui_function, "", "uiFunctionDownSpoutHole_flat(1)", midX - 5 - 36, posY-1, 36, 22, -TABID_DOWNSPOUT ! Set GLOB_UI_BUTTON_ID

	ui_infield{3}	"gs_gutter_downspout2", midX + 10, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Отверстие Воронки`, 0,
			"",`Отверстие Воронки`, 1

	ui_style 0, 0

	ui_pict_button ui_function, "", "uiFunctionDownSpoutHole_flat(1)", endX - 1 - 36, posY-1, 36, 22, -TABID_DOWNSPOUT ! Set GLOB_UI_BUTTON_ID

	posY = posY + dy + 2

	ui_outfield `Уклон`, posX, posY + textElevInfield, 85, outfieldHeight
	ui_outfield `Уклон`, midX + 10, posY + textElevInfield, 81, outfieldHeight

	ui_infield{3} "iSlopeMode1", midX - 5 - inXs, posY, inXs, infieldHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", `°`, SLOPEMODE_1,
				"", `%`, SLOPEMODE_2,
				"", `на 12'`, SLOPEMODE_4,
				"", `на 12"`, SLOPEMODE_3


	ui_infield{3} "iSlopeMode2", endX - inXs, posY, inXs, infieldHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", `°`, SLOPEMODE_1,
				"", `%`, SLOPEMODE_2,
				"", `на 12'`, SLOPEMODE_4,
				"", `на 12"`, SLOPEMODE_3

	inX = 65
	if iSlopeMode1 = SLOPEMODE_1 then
		ui_infield "gs_gutter_slope1_in_deg", midX - 5 - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (°)`
	endif
	if iSlopeMode1 = SLOPEMODE_2 then
		ui_infield "gs_gutter_slope1_in_perc", midX - 5 - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (%)`
	endif
	if iSlopeMode1 = SLOPEMODE_4 then
		ui_infield "gs_gutter_slope1_in_12_f", midX - 5 - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (на 12')`
	endif
	if iSlopeMode1 = SLOPEMODE_3 then
		ui_infield "gs_gutter_slope1_in_12_i", midX - 5 - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (на 12")`
	endif

	if iSlopeMode2 = SLOPEMODE_1 then
		ui_infield "gs_gutter_slope2_in_deg", endX - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (°)`
	endif
	if iSlopeMode2 = SLOPEMODE_2 then
		ui_infield "gs_gutter_slope2_in_perc", endX - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (%)`
	endif
	if iSlopeMode2 = SLOPEMODE_4 then
		ui_infield "gs_gutter_slope2_in_12_f", endX - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (на 12')`
	endif
	if iSlopeMode2 = SLOPEMODE_3 then
		ui_infield "gs_gutter_slope2_in_12_i", endX - inXs - fieldToField - inX, posY, inX, infieldHeight ui_tooltip `Уклон Желоба (на 12")`
	endif
	
	pic1x = midX - 5 - 119
	pic1y = 160
	inXl = 110
	ui_pict 1, pic1x, pic1y, 119, 78, 1
	ui_infield "gs_gutter_lgth1", pic1x - fieldToPic - inX, pic1y + 18, inX, infieldHeight ui_tooltip `Длина Соединения 1`
	ui_infield "gs_gutter_endcap1_ang", pic1x - fieldToPic - inXs, pic1y + 48, inXs, infieldHeight ui_tooltip `Угол Заглушки 1`
	ui_infield{3}	"gs_gutter_endcap1", pic1x + 12, pic1y + 78 + fieldToPic, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Заглушка`, 0,
			"",`Заглушка`, 1

	pic2x = midX + 7
	pic2y = 160

	ui_pict 2, pic2x, pic2y, 122, 78, 1
	ui_infield "gs_gutter_lgth2", pic2x + 122 + fieldToPic, pic2y + 18, inX, infieldHeight ui_tooltip `Длина Соединения 2`
	ui_infield "gs_gutter_endcap2_ang", pic2x + 122 + fieldToPic, pic2y + 48, inXs, infieldHeight ui_tooltip `Угол Заглушки 2`
	ui_infield{3}	"gs_gutter_endcap2", pic2x + 122 - 22, pic1y + 78 + fieldToPic, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Заглушка`, 0,
			"",`Заглушка`, 1

return

! -----------------------------------------------------------------------------
"hangersPage":
! -----------------------------------------------------------------------------
	x0		= 0
	y0		= pageStartY
	dy		= 22

	endX = 444
	endY = 265
	midX = 222

	outX	= 110
	outXs	= 60
	outXl	= 150
	inX		= 90
	inXs	= 30
	inXl	= 120

	sepY	= 85

	picX	= 108
	picY	= 172

	posX 	= x0
	posY	= y0

	ui_outfield `Тип Кронштейна`, posX, posY + textElevInfield, outX, outfieldHeight, 4-4*(gs_gutter_hanger1 | gs_gutter_hanger2)

		ui_infield{3}	"iHangerType", 120, posY, inXl, 80,
			2, "ui_gutter_hangers_matrix(2)", 6, 1,
			107, 75, 107, 75,
			2, `Хомут`, HANGERTYPE_STRAP,
			3, `Гвоздь и Муфта`, HANGERTYPE_SPIKE,
			4, `Планка 1`, HANGERTYPE_BAR1,
			6, `Планка 2`, HANGERTYPE_BAR2

	ui_pict "ui_gutter_hanger_profile(1)", endX - inX - fieldToPic - 69, 12, 69, 59, 1
	ui_infield "gs_gutter_hanger_v", endX - inX, 29, inX, infieldHeight ui_tooltip `Высота Лобовой Доски Крыши`

	ui_separator 0, sepY, endX, sepY
	ui_separator midX, sepY + 4, midX, endY

	posY = sepY + 8
	inXl = 200
	ui_style 0, 1
	ui_infield{3}	"gs_gutter_hanger1", posX, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Кронштейны`, 0,
			"",`Кронштейны`, 1


	ui_infield{3}	"gs_gutter_hanger2", midX + 10, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Кронштейны`, 0,
			"",`Кронштейны`, 1

	ui_style 0, 0
	posY = posY + dy
	ui_outfield `Расположение`, posX, posY + textElevInfield, outX, outfieldHeight, 4*not(gs_gutter_hanger1)

	ui_infield{3}	"gs_gutter_hanger_positioning1", midX - 10 - inX, posY, inX, infieldHeight,
					8, "", 0, 0,
					0, 0, 0, 0,
					"", stHangerPositioning[1], HANGER_CUSTOM,
					"", stHangerPositioning[2], HANGER_UNIFORM


	ui_outfield `Расположение`, midX + 10, posY + textElevInfield, outX, outfieldHeight, 4*not(gs_gutter_hanger2)

	ui_infield{3}	"gs_gutter_hanger_positioning2", endX - inX, posY, inX, infieldHeight,
					8, "", 0, 0,
					0, 0, 0, 0,
					"", stHangerPositioning[1], HANGER_CUSTOM,
					"", stHangerPositioning[2], HANGER_UNIFORM

	posY = posY + dy

	pic1x = midX - 200 - 15
	pic1y = 171

	if gs_gutter_hanger_positioning1 = HANGER_CUSTOM then
		ui_infield "gs_gutter_hanger_pos1", posX, posY, infieldHeight, infieldHeight
		ui_outfield `Расстояние до Угла Желоба`, posX + 23, posY + textElevInfield, midX - posX - 24, outfieldHeight, 4*not(gs_gutter_hanger1)

		ui_pict 3, pic1x, pic1y, 203, 71, 1
	endif
	if gs_gutter_hanger_positioning1 = HANGER_UNIFORM then
		ui_pict 5, pic1x, pic1y, 203, 71, 1
		ui_infield "gs_gutter_hanger_dist1", pic1x + 16, pic1y + 71 + fieldToPic, inX, infieldHeight ui_tooltip `Расстояние Между Кронштейнами 1`
		ui_infield "gs_gutter_hanger_off1", pic1x + 120, pic1y + 71 + fieldToPic, inX, infieldHeight ui_tooltip `Смещение Кронштейна 1`
	endif

	pic2x = midX + 15
	pic2y = 171

	if gs_gutter_hanger_positioning2 = HANGER_CUSTOM then
		ui_infield "gs_gutter_hanger_pos2", midX + 10, posY, infieldHeight, infieldHeight
		ui_outfield `Расстояние до Угла Желоба`, midX + 33, posY + textElevInfield, endX - midX - 34, outfieldHeight, 4*not(gs_gutter_hanger2)

		ui_pict 4, pic2x, pic2y, 203, 71, 1
	endif
	if gs_gutter_hanger_positioning2 = HANGER_UNIFORM then
		ui_pict 6, pic2x, pic2y, 203, 71, 1
		ui_infield "gs_gutter_hanger_dist2", pic2x + 117, pic2y + 71 + fieldToPic, inX, infieldHeight ui_tooltip `Расстояние Между Кронштейнами 2`
		ui_infield "gs_gutter_hanger_off2", pic2x - 4, pic2y + 71 + fieldToPic, inX, infieldHeight ui_tooltip `Смещение Кронштейна 2`
	endif

return

! -----------------------------------------------------------------------------
"downspoutPage":
! -----------------------------------------------------------------------------

	x0		= 5
	y0		= pageStartY
	dy		= 25

	endX = 444
	endY = 265
	midX = 222

	outX	= 110
	outXs	= 60
	inX		= 90
	inXs	= 60
	inXl	= 120

	sepY	= 71

	picX	= 110
	picY	= 130

	posX 	= x0
	posY	= y0

	ui_outfield `Тип Профиля`, posX, posY + textElevInfield, outX, outfieldHeight

	ui_infield{3} "iDownspoutProfile", (endX - inXl) / 2, posY, inXl, 37,
			2, "ui_downspout_profile_matrix(1)", 13, 1,
			33, 33, 33, 33,
			1,  stDownspoutProfiles[4],	 DOWNSPOUT_ROUND_3,
			2,  stDownspoutProfiles[5],	 DOWNSPOUT_ROUND_4,
			3,  stDownspoutProfiles[6],	 DOWNSPOUT_RECTANGULAR_2BY3,
			4,  stDownspoutProfiles[7],	 DOWNSPOUT_RECTANGULAR_3BY2,
			5,  stDownspoutProfiles[8],	 DOWNSPOUT_RECTANGULAR_3BY4,
			6,  stDownspoutProfiles[9],	 DOWNSPOUT_RECTANGULAR_4BY3,
			7,	stDownspoutProfiles[10], DOWNSPOUT_CORRUGATED_2BY3,
			8,	stDownspoutProfiles[11], DOWNSPOUT_CORRUGATED_3BY2,
			9,	stDownspoutProfiles[12], DOWNSPOUT_CORRUGATED_3BY4,
			10,	stDownspoutProfiles[13], DOWNSPOUT_CORRUGATED_4BY3,
			11,	stDownspoutProfiles[1],  DOWNSPOUT_ROUND,
			12,	stDownspoutProfiles[2],  DOWNSPOUT_RECTANGULAR,
			13,	stDownspoutProfiles[3],  DOWNSPOUT_CORRUGATED

	if gs_gutter_g_ds_profile_num = DOWNSPOUT_ROUND then

		ui_pict "ui_downspout_profile_round(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		ui_infield "gs_gutter_dd", endX - inX-1, posY + 13, inX, infieldHeight ui_tooltip `Радиус Профиля`
		ui_infield "gs_gutter_dd2", endX - inX-1, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Диаметр Профиля`

	endif

	if gs_gutter_g_ds_profile_num = DOWNSPOUT_RECTANGULAR | gs_gutter_g_ds_profile_num = DOWNSPOUT_CORRUGATED then

		if gs_gutter_g_ds_profile_num = DOWNSPOUT_RECTANGULAR then
			ui_pict "ui_downspout_profile_rectangle(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		else
			ui_pict "ui_downspout_profile_corrugated(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		endif
		ui_infield "gs_gutter_bb2", endX - inX-1, posY + 13, inX, infieldHeight ui_tooltip `Длина Профиля`
		ui_infield "gs_gutter_aa2", endX - inX-1, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Ширина Профиля`
	endif

	ui_separator 0, sepY, endX, sepY
	ui_separator midX, sepY + 4, midX, endY
	posY = sepY + 6
	inXl	= 200

	ui_style 0, 1
	ui_infield{3}	"gs_gutter_downspout1", posX, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Отверстие Воронки`, 0,
			"",`Отверстие Воронки`, 1

	ui_infield{3}	"gs_gutter_downspout2", midX + 10, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Отверстие Воронки`, 0,
			"",`Отверстие Воронки`, 1

	ui_style 0, 0
	posY = posY + dy
	ui_infield{3}	"gs_gutter_strainer1", posX, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Фильтр`, 0,
			"",`Фильтр`, 1

	ui_infield{3}	"gs_gutter_strainer2", midX + 10, posY, inXl, booleanHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"",`Фильтр`, 0,
			"",`Фильтр`, 1

	pic1x = midX - 203 - 15
	ui_pict 7, pic1x, picY, 203, 63, 1
	ui_infield "gs_gutter_downspout_x_pos1", midX - fieldToField - inX, picY + 63 + fieldToPic, inX, infieldHeight ui_tooltip `Расположение по X Отверстия Воронки 1`

	pic2x = midX + 15
	ui_pict 8, pic2x, picY, 203, 63, 1
	ui_infield "gs_gutter_downspout_x_pos2", midX + fieldToField, picY + 63 + fieldToPic, inX, infieldHeight ui_tooltip `Расположение по X Отверстия Воронки 2`

return

! -----------------------------------------------------------------------------
"representationPage_2d":
! -----------------------------------------------------------------------------

	x0 = 5
	y0 = pageStartY + 5
	dy = 25
	
	outX	= 110
	outXs	= 60
	inX		= 90
	inXs	= 30
	inXl	= 130
	
	sepX = 222
	endX = 444
	
	posX = x0
	posY = y0 + 5

	ui_style 0, 1
	ui_outfield `2D-отображение`, posX, posY, outX, outfieldHeight
	ui_style 0, 0

	posY = posY + dy
	ui_outfield `Тип Линии`, posX, posY + textElevMaterial, outXs, outfieldHeight
	ui_infield "gs_line_type", sepX - 5 - inXl, posY, inXl, attributeHeight
	
	posY = posY + dy + 5
	ui_outfield `Перо Контура`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield "gs_cont_pen", sepX - 5 - inXs, posY, inXs, infieldHeight
	
	posY = posY + dy
	ui_outfield `Тип Штриховки`, posX, posY + textElevMaterial, outXs, outfieldHeight
	ui_infield "gs_fill_type", sepX - 5 - inXl, posY, inXl, attributeHeight
	
	posY = posY + dy + 5
	ui_outfield `Перо Штриховки`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield "gs_fill_pen", sepX - 5 - inXs, posY, inXs, infieldHeight
	
	posY = posY + dy
	ui_outfield `Перо Фона Штриховки`, posX, posY + textElevInfield, outX + 30, outfieldHeight
	ui_infield "gs_back_pen", sepX - 5 - inXs, posY, inXs, infieldHeight

return

! -----------------------------------------------------------------------------
"representationPage_3d":
! -----------------------------------------------------------------------------

	x0 = 5
	y0 = pageStartY + 5
	dy = 25
	
	outX	= 110
	outXs	= 60
	inX		= 90
	inXs	= 30
	inXl	= 130
	
	sepX = 222
	endX = 444
	
	posX = x0
	posY = y0 + 5

	ui_style 0, 1
	ui_outfield `3D-отображение`, posX, posY, outX, outfieldHeight
	ui_style 0, 0
	
	posY = posY + dy
	ui_outfield `3D-детализация`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield{3}	"gs_detlevel_3D_m", sepX - 5 - inX, posY, inX, infieldHeight,
					8, "", 0, 0,
					0, 0, 0, 0,
					"", `Детальный`, DLEV3D_DETAILED,
					"", `Простой`, DLEV3D_SIMPLE,
					"", `Откл.`, DLEV3D_OFF
					
	posY = posY + dy
	ui_outfield `Аппроксимация`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield "gs_resol", sepX - 5 - inX, posY, inX, infieldHeight
	
	posY = posY + dy
	ui_infield{3}	"gs_shadow", posX, posY, outX, booleanHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"",`Отбрасывание Теней`, 0,
					"",`Отбрасывание Теней`, 1
					
	posY = posY + dy + 8

	ui_separator sepX, y0, sepX, 265

	posX = sepX + 5
	posY = y0 + 5

	ui_style 0, 1
	ui_outfield `Покрытия`, posX, posY, outX, outfieldHeight
	ui_style 0, 0
	
	dy2 = 20
	inXl = 180
	posY = posY + dy
	ui_outfield `Желоб`, posX, posY + textElevMaterial, endX-posX, outfieldHeight
	posY = posY + dy2
	ui_infield "gs_gutter_mat", endX - 5 - inXl, posY, inXl, attributeHeight
	
	posY = posY + dy + 5
	ui_outfield `Сетка Желоба`, posX, posY + textElevMaterial, endX-posX, outfieldHeight
	posY = posY + dy2
	ui_infield "gs_gutter_guard_mat", endX - 5 - inXl, posY, inXl, attributeHeight

	posY = posY + dy + 5
	ui_outfield `Кронштейн`, posX, posY + textElevMaterial, endX-posX, outfieldHeight
	posY = posY + dy2
	ui_infield "gs_gutter_hanger_mat", endX - 5 - inXl, posY, inXl, attributeHeight

	posY = posY + dy + 5
	ui_outfield `Фильтр`, posX, posY + textElevMaterial, endX-posX, outfieldHeight
	posY = posY + dy2
	ui_infield "gs_gutter_strainer_mat", endX - 5 - inXl, posY, inXl, attributeHeight

return

! -----------------------------------------------------------------------------
"listingPage":
! -----------------------------------------------------------------------------

	! Listing Parameters

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_LISTING,
											listFieldPosY = 0,
											listFieldHeight = 265,
											stDescHeader = "",
											stValueHeader = "",
											stParamNames = stParamNames
return

