
ui_dialog `Параметры Колена Водосточной Трубы`

ui_current_page gs_ui_current_page

TABID_ROOT			= -1
TABID_DIMENSIONS	= 1
TABID_REP_2D		= 2
TABID_REP_3D		= 3
TABID_LISTING		= 4

pageStartY 			= 0
textElevInfield		= 3
textElevBoolean		= 2
textElevMaterial	= 2
infieldHeight		= 19
booleanHeight		= 20
attributeHeight		= 25
outfieldHeight		= 15
fieldToPic			= 3
fieldToField		= 5

ui_page TABID_DIMENSIONS, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "dimensionsPage"

ui_page TABID_REP_2D, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
gosub "representationPage_2d"

ui_page TABID_REP_3D, TABID_ROOT, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
gosub "representationPage_3d"

ui_page TABID_LISTING, TABID_ROOT, `Параметры Спецификаций`, "uiTab_Descriptions_flat(1)"
gosub "listingPage"

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================

! -----------------------------------------------------------------------------
"dimensionsPage":
! -----------------------------------------------------------------------------

	x0		= 0
	y0		= pageStartY
	dy		= 22

	endX = 444
	endY = 266

	outX	= 160
	outXs	= 60
	inX		= 90
	inXs	= 60
	inXl	= 120

	sepY	= 62

	picX	= 148+15
	picY	= 92

	posX 	= x0
	posY	= y0

	ui_outfield `Тип Профиля`, posX, posY + textElevInfield, outX, outfieldHeight

	if LibraryLangCode = "USA" then

		ui_infield{3}	"gs_gutter_ds_profile", (endX - inXl) / 2, posY, inXl, 37,
				2, "ui_downspout_profile_matrix(1)", 13, 1,
				33, 33, 33, 33,
				1,  stGutterDsProfiles[1], stGutterDsProfiles[1],
				2,  stGutterDsProfiles[2], stGutterDsProfiles[2],
				3,  stGutterDsProfiles[3], stGutterDsProfiles[3],
				4,  stGutterDsProfiles[4], stGutterDsProfiles[4],
				5,  stGutterDsProfiles[5], stGutterDsProfiles[5],
				6,  stGutterDsProfiles[6], stGutterDsProfiles[6],
				7,  stGutterDsProfiles[7], stGutterDsProfiles[7],
				8,  stGutterDsProfiles[8], stGutterDsProfiles[8],
				9,  stGutterDsProfiles[9], stGutterDsProfiles[9],
				10, stGutterDsProfiles[10], stGutterDsProfiles[10],
				11, stGutterDsProfiles[11], stGutterDsProfiles[11],
				12, stGutterDsProfiles[12], stGutterDsProfiles[12],
				13, stGutterDsProfiles[13], stGutterDsProfiles[13]

	else

		ui_infield{3}	"gs_gutter_ds_profile_num", (endX - inXl) / 2, posY, inXl, 37,
			2, "ui_downspout_profile_matrix(1)", 13, 1,
			33, 33, 33, 33,
			11, `Круглый`, PROFILE_ROUND,
			12, `Прямоугольный`, PROFILE_RECTANGULAR,
			13, `Закругленный`, PROFILE_CORRUGATED

	endif


	if gs_gutter_ds_profile_num = PROFILE_ROUND then

		ui_pict "ui_downspout_profile_round(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		ui_infield "gs_gutter_dd", endX - inX, posY + 13, inX, infieldHeight ui_tooltip `Радиус Профиля`
		ui_infield "gs_gutter_dd2", endX - inX, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Диаметр Профиля`

	endif

	if gs_gutter_ds_profile_num = PROFILE_RECTANGULAR then

		ui_pict "ui_downspout_profile_rectangle(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		ui_infield "gs_gutter_bb2", endX - inX, posY + 13, inX, infieldHeight ui_tooltip `Длина Профиля`
		ui_infield "gs_gutter_aa2", endX - inX, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Ширина Профиля`

	endif

	if gs_gutter_ds_profile_num = PROFILE_CORRUGATED then

		ui_pict "ui_downspout_profile_corrugated(1)", endX - inX - fieldToPic - 44, posY + 6, 44, 44, 1
		ui_infield "gs_gutter_bb2", endX - inX, posY + 13, inX, infieldHeight ui_tooltip `Длина Профиля`
		ui_infield "gs_gutter_aa2", endX - inX, posY + 13 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Ширина Профиля`

	endif

	ui_separator 0, sepY, endX, sepY

	ui_pict 1, picX, picY, 127, 149, 1

	ui_infield "gs_gutter_elb_u_lgth", picX + 125 + fieldToPic, picY-4, inX, infieldHeight ui_tooltip `Длина Верхней Части`
	ui_infield "gs_gutter_elb_u_ang", picX + 125 + fieldToPic, picY + 32, inXs, infieldHeight ui_tooltip `Угол Верхней Части`
	ui_infield "gs_gutter_elb_l_ang", picX + 125 + fieldToPic, picY + 114, inXs, infieldHeight ui_tooltip `Угол Нижней Части`
	ui_infield "gs_gutter_elb_l_lgth", picX + 33, picY + 149 + fieldToPic, inX, infieldHeight ui_tooltip `Длина Нижней Части`
	ui_infield "dis_wall", picX + 33, picY - infieldHeight , inX, infieldHeight ui_tooltip `Расстояние от Оси Трубы до Стены`

return

! -----------------------------------------------------------------------------
"representationPage_2d":
! -----------------------------------------------------------------------------

	x0 = 5
	y0 = pageStartY + 5
	dy = 25
	
	outX	= 110
	outXs	= 60
	inX		= 90
	inXs	= 30
	inXl	= 130
	
	sepX = 222
	endX = 444
	
	posX = x0
	posY = y0 + 5

	ui_style 0, 1
	ui_outfield `2D-отображение`, posX, posY, outX, outfieldHeight
	ui_style 0, 0

	posY = posY + dy
	ui_outfield `Тип Линии`, posX, posY + textElevMaterial, outXs, outfieldHeight
	ui_infield "gs_line_type", sepX - 5 - inXl, posY, inXl, attributeHeight
	
	posY = posY + dy + 5
	ui_outfield `Перо Контура`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield "gs_cont_pen", sepX - 5 - inXs, posY, inXs, infieldHeight
	
	posY = posY + dy
	ui_outfield `Тип Штриховки`, posX, posY + textElevMaterial, outXs, outfieldHeight
	ui_infield "gs_fill_type", sepX - 5 - inXl, posY, inXl, attributeHeight
	
	posY = posY + dy + 5
	ui_outfield `Перо Штриховки`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield "gs_fill_pen", sepX - 5 - inXs, posY, inXs, infieldHeight
	
	posY = posY + dy
	ui_outfield `Перо Фона Штриховки`, posX, posY + textElevInfield, outX + 30, outfieldHeight
	ui_infield "gs_back_pen", sepX - 5 - inXs, posY, inXs, infieldHeight

return

! -----------------------------------------------------------------------------
"representationPage_3d":
! -----------------------------------------------------------------------------

	x0 = 5
	y0 = pageStartY + 5
	dy = 25
	
	outX	= 110
	outXs	= 60
	inX		= 90
	inXs	= 30
	inXl	= 130
	
	sepX = 222
	endX = 444
	
	posX = x0
	posY = y0 + 5

	ui_style 0, 1
	ui_outfield `3D-отображение`, posX, posY, outX, outfieldHeight
	ui_style 0, 0
	
	posY = posY + dy
	ui_outfield `3D-детализация`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield{3}	"gs_detlevel_3D_m", sepX - 5 - inX, posY, inX, infieldHeight,
					8, "", 0, 0,
					0, 0, 0, 0,
					"", `Детальный`, DETLEVEL_3D_DETAILED,
					"", `Простой`, DETLEVEL_3D_SIMPLE,
					"", `Откл.`, DETLEVEL_3D_OFF
					
	posY = posY + dy
	ui_outfield `Аппроксимация`, posX, posY + textElevInfield, outX, outfieldHeight
	ui_infield "gs_resol", sepX - 5 - inX, posY, inX, infieldHeight
	
	posY = posY + dy
	ui_infield{3}	"gs_shadow", posX, posY, outX, booleanHeight,
					7, "", 2, 1, 0, 0, 0, 0,
					"",`Отбрасывание Теней`, 0,
					"",`Отбрасывание Теней`, 1
					
	posY = posY + dy + 8

	ui_separator sepX, y0, sepX, 265

	posX = sepX + 5
	posY = y0 + 5

	ui_style 0, 1
	ui_outfield `Покрытия`, posX, posY, outX, outfieldHeight
	ui_style 0, 0

	dy2 = 20
	inXl = 180
	posY = posY + dy
	ui_outfield `Колено`, posX, posY + textElevMaterial, endX-posX, outfieldHeight
	posY = posY + dy2
	ui_infield "gs_gutter_ds_mat", endX - 5 - inXl, posY, inXl, attributeHeight

return

! -----------------------------------------------------------------------------
"listingPage":
! -----------------------------------------------------------------------------

	! Listing Parameters

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_LISTING,
											listFieldPosY = 0,
											listFieldHeight = 265,
											stDescHeader = "",
											stValueHeader = "",
											stParamNames = stParamNames
return


