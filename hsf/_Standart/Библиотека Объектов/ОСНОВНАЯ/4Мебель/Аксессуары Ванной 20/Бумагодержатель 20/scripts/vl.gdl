
! --- Bottom, Top Level Calculation  ---
			ac_toplevel		= topHeight
			ac_bottomlevel 	= ac_toplevel - ZZYZX
parameters	ac_toplevel		= ac_toplevel,
			ac_bottomlevel 	= ac_bottomlevel

hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------

values{2} "iDetlevel3D"	DETLEVEL3D_DETAILED,	typRes[1],
						DETLEVEL3D_SIMPLE,		typRes[2],
						DETLEVEL3D_OFF,			typRes[3]

values "gs_resol" range [4,)

if iDetlevel3D = DETLEVEL3D_SIMPLE then
	hideparameter "gs_resol"
	lock "gs_resol"
endif
if iDetlevel3D = DETLEVEL3D_OFF then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------
values{2} "iDispenserstyle"	DISPENSERSTYLE_WALLMOUNTED,	stStyles[1],
							DISPENSERSTYLE_BUILTIN1,	stStyles[2], 
							DISPENSERSTYLE_BUILTIN2,	stStyles[3],
							DISPENSERSTYLE_COMMERCIAL,	stStyles[4]

! -----------------------------------------------------------------------------
! Default Sizes
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "iDispenserstyle" then
	a = defaultSizes[iDispenserstyle][1]
	b = defaultSizes[iDispenserstyle][2]
	zzyzx = defaultSizes[iDispenserstyle][3]
	parameters a = a, b = b, zzyzx = zzyzx
endif
if GLOB_MODPAR_NAME = "A" or GLOB_MODPAR_NAME = "iDispenserstyle" then
	halfA = a/2
	parameters halfA = halfA
else
	if GLOB_MODPAR_NAME = "halfA" or GLOB_MODPAR_NAME = "" then
		A = 2*halfA
		parameters A = A
	endif
endif

! -----------------------------------------------------------------------------
! Symbol Types
! -----------------------------------------------------------------------------
values{2} "iSymbolType"	SYMBTYPE_DETAILED,		stSymbTypes[1],
						SYMBTYPE_SIMPLE,		stSymbTypes[2]

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if iDispenserstyle = 1 then
	values "a" range [0.1,)
	values "b" range [0.1,)
	values "zzyzx" range [0.1,)
	hideparameter "RollRadius", "iSymbolType", "CabinetLineType", "CabinetMat", \
				  "DoorMat", "FlangeMat", "HoodMat", "AccMat", "matDoorFrame", "matDoorPanel", "FlangeBracketMat"
	lock "RollRadius", "iSymbolType", "CabinetLineType", "CabinetMat", \
				  "DoorMat", "FlangeMat", "HoodMat", "AccMat", "matDoorFrame", "matDoorPanel", "FlangeBracketMat"
endif
if iDispenserstyle = 2 then
	values "A" range [(BoxThk)*2+0.01*2+0.01,)
	values "B" range [0.02,)
	values "ZZYZX" range [(BoxThk)*2+0.05,)
	values "RollRadius" range [0.02,MIN((ZZYZX-(BoxThk)*2-0.002*2)/2,B-BoxThk+RollRadius*0.25+0.01)]
	hideparameter "doorMat", "accMat", "matSteel", "matDoorFrame", "matDoorPanel", "FlangeMat"
	lock  "doorMat", "accMat", "matSteel", "matDoorFrame", "matDoorPanel", "FlangeMat"
	if iDetlevel3D = DETLEVEL3D_SIMPLE then	lock "HoodMat"
endif
if iDispenserstyle = 3 then
	values "A" range [(BoxThk)*2+0.01*2+0.01,)
	values "B" range [0.02,)
	values "ZZYZX" range [(BoxThk)*2+0.05,)
	values "RollRadius" range [0.02,MIN((ZZYZX-(BoxThk)*2-0.002*2)/2,B-BoxThk-0.01)]
	hideparameter "HoodMat", "matSteel", "matDoorFrame", "matDoorPanel", "FlangeBracketMat"
	lock  "HoodMat", "matSteel", "matDoorFrame", "matDoorPanel", "FlangeBracketMat"
	if iDetlevel3D = DETLEVEL3D_SIMPLE then	lock "AccMat"
endif
if iDispenserstyle = 4 then
	values "a" range [0.1,)
	values "b" range [0.1,)
	values "zzyzx" range [0.1,)
	hideparameter "RollRadius", "iSymbolType", "CabinetLineType", "FlangeMat", "HoodMat", "doorMat", "matSteel", "FlangeBracketMat"
	lock "RollRadius", "iSymbolType", "CabinetLineType", "FlangeMat", "HoodMat", "doorMat", "matSteel", "FlangeBracketMat"
	lock "a", "b", "zzyzx"
endif

if GLOB_MODPAR_NAME = "FlangeBracketMat" then
	FlangeMat = FlangeBracketMat
	parameters FlangeMat = FlangeMat
endif
if GLOB_MODPAR_NAME = "FlangeMat" then
	FlangeBracketMat = FlangeMat
	parameters FlangeBracketMat = FlangeBracketMat
endif

if gs_cont_pen = 0 then parameters gs_cont_pen = 7
if gs_fill_pen = 0 then parameters gs_fill_pen = 91




