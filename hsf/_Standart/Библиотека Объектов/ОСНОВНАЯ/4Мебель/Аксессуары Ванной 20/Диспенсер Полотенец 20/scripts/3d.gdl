
if gs_shadow=0 then SHADOW OFF
if gs_shadow=1 then SHADOW ON

!!! 3D Feedback to editing
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN
	iDetlevel3D = DETLEVEL3D_SIMPLE
ENDIF

IF iDetlevel3D = DETLEVEL3D_OFF THEN END

PEN gs_cont_pen
RESOL gs_resol


!!! *************************************************************
!!! *** 3D HOTSPOTs *********************************************

Unid=1

!!! *** Width
HOTSPOT 0,0,0,unID, A,1+256 : unID=unID+1
HOTSPOT -1,0,0,unID, A,3 : unID=unID+1
HOTSPOT A, 0, 0,unID, A,2 : unID=unID+1

!!! *** HEIGHT
HOTSPOT 0,0,0,unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT 0,0,-1,unID, ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,ZZYZX,unID, ZZYZX,2 : unID=unID+1

HOTSPOT A,0,0,unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT A,0,-1,unID, ZZYZX,3 : unID=unID+1
HOTSPOT A,0,ZZYZX,unID, ZZYZX,2 : unID=unID+1


!!! *** Depth
HOTSPOT 0,0,0,unID, B,1 : unID=unID+1
HOTSPOT 0,-1, 0,unID, B,3 : unID=unID+1
HOTSPOT 0,B, 0,unID, B,2 : unID=unID+1

HOTSPOT A,0,0,unID, B,1 : unID=unID+1
HOTSPOT A,-1,0,unID, B,3 : unID=unID+1
HOTSPOT A,B, 0,unID, B,2 : unID=unID+1

HOTSPOT A,0,ZZYZX,unID, B,1 : unID=unID+1
HOTSPOT A,-1, ZZYZX,unID, B,3 : unID=unID+1
HOTSPOT A,B, ZZYZX,unID, B,2 : unID=unID+1

HOTSPOT 0,0,ZZYZX,unID, B,1 : unID=unID+1
HOTSPOT 0,-1, ZZYZX,unID, B,3 : unID=unID+1
HOTSPOT 0,B, ZZYZX,unID, B,2 : unID=unID+1

HOTSPOT A/2,0,0,unID : unID=unID+1
HOTSPOT A/2,0,ZZYZX,unID : unID=unID+1


!!! *************************************************************
!!! *** 3D MODEL ************************************************

IF GLOB_CONTEXT=43 OR GLOB_CONTEXT=44 OR GLOB_CONTEXT=46 THEN
	!!! *** If the user want to substract the cabinet from the wall
	MATERIAL  5
	PRISM_ 5,ZZYZX,
		0,0,15,
		A,0,15,
		A,B,15,
		0,B,15,
		0,0,-1
ENDIF

!!! *** Cabinet *************************************************

ADD A/2,B,0


	ROTx 90


	MATERIAL  CabinetMat
	!!! *** Box / Top, Bottom, Lateral Parts
	PRISM_ 10, B,
		-A*0.5,	0,			15,
		A*0.5,	0,			15,
		A*0.5,	zzyzx,	15,
		-A*0.5,	zzyzx,	15,
		-A*0.5,	0,			-1,
		-A*0.5+BoxThk,	BoxThk,			10,
		A*0.5-BoxThk,	BoxThk,			10,
		A*0.5-BoxThk,	zzyzx-(BoxThk),	10,
		-A*0.5+BoxThk,	zzyzx-(BoxThk),	10,
		-A*0.5+BoxThk,	BoxThk,			-1

	!!! *** Box /Rear Part
	PRISM_ 5, BoxThk,
		-A*0.5+BoxThk,		BoxThk,			12,
		A*0.5-(BoxThk),		BoxThk,			12,
		A*0.5-(BoxThk),		zzyzx-(BoxThk),	12,
		-A*0.5+BoxThk,		zzyzx-(BoxThk),	12,
		-A*0.5+BoxThk,		BoxThk,			12

	!!! *** Frame

	MATERIAL FlangeMat
	ADDz B

	PRISM_ 10,FrameThk,
		-A*0.5-FrameWidth,			-FrameWidth,				15,
		A*0.5+FrameWidth,			-FrameWidth,				15,
		A*0.5+FrameWidth,			zzyzx+FrameWidth,			15,
		-A*0.5-FrameWidth,			zzyzx+FrameWidth,			15,
		-A*0.5-FrameWidth,			-FrameWidth,				-1,
		-A*0.5+BoxThk,	BoxThk,			14,
		A*0.5-BoxThk,	BoxThk,			14,
		A*0.5-BoxThk,	zzyzx-BoxThk,	14,
		-A*0.5+BoxThk,	zzyzx-BoxThk,	14,
		-A*0.5+BoxThk,	BoxThk,			-1

	!!! *************************************************************
	!!! *** Font Types **********************************************

	IF iTowelDispType = SYMBTYPE_TYPE1 THEN
		!!! *** Door
		MATERIAL DoorMat
		ROTx -90
		ROTy 90
		ADD -ZZYZX+BoxThk+BoxThk/2, B-BoxThk-FrameThk, -A/2+BoxThk+BoxThk/2
		PRISM_ 6, A-(BoxThk)*2-BoxThk,
			0,			0,			15,
			0,			-B+BoxThk,			15,
			zzyzx*0.6,			-B+BoxThk,		15,
			zzyzx*0.75,			(-B+BoxThk)/2,		15,
			zzyzx*0.75,			0,			15,
			0,			0,			15


		DEL 3

		!!! *** Accessories

		IF iDetlevel3D = DETLEVEL3D_DETAILED  THEN
			MATERIAL AccMat
			ADDz FrameThk
			ADDy ZZYZX-BoxThk*1.5-0.008*2
			CYLIND 0.002, 0.008
			DEL 2

			Alfa=ATN(((ZZYZX*0.15)/((B-BoxThk)/2)))
			AccLength=SQR((ZZYZX*0.15)^2+((B-BoxThk)/2)^2)

			Addx -A/2+FrameWidth*2
			Addy zzyzx*0.25-(BoxThk+BoxThk/2)+ZZYZX*0.15/4
			Addz FrameThk-((B-BoxThk)/2)+0.002+((B-BoxThk)/2)/4
			ROTx -Alfa
			BLOCK A-FrameWidth*4,0.002,AccLength/2
			DEL 4
		ENDIF
	ELSE

		!!! *** Door
		MATERIAL DoorMat
		ROTx -90
		ROTy 90
		ADD -ZZYZX+BoxThk+BoxThk/2, B-BoxThk-FrameThk, -A/2+BoxThk+BoxThk/2
		PRISM_ 6, A-(BoxThk)*2-BoxThk,
			0,			0,			15,
			0,			-B+BoxThk,			15,
			zzyzx*0.3,			-B+BoxThk,		15,
			zzyzx*0.45,			(-B+BoxThk)/2,		15,
			zzyzx*0.45,			0,			15,
			0,			0,			15


		DEL 3

		Addz FrameThk
		ADDy BoxThk
		ADDx -A/2+(BoxThk)
		ROTx -90
		BLOCK A-2*(BoxThk),FrameThk,zzyzx*0.2
		DEL 4
		!!! *** Accessories

		IF iDetlevel3D = DETLEVEL3D_DETAILED  THEN
			MATERIAL AccMat
			ADDz FrameThk
			ADDy ZZYZX-zzyzx*0.15-FrameWidth-BoxThk*1.5
			Addx A/2-BoxThk*1.5-0.008*2
			CYLIND 0.002, 0.008
			DEL 3

			Alfa=ATN(((ZZYZX*0.15)/((B-BoxThk)/2)))
			AccLength=SQR((ZZYZX*0.15)^2+((B-BoxThk)/2)^2)

			Addx -A/2+FrameWidth*2
			Addy zzyzx*0.55-(BoxThk+BoxThk/2)+ZZYZX*0.15/4
			Addz FrameThk-((B-BoxThk)/2)+0.002+((B-BoxThk)/2)/4
			ROTx -Alfa
			BLOCK A-FrameWidth*4,0.002,AccLength/2
			DEL 4
		ENDIF

	ENDIF

	DEL 2

DEL 1
