
if gs_shadow=0 then SHADOW OFF
if gs_shadow=1 then SHADOW ON

!!! 3D Feedback to editing
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN
	iDetlevel3D = DETLEVEL3D_SIMPLE
ENDIF

IF iDetlevel3D = DETLEVEL3D_OFF THEN END

PEN gs_cont_pen
RESOL gs_resol


!!! *************************************************************
!!! *** 3D HOTSPOTs *********************************************

Unid=1

!!! *** Width
HOTSPOT 0,0,0,unID, A,1+256 : unID=unID+1
HOTSPOT -1,0,0,unID, A,3 : unID=unID+1
HOTSPOT A, 0, 0,unID, A,2 : unID=unID+1

!!! *** HEIGHT
HOTSPOT 0,0,0,unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT 0,0,-1,unID, ZZYZX,3 : unID=unID+1
HOTSPOT 0,0,ZZYZX,unID, ZZYZX,2 : unID=unID+1

HOTSPOT A,0,0,unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT A,0,-1,unID, ZZYZX,3 : unID=unID+1
HOTSPOT A,0,ZZYZX,unID, ZZYZX,2 : unID=unID+1


!!! *** Depth
HOTSPOT 0,0,0,unID, B,1 : unID=unID+1
HOTSPOT 0,-1, 0,unID, B,3 : unID=unID+1
HOTSPOT 0,B, 0,unID, B,2 : unID=unID+1

HOTSPOT A,0,0,unID, B,1 : unID=unID+1
HOTSPOT A,-1,0,unID, B,3 : unID=unID+1
HOTSPOT A,B, 0,unID, B,2 : unID=unID+1

HOTSPOT A,0,ZZYZX,unID, B,1 : unID=unID+1
HOTSPOT A,-1, ZZYZX,unID, B,3 : unID=unID+1
HOTSPOT A,B, ZZYZX,unID, B,2 : unID=unID+1

HOTSPOT 0,0,ZZYZX,unID, B,1 : unID=unID+1
HOTSPOT 0,-1, ZZYZX,unID, B,3 : unID=unID+1
HOTSPOT 0,B, ZZYZX,unID, B,2 : unID=unID+1

HOTSPOT A/2,0,0,unID : unID=unID+1
HOTSPOT A/2,0,ZZYZX,unID : unID=unID+1


!!! *************************************************************
!!! *** 3D MODEL ************************************************

IF GLOB_CONTEXT=43 OR GLOB_CONTEXT=44 OR GLOB_CONTEXT=46 THEN
	!!! *** If the user want to substract the cabinet from the wall
	MATERIAL  5
	PRISM_ 5,ZZYZX,
		0,0,15,
		A,0,15,
		A,B,15,
		0,B,15,
		0,0,-1
ENDIF

ADD A/2,B,0


	ROTx 90


	MATERIAL  CabinetMat
	!!! *** Box / Top, Bottom, Lateral Parts
	PRISM_ 10, B,
		-A*0.5,	0,			15,
		A*0.5,	0,			15,
		A*0.5,	zzyzx,	15,
		-A*0.5,	zzyzx,	15,
		-A*0.5,	0,			-1,
		-A*0.5+BoxThk,	BoxThk,			14,
		A*0.5-BoxThk,	BoxThk,			14,
		A*0.5-BoxThk,	zzyzx-(BoxThk),	14,
		-A*0.5+BoxThk,	zzyzx-(BoxThk),	14,
		-A*0.5+BoxThk,	BoxThk,			-1


	!!! *** Box /Rear Part
	PRISM_ 5, BoxThk,
		-A*0.5+BoxThk,		BoxThk,			12,
		A*0.5-(BoxThk),		BoxThk,			12,
		A*0.5-(BoxThk),		zzyzx-(BoxThk),	12,
		-A*0.5+BoxThk,		zzyzx-(BoxThk),	12,
		-A*0.5+BoxThk,		BoxThk,			12

	!!! *** Frame

	MATERIAL FlangeMat
	ADDz B
	PRISM_ 10,FrameThk,
		-A*0.5-FrameWidth,			-FrameWidth,				15,
		A*0.5+FrameWidth,			-FrameWidth,				15,
		A*0.5+FrameWidth,			zzyzx+FrameWidth,			15,
		-A*0.5-FrameWidth,			zzyzx+FrameWidth,			15,
		-A*0.5-FrameWidth,			-FrameWidth,				-1,
		-A*0.5+BoxThk/4,	BoxThk/4,			15,
		A*0.5-BoxThk/4,	BoxThk/4,			15,
		A*0.5-BoxThk/4,	zzyzx-BoxThk/4,	15,
		-A*0.5+BoxThk/4,	zzyzx-BoxThk/4,	15,
		-A*0.5+BoxThk/4,	BoxThk/4,			-1

	IF iDetlevel3D = DETLEVEL3D_DETAILED  THEN
		!!! *** Door
		MATERIAL DoorMat

		PRISM_ 12, FrameThk,
			-A*0.5+BoxThk*3/4,	BoxThk*3/4,			15,
			A*0.5-(BoxThk*3/4),	BoxThk*3/4,			15,
			A*0.5-(BoxThk*3/4),	zzyzx-(BoxThk*3/4),	15,
			-A*0.5+BoxThk*3/4,	zzyzx-(BoxThk*3/4),	15,
			-A*0.5+BoxThk*3/4,	BoxThk*3/4,	-1,
			-A*0.5+BoxThk*3/4+A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25,			15,
			0,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25-ZZYZX*0.03,	15,
			A*0.5-(BoxThk*3/4)-A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25,			15,
			A*0.5-(BoxThk*3/4)-A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.03,	15,
			0,	zzyzx-(BoxThk*3/4)-A*0.15,	15,
			-A*0.5+BoxThk*3/4+A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.03,	15,
			-A*0.5+BoxThk*3/4+A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25,	-1

		!!! *** Push Flap Door
		ADDz -FrameThk
		MATERIAL PushDoorMat
			PRISM_ 7, FrameThk,
			-A*0.5+BoxThk*3/4+A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25,			15,
			0,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25-ZZYZX*0.03,	15,
			A*0.5-(BoxThk*3/4)-A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25,			15,
			A*0.5-(BoxThk*3/4)-A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.03,	15,
			0,	zzyzx-(BoxThk*3/4)-A*0.15,	15,
			-A*0.5+BoxThk*3/4+A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.03,	15,
			-A*0.5+BoxThk*3/4+A*0.15,	zzyzx-(BoxThk*3/4)-A*0.15-ZZYZX*0.25,	-1

		DEL 1

		!!! *** KEY
		MATERIAL AccMat
		ADDz FrameThk
		ADDy BoxThk*1.5+0.008*2
		CYLIND 0.002, 0.008
		DEL 2

	ELSE
		MATERIAL DoorMat

		PRISM_ 5, FrameThk,
			-A*0.5+BoxThk*3/4,	BoxThk*3/4,			15,
			A*0.5-(BoxThk*3/4),	BoxThk*3/4,			15,
			A*0.5-(BoxThk*3/4),	zzyzx-(BoxThk*3/4),	15,
			-A*0.5+BoxThk*3/4,	zzyzx-(BoxThk*3/4),	15,
			-A*0.5+BoxThk*3/4,	BoxThk*3/4,	-1

	ENDIF

	DEL 2
DEL 1
