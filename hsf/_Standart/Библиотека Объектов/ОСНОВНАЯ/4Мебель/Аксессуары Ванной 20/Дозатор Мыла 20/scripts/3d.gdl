
if iDetlevel3D = DETLEVEL3D_OFF then end
if not(gs_shadow) then SHADOW OFF
if iDetlevel3D = DETLEVEL3D_SIMPLE then	gs_resol = 6

! --- 3D Feedback to editing ---
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then
	iDetlevel3D = DETLEVEL3D_SIMPLE
endif

! -----------------------------------------------------------------------------
! 3D Hotspots
! -----------------------------------------------------------------------------
Unid=1

if iDispenserstyle = DISPENSERSTYLE_BUILTIN then
	!!! *** Width
	hotspot  0, 	0, 0,	unID, A, 	1+256, a : unID=unID+1
	hotspot -1, 	0, 0,	unID, A, 	3 		 : unID=unID+1
	hotspot  A,	0, 0,	unID, A, 	2, a 	 : unID=unID+1

	!!! *** HEIGHT
	hotspot 0, 0,     0,	unID, zzyzx, 	1+128 : unID=unID+1
	hotspot 0, 0,    -1,	unID, zzyzx, 	3 : unID=unID+1
	hotspot 0, 0, zzyzx,	unID, zzyzx, 	2 : unID=unID+1

	hotspot A, 0,	  0,	unID, zzyzx,	1+128 : unID=unID+1
	hotspot A, 0,	 -1,	unID, zzyzx,	3 : unID=unID+1
	hotspot A, 0, zzyzx,	unID, zzyzx,	2 : unID=unID+1

	!!!!! *** Depth
	hotspot 0,  0, 0,	unID, b,	1 : unID=unID+1
	hotspot 0, -1, 0,	unID, b,	3 : unID=unID+1
	hotspot 0,  b, 0,	unID, b,	2 : unID=unID+1

	hotspot A,  0, 0,	unID, b,	1 : unID=unID+1
	hotspot A, -1, 0,	unID, b,	3 : unID=unID+1
	hotspot A,  b, 0,	unID, b,	2 : unID=unID+1

	hotspot 0,  0, ZZYZX,	unID, b,	1 : unID=unID+1
	hotspot 0, -1, ZZYZX,	unID, b,	3 : unID=unID+1
	hotspot 0,  b, ZZYZX,	unID, b,	2 : unID=unID+1

	hotspot A,  0, ZZYZX,	unID, b,	1 : unID=unID+1
	hotspot A, -1, ZZYZX,	unID, b,	3 : unID=unID+1
	hotspot A,  b, ZZYZX,	unID, b,	2 : unID=unID+1

	hotspot  A/2, 	0, 		0,	unID : unID=unID+1
	hotspot  A/2, 	0, 	zzyzx,	unID : unID=unID+1
else
    if iDispenserstyle = DISPENSERSTYLE_WALLMOUNTED then
        !!! *** Width
        hotspot  0, 	0, zzyzx*0.12,	unID, A, 	1+256	: unID=unID+1
        hotspot -1, 	0, zzyzx*0.12,	unID, A, 	3		: unID=unID+1
        hotspot  A,		0, zzyzx*0.12,	unID, A, 	2		: unID=unID+1
        
        !!! *** HEIGHT
        hotspot A*0.03, -B*0.15,     0,	unID, zzyzx, 	1+128	: unID=unID+1
        hotspot A*0.03, -B*0.15,    -1,	unID, zzyzx, 	3		: unID=unID+1
        hotspot A*0.03, -B*0.15, zzyzx,	unID, zzyzx, 	2		: unID=unID+1
        
        hotspot A*0.97, -B*0.15,	 0,	unID, zzyzx,	1+128	: unID=unID+1
        hotspot A*0.97,- B*0.15,	-1,	unID, zzyzx,	3		: unID=unID+1
        hotspot A*0.97, -B*0.15, zzyzx,	unID, zzyzx,	2		: unID=unID+1
        
        hotspot A*0.03, -B*0.95,     0,	unID, zzyzx, 	1+128	: unID=unID+1
        hotspot A*0.03, -B*0.95,    -1,	unID, zzyzx, 	3		: unID=unID+1
        hotspot A*0.03, -B*0.95, zzyzx,	unID, zzyzx, 	2		: unID=unID+1
        
        hotspot A*0.97, -B*0.95,	 0,	unID, zzyzx,	1+128	: unID=unID+1
        hotspot A*0.97,- B*0.95,	-1,	unID, zzyzx,	3		: unID=unID+1
        hotspot A*0.97, -B*0.95, zzyzx,	unID, zzyzx,	2		: unID=unID+1
        
        !!!!! *** Depth
        hotspot 0, 	 0, zzyzx*0.22,	unID, B, 	1+128	: unID=unID+1
        hotspot 0,	 1, zzyzx*0.22,	unID, B, 	3		: unID=unID+1
        hotspot 0,	-b, zzyzx*0.22,	unID, B, 	2		: unID=unID+1
        
        hotspot A, 	 0, zzyzx*0.22,	unID, B, 	1+128	: unID=unID+1
        hotspot A,	 1, zzyzx*0.22,	unID, B, 	3		: unID=unID+1
        hotspot A,	-b, zzyzx*0.22,	unID, B, 	2		: unID=unID+1
 	else
	    _hsOffset = 0.008
	    _ref = 1
	    _b = -B
    
	    HOTSPOT  A / 2, 0, ZZYZX, unID :	unID=unID+1
    
	    HOTSPOT 0, 0, 0, unID, A, 1+256 : unID=unID+1
	    HOTSPOT -1, 0, 0, unID, A, 3 : unID=unID+1
	    HOTSPOT A, 0, 0, unID, A, 2 : unID=unID+1
    
	    HOTSPOT 0, 0, ZZYZX, unID, A, 1+256 : unID=unID+1
	    HOTSPOT -1, 0, ZZYZX, unID, A, 3 : unID=unID+1
	    HOTSPOT A, 0, ZZYZX, unID, A, 2 : unID=unID+1
    
	    HOTSPOT A-_hsOffset,  0, _hsOffset, unID, B, 1+128 : unID=unID+1
	    HOTSPOT A-_hsOffset, _ref, _hsOffset, unID, B, 3 : unID=unID+1
	    HOTSPOT A-_hsOffset,  _b, _hsOffset, unID, B, 2 : unID=unID+1
    
	    HOTSPOT A-_hsOffset,  0, ZZYZX-_hsOffset, unID, B, 1+128 : unID=unID+1
	    HOTSPOT A-_hsOffset, _ref, ZZYZX-_hsOffset, unID, B, 3 : unID=unID+1
	    HOTSPOT A-_hsOffset,  _b, ZZYZX-_hsOffset, unID, B, 2 : unID=unID+1
	    
	    HOTSPOT _hsOffset,  0, _hsOffset, unID, B, 1+128 : unID=unID+1
	    HOTSPOT _hsOffset, _ref, _hsOffset, unID, B, 3 : unID=unID+1
	    HOTSPOT _hsOffset,  _b, _hsOffset, unID, B, 2 : unID=unID+1
    
	    HOTSPOT _hsOffset,  0, ZZYZX-_hsOffset, unID, B, 1+128 :	unID=unID+1
	    HOTSPOT _hsOffset, _ref, ZZYZX-_hsOffset, unID, B, 3 : unID=unID+1
	    HOTSPOT _hsOffset,  _b, ZZYZX-_hsOffset, unID, B, 2 : unID=unID+1
	endif
endif


! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

PEN gs_cont_pen
resol gs_resol

addx A/2
gosub iDispenserstyle
del 1
end

! =============================================================================
! SUBRUTINES
! =============================================================================

! -----------------------------------------------------------------------------
! Built-In Soap dispenser
! -----------------------------------------------------------------------------

1:
	if GLOB_CONTEXT = 43 OR GLOB_CONTEXT = 44 OR GLOB_CONTEXT = 46 then
		! --- if the user want to substract the cabinet from the wall ---
		material  5
		add -a/2, 0, 0
			prism_ 5,zzyzx,
				0, 0, 15,
				a, 0, 15,
				a, b, 15,
				0, b, 15,
				0, 0, -1
		del 1
	endif

	add 0, B, 0
	rotx 90

	material  matCabinet
	! --- Box / Top, Bottom, Lateral Parts ---
	prism_ 10, B,
		-A*0.5,	0,			15,
		A*0.5,	0,			15,
		A*0.5,	zzyzx,	15,
		-A*0.5,	zzyzx,	15,
		-A*0.5,	0,			-1,
		-A*0.5+BoxThk,	BoxThk,			14,
		A*0.5-BoxThk,	BoxThk,			14,
		A*0.5-BoxThk,	zzyzx-(BoxThk),	14,
		-A*0.5+BoxThk,	zzyzx-(BoxThk),	14,
		-A*0.5+BoxThk,	BoxThk,			-1

	! --- Box /Rear Part ---
	prism_ 5, BoxThk,
		-A*0.5+BoxThk,		BoxThk,			12,
		A*0.5-(BoxThk),		BoxThk,			12,
		A*0.5-(BoxThk),		zzyzx-(BoxThk),	12,
		-A*0.5+BoxThk,		zzyzx-(BoxThk),	12,
		-A*0.5+BoxThk,		BoxThk,			12

	! --- Frame ---

	material matFlange
	addz B
	prism_ 10,FrameThk,
		-A*0.5-FrameWidth,			-FrameWidth,				15,
		A*0.5+FrameWidth,			-FrameWidth,				15,
		A*0.5+FrameWidth,			zzyzx+FrameWidth,			15,
		-A*0.5-FrameWidth,			zzyzx+FrameWidth,			15,
		-A*0.5-FrameWidth,			-FrameWidth,				-1,
		-A*0.5+BoxThk/4,	BoxThk/4,			15,
		A*0.5-BoxThk/4,	BoxThk/4,			15,
		A*0.5-BoxThk/4,	zzyzx-BoxThk/4,	15,
		-A*0.5+BoxThk/4,	zzyzx-BoxThk/4,	15,
		-A*0.5+BoxThk/4,	BoxThk/4,			-1

	! --- Door ---
	material matDoor
	prism_ 5, FrameThk,
		-A*0.5+BoxThk*3/4,	BoxThk*3/4,			15,
		A*0.5-(BoxThk*3/4),	BoxThk*3/4,			15,
		A*0.5-(BoxThk*3/4),	zzyzx-(BoxThk*3/4),	15,
		-A*0.5+BoxThk*3/4,	zzyzx-(BoxThk*3/4),	15,
		-A*0.5+BoxThk*3/4,	BoxThk*3/4,	-1


	if iDetlevel3D = DETLEVEL3D_DETAILED  then
		! --- Soap Valve ---
		material matValve1
		addz FrameThk
		addy BoxThk+0.01
		cylind 0.015, 0.005
		addz 0.015/2
		rotx 90
		cylind 0.008, 0.003
		del 1
		addz 0.015/2
		cylind 0.003, 0.004
		addz 0.003
		cylind 0.002, 0.005
		del 3

		material matAccessories
		addy 0.008*2
		cylind 0.002, 0.005
		del 3

		addz FrameThk
		addy ZZYZX-(BoxThk+0.01)
		cylind 0.002, 0.005
		del 2
	endif

	del 3

return

! -----------------------------------------------------------------------------
! Wall-mounted Soap dispenser
! -----------------------------------------------------------------------------

2:

	material matBracket

	rotz 180

	add -A*0.5, 0, zzyzx*0.12
	block A, B*0.1, zzyzx*0.4
	add A*0.01, B*0.1, zzyzx*0.1
	block A*0.98, B*0.9, zzyzx*0.15

	if iDetlevel3D = DETLEVEL3D_DETAILED then
		material matContainer
		add A*0.07, B*0.1, zzyzx*0.15
		block A*0.84, B*0.7, zzyzx*0.03
		add -A*0.05, -B*0.05, zzyzx*0.03
		block A*0.94, B*0.8, zzyzx*0.6
		del 4
	endif
	if iDetlevel3D = DETLEVEL3D_SIMPLE then
		material matContainer
		add A*0.02, B*0.05, zzyzx*0.15
		block A*0.94, B*0.8, zzyzx*0.63
		del 3
	endif

	if iDetlevel3D = DETLEVEL3D_DETAILED then
		material matValve2
		addy B*0.5
		resol gs_resol
		cylind zzyzx*0.5, B*0.06
		addz zzyzx*0.07
		resol gs_resol*2
		cone zzyzx*0.1, B*0.15, B*0.3, 90, 90
		addz zzyzx*0.1
		cylind zzyzx*0.05, B*0.35
		del 3
	endif

	if iDetlevel3D = DETLEVEL3D_SIMPLE then
		material matValve2
		addy B*0.5
		cylind zzyzx*0.3, B*0.2
		del 1
	endif

	del 1

return

! -----------------------------------------------------------------------------
! Wall-mounted Soap dispenser 2
! -----------------------------------------------------------------------------

3:

	addx -A / 2
	rotx 90

	if iDetlevel3D = DETLEVEL3D_DETAILED then

		hprism_ matCabinet,matCabinet,matCabinet,matCabinet,
			12, 0.009,0,0.008,1,
			0,0.01,79,
			0.01,0.01,979,
			0.01,90,4079,
			A-0.01,0,79,
			A-0.01,0.01,979,
			0.01,90,4079,
			A,zzyzx-0.01,79,
			A-0.01,zzyzx-0.01,979,
			0.01,90,4079,
			0.01,zzyzx,79,
			0.01,zzyzx-0.01,979,
			0.01,90,4079

		addz 0.009
		cutpolya 2,2,0,
			a/2,zzyzx/3+0.003,979,
			0.005,360,4079

		hprism_ matCabinet,matCabinet,matCabinet,matCabinet,
			12, b-0.009,0,0.015,1,
			0.008,0.018,79,
			0.018,0.018,979,
			0.01,90,4079,
			a-0.018,0.008,79,
			a-0.018,0.018,979,
			0.01,90,4079,
			a-0.008,zzyzx-0.018,79,
			a-0.018,zzyzx-0.018,979,
			0.01,90,4079,
			0.018,zzyzx-0.008,79,
			0.018,zzyzx-0.018,979,
			0.018,90,4079

		cutend

		MATERIAL matContainer

		prism_ 2,0.081,
				a/2,zzyzx/3+0.003,979,
				0.005,360,4079
		addz b-0.009

		prism_ 2,0.002,
				a/2,zzyzx/3+0.003,979,
				0.006,360,4079
		del 1

		MATERIAL matAccessories

		add a/2-0.005,zzyzx/5,b-0.009

			prism_	5,0.005,
					0,0,15,
					0,0.005,15,
					0.01,0.005,15,
					0.01,0,15,
					0,0,-1

		add -0.005,-0.0035,0.005

			prism_	5,0.003,
					0,0,15,
					0,0.015,15,
					0.02,0.015,15,
					0.02,0,15,
					0,0,-1

		del 3

	endif

	if iDetlevel3D = DETLEVEL3D_SIMPLE then

		MATERIAL  matCabinet

		prism_	5,0.009,
				0,0,15,
				a,0,15,
				a,ZZYZX,15,
				0,ZZYZX,15,
				0,0,-1

		addz 0.009

			prism_	5,B-0.009,
					0.008,0.008,15,
					a-0.008,0.008,15,
					a-0.008,ZZYZX-0.008,15,
					0.008,ZZYZX-0.008,15,
					0.008,0.008,-1

		MATERIAL matAccessories

		add a/2-0.005,zzyzx/5,b-0.009

			prism_	5,0.005,
					0,0,15,
					0,0.005,15,
					0.01,0.005,15,
					0.01,0,15,
					0,0,-1

		add -0.005,-0.0035,0.005

			prism_	5,0.003,
					0,0,15,
					0,0.015,15,
					0.02,0.015,15,
					0.02,0,15,
					0,0,-1
		del 2
		MATERIAL matContainer

		addz b-0.009

		prism_ 2,0.002,
			a/2,zzyzx/3+0.003,979,
			0.006,360,4079

		del 2

	endif

	del 2

return

