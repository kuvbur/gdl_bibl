
unID=1

!!! *** Cabinet

	HOTSPOT2  A/2 , 0, unID :	unID=unID+1

	HOTSPOT2 0, 0, unID, A, 1+256 :	unID=unID+1
	HOTSPOT2 -1, 0, unID, A, 3+128 : unID=unID+1
	HOTSPOT2 A, 0, unID, A, 2 : unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+128 :	unID=unID+1
	HOTSPOT2 0, -1, unID, B, 3+128 : unID=unID+1
	HOTSPOT2 0, B, unID, B, 2 : unID=unID+1

ADD2 A,0
	HOTSPOT2 0, 0, unID, B, 1+128 :	unID=unID+1
	HOTSPOT2 0, -1, unID, B, 3+128 : unID=unID+1
	HOTSPOT2 0, B, unID, B, 2 : unID=unID+1
DEL 1

IF iSymbType = SYMBTYPE_DETAILED THEN
	HOTSPOT2  -FrameWidth  , 0, unID :	unID=unID+1
	HOTSPOT2  A+FrameWidth , 0, unID :	unID=unID+1
ENDIF

!! *** Waste Container
IF iSymbType = SYMBTYPE_DETAILED THEN
	temp=BoxThk
	ELSE
	temp=0
ENDIF

ADD2 temp, 0
	HOTSPOT2 0, 0, unID, WasteContOverhang, 1+128 :	unID=unID+1
	HOTSPOT2 0, 1, unID, WasteContOverhang, 3+128 : unID=unID+1
	HOTSPOT2 0, -WasteContOverhang, unID, WasteContOverhang, 2 : unID=unID+1
DEL 1

ADD2 A-(temp), 0
	HOTSPOT2 0, 0, unID, WasteContOverhang, 1+128 :	unID=unID+1
	HOTSPOT2 0, 1, unID, WasteContOverhang, 3+128 : unID=unID+1
	HOTSPOT2 0, -WasteContOverhang, unID, WasteContOverhang, 2 : unID=unID+1
DEL 1
!!! *** Cabinet *************************************************

ADD2 A/2,B

PEN gs_cont_pen
FILL gs_fill_type

LINE_TYPE CabinetLineType
ADD2 0,0
	POLY2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
			-A*0.5,		0,		3,
			A*0.5,		0,		3,
			A*0.5,	-B,		0,
			-A*0.5,	-B,		3,
			-A*0.5,		0,		3

	IF iSymbType = SYMBTYPE_SIMPLE THEN LINE2 -A*0.5,	-B, A*0.5,	-B
DEL 1

ADD2 A*0.5,		-B
	IF iSymbType = SYMBTYPE_DETAILED THEN
		LINE_TYPE  1
		POLY2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
				-A-FrameWidth,		0,			3,
				FrameWidth,		0,			3,
				FrameWidth,		-FrameThk,		3,
				-A-FrameWidth,		-FrameThk,		3,
			-A-FrameWidth,		0,			3
	ENDIF
DEL 1
DEL 1

!!! *** Waste Container *****************************************


IF iSymbType = SYMBTYPE_DETAILED THEN
	WasteContWidth= A- 2*(BoxThk)
	ADD2 BoxThk, -WasteContDepth+B-BoxThk
		LINE_TYPE  1
		POLY2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
				0,		0,			1,
				WasteContWidth,		0,			1,
				WasteContWidth,		WasteContOverhang-FrameThk,		1,
				0,		WasteContOverhang-FrameThk,		1,
				0,		0,			1

	DEL 1

	ELSE

	WasteContWidth= A
	ADD2 0, -WasteContDepth+B-BoxThk
		LINE_TYPE  1
		POLY2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
				0,		0,			1,
				WasteContWidth,		0,			1,
				WasteContWidth,		WasteContOverhang,		0,
				0,		WasteContOverhang,		1,
				0,		0,			1

	DEL 1

ENDIF
