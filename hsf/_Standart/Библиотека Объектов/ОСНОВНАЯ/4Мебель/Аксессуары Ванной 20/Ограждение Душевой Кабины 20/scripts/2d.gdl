
pen gs_cont_pen
fill gs_fill_type

! -----------------------------------------------------------------------------
! 2D Hotspots
! -----------------------------------------------------------------------------
unID=1

hotspot2    0,    0, unID : unID = unID + 1
hotspot2    0,   -b, unID : unID = unID + 1
hotspot2    a,    0, unID : unID = unID + 1

if iDoorStyle = DOORSTYLE_CORNER then
	hotspot2    a, -ysl, unID : unID = unID + 1
else
	hotspot2    a,   -b, unID : unID = unID + 1
endif

if iDoorStyle = DOORSTYLE_CORNER then hotspot2  xsl,   -b, unID : unID = unID + 1


! -----------------------------------------------------------------------------
! 2D detaillevel
! -----------------------------------------------------------------------------

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

gosub iDoorStyle

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Shower Corner
! -----------------------------------------------------------------------------

1:
	add2 0, -b
	line2 xsl, 0,   a, b-ysl

	if not(bPanelSide1) and not(bPanelSide2) then				!None
		if det2D = 3 then
			poly2_b       5, 1+2+4+64,     gs_fill_pen, gs_back_pen,      !!Fill
				xsl, 0,1,
				xsl-wFrame*(SIN(ATN((b-ysl)/(a-xsl)))), wFrame*(COS(ATN((b-ysl)/(a-xsl)))),1,
				a-wFrame*(SIN(ATN((b-ysl)/(a-xsl)))), b-ysl+wFrame*(COS(ATN((b-ysl)/(a-xsl)))),1,
				a, b-ysl,1,
				xsl, 0,-1
		endif
	else
		if bPanelSide2 and not(bPanelSide1) then				!Side2
			line2 0, 0,   xsl, 0
			if det2D = 3 then
				poly2_b       6, 1+2+4+64,     gs_fill_pen, gs_back_pen,       !!Fill
					0, 0,1,
					0, wFrame,1,
					xsl-TAN((ATN((b-ysl)/(a-xsl)))/2)*wFrame, wFrame,1,
					a-wFrame*(SIN(ATN((b-ysl)/(a-xsl)))), b-ysl+wFrame*(COS(ATN((b-ysl)/(a-xsl)))),1,
					a, b-ysl,1,
					xsl, 0,1
			endif
		else
			if bPanelSide1 and not(bPanelSide2) then			!Side1
				line2 a, b-ysl,   a, b
				if det2D = 3 then
					poly2_b       6, 1+2+4+64,     gs_fill_pen, gs_back_pen,       !!Fill
						xsl, 0,1,
						xsl-wFrame*(SIN(ATN((b-ysl)/(a-xsl)))), wFrame*(COS(ATN((b-ysl)/(a-xsl)))),1,
						a-wFrame, b-ysl+TAN((ATN((a-xsl)/(b-ysl)))/2)*wFrame,1,
						a-wFrame, b,1,
						a, b,1,
						a, b-ysl,1
				endif
			else
				if bPanelSide1 and bPanelSide2 then			!Both
					line2 0, 0,   xsl, 0
					line2 a, b-ysl,   a, b

					if det2D = 3 then
						poly2_b       8, 1+2+4+64,    gs_fill_pen, gs_back_pen,     !!Fill
							0, 0, 1,
							0, wFrame, 1,
							xsl-TAN((ATN((b-ysl)/(a-xsl)))/2)*wFrame, wFrame, 1,
							a-wFrame, b-ysl+TAN((ATN((a-xsl)/(b-ysl)))/2)*wFrame, 1,
							a-wFrame, b, 1,
							a, b, 1,
							a, b-ysl, 1,
							xsl, 0, 1
					endif
					del 1
				endif
			endif
		endif
	endif

return


! -----------------------------------------------------------------------------
! Shower Rectangle
! -----------------------------------------------------------------------------
2:
	add2 0, -b
	line2 0, 0, a, 0
	if det2D = 3 then
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 1,
				a, 0, 1,
				a, wFrame, 1,
				0, wFrame, 1
	endif

	if bPanelSide2 then			!Has Side2
		line2 0, 0, 0, b
		if det2D = 3 then
			poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					0, wFrame, 1,
					wFrame, wFrame, 1,
					wFrame, b, 1,
					0, b, 1
		endif
	endif
	if bPanelSide1 then			!Has Side1
		line2 a, 0, a, b
		if det2D = 3 then
			poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					a-wFrame, wFrame, 1,
					a, wFrame, 1,
					a, b, 1,
					a-wFrame, b, 1
		endif
		del 1
	endif

return
