
if not(gs_shadow) then shadow off
if iDetlevel3D = DETLEVEL3D_OFF then end

! -----------------------------------------------------------------------------
! 3D Hotspots
! -----------------------------------------------------------------------------
unID = 1

pen gs_cont_pen
gosub iDoorStyle

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Shower Corner
! -----------------------------------------------------------------------------

1:
	c=zzyzx

	add 0, -b, 0
	! --- middle panel ---
	addx xsl
	rotz atn ((b-ysl) / (a-xsl))
	rotx 90
	material gs_frame_mat

	if bPanelSide2 then			!Has Side2
		roty 180-atn ((B-ysl)/(A-xsl))/2
		cutplane 0, 1, 1
		del 1
	endif

	if bPanelSide1 then			!Has Side1
		addx SQR((a-xsl)^2+(b-ysl)^2)
		roty atn ((A-xsl)/(B-ysl))/2
		cutplane 0, 1, 1
		del 2
	endif

	a = SQR((a-xsl)^2 + (b-ysl)^2)
	extrude 10, 0, 0, -wFrame, 1+2+16+32,
		0, 0, 0,
		a, 0, 0,
		a, c, 0,
		0, c, 0,
		0, 0, -1,

		wFrame, wFrame, 0,
		a-wFrame, wFrame, 0,
		a-wFrame, c-wFrame, 0,
		wFrame, c-wFrame, 0,
		wFrame, wFrame, -1

	if bPanelSide2 then cutend
	if bPanelSide1 then cutend

	addz -wFrame
	addz 0.015

	extrude 10, 0, 0, 0.02, 1+2+16+32,
		wFrame, wFrame, 0,
		a-wFrame, wFrame, 0,
		a-wFrame, c-wFrame, 0,
		wFrame, c-wFrame, 0,
		wFrame, wFrame, -1,

		0.07, 0.07, 0,
		a-0.07, 0.07, 0,
		a-0.07, c-0.07, 0,
		0.07, c-0.07, 0,
		0.07, 0.07, -1

	add 0.07, 0.07, 0.01
	material gs_glass_mat
	rect a-0.14, c-0.14

	a = SQR(-1*(b-ysl)^2 + (a)^2) + xsl
	del 6

	if bPanelSide2 then			!Has Side2
		add xsl, 0, 0
		rotx 90
		material gs_frame_mat

		roty atn ((B-ysl)/(A-xsl))/2
		cutplane 0, 1, 1
		del 1

		extrude 10, 0, 0, -wFrame, 1+2+16+32,
			0, 0, 0,
			-xsl, 0, 0,
			-xsl, c, 0,
			0, c, 0,
			0, 0, -1,

			-wFrame, c-wFrame, 0,
			-xsl+wFrame, c-wFrame, 0,
			-xsl+wFrame, wFrame, 0,
			-wFrame, wFrame, 0,
			-wFrame, c-wFrame, -1

		cutend

		add -xsl+wFrame, wFrame, -wFrame/2
		body -1
		material gs_glass_mat
		rect xsl-2*wFrame, c-2*wFrame
		del 3
	endif

	if bPanelSide1 then			!Has Side1

		add a, b-ysl, 0
		rotz -90
		rotx 90
		body -1
		material gs_frame_mat

		roty -atn ((A-xsl)/(B-ysl))/2
		cutplane 0, 1, 1
		del 1

		extrude 10, 0, 0, wFrame, 1+2+16+32,
			0, 0, 0,
			-ysl, 0, 0,
			-ysl, c, 0,
			0, c, 0,
			0, 0, -1,

			-wFrame, c-wFrame, 0,
			-ysl+wFrame, c-wFrame, 0,
			-ysl+wFrame, wFrame, 0,
			-wFrame, wFrame, 0,
			-wFrame, c-wFrame, -1

		cutend

		add -ysl+wFrame, wFrame, wFrame/2
		body -1
		material gs_glass_mat
		rect ysl-2*wFrame, c-2*wFrame
		del 5
	endif

return


! -----------------------------------------------------------------------------
! Shower Rectangle
! -----------------------------------------------------------------------------

2:
	c=zzyzx
	add 0, -b, 0
	addy wFrame
	rotx 90

	material gs_frame_mat
	extrude 10, 0, 0, wFrame, 1+2+16+32,
		0, 0, 0,
		a, 0, 0,
		a, c, 0,
		0, c, 0,
		0, 0, -1,

		wFrame, wFrame, 0,
		a-wFrame, wFrame, 0,
		a-wFrame, c-wFrame, 0,
		wFrame, c-wFrame, 0,
		wFrame, wFrame, -1

	addz 0.015
	extrude 10, 0, 0, 0.02, 1+2+16+32,
		wFrame, wFrame, 0,
		a-wFrame, wFrame, 0,
		a-wFrame, c-wFrame, 0,
		wFrame, c-wFrame, 0,
		wFrame, wFrame, -1,

		0.07, 0.07, 0,
		a-0.07, 0.07, 0,
		a-0.07, c-0.07, 0,
		0.07, c-0.07, 0,
		0.07, 0.07, -1

	add 0.07, 0.07, 0.01
	material gs_glass_mat
	rect a-0.14, c-0.14
	del 2

	if bPanelSide2 then			!Has Side2
		roty -90
		addz -wFrame
		material gs_frame_mat
		extrude 9, 0, 0, wFrame, 1+2+16+32,
			0, 0, 0,
			-b+wFrame, 0, 0,
			-b+wFrame, c, 0,
			0, c, 0,
			0, c-wFrame, 0,
			-b+2*wFrame, c-wFrame, 0,
			-b+2*wFrame, wFrame, 0,
			0, wFrame, 0,
			0, 0, -1

		add -b+2*wFrame, wFrame, wFrame/2
		body -1
		material gs_glass_mat
		rect b-2*wFrame, c-2*wFrame
		del 3
	endif

	if bPanelSide1 then			!Has Side1

		roty -90
		addz -a
		body -1
		material gs_frame_mat
		extrude 9, 0, 0, wFrame, 1+2+16+32,
			0, 0, 0,
			-b+wFrame, 0, 0,
			-b+wFrame, c, 0,
			0, c, 0,
			0, c-wFrame, 0,
			-b+2*wFrame, c-wFrame, 0,
			-b+2*wFrame, wFrame, 0,
			0, wFrame, 0,
			0, 0, -1

		add -b+2*wFrame, wFrame, wFrame/2
		body -1
		material gs_glass_mat
		rect b-2*wFrame, c-2*wFrame
		del 1
	endif

	if bPanelSide1 then			!Has Side1
		del 5
	else
		del 2
	endif

return

