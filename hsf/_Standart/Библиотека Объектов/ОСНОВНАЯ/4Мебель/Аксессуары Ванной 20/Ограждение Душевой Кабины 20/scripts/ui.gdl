
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Ограждения Душевой Кабины`
ui_current_page gs_ui_current_page

UI_PAGE TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
gosub "StyleAndDimensionsPage"

UI_PAGE TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "RepresentationAndSurfacesPage"

UI_PAGE TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
gosub "DescriptionsPage"

! ==============================================================================
end
! ==============================================================================


! --------------------------------------------------------------------------------
!Subroutines
! --------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"StyleAndDimensionsPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	xPict = 171
	if iDoorStyle = 1 then ! Corner
		yPict = 95
	else
		yPict = 71
	endif
	PictWidth = 111
	PictHeight = 106
	_infHeight = 19
	_outfHeight = 15
	_offsetY = 4
	x1 = 0
	x2 =  xPict - 103
	x3 = 123
	x4 = 185
	x5 = 230
	x6 = xPict + 83
	x7 = xPict + PictWidth + 3
	x8 = 353
	_yCurr = pageStartY

	ui_infield "wFrame", x8, _yCurr, 90, _infHeight ui_tooltip `Толщина Рамы`
	ui_infield{3} "iDoorStyle", x3, _yCurr, 90, _infHeight,
					8, "",
					0, 0,
					0, 0, 0, 0,
					"", `Угловое`, 1,
					"", `Пристенное`, 2

	_yCurr = pageStartY + _offsetY
	ui_outfield `Тип`, x1, _yCurr, 121, _outfHeight
	ui_outfield `Толщина Рамы`, x5, _yCurr, 121, _outfHeight
	
	_yCurr = pageStartY + 24
	
	ui_separator x1, _yCurr, 444, _yCurr
	
	if iDoorStyle = 1 then ! Corner
		if not(bPanelSide1) and not(bPanelSide2) then
			ui_pict 5, xPict, yPict, PictWidth, PictHeight, 1
		else
			if bPanelSide1 and bPanelSide2 then
				ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1
			else
				if not(bPanelSide1) then
					ui_pict 3, xPict, yPict, PictWidth, PictHeight, 1
				else
					ui_pict 4, xPict, yPict, PictWidth, PictHeight, 1
				endif
			endif
		endif
		_yCurr = yPict - 42
		ui_infield{3} "bPanelSide1", x6, _yCurr, 90, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Боковая Панель`, 0,
						"", `Боковая Панель`, 1

		_yCurr = _yCurr + 20
		ui_infield "ysl", x6, _yCurr, 90, _infHeight ui_tooltip `Длина Боковой Панели по Y`

		_yCurr = _yCurr + 57
		ui_infield "B", x7, _yCurr, 90, _infHeight ui_tooltip `Размер 2`
	
		_yCurr = yPict + 65
		ui_infield{3} "bPanelSide2", x2, _yCurr, 90, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Боковая Панель`, 0,
						"", `Боковая Панель`, 1

		_yCurr = _yCurr + 20
		ui_infield "xsl", x2, _yCurr, 90, _infHeight ui_tooltip `Длина Боковой Панели по X`

		_yCurr = _yCurr + 23
		ui_infield "A", x4, _yCurr, 90, _infHeight ui_tooltip `Размер 1`
	
	else	! Rectangle
		if not(bPanelSide1) and not(bPanelSide2) then
			ui_pict 9, xPict, yPict, PictWidth, PictHeight, 1
		else
			if bPanelSide1 and bPanelSide2 then
				ui_pict 6, xPict, yPict, PictWidth, PictHeight, 1
			else
				if not(bPanelSide1) then
					ui_pict 7, xPict, yPict, PictWidth, PictHeight, 1
				else
					ui_pict 8, xPict, yPict, PictWidth, PictHeight, 1
				endif
			endif
		endif

		_yCurr = yPict - 21
		ui_infield{3} "bPanelSide1", x2 + 177, _yCurr, 90, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Боковая Панель`, 0,
						"", `Боковая Панель`, 1
		ui_infield{3} "bPanelSide2", x2 + 85, _yCurr, 90, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Боковая Панель`, 0,
						"", `Боковая Панель`, 1
	
		_yCurr = _yCurr + 57
		ui_infield "B", x7, _yCurr, 90, _infHeight ui_tooltip `Размер 2`

		_yCurr = _yCurr + 72
		ui_infield "A", x2 + 117, _yCurr, 90, _infHeight ui_tooltip `Размер 1`
	endif
return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray	[_idx] = "gs_2D_representation"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_detlevel_2D"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_detailLevel2D_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_cont_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_contourPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_fill_type"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillType_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_fill_pen"
		childFlagReprArray	[_idx] = 1			
		iconNameReprArray	[_idx] = "ui_list_fillPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_back_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillBgPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_3D_representation"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "iDetlevel3D"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_detailLevel3D_flat(1)"
		
		if iDetlevel3D <> DETLEVEL3D_OFF then
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "gs_shadow"
			childFlagReprArray	[_idx] = 1
			iconNameReprArray	[_idx] = "ui_list_shadow_flat(1)"
		endif
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_material"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_glass_mat"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_surface_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_frame_mat"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_surface_flat(1)"
	
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID		= TABID_RepresentationAndSurfaces,
													paramNameArray	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
