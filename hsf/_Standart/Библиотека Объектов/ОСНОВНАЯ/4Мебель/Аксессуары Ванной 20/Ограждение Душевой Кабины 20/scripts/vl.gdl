
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Shower Styles
! -----------------------------------------------------------------------------

values{2} "iDoorStyle"	DOORSTYLE_CORNER,		stDoorStyles[1],
						DOORSTYLE_RECTANGLE,	stDoorStyles[2]

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------

values{2} "iDetlevel3D"	DETLEVEL3D_DETAILED,	typRes[1],
						DETLEVEL3D_OFF,			typRes[2]

! -----------------------------------------------------------------------------
! 2D detail levels
! -----------------------------------------------------------------------------

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

! -----------------------------------------------------------------------------
! VALUES, LOCK, HIDE
! -----------------------------------------------------------------------------

if iDoorStyle = DOORSTYLE_CORNER then
	values "a" range [0.3, )
	values "b" range [0.3, )
	corlength = SQR ((a - xsl) ^ 2 + (b - ysl) ^ 2) ! corner element length
	wFrameMax = min (xsl/2, ysl/2, corlength/2, zzyzx/2)
	values "wFrame" range [0.01, wFrameMax)
else
	values "a" range [0.2, )
	values "b" range [0.2, )
	wFrameMax = min (a/2, b/2, zzyzx/2)
	values "wFrame" range [0.01, wFrameMax)
	hideparameter "xsl", "ysl"
endif

values "xsl" range [0.2, a - 0.1]
values "ysl" range [0.2, b - 0.1]
values "zzyzx" range [0.2, )

if gs_cont_pen = 0 then parameters gs_cont_pen = 4
if gs_fill_pen = 0 then parameters gs_fill_pen = 19

if not(bPanelSide1) then
	lock "ysl"
	hideparameter "ysl"
endif

if not(bPanelSide2) then
	lock "xsl"
	hideparameter "xsl"
endif
