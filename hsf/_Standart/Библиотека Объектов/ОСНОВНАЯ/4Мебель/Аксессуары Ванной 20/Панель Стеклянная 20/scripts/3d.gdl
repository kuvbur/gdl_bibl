

if iDetlevel3D = DETLEVEL3D_OFF then end
if not(gs_shadow) then SHADOW OFF

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

pen gs_cont_pen
gosub iPaneStyle

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Glass Pane Simple
! -----------------------------------------------------------------------------

1:

	material gs_frame_mat

	addy b
	rotx 90
	for i=1 to 2
		prism_ 5,tFrame,
			0, 0, 15,
			0, zzyzx, 15,
			wFrame, zzyzx-wFrame, 15,
			wFrame, wFrame, 15,
			0, 0, -1
		base
		vert 0, 0, 0
		vert 0, 1, 0
		vert 1, 0, 0
		vert 0, 0, 1
		coor 2, -1, -2, -3, -4
		body -1
		addx a
		mulx -1
	next i
	del 4

	for i=1 to 2
		prism_ 5, tFrame,
			0, 0, 15,
			a, 0, 15,
			a-wFrame, wFrame, 15,
			wFrame, wFrame, 15,
			0, 0, -1
		addy zzyzx
		muly -1
	next i
	del 4

	if gs_glass_mat=0 then end
	body -1
	material gs_glass_mat

	add wFrame, wFrame, (tFrame-tGlass)/2
	block a-2*wFrame, zzyzx-2*wFrame, tGlass
	body -1

	del 3

return

! -----------------------------------------------------------------------------
! Glass Pane Sliding
! -----------------------------------------------------------------------------

2:
	ho = zzyzx
	bm = (a - 4 * wFrame)/3 + 2*wFrame

	addy b
	rotx 90

	material gs_frame_mat

	for i=1 to 3
		for k=1 to 2
			prism_ 5, tFrame,
				0, 0, 15,
				0, zzyzx, 15,
				wFrame, zzyzx-wFrame, 15,
				wFrame, wFrame, 15,
				0,0,-1
			base
			vert 0, 0, 0
			vert 0, 1, 0
			vert 1, 0, 0
			vert 0, 0, 1
			coor 2, -1, -2, -3, -4
			body -1
			addx bm
			mulx -1
		next k
		del 4

		for k=1 to 2
			prism_ 5, tFrame,
				0, 0, 15,
				bm, 0, 15,
				bm-wFrame, wFrame, 15,
				wFrame, wFrame, 15,
				0,0,-1
			base
			vert 0, 0, 0
			vert 1, 0, 0
			vert 0, 0, 1
			vert 0, 1, 0
			coor 2, -1, -2, -3, -4
			body -1
			addy zzyzx
			muly -1
		next k
		del 4

		addx bm - wFrame
		addz tFrame + tGap
	next i

	if gs_glass_mat = 0 then end
	body -1
	material gs_glass_mat

	del 7

	rotx 90
	add wFrame, wFrame, tFrame/2-tGlass/2

	for i=1 to 3 step 1
		block bm-2*wFrame, ho-2*wFrame, tGlass
		addx bm - wFrame
		addz tFrame + tGap
	next i

	body -1
	del 9

return

