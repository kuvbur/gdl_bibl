
pen gs_cont_pen
fill gs_fill_type

! -----------------------------------------------------------------------------
! 2D Hotspots
! -----------------------------------------------------------------------------

unID = 1

hotspot2  0,	0,	unID : unID = unID + 1				! middle

if iTowelRackStyle = TOWELRACKSTYLE_SIMPLE then
	hotspot2  -a/2 + b/4,		0,	unID : unID = unID + 1				! left overhang
	hotspot2   a/2 - b/4,		0,	unID : unID = unID + 1				! right overhang

	hotspot2  -a/2,		-b/2,	unID : unID = unID + 1				! left overhang
	hotspot2   a/2,		-b/2,	unID : unID = unID + 1				! left overhang
endif

if iTowelRackStyle = TOWELRACKSTYLE_MULTIPLE then
	hotspot2  -b/2,		0,	unID : unID = unID + 1				! left overhang
	hotspot2   b/2,		0,	unID : unID = unID + 1				! right overhang

	add2 0, -b/2
	rot2 -angArm
	add2 -b/2, b/2
	hotspot2     0,   -b/2,	unID, a, 1+128  : unID = unID + 1			! end
	hotspot2    -1,   -b/2,	unID, a, 3 : unID = unID + 1				! end
	hotspot2     a,   -b/2,	unID, a, 2 : unID = unID + 1				! end
	del 3

	hotspot2  			 0, 			-b/2, 				unID, angArm, 6 + 512  : unID = unID + 1	! rotate
	hotspot2  			 a/2, 			-b/2, 				unID, angArm, 4 + 128 : unID = unID + 1	! rotate
	hotspot2  (a/2)*COS(-angArm),  (a/2)*SIN(-angArm)-b/2, 	unID, angArm, 5  : unID = unID + 1 	! rotate
endif

if iTowelRackStyle = TOWELRACKSTYLE_RING then
	hotspot2 0, 0, unID : unID = unID + 1

	hotspot2 0, 0, unID, B, 1 + 128 : unID = unID + 1
	hotspot2 0, 1, unID, B, 3 : unID = unID + 1
	hotspot2 0, -B, unID, B, 2 : unID = unID + 1

	hotspot2 0, -ringPosition, unID, ringRad, 1 + 128 : unID = unID + 1
	hotspot2 -1, -ringPosition, unID, ringRad, 3 : unID = unID + 1
	hotspot2 ringRad, -ringPosition, unID, ringRad, 2 : unID = unID + 1

	hotspot2 0, -ringPosition, unID, ringRad, 1 + 128 : unID = unID + 1
	hotspot2 1, -ringPosition, unID, ringRad, 3 : unID = unID + 1
	hotspot2 -ringRad, -ringPosition, unID, ringRad, 2 : unID = unID + 1
endif

if iTowelRackStyle = TOWELRACKSTYLE_HOOK then
	hotspot2 0, 0, unID : unID = unID + 1

	hotspot2 0, 0, unID, B, 1 + 128 : unID = unID + 1
	hotspot2 0, 1, unID, B, 3 : unID = unID + 1
	hotspot2 0, -B, unID, B, 2 : unID = unID + 1

	hotspot2 0, -B, unID, hookRad, 1 + 128 : unID = unID + 1
	hotspot2 -1, -B, unID, hookRad, 3 : unID = unID + 1
	hotspot2 hookRad, -B, unID, hookRad, 2 : unID = unID + 1

	hotspot2 0, -B, unID, hookRad, 1 + 128 : unID = unID + 1
	hotspot2 1, -B, unID, hookRad, 3 : unID = unID + 1
	hotspot2 -hookRad, -B, unID, hookRad, 2 : unID = unID + 1
endif

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

gosub iTowelRackStyle

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Single Bar
! -----------------------------------------------------------------------------

1:

	add2 -a/2 + b/4, -b/2
	! --- Railing ---
	poly2_b      5, 1+2+4+64,      gs_fill_pen,     gs_back_pen,
			0,		-B/8,	1,
			A-B/2,	-B/8,	1,
			A-B/2,	 B/8,	1,
			0,		 B/8,	1,
			0,		-B/8,	-1

	! --- Left End Support Overhang ---
	for k = 1 to 2
		add2 (a - b/2) * (k-1), 0
		poly2_b      4, 1+2+4+64,      gs_fill_pen,     gs_back_pen,
			0,		B/4,	1,
			B/4,	B/2,	1,
		   -B/4,	B/2,	1,
			0,		B/4,	-1

		poly2_b      2, 1+2+4+64,      gs_fill_pen,     gs_back_pen,
			0,		0,		900,
			B/4,	360,	4001
		del 1
	next k
	del 1
	
return


! -----------------------------------------------------------------------------
! Multiple Bars
! -----------------------------------------------------------------------------

2:

	R = B/6

	add2 0, -b/2
	rot2 -angArm
	poly2_b      5, 1+2+4+64,    gs_fill_pen,     gs_back_pen,
		0,		-R,	1,
		A-B/2,	-R,	1,
		A-B/2,	 R,	1,
		0,		 R,	1,
		0,		-R,	-1
	del 2

	rot2 -90
	poly2_b      7, 1+2+4+64,    gs_fill_pen,     gs_back_pen,
		0,		-B/2,	1,
		B/2,	-B/2,	1,
		B/2,	   0,	900,
		B/2,	180,	4000,
		B/2,	B/2,	1,
		0,		B/2,	1,
		0,	   -B/2,	-1
	del 1
	
return

! -----------------------------------------------------------------------------
! Ring
! -----------------------------------------------------------------------------

3:	

	add2 0, -ringPosition
	
	poly2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-A / 2 + ringThk / 2, ringThk / 2, 1,
		-A / 2 + ringThk / 2, 0, 901,
		ringThk / 2, 180, 4001,
		A / 2 - ringThk / 2, -ringThk / 2, 1,
		A / 2 - ringThk / 2, 0, 901,
		ringThk / 2, 180, 4001,
		-A / 2 + ringThk / 2, ringThk / 2, -1
	
	del 1
	
	cornerRad = baseThk / 3
	
	poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-baseWidth / 2, 0, 1,
		-baseWidth / 2, -baseThk + cornerRad, 1,
		-baseWidth / 2 + cornerRad, -baseThk + cornerRad, 901,
		cornerRad, 90, 4001,
		baseWidth / 2 - cornerRad, -baseThk, 1,
		baseWidth / 2 - cornerRad, -baseThk + cornerRad, 901,
		cornerRad, 90, 4001,
		baseWidth / 2, 0, 1,
		-baseWidth / 2, 0, -1
		
	add2 0, -baseThk
	
	poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-baseRad1, 0, 1,
		-baseRad1, -baseThk + cornerRad, 1,
		-baseRad1 + cornerRad, -baseThk + cornerRad, 901,
		cornerRad, 90, 4001,
		baseRad1 - cornerRad, -baseThk, 1,
		baseRad1 - cornerRad, -baseThk + cornerRad, 901,
		cornerRad, 90, 4001,
		baseRad1, 0, 1,
		-baseRad1, 0, -1
		
	add2 0, -baseThk
		
	poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-baseRad2, 0, 1,
		-baseRad2, -baseThk2 + cornerRad, 1,
		-baseRad2 + cornerRad, -baseThk2 + cornerRad, 901,
		cornerRad, 90, 4001,
		baseRad2 - cornerRad, -baseThk2, 1,
		baseRad2 - cornerRad, -baseThk2 + cornerRad, 901,
		cornerRad, 90, 4001,
		baseRad2, 0, 1,
		-baseRad2, 0, -1
		
	del 2
		
return

! -----------------------------------------------------------------------------
! Hook
! -----------------------------------------------------------------------------

4:

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-A / 2, 0, 1,
		-A / 2, -B, 1,
		A / 2, -B, 1,
		A / 2, 0, 1,
		-A / 2, 0, -1
		
	line2 -A / 2, -B + frontRingThk, A / 2, -B + frontRingThk
	line2 -A / 2, -B + frontRingThk + frontGapThk, A / 2, -B + frontRingThk + frontGapThk
		
	add2 0, -dentPosition
	
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, 0, 901,
		dentRad, 360, 4001
		
		
	del 1

return


