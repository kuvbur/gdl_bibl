
if iDetlevel3D = DETLEVEL3D_OFF then end
if not(gs_shadow) then SHADOW OFF

if iDetlevel3D = DETLEVEL3D_SIMPLE then  gs_resol=8

! --- 3D Feedback to editing ---
if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then
	iDetlevel3D = DETLEVEL3D_SIMPLE
endif

! -----------------------------------------------------------------------------
! 3D Hotspots
! -----------------------------------------------------------------------------

UnID = 1

if iTowelRackStyle = TOWELRACKSTYLE_SIMPLE then
	add -a/2 + b/4, -b/2, 0
	roty inc_angle
	hotspot (length-B/2)/2,	B/2,	0,	1				! middle
	hotspot 0,				B/2,	0,	3				! left overhang
	hotspot length-B/2,		B/2,	0,	5				! right overhang

	hotspot -b/4,			0,		0,	2,	length,	1+256	! left end
	hotspot length-B/4,		0,		0,	4,	length,	2		! right end
	hotspot	-b,				0,		0,	6,	length,	3		! ref. point
	del 2
endif

if iTowelRackStyle = TOWELRACKSTYLE_MULTIPLE then
	hotspot 0, 0,     0,	unID, zzyzx, 	1+256 : unID=unID+1
	hotspot 0, 0,    -1,	unID, zzyzx, 	3 : unID=unID+1
	hotspot 0, 0, zzyzx,	unID, zzyzx, 	2 : unID=unID+1

	add 0, -b/2, 0
	rotz -angArm
	add -b/2, b/2, 0
	hotspot  0, -b/2, 0,	unID, a, 	1+128 : unID=unID+1
	hotspot -1, -b/2, 0,	unID, a, 	3 : unID=unID+1
	hotspot  a, -b/2, 0,	unID, a, 	2 : unID=unID+1

	hotspot  0, -b/2, zzyzx,	unID, a, 	1+128 : unID=unID+1
	hotspot -1, -b/2, zzyzx,	unID, a, 	3 : unID=unID+1
	hotspot  a, -b/2, zzyzx,	unID, a, 	2 : unID=unID+1
	del 3

	hotspot  			 0, 			-b/2, 			   -1, unID, angArm, 7 + 512  : unID = unID + 1	! rotate
	hotspot  			 0, 			-b/2, 				zzyzx/2, unID, angArm, 6 + 512  : unID = unID + 1	! rotate
	hotspot  			 a/2, 			-b/2, 				zzyzx/2, unID, angArm, 4 + 128 : unID = unID + 1	! rotate
	hotspot  (a/2)*COS(-angArm),  (a/2)*SIN(-angArm)-b/2, 	zzyzx/2, unID, angArm, 5  : unID = unID + 1 	! rotate
endif

if iTowelRackStyle = TOWELRACKSTYLE_RING then
	hotspot 0, 0, 0, unID : unID = unID + 1
	hotspot 0, -B, -A + ringThk / 2, unID : unID = unID + 1

	hotspot 0, 0, 0, unID, B, 1 + 128 : unID = unID + 1
	hotspot 0, 1, 0, unID, B, 3 : unID = unID + 1
	hotspot 0, -B, 0, unID, B, 2 : unID = unID + 1

	hotspot 0, -ringPosition, -A / 2 + ringThk / 2, unID, ringRad, 1 + 128 : unID = unID + 1
	hotspot -1, -ringPosition, -A / 2 + ringThk / 2, unID, ringRad, 3 : unID = unID + 1
	hotspot ringRad, -ringPosition, -A / 2 + ringThk / 2, unID, ringRad, 2 : unID = unID + 1

	hotspot 0, -ringPosition, -A / 2 + ringThk / 2, unID, ringRad, 1 + 128 : unID = unID + 1
	hotspot 1, -ringPosition, -A / 2 + ringThk / 2, unID, ringRad, 3 : unID = unID + 1
	hotspot -ringRad, -ringPosition, -A / 2 + ringThk / 2, unID, ringRad, 2 : unID = unID + 1
endif

if iTowelRackStyle = TOWELRACKSTYLE_HOOK then
	hotspot 0, 0, 0, unID : unID = unID + 1

	hotspot 0, 0, 0, unID, B, 1 + 128 : unID = unID + 1
	hotspot 0, 1, 0, unID, B, 3 : unID = unID + 1
	hotspot 0, -B, 0, unID, B, 2 : unID = unID + 1

	hotspot 0, -B, 0, unID, hookRad, 1 + 128 : unID = unID + 1
	hotspot -1, -B, 0, unID, hookRad, 3 : unID = unID + 1
	hotspot hookRad, -B, 0, unID, hookRad, 2 : unID = unID + 1

	hotspot 0, -B, 0, unID, hookRad, 1 + 128 : unID = unID + 1
	hotspot 1, -B, 0, unID, hookRad, 3 : unID = unID + 1
	hotspot -hookRad, -B, 0, unID, hookRad, 2 : unID = unID + 1
endif

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

pen gs_cont_pen
resol gs_resol

gosub iTowelRackStyle

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================



! -----------------------------------------------------------------------------
! Single Bar
! -----------------------------------------------------------------------------


1:

	material matArm

	add -a/2 + b/4, -b/2, 0
	roty inc_angle

	! --- Left End Support Overhang ---
	s2 = sqr(2)

	sphere B/4
	rotz 90
	revolve 2, 360, 2,
		B/4.1,  0, 0,
		B/2,  B/4, 0
	del 1

	! --- Railing ---
	roty 90
	cylind length-B/2, B/8
	del 1

	! --- Right End Support Overhang ---
	addx length - B/2
	sphere B/4
	rotz 90
	revolve 2, 360, 2,
		B/4.1,  0, 0,
		B/2,  B/4, 0
	del 4

return

! -----------------------------------------------------------------------------
! Multiple Bars
! -----------------------------------------------------------------------------

2:

	h1 = min(zzyzx/10, 0.01)
	h2 = max((zzyzx - 4*h1)/iArmNum, 0)
	R = B/6

	while R > h2/4 do

		iArmNum = iArmNum - 1
		h1 = min(zzyzx/10, 0.01)
		h2 = max((zzyzx - 4*h1)/iArmNum, 0)
		R = B/6

	endwhile

	material matArm
	resol 1.5*gs_resol
	for i = 0 to 1
		rotz -90
		addz (zzyzx-h1) * i
		extrude 6,0,0,h1,1+2+4+16+32,
			0,-B/2,0,
			B/2,-B/2,1,
			B,0,1001,
			B/2,B/2,1001,
			0,B/2,0,
			0,-B/2,-1
		del 2
	next i

	rotz -90
	addx B/2
	resol gs_resol
	cylind zzyzx, R
	del 1

	addx B/2
	rotz -angArm + 90
	addz 2*h1 + h2/2

	if abs(h2) < EPS then end

	for i= 1 to iArmNum
		resol gs_resol
		armc R,R,A-B/2,0,0,90
		addx A-B/2
		sphere R
		del 1
		addz h2
	next i
	del iArmNum

	material matRevolver
	for i= 1 to iArmNum
		addz -h2/2
		cylind min(h2/10,(h2/2-R/2)/2),R*1.02
		del 1
		addz h2/2
		mulz -1
		cylind min(h2/10,(h2/2-R/2)/2),R*1.02
		del 2
		addz h2
	next i
	del iArmNum
	del 4
	
return

! -----------------------------------------------------------------------------
! Ring
! -----------------------------------------------------------------------------

3:

	material matChrome
	rotx 90
	hprism_ matChrome, matChrome, matChrome, matChrome,
			5, baseThk, 0, baseThk / 3, 1,
			-baseWidth / 2, -baseHeight / 2, 79,
			baseWidth / 2, -baseHeight / 2, 79,
			baseWidth / 2, baseHeight / 2, 79,
			-baseWidth / 2, baseHeight / 2, 79,
			-baseWidth / 2, -baseHeight / 2, 79

	addz baseThk
	hprism_ matChrome, matChrome, matChrome, matChrome,
			2, baseThk, 0, baseThk / 3, 1,
			0, 0, 979,
			baseRad1, 360, 4079
	
	addz baseThk
	hprism_ matChrome, matChrome, matChrome, matChrome,
		2, B - baseThk * 2, 0, baseThk / 3, 1,
		0, 0, 979,
		baseRad2, 360, 4079

	pathRad = A / 2 - ringThk / 2
	delta = 180 / gs_resol
	alpha = -delta
	
	for i = 1 to gs_resol * 2 + 3
		put pathRad * cos(alpha), pathRad * sin(alpha), 0, 0
		alpha = alpha + delta
	next i
	
	del 2
	
	add 0, -pathRad, ringPosition
	tube 2, NSP / 4, 0,
		0, 0, 901,
		ringThk / 2, 360, 4001,
		get(NSP)

	del 2

return

! -----------------------------------------------------------------------------
! Hook
! -----------------------------------------------------------------------------

4:	

	add 0, -dentPosition, ZZYZX / 2 + dentRad / 4
	roty 90
	
	cutpolya 2, 1, 0,
		0, 0, 979,
		dentRad, 360, 4079
		
	del 2
	
	addy -B
	rotx -90
	material matChrome
	cylind frontRingThk, frontRingRad
	addz frontRingThk
	cylind frontGapThk, frontGapRAd
	addz frontGapThk
	cylind B - frontRingThk - frontGapThk, A / 2
	
	del 3
	cutend
	
return

