
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Полотенцедержателя`
ui_current_page gs_ui_current_page

UI_PAGE TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionsPage"

UI_PAGE TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

UI_PAGE TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! ==============================================================================
end
! ==============================================================================


! ------------------------------------------------------------------------------
!Subroutines
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"StyleAndDimensionsPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	xPict = 230
	yPict = pageStartY + 46
	PictWidth = 121 
	PictHeight = 72
	_infHeight = 19
	_outfHeight = 15
	_offsetY = 4
	x1 = 0
	x2 = 222
	x3 = 230
	x4 = 354
	x5 = 384
	
	_yCurr = pageStartY + 8
	ui_style 0, 1
	ui_outfield `Тип Полотенцедержателя`, x1, _yCurr, 150, _outfHeight
	ui_outfield `Размеры`, x3, _yCurr, 150, _outfHeight
	ui_style 0, 0
	ui_separator x2, 0, x2, 122

	ui_infield{3} "iTowelRackStyle", 76, 23, 135, 96,
		2, 1, 4, 1,
		90, 90, 120, 90,
		1, stStyles[1],	TOWELRACKSTYLE_SIMPLE,
		2, stStyles[2],	TOWELRACKSTYLE_MULTIPLE,
		3, stStyles[3],	TOWELRACKSTYLE_RING,
		4, stStyles[4],	TOWELRACKSTYLE_HOOK
	
	if iTowelRackStyle = TOWELRACKSTYLE_SIMPLE then
		ui_pict 3, xPict, yPict, PictWidth, PictHeight, 1
		_yCurr = yPict + 3
		ui_infield "inc_angle", x4, _yCurr, 90, _infHeight ui_tooltip `Угол Наклона`
		_yCurr = _yCurr + 48
		ui_infield "length", x4, _yCurr, 90, _infHeight ui_tooltip `Длина Поручня`
	endif
	
	if iTowelRackStyle = TOWELRACKSTYLE_MULTIPLE then
		ui_pict 4, xPict, yPict, PictWidth, PictHeight, 1
		_yCurr = yPict - 21
		ui_infield "iArmNum", x5, _yCurr, 60, _infHeight ui_tooltip `Количество Спиц`
		ui_outfield `Количество Спиц`, x3,  _yCurr + _offsetY, 150, _outfHeight
		_yCurr = _yCurr + 72
		ui_infield "angArm", x4, _yCurr, 90, _infHeight ui_tooltip `Угол`
	endif
	
	if iTowelRackStyle = TOWELRACKSTYLE_RING then
		ui_pict 5, xPict, yPict, PictWidth, PictHeight, 1
		_yCurr = yPict + 3
		ui_infield "ringRad", x4, _yCurr, 90, _infHeight ui_tooltip `Радиус Кольца`
	endif
	
	if iTowelRackStyle = TOWELRACKSTYLE_HOOK then
		ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1
		_yCurr = yPict + 3
		ui_infield "hookRad", x4, _yCurr, 90, _infHeight ui_tooltip `Радиус Крючка`
	endif
return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray	[_idx] = "gs_2D_representation"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_cont_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_contourPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_fill_type"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillType_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_fill_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_back_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillBgPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_3D_representation"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "iDetlevel3D"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_detailLevel3D_flat(1)"
		
		if iDetlevel3D = DETLEVEL3D_DETAILED then
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "gs_resol"
			childFlagReprArray	[_idx] = 1
			iconNameReprArray	[_idx] = "ui_list_resol_flat(1)"
		endif
		
		if iDetlevel3D <> DETLEVEL3D_OFF then
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "gs_shadow"
			childFlagReprArray	[_idx] = 1
			iconNameReprArray	[_idx] = "ui_list_shadow_flat(1)"
		endif
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_material"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		if iTowelRackStyle = 1 or iTowelRackStyle = 2 then	! Single Bar, Multiple Bars
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "matArm"
			childFlagReprArray	[_idx] = 1
			iconNameReprArray	[_idx] = "ui_list_surface_flat(1)"
		endif

		if iTowelRackStyle = 2 then	! Multiple Bars
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "matRevolver"
			childFlagReprArray	[_idx] = 1
			iconNameReprArray	[_idx] = "ui_list_surface_flat(1)"
		endif

		if iTowelRackStyle = 3 or iTowelRackStyle = 4 then	! Ring, Hook
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "matChrome"		
			childFlagReprArray	[_idx] = 1		
			iconNameReprArray	[_idx] = "ui_list_surface_flat(1)"
		endif

	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray
return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
