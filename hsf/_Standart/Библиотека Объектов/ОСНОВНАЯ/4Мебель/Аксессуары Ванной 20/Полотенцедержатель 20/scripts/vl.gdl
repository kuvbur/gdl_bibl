
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------

values{2} "iDetlevel3D"	DETLEVEL3D_DETAILED,	typRes[1],
						DETLEVEL3D_SIMPLE,		typRes[2],
						DETLEVEL3D_OFF,			typRes[3]
values "gs_resol" RANGE [4,)

if iDetlevel3D = DETLEVEL3D_SIMPLE then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

if  iDetlevel3D = DETLEVEL3D_OFF then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif

! -----------------------------------------------------------------------------
! Towel Rack Styles
! -----------------------------------------------------------------------------

values{2} "iTowelRackStyle"	TOWELRACKSTYLE_SIMPLE,		stStyles[1],
							TOWELRACKSTYLE_MULTIPLE,	stStyles[2],
							TOWELRACKSTYLE_RING,		stStyles[3],
							TOWELRACKSTYLE_HOOK,		stStyles[4]

! -----------------------------------------------------------------------------
! Default Sizes
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "iTowelRackStyle" then
	a = defaultSizes[iTowelRackStyle][1]
	b = defaultSizes[iTowelRackStyle][2]
	zzyzx = defaultSizes[iTowelRackStyle][3]
	parameters a = a, b = b, zzyzx = zzyzx	
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if iTowelRackStyle = TOWELRACKSTYLE_SIMPLE then
	values "a" range [0.1,)
	values "b" range [0.04, a/2]
	values "inc_angle" RANGE [0, 90)

	ZZYZX = b/2
	parameters ZZYZX = ZZYZX
	lock "ZZYZX"
	hideparameter "iArmNum", "angArm", "matRevolver", "matChrome", "hookRad", "ringRad"	
	lock "iArmNum", "angArm", "matRevolver", "matChrome", "hookRad", "ringRad"
else
	if iTowelRackStyle = TOWELRACKSTYLE_MULTIPLE then	
		values "a" RANGE [0.1, )
		values "b" RANGE [0.05, zzyzx]
		values "zzyzx" RANGE [0.1, )
		values "iArmNum" RANGE [1, )
		values "angArm" RANGE [0, 180]
		hideparameter "inc_angle", "length", "matChrome", "hookRad", "ringRad"
		lock "inc_angle", "length", "matChrome", "hookRad", "ringRad"
	else	
		hideparameter "iArmNum", "angArm", "matRevolver", "inc_angle", "length", "matArm"
		lock "iArmNum", "angArm", "matRevolver", "inc_angle", "length", "matArm"
	endif	
endif

if iTowelRackStyle = TOWELRACKSTYLE_SIMPLE | iTowelRackStyle = TOWELRACKSTYLE_MULTIPLE then
	if GLOB_MODPAR_NAME = "length" then
		a = cos (inc_angle)* length
		parameters a = a
	else
		length = a/cos (inc_angle)
		parameters length = length
	endif
	ac_bottomlevel	= 0
	ac_toplevel		= ZZYZX
endif

if iTowelRackStyle = TOWELRACKSTYLE_RING then
	values "A" range [0.04, ]
	values "B" range [0.04, ]
	values "ZZYZX" range [0.04 + baseHeight / 2 - ringThk / 2, ]
	values "ringRad" range [0.02, ]

	hideparameter "hookRad"
	lock "hookRad"

	if GLOB_MODPAR_NAME = "ringRad" then
		A = ringRad * 2
	else
		ringRad = A / 2
	endif

	if GLOB_MODPAR_NAME = "ZZYZX" then
		A = ZZYZX - baseHeight / 2 + ringThk / 2
		ringRad = A / 2
	else
		ZZYZX = A + baseHeight / 2 - ringThk / 2
	endif
	ac_bottomlevel	= -A + ringThk / 2
	ac_toplevel		= baseHeight / 2
endif

if iTowelRackStyle = TOWELRACKSTYLE_HOOK then
	values "A" range [0.03, ]
	values "B" range [0.07, ]
	values "ZZYZX" range [0.03, ]
	values "hookRad" range [0.015, ]

	hideparameter "ringRad"
	lock "ringRad"

	if GLOB_MODPAR_NAME = "hookRad" then
		A = hookRad * 2
	else
		hookRad = A / 2
	endif

	if GLOB_MODPAR_NAME = "ZZYZX" then
		A = ZZYZX
		hookRad = A / 2
	else
		ZZYZX = A
	endif

	ac_bottomlevel	= -ZZYZX / 2
	ac_toplevel		= ZZYZX / 2
endif

parameters	ac_bottomlevel = ac_bottomlevel,
		  	ac_toplevel = ac_toplevel,
			A = A,
			B = B,
			ZZYZX = ZZYZX,
			ringRad = ringRad,
			hookRad = hookRad

if gs_cont_pen = 0 then parameters gs_cont_pen = 7
if gs_fill_pen = 0 then parameters gs_fill_pen = 91

