
pen gs_cont_pen
fill gs_fill_type

unID = 1
gosub irailingstyle

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Style 1
! -----------------------------------------------------------------------------
1:
	! --- Railing ---

	if iSymbType = SYMBTYPE_DETAILED  then
		poly2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
				0, -lengthWallDistance - Dia/2,3,
				A, -lengthWallDistance - Dia/2,3,
				A, -lengthWallDistance + Dia/2,3,
				0, -lengthWallDistance + Dia/2,3,
				0, -lengthWallDistance - Dia/2,3
	else
		line2 	0,	-lengthWallDistance,	A,	-lengthWallDistance
	endif

	! --- First Support ---

	add2 RightDist, 0
	gosub "support"
	del 1

	gosub "hotspotA"
	gosub "hotspotRightDist"

	if nr > 1 then
		add2 A, 0
		gosub "hotspotLeftDist"
		del 1

		add2 A - LeftDist, 0
		gosub "support"
		del 1
	endif

	if nr > 2 then
		add2 RightDist, 0
		for i = 1 TO nr-2
			add2 ((A - LeftDist) - RightDist) / (Nr - 1), 0
			gosub "support"
		next i
		del (nr-2)
		del 1
	endif

return

! -----------------------------------------------------------------------------
! Style 2
! -----------------------------------------------------------------------------
2:
	! --- Railing ---

	if iSymbType = SYMBTYPE_DETAILED  then
		poly2_b 12, 1+2+64, gs_fill_pen, gs_back_pen,
				0,			-lengthWallDistance - Dia/2,	1,
				a,			-lengthWallDistance - Dia/2,	1,
				a,			-lengthWallDistance + Dia/2,	1,
				0,			-lengthWallDistance + Dia/2,	1,
				0,			-lengthWallDistance + Dia * 2,	900,
				Dia * 1.5,	-90,					4001,
				-Dia * 1.5,	0,						1,
				-Dia * 2.5,	0,						1,
				-Dia * 2.5,	-lengthWallDistance + Dia * 2,	1,
				0,			-lengthWallDistance + Dia * 2,	900,
				Dia * 2.5,	90,						4001,
				0,		  	-lengthWallDistance - Dia/2,	1
	else
		line2 	0,		-lengthWallDistance,				A,		-lengthWallDistance
		arc2 	0,		-lengthWallDistance + Dia * 2,		Dia * 2,	180,						270
		line2 	-Dia * 2,	0,									-Dia * 2,	-lengthWallDistance + Dia * 2
	endif

	! --- First Support ---

	add2 RightDist, 0
	gosub "support"
	del 1

	gosub "hotspotA"
	gosub "hotspotRightDist"
	hotspot2 	-Dia*2,		0,	uniD: unID = unID+1

	if nr > 1 then
		add2 a, 0
		gosub "hotspotLeftDist"
		del 1

		add2 a-LeftDist, 0
		gosub "support"
		del 1
	endif

	if nr > 2 then
		add2 RightDist, 0
		for i = 1 to nr-2
			add2 ((a-LeftDist)-RightDist)/(Nr-1), 0
			gosub "support"
		next i
		del (Nr-2)
		del 1
	endif

return

! -----------------------------------------------------------------------------
! Style 3
! -----------------------------------------------------------------------------
3:
	! --- Railing ---

	if iSymbType = SYMBTYPE_DETAILED  then
		poly2_b 12, 1+2+64, gs_fill_pen, gs_back_pen,
				0,			-lengthWallDistance - Dia/2,	1,
				a,			-lengthWallDistance - Dia/2,	1,
				a,			-lengthWallDistance + Dia/2,	1,
				0,			-lengthWallDistance + Dia/2,	1,
				0,			-lengthWallDistance + Dia * 2,	900,
				Dia * 1.5,	-90,					4001,
				-Dia * 1.5,	0,						1,
				-Dia * 2.5,	0,						1,
				-Dia * 2.5,	-lengthWallDistance + Dia * 2,	1,
				0,			-lengthWallDistance + Dia * 2,	900,
				Dia * 2.5,	90,						4001,
				0,		  	-lengthWallDistance - Dia/2,	1
	else
		line2 	0,			-lengthWallDistance,			A,			-lengthWallDistance
		arc2 	0,			-lengthWallDistance + Dia * 2,	Dia * 2,		180,						270
		line2 	-Dia * 2,		0,						-Dia * 2,		-lengthWallDistance + Dia * 2
	endif

	hotspot2 	-Dia*2,		0,	unID: unID=unID+1
	gosub "hotspotA"

	add2 a, 0
	gosub "hotspotLeftDist"
	del 1

	add2 a-LeftDist, 0
	gosub "support"
	del 1

return

! -----------------------------------------------------------------------------
! Support
! -----------------------------------------------------------------------------
"support":

	hotspot2 0, 0, UniD : unID=unID+1

	if iSymbType = SYMBTYPE_DETAILED then
		poly2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
			-Dia/2,		0,						1,
			 Dia/2,		0,						1,
			 Dia/2,		-lengthWallDistance + Dia/2,	1,
			-Dia/2,		-lengthWallDistance + Dia/2,	1,
			-Dia/2,		0,					    -1
	else
		line2 0, 0,	0,	-lengthWallDistance
	endif

return

! -----------------------------------------------------------------------------
! hotspot editings A, LeftDist, RightDist
! -----------------------------------------------------------------------------

"hotspotA":
	hotspot2 	0,		-lengthWallDistance ,	unID,	A,	1+256	: unID=unID+1
	hotspot2 	A,		-lengthWallDistance ,	unID,	A,	2		: unID=unID+1
	hotspot2 	-1,		-lengthWallDistance ,	unID,	A,	3		: unID=unID+1
return

"hotspotLeftDist":
	hotspot2 	0,			0,	unID,	LeftDist,	1+128	: unID=unID+1
	hotspot2 	-LeftDist,	0,	unID,	LeftDist,	2		: unID=unID+1
	hotspot2 	1,			0,	unID,	LeftDist,	3		: unID=unID+1
return

"hotspotRightDist":
	hotspot2 	0,			0,	unID,	RightDist,	1+128	: unID=unID+1
	hotspot2 	RightDist,	0,	unID,	RightDist,	2		: unID=unID+1
	hotspot2 	-1,			0,	unID,	RightDist,	3		: unID=unID+1
return



