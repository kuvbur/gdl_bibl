
if iDetlevel3D = DETLEVEL3D_OFF then end
if not(gs_shadow) then SHADOW OFF
if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then iDetlevel3D = DETLEVEL3D_SIMPLE

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

_elbowResol = ceil(gs_resol/4)

pen gs_cont_pen
resol gs_resol

unID = 1
gosub irailingstyle

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Style 1
! -----------------------------------------------------------------------------
1:
	! --- Railing ---

	material RailingMat

	roty inc_angle
	rotx 90
	addz lengthWallDistance
	roty 90

	if iDetlevel3D = DETLEVEL3D_DETAILED then
		cylind RightDist - Dia*0.7, Dia*0.5
		addz RightDist + Dia*0.7
		cylind length - (LeftDist + Dia*0.7)-(RightDist + Dia*0.7), Dia*0.5
		addz length - LeftDist - RightDist 
		cylind LeftDist - Dia*0.7, Dia*0.5
		del 2
	else
		add -Dia/2,-Dia/2,0
		block Dia, Dia, length
		del 1
	endif

	gosub "hotspotLength"
	del 3

	! --- Right End Support Overhang/ Htps ---

	add 0, 0, -Dia * 3.2
	gosub "hotspotRightDist"

	addx RightDist

	if NR = 1 then
		gosub "support"
		del 1
	else

		! --- Right End Support Overhang ---

		gosub "support"
		del 1

		addx length
		gosub "hotspotLeftDist"
		del 1

		! --- Left End Support Overhang ---

		addx length - LeftDist
		gosub "support"
		del 1

		addx RightDist

		for i = 1 TO Nr-2
			addx ((length-LeftDist)-RightDist)/(Nr-1)
			gosub "support"
		next i

	endif
	del top

return

! -----------------------------------------------------------------------------
! Style 2
! -----------------------------------------------------------------------------
2:
	! --- Railing ---

	material RailingMat

	roty inc_angle
	rotx 90
	addz lengthWallDistance
	roty 90


	if iDetlevel3D = DETLEVEL3D_SIMPLE then
		add -Dia/2,-Dia/2,-2.5*Dia
		block Dia, Dia, length + 2.5*Dia
		del 1
	endif

	gosub "hotspotLength"

	del 3

	! --- ELBOW / Railing ---

	addy -lengthWallDistance + Dia * 2
	addx -Dia*2
	rotx 90
	resol gs_resol

	if iDetlevel3D = DETLEVEL3D_DETAILED then 
		addx 2 * Dia 
		rotx 90
		rotz 90

		gosub "railing"

		del 3
	endif

	del 3

	addx -Dia*2
	rotx 90
	resol gs_resol

	if iDetlevel3D = DETLEVEL3D_SIMPLE then
		add -Dia/2,-Dia/2,0
		block Dia,Dia,lengthWallDistance - Dia/2
		del 1
	endif

	HOTSPOT 0,	0,	0,	unID : unID=unID+1

	del 2

	! --- Right End Support Overhang / Htps ---

	add 0, 0, -Dia*3.2
	gosub "hotspotRightDist"

	addx RightDist

	if NR = 1 then
		gosub "support"
		del 1
	else

		! --- Right End Support Overhang / Htps ---

		gosub "support"
		del 1

		addx length
		gosub "hotspotLeftDist"
		del 1

		! --- Left End Support Overhang ---

		addx length - LeftDist
		gosub "support"
		del 1

		addx RightDist
		for i = 1 to Nr-2
			addx ((length-LeftDist)-RightDist)/(Nr-1)
			gosub "support"
		next i

	endif
	del top

return

! -----------------------------------------------------------------------------
! Style 3
! -----------------------------------------------------------------------------
3:
	! --- Railing ---

	material RailingMat

	roty inc_angle
	rotx 90
	addz lengthWallDistance
	roty 90

	if iDetlevel3D = DETLEVEL3D_SIMPLE then
		add -Dia/2,-Dia/2,-2.5*Dia
		block Dia,Dia,length+2.5*Dia
		del 1
	endif

	gosub "hotspotLength"
	del 3

	! --- ELBOW / Railing ---


	if iDetlevel3D = DETLEVEL3D_DETAILED then 
		resol gs_resol
		material RailingMat
		addy -lengthWallDistance + Dia * 2 
		rotz -90
		rotx 180

		gosub "railing"

		del 3
		resol gs_resol
		material supMat
		rotx -90
		addx -2 * Dia
		addz - largeDia*0.1
		cylind largeDia*0.1, largeDia*0.5
		del 3
		rotx 90
	else
		addx -Dia * 2
		rotx 90
		resol gs_resol
		add -Dia/2,-Dia/2,0
		block Dia,Dia,lengthWallDistance - Dia/2
	del 3
	endif

	HOTSPOT -2 * Dia, 0, 0,	uniD : unID=unID+1

	del 1

	! --- Left End Support Overhang ---

	add 0, 0, -Dia*3.2
	addx length-LeftDist
	gosub "support"
	del 1

	addx length
	gosub "hotspotLeftDist"

	del top

return


! ==============================================================================
! Railing for Style2, Style3
! ------------------------------------------------------------------------------
! Input Parameters:
! Dia: 					diameter of the railing (Length)
! lengthWallDistance: 	the distance of the railing from the wall (Length)
! irailingstyle:		style of the railing (Integer)
! largeDia:				diameter of the support base, [3*Dia] (Length)
! _elbowResol: 			decreased resolution, the resolution of the elbow (Integer)
! ...
! Output:
! ...
! ==============================================================================

"railing":

	__largeDiaBase = 0
	if irailingstyle = RAILING_STYLE3 then __largeDiaBase = 1

	put -Dia * 10-lengthWallDistance,	Dia * 2,	0,	0,
		-lengthWallDistance + Dia * 2 + (largeDia * 0.1)*__largeDiaBase, Dia * 2,	0,	0

	for i = 0 to _elbowResol
		_currAlpha = (90 / _elbowResol) * i

		put (Dia * 2) * sin(_currAlpha),	(Dia * 2) * cos(_currAlpha),	0, 0
	next i

	put Dia * 2, -length, 0, 0,
		Dia * 2, -length - 1, 0, 0
	tube 2, (NSP) / 4, 2 + 16 + 32,
				0, 0, 900,
				Dia/2, 360, 4000,

				get(NSP)
return

! -----------------------------------------------------------------------------
! Support
! -----------------------------------------------------------------------------
"support":
	material supMat

	if iDetlevel3D = DETLEVEL3D_DETAILED then
		rotx 90

		! --- Flange ---
		RESOL gs_resol
		cylind largeDia*0.1, largeDia*0.5

		! --- Elbow ---
		rotx 180
		roty 90
		addy -(Dia/2 +  largeDia*0.5)
		addx lengthWallDistance - 2 * Dia		
		put -Dia * 10-lengthWallDistance,	Dia * 2,	0,	0,
			-lengthWallDistance + Dia * 2 + largeDia * 0.1, Dia * 2,	0,	0

		for jj = 0 to _elbowResol
			_currAlpha = (90/_elbowResol) * jj

			put (Dia*2) * sin(_currAlpha),	(Dia*2) * cos(_currAlpha),	0, 0
		next jj

		put Dia * 2, -Dia , 0, 0,
			Dia * 2, -1, 0, 0
		tube 2, (NSP)/4, 2+16+32,
					0, 0, 900,
					Dia/2, 360, 4000,

					get(NSP)
		del 4

		addz lengthWallDistance - 2 * Dia
		rotz 90
		add Dia*2, 0, Dia*2
		roty 90

		! --- Railing Sleeve ---
		add 0, -Dia*0.7, Dia*1.2
		rotx -90
		cylind Dia*1.4, Dia*0.6
		del 7

	else

		add -Dia/2, -lengthWallDistance + Dia/2, -Dia/2
		rotx 90
		block Dia, 3.5*Dia-largeDia*0.1, Dia
		del 2

		add -Dia/2, -lengthWallDistance + Dia/2, -Dia/2
		block Dia,lengthWallDistance - Dia/2, Dia
		del 1

	endif

return

! -----------------------------------------------------------------------------
! hotspot editings Length, LeftDist, RightDist
! -----------------------------------------------------------------------------

"hotspotLength":
	HOTSPOT		0, 0,	0,		unID,	length,	1+256	: unID=unID+1
	HOTSPOT 	0, 0,	length,	unID,	length,	2		: unID=unID+1
	HOTSPOT 	0, 0,	-1,		unID,	length,	3		: unID=unID+1
return

"hotspotLeftDist":
	HOTSPOT 	0,			0, 0,	unID,	LeftDist,	1+128	: unID=unID+1
	HOTSPOT 	-LeftDist,	0, 0,	unID,	LeftDist,	2		: unID=unID+1
	HOTSPOT 	1,			0, 0,	unID,	LeftDist,	3		: unID=unID+1
return

"hotspotRightDist":
	HOTSPOT 	0,			0, 0,	unID,	RightDist,	1+128	: unID=unID+1
	HOTSPOT 	RightDist,	0, 0,	unID,	RightDist,	2		: unID=unID+1
	HOTSPOT 	-1,			0, 0,	unID,	RightDist,	3		: unID=unID+1
return


