
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						= - 1
_idxTab = 1
TABID_Style						= _idxTab  : _idxTab = _idxTab + 1
TABID_Dimensions				= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Защитного Поручня`
ui_current_page gs_ui_current_page

UI_PAGE TABID_Style, TABID_Root, `Стиль`, "uiTab_Style_flat(1)"
	gosub "StylePage"

UI_PAGE TABID_Dimensions, TABID_Root, `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "DimensionsPage"

UI_PAGE TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

UI_PAGE TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! ==============================================================================
end
! ==============================================================================


! ------------------------------------------------------------------------------
!Subroutines
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"StylePage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	ui_infield{3} "irailingstyle", 0, pageStartY, 444, 260,
		1, 1,
		3, 1,
		105, 125, 90, 90,
		1, "", 1,
		2, "", 2,
		3, "", 3
return

! ------------------------------------------------------------------------------
"DimensionsPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	xPict = 91
	yPict = 58
	PictWidth = 181 
	PictHeight = 129
	_infHeight = 19
	_outfHeight = 15
	_offsetY = 4
	x1 = 0
	x2 = xPict + 10
	x3 = xPict + 68
	x4 = xPict + 105
	x5 = xPict + PictWidth + 3
	dy = 24
	_yCurr = PageStartY

	if irailingstyle = RAILING_STYLE1 then
		ui_pict 2, xPict, yPict, PictWidth, PictHeight,1
	endif
	if irailingstyle = RAILING_STYLE2 then
		ui_pict 3, xPict, yPict, PictWidth, PictHeight,1
	endif
	if irailingstyle = RAILING_STYLE3 then
		ui_pict 4, xPict, yPict, PictWidth, PictHeight,1
	endif

	if irailingstyle = RAILING_STYLE1 or irailingstyle = RAILING_STYLE2 then
		ui_infield "Nr", x3, PageStartY, 60, _infHeight ui_tooltip `Количество Креплений`

		_yCurr = _yCurr + _offsetY
		ui_outfield `Количество Креплений`, x1, _yCurr, x3 - 2, _outfHeight

		ui_separator x1, 25, 444, 25
	endif

	_yCurr = yPict - 4	
	ui_infield "inc_angle", x5, _yCurr, 90, _infHeight ui_tooltip `Угол Наклона`

	_yCurr = _yCurr + dy	
	ui_infield "Dia", x5, _yCurr, 90, _infHeight ui_tooltip `Диаметр Поручня`

	_yCurr = _yCurr + dy	
	ui_infield "length", x5, _yCurr, 90, _infHeight ui_tooltip `Длина Поручня`

	dy = 49
	_yCurr = _yCurr + dy	
	ui_infield "lengthWallDistance", x5, _yCurr, 90, _infHeight ui_tooltip `Расстояние до Стены`

	dy = 39
	_yCurr = _yCurr + dy
	ui_infield "RightDist", x4, _yCurr, 90, _infHeight ui_tooltip `Смещение Правого Конца от Крепления`

	if irailingstyle = RAILING_STYLE1 or irailingstyle = RAILING_STYLE2 then 
		ui_infield "LeftDist", x2, _yCurr, 90, _infHeight ui_tooltip `Смещение Левого Конца от Крепления`
	endif

return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray	[_idx] = "gs_2D_representation"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "iSymbType"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_detailLevel2D_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_cont_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_contourPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_fill_type"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillType_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_fill_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_back_pen"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_fillBgPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray	[_idx] = "gs_3D_representation"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "iDetlevel3D"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_detailLevel3D_flat(1)"
		
		if iDetlevel3D = DETLEVEL3D_DETAILED then
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "gs_resol"
			childFlagReprArray	[_idx] = 1
			iconNameReprArray	[_idx] = "ui_list_resol_flat(1)"
		endif
		
		if iDetlevel3D <> DETLEVEL3D_OFF then
			_idx = _idx + 1
			paramNameReprArray	[_idx] = "gs_shadow"
			childFlagReprArray	[_idx] = 1
			iconNameReprArray	[_idx] = "ui_list_shadow_flat(1)"
		endif
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "materials"
		childFlagReprArray	[_idx] = 0
		iconNameReprArray	[_idx] = ""
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "RailingMat"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_surface_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray	[_idx] = "SupMat"
		childFlagReprArray	[_idx] = 1
		iconNameReprArray	[_idx] = "ui_list_surface_flat(1)"
	
	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID		= TABID_RepresentationAndSurfaces,
													paramNameArray	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray
return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
