
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------
values{2} "irailingstyle"	RAILING_STYLE1, stStyles[1],
							RAILING_STYLE2, stStyles[2],
							RAILING_STYLE3, stStyles[3]

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------
values{2} "iDetlevel3D"	DETLEVEL3D_DETAILED,	typRes[1],
						DETLEVEL3D_SIMPLE,		typRes[2],
						DETLEVEL3D_OFF,			typRes[3]

values "gs_resol" range [3,)


if iDetlevel3D = DETLEVEL3D_SIMPLE then hideparameter "gs_resol"
if iDetlevel3D = DETLEVEL3D_OFF then hideparameter "gs_resol", "gs_shadow"

! -----------------------------------------------------------------------------
! Symbol Types
! -----------------------------------------------------------------------------
values{2} "iSymbType"	SYMBTYPE_SIMPLE ,stSymbType[2],
						SYMBTYPE_DETAILED, stSymbType[1]


! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------
values "a" range [0.36,)
values "inc_angle" range [0, 90)
values "lengthWallDistance" range [Dia * 2.8,)

if GLOB_MODPAR_NAME = "length" then
	A = cos (inc_angle)* length
	parameters A = A
else
	length = A/cos (inc_angle)
	parameters length = length
endif

if irailingstyle = RAILING_STYLE1 then
	parameters B = Dia * 2.5 + 3 * Dia * 0.1
	parameters ZZYZX = 3.5 * Dia - 3 * Dia * 0.1
else
	parameters B = Dia * 2.5 + largeDia * 0.1
	parameters ZZYZX = 3.5 * Dia - largeDia * 0.1
endif
lock "B", "ZZYZX"

values "length" range [RightDist + LeftDist + 3 * Dia,)
values "Dia" range (0,A/6]
values "Nr" range [1, INT(length / largeDia)]
values "RightDist" range [Dia/2, length - (Nr - 1) * Dia * 3 - LeftDist]
values "LeftDist" range [Dia/2, length - (Nr - 1) * Dia * 3 - RightDist]

if irailingstyle = RAILING_STYLE3 then hideparameter "Nr", "RightDist"

if Nr=1 then hideparameter "LeftDist"

if gs_cont_pen = 0 then parameters gs_cont_pen = 7
if gs_fill_pen = 0 then parameters gs_fill_pen = 91

! -----------------------------------------------------------------------------
! Bottom, Top Level Calculation
! -----------------------------------------------------------------------------

if iDetlevel3D = DETLEVEL3D_DETAILED then
	if irailingstyle = RAILING_STYLE1 then
		topPointRad1	= Dia * 0.5
		alpha1			= ASN(Dia * 0.5 / topPointRad1)
		topPoint1		= topPointRad1 * SIN(alpha1 - inc_angle)

		alpha2			= ATN((Dia * 0.6) / (RightDist - Dia * 1.4 / 2))
		topPointRad2	= Dia * 0.6 / SIN(alpha2)
		topPoint2		= topPointRad2 * SIN(alpha2 - inc_angle)

		ac_toplevel = MAX(topPoint1, topPoint2)
	endif
	if irailingstyle = RAILING_STYLE2 then
		alpha1			= ATN((Dia * 0.6) / (RightDist - Dia * 1.4 / 2))
		topPointRad1	= Dia * 0.6 / SIN(alpha1)
		topPoint1		= topPointRad1 * SIN(alpha1 - inc_angle)

		topPointRad2	= Dia * 2
		alpha2			= ASN(Dia * 2 / topPointRad2)
		topPoint2		= topPointRad2 * COS(alpha2 - inc_angle) + Dia / 2

		ac_toplevel = MAX(topPoint1, topPoint2)
	endif
	if irailingstyle = RAILING_STYLE3 then
		topPointRad1	= Dia * 2
		alpha1			= ASN(Dia * 2 / topPointRad1)
		topPoint1		= topPointRad1 * COS(alpha1 - inc_angle)

		ac_toplevel = topPoint1 + largeDia / 2
	endif

	bottomPoint1	= -SIN(inc_angle) * length + SIN(inc_angle) * LeftDist - COS(inc_angle) * (Dia * 3.2) - largeDia / 2
	bottomPoint2	= -SIN(inc_angle) * length - COS(inc_angle) * Dia * 0.5

	ac_bottomlevel = MIN(bottomPoint1, bottomPoint2)
else
	if irailingstyle = RAILING_STYLE1 then
		topPointRad1	= Dia * 0.5
		alpha1			= ASN(Dia * 0.5 / topPointRad1)
		topPoint1		= topPointRad1 * SIN(alpha1 - inc_angle)

		ac_toplevel = topPoint1
	endif
	if irailingstyle = RAILING_STYLE2 then
		alpha1			= ATN((Dia * 0.5) / (-Dia * 2.5))
		topPointRad1	= Dia * 0.5 / SIN(alpha1)
		topPoint1		= topPointRad1 * SIN(alpha1 - inc_angle)

		ac_toplevel = topPoint1
	endif
	if irailingstyle = RAILING_STYLE3 then
		alpha1			= ATN((Dia * 0.5) / (-Dia * 2.5))
		topPointRad1	= Dia * 0.5 / SIN(alpha1)
		topPoint1		= topPointRad1 * SIN(alpha1 - inc_angle)

		ac_toplevel = topPoint1
	endif

	bottomPoint1	= -SIN(inc_angle) * length + SIN(inc_angle) * (LeftDist - Dia / 2) - COS(inc_angle) * (Dia * 3.2 + Dia / 2)
	bottomPoint2	= -SIN(inc_angle) * length - COS(inc_angle) * Dia * 0.5

	ac_bottomlevel = MIN(bottomPoint1, bottomPoint2)
endif
parameters	ac_bottomlevel	= ac_bottomlevel,
			ac_toplevel		= ac_toplevel

