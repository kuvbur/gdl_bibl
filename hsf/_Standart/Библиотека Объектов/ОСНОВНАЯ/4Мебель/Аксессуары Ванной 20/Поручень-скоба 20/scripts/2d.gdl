
! ========= Grab Bar.gsm =========

! ========= Hotspots ==========

unID = 1

! --- Htps in origo
if length > 3 * rx then
	hotspot2 0, 		0, 		unID,	b, 1 + 256	: unID = unID + 1
	hotspot2 0, 		-b, 	unID,	b, 2 + 128	: unID = unID + 1
	hotspot2 0, 		2 * b,	unID,	b, 3		: unID = unID + 1
else
	hotspot2 0, 		0,		unID,	b, 1 + 256	: unID = unID + 1
	hotspot2 0, 		-b,		unID,	b, 2		: unID = unID + 1
	hotspot2 0, 		2 * b,	unID,	b, 3		: unID = unID + 1
endif


! --- Length editing
if ABS (inc_angle - 90) > EPS then                                                       
	hotspot2 0,								0,		unID,	half_a, 1 + 128	, length	: unID = unID + 1
	hotspot2 -length * cos (inc_angle) / 2, 0,		unID, 	half_a, 2		, length	: unID = unID + 1		! left overhang
	hotspot2 a,								0,		unID,	half_a, 3		, length	: unID = unID + 1
	
	hotspot2 0,								0,		unID,	half_a, 1 + 128	, length	: unID = unID + 1
	hotspot2 length * cos (inc_angle) / 2, 0,		unID,	half_a, 2		, length	: unID = unID + 1		! right overhang
	hotspot2 -a,							0,		unID,	half_a, 3		, length	: unID = unID + 1
endif

! --- Htps at the corners
if length > 3 * rx then
	hotspot2 cos (inc_angle) * (length / 2 - rx), 	-b,		unID,		b, 2		: unID = unID + 1
	hotspot2 cos (inc_angle) * (length / 2 - rx), 	 0, 	unID,		b, 1 + 128	: unID = unID + 1
	hotspot2 cos (inc_angle) * (length / 2 - rx), 	 b, 	unID,		b, 3		: unID = unID + 1

	hotspot2 -cos (inc_angle) * (length / 2 - rx), 	-b, 	unID,		b, 2		: unID = unID + 1
	hotspot2 -cos (inc_angle) * (length / 2 - rx), 	 0, 	unID,		b, 1 + 128	: unID = unID + 1
	hotspot2 -cos (inc_angle) * (length / 2 - rx),	 b, 	unID,		b, 3		: unID = unID + 1
endif



pen 	gs_cont_pen
fill 	gs_fill_type

if inc_angle <= 15 then
	_overHang = (plateDia - di) / 2

! ========= Plates ==========
	poly2_B		5, 1+2+4+64,	gs_fill_pen, gs_back_pen,
			-a / 2 - di / 2 - _overHang, 	0, 			1,
			-a / 2 - di / 2 - _overHang, 	-plateThk, 	1,
			-a / 2 + di / 2 + _overHang, 	-plateThk, 	1,
			-a / 2 + di / 2 + _overHang, 	0, 			1,
			-a / 2 - di / 2 - _overHang, 	0, 			1

	poly2_B		5, 1+2+4+64,		gs_fill_pen, gs_back_pen,
			a / 2 - di / 2 - _overHang, 	0, 			1,
			a / 2 - di / 2 - _overHang, 	-plateThk, 	1,
			a / 2 + di / 2 + _overHang, 	-plateThk, 	1,
			a / 2 + di / 2 + _overHang, 	0, 			1,
			a / 2 - di / 2 - _overHang, 	0, 			1

! ========= Railing =========
	if b - ror - plateThk < 0 then
		poly2_B  13, 	1+2+4+64, 	gs_fill_pen, gs_back_pen,
				-a/2 + rx, -b + di/2, 	1,
				-a/2 + rx, -b + ror,	901,
				-a/2 + rd1, -plateThk, 	3001,
				-a/2 - rd2, -plateThk, 	1,
				-a/2 + rx, -b + ror,	901,
				-a/2 + rx, -b - di/2,	3001,
				 a/2 - rx, -b - di/2, 	1,
				 a/2 - rx, -b + ror, 	901,
				 a/2 + rd2, -plateThk, 	3001,
				 a/2 - rd1, -plateThk, 	1,
				 a/2 - rx, -b + ror, 	901,
				 a/2 - rx, -b + di/2, 	3001,
				-a/2 + rx, -b + di/2, 	1
	else
		poly2_B 16, 1+2+4+64, 	gs_fill_pen, 	gs_back_pen,
				-a	/ 2 - di / 2, -plateThk, 	1,
				-a / 2 - di / 2, -b + ror, 		1,
				-a / 2 + ror, -b + ror, 		901,
				-a / 2 + ror, -b - di / 2, 		3001,
				a / 2 - ror, -b - di / 2, 		1,
				a / 2 - ror, -b + ror, 			901,
				a / 2 + di / 2, -b + ror, 		3001,
				a / 2 + di / 2, -plateThk, 		1,
				a / 2 - di / 2, -plateThk, 		1,
				a / 2 - di / 2, -b + ror, 		1,
				a / 2 - ror, -b + di / 2, 		3001,
				-a / 2 + ror, -b + di / 2, 		1,
				-a / 2 + ror, -b + ror, 		901,
				-a / 2 + di / 2, -b + ror, 		3001,
				-a / 2 + di / 2, -plateThk, 	1,
				-a / 2 - di / 2, -plateThk, 	1
	endif
else
	PROJECT2{2} 3, 270, 3 + 32 + 1024, gs_back_pen
endif

