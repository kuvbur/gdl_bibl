

! ========= Grab Bar.gsm =========

if iDetlevel3D = DETLEVEL3D_OFF then end
if iDetlevel3D = DETLEVEL3D_SIMPLE then gs_resol = 6

if not(gs_shadow) then shadow off

! ========= Hotspots =========

hotspot	half_a,						0, 0,							204, inc_angle, 4+128
hotspot	(length/2)*cos (inc_angle), 0, -length/2*sin (inc_angle),	2,	 inc_angle, 5		! right end
hotspot 0,							0, 0,							206, inc_angle, 6
hotspot 0,							b, 0,							207, inc_angle, 7

roty inc_angle

hotspot 	0, 				0,		0,	101,	b, 				1+256    		! middle point
hotspot 	0, 				-b,		0,	1,		b,				2
hotspot 	0, 				2*b,	0,	103,	b,				3

hotspot		0,				0,		0,	301,	half_length,	1+128,	length
hotspot 	-half_length,	0,		0,	3,		half_length,	2,		length	! left end
hotspot 	length,			0,		0,	303,	half_length,	3,		length

if length > 3 * rx then
	hotspot -length/2 + rx, 0, 		0, 401,		b,				1+128			! left elbow
	hotspot -length/2 + rx, -b,		0, 4,		b,				2
	hotspot -length/2 + rx, b, 		0, 403,		b,				3

	hotspot length/2 - rx, 	0, 		0, 501, 	b,				1+128			! right elbow
	hotspot length/2 - rx, 	-b, 	0, 5, 		b,				2
	hotspot length/2 - rx, 	b, 		0, 503,		b,				3
endif

unID = 1000

! ========= Tube =========

material tube_mat
pen gs_cont_pen

delta = turn_angle / int(gs_resol / 2)

if abs(rx - ror) < EPS then				!90 elbow
	put -length/2, 1, 0, 1,
		-length/2, 0, 0, 1
else									!point on elbow after cut
	alpha = min(90, turn_angle + delta)
	put -length/2, -b + ror, 0, 1,
		-length/2 + rx - ror * sin(alpha), -b + ror - ror * cos(alpha), 0, 1
endif

for i = int(gs_resol/2) to 0 step -1
	alpha = i * delta
	put -length/2 + rx - ror * sin(alpha), -b + ror - ror * cos(alpha), 0, 1
next i

put length/2 - rx, -b, 0, 1

for i = 1 to gs_resol/2
	alpha = i * delta
	put length/2 - rx + ror * sin(alpha), -b + ror - ror * cos (alpha), 0, 1
next i

if abs(rx - ror) < EPS then				!90 elbow
	put	length/2, 0, 0, 1,
		length/2, 1, 0, 1
else									!point on elbow after cut
	alpha = min(90, turn_angle + delta)
	put length/2 - rx + ror * sin(alpha), -b + ror - ror * cos(alpha), 0, 1,
		length/2, -b + ror, 0, 1
endif

addy -plateThk
cutplane -90
del 1

tube 2, (NSP/4), 16+32,

	0, 0, 900,
	di/2, 360, 4001,

	get(NSP)

cutend


! ========= Plates =========

add -length/2, ror-b, 0
rotx -90

if plateThk > EPS then
	addz b-ror-plateThk
	resol 1.5*gs_resol
	prism_ 2, plateThk,
		0, 0, 979,
		plateDia/2, 360, 4079
	
	add length, 0, 0
	resol 1.5*gs_resol
	prism_ 2, plateThk,
		0, 0, 979,
		plateDia/2, 360, 4079

	del 2
endif

del 2
