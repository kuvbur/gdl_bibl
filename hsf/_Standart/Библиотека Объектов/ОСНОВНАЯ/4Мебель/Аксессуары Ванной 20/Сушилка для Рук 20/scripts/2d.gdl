
! -----------------------------------------------------------------------------
! 2D Hotspots
! -----------------------------------------------------------------------------
unID = 1

hotspot2 -A / 2, 0, unID : unId = unID + 1
hotspot2 0, 0, unID : unId = unID + 1
hotspot2 A / 2, 0, unID : unId = unID + 1

hotspot2 -A / 2, -B, unID : unId = unID + 1
hotspot2 A / 2, -B, unID : unId = unID + 1

! -----------------------------------------------------------------------------
! Types
! -----------------------------------------------------------------------------

fill gs_fill_type
pen gs_cont_pen

if iSymbolType = SYMBTYPE_SIMPLE then
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-A / 2, 0, 1,
		-A / 2, -B, 1,
		A / 2, -B, 1,
		A / 2, 0, 1
else
	gosub iDryerType
endif

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! =============================================================================

! =============================================================================
! SUBROUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Type 1
! -----------------------------------------------------------------------------

1:
	
	poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-A / 2, 0, 1,
		-A / 2, -B + cornerRad, 1,
		-A / 2 + cornerRad, -B + cornerRad, 901,
		cornerRad, 90, 4001,
		A / 2 - cornerRad, -B, 1,
		A / 2 - cornerRad, -B + cornerRad, 901,
		cornerRad, 90, 4001,
		A / 2, 0, 1,
		-A / 2, 0, -1

return

! -----------------------------------------------------------------------------
! Type 2
! -----------------------------------------------------------------------------

2:
	
	add2 -nozzlePos, -(B / 2 + nozzleRad / 2)
		
	poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
		nozzleRad, 0, 1,
		nozzleRad, -nozzleLength, 1,
		0, -nozzleLength, 901,
		nozzleRad, -180, 4001,
		-nozzleRad, 0, 1,
		nozzleRad, 0, -1
		
	del 1

	poly2_b 13, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-A / 2 + roundOff, 0, 1,
		-A / 2 + roundOff, -roundOff, 901,
		roundOff, 90, 4001,
		-A / 2, -B + roundOff, 1,
		-A / 2 + roundOff, -B + roundOff, 901,
		roundOff, 90, 4001,
		A / 2 - roundOff, -B, 1,
		A / 2 - roundOff, -B + roundOff, 901,
		roundOff, 90, 4001,
		A / 2, -roundOff, 1,
		A / 2 - roundOff, -roundOff, 901,
		roundOff, 90, 4001,
		-A / 2 + roundOff, 0, -1
		
	add2 0, -topThk
	
	poly2_b 6, 1, gs_fill_pen, gs_back_pen,
		-A / 2, roundOff, 1,
		-A / 2 + roundOff, roundOff, 901,
		roundOff, 90, 4001,
		A / 2 - roundOff, 0, 1,
		A / 2 - roundOff, roundOff, 901,
		roundOff, 90, 4001
		
	del 1
		
return

! -----------------------------------------------------------------------------
! Type 3
! -----------------------------------------------------------------------------

3:

	poly2_b 13, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-A / 2 + roundOff, 0, 1,
		-A / 2 + roundOff, -roundOff, 901,
		roundOff, 90, 4001,
		-A / 2, -B + roundOff, 1,
		-A / 2 + roundOff, -B + roundOff, 901,
		roundOff, 90, 4001,
		A / 2 - roundOff, -B, 1,
		A / 2 - roundOff, -B + roundOff, 901,
		roundOff, 90, 4001,
		A / 2, -roundOff, 1,
		A / 2 - roundOff, -roundOff, 901,
		roundOff, 90, 4001,
		-A / 2 + roundOff, 0, -1
		
	add2 0, -backThk - insertThk
	
	for i = 1 to 2
	
		poly2_b 6, 1, gs_fill_pen, gs_back_pen,
			-A / 2, roundOff, 1,
			-A / 2 + roundOff, roundOff, 901,
			roundOff, 90, 4001,
			A / 2 - roundOff, 0, 1,
			A / 2 - roundOff, roundOff, 901,
			roundOff, 90, 4001
			
		add2 0, -midThk + insertThk * 2
		mul2 1, -1
		
	next i
	
	del i + 2
		
return
