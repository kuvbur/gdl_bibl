
if iDetlevel3D = DETLEVEL3D_OFF then end
if not(gs_shadow) then SHADOW OFF
! if iDetlevel3D = DETLEVEL3D_SIMPLE then	gs_resol = 8

! --- 3D Feedback to editing ---
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then
	iDetlevel3D = DETLEVEL3D_SIMPLE
endif

! -----------------------------------------------------------------------------
! 3D Hotspots
! -----------------------------------------------------------------------------
unID = 1

add -A / 2, -B, 0

hotspot A, B, 0, unID : unID = unID + 1
hotspot 0, B, 0, unID : unID = unID + 1
hotspot A / 2, B, 0, unID : unID = unID + 1

if iDryerType = DRYERTYPE_AUTOMATIC then
	hotspot A, B, ZZYZX, unID : unID = unID + 1
	hotspot 0, B, ZZYZX, unID : unID = unID + 1
endif

if iDryerType = DRYERTYPE_PUSHBUTTON then
	hotspot A, B, ZZYZX, unID : unID = unID + 1
	hotspot 0, B, ZZYZX, unID : unID = unID + 1
	hotspot A, B-bottomThk, 0, unID : unID = unID + 1
	hotspot 0, B-bottomThk, 0, unID : unID = unID + 1
endif

if iDryerType = DRYERTYPE_HISPEED then
	hotspot A, B, backHeight - topRoundOff, unID : unID = unID + 1
	hotspot 0, B, backHeight - topRoundOff, unID : unID = unID + 1
	hotspot A, B, 0, unID : unID = unID + 1
	hotspot 0, B, 0, unID : unID = unID + 1
endif

del 1

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

pen gs_pen_3d
resol gs_resol

gosub iDryerType

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! =============================================================================

! =============================================================================
! SUBROUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Automatic
! -----------------------------------------------------------------------------

1:

	resol gs_resol

	rotx 90
	addy baseHeight / 2 + nozzleOverhang
	
	material matDryer

	! back plate
	! -----------------------------------------
	prism_ 13, baseThk,
		0, -baseHeight / 2, 75,
		A / 2 - cornerRad, -baseHeight / 2, 75,
		A / 2, -baseHeight / 2 + cornerRad, 1075,
		A / 2, 0, 75,
		A / 2, baseHeight / 2 - cornerRad, 75,
		A / 2 - cornerRad, baseHeight / 2, 1075,
		0, baseHeight / 2, 75,
		-A / 2 + cornerRad, baseHeight / 2, 75,
		-A / 2, baseHeight / 2 - cornerRad, 1075,
		-A / 2, 0, 75,
		-A / 2, -baseHeight / 2 + cornerRad, 75,
		-A / 2 + cornerRad, -baseHeight / 2, 1075,
		0, -baseHeight / 2, -1

	addz baseThk
	cutplane{2} 180, 4
	roty -90

	! round cover
	! -----------------------------------------
	for i = 1 to 2
	
	hprism_ matDryer, matDryer, matDryer, matDryer,
		   2, A / 2, 0, cornerRad, 1,
			0, 0, 978,
			baseHeight / 2, 360, 4078

	mulz -1

	next i

	del i + 1
	cutend

	! nozzle
	! -----------------------------------------
	addx -A / 2
	rotz 90
	rotx 90

	cutpolya 6, 1, 0,
		-baseHeight / 2, 0, 79,
		baseHeight / 2, 0, 79,
		baseHeight / 2, baseThk, 79,
		0, baseThk, 979,
		baseHeight / 2, 180, 4079,
		-baseHeight / 2, 0, -1
	
	del 3

	rotx 90

	if iDetlevel3D = DETLEVEL3D_DETAILED then
		mul 0.77, 0.77, 1
		addy baseThk + (nozzleHeight) * 0.1

		material matPlastic

		prism_ 	6, baseHeight / 2 + nozzleOverhang / 2,
				-nozzleWidth / 2, 0, 72,
				nozzleWidth / 2, 0, 72,
				nozzleRad, nozzleHeight, 72,
				0, nozzleHeight, 972,
				nozzleRad, 180, 4072,
				-nozzleWidth / 2, 0, -1

		cutpolya 6, 1, 0,
				-nozzleWidth / 2, 0, 72,
				nozzleWidth / 2, 0, 72,
				nozzleRad, nozzleHeight, 72,
				0, nozzleHeight, 972,
				nozzleRad, 180, 4072,
				-nozzleWidth / 2, 0, -1

		del 2
	endif

	material matDryer
	
	hprism_ matDryer, matDryer, matDryer, matDryer,
			6, baseHeight / 2 + nozzleOverhang, 0, nozzleOverhang, 1,
			-nozzleWidth / 2, 0, 78,
			nozzleWidth / 2, 0, 78,
			nozzleRad, nozzleHeight, 78,
			0, nozzleHeight, 978,
			nozzleRad, 180, 4078,
			-nozzleWidth / 2, 0, -1

	del 1
	
	if iDetlevel3D = DETLEVEL3D_DETAILED then
	
		cutend
		
		! label
		! -----------------------------------------
		addz baseThk
		rotx -34
		addz baseHeight / 2 - labelThk + 0.0025

		hprism_ matPlastic, matPlastic, matPlastic, matPlastic,
				8, labelThk, 0, labelThk / 2, 1,		
				0, -labelHeight / 2, 78,
				labelWidth / 2 - labelHeight / 2, -labelHeight / 2, 78,
				labelWidth / 2 - labelHeight / 2, 0, 978,
				labelHeight / 2, 180, 4078,
				-labelWidth / 2 + labelHeight / 2, labelHeight / 2, 78,
				-labelWidth / 2 + labelHeight / 2, 0, 978,
				labelHeight / 2, 180, 4078,
				0, -labelHeight / 2, -1

		del 3
	endif
	
	cutend
	del 2

return

! -----------------------------------------------------------------------------
! Push Button
! -----------------------------------------------------------------------------

2:

	material matDryer

	! top and bottom cuts
	! -----------------------------------------
	addz roundOff
	cutplane{2} 180, 4
	addz ZZYZX - roundOff * 2
	cutplane{2} 0, 4
	del 2

	! dryer chassis
	! -----------------------------------------
	roty 90

	for i = 1 to 2

		hprism_ matDryer, matDryer, matDryer, matDryer,
			7, A / 2, 0, roundOff, 1,
			0, 0, 78,
			-ZZYZX, 0, 78,
			-ZZYZX, -topThk, 78,
			-ridgePos - roundOff / 2, -(B - roundOff), 78,
			-ridgePos + roundOff / 2, -(B - roundOff), 1078,
			0, -bottomThk, 78,
			0, 0, -1

	mulz -1

	next i
	
	del i

	cutend
	cutend

	! top and bottom covers + bottom grill
	! -----------------------------------------
	addz ZZYZX - roundOff
	_B = topThk + 0.00122

	for i = 1 to 2

		if i = 2 & iDetlevel3D = DETLEVEL3D_DETAILED then
			
			addy -_B / 2
			mul 0.6, 0.4, 1			
			material matPlastic

			prism_ 13, roundOff * 0.8,
				A / 2 - roundOff, 0, 72,
				A / 2 - roundOff, -roundOff, 972,
				roundOff, -90, 4072,
				A / 2, -_B + roundOff, 72,
				A / 2 - roundOff, -_B + roundOff, 972,
				roundOff, -90, 4072,
				-A / 2 + roundOff, -_B, 72,
				-A / 2 + roundOff, -_B + roundOff, 972,
				roundOff, -90, 4072,
				-A / 2, -roundOff, 72,
				-A / 2 + roundOff, -roundOff, 972,
				roundOff, -90, 4072,
				A / 2 - roundOff, 0, -1

			cutpolya 13, 1, 0,
				A / 2 - roundOff, 0, 72,
				A / 2 - roundOff, -roundOff, 972,
				roundOff, -90, 4072,
				A / 2, -_B + roundOff, 72,
				A / 2 - roundOff, -_B + roundOff, 972,
				roundOff, -90, 4072,
				-A / 2 + roundOff, -_B, 72,
				-A / 2 + roundOff, -_B + roundOff, 972,
				roundOff, -90, 4072,
				-A / 2, -roundOff, 72,
				-A / 2 + roundOff, -roundOff, 972,
				roundOff, -90, 4072,
				A / 2 - roundOff, 0, -1

			material matDryer

			del 2

		endif

		hprism_ matDryer, matDryer, matDryer, matDryer,
			13, roundOff, 0, roundOff, 1,
			A / 2 - roundOff, 0, 78,
			A / 2 - roundOff, -roundOff, 978,
			roundOff, -90, 4078,
			A / 2, -_B + roundOff, 78,
			A / 2 - roundOff, -_B + roundOff, 978,
			roundOff, -90, 4078,
			-A / 2 + roundOff, -_B, 78,
			-A / 2 + roundOff, -_B + roundOff, 978,
			roundOff, -90, 4078,
			-A / 2, -roundOff, 78,
			-A / 2 + roundOff, -roundOff, 978,
			roundOff, -90, 4078,
			A / 2 - roundOff, 0, -1

		_B = bottomThk + 0.001

		del 1
		addz roundOff
		mulz -1

	next i
		
	del i
	
	if iDetlevel3D = DETLEVEL3D_DETAILED then cutend

	! nozzle
	! -----------------------------------------
	roty 90
	addz -A

	cutpolya 6, 1, 0,
		0, 0, 79,
		-ZZYZX, 0, 79,
		-ZZYZX, -topThk, 79,
		-ridgePos - roundOff / 2, -(B - roundOff), 79,
		-ridgePos + roundOff / 2, -(B - roundOff), 79,
		0, -bottomThk, 79,
		0, 0, -1

	del 2

	material matChrome

	rotz -90
	add B / 2 + nozzleRad / 2, -nozzlePos, nozzleHeight * 1.5

	revolve 4, 180, 1+2,
		0, nozzleRad, 1,
		nozzleLength, nozzleRad, 1,
		nozzleLength, 0, 901,
		nozzleRad, -90, 4001

	addx nozzleLength

	cutpolya 2, 1, 0,
		0, 0, 979,
		nozzleRad * 0.8, 360, 4079

	del 1

	mulz -1
	
	prism_ 6, nozzleHeight,
		0, nozzleRad, 78,
		nozzleLength, nozzleRad, 78,
		nozzleLength, 0, 978,
		nozzleRad, -180, 4078,
		0, -nozzleRad, 78,
		0, nozzleRad, -1

	del 3

	cutend

	if iDetlevel3D = DETLEVEL3D_DETAILED then
	
		! button and labels
		! -----------------------------------------

		add -nozzlePos, -bottomThk, nozzleHeight * 0.25
		rotx 90

		hprism_ matPlastic, matPlastic, matPlastic, matPlastic,
				2, buttonRad, 0, buttonRad / 3, 1,
				0, 0, 978,
				buttonRad, 360, 4078

		del 2
		
		add A / 3, 0,  ridgePos + labelHeight
		rotx 81.2
		addz B * 0.94

		hprism_ matPlastic, matPlastic, matPlastic, matPlastic,
				8, labelThk, 0, labelThk / 2, 1,		
				0, -labelHeight / 2, 78,
				labelWidth / 2 - labelHeight / 2, -labelHeight / 2, 78,
				labelWidth / 2 - labelHeight / 2, 0, 978,
				labelHeight / 2, 180, 4078,
				-labelWidth / 2 + labelHeight / 2, labelHeight / 2, 78,
				-labelWidth / 2 + labelHeight / 2, 0, 978,
				labelHeight / 2, 180, 4078,
				0, -labelHeight / 2, -1
				
		del 3
		
	endif
	
	cutend
	
return

! -----------------------------------------------------------------------------
! High Speed
! -----------------------------------------------------------------------------

3:

	! back part
	! -----------------------------------------
	rotx 90
	mulz -1
	addz -backThk

	material matDryer

	hprism_	matDryer, matDryer, matDryer, matDryer,
			6, backThk, 0, roundOff, 1,
			-A / 2, 0, 78,
			A / 2, 0, 78,
			A / 2, backHeight - topRoundOff, 78,
			0, backHeight * 0.7, 978,
			-A / 2, backHeight - topRoundOff, 3078,
			-A / 2, 0, -1
	
	! middle part and inserts
	! -----------------------------------------
	addy midHeight
	cutplane{2} 90, 1
	del 1
	mulz -1

	hprism_	matPlastic, matPlastic, matPlastic, matPlastic,
			6, insertThk, 0, roundOff, 1,
			-A / 2, 0, 78,
			A / 2, 0, 78,
			A / 2, backHeight - topRoundOff, 78,
			0, backHeight * 0.7, 978,
			-A / 2, backHeight - topRoundOff, 3078,
			-A / 2, 0, -1

	del 1
				
	addz -midThk

	hprism_	matPlastic, matPlastic, matPlastic, matPlastic,
			6, insertThk, 0, roundOff, 1,
			-A / 2, 0, 78,
			A / 2, 0, 78,
			A / 2, frontHeight - topRoundOff, 78,
			0, frontHeight * 0.7, 978,
			-A / 2, frontHeight - topRoundOff, 3078,
			-A / 2, 0, -1

	cutend

	! add 0, midHeight, midThk / 2
	! roty -90
	
	! cutpolya 2, 1, 0,
		! 0, 0, 972,
		! (midThk - insertThk * 2) / 2, 360, 4072

	! del 2

	material matPlastic				

	prism_ 5, midThk,
		-A / 2, 0, 15,
		-A / 2, midHeight, 15,
		A / 2, midHeight, 15,
		A / 2, 0, 15,
		-A / 2, 0, -1

	! cutend
	
	! front part
	! -----------------------------------------
	mulz -1
	material matDryer

	hprism_	matDryer, matDryer, matDryer, matDryer,
			6, frontThk, 0, roundOff, 1,
			-A / 2, 0, 78,
			A / 2, 0, 78,
			A / 2, frontHeight - topRoundOff, 78,
			0, frontHeight * 0.7, 978,
			-A / 2, frontHeight - topRoundOff, 3078,
			-A / 2, 0, -1

	if iDetlevel3D = DETLEVEL3D_DETAILED then
	
		! button and labels
		! -----------------------------------------
		add 0, frontHeight * 0.7, frontThk
		
		hprism_ matPlastic, matPlastic, matPlastic, matPlastic,
				8, labelThk, 0, labelThk / 2, 1,		
				0, -labelHeight / 2, 78,
				labelWidth / 2 - labelHeight / 2, -labelHeight / 2, 78,
				labelWidth / 2 - labelHeight / 2, 0, 978,
				labelHeight / 2, 180, 4078,
				-labelWidth / 2 + labelHeight / 2, labelHeight / 2, 78,
				-labelWidth / 2 + labelHeight / 2, 0, 978,
				labelHeight / 2, 180, 4078,
				0, -labelHeight / 2, -1

		del 1
		
	endif
	
	del 5

return
