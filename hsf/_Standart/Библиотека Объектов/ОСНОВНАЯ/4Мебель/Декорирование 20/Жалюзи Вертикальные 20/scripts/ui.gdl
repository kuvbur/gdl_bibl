
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 							=-1
_idxTab = 1
TABID_StyleAndDimensions			= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces		= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions					= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Жалюзи`
ui_current_page gs_ui_current_page

UI_PAGE TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionsPage"

UI_PAGE TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

UI_PAGE TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ------------------------------------------------------------------------------
!Subroutines
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"StyleAndDimensionsPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	xPict = 56
	yPict = 136
	PictWidth = 361
	PictHeight = 42
	dy = 24
	x1 = 0
	x2 = 170
	x3 = xPict - 28
	x4 = xPict + 77

	_yCurr = pageStartY
	ui_infield{3} "iOpOrientation", x2, _yCurr, 90, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", `1 Сторона`, 		ONE_SIDE,
				"", `Обе Стороны`, 	BOTH_SIDES ui_tooltip `Направление Открывания`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Направление Открывания`, x1, _yCurr, x2 - 2, _outfHeight

	_yCurr = _yCurr + dy - _yOffset
	ui_infield "pc", x2, _yCurr, 60, _infHeight  ui_tooltip `Жалюзи Закрыты (%)`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Поворот Ламелей`, x1, _yCurr, x2 - 2, _outfHeight
	ui_outfield `%`, x2 + 63, _yCurr, 27, _outfHeight

	_yCurr = _yCurr + dy - _yOffset
	ui_separator x1, _yCurr, 444, _yCurr

	if iOpOrientation = ONE_SIDE then
		ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1	! ui_VerticalBlind_Style1.png
	else	! BOTH_SIDES
		ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1	! ui_VerticalBlind_Style2.png
	endif

	_yCurr = yPict  - _infHeight - 2
	ui_infield "wc", x3, _yCurr, 90, _infHeight  ui_tooltip `Ширина Ламелей`
	ui_infield "pc", x4, _yCurr, 90, _infHeight  ui_tooltip `Жалюзи Закрыты (%)`

return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"

		if gs_detlevel_3D_m <> DLEV3D_OFF then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_materials"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "hemat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "cumat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID		= TABID_RepresentationAndSurfaces,
													paramNameArray	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray


return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
