
if gs_detlevel_3D_m = DLEV3D_OFF then END

if gs_shadow=0 then SHADOW OFF

pen gs_cont_pen

if iType = RECTANGULAR then
	addy b
	rotx 90
		if ABS(gs_frame_width)<EPS & same_thk then
			cprism_ smat,smat,gs_frame_mat,5,b,
					0,0,15,
					a,0,15,
					a,zzyzx,15,
					0,zzyzx,15,
					0,0,-1
		else	
			material gs_frame_mat
			if bFrameLeft then   ! Left
				prism_	5,b,
						0,         0,             15,
						p_th_left, p_th_low,      15,
						p_th_left, zzyzx-p_th_up, 15,
						0,         zzyzx,         15,
						0,         0,             -1
			endif
	
			if bFrameRight then   ! Right
				prism_	5, b,
						a,            0,            15,
						a,            zzyzx,        15,
						a-p_th_right, zzyzx-p_th_up,15,
						a-p_th_right, p_th_low,     15,
						a,            0,            -1
			endif
	
			base
			vert 0, 0, 0
			vert 0, 1, 0
			vert 0, 0, 1
			vert 1, 0, 0
			coor 2 + 256, -1, -2, -3, -4
			body -1
	
			if bFrameLower then   ! Lower
				prism_	5,b,
						0,            0,        15,
						a,            0,        15,
						a-p_th_right, p_th_low, 15,
						p_th_left,    p_th_low, 15,
						0,            0,        -1
			endif
	
			if bFrameUpper then   ! Upper
				prism_	5,b,
						0,            zzyzx,15,
						a,            zzyzx,15,
						a-p_th_right, zzyzx-p_th_up,15,
						p_th_left,    zzyzx-p_th_up,15,
						0,            zzyzx,-1
			endif

			base
			vert 0, 0, 0
			vert 1, 0, 0
			vert 0, 1, 0
			vert 0, 0, 1
			coor 2 + 256, -1, -2, -3, -4
			body -1

		material smat

		addz b/6
		prism_	5,b*4/6,
				p_th_left,p_th_low,15,
				a-p_th_right,p_th_low,15,
				a-p_th_right,zzyzx-p_th_up,15,
				p_th_left,zzyzx-p_th_up,15,
				p_th_left,p_th_low,-1
		del 1
	endif

	del 2
endif

if iType = ROUND then
	zmer=a
	hotspot 0,-b,zmer
	hotspot 0,0,zmer
	hotspot a,-b,zmer
	hotspot a,0,zmer
	hotspot a/2,-b,zmer
	hotspot a/2,0,zmer

	add a/2,b,a/2
	rotx 90
	resol gs_resol

	if bFrame then
		material gs_frame_mat

		prism_	14,  B,
				a/2,     0,      79,
				0,     0,    900,
				0.0,     180.0,   4000,
				-a/2,    0,      79,
				0,     0,    900,
				0,     180.0,   4001,
				a/2,     0.0,     -1,

				a/2-gs_frame_width,     0,      79,
				0,     0,    900,
				0.0,     180.0,   4000,
				-a/2+gs_frame_width,    0,      79,
				0,     0,    900,
				0,     180.0,   4001,
				a/2-gs_frame_width,     0.0,     -1
		base
		rotz 90
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1
		del 1
		coor 2 + 256, -1, -2, -3, -4
		body -1
	endif

	material smat

	prism_	7,  B/6,
			a/2-gs_frame_width,     0,      79,
			0,     0,    900,
			0.0,     180.0,   4000,
			-a/2+gs_frame_width,    0,      79,
			0,     0,    900,
			0,     180.0,   4001,
			a/2-gs_frame_width,     0.0,     -1
	del 2
endif

if iType = ELLIPTIC then
	add a/2,b,zzyzx/2
	rotx 90
	resol gs_resol

	n=0
	for x=0 to 360 step 7.5
		n=n+1
		put (a/2-gs_frame_width)*cos(x), (zzyzx/2-gs_frame_width)*sin(x), 79
	next x

	material smat
	prism_  n, b*4/6,
			get(nsp)

	if bFrame then
		material gs_frame_mat

		nn=0
		for x=0 to 375 step 7.5
			nn=nn+1
			put (a/2-gs_frame_width)*cos(x), (zzyzx/2-gs_frame_width)*sin(x), 0, 0
		next x

		tube 4, nn, 0,
			 0, 0, 0,
			 -gs_frame_width, 0, 0,
			 -gs_frame_width, b, 0,
			 0, b, 0,
			 get(nsp)
		base
		rotz 90
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1
		del 1
		coor 2 + 256, -1, -2, -3, -4
		body -1
		del 1
	endif
endif
end

