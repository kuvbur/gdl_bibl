
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "9D1ED332-F770-422D-A760-CF2F9DB84D22"
	_startID	= "B43F3F2E-A900-48C0-A32E-F7C21FD0DC4A"
	_endID		= "C0551B28-4116-4FEF-A6B8-BB7762BDF7AA"
gosub "newparameters_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! New Parameters_iType_bFrameUpper_bFrameLower_bFrameRight_bFrameLeft
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		
		bHas =	DELETED_PAR_VALUE("gs_typ", _gs_typ)
		
		if _gs_typ = 1 then		! Rectangular	
			iType = 1
		else
			if _gs_typ = 2 then	!	Round
				iType = 2
			else				!	Elliptic
				iType = 3
			endif
		endif
		
		parameters iType	= iType
		
		bHas =	STORED_PAR_VALUE ("p_th_up",		_p_th_up) |\
				STORED_PAR_VALUE ("p_th_low",		_p_th_low) |\
				STORED_PAR_VALUE ("p_th_right",		_p_th_right) |\
				STORED_PAR_VALUE ("p_th_left",		_p_th_left) |\
				STORED_PAR_VALUE ("gs_frame_width",	_gs_frame_width)
		
		! if Mirror Frame Width is 0 then Frame is off
		if iType = 1 then		! Rectangular
			if _gs_frame_width > EPS or (_p_th_up > EPS and _p_th_low > EPS and _p_th_right > EPS and _p_th_left > EPS) then
				bFrame = 1
			else
				bFrame = 0
			endif
			
		else	!	Round, Elliptic
			if _gs_frame_width > EPS then
				bFrame = 1
			else
				bFrame = 0
			endif
		endif
		
		! if Upper Frame Thickness is 0 then Frame Upper is off
		if _p_th_up > EPS then
			bFrameUpper = 1
		else
			bFrameUpper = 0
		endif
		
		! if Lower Frame Thickness is 0 then Frame Lower is off
		if _p_th_low > EPS then
			bFrameLower = 1
		else
			bFrameLower = 0
		endif
		
		! if Right Frame Thicknes is 0 then Frame on Side 1 is off
		if _p_th_right > EPS then
			bFrameRight = 1
		else
			bFrameRight = 0
		endif
		
		! if Left Frame Thicknes is 0 then Frame on Side 2 is off
		if _p_th_left > EPS then
			bFrameLeft = 1
		else
			bFrameLeft = 0
		endif

		parameters	bFrameUpper		= bFrameUpper,
					bFrameLower		= bFrameLower,
					bFrameRight		= bFrameRight,
					bFrameLeft		= bFrameLeft
		actualGuid = _endID
	endif
return

