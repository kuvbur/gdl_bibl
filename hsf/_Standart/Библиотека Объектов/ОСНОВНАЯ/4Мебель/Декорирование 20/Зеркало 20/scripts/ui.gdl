
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 							=-1
_idxTab = 1
TABID_StyleAndDimensions			= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces		= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions					= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Зеркала`
ui_current_page gs_ui_current_page

UI_PAGE TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionsPage"

UI_PAGE TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

UI_PAGE TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ------------------------------------------------------------------------------
!Subroutines
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"StyleAndDimensionsPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_yOffset = 4
	x1 = 0
	x2 = 123
	x3 = 231
	x4 = 354
	dy = 24

	_yCurr = pageStartY

	if iType = RECTANGULAR then
		ui_infield{3} "same_thk", x1, _yCurr, 444 - 3, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Единая Ширина Рамы`, 0,
						"", `Единая Ширина Рамы`, 1
	_yCurr = _yCurr + dy
	endif

	if same_thk then
		ui_infield{3} "bFrame", x1, _yCurr, x2 - 3, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Рама`, 0,
						"", `Рама`, 1

		ui_infield "gs_frame_width", x2, _yCurr, 90, _infHeight  ui_tooltip `Ширина Рамы`
	else
		ui_infield{3} "bFrameUpper", x1, _yCurr, x2 - 3, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Рама Вверху`, 0,
						"", `Рама Вверху`, 1

		ui_infield "p_th_up", x2, _yCurr, 90, _infHeight  ui_tooltip `Ширина Рамы Вверху`

		ui_infield{3} "bFrameRight", x3, _yCurr, x2 - 3, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Рама Сбоку 1`, 0,
						"", `Рама Сбоку 1`, 1

		ui_infield "p_th_right", x4, _yCurr, 90, _infHeight  ui_tooltip `Ширина Рамы со Стороны 1`

		_yCurr = _yCurr + dy
		ui_infield{3} "bFrameLower", x1, _yCurr, x2 - 3, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Рама Внизу`, 0,
						"", `Рама Внизу`, 1

		ui_infield "p_th_low", x2, _yCurr, 90, _infHeight  ui_tooltip `Ширина Рамы Внизу`

		ui_infield{3} "bFrameLeft", x3, _yCurr, x2 - 3, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Рама Сбоку 2`, 0,
						"", `Рама Сбоку 2`, 1

		ui_infield "p_th_left", x4, _yCurr, 90, _infHeight  ui_tooltip `Ширина Рамы со Стороны 2`

	endif

	ui_infield{3} "iType", 0, 72, 444, 193,
		1, 1,	! ui_Mirror_Style.png
		3, 1,
		105, 125, 90, 90,
		1, `Прямоугольное`,	1,
		2, `Круглое`,			2,
		3, `Эллиптическое`,		3

return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"

		if gs_detlevel_3D_m = DLEV3D_DETAILED and iType <> RECTANGULAR then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
		endif

		if gs_detlevel_3D_m <> DLEV3D_OFF then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "smat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if (same_thk and bFrame) or (not(same_thk) and (bFrameUpper or bFrameLower or bFrameRight or bFrameLeft)) then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_frame_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
