
! Picture

if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_shadow = 0 then SHADOW OFF

addx a/2

pen gs_cont_pen
rotx 90
rotx tiltingAngle

hotspot -a/2, 0, 0
hotspot a/2, 0, 0
if gs_frame <> 0 then
	hotspot -a/2, 0, gs_frame_thk
	hotspot a/2, 0, gs_frame_thk
	hotspot -a/2, zzyzx, gs_frame_thk
	hotspot a/2, zzyzx, gs_frame_thk
else
	hotspot -a/2, 0, B
	hotspot a/2, 0, B
	hotspot -a/2, zzyzx, B
	hotspot a/2, zzyzx, B
endif
hotspot -a/2, zzyzx, 0
hotspot a/2, zzyzx, 0

material gs_frame_mat

if gs_frame = 1 and gs_frame_width > EPS and gs_frame_thk > EPS then	! detailed frame

	add -a/2,0,gs_frame_thk/3

	PRISM_ 5, gs_frame_thk/3,
		0,0,15,
		a,0,15,
		a-gs_frame_width*2/3,gs_frame_width*2/3,15,
		gs_frame_width*2/3,gs_frame_width*2/3,15,
		0,0,-1
	gosub 2000

	PRISM_ 5, gs_frame_thk/3,
		0,zzyzx,15,
		a,zzyzx,15,
		a-gs_frame_width*2/3,zzyzx-gs_frame_width*2/3,15,
		gs_frame_width*2/3,zzyzx-gs_frame_width*2/3,15,
		0,zzyzx,-1
	gosub 2000

	PRISM_ 5, gs_frame_thk/3,
		0,0,15,
		0,zzyzx,15,
		gs_frame_width*2/3,zzyzx-gs_frame_width*2/3,15,
		gs_frame_width*2/3,gs_frame_width*2/3,15,
		0,0,-1
	gosub 1000

	PRISM_ 5, gs_frame_thk/3,
		a,0,15,
		a,zzyzx,15,
		a-gs_frame_width*2/3,zzyzx-gs_frame_width*2/3,15,
		a-gs_frame_width*2/3,gs_frame_width*2/3,15,
		a,0,-1
	gosub 1000


	addz gs_frame_thk/3

	PRISM_ 5, gs_frame_thk/3,
		0,0,15,
		a,0,15,
		a-gs_frame_width/3,gs_frame_width/3,15,
		gs_frame_width/3,gs_frame_width/3,15,
		0,0,-1
	gosub 2000

	PRISM_ 5, gs_frame_thk/3,
		0,zzyzx,15,
		a,zzyzx,15,
		a-gs_frame_width/3,zzyzx-gs_frame_width/3,15,
		gs_frame_width/3,zzyzx-gs_frame_width/3,15,
		0,zzyzx,-1
	gosub 2000

	PRISM_ 5, gs_frame_thk/3,
		0,0,15,
		0,zzyzx,15,
		gs_frame_width/3,zzyzx-gs_frame_width/3,15,
		gs_frame_width/3,gs_frame_width/3,15,
		0,0,-1
	gosub 1000

	PRISM_ 5, gs_frame_thk/3,
		a,0,15,
		a,zzyzx,15,
		a-gs_frame_width/3,zzyzx-gs_frame_width/3,15,
		a-gs_frame_width/3,gs_frame_width/3,15,
		a,0,-1
	gosub 1000

	del 2

endif

if bIsFrame and gs_frame_width > EPS and gs_frame_thk > EPS then	! frame is on

	! === plate ===
	PRISM_ 4,b,
		-a/2 + gs_frame_width, gs_frame_width,       15,
		a/2 - gs_frame_width,  gs_frame_width,       15,
		a/2 - gs_frame_width,  zzyzx-gs_frame_width, 15,
		-a/2 + gs_frame_width, zzyzx-gs_frame_width, 15
	gosub 1000

	! === frame ===
	add -a/2,0,0

	if gs_frame = 2 then g=gs_frame_thk
	if gs_frame = 1 then g=gs_frame_thk/3

	PRISM_ 5,g,
		0,               0,             15,
		a,               0,             15,
		a-gs_frame_width,gs_frame_width,15,
		gs_frame_width,  gs_frame_width,15,
		0,               0,             -1
	gosub 2000

	PRISM_ 5,g,
		0,zzyzx,15,
		a,zzyzx,15,
		a-gs_frame_width,zzyzx-gs_frame_width,15,
		gs_frame_width,zzyzx-gs_frame_width,15,
		0,zzyzx,-1
	gosub 2000

	PRISM_ 5,g,
		0,0,15,
		0,zzyzx,15,
		gs_frame_width,zzyzx-gs_frame_width,15,
		gs_frame_width,gs_frame_width,15,
		0,0,-1
	gosub 1000

	PRISM_ 5,g,
		a,0,15,
		a,zzyzx,15,
		a-gs_frame_width,zzyzx-gs_frame_width,15,
		a-gs_frame_width,gs_frame_width,15,
		a,0,-1
	gosub 1000

	del 1
else
	PRISM_ 4,b,
		-a/2, 0,       15,
		a/2,  0,       15,
		a/2,  zzyzx, 15,
		-a/2, zzyzx, 15
	gosub 1000
endif

if gs_frame_thk < EPS then
	_picOffset = 0
else
	_picOffset = gs_frame_width
endif

DEFINE MATERIAL "pictmat" 2,
	1,1,1

MATERIAL "pictmat"

add -a/2 + bIsFrame * _picOffset,
		bIsFrame * _picOffset,
		b + 0.001

if gs_picture_custom = 0 then
	picture 1, a - 2*bIsFrame * _picOffset, zzyzx - 2*bIsFrame * _picOffset, 1
else
	if gs_picture_name <> "" then
		picture gs_picture_name, a - 2*bIsFrame * _picOffset, zzyzx - 2*bIsFrame * _picOffset, 1
	else
		picture 1, a - 2*bIsFrame * _picOffset, zzyzx - 2*bIsFrame * _picOffset, 1
	endif
endif

del 3

end

1000:

	vert 0, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	vert 1, 0, 0

	coor 2, -1, -2, -3, -4
	base

	body -1

return

2000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4
	base

	body -1

return
