
if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 12

if not(gs_shadow) then shadow off

pen gs_cont_pen
add a/2,b,0

hotspot 0,0,0,1
hotspot 0,0,a/2,2
hotspot 0,0,a,3
hotspot -a/2,0,a/2,4
hotspot a/2,0,a/2,5

hotspot 0,-b,0,6
hotspot 0,-b,a,7
hotspot -a/2,-b,a/2,8
hotspot a/2,-b,a/2,9

addz a/2
rotx 90

material framat
resol gs_resol
prism_      14,  B,
			a/2,     0,      79,
			0,     0,    900,
			0.0,     180.0,   4000,
		   -a/2,    0,      79,
			0,     0,    900,
			0,     180.0,   4001,
			a/2,     0.0,     -1,

			a/2-frw,     0,      79,
			0,     0,    900,
			0.0,     180.0,   4000,
		   -a/2+frw,    0,      79,
			0,     0,    900,
			0,     180.0,   4001,
			a/2-frw,     0.0,     -1


material plamat

prism_      7,  B/6,
			a/2-frw,     0,      79,
			0,     0,    900,
			0.0,     180.0,   4000,
		   -a/2+frw,    0,      79,
			0,     0,    900,
			0,     180.0,   4001,
			a/2-frw,     0.0,     -1

if gs_resol < 5 then
	mul 0.70, 0.70, 1
else
	if gs_resol < 7 then
		mul 0.85, 0.85, 1
	else
		mul 1, 1, 1
	endif
endif

addz B/6 + 0.0001

material numat
for n = 1 to 4
	prism_       5,  0,
			-a/150,a/2-frw-a/80-a/15,15,
			a/150,a/2-frw-a/80-a/15,15,
			a/150,a/2-frw-a/80,15,
			-a/150,a/2-frw-a/80,15,
			-a/150,a/2-frw-a/80-a/15,-1
	rotz 90
next n
del 4

for i = 1 to 2
	rotz 30
	for n = 1 to 4
		prism_       5,  0,
				-a/180,a/2-frw-a/80-a/20,15,
				a/180,a/2-frw-a/80-a/20,15,
				a/180,a/2-frw-a/80,15,
				-a/180,a/2-frw-a/80,15,
				-a/180,a/2-frw-a/80-a/20,-1
		rotz 90
	next n
next i
del 10

if numb then
	define style "your text"  fontType ,a*50,5,0
	set style "your text"

	rotz -30
	for t = 1 to 12
		addy a/2 - frw - a/9
		rotz t * 30
		text 0, 0, t
		del 2
		rotz -30
	next t
	del 13
endif

del 1

material hamat
addz B/6

if gs_detlevel_3D_m = DLEV3D_SIMPLE then
	resolnum = 5
else
	resolnum = 12
endif

resol resolnum
cylind B/2,A/40

addz B/3

rotz -mnt*6

prism_ 7,0,
	-a/100,a/40,15,
	a/100,a/40,15,
	a/80,a/5,15,
	a/200,a/2-frw-a/40,15,
	-a/200,a/2-frw-a/40,15,
	-a/80,a/5,15,
	-a/100,a/40,-1

del 1

addz -b/6
rotz -hrs*30-mnt/2
prism_ 7,0,
	-a/100,a/40,15,
	a/100,a/40,15,
	a/70,a/6,15,
	a/300,a/2-frw-a/7,15,
	-a/300,a/2-frw-a/7,15,
	-a/70,a/6,15,
	-a/100,a/40,-1

