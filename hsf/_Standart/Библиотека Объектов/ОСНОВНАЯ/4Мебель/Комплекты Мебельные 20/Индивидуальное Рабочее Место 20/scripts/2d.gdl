
unID = 1
centerunID = 20000


rot2 180

! ======================================================================
! Desks (with Front Panel, Desk Cabinet, Chair, Keyboard, LCD Monitor)
! ======================================================================

gosub "desk"



! ======================================================================
! Desk Partitions
! ======================================================================

for iPa = 1 to 4
	if existPartition[iPa] then
		partLengthType = iPartitionLength * (iPa = 1 or iPa = 2) + PARLENGTH_DESK * (iPa = 3 or iPa = 4)
		if iTableTopType = TTOP_L_SHAPED then
			if iPa = 4 then
				anglePartititon	= iDeskAngle - 90
				bShowPartEdgeLeft = NOT(bSidePartitions) or (iPartitionLength = PARLENGTH_CUSTOM)
				bShowPartEdgeRight = 1
			endif
			if iPa = 2 then
				anglePartititon	= iDeskAngle
				bShowPartEdgeLeft = NOT(bFrontPartitions) or ((iPartitionLength = PARLENGTH_CUSTOM) and ABS(partitionOffsetCustom) > EPS)
				bShowPartEdgeRight = NOT(bEndPartitions1) or (iPartitionLength = PARLENGTH_CUSTOM)
			endif
			if iPa = 1 then
				anglePartititon	= 0
				bShowPartEdgeLeft = NOT(bSidePartitions) or ((iPartitionLength = PARLENGTH_CUSTOM) and ABS(partitionOffsetCustom) > EPS)
				bShowPartEdgeRight = NOT(bEndPartitions2) or (iPartitionLength = PARLENGTH_CUSTOM)
			endif
			if iPa = 3 then
				anglePartititon	= 90
				bShowPartEdgeLeft = NOT(bFrontPartitions) or (iPartitionLength = PARLENGTH_CUSTOM)
				bShowPartEdgeRight = 1
			endif
		else
			if iPa = 4 then
				anglePartititon	= 90
				bShowPartEdgeLeft = NOT(bFrontPartitions) or (iPartitionLength = PARLENGTH_CUSTOM)
				bShowPartEdgeRight = 1
			endif
			if iPa = 2 then
				anglePartititon	= 0
				bShowPartEdgeLeft = 0
				bShowPartEdgeRight = 0
			endif
			if iPa = 1 then
				anglePartititon	= 0
				bShowPartEdgeLeft = NOT(bEndPartitions2) or (iPartitionLength = PARLENGTH_CUSTOM)
				bShowPartEdgeRight = NOT(bEndPartitions1) or (iPartitionLength = PARLENGTH_CUSTOM)
			endif
			if iPa = 3 then
				anglePartititon	= 90
				bShowPartEdgeLeft = NOT(bFrontPartitions) or (iPartitionLength = PARLENGTH_CUSTOM)
				bShowPartEdgeRight = 1
			endif
		endif

		gosub "partition"
	endif
next iPa

for iPaC = 1 to 3
	if existPartColumn[iPaC] then
		rot2 (iDeskAngle - 90) * (iPaC = 1) * (iTableTopType = TTOP_L_SHAPED)
		add2 partColumnPos[iPaC][1], partColumnPos[iPaC][2]
		deskAngleForPart = 90 + 30 * (iPaC = 2) * (iDeskAngle = 120)
		if iTableTopType = TTOP_L_SHAPED then
			bShowPartEdgeTop		= (iPaC = 1)
			bShowPartEdgeLeft		= (iPaC = 1) + (iPaC = 2)
			bShowPartEdgeBottom		= (iPaC = 2) + (iPaC = 3)
			bShowPartEdgeRight		= (iPaC = 3)
		else
			bShowPartEdgeTop		= 0
			bShowPartEdgeLeft		= (iPaC = 1)
			bShowPartEdgeBottom		= (iPaC = 1) + (iPaC = 3)
			bShowPartEdgeRight		= (iPaC = 3)
		endif

		gosub "partitionColumn"

		del 2
	endif
next iPaC

del 1

! ======================================================================
end
! ======================================================================



"desk":
	call "completeTable_m" parameters all	iTableTopType			= iTableTopType,
											zzyzx					= tableTopHeight,
											iDeskAngle				= iDeskAngle,
											bChair					= 1,
											bCalledAsExtension		= 0,
											bPartOfLayout			= 0,
											bPartOfWsSolo			= 1,
											bEnableDeskHsCorner		= 0,
											bEnableDeskHsHalfEdge	= 0,
											bEnableDeskHsEditingX	= 1,
											bEnableDeskHsEditingY	= 1,
											bEnableDeskHsEditingC	= 1,
											bEnableChairHsEditing 	= 1,
											bMirroredCabPos			= 0,
											chairPos				= chairPos,
											completeTable_unID		= unID,
											stSymbolTypes			= stSymbolTypes,
											returned_parameters completeTable_unID
	unID = completeTable_unID
	hotspot2 a/2, b/2, centerunID : centerunID = centerunID + 1
return

"partition":
	call "deskClosings_m" parameters all	iExtensionType			= DESK_PARTITION,
											partitionFrameThk		= partitionFrameThk,
											bCalledAsExtension		= 0,
											bPartOfLayout			= 0,
											bPartOfWsSolo			= 1,
											bOnlyPartitionColumn	= 0,
											bOnlyPartitionCover		= 0,
											iPartitionLength		= partLengthType,
											lengthPartition			= partLength[iPa],
											bShowPartEdgeLeft		= bShowPartEdgeLeft,
											bShowPartEdgeRight		= bShowPartEdgeRight,
											anglePartititon			= anglePartititon,
											xPartOrigPos			= partPos[iPa][1],
											yPartOrigPos			= partPos[iPa][2],
											deskClosings_unID		= unID,
											stSymbolTypes			= stSymbolTypes,
											returned_parameters deskClosings_unID
	unID = deskClosings_unID
return

"partitionColumn":
	call "deskClosings_m" parameters all	iExtensionType			= DESK_PARTITION,
											partitionFrameThk		= partitionFrameThk,
											iDeskAngle				= deskAngleForPart,
											bCalledAsExtension		= 0,
											bPartOfLayout			= 0,
											bPartOfWsSolo			= 1,
											bOnlyPartitionColumn	= 1,
											bOnlyPartitionCover		= 0,
											bShowPartEdgeTop		= bShowPartEdgeTop,
											bShowPartEdgeLeft		= bShowPartEdgeLeft,
											bShowPartEdgeBottom		= bShowPartEdgeBottom,
											bShowPartEdgeRight		= bShowPartEdgeRight,
											deskClosings_unID		= unID,
											stSymbolTypes			= stSymbolTypes,
											returned_parameters deskClosings_unID
	unID = deskClosings_unID
return





