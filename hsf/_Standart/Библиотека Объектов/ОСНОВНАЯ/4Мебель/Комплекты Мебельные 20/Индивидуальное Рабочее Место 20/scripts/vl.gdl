
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

! ======================================================================
! UI
! ======================================================================

call "ui_tabcontrol" parameters		controlType			= 1,
									nTabs				= 6,
									gs_ui_current_page	= gs_ui_current_page



! ======================================================================
! Object sizing
! ======================================================================

if iTableTopType = TTOP_L_SHAPED then
	if iDeskAngle = 90 then
		if GLOB_MODPAR_NAME = "A" or GLOB_MODPAR_NAME = "B" then
			tableLength1 = a
			tableLength2 = b
			parameters tableLength1 = tableLength1
			parameters tableLength2 = tableLength2
		else
			a = tableLength1
			b = tableLength2
			parameters a = a
			parameters b = b
		endif
	else
		a = tableLength1 + tableLength2 * COS(180 - iDeskAngle)
		b = yPosTopCorner
		parameters a = a
		parameters b = b
	endif

	lock	"a",
			"b"
else
	if GLOB_MODPAR_NAME = "A" or GLOB_MODPAR_NAME = "B" then
		tableLength1	= a
		tableDepth1		= b
		parameters tableLength1 = tableLength1
		parameters tableDepth1 = tableDepth1
	else
		a = tableLength1
		b = tableDepth1
		parameters a = a
		parameters b = b
	endif

	lock			"tableLength2",
					"tableDepth2"

	hideparameter	"tableLength1",
					"tableLength2",
					"tableDepth1",
					"tableDepth2"
endif

zzyzx	= MAX(tableTopHeight, realPartHeight)
parameters zzyzx	= zzyzx
lock	"zzyzx"



! ======================================================================
! Desks
! ======================================================================

if GLOB_MODPAR_NAME = "deskStyle" or GLOB_MODPAR_NAME = "iDeskStyle" then
	if iTableTopType = TTOP_L_SHAPED then
		defaultTableChairDistX	= 0.5
	else
		defaultTableChairDistX	= 0
	endif

	tableChairDistX	= defaultTableChairDistX
	parameters tableChairDistX	= tableChairDistX
endif

call "completeTable_m" parameters all	iTableTopType			= iTableTopType,
										zzyzx					= tableTopHeight,
										iDeskAngle				= iDeskAngle,
										chairPos				= chairPos,
										bCalledAsExtension		= 0,
										bPartOfLayout			= 0,
										bPartOfWsSolo			= 1,
										bEnableBasetypes		= 0,
										bMirroredCabPos			= 0,
										bShowCommonCabParams	= bShowCommonCabParams,
										bShowCabParams			= bShowCabParams,
										bShowExtDeskCabParams	= bShowExtDeskCabParams,
										bKnobNoneType			= 0,
										stSymbolTypes			= stSymbolTypes

values "deskStyle" stDeskStyle
values "iDeskStyle" 1, 2, 3, 4, 5
if GLOB_MODPAR_NAME = "deskStyle" then
	for iDesk = 1 to vardim1(stDeskStyle)
		if deskStyle = stDeskStyle[iDesk] then iDeskStyle = iDesk
	next iDesk
	parameters iDeskStyle = iDeskStyle
else
	deskStyle = stDeskStyle[max(iDeskStyle, 1)]
	parameters deskStyle = deskStyle
endif

if GLOB_MODPAR_NAME = "deskStyle" or GLOB_MODPAR_NAME = "iDeskStyle" then
	if iTableTopType = TTOP_L_SHAPED then
		defaultTableChairDistX	= 0.5
	else
		defaultTableChairDistX	= 0
	endif

	tableChairDistX	= defaultTableChairDistX
	parameters tableChairDistX	= tableChairDistX
endif

if not(gs_bOfficeCab) then
	hideparameter "bKnob"
	lock "bKnob"
endif

if iDeskStyle = DESKSTYLE_STR then
	hideparameter "innerEdgeRad"
	lock "innerEdgeRad"
endif

! ======================================================================
! Partitions
! ======================================================================

call "deskClosings_m" parameters all	iExtensionType			= DESK_PARTITION,
										bCalledAsExtension		= 0,
										bPartOfLayout			= 0,
										bPartOfWsSolo			= 1,
										bShowPartitionParams	= bShowPartitionParams,
										bShowExtCabParams		= bShowExtCabParams,
										bShowExtDeskParams		= bShowExtDeskParams,
										bShowExtDeskChairParams	= bShowExtDeskChairParams,
										bShowExtDeskCabParams	= bShowExtDeskCabParams,
										bShowCommonCabParams	= bShowCommonCabParams,
										bEnableCustPartLength	= 1,
										bKnobNoneType			= 0,
										stSymbolTypes			= stSymbolTypes

if iTableTopType = TTOP_STRAIGHT then
	lock	"bSidePartitions"
else
	values "partitionOffsetCustom" range[0, ]
endif



! ======================================================================
! Dimensions
! ======================================================================

if (iDeskStyle = DESKSTYLE_90STR) or (iDeskStyle = DESKSTYLE_120STR) or (iDeskStyle = DESKSTYLE_STR) then
	iInnerEdge = STRAIGHT
	parameters iInnerEdge = iInnerEdge
endif
if (iDeskStyle = DESKSTYLE_90ROUND) or (iDeskStyle = DESKSTYLE_120ROUND) then
	iInnerEdge = ROUNDED
	parameters iInnerEdge = iInnerEdge
endif



! ======================================================================
! Layout Components Listing
! ======================================================================

parameters iListDeskNum90	= 0
parameters iListDeskNum120	= 0
parameters iListDeskNumRect	= 0
parameters iListDeskCabNum	= 0
parameters iListChairNum	= 0
parameters iListPartNum		= 0

lock	"iListDeskNum90",
		"iListDeskNum120",
		"iListDeskNumRect",
		"iListDeskCabNum",
		"iListChairNum",
		"iListPartNum"

parameters iListDeskNum90	= (iDeskStyle = DESKSTYLE_90STR) or (iDeskStyle = DESKSTYLE_90ROUND)
parameters iListDeskNum120	= (iDeskStyle = DESKSTYLE_120STR) or (iDeskStyle = DESKSTYLE_120ROUND)
parameters iListDeskNumRect	= (iDeskStyle = DESKSTYLE_STR)
parameters iListDeskCabNum	= gs_bOfficeCab * (iTableTopType = TTOP_L_SHAPED) + CEIL(gs_officeCab_m / 2) * (iTableTopType = TTOP_STRAIGHT)
parameters iListChairNum	= 1
parameters iListPartNum		= existPartition[1] + existPartition[2] + existPartition[3] + existPartition[4]


