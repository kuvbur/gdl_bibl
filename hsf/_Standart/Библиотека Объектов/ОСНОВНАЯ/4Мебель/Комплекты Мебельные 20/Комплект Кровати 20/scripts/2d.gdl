
! --- Bed ---

add2 -bedWidth/2, -bedLength

! --- Minimal Space ---
reqBMinSpace = 0
call "fa_minimalspace" parameters all MSType = 2,
									 unID = 3000,
									 A = bedWidth,
									 B = BedLength,
									 MSSide = MSSide,
									 MSSide2 = MSSide2,
									 MSFront = MSFrontBed,
									 MSLength = MSBedside


call "fa_bed" parameters all a	= bedWidth,
				b				= bedLength,
				iBedType		= iBedType,
				bDoubleBed		= bDoubleBed,
				bCalledAsMacro	= 1,
				bEnableDiffFills= 1,
				stSymbolTypes	= stSymbolTypes

del 1

! --- Night Table & Lamp ---

add2 -bedWidth/2 - distBedTable - tableWidth, -tableDepth

if bUniformFill then
	fillTypeNightTable		= fillTypeUniform
	fillPenNightTable		= fillPenUniform
	backPenNightTable		= backPenUniform
	fillTypeLamp			= fillTypeUniform
	fillPenLamp				= fillPenUniform
	backPenLamp				= backPenUniform
endif

call "fa_nightTable" parameters all a	= tableWidth,
						b				= tableDepth,
						iNightTableType	= iNightTableType,
						gs_fill_type	= fillTypeNightTable,
						gs_fill_pen		= fillPenNightTable,
						gs_back_pen		= backPenNightTable,
						bCalledAsMacro	= 1,
						MSType			= 0,
						gs_leg_mat		= matLeg

if iLampOnSides = LAMP_ON_SIDES_TABLE_1 or iLampOnSides = LAMP_ON_SIDES_BOTH then
	add2 tableWidth/2, tableDepth/2
	call "desk_lamp_04_m" parameters all stType	= stType,
								bCalledAsMacro	= 1,
								gs_fill_type	= fillTypeLamp,
								gs_fill_pen		= fillPenLamp,
								gs_back_pen		= backPenLamp,
								a				= lampDiameter,
								b				= lampDiameter,
								a2				= lampDiameter/2,
								unIDStart		= 1000
	del 1
endif

del 1

if iNightTableOnSides = TABLE_ON_BOTHSIDES then
	add2 bedWidth/2 + distBedTable, -tableDepth

	call "fa_nightTable" parameters all a	= tableWidth,
							b				= tableDepth,
							iNightTableType	= iNightTableType,
							gs_fill_type	= fillTypeNightTable,
							gs_fill_pen		= fillPenNightTable,
							gs_back_pen		= backPenNightTable,
							bCalledAsMacro	= 1,
							MSType			= 0,
							gs_leg_mat		= matLeg

	if iLampOnSides = LAMP_ON_SIDES_TABLE_2 or iLampOnSides = LAMP_ON_SIDES_BOTH then
		add2 tableWidth/2, tableDepth/2
		call "desk_lamp_04_m" parameters all stType	= stType,
								bCalledAsMacro	= 1,
								gs_fill_type	= fillTypeLamp,
								gs_fill_pen		= fillPenLamp,
								gs_back_pen		= backPenLamp,
								a				= lampDiameter,
								b				= lampDiameter,
								a2				= lampDiameter/2,
								unIDStart		= 2000
		del 1
	endif

	del 1
endif

! --- Hotspots ---
unID = 100

! - bed dimensions ---
hotspot2 0,		0, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot2 -1,	0, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot2 halfBedWidth, 		0, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot2 0,		0, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot2 1,	0, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot2 -halfBedWidth, 		0, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot2 0,		-bedLength, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot2 -1,	-bedLength, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot2 halfBedWidth, 		-bedLength, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot2 0,		-bedLength, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot2 1,	-bedLength, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot2 -halfBedWidth, 		-bedLength, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot2 halfBedWidth, 0, unID, bedLength, 1+256 : unID = unID + 1
hotspot2 halfBedWidth, 1, unID, bedLength, 3 : unID = unID + 1
hotspot2 halfBedWidth, -bedLength, unID, bedLength, 2 : unID = unID + 1

hotspot2 -halfBedWidth, 0, unID, bedLength, 1+256 : unID = unID + 1
hotspot2 -halfBedWidth, 1, unID, bedLength, 3 : unID = unID + 1
hotspot2 -halfBedWidth, -bedLength, unID, bedLength, 2 : unID = unID + 1

! - table-bed distance ---
if iNightTableOnSides = TABLE_ON_BOTHSIDES then
	hotspot2 halfBedWidth,		-tableDepth, unID, distBedTable, 1+128 : unID = unID + 1
	hotspot2 halfBedWidth-1,	-tableDepth, unID, distBedTable, 3 : unID = unID + 1
	hotspot2 halfBedWidth+distBedTable, 		-tableDepth, unID, distBedTable, 2 : unID = unID + 1

	if distBedTable > EPS then
		hotspot2 halfBedWidth,		0, unID, distBedTable, 1+128 : unID = unID + 1
		hotspot2 halfBedWidth-1,	0, unID, distBedTable, 3 : unID = unID + 1
		hotspot2 halfBedWidth+distBedTable, 		0, unID, distBedTable, 2 : unID = unID + 1
	else
		unID = unID + 3
	endif
else
	unID = unID + 6
endif

hotspot2 -halfBedWidth,		-tableDepth, unID, distBedTable, 1+128 : unID = unID + 1
hotspot2 -halfBedWidth+1,	-tableDepth, unID, distBedTable, 3 : unID = unID + 1
hotspot2 -halfBedWidth-distBedTable, 		-tableDepth, unID, distBedTable, 2 : unID = unID + 1

if distBedTable > EPS then
	hotspot2 -halfBedWidth,		0, unID, distBedTable, 1+128 : unID = unID + 1
	hotspot2 -halfBedWidth+1,	0, unID, distBedTable, 3 : unID = unID + 1
	hotspot2 -halfBedWidth-distBedTable, 		0, unID, distBedTable, 2 : unID = unID + 1
else
	unID = unID + 3
endif

! - table dimensions ---

if iNightTableOnSides = TABLE_ON_BOTHSIDES then
	hotspot2 halfBedWidth+distBedTable,		0, unID, tableWidth, 1+128 : unID = unID + 1
	hotspot2 halfBedWidth+distBedTable-1,	0, unID, tableWidth, 3 : unID = unID + 1
	hotspot2 halfBedWidth+distBedTable+tableWidth, 	0, unID, tableWidth, 2 : unID = unID + 1

	hotspot2 halfBedWidth+distBedTable,		-tableDepth, unID, tableWidth, 1+128 : unID = unID + 1
	hotspot2 halfBedWidth+distBedTable-1,	-tableDepth, unID, tableWidth, 3 : unID = unID + 1
	hotspot2 halfBedWidth+distBedTable+tableWidth, 	-tableDepth, unID, tableWidth, 2 : unID = unID + 1

	hotspot2 halfBedWidth+distBedTable+tableWidth, 0, unID, tableDepth, 1+128 : unID = unID + 1
	hotspot2 halfBedWidth+distBedTable+tableWidth, 1, unID, tableDepth, 3 : unID = unID + 1
	hotspot2 halfBedWidth+distBedTable+tableWidth, -tableDepth, unID, tableDepth, 2 : unID = unID + 1
else
	unID = unID + 9
endif

mul2 -1, 1
hotspot2 halfBedWidth+distBedTable,		0, unID, tableWidth, 1+128 : unID = unID + 1
hotspot2 halfBedWidth+distBedTable-1,	0, unID, tableWidth, 3 : unID = unID + 1
hotspot2 halfBedWidth+distBedTable+tableWidth, 	0, unID, tableWidth, 2 : unID = unID + 1

hotspot2 halfBedWidth+distBedTable,		-tableDepth, unID, tableWidth, 1+128 : unID = unID + 1
hotspot2 halfBedWidth+distBedTable-1,	-tableDepth, unID, tableWidth, 3 : unID = unID + 1
hotspot2 halfBedWidth+distBedTable+tableWidth, 	-tableDepth, unID, tableWidth, 2 : unID = unID + 1

hotspot2 halfBedWidth+distBedTable+tableWidth, 0, unID, tableDepth, 1+128 : unID = unID + 1
hotspot2 halfBedWidth+distBedTable+tableWidth, 1, unID, tableDepth, 3 : unID = unID + 1
hotspot2 halfBedWidth+distBedTable+tableWidth, -tableDepth, unID, tableDepth, 2 : unID = unID + 1
del 1

hotspot2 0, 0, unID : unID = unID + 1

