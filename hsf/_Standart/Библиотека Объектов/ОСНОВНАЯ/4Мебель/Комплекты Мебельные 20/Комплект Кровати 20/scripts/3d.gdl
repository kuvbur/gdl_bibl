
! --- Bed ---

if	iTypeOfBed = BED_TYPE_SINGLE &\
	iBedType= SUPPORT_TYPE_FRAME &\
	iNightTableOnSides = TABLE_ON_ONESIDE &\
	bSameSideDrawer then
	mulx -1
	nTrans = 1
else
	nTrans = 0
endif

add -bedWidth/2, -bedLength, 0

call "fa_bed" parameters all a		= bedWidth,
				b					= bedLength,
				zzyzx				= bedHeight,
				iBedType			= iBedType,
				bDoubleBed			= bDoubleBed,
				bCalledAsMacro		= 1,
				hboard_type			= hboard_type,
				stBedLegType		= stBedLegType,
				iBedLegType			= iBedLegType,
				stBedLegTypeValues	= stBedLegTypeValues,
				leg_h				= legHeightBed,
				stDetLevel3d		= stDetLevel3d,
				stHeadBoardStyles	= stHeadBoardStyles,
				stHeadBoard			= stHeadBoard

del 1

del nTrans

! --- Night Table & Lamp ---

add -bedWidth/2 - distBedTable - tableWidth, -tableDepth, 0

call "fa_nightTable" parameters all a	= tableWidth,
						b				= tableDepth,
						zzyzx			= tableHeight,
						iNightTableType	= iNightTableType,
						bCalledAsMacro	= 1,
						gs_leg_mat		= matLeg

if iLampOnSides = LAMP_ON_SIDES_TABLE_1 or iLampOnSides = LAMP_ON_SIDES_BOTH then
	add tableWidth/2, tableDepth/2, tableHeight

	call "desk_lamp_04_m" parameters all stType		= stType,
								bCalledAsMacro		= 1,
								a					= lampDiameter,
								b					= lampDiameter,
								a2					= a2,
								zzyzx				= lampHeight,
								iColorPreset		= 1,
								unIDStart			= 1000
	del 1
endif

del 1

if iNightTableOnSides = TABLE_ON_BOTHSIDES then
	add bedWidth/2 + distBedTable, -tableDepth, 0

	call "fa_nightTable" parameters all a	= tableWidth,
							b				= tableDepth,
							zzyzx			= tableHeight,
							iNightTableType	= iNightTableType,
							bCalledAsMacro	= 1,
							gs_leg_mat		= matLeg

	if iLampOnSides = LAMP_ON_SIDES_TABLE_2 or iLampOnSides = LAMP_ON_SIDES_BOTH then
		add tableWidth/2, tableDepth/2, tableHeight
		call "desk_lamp_04_m" parameters all stType		= stType,
									bCalledAsMacro		= 1,
									a					= lampDiameter,
									b					= lampDiameter,
									a2					= a2,
									zzyzx				= lampHeight,
									iColorPreset		= 1,
									unIDStart			= 2000
		del 1
	endif

	del 1
endif

! --- Hotspots ---
unID = 100

! - bed dimensions ---
hotspot 0,		0, 0, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot -1,	0, 0, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot halfBedWidth, 		0, 0, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot 0,		0, 0, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot 1,	0, 0, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot -halfBedWidth, 		0, 0, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot 0,		-bedLength, 0, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot -1,	-bedLength, 0, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot halfBedWidth, 		-bedLength, 0, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot 0,		-bedLength, 0, unID, halfBedWidth, 1+128, bedWidth : unID = unID + 1
hotspot 1,	-bedLength, 0, unID, halfBedWidth, 3, bedWidth : unID = unID + 1
hotspot -halfBedWidth, 		-bedLength, 0, unID, halfBedWidth, 2, bedWidth : unID = unID + 1

hotspot halfBedWidth, 0, 0, unID, bedLength, 1+256 : unID = unID + 1
hotspot halfBedWidth, 1, 0, unID, bedLength, 3 : unID = unID + 1
hotspot halfBedWidth, -bedLength, 0, unID, bedLength, 2 : unID = unID + 1

hotspot -halfBedWidth, 0, 0, unID, bedLength, 1+256 : unID = unID + 1
hotspot -halfBedWidth, 1, 0, unID, bedLength, 3 : unID = unID + 1
hotspot -halfBedWidth, -bedLength, 0, unID, bedLength, 2 : unID = unID + 1

! - table-bed distance ---
if iNightTableOnSides = TABLE_ON_BOTHSIDES then
	hotspot halfBedWidth,		-tableDepth, 0, unID, distBedTable, 1+128 : unID = unID + 1
	hotspot halfBedWidth-1,	-tableDepth, 0, unID, distBedTable, 3 : unID = unID + 1
	hotspot halfBedWidth+distBedTable, 		-tableDepth, 0, unID, distBedTable, 2 : unID = unID + 1

	if distBedTable > EPS then
		hotspot halfBedWidth,		0, 0, unID, distBedTable, 1+128 : unID = unID + 1
		hotspot halfBedWidth-1,	0, 0, unID, distBedTable, 3 : unID = unID + 1
		hotspot halfBedWidth+distBedTable, 		0, 0, unID, distBedTable, 2 : unID = unID + 1
	else
		unID = unID + 3
	endif
else
	unID = unID + 6
endif

hotspot -halfBedWidth,		-tableDepth, 0, unID, distBedTable, 1+128 : unID = unID + 1
hotspot -halfBedWidth+1,	-tableDepth, 0, unID, distBedTable, 3 : unID = unID + 1
hotspot -halfBedWidth-distBedTable, 		-tableDepth, 0, unID, distBedTable, 2 : unID = unID + 1

if distBedTable > EPS then
	hotspot -halfBedWidth,		0, 0, unID, distBedTable, 1+128 : unID = unID + 1
	hotspot -halfBedWidth+1,	0, 0, unID, distBedTable, 3 : unID = unID + 1
	hotspot -halfBedWidth-distBedTable, 		0, 0, unID, distBedTable, 2 : unID = unID + 1
else
	unID = unID + 3
endif

! - table dimensions ---

if iNightTableOnSides = TABLE_ON_BOTHSIDES then
	hotspot halfBedWidth+distBedTable,		0, 0, unID, tableWidth, 1+128 : unID = unID + 1
	hotspot halfBedWidth+distBedTable-1,	0, 0, unID, tableWidth, 3 : unID = unID + 1
	hotspot halfBedWidth+distBedTable+tableWidth, 	0, 0, unID, tableWidth, 2 : unID = unID + 1

	hotspot halfBedWidth+distBedTable,		-tableDepth, 0, unID, tableWidth, 1+128 : unID = unID + 1
	hotspot halfBedWidth+distBedTable-1,	-tableDepth, 0, unID, tableWidth, 3 : unID = unID + 1
	hotspot halfBedWidth+distBedTable+tableWidth, 	-tableDepth, 0, unID, tableWidth, 2 : unID = unID + 1

	hotspot halfBedWidth+distBedTable+tableWidth, 0, 0, unID, tableDepth, 1+128 : unID = unID + 1
	hotspot halfBedWidth+distBedTable+tableWidth, 1, 0, unID, tableDepth, 3 : unID = unID + 1
	hotspot halfBedWidth+distBedTable+tableWidth, -tableDepth, 0, unID, tableDepth, 2 : unID = unID + 1
else
	unID = unID + 9
endif

mulx -1
hotspot halfBedWidth+distBedTable,		0, 0, unID, tableWidth, 1+128 : unID = unID + 1
hotspot halfBedWidth+distBedTable-1,	0, 0, unID, tableWidth, 3 : unID = unID + 1
hotspot halfBedWidth+distBedTable+tableWidth, 	0, 0, unID, tableWidth, 2 : unID = unID + 1

hotspot halfBedWidth+distBedTable,		-tableDepth, 0, unID, tableWidth, 1+128 : unID = unID + 1
hotspot halfBedWidth+distBedTable-1,	-tableDepth, 0, unID, tableWidth, 3 : unID = unID + 1
hotspot halfBedWidth+distBedTable+tableWidth, 	-tableDepth, 0, unID, tableWidth, 2 : unID = unID + 1

hotspot halfBedWidth+distBedTable+tableWidth, 0, 0, unID, tableDepth, 1+128 : unID = unID + 1
hotspot halfBedWidth+distBedTable+tableWidth, 1, 0, unID, tableDepth, 3 : unID = unID + 1
hotspot halfBedWidth+distBedTable+tableWidth, -tableDepth, 0, unID, tableDepth, 2 : unID = unID + 1
del 1


