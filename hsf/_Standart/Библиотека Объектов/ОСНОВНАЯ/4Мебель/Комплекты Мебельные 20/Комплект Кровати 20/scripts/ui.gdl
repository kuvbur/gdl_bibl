
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 					=-1
_idxTab = 1
TABID_StyleAndDimensions	= _idxTab  : _idxTab = _idxTab + 1
TABID_HeadMattPillow		= _idxTab  : _idxTab = _idxTab + 1
TABID_Leg					= _idxTab  : _idxTab = _idxTab + 1
TABID_Drawer				= _idxTab  : _idxTab = _idxTab + 1
TABID_Knob					= _idxTab  : _idxTab = _idxTab + 1
TABID_CorniceBase			= _idxTab  : _idxTab = _idxTab + 1
TABID_Lamp					= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions			= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Комплекта Кровати`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль, Размеры и Минимальное Пространство`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_HeadMattPillow, TABID_StyleAndDimensions, `Спинка Кровати, Матрас и Подушка`, "uiTab_FurnitureAccessories1_flat(1)"
	gosub "HeadMattPillowpage"

ui_page TABID_Leg, TABID_StyleAndDimensions, `Каркас и Ножки Кровати`, "uiTab_Leg_flat(1)"
	gosub "Legpage"

ui_page TABID_Drawer, TABID_StyleAndDimensions, `Выдвижной Ящик Тумбочки`, "uiTab_Drawer_flat(1)"
	gosub "Drawerpage"

ui_page TABID_Knob, TABID_StyleAndDimensions, `Ручка Тумбочки`, "uiTab_Knob_flat(1)"
	gosub "Knobpage"

ui_page TABID_CorniceBase, TABID_StyleAndDimensions, `Карниз и Основание Тумбочки`, "uiTab_Leg_flat(1)"
	gosub "CorniceBasepage"

ui_page TABID_Lamp, TABID_StyleAndDimensions, `Прикроватный Светильник`, "uiTab_Light_flat(1)"
	gosub "Lamppage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_FurnitureAccessories1_flat(1)"
file_dependence "uiTab_Leg_flat(1)"
file_dependence "uiTab_Drawer_flat(1)"
file_dependence "uiTab_Knob_flat(1)"
file_dependence "uiTab_Light_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37
	yPict = 93
	PictHeight = 130

	x1 = 0
	x2 = 100
	x3 = 230
	x4 = 331
	x5 = 178
	dy = 24

	_yCurr = pageStartY
	ui_infield{3} "iTypeOfBed", x2, _yCurr, 112, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Односпальная`, 1,
			"", `Двуспальная`, 2 ui_tooltip `Тип Кровати`

	ui_infield{3} "iNightTableType", x4, _yCurr, 112, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Несколько Ящиков`, 1,
			"", `Открытая`, 2 ui_tooltip `Тип Тумбочки`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Тип Кровати`, x1, _yCurr, x2 - 2, _outfHeight
	ui_outfield `Тип Тумбочки`, x3, _yCurr, x2 - 2, _outfHeight

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "iNightTableOnSides", x2, _yCurr, 112, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `С одной стороны`, 1,
			"", `С обеих сторон`, 2 ui_tooltip `Тумбочки`

	ui_infield{3} "iLampOnSides", x4, _yCurr, 112, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Нет`, 			1,
			"", `Лампа на Тумбочке 1`, 	2,
			"", `Лампа на Тумбочке 2`, 	3,
			"", `Две Лампы`, 		4 ui_tooltip `Светильники`


	_yCurr = _yCurr + _yOffset
	ui_outfield `Тумбочки`, x1, _yCurr, x2 - 2, _outfHeight
	ui_outfield `Светильники`, x3, _yCurr, x2 - 2, _outfHeight

	_yCurr = _yCurr + dy - _yOffset + 3
	ui_separator 0, _yCurr, 444, _yCurr
	ui_separator 222, 0, 222, 48

	if iTypeOfBed = 1 then	! Single
		xPict = 152
		PictWidth = 139 
		ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1
	else
		xPict = 131
		PictWidth = 182 
		ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1
	endif

	_yCurr = yPict - _infHeight - 3
	ui_infield "tableWidth", xPict - 39, _yCurr, 90, _infHeight ui_tooltip `Ширина Тумбочки`
	ui_infield "bedWidth", xPict + 56, _yCurr, 90, _infHeight ui_tooltip `Ширина Кровати`

	if iTypeOfBed = 1 then
		_yCurr = yPict - 6
		ui_infield "distBedTable", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Расстояние между Кроватью и Тумбочкой`
	else
		ui_infield "distBedTable", xPict + 151, _yCurr, 90, _infHeight ui_tooltip `Расстояние между Кроватью и Тумбочкой`
	endif


	_yCurr = yPict + 12
	ui_infield "tableDepth", xPict - 93, _yCurr, 90, _infHeight ui_tooltip `Глубина Тумбочки`

	_yCurr = yPict + 46
	ui_infield "bedLength", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Длина Кровати`

	_yCurr = yPict + 67
	ui_infield "MSBedside", xPict - 93, _yCurr, 90, _infHeight ui_tooltip `Длина Прикроватного Пространства`

	_yCurr = yPict + 103
	ui_infield "MSFrontBed", xPict - 93, _yCurr, 90, _infHeight ui_tooltip `Расстояние Перед Кроватью`

	_yCurr = yPict + PictHeight + 3
	ui_infield "MSSide", xPict - 6, _yCurr, 72, _infHeight ui_tooltip `Ширина Прикроватного Пространства 1`

	_yCurr = yPict + PictHeight + 3
	ui_infield "MSSide2", xPict + 112, _yCurr, 72, _infHeight ui_tooltip `Ширина Прикроватного Пространства 2`

	_outfFlag = 0

return


! ------------------------------------------------------------------------------
"HeadMattPillowpage":
! ------------------------------------------------------------------------------
	dim stUIHeadBoardStyles[4]
		stUIHeadBoardStyles[1] = ``
		stUIHeadBoardStyles[2] = ``
		stUIHeadBoardStyles[3] = ``
		stUIHeadBoardStyles[4] = ``
	
	call "ui_furniture_parts" parameters all 	bIsHeadboard		= 1,
												stHeadBoard			= stHeadBoard,
												stHeadBoardStyles 	= stUIHeadBoardStyles

	pageStartY	= 5
	pageEndY	= 266
	ui_separator 222, pageStartY - 5, 222, pageEndY

	call "ui_furniture_parts" parameters all 	bIsMattresAndPillow	= 1,
												pageStartX			= 232

return

! ------------------------------------------------------------------------------
"Legpage":
! ------------------------------------------------------------------------------
dim stUIBedLegTypeValues[6]
	stUIBedLegTypeValues[1] = ``
	stUIBedLegTypeValues[2] = ``
	stUIBedLegTypeValues[3] = ``
	stUIBedLegTypeValues[4] = ``
	stUIBedLegTypeValues[5] = ``
	stUIBedLegTypeValues[6] = ``

	call "ui_furniture_parts" parameters all 	bIsFrameAndLeg		= 1,
												stBedTypes 			= stBedTypes,
												stBedLegTypeValues 	= stUIBedLegTypeValues,
												bLayout				= 1,
												pageStartY 			= 0
return

! ------------------------------------------------------------------------------
"Drawerpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	x1 = 0
	x2 = 76
	x3 = 230
	x4 = 123 + 230
	dy = 24
	pictWidth = 18
	pictHeight = 18
	_yCurr = pageStartY + _yOffset
	_outfFlag 		= 0

	ui_style 0, 1
	ui_outfield `Стиль Ящика`, x1, _yCurr, 210, _outfHeight, _outfFlag
	ui_style 0, 0
	
	if dnum = 0 or iNightTableDrawerPos = DRAWERPOS_NONE then _outfFlag = 4

	_yCurr = _yCurr + _infHeight + 2
	ui_infield{4} "gs_door_type_m", x2, _yCurr, 135, 96,
		2, 3, 4, 1,
		120, 90, 120, 90,
		1, "",	1,
		2, "",	2,
		3, "", 	3,
		4, "",	8 ui_tooltip `Стиль Ящика`
	
	if iNightTableType = 1 then	! Multi Drawer
		ui_infield{3} "dnum", x4 + 30, _yCurr, 60, _infHeight,
			8, "", 3, 1,
			0, 0, 0, 0,
			1, `2`, 	2,
			2, `1`, 	1,
			3, `Нет`, 	0 ui_tooltip `Количество Ящиков`

		_yCurr = _yCurr + _yOffset
		ui_outfield `Количество Ящиков`, x3, _yCurr, 120, _outfHeight, _outfFlag
	else
		ui_infield{3} "iNightTableDrawerPos", x4, _yCurr, 90, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Вверху`, DRAWERPOS_UP,
			"", `Внизу`, DRAWERPOS_DOWN,
			"", `Нет`, DRAWERPOS_NONE ui_tooltip `Размещение Ящика`

		_outfFlag = 0

		_yCurr = _yCurr + _yOffset
		ui_outfield `Размещение Ящика`, x3, _yCurr, 120, _outfHeight, _outfFlag

		if dnum = 0 or iNightTableDrawerPos = DRAWERPOS_NONE then _outfFlag = 4

		_yCurr = _yCurr + dy - _yOffset
		ui_infield{3} "dwh", x4, _yCurr, 90, _infHeight ui_tooltip `Верхняя Секция`

		_yCurr = _yCurr + _yOffset
		ui_outfield `Верхняя Секция`, x3, _yCurr, 120, _outfHeight, _outfFlag
	endif

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "fr_th1", x4, _yCurr, 90, _infHeight ui_tooltip `Толщина`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Толщина`, x3, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "doorframe_w", x4, _yCurr, 90, _infHeight ui_tooltip `Ширина Обвязки Ящика`

	_yCurr = _yCurr + _yOffset
	if gs_door_type_m = 1 or gs_door_type_m = 8 then _outfFlag = 4
	ui_outfield `Ширина Рамы`, x3, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "hor_gnum", x4 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Профилей Горизонтальной Раскладки`
	
	_yCurr = _yCurr + _yOffset
	if gs_door_type_m <> 3 then _outfFlag = 4
	ui_outfield `Гориз. Раскладка`, x3, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "vert_gnum", x4 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Профилей Вертикальной Раскладки`
	
	_yCurr = _yCurr + _yOffset
	ui_outfield `Верт. Раскладка`, x3, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "gs_bevel", x3, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Фаска Торца`, 0,
					"", `Фаска Торца`, 1


	if iNightTableType = 1 then
		_yCurr = _yCurr + 55
	else
		_yCurr = _yCurr + 55 - dy
	endif

	ui_infield "doorframe_mat", x3 + 20, _yCurr, 213 - 20, 25 ui_tooltip `Покрытие Обвязки Ящика`
	ui_pict 4, x3, _yCurr + 3, pictWidth , pictHeight, 1 ui_tooltip `Покрытие Обвязки Ящика`

	_yCurr = _yCurr + 30
	ui_infield "doorglas_mat", x3 + 20, _yCurr, 213 - 20, 25 ui_tooltip `Покрытие Филенки Ящика`
	ui_pict 5, x3, _yCurr + 3, pictWidth , pictHeight, 1 ui_tooltip `Покрытие Филенки Ящика`

	_outfFlag = 0

return

! ------------------------------------------------------------------------------
"Knobpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	x1 = 0
	x2 = 230
	x3 = 354
	dy = 24
	_yCurr = pageStartY
	_outfFlag 		= 0

	ui_infield{3} "bKnob", x1, _yCurr, x2 - 2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Ручка`, 0,
					"", `Ручка`, 1

	ui_infield "knob_size", x3, _yCurr, 90, _infHeight ui_tooltip `Размер Ручки`

	_yCurr = _yCurr + _yOffset
	if gs_knob_type_m < 4 or not(bKnob) then _outfFlag = 4
	ui_outfield `Размер`, x2, _yCurr, 120, _outfHeight, _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{4} "gs_knob_type_m", x1, _yCurr, 444, 235,
		1, 8, 12, 3,
		90, 90, 90, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,	4,
		5, ``,	5,
		6, ``,	6,
		7, ``,	7,
		8, ``,	8 ui_tooltip `Стиль Ручки`

	_outfFlag = 0

return

! ------------------------------------------------------------------------------
"CorniceBasepage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4	
	x1 = 76
	x2 = 0
	x3 = 123
	dy = 24
	_yCurr = pageStartY

	if not(bCornice) then _outfFlag = 4

	ui_style 0, 1
	ui_infield{3} "bCornice", x2, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Карниз`, 0,
					"", `Карниз`, 1
	ui_style 0, 0

	_yCurr = _yCurr + _infHeight + 2
	ui_infield{3} "gs_cornice_type_m", x1, _yCurr, 135, 96,
		2, 7, 4, 1,
		120, 90, 120, 90,
			1, ``,	1,
			2, ``,	2,
			3, ``, 	3,
			4, ``,	4 ui_tooltip `Стиль Карниза`

	_yCurr = _yCurr + dy * 4 + 5
	ui_infield{3} "tbt", x3, _yCurr, 90, _infHeight ui_tooltip `Толщина`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Толщина`, x2, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "tboh", x3, _yCurr, 90, _infHeight ui_tooltip `Свес Карниза`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Свес Карниза`, x2, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy + _outfHeight
	ui_infield{3} "gs_cornice_mat", x2, _yCurr, 213, 25 ui_tooltip `Карниз`

	_outfFlag = 0

	_yCurr = _yCurr + 30 + 2

	ui_separator 222, 0, 222, _yCurr

	x1 = 76 + 230
	x2 = 0 + 230
	x3 = 123 + 230
	_yCurr = pageStartY

	if not(bBase) then _outfFlag = 4

	ui_style 0, 1
	ui_infield{4} "bBase", x2, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Основание`, 0,
					"", `Основание`, 1
	ui_style 0, 0

	_yCurr = _yCurr + _infHeight + 2

	ui_infield{4} "gs_leg_type_m", x1, _yCurr, 135, 96,
		2, 6, 8, 2,
		120, 90, 120, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,  4,
		5, ``,	5	ui_tooltip `Стиль Основания`

	_yCurr = _yCurr + dy * 4 + 5
	ui_infield{3} "leg_h", x3, _yCurr, 90, _infHeight ui_tooltip `Высота`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Высота`, x2, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "leg_w", x3, _yCurr, 90, _infHeight ui_tooltip `Толщина/Диаметр`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Толщина/Диаметр`, x2, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy + _outfHeight

	ui_infield{3} "matLeg", x2, _yCurr, 213, 25 ui_tooltip `Основание`

	_outfFlag = 0

return

! ------------------------------------------------------------------------------
"Lamppage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4	

	x1 = 0
	x2 = 169
	x3 = 123
	dy = 24
	_yCurr = pageStartY + _yOffset

	ui_style 0, 1
	ui_outfield `Размеры Светильника`, x1, _yCurr, 444, _outfHeight
	ui_style 0, 0

	if iLampOnSides = LAMP_ON_SIDES_NONE then _outfFlag = 4

	_yCurr = _yCurr + dy - _yOffset	
	ui_infield{3} "lampDiameter", x2, _yCurr, 97, _infHeight ui_tooltip `Нижний Диаметр`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Нижний Диаметр`, x1, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "lud", x2, _yCurr, 97, _infHeight ui_tooltip `Верхний Диаметр`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Верхний Диаметр`, x1, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "lsh", x2, _yCurr, 97, _infHeight ui_tooltip `Высота Светильника`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Высота Плафона`, x1, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "lampHeight", x2, _yCurr, 97, _infHeight ui_tooltip `Общая Высота`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Общая Высота`, x1, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_separator x1, _yCurr, 444, _yCurr

 	_yCurr = _yCurr + 5

	ui_infield{3} "tempIntC", x1, _yCurr,27 * 2,	27,
		4, 9,
		2, 1,
		25,	25,	18, 22,
		1, "", 1,
		2, "", 0

 	_yCurr = _yCurr + 27 + 3

	if not(gs_light_switch) then _outfFlag = 4

	ui_outfield `Цвет`, x1, _yCurr, 120, _outfHeight, _outfFlag

 	_yCurr = _yCurr - _yOffset
	ui_colorpicker	"gs_color_red", "gs_color_green", "gs_color_blue", x2, _yCurr, 42, 19

	_yCurr = _yCurr + dy

	ui_outfield `Основная Яркость`, x1, _yCurr, 160, _outfHeight, _outfFlag

 	_yCurr = _yCurr + dy - _yOffset

	ui_slider "gs_light_intensity", x1, _yCurr, x2 - 2,	19, 10, 1

	ui_infield{3} "gs_light_intensity", x2, _yCurr, 60, _infHeight ui_tooltip `Интенсивность`
	_yCurr = _yCurr + _yOffset
	ui_outfield `%`, x2 + 60 + 3, _yCurr, 120, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy
	ui_outfield `Дистанция Света`, x1, _yCurr, x2 - 2, _outfHeight, _outfFlag
	_yCurr = _yCurr - _yOffset
	ui_infield{3} "c4dDetRadius", x2, _yCurr, 97, _infHeight ui_tooltip `Интенсивность`

	_yCurr = _yCurr + dy

	ui_infield{4} "bGenShadow", x1, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Отбрасывание Теней`, 0,
					"", `Отбрасывание Теней`, 1

	_outfFlag = 0

return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	dim stUISymbolTypes[4]
		stUISymbolTypes[1] = ``
		stUISymbolTypes[2] = ``
		stUISymbolTypes[3] = ``
		stUISymbolTypes[4] = ``

	if iTypeOfBed = BED_TYPE_SINGLE then
		iSymbolClass = 2	! SYMBOLCLAS_BED
	else
		iSymbolClass = 3	! SYMBOLCLAS_BED_DOUBLE
	endif

	call "ui_object_2DRepresentation" parameters all	iSymbolClass		= iSymbolClass,
														stSymbolTypes 		= stUISymbolTypes,
														bLayout 			= 1,
														iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1

	surfacesArray[SurfItemID]	= "frmat"
	SurfItemID = SurfItemID + 1

	if iBedType = 2 then 
		surfacesArray[SurfItemID]	= "gs_leg_mat"
		SurfItemID = SurfItemID + 1
	endif

	if bHeadboard then
		surfacesArray[SurfItemID]	= "hbmat"
		SurfItemID = SurfItemID + 1
	endif

	surfacesArray[SurfItemID]	= "mmat"
	SurfItemID = SurfItemID + 1

	if pw then
		surfacesArray[SurfItemID]	= "pillow_mat"
		SurfItemID = SurfItemID + 1
	endif

	surfacesArray[SurfItemID]	= "gs_cabinet_mat"
	SurfItemID = SurfItemID + 1

	if bCornice then
		surfacesArray[SurfItemID]	= "gs_cornice_mat"
		SurfItemID = SurfItemID + 1
	endif

	if bBase then
		surfacesArray[SurfItemID]	= "matLeg"
		SurfItemID = SurfItemID + 1
	endif

	surfacesArray[SurfItemID]	= "doorframe_mat"
	SurfItemID = SurfItemID + 1

	if gs_door_type_m = 3 then
		surfacesArray[SurfItemID]	= "doorglas_mat"
		SurfItemID = SurfItemID + 1
	endif

	if bKnob or gs_door_type_m <> 8 then
		surfacesArray[SurfItemID]	= "gs_knob_mat"
		SurfItemID = SurfItemID + 1
	endif

	if iLampOnSides <> 1 then	! None
		surfacesArray[SurfItemID]	= "matFrame"
		SurfItemID = SurfItemID + 1
		surfacesArray[SurfItemID]	= "matShadeOut"
		SurfItemID = SurfItemID + 1
	endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters all	surfacesArray		= surfacesArray,
														childFlagArray		= childFlagArray,
														iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
