
call "FM_types" parameters all

!Hide all parameters from UI
hideparameter all  "A", "B", "ZZYZX"

values "tableWidth" range [0.1,)
values "tableDepth" range [0.1,)
values "tableHeight" range (0,)
values "fr_th1" range [0.01,)
values "tbt" range (0,)
values "tboh" range [0,)
values "leg_h" range [0,)
values "leg_w" range (0,)
values "gs_resol" range [3,)
values "MSFrontBed" range [0,)
values "frh" range [0.01,)

values "gs_detlevel_3D" stDetLevel3d
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	if gs_detlevel_3D = stDetLevel3d[3] then gs_detlevel_3D_m = 0		! Off
	if gs_detlevel_3D = stDetLevel3d[2] then gs_detlevel_3D_m = 1		! Simple
	if gs_detlevel_3D = stDetLevel3d[1] then gs_detlevel_3D_m = 2		! Detailed
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetLevel3d[3]		! Off
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = stDetLevel3d[2]		! Simple
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = stDetLevel3d[1]		! Detailed
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

values{2} "iTypeOfBed" 	BED_TYPE_SINGLE,	`Односпальная`,
						BED_TYPE_DOUBLE,	`Двуспальная`

values{2} "iBedType" 	SUPPORT_TYPE_FRAME, `Каркас`,
						SUPPORT_TYPE_LEG,	`Ножки`

values "sNightTableOnSides" stNightTableOnSides
values "iNightTableOnSides" TABLE_ON_ONESIDE, TABLE_ON_BOTHSIDES

if GLOB_MODPAR_NAME = "sNightTableOnSides" then
	iNightTableOnSides = TABLE_ON_ONESIDE
	if sNightTableOnSides = stNightTableOnSides[2] then iNightTableOnSides = TABLE_ON_BOTHSIDES
	parameters iNightTableOnSides = iNightTableOnSides
else
	sNightTableOnSides = stNightTableOnSides[1]									! On one side
	if iNightTableOnSides = TABLE_ON_BOTHSIDES then sNightTableOnSides = stNightTableOnSides[2]
	parameters sNightTableOnSides = sNightTableOnSides
endif

values "sNightTableType" stNightTableTypes
values "iNightTableType" NIGHT_TABLE_TYPE_1, NIGHT_TABLE_TYPE_2

if GLOB_MODPAR_NAME = "sNightTableType" then
	iNightTableType = NIGHT_TABLE_TYPE_1
	if sNightTableType = stNightTableTypes[2] then iNightTableType = NIGHT_TABLE_TYPE_2
	parameters iNightTableType = iNightTableType
else
	sNightTableType = stNightTableTypes[1]								! Night Table 01
	if iNightTableType = NIGHT_TABLE_TYPE_2	then sNightTableType = stNightTableTypes[2]
	parameters sNightTableType = sNightTableType
endif

if iNightTableOnSides = TABLE_ON_ONESIDE then
	values "sLampOnSides" stLampOnSides[1], stLampOnSides[2]
	values "iLampOnSides" LAMP_ON_SIDES_NONE, LAMP_ON_SIDES_TABLE_1
else
	values "sLampOnSides" stLampOnSides
	values "iLampOnSides" LAMP_ON_SIDES_NONE, LAMP_ON_SIDES_TABLE_1, LAMP_ON_SIDES_TABLE_2, LAMP_ON_SIDES_BOTH
endif

if GLOB_MODPAR_NAME = "sLampOnSides" then
	iLampOnSides = LAMP_ON_SIDES_NONE
	if sLampOnSides = stLampOnSides[2] then iLampOnSides = LAMP_ON_SIDES_TABLE_1
	if sLampOnSides = stLampOnSides[3] then iLampOnSides = LAMP_ON_SIDES_TABLE_2
	if sLampOnSides = stLampOnSides[4] then iLampOnSides = LAMP_ON_SIDES_BOTH
	parameters iLampOnSides = iLampOnSides
else
	sLampOnSides = stLampOnSides[1]								! None
	if iLampOnSides = LAMP_ON_SIDES_TABLE_1	then sLampOnSides = stLampOnSides[2]
	if iLampOnSides = LAMP_ON_SIDES_TABLE_2	then sLampOnSides = stLampOnSides[3]
	if iLampOnSides = LAMP_ON_SIDES_BOTH	then sLampOnSides = stLampOnSides[4]
	parameters sLampOnSides = sLampOnSides
endif

values "tempIntC" 0, 1

if GLOB_MODPAR_NAME = "tempIntC" then
	gs_light_switch	= tempIntC
	parameters gs_light_switch = gs_light_switch
else
	tempIntC	= gs_light_switch
	parameters tempIntC = tempIntC
endif

! --- Main dimensions ---

if iNightTableOnSides = TABLE_ON_ONESIDE then
	tablenum = 1
else
	tablenum = 2
endif


A = bedWidth + (tableWidth + distBedTable) * tablenum
parameters A = A

B = bedLength
parameters B = B

ZZYZX = max(bedHeight, tableHeight + lampHeight * (iLampOnSides <> LAMP_ON_SIDES_NONE))
parameters ZZYZX = ZZYZX
lock "A", "B", "ZZYZX"


if GLOB_MODPAR_NAME = "halfBedWidth" then
	bedWidth = 2 * halfBedWidth
	parameters bedWidth = bedWidth
else
	halfBedWidth = bedWidth / 2
	parameters halfBedWidth = halfBedWidth
endif

values "distBedTable" range [0, )


! --- Bed Parameters ---

call "fa_bed" parameters all bCalledAsMacro = 1,
						a					= bedWidth,
						b					= bedLength,
						zzyzx				= bedHeight,
						iBedType			= iBedType,
						stBedType			= stBedType,
						iBedLegType			= iBedLegType,
						stBedLegType		= stBedLegType,
						stBedLegTypeValues	= stBedLegTypeValues,
						bDoubleBed			= bDoubleBed,
						leg_h				= legHeightBed,
						stSymbolTypes		= stSymbolTypes,
						stHeadBoardStyles	= stHeadBoardStyles,
						stHeadBoard			= stHeadBoard,
				returned_parameters	minBedWidth, minBedLength, iSymbolType

VALUES "bedWidth" RANGE [minBedWidth,)
VALUES "bedLength" RANGE [minBedLength,)
values "hboard_type_m" 1, 2, 3, 4

if iBedType = 1 then
	hideparameter "legHeightBed", "stBedLegType", "iBedLegType", "LegWidth"
	lock "legHeightBed", "stBedLegType", "iBedLegType", "LegWidth"
else
	hideparameter "iFrameType"
	lock "iFrameType"
endif

if	iFrameType <> FRAME_DRAWER |\
	iTypeOfBed <> BED_TYPE_SINGLE &\
	iBedType = SUPPORT_TYPE_FRAME then
	hideparameter "bSameSideDrawer"
	lock "bSameSideDrawer"
endif


values "bedWidth" range [0.4 * (1 + bDoubleBed), )

if GLOB_MODPAR_NAME = "sTypeOfBed" | GLOB_MODPAR_NAME = "iTypeOfBed" then
	if bDoubleBed then
		bedWidth = 1.8
		parameters bedWidth = bedWidth
	else
		bedWidth = 0.9
		parameters bedWidth = bedWidth
	endif
	halfBedWidth = bedWidth / 2
	parameters halfBedWidth = halfBedWidth
endif

if not(bFrontHeadBoard) then hbh1 = 0
if not(bRearHeadBoard) then hbh2 = 0
if iBedType = 1 then legHeightBed = 0

bedHeight =  legHeightBed + frh + max(hbh1, hbh2, mrh)
parameters bedHeight = bedHeight
lock "bedHeight"


! --- NightTable Parameters ---

call "fa_nightTable" parameters all bCalledAsMacro	= 1,
								iNightTableType		= iNightTableType,
								stNightTableTypes	= stNightTableTypes,	! value array
								stDoorTypes			= stDoorTypes,
								stCorniceTypes		= stCorniceTypes,
								stLegTypes			= stLegTypes,
								stKnobTypes			= stKnobTypes,
								gs_leg_mat			= matLeg

! --- Lamp Parameters ---

if iLampOnSides = LAMP_ON_SIDES_NONE then
	values{2} "gs_symbolType_m"	LAMP_TYPE_1,	stType[1],
								LAMP_TYPE_2,	stType[2]
	hideparameter "gs_lampSettings", "gs_light_switch", "gs_light_intensity", "c4dDetRadius", "bGenShadow"
	lock	"lampDiameter", "lampHeight", "lsh", "lud", "lud2", "gs_light_switch", "gs_light_intensity", "c4dDetRadius", "bGenShadow"
else
	if not(gs_light_switch) then
		lock "gs_light_intensity", "c4dDetRadius", "bGenShadow"
		hideparameter "gs_light_intensity", "c4dDetRadius", "bGenShadow"
	endif

	values "gs_light_intensity" range [0, 200]
	values "c4dDetRadius" range (0, )
	values{2} "gs_symbolType_m"	LAMP_TYPE_1,	stType[1],
								LAMP_TYPE_2,	stType[2]

	if GLOB_MODPAR_NAME = "lampDiameter" then
		A2 = lampDiameter/2
		parameters A2 = A2
	else
		lampDiameter = A2*2
		parameters lampDiameter = lampDiameter
	endIf

endif

! --- Minimal Space Parameters ---
if MSSide < 0 then
	MSSide = 0
	parameters MSSide = MSSide
endif

if MSSide2 < 0 then
	MSSide2 = 0
	parameters MSSide2 = MSSide
endif

if MSFrontBed < 0 then
	MSFrontBed = 0
	parameters MSFrontBed = MSFrontBed
endif

values "MSBedside" range [0, bedLength]


!!! ******************************** Different fills
if GLOB_MODPAR_NAME = "fillFor" then
	iFillFor = 1
	for i = 1 to vardim1(stFillFor)
		if fillFor = stFillFor[i] then	iFillFor = i
	next i
	parameters iFillFor = iFillFor
else
	iFillFor = max(min(iFillFor,5),1)
	parameters fillFor = stFillFor[iFillFor]
endif

if fillPenUniform=0 then parameters fillPenUniform=19
if fillPenPillow=0 then parameters fillPenPillow=19
if fillPenBlanket=0 then parameters fillPenBlanket=19
if fillPenNightTable=0 then parameters fillPenNightTable=19
if fillPenLamp=0 then parameters fillPenLamp=19

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19

if iSymbolType = 1 or iSymbolType = 2 then
	if iSymbolType = 1 then
		if bLamp then
			values "fillFor" stFillFor
			values "iFillFor" 1, 2, 3, 4, 5
		else
			values "fillFor" stFillFor[1], stFillFor[2], stFillFor[3], stFillFor[4]
			values "iFillFor" 1, 2, 3, 4
		endif
	endif
	if iSymbolType = 2 then
		if bLamp then
			values "fillFor" stFillFor[1], stFillFor[2], stFillFor[4], stFillFor[5]
			values "iFillFor" 1, 2, 4, 5
		else
			values "fillFor" stFillFor[1], stFillFor[2], stFillFor[4]
			values "iFillFor" 1, 2, 4
		endif
		hideparameter "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
		lock 	      "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	endif
else
	if bLamp then
		values "fillFor" stFillFor[1], stFillFor[4], stFillFor[5]
		values "iFillFor" 1, 4, 5
	else
		values "fillFor" stFillFor[1], stFillFor[4]
		values "iFillFor" 1, 4
	endif
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
endif

if not(bLamp) then
	hideparameter "fillTypeLamp", "fillPenLamp", "backPenLamp"
	lock "fillTypeLamp", "fillPenLamp", "backPenLamp"
endif

if bUniformFill then
	lock "fillFor", "iFillFor"
	hideparameter "fillFor", "iFillFor"
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", \
					"fillTypeNightTable", "fillPenNightTable", "backPenNightTable", "fillTypeLamp", "fillPenLamp", "backPenLamp", "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", \
					"fillTypeNightTable", "fillPenNightTable", "backPenNightTable", "fillTypeLamp", "fillPenLamp", "backPenLamp", "gs_fill_type", "gs_fill_pen", "gs_back_pen"
else
	hideparameter "fillTypeUniform", "fillPenUniform", "backPenUniform"
	lock 	      "fillTypeUniform", "fillPenUniform", "backPenUniform"
endif


