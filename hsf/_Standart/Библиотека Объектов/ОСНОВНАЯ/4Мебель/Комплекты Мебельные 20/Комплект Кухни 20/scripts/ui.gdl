
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 				= - 1
_idxTab = 1
TABID_Layout					= _idxTab  : _idxTab = _idxTab + 1
TABID_Accessories				= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_SymbolText				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Кухни`
ui_current_page gs_ui_current_page

ui_page TABID_Layout, TABID_Root, `Планировка, Размеры и Минимальное Пространство`, "uiTab_Dimension_flat(1)"
	gosub "layoutPage"

ui_page TABID_Accessories, TABID_Root, `Аксессуары`, "uiTab_Cooktops_flat(1)"
	gosub "accessoriesPage"

ui_page TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "representationpage"

ui_page TABID_SymbolText, TABID_Root, `Текст Символа`, "uiTab_Text_flat(1)"
	gosub "symboltextpage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Cooktops_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Text_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"layoutPage":
! ------------------------------------------------------------------------------

	pageStartY 	= 0
	_yOffset 	= 4
	x1			= 5
	xEndLevel	= 439
	yStart		= pageStartY + _yOffset
	lineSpace	= 24
	hRow		= 15
	yInfield	= 19

	xHalfLevel 	= 148
	xMargin1	= 6
	x2			= xHalfLevel + xMargin1 + 1
	x3			= 348
	x4			= 296
	wInteger	= 50
	wLength		= 90

	lineSpace	= 28

	! ------ 1. Column - Layout -------

	ui_style 0, 1
	ui_outfield `Планировка`,	x1,		yStart,	xHalfLevel-x1-xMargin1,	hRow					! Layout
	ui_style 0, 0
	y1 = pageStartY+22

	ui_infield{3} "layoutType", x1, y1, 138, 95,
		2, 15,
		6, 1,
		59, 69, 55, 65,
		1, stLayoutType[1], stLayoutType[1],
		2, stLayoutType[2], stLayoutType[2],
		3, stLayoutType[3], stLayoutType[3],
		4, stLayoutType[4], stLayoutType[4],
		5, stLayoutType[5], stLayoutType[5],
		6, stLayoutType[6], stLayoutType[6]
	y1 = pageStartY+22 + 109

	iFlag = (iLayoutType <> LAYOUT_CUSTOM) * 4
	ui_outfield `Столешницы`,	x1,	y1,	xHalfLevel-x1-xMargin1-wInteger, hRow, iFlag			! Counters
	if iLayoutType = LAYOUT_CUSTOM then
		ui_infield "nChains",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	else
		ui_infield "nCounterForUI",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	endif
	y1 = y1 + lineSpace

	ui_outfield `Сегменты (1)`,	x1,	y1,	xHalfLevel-x1-xMargin1-wInteger, hRow, iFlag			! Segments(1)
	if iLayoutType = LAYOUT_CUSTOM then
		ui_infield "nChainSegment_1",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	else
		ui_infield "nSegmentForUI_1",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	endif
	y1 = y1 + lineSpace

	iFlag = not(bChains[2]) * 4
	ui_outfield `Сегменты (2)`,	x1,	y1,	xHalfLevel-x1-xMargin1-wInteger, hRow, iFlag			! Segments(2)
	if iLayoutType = LAYOUT_CUSTOM then
		ui_infield "nChainSegment_2",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	else
		ui_infield "nSegmentForUI_2",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	endif
	y1 = y1 + lineSpace

	iFlag = not(bChains[3]) * 4
	ui_outfield `Сегменты (3)`,	x1,	y1,	xHalfLevel-x1-xMargin1-wInteger, hRow, iFlag			! Segments(3)
	if iLayoutType = LAYOUT_CUSTOM then
		ui_infield "nChainSegment_3",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	else
		ui_infield "nSegmentForUI_3",	xHalfLevel-xMargin1-wInteger+2, y1-4, wInteger, yInfield
	endif
	y1 = y1 + lineSpace


	! ------ Separator -------
	ui_separator xHalfLevel, pageStartY-3, xHalfLevel, 265


	! ------ 2. Column - Dimensions -------
	ui_style 0,1
	ui_outfield `Размеры`, x2, yStart, xEndLevel-x2,	hRow				! Dimensions
	ui_style 0,0
	y1 = pageStartY+22

	ui_pict 10, 251, pageStartY+25, 91, 104, 1			!ui_KitchenLayoutView_1(1).png
	ui_pict 11, 229, pageStartY+129, 113, 51, 1		!ui_KitchenLayoutView_2(1).png

	ui_infield "wallCabinetDepth",	x2, y1,		wLength, yInfield		ui_tooltip `Глубина Навесного Шкафа`
	ui_infield "counterDepth",		x2, y1+45,	wLength, yInfield		ui_tooltip `Глубина Столешницы`
	ui_infield "counterThickness",	x2, y1+75,	wLength, yInfield		ui_tooltip `Толщина Столешницы`
	ui_infield "toeHeigth",			x2, y1+105, wLength, yInfield		ui_tooltip `Высота Цоколя`

	ui_infield "wallCabinetHeigth",		x3, y1+39,	wLength, yInfield		ui_tooltip `Высота Навесного Шкафа`
	ui_infield "wallCabinetClearence",	x3, y1+69,	wLength, yInfield		ui_tooltip `Зазор Навесного Шкафа`
	ui_infield "counterHeigth",			x3, y1+99,	wLength, yInfield		ui_tooltip `Высота Столешницы`
	ui_infield "MSFront",				x3, y1+129,	wLength, yInfield		ui_tooltip `Минимальное Пространство Спереди`

	y1 = pageStartY+22 + 39 + 3*30 + 36

	ui_outfield `Высокие Шкафы`, x2, y1, x4-x2-2,hRow							! Tall Cabinets
	ui_infield "nTallCabinet", x4, y1-4, wInteger, yInfield
	y1 = y1 + lineSpace

	iFlag = (iLayoutType = LAYOUT_ISLAND) * 4
	ui_outfield `Навесные Шкафы`, x2, y1, x4-x2-2, hRow, iFlag					! Wall Cabinets
	ui_infield "nWallCabinet", x4, y1-4, wInteger, yInfield

return

! ------------------------------------------------------------------------------
"accessoriesPage":
! ------------------------------------------------------------------------------
	pageStartY 	= 0
	_infHeight	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_outfFlag 	= 0

	dy 			= 24
	x1			= 0
	x2			= x1 + 20
	x3			= 170
	x4			= 273
	wPopup		= 96
	hPopup		= 67

	ui_style 0,1
	_yCurr = pageStartY
	ui_infield{3} "bCooktop", x1, _yCurr, x3-x1-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Варочные Панели`, 0,
		1, `Варочные Панели`, 1
	ui_style 0,0
	ui_infield "nCooktops", x3, _yCurr, 54, _infHeight
	ui_infield{3} "cooktopType", x4, _yCurr, wPopup, hPopup,
		2, 12, 3, 1,			! ui_cooktop_type.png
		82, 61, 82, 61,
		1, stCooktopType[1], stCooktopType[1],
		2, stCooktopType[2], stCooktopType[2],
		3, stCooktopType[3], stCooktopType[3]

	_yCurr = _yCurr + dy
	ui_infield{3} "bHood", x2, _yCurr, x3-x2-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Вытяжка `, 0,
		1, `Вытяжка `, 1
	ui_infield{3} "hoodType", x3, _yCurr, 97, _infHeight,
		8, "", 0, 0,
		0, 0, 0, 0,
		0, stHoodType[1], stHoodType[1],
		0, stHoodType[2], stHoodType[2],
		0, stHoodType[3], stHoodType[3]

	_yCurr = _yCurr + dy
	ui_infield "hoodHeigth", x3, _yCurr, 97, _infHeight ui_tooltip `Высота Верха Вытяжки`

	if not(bHood) then _outfFlag = 4

	_yCurr = _yCurr + _yOffset
	ui_outfield `Высота Верха Вытяжки`, x2, _yCurr, x3-x2-2, _outfHeight, _outfFlag

	_outfFlag = 0

	_yCurr = _yCurr - _yOffset + dy
	ui_style 0, 1
	ui_infield{3} "bRefrigerator", x1, _yCurr, x3-x1-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Холодильники`, 0,
		1, `Холодильники`, 1
	ui_style 0, 0

	ui_infield "nRefrigerators", x3, _yCurr, 54, _infHeight

	ui_infield{3} "iRefrigeratorType", x4, _yCurr, wPopup, hPopup,
		2, 13, 8, 2,
		77, 64, 82, 61,
		1, stRefrigeratorType[1], ACC_REFRI_TOP,
		2, stRefrigeratorType[2], ACC_REFRI_BOTTOM,
		3, stRefrigeratorType[3], ACC_REFRI_SIDE,
		4, stRefrigeratorType[4], ACC_REFRI_FRENCH,
		5, stRefrigeratorType[5], ACC_REFRI_COMPACT

	if not(bRefrigerator) then _outfFlag = 4

	_yCurr = _yCurr + dy + _yOffset
	ui_outfield `Высота`, x2, _yCurr, x3-x2-2, _outfHeight, _outfFlag

	_outfFlag = 0

	_yCurr = _yCurr - _yOffset
	ui_infield "refrigeratorHeigth", x3, _yCurr, 97, _infHeight

	_yCurr = _yCurr + dy
	ui_infield{3} "bRefriWallCabinet", x2, _yCurr, x4-x2-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Навесной Шкаф`, 0,
		1, `Навесной Шкаф`, 1

	_yCurr = _yCurr + dy
	ui_style 0,1
	ui_infield{3} "bSink", x1, _yCurr, x3-x1-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Мойки`, 0,
		1, `Мойки`, 1
	ui_style 0,0
	ui_infield "nSink", x3, _yCurr, 54, _infHeight
	ui_infield{3} "sinkType", x4, _yCurr, wPopup, hPopup,
		2, 14, 16, 4,			! ui_sink_type.png
		82, 61, 82, 61,
		1,  stSinkType[1], stSinkType[1],
		2,  stSinkType[2], stSinkType[2],
		4,  stSinkType[3], stSinkType[3],
		3,  stSinkType[4], stSinkType[4],
		9,  stSinkType[5], stSinkType[5],
		8,  stSinkType[6], stSinkType[6]

	_yCurr = _yCurr + dy
	ui_infield{3} "bMirroredSink", x2, _yCurr, x4-x2-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Зеркально`, 0,
		1, `Зеркально`, 1

	_yCurr = _yCurr + dy
	ui_style 0, 1
	ui_infield{3} "bDishwasher", x1, _yCurr, x3-x1-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Посудомоечные Машины`, 0,
		1, `Посудомоечные Машины`, 1
	ui_style 0, 0

	ui_infield "nDishwasher", x3, _yCurr, 54, _infHeight

	_yCurr = _yCurr + dy

	ui_style 0, 1
	ui_infield{3} "bWasher", x1, _yCurr, x3-x1-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Стиральные Машины`, 0,
		1, `Стиральные Машины`, 1
	ui_style 0, 0

	ui_infield "nWasher", x3, _yCurr, 54, _infHeight

	_yCurr = _yCurr + dy
	ui_style 0, 1
	ui_infield{3} "bOven", x1, _yCurr, x3-x1-2, _infHeight,
		7, "",
		2, 1,
		20, 20, 20, 20,
		0, `Духовые Шкафы`, 0,
		1, `Духовые Шкафы`, 1
	ui_style 0, 0

	ui_infield "nOvens", x3, _yCurr, 54, _infHeight
    
return

! ------------------------------------------------------------------------------
"representationpage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_shadow"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cabinet_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if nWallCabinet > 0 then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "wallcab_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_base_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

        if bDishwasher then
            _idx = _idx + 1
            paramNameReprArray 	[_idx] = "dishwasher_mat"
            childFlagReprArray 	[_idx] = 1
            iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
        endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "count_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if bRefrigerator then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "refrigerator_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bSink then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "sink_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bWasher then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "washer_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bOven then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "oven_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bCooktop and bHood then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "hood_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bOven then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "glass_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bCooktop then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "cooktop_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
	
			if bHood then
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "hotplate_mat"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
			endif
		endif

	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return

! ------------------------------------------------------------------------------
"symboltextpage":
! ------------------------------------------------------------------------------
	pageStartY 	= 0
	_infHeight	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_outfFlag 	= 0

	dy 			= 24
	x1			= 0
	x2			= 170
	x3			= 273
	x4			= 374

	_yCurr = pageStartY + _yOffset
	if not(bRefrigerator) then _outfFlag = 4
	ui_outfield `Холодильник`, x1, _yCurr, x2 - 2, _outfHeight, _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr - _yOffset
	ui_radiobutton "iRefriSymbType", 1, `2D-символ`, 	x2, _yCurr, 96, _infHeight
	ui_radiobutton "iRefriSymbType", 2, `Текст`, 		x3, _yCurr, 96, _infHeight
	ui_infield "refriText", x4, _yCurr, 70, _infHeight

	_yCurr = _yCurr + dy + _yOffset
	if not(bOven) then _outfFlag = 4
	ui_outfield `Духовой Шкаф`, x1, _yCurr, x2 - 2, _outfHeight, _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr - _yOffset
	ui_radiobutton "iOvenSymbType", 1, `2D-символ`, 	x2, _yCurr, 96, _infHeight
	ui_radiobutton "iOvenSymbType", 2, `Текст`, 		x3, _yCurr, 96, _infHeight
	ui_infield "ovenText", x4, _yCurr, 70, _infHeight

	_yCurr = _yCurr + dy + _yOffset
	if not(bDishwasher) then _outfFlag = 4
	ui_outfield `Посудомоечная Машина`, x1, _yCurr, x2 - 2, _outfHeight, _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr - _yOffset
	ui_radiobutton "iDishwasherSymbType", 1, `2D-символ`, 	x2, _yCurr, 96, _infHeight
	ui_radiobutton "iDishwasherSymbType", 2, `Текст`, 		x3, _yCurr, 96, _infHeight
	ui_infield "dishwasherText", x4, _yCurr, 70, _infHeight

	_yCurr = _yCurr + dy + _yOffset
	if not(bWasher) then _outfFlag = 4
	ui_outfield `Стиральная Машина`, x1, _yCurr, x2 - 2, _outfHeight, _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr - _yOffset
	ui_radiobutton "iWasherSymbType", 1, `2D-символ`, 	x2, _yCurr, 96, _infHeight
	ui_radiobutton "iWasherSymbType", 2, `Текст`, 		x3, _yCurr, 96, _infHeight
	ui_infield "washerText", x4, _yCurr, 70, _infHeight

	_yCurr = _yCurr + dy
	ui_separator x1, _yCurr, 444, _yCurr


	call "ui_object_SymbolText" parameters	dt 						= displayText,
											bIsTextDisplay 			= 0,
											bParName_stDisplayText	= 0,
											fontType				= fontType,
											fontSize				= fontSize,
											bParName_FontSize		= 1,
											gs_text_style_bold		= gs_text_style_bold,
											gs_text_style_italic	= gs_text_style_italic,
											gs_text_style_underline	= gs_text_style_underline,
											bStrikeText				= 1,
											gs_text_style_strike	= gs_text_style_strike,
											bParName_PenText		= 1,
											penText					= penText,
											bOrientationShortName	= 1,
											typeTextRotation_m		= typeTextRotation_m,
											bTextPos 				= 0,
											biTextPen				= 1,
											iTextPen				= iTextPen,
											pageStartY				= 106


return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
