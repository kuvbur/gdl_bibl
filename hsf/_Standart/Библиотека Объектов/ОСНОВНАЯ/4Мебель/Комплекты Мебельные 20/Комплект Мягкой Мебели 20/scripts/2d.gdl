
! ******************************** Minimal Space and Hotspots

![#COMPATIBILITY] - START

! have to move to migration script if it is possible
gs_detlevel_2D_m = min(4, gs_detlevel_2D_m)

![#COMPATIBILITY] - END

unID = 600

HOTSPOT2 TableWidth/2, TableDepth/2, unID : unID = unID + 1

HOTSPOT2 TableWidth,   TableDepth,   unID : unID = unID + 1
HOTSPOT2 TableWidth,   0,            unID : unID = unID + 1
HOTSPOT2 0,            0,            unID : unID = unID + 1
HOTSPOT2 0,            TableDepth,   unID : unID = unID + 1

if reqBMinSpace <> 0 then
	DRAWINDEX 10
	unID = 3000
	HOTSPOT2 TableWidth/2+SofaWW/2+RightA,TableDepth/2+SofaDD/2+TopB, unID : unID = unID + 1
	HOTSPOT2 TableWidth/2-SofaWW/2-LeftA,TableDepth/2+SofaDD/2+TopB, unID : unID = unID + 1

	HOTSPOT2 TableWidth/2+SofaWW/2+RightA,TableDepth/2-SofaDD/2-BottomB, unID : unID = unID + 1
	HOTSPOT2 TableWidth/2-SofaWW/2-LeftA,TableDepth/2-SofaDD/2-BottomB, unID : unID = unID + 1

	FILL MSFillType
	pen MSContLinePen
	line_type MSContLineType

	POLY2_B 4, 1+2+4+64,MSFillPen,MSFillBackPen,
			TableWidth/2-SofaWW/2-LeftA, 	TableDepth/2+SofaDD/2+TopB,1,
			TableWidth/2+SofaWW/2+RightA, 	TableDepth/2+SofaDD/2+TopB,1,
			TableWidth/2+SofaWW/2+RightA, 	TableDepth/2-SofaDD/2-BottomB,1,
			TableWidth/2-SofaWW/2-LeftA, 	TableDepth/2-SofaDD/2-BottomB,1


	HOTLINE2 TableWidth/2-SofaWW/2-LeftA, TableDepth/2+SofaDD/2+TopB,TableWidth/2+SofaWW/2+RightA, TableDepth/2+SofaDD/2+TopB
	HOTLINE2 TableWidth/2+SofaWW/2+RightA, TableDepth/2+SofaDD/2+TopB,TableWidth/2+SofaWW/2+RightA, TableDepth/2-SofaDD/2-BottomB
	HOTLINE2 TableWidth/2+SofaWW/2+RightA, TableDepth/2-SofaDD/2-BottomB,TableWidth/2-SofaWW/2-LeftA, TableDepth/2-SofaDD/2-BottomB
	HOTLINE2 TableWidth/2-SofaWW/2-LeftA, TableDepth/2-SofaDD/2-BottomB,TableWidth/2-SofaWW/2-LeftA, TableDepth/2+SofaDD/2+TopB

endif

! ******************************** 2D
	DRAWINDEX 30
	PEN gs_cont_pen
	line_type 1

	! --- Top Sofa ---
	if bSeatTop then
		if iTopSofaType = TOP_SOFATYPE_1CHAIR then
			add2 - ArmchairWidth / 2 + TableWidth / 2, TableDepth / 2 + SofaDD / 2 + MS
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
				  							ZZYZX				= 0.8,
											gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 1
			del 1
		endif
	
		if iTopSofaType = TOP_SOFATYPE_2CHAIRS then
			add2 TableWidth / 2 + MS / 2, TableDepth / 2 + SofaDD / 2 + MS
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 1
			add2 -ArmchairWidth-MS,0
			call "fa_sofa01" parameters all	A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 100
			del 2
		endif
	
		if iTopSofaType = TOP_SOFATYPE_2SEATSOFA then
			add2 - TopSofaWidth / 2 + TableWidth / 2, TableDepth / 2 + SofaDD / 2 + MS
			call "fa_sofa01" parameters all	A					= TopSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 2,
											gs_hotspotUnIdBegin = 1
			del 1
		endif
	
		if iTopSofaType = TOP_SOFATYPE_3SEATSOFA then
			add2 - TopSofaWidth / 2 + TableWidth / 2, TableDepth / 2 + SofaDD / 2 + MS
			call "fa_sofa01" parameters all	A					= TopSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 3,
											gs_hotspotUnIdBegin = 1
			del 1
		endif
	endif

	! --- Bottom Sofa ---

	if bSeatBottom then
		if iBottomSofaType = BOT_SOFATYPE_1CHAIR then
			add2 - ArmchairWidth / 2 + TableWidth / 2, TableDepth / 2 - SofaDD / 2 - MS
			mul2 1,-1
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 200
			del 2
		endif
	
		if iBottomSofaType = BOT_SOFATYPE_2CHAIRS then
			add2 TableWidth / 2 + MS / 2, TableDepth / 2 - SofaDD / 2 - MS
			mul2 1,-1
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 200
			add2 - ArmchairWidth - MS, 0
			call "fa_sofa01" parameters all	A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
										 	gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 300
			del 3
		endif
	
		if iBottomSofaType = BOT_SOFATYPE_2SEATSOFA then
			add2 - BottomSofaWidth / 2 + TableWidth / 2, TableDepth / 2 - SofaDD / 2 - MS
			mul2 1,-1
			call "fa_sofa01" parameters all	A					= BottomSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 2,
											gs_hotspotUnIdBegin = 200
			del 2
		endif
	
		if iBottomSofaType = BOT_SOFATYPE_3SEATSOFA then
			add2 - BottomSofaWidth / 2 + TableWidth / 2, TableDepth / 2 - SofaDD / 2 - MS
			mul2 1,-1
			call "fa_sofa01" parameters all A					= BottomSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
				  							gs_SeatNum_m		= 3,
											gs_hotspotUnIdBegin = 200
			del 2
		endif
	endif


	! --- Right Armchair ---
	IF RightArmchair = 1 THEN
		ADD2 TableWidth/2+SofaWW/2+MS,TableDepth/2+ArmchairWidth/2
			ROT2 -90
			CALL "fa_sofa01" PARAMETERS ALL A=ArmchairWidth, B=ArmchairDepth, ZZYZX=0.8,
					 gs_SeatNum_m=1, gs_hotspotUnIdBegin = 400
			DEL 1
		DEL 1
	ENDIF


	! --- Left Armchair ---

	IF LeftArmchair = 1 THEN
		ADD2 TableWidth/2-SofaWW/2-MS,TableDepth/2-ArmchairWidth/2
			ROT2 90
			CALL "fa_sofa01" PARAMETERS ALL	 A=ArmchairWidth, B=ArmchairDepth, ZZYZX=0.8,
					 gs_SeatNum_m=1, gs_hotspotUnIdBegin = 500
			DEL 1
		DEL 1
	ENDIF


	! --- Table ---
	FILL gs_fill_type
	POLY2_B 5, 1+2+64,gs_fill_pen,gs_back_pen,
		0,0,1,
		TableWidth,0,1,
		TableWidth,TableDepth,1,
		0,TableDepth,1,
		0,0,-1

END
