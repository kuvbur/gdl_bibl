


IF gs_detlevel_3D_m=0 THEN END

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1
IF gs_detlevel_3D_m=1 THEN gs_resol=4

IF not(gs_shadow) THEN SHADOW OFF

PEN gs_cont_pen

	!!! *** Top Sofa *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if bSeatTop then
		if iTopSofaType = TOP_SOFATYPE_1CHAIR then
			add - ArmchairWidth / 2 + TableWidth / 2, TableDepth / 2 + SofaDD / 2 + MS,0
			 call "fa_sofa01" parameters all	A					= ArmchairWidth,
												B					= ArmchairDepth,
												ZZYZX				= 0.8,
												gs_SeatNum_m		= 1,
												gs_hotspotUnIdBegin = 1,
												gs_leg_type_m		= iLegType
			del 1
		endif
	
		if iTopSofaType = TOP_SOFATYPE_2CHAIRS then
			add TableWidth / 2 + MS / 2, TableDepth / 2 + SofaDD / 2 + MS,0
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 1,
											gs_leg_type_m		= iLegType
			add - ArmchairWidth - MS, 0, 0
			call "fa_sofa01" parameters all	A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 100,
											gs_leg_type_m		= iLegType
			del 2
		endif
	
		if iTopSofaType = TOP_SOFATYPE_2SEATSOFA then
			add - TopSofaWidth / 2 + TableWidth / 2, TableDepth / 2 + SofaDD / 2 + MS, 0
			call "fa_sofa01" parameters all	A					= TopSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 2,
											gs_hotspotUnIdBegin = 1,
											gs_leg_type_m		= iLegType
			del 1
		endif
	
		if iTopSofaType = TOP_SOFATYPE_3SEATSOFA then
			add - TopSofaWidth / 2 + TableWidth / 2, TableDepth / 2 + SofaDD / 2 + MS, 0
			call "fa_sofa01" parameters all A					= TopSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 3,
											gs_hotspotUnIdBegin = 1,
											gs_leg_type_m		= iLegType
			del 1
		endif
	endif

	!!! *** Bottom Sofa *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if bSeatBottom then
		if iBottomSofaType = BOT_SOFATYPE_1CHAIR then
			add - ArmchairWidth / 2 + TableWidth / 2, TableDepth / 2 - SofaDD / 2 - MS, 0
			mul 1, -1, 1
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 200,
											gs_leg_type_m		= iLegType
			del 2
		endif
	
		if iBottomSofaType = BOT_SOFATYPE_2CHAIRS then
			add TableWidth / 2 + MS / 2, TableDepth / 2 - SofaDD / 2 - MS, 0
			mul 1, -1, 1
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 200,
											gs_leg_type_m		= iLegType
			add -ArmchairWidth-MS,0,0
			call "fa_sofa01" parameters all A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 300,
											gs_leg_type_m		= iLegType
			del 3
		endif
	
		if iBottomSofaType = BOT_SOFATYPE_2SEATSOFA then
			add - BottomSofaWidth / 2 + TableWidth / 2, TableDepth / 2 - SofaDD / 2 - MS, 0
			mul 1, -1, 1
			call "fa_sofa01" parameters all	A					= BottomSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 2,
											gs_hotspotUnIdBegin = 200,
											gs_leg_type_m		= iLegType
			del 2
		endif
	
		if iBottomSofaType = BOT_SOFATYPE_3SEATSOFA then
			add - BottomSofaWidth / 2 + TableWidth / 2, TableDepth / 2 - SofaDD / 2 - MS, 0
			mul 1, -1, 1
			call "fa_sofa01" parameters all A					= BottomSofaWidth,
											B					= SofaDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 3,
											gs_hotspotUnIdBegin = 200,
											gs_leg_type_m		= iLegType
			del 2
		endif
	endif

	!!! *** Right Armchair *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	if RightArmchair = 1 then
		add TableWidth / 2 + SofaWW / 2 + MS, TableDepth / 2 + ArmchairWidth / 2,0
			ROTz -90
			call "fa_sofa01" parameters all	A					= ArmchairWidth,
											B					= ArmchairDepth,
											ZZYZX				= 0.8,
											gs_SeatNum_m		= 1,
											gs_hotspotUnIdBegin = 400,
											gs_leg_type_m		= iLegType
			del 1
		del 1
	endif


	!!! *** Left Armchair *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if LeftArmchair = 1 then
		add TableWidth / 2 - SofaWW / 2 - MS, TableDepth / 2 - ArmchairWidth / 2, 0
			rotz  90
			call "fa_sofa01" parameters all A 					= ArmchairWidth,
											B 					= ArmchairDepth,
											ZZYZX 				= 0.8,
											gs_SeatNum_m 		= 1, 
											gs_hotspotUnIdBegin = 500,
											gs_leg_type_m		= iLegType
			del 1
		del 1
	endif


	!!! *** Table *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		h=TableHeight
		a=TableWidth
		b=TableDepth

		unID = 1000
		hotspot a/2, b/2, 0,	unID, TableHeight, 1	: unID = unID + 1
		hotspot a/2, b/2, h,	unID, TableHeight, 2	: unID = unID + 1
		hotspot a/2, b/2, -1,	unID, TableHeight, 3	: unID = unID + 1

		!!!!!!!Tabletop!!!!!!!

		if gs_glass then
			material gs_top_mat
			addz h-gs_top_thk

			Prism_ 5,gs_top_thk,
				0,0,15,
				a,0,15,
				a-tbo-gs_leg,tboy+gs_leg,15,
				tbo+gs_leg,tboy+gs_leg,15,
				0,0,-1

			gosub 120

			Prism_ 5,gs_top_thk,
				a,b,15,
				0,b,15,
				tbo+gs_leg,b-tboy-gs_leg,15,
				a-tbo-gs_leg,b-tboy-gs_leg,15,
				a,b,-1

			gosub 120

			PRISM_ 5,gs_top_thk,
				0,0,15,
				0,b,15,
				tbo+gs_leg,b-tboy-gs_leg,15,
				tbo+gs_leg,tboy+gs_leg,15,
				0,0,-1

			PRISM_ 5,gs_top_thk,
				a,0,15,
				a,b,15,
				a-tbo-gs_leg,b-tboy-gs_leg,15,
				a-tbo-gs_leg,tboy+gs_leg,15,
				a,0,-1

			material gs_glass_mat
			PRISM_ 5,gs_top_thk,
				tbo+gs_leg,tboy+gs_leg,15,
				a-tbo-gs_leg,tboy+gs_leg,15,
				a-tbo-gs_leg,b-tboy-gs_leg,15,
				tbo+gs_leg,b-tboy-gs_leg,15,
				tbo+gs_leg,tboy+gs_leg,-1

		del 1
		else
			material gs_top_mat
			addz h-gs_top_thk
			PRISM_ 4,gs_top_thk,
				0,0,15,
				a,0,15,
				a,b,15,
				0,b,15

			gosub 120

			del 1
		endif

		!!!!! Leg !!!!!!

		material TableLegMat
		addy tboy
			addx tbo
				gosub 110
			del 1
			addx a-gs_leg-tbo
				gosub 110
			addy b-gs_leg-tboy*2
				gosub 110
			addx -(a-gs_leg-tbo*2)
				gosub 110
		del 4

		!!!!!!!Shelv!!!!!!
		if gs_shelv then

			material gs_shelv_mat

			addz gs_shelv_pos
			PRISM_ 4,gs_shelv_thk,
				tbo,gs_leg+tboy,15,
				a-tbo,gs_leg+tboy,15,
				a-tbo,b-gs_leg-tboy,15,
				tbo,b-gs_leg-tboy,15
			del 1
		endif


		a=a
		b=b

END



!!! *********************** S U B R O U T I N E S ***************************


110:
	brick gs_leg,gs_leg,h-gs_top_thk

	roty 90

		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4

		base

		body 1
	del 1

	return

120:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body 1

return



