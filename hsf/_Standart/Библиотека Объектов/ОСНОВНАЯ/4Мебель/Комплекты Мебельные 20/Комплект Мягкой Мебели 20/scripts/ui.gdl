
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_Layout					= _idxTab  : _idxTab = _idxTab + 1
TABID_Dim						= _idxTab  : _idxTab = _idxTab + 1
TABID_Leg						= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Комплекта Мягкой Мебели`
ui_current_page gs_ui_current_page

ui_page TABID_Layout, TABID_Root, `Компоновка`, "uiTab_Style_flat(1)"
	gosub "layoutPage"

ui_page TABID_Dim, TABID_Root, `Размеры и Минимальное Пространство`, "uiTab_Dimension_flat(1)"
	gosub "dimensionsPage"

ui_page TABID_Leg, TABID_Root, `Ножки Дивана и Стол`, "uiTab_Leg_flat(1)"
	gosub "legpage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Leg_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"layoutPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_infHeight2 = 27
	_outfHeight = 15
	_yOffset = 4
	dy = 30

	xPictTable = 187
	yPictTable = pageStartY + 113
	PictWidthTable = 69
	PictHeightTable = 35

	PictWidthChair 	= 41
	PictDepthChair 	= 41
	PictWidthSofa2 	= 81
	PictWidthSofa3 	= 101
	PictDepthSofa	= 45

	ui_pict	1,	xPictTable,	yPictTable,	PictWidthTable, PictHeightTable
	
	! Top seating
	_yCurr = yPictTable - PictDepthSofa - 10 - _infHeight2 - 3 - dy
	ui_infield{3} "bSeatTop", xPictTable + PictWidthTable / 2 - 120 / 2, _yCurr, 120, _infHeight2,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Элемент Вверху`, 0,
					"", `Элемент Вверху`, 1

	
	ui_infield{3} "iTopSofaType", xPictTable + PictWidthTable / 2 - 120 / 2, _yCurr + dy,	120,	_infHeight,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"", `1 кресло`, 			TOP_SOFATYPE_1CHAIR,
		"", `2 кресла`,			TOP_SOFATYPE_2CHAIRS,
		"", `Диван Двухместный`, 	TOP_SOFATYPE_2SEATSOFA,
		"", `Диван Трехместный`, 	TOP_SOFATYPE_3SEATSOFA


	if bSeatTop then
		if iTopSofaType = TOP_SOFATYPE_1CHAIR then
			ui_pict	2,	xPictTable + 14 ,	yPictTable - PictDepthChair - 13, PictWidthChair, PictDepthChair
		endif
		
		if iTopSofaType = TOP_SOFATYPE_2CHAIRS then
			ui_pict	2,	xPictTable + PictWidthTable / 2 + 5,					yPictTable - PictDepthChair - 13,	PictWidthChair, PictDepthChair 
			ui_pict	2,	xPictTable + PictWidthTable / 2 - PictWidthChair - 5,	yPictTable - PictDepthChair - 13,	PictWidthChair, PictDepthChair 
		endif
		
		if iTopSofaType = TOP_SOFATYPE_2SEATSOFA then
			ui_pict	3,	xPictTable + PictWidthTable / 2 - PictWidthSofa2 / 2,	yPictTable - PictDepthSofa - 13,	PictWidthSofa2,	PictDepthSofa
		endif
		
		if iTopSofaType = TOP_SOFATYPE_3SEATSOFA then
			ui_pict	4,	xPictTable + PictWidthTable / 2 - PictWidthSofa3 / 2,	yPictTable - PictDepthSofa - 13,	PictWidthSofa3,	PictDepthSofa
		endif
	endif

	! Bottom Seating	
	_yCurr = yPictTable + PictHeightTable + PictDepthSofa + 13 + 5
	ui_infield{3} "bSeatBottom", xPictTable + PictWidthTable / 2 - 120 / 2, _yCurr, 120, _infHeight2,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Элемент Внизу`, 0,
					"", `Элемент Внизу`, 1
	
	ui_infield{3} "iBottomSofaType", xPictTable + PictWidthTable / 2 - 120 / 2,	_yCurr + dy,	120,	_infHeight,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"", `1 кресло`, 			TOP_SOFATYPE_1CHAIR,
		"", `2 кресла`,			TOP_SOFATYPE_2CHAIRS,
		"", `Диван Двухместный`, 	TOP_SOFATYPE_2SEATSOFA,
		"", `Диван Трехместный`, 	TOP_SOFATYPE_3SEATSOFA

	if bSeatBottom then
		if iBottomSofaType = BOT_SOFATYPE_1CHAIR then
			ui_pict	5,	xPictTable + 14, yPictTable + PictHeightTable + 13, PictWidthChair, PictDepthChair
		endif
		
		if iBottomSofaType = BOT_SOFATYPE_2CHAIRS then
			ui_pict	5,	xPictTable + PictWidthTable / 2 + 5,					yPictTable + PictHeightTable + 13, PictWidthChair,	PictDepthChair
			ui_pict	5,	xPictTable + PictWidthTable / 2 - PictWidthChair - 5,	yPictTable + PictHeightTable + 13, PictWidthChair,	PictDepthChair
		endif
		
		if iBottomSofaType = BOT_SOFATYPE_2SEATSOFA then
			ui_pict	6,	xPictTable + PictWidthTable / 2 - PictWidthSofa2 / 2, yPictTable + PictHeightTable + 13, PictWidthSofa2, PictDepthSofa
		endif
		
		if iBottomSofaType = BOT_SOFATYPE_3SEATSOFA then
			ui_pict	7, xPictTable + PictWidthTable / 2 - PictWidthSofa3 / 2, yPictTable + PictHeightTable + 13, PictWidthSofa3, PictDepthSofa
		endif
	endif

	! Left Seating	
	_yCurr = yPictTable + 4
	ui_infield{3} "LeftArmchair", 0, _yCurr, 120, _infHeight2,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Кресло Слева`, 0,
					"", `Кресло Слева`, 1
	

	if LeftArmchair = 1 then
		ui_pict	8,	xPictTable - PictDepthChair - 25,	yPictTable + PictHeightTable / 2 - PictWidthChair / 2,	PictDepthChair,	PictWidthChair
	endif
	
	! Right Seating
	ui_infield{3} "RightArmchair", xPictTable + PictWidthTable + 25 + PictDepthChair + 3, _yCurr, 118, _infHeight2,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Кресло Справа`, 0,
					"", `Кресло Справа`, 1

	if RightArmchair = 1 then
		ui_pict	9,	xPictTable + PictWidthTable + 25,	yPictTable + PictHeightTable / 2 - PictWidthChair / 2,	PictDepthChair,	PictWidthChair
	endif

return

! ------------------------------------------------------------------------------
"dimensionsPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37
	xPict = 100
	yPict = 77
	PictWidth = 227 
	PictHeight = 167
	_outfFlag = 0
	x1 = 0
	x2 = 123
	x3 = 232
	x4 = 354
	x5 = xPict - 93
	x6 = xPict - 3
	x7 = xPict + PictWidth +3
	dy = 24

	_yCurr = pageStartY + _yOffset
	ui_style 0, 1
	ui_outfield `Стол`, x1, _yCurr, 442, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + dy
	ui_outfield `Ширина`, 	x1, _yCurr, 120, _outfHeight
	ui_outfield `Ширина`, 	x3, _yCurr, 120, _outfHeight

	_yCurr = _yCurr - _yOffset
	ui_infield "TableWidth", x2, _yCurr, 90, _infHeight ui_tooltip `Ширина Стола`
	ui_infield "TableDepth", x4, _yCurr, 90, _infHeight ui_tooltip `Длина Стола`

	_yCurr = _yCurr + dy
	ui_separator x1, _yCurr, 444, _yCurr

	ui_pict 10, xPict, yPict, PictWidth, PictHeight, 1

	dimensionForm = ""
	 qq= REQUEST ("Working_length_unit", "", dimensionForm)

	_yCurr = yPict - _infHeight - 3
	ui_infield "TopSofaWidth", xPict + 77, _yCurr, 90, _infHeight ui_tooltip `Ширина Верхнего Дивана`

	ui_infield "ArmchairDepth", xPict + 172, _yCurr, 90, _infHeight ui_tooltip `Глубина Кресла`
	ui_outfield str(dimensionForm, MS), xPict - 20, _yCurr + _yOffset, 90, _outfHeight, 1 ui_tooltip `Расстояние между Элементами`

	_yCurr = yPict + 21
	ui_infield "SofaDepth", x7, _yCurr, 90, _infHeight ui_tooltip `Глубина Дивана`

	_yCurr = yPict + 69
	ui_infield "ArmchairWidth", x7, _yCurr, 90, _infHeight ui_tooltip `Ширина Кресла`

	_yCurr = yPict + 93
	ui_infield "MS", x7, _yCurr, 90, _infHeight ui_tooltip `Расстояние между Элементами`

	_yCurr = yPict + 119

	if 	(iTopSofaType = TOP_SOFATYPE_1CHAIR or iTopSofaType = TOP_SOFATYPE_2CHAIRS) &\
		(iBottomSofaType = BOT_SOFATYPE_1CHAIR or iBottomSofaType = BOT_SOFATYPE_2CHAIRS) then
		_outfFlag = 4
	else
		_outfFlag = 0
	endif

	ui_outfield str(dimensionForm, SofaDepth), x7, _yCurr, 72, _outfHeight, _outfFlag + 1 ui_tooltip `Глубина Дивана`

	_yCurr = yPict + PictHeight + 3
	ui_infield "BottomSofaWidth", xPict + 77, _yCurr, 90, _infHeight ui_tooltip `Ширина Нижнего Дивана`

	ui_infield "MS2", xPict + 172, _yCurr, 90, _infHeight ui_tooltip `Расстояние за Креслами`
	ui_outfield str(dimensionForm, MS2), xPict - 65, _yCurr + _yOffset, 90, _outfHeight, 1 ui_tooltip `Расстояние за Креслами`

	_yCurr = yPict + 69
	ui_outfield str(dimensionForm, B), x5, _yCurr, 90, _outfHeight, 1 ui_tooltip `Размер 2`

	_yCurr = yPict + PictHeight - 9
	ui_outfield str(dimensionForm, A), x5, _yCurr, 90, _outfHeight, 1 ui_tooltip `Размер 1`

return

! ------------------------------------------------------------------------------
"legpage":
! ------------------------------------------------------------------------------

	ui_separator 222, 0, 222, 265

	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	xPict = 231
	yPict = 88
	PictWidth = 120 
	PictHeight = 63
	_outfFlag = 0
	x1 = 231
	x2 = xPict + PictWidth + 3
	dy = 22

	_yMatInf = yPict + dy * 2 + 25
	call "ui_furniture_parts" parameters all 	pageStartX	= 0,
												pageStartY	= 0,
												bIsSofaLeg	= 1,
												bSofaLayout	= 1,
												stLegTypes	= stLegTypes,
												yLegMatInf	= _yMatInf

	_yCurr = pageStartY + _yOffset
	ui_style 0, 1
	ui_outfield `Стол`, x1, _yCurr, 444 - x1, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr- _yOffset + dy
	ui_infield{3} "gs_shelv", x1, _yCurr, 444 - x1, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Полка`, 0,
					"", `Полка`, 1


	_yCurr = _yCurr + dy
	ui_infield{3} "gs_glass", x1, _yCurr, 444 - x1, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Стекло в Столешнице`, 0,
					"", `Стекло в Столешнице`, 1

	ui_pict 11, xPict, yPict, PictWidth, PictHeight, 1	! ui_SofaLayout_SofaLegAndTable.png

	_yCurr = yPict - _infHeight - 3
	ui_infield "TableHeight", xPict, _yCurr, 90, _infHeight ui_tooltip `Высота Столешницы`
	ui_infield "gs_top_thk", xPict + 92, _yCurr, 90, _infHeight ui_tooltip `Толщина Столешницы`

	_yCurr = yPict
	ui_infield "gs_shelv_thk", x2, _yCurr, 90, _infHeight ui_tooltip `Толщина Полки`

	_yCurr = yPict + dy
	ui_infield "gs_shelv_pos", x2, _yCurr, 90, _infHeight ui_tooltip `Расположение Полки`

	_yCurr = yPict + dy * 2
	ui_infield "gs_leg", x2, _yCurr, 90, _infHeight ui_tooltip `Толщина Ножек`

	dy = 27
	_yCurr = _yCurr + 25
	ui_pict 13, x1, _yCurr + 3, 18, 18, 1 ui_tooltip `Покрытие Ножек Стола`	! ui_SofaLayout_SurfaceTableLeg.png
	ui_infield{3} "TableLegMat", x1 + 20, _yCurr, 193, 25 ui_tooltip `Ножки Стола`

	_yCurr = _yCurr + dy
	ui_pict 15, x1, _yCurr + 3, 18, 18, 1 ui_tooltip `Покрытие Столешницы`	! ui_SofaLayout_SurfaceTableTop.png
	ui_infield{3} "gs_top_mat", x1 + 20, _yCurr, 193, 25 ui_tooltip `Столешница`

	if gs_shelv then
		_yCurr = _yCurr + dy
		ui_pict 14, x1, _yCurr + 3, 18, 18, 1 ui_tooltip `Покрытие Полки Стола`	! ui_SofaLayout_SurfaceTableShelf.png
		ui_infield{3} "gs_shelv_mat", x1 + 20, _yCurr, 193, 25 ui_tooltip `Полка Стола`
	endif

	if gs_glass then
		_yCurr = _yCurr + dy
		ui_pict 12, x1, _yCurr + 3, 18, 18, 1 ui_tooltip `Покрытие Стекла`	! ui_SofaLayout_SurfaceGlass.png
		ui_infield{3} "gs_glass_mat", x1 + 20, _yCurr, 193, 25 ui_tooltip `Стекло`
	endif
return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	dim stSymbolTypes[7]
		stSymbolTypes[1] = `Прямоугольный, Простой`
		stSymbolTypes[2] = `Прямоугольный 1`
		stSymbolTypes[3] = `Прямоугольный 2`
		stSymbolTypes[4] = `Прямоугольный 3`
		stSymbolTypes[5] = `Прямоугольный 4`
		stSymbolTypes[6] = `Криволинейный, Простой`
		stSymbolTypes[7] = `Криволинейный`

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 4,
													iSofaSymbolType		= iSofaSymbolType,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													bShowSeatsIn2D		= 1,
													gs_2DSeat			= gs_2DSeat,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													pageStartY 			= - 1,
													iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
 	SurfItemID = 1
		if bSeatTop and bSeatBottom and LeftArmchair and RightArmchair then   
			surfacesArray[SurfItemID]	= "gs_sofa_mat"
			SurfItemID = SurfItemID + 1

			surfacesArray[SurfItemID]	= "gs_cush_mat"
			SurfItemID = SurfItemID + 1

			if bLeg then
				surfacesArray[SurfItemID]	= "gs_leg_mat"
				SurfItemID = SurfItemID + 1
			endif
		endif

		surfacesArray[SurfItemID]	= "gs_top_mat"
		SurfItemID = SurfItemID + 1

		if gs_shelv then 
			surfacesArray[SurfItemID]	= "gs_shelv_mat"
			SurfItemID = SurfItemID + 1
		endif

		surfacesArray[SurfItemID]	= "TableLegMat"
		SurfItemID = SurfItemID + 1

		if gs_glass then
			surfacesArray[SurfItemID]	= "gs_glass_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													pageStartY 			= 3,
													iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
