
call "FM_types" parameters all

!Hide all parameters from UI
hideparameter all  "A", "B", "ZZYZX"

parameters resolleg = gs_resol
!!! *************************************************

values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
values "gs_detlevel_3D_m" 2,1,0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	if gs_detlevel_3D = `Детальный` then gs_detlevel_3D_m = 2
	if gs_detlevel_3D = `Простой` then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Откл.` then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = `Детальный`
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

IF gs_detlevel_3D_m = 1 then
	hideparameter "gs_resol"
	lock "gs_resol"
endif
IF gs_detlevel_3D_m = 0 then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif

values{2} "iTopSofaType"	TOP_SOFATYPE_1CHAIR, `1 кресло`,
							TOP_SOFATYPE_2CHAIRS, `2 кресла`,
							TOP_SOFATYPE_2SEATSOFA, `Диван Двухместный`,
							TOP_SOFATYPE_3SEATSOFA, `Диван Трехместный`

values{2} "iBottomSofaType"		BOT_SOFATYPE_1CHAIR, `1 кресло`,
								BOT_SOFATYPE_2CHAIRS, `2 кресла`,
								BOT_SOFATYPE_2SEATSOFA, `Диван Двухместный`,
								BOT_SOFATYPE_3SEATSOFA, `Диван Трехместный`


values{2} "iSofaSymbolType" 	SOFASYMBOL_RECTSIMPLE, `Прямоугольный, Простой`,
								SOFASYMBOL_RECT1, `Прямоугольный 1`,
								SOFASYMBOL_RECT2, `Прямоугольный 2`,
								SOFASYMBOL_RECT3, `Прямоугольный 3`,
								SOFASYMBOL_RECT4, `Прямоугольный 4`,
								SOFASYMBOL_CURVEDSIMPLE, `Криволинейный, Простой`,
								SOFASYMBOL_CURVED, `Криволинейный`

VALUES{2} "iLegType"	LEG_TYPE_1,		stLegTypes[1],
							LEG_TYPE_2,		stLegTypes[2],
							LEG_TYPE_3,		stLegTypes[3],
							LEG_TYPE_4,		stLegTypes[4],
							LEG_TYPE_5,		stLegTypes[5]

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

if gs_detlevel_2D_m <> 4 then 
	hideparameter	"iSofaSymbolType"
	lock			"iSofaSymbolType"
endif

IF gs_detlevel_3D_m = 1 THEN LOCK "gs_resol"
if not(bLeg) then
	lock "leg_h", "gs_leg_mat", "iLegType"
	leg_h = 0
endif

if iLegType = LEG_TYPE_1 and bLeg then
	PARAMETERS leg_h = 0.08
	LOCK "leg_h"
	leg_h = 0.08
endif

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19

IF NOT(gs_glass) THEN
	HIDEPARAMETER "gs_glass_mat"
	LOCK "gs_glass_mat"
ENDIF

!!! ******************************** Limitation

VALUES "MS" RANGE [0,)

VALUES "TopSofaWidth" RANGE (0.3,)
VALUES "BottomSofaWidth" RANGE (0.3,)
VALUES "SofaDepth" RANGE (0.15,)
VALUES "ArmchairWidth" RANGE (0.3,)
VALUES "ArmchairDepth" RANGE (0.3,)
VALUES "leg_h" RANGE (0,gs_seat_height)

VALUES "TableWidth" RANGE (0,)
VALUES "TableDepth" RANGE (0,)
VALUES "TableHeight" RANGE (0,ZZYZX]
VALUES "gs_top_thk" RANGE (0,)
VALUES "gs_leg" RANGE (0,)

VALUES "gs_resol" RANGE [3,)

VALUES "MS2" RANGE [0,)

values "gs_shelv_pos" range [0, TableHeight - gs_top_thk - 0.05]
values "gs_shelv_thk" range (0, TableHeight - gs_top_thk - gs_shelv_pos]

!!! ******************************** Sofa / Armchair Hide

if not(bSeatTop) or iTopSofaType = TOP_SOFATYPE_1CHAIR or iTopSofaType = TOP_SOFATYPE_2CHAIRS then
	hideparameter "TopSofaWidth"
	lock "TopSofaWidth"
	if not(bSeatBottom) or iBottomSofaType = BOT_SOFATYPE_1CHAIR or iBottomSofaType = BOT_SOFATYPE_2CHAIRS then
		hideparameter "SofaDepth", "gs_2DSeat"
		lock "SofaDepth"
	endif
endif

if not(bSeatBottom) or iBottomSofaType = BOT_SOFATYPE_1CHAIR or iBottomSofaType = BOT_SOFATYPE_2CHAIRS then
	hideparameter "BottomSofaWidth"
	lock "BottomSofaWidth"
endif

IF iTopSofaType <> TOP_SOFATYPE_1CHAIR AND iTopSofaType <> TOP_SOFATYPE_2CHAIRS AND iBottomSofaType <> BOT_SOFATYPE_1CHAIR AND \
 iBottomSofaType <> BOT_SOFATYPE_2CHAIRS AND LeftArmchair = 0 AND RightArmchair = 0 THEN
	HIDEPARAMETER "ArmchairWidth", "ArmchairDepth"
	LOCK "ArmchairWidth", "ArmchairDepth"
ENDIF

IF not(bSeatTop) and not(bSeatBottom) and LeftArmchair = 0 and RightArmchair = 0 then
	hideparameter "MS", "gs_structural_dim", "gs_resol", "gs_sofa_mat", "gs_cush_mat", "gs_leg_mat", "bLeg"
	lock "MS", "iLegType", "leg_h", "gs_resol", "gs_sofa_mat", "gs_cush_mat", "gs_leg_mat", "bLeg"
endif

if not(bSeatTop) then
	hideparameter "iTopSofaType"
	lock "iTopSofaType"
endif

if not(bSeatBottom) then
	hideparameter "iBottomSofaType"
	lock "iBottomSofaType"
endif

if not(gs_shelv) then
	hideparameter "gs_shelv_thk", "gs_shelv_pos"
	lock "gs_shelv_thk", "gs_shelv_pos"
endif

!!! ******************************** Def. Sofa Width

IF GLOB_MODPAR_NAME = "iTopSofaType" THEN
		IF iTopSofaType = TOP_SOFATYPE_2SEATSOFA THEN
		PARAMETERS TopSofaWidth = 1.6
		TopSofaWidth = 1.6
	endif
	IF iTopSofaType = TOP_SOFATYPE_3SEATSOFA THEN
		PARAMETERS TopSofaWidth = 2
		TopSofaWidth = 1.6
	endif
ENDIF

IF GLOB_MODPAR_NAME = "iBottomSofaType" THEN
	IF iBottomSofaType = BOT_SOFATYPE_2SEATSOFA THEN
		PARAMETERS BottomSofaWidth = 1.6
		BottomSofaWidth = 1.6
	endif
	IF iBottomSofaType = BOT_SOFATYPE_3SEATSOFA THEN
		PARAMETERS BottomSofaWidth = 2
		BottomSofaWidth = 1.6
	endif
ENDIF


!!! ******************************** A, B, ZZYZX Sizes

A = SofaWW+LeftArmchair*(MS+ArmchairDepth)+RightArmchair*(MS+ArmchairDepth)
PARAMETERS A = A

B = SofaDD+TopBB+BottomBB
PARAMETERS B = B

if not(bSeatTop) and not(bSeatBottom) and LeftArmchair = 0 and RightArmchair = 0 then
	parameters zzyzx = TableHeight
else
	parameters zzyzx = 0.8
endif

MinSpaceLength = A+MS2*LeftArmchair+MS2*RightArmchair
PARAMETERS MinSpaceLength = MinSpaceLength

MinSpaceWidth = B+MSTop+MSBottom
PARAMETERS MinSpaceWidth = MinSpaceWidth

LOCK "A","B","ZZYZX", "MinSpaceWidth", "MinSpaceLength"

