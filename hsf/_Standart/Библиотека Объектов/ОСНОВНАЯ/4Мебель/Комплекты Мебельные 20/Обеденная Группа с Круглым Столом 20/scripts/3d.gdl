
if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then SHADOW off
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m = 1
if gs_detlevel_3D_m = 1 then gs_resol=12

RESOL gs_resol

pen gs_cont_pen
h=tableh


!!!!! ******************************** Table

MATERIAL gs_top_mat
addz tableh-gs_top_thk
cylind gs_top_thk, tablew/2

del 1


material gs_leg_mat
addy -tablew/3+gs_leg
addx -tablew/3+gs_leg
GOSUB 80
del 1
addx tablew/3-gs_leg
GOSUB 80
addy tablew*2/3-gs_leg*2
GOSUB 80
addx -tablew*2/3+gs_leg*2
GOSUB 80
del 4

!!! ******************************** Chair Layout
chairHotspotUnId  = 5
FOR kk=1 to ChairNum
	IF kk = 1 AND bWheelchair THEN
	ELSE
		ADD -tablew/2-ChairTableDist,-chairw/2,0
		ROTz 90
		GOSUB 500
		DEL 2
		hotspot -tablew2, 0, 0, chairHotspotUnId ,  "ChairTableDist", 1+128				: chairHotspotUnId = chairHotspotUnId + 1
		hotspot 1, 0, 0, chairHotspotUnId , "ChairTableDist", 3							: chairHotspotUnId = chairHotspotUnId + 1
		hotspot -tablew2-ChairTableDist, 0, 0, chairHotspotUnId , "ChairTableDist", 2	: chairHotspotUnId = chairHotspotUnId + 1
	ENDIF
	ROTz alfa
NEXT kk
DEL ChairNum

unID = chairHotspotUnId
! --- Middle hotspot stretchable -----------------------------------------------
hotspot 0, 0, 0, 					unID, tableh, 1: unID = unID + 1
hotspot 0, 0, -1, 					unID, tableh, 3: unID = unID + 1
hotspot 0, 0, tableh,				unID, tableh, 2: unID = unID + 1

! --- Radius stretchable -------------------------------------------------------
for k = 1 to 4
	hotspot 0, 0, tableh,			unID, tablew2, 1+128, tablew:	unID=unID+1
	hotspot -1, 0, tableh, 			unID, tablew2, 3+128, tablew: 	unID=unID+1
	hotspot tablew2, 0, tableh,		unID, tablew2, 2, tablew: 		unID=unID+1
	rotz 90
next k
del 4

END



!!! *********************** S U B R O U T I N E S ***************************



80:

!!!!! ******************************** Table/Texture Coordination

	add -gs_leg/2, -gs_leg/2, 0
	brick gs_leg,gs_leg,tableh-gs_top_thk
	roty 90

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body 1
	del 2

	return


500:

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!! ******************* CHAIR / CIRCLE ************************************

	if iChairType = CHAIRTYPE_ROUND then

		ADD chairw/2,chairw/2,0

		RESOL gs_resol

		MATERIAL gs_seat_mat
		ADDZ gs_seat_height-0.02
		CYLIND 0.02,chairw/2
		DEL 1


		H=0.45/40

		MATERIAL gs_frame_mat
		ADD chairw/2*COS(45),chairw/2*COS(45),0
		FOR K=1 TO 2
			FOR I=1 TO 2
				RULED 4,119,
					-H,-H,0,
					-H,H,0,
					H,H,0,
					H,-H,0,

					-H, -3*H-0.15/40, gs_seat_height-0.02,
					-H, -H-0.15/40, gs_seat_height-0.02,
					H,-H-0.15/40, gs_seat_height-0.02,
					H,-3*H-0.15/40, gs_seat_height-0.02

				GOSUB 2000

				ADDX -chairw*COS(45)
			NEXT I
			DEL 3
			MULY -1

			ADD chairw/2*COS(45),chairw/2*COS(45),0

		NEXT K
		DEL 3
		MATERIAL gs_back_mat
		ADD chairw/2*COS(45),chairw/2*COS(45)-2*H,gs_seat_height
			FOR I=1 TO 2
				PRISM_ 4,3*H,
						-H,-H,11,
						-H,H,11,
						H,H,11,
						H,-H,11
				ADDX -chairw*COS(45)
			NEXT I
		DEL 3


		ADD chairw/2*COS(45),chairw/2*COS(45)-2*H,gs_seat_height+3*H
			FOR I=1 TO 2
				RULED 4,111,
					-H,-H,0,
					-H,H,0,
					H,H,0,
					H,-H,0,

					-H,3*H,toth-3*H,
					-H,4*H,toth-3*H,
					H,4*H,toth-3*H,
					H,3*H,toth-3*H
				ADDX -chairw*COS(45)
		NEXT I

		DEL 3

		GOSUB 2000

		Beta=atn((toth-3*H)/(1.5*H))
		ADD -0,chairw/2*COS(45)+2*H,gs_seat_height+toth
		rotx Beta*2
		CUTPLANE 1,0,1,0
		DEL 2

		ADDZ gs_seat_height+toth-H
		ROTY 90

		RESOL INT(1.5 * gs_resol)
		REVOLVE 5,360,63,
				0,chairw/2-H/8+H,0,
				0,chairw/2+H,0,
				5*H,chairw/2+H,0,
				5*H,chairw/2-H/8+H,0,
				0,chairw/2-H/8+H,0
		DEL 2

		ADDZ gs_seat_height+toth-8*H
		ROTY 90
		xx=(8*H)/tan(Beta)

		REVOLVE 5,360,63,
				0,chairw/2-H/8+H-xx,0,
				0,chairw/2+H-xx,0,
				5*H,chairw/2+H-xx,0,
				5*H,chairw/2-H/8+H-xx,0,
				0,chairw/2-H/8+H-xx,0
		DEL 2

		CUTEND

		GOSUB 1000

		DEL 1

		hotspot chairw/2,chairw,0

	ELSE

		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		!!!!! ******************* CHAIR / RECTANGLE *********************************

		H=0.03				!!! Chair Leg Size
		gs_seat_thk=0.03	!!!Seat Thickness


		ADD chairw/2,chaird/2,0

		!!!!! ******************************** CHAIR/Simple

		IF gs_detlevel_3D_m = 1 THEN

				MATERIAL gs_seat_mat
					 ADDZ gs_seat_height-gs_seat_thk
						PRISM_ 5, gs_seat_thk,		! Seat prism
							-chairw/2,-chaird/2,15,
							-chairw/2,chaird/2,15,
							chairw/2,chaird/2,15,
							chairw/2,-chaird/2,15,
							-chairw/2,-chaird/2,-1
					   DEL 1

				GOSUB 1000

				MATERIAL gs_frame_mat


				ADD -chairw/2, -chaird/2,0
					BLOCK H,H, gs_seat_height-gs_seat_thk
				DEL 1

				ADD  chairw/2-h, -chaird/2, 0
					BLOCK H,H, gs_seat_height-gs_seat_thk
				DEL 1

				ADD chairw/2-h, chaird/2-h, 0
					BLOCK H, H, gs_seat_height-gs_seat_thk
				DEL 1

				ADD -chairw/2,  chaird/2-h, 0
					BLOCK H, H, gs_seat_height-gs_seat_thk
				DEL 1

				MATERIAL gs_back_mat
				ADD -chairw/2,  chaird/2-h, gs_seat_height
					BLOCK chairw, H, toth
				DEL 1

				GOSUB 1000

		ENDIF

		!!!!! ******************************** CHAIR/Detailed

		IF gs_detlevel_3D_m = 2 THEN

			MULY -1

				MATERIAL gs_seat_mat
				ADDZ gs_seat_height-gs_seat_thk

				PRISM_ 8, gs_seat_thk,		! Seat prism
					-chairw/2+H,-chaird/2,15,
					-chairw/2+H,-chaird/2+H,15,
					-chairw/2,-chaird/2+H,15,
					-chairw/2,chaird/2,15,
					chairw/2,chaird/2,15,
					chairw/2,-chaird/2+H,15,
					chairw/2-H,-chaird/2+H,15,
					chairw/2-H,-chaird/2,15

				GOSUB 2000


				DEL 1

				MATERIAL gs_frame_mat

				FOR I= 1 TO 2
					ADD -chairw/2,-chaird/2,0
						BLOCK H,H,gs_seat_height+toth
					DEL 1

					ADD -chairw/2,chaird/2-H,0
						BLOCK H,H,gs_seat_height-gs_seat_thk
					DEL 1

					MULX -1

				NEXT I
				GOSUB 1000

				FOR I= 1 TO 2
					ADD -chairw/2+H/2,-chaird/2+H,gs_seat_height-gs_seat_height/8-gs_seat_thk
						BLOCK H/2,chaird-2*H,gs_seat_height/8
					DEL 1

					ADD -chairw/2+H/2,-chaird/2+H,0.5*gs_seat_height
						BLOCK H/2,chaird-2*H,gs_seat_height/8
					DEL 1
					MULX -1

				NEXT I

				GOSUB 2000

				MATERIAL gs_back_mat


				 ADD -chairw/2+H,-chaird/2,gs_seat_height+toth-toth/6-toth/15
					 BLOCK chairw-2*H,H/2,toth/6
				 DEL 1


				 ADD -chairw/2+H,-chaird/2,gs_seat_height+toth-toth/6-toth/15-toth/4
					 BLOCK chairw-2*H,H/2,toth/6
				 DEL 1

				GOSUB 2000

			del 5
		ENDIF

		del 1

		hotspot chairw/2,chaird,0

	ENDIF

	return


1000:

	roty 90
	GOSUB 5000
	del 1

	return


2000:

	GOSUB 5000
	return


5000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body 1

	return
!!!!! ******************************** END of CHAIR


