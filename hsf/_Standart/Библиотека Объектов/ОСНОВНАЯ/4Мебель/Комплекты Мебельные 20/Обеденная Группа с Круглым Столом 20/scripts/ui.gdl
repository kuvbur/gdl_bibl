
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 					= - 1
_idxTab = 1
TABID_LayoutAndDimensions	= _idxTab  : _idxTab = _idxTab + 1
TABID_TableChair			= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions			= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Обеденного Круглого Стола`
ui_current_page gs_ui_current_page

ui_page TABID_LayoutAndDimensions, TABID_Root, `Компоновка и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "LayoutAndDimensionpage"

ui_page TABID_TableChair, TABID_Root, `Стол и Стул`, "uiTab_TableChair_flat(1)"
	gosub "TableChairpage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentation"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_TableChair_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"LayoutAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	_outfFlag = 0
	x1 = 0
	x2 = 232
	x3 = 390
	dy = 24

	_yCurr = pageStartY
	ui_infield{3} "ChairNum", x3, _yCurr, 54, _infHeight ui_tooltip `Количество Мест`

	_yCurr = pageStartY + _yOffset
	ui_style 0, 1
	ui_outfield `Размер`, x1, _yCurr, 444 - x2, _outfHeight
	ui_style 0, 0

	ui_outfield `Количество Мест`, x2, _yCurr, 444 - x2 - 56, _outfHeight

	_yCurr = _yCurr + dy - _yOffset
	
	ui_infield{3} "iSizingType", x1 + 21, _yCurr, 191, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", stSizingTypes[1], 1,
			"", stSizingTypes[2], 2 ui_tooltip `Размер`

	ui_infield{3} "bWheelchair", x2, _yCurr, 444 - x2, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `с Креслом-Коляской`, 0,
					"", `с Креслом-Коляской`, 1

	_yCurr = _yCurr + dy
	ui_separator x1, _yCurr, 444,_yCurr

	xPict = 129
	yPict = 76
	PictWidth = 188
	PictHeight = 189
	x4 = xPict - 90 - 3
	x5 = xPict + PictWidth + 3

	_yCurr = yPict + dy * 4
	ui_infield{3} "tablew", x4, _yCurr, 90, _infHeight ui_tooltip `Диаметр Стола`

	if iSizingType = 1 then
		_yCurr = _yCurr + dy * 2 - 12
		ui_infield{3} "ChairTableDist", x4, _yCurr, 90, _infHeight ui_tooltip `Расстояние Стул-Стол`
	
		_yCurr = yPict + 42
		ui_infield{3} "chairw", x5, _yCurr, 90, _infHeight ui_tooltip `Ширина Стула`
	
		_yCurr = yPict + 148
		ui_infield{3} "chaird", x5, _yCurr, 90, _infHeight ui_tooltip `Глубина Стула`
	
		_yCurr = yPict + 170
		ui_infield{3} "MSRear", x5, _yCurr, 90, _infHeight ui_tooltip `Расстояние за Стулом`
	
		if bWheelchair then
			ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRound_LayoutAndDimensions_DisByChair2.png
	
			_yCurr = yPict + 8
			ui_infield{3} "MSRearW", x4, _yCurr, 90, _infHeight ui_tooltip `Расстояние за Креслом-Коляской`
	
			_yCurr = yPict + 40
			ui_infield{3} "wheelchaird", x4, _yCurr, 90, _infHeight ui_tooltip `Глубина Стула`
	
			_yCurr = yPict - _infHeight - 3
			ui_infield{3} "wheelchairw", xPict + 22, _yCurr, 90, _infHeight ui_tooltip `Ширина Кресла-Коляски`
			ui_infield{3} "DistNextToWheelchair", xPict + 117, _yCurr, 90, _infHeight ui_tooltip `Расстояние возле Кресла-Коляски`
		else
			ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRound_LayoutAndDimensions_DisByChair1.png
		endif
	else
		_yCurr = yPict + dy * 6 + 12
		ui_infield{3} "DepPerChair", x4, _yCurr, 90, _infHeight ui_tooltip `Радиальное Пространство для Стульев`
		if bWheelchair then
			_yCurr = yPict + dy + 8
			ui_infield{3} "DepForWheelchair", x4, _yCurr, 90, _infHeight ui_tooltip `Радиальное Пространство для Кресла-Коляски`
	
			_yCurr = yPict - _infHeight - 3
			ui_infield{3} "WidForWheelchair", xPict + 50, _yCurr, 90, _infHeight ui_tooltip `Чистая Ширина для Кресла-Коляски`
	
			ui_pict 4, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRound_LayoutAndDimensions_TotalDis2.png
		else
			ui_pict 3, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRound_LayoutAndDimensions_TotalDis1.png
		endif
	endif

return

! ------------------------------------------------------------------------------
"TableChairpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	_outfFlag = 0
	x1 = 0
	x2 = 232
	dy = 24

	_yCurr = pageStartY	+ _yOffset
	ui_style 0, 1
	ui_outfield `Стол`, x1, _yCurr, 444 - x2, _outfHeight
	ui_outfield `Стул`, x2, _yCurr, 444 - x2, _outfHeight
	ui_style 0, 0

	ui_separator x2 - 10, 0, x2 - 10, 265

	_yCurr = _yCurr	+ dy
	ui_outfield `Тип`, x2, _yCurr, 444 - x2 - 92, _outfHeight

	_yCurr = _yCurr	- _yOffset
	ui_infield{3}	"iChairType", 444 - 90, _yCurr, 90, _infHeight,
			8, "", 3, 1,
			0, 0, 0, 0,
			1, _strChairType[1], CHAIRTYPE_RECTANGLE,
			2, _strChairType[2], CHAIRTYPE_ROUND ui_tooltip `Тип Стула`

	xPict = 7
	yPict = 118
	PictWidth = 113
	PictHeight = 106

	ui_pict 5, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRound_TableChair1.png

	_yCurr = yPict - _infHeight - 3
	ui_infield{3} "tableh", x1, _yCurr, 90, _infHeight ui_tooltip `Высота Стола`

	_yCurr = yPict - 4
	ui_infield{3} "gs_top_thk", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Толщина Столешницы`

	_yCurr = yPict + PictHeight + 3
	ui_infield{3} "gs_leg", xPict + 48, _yCurr, 90, _infHeight ui_tooltip `Толщина Ножек`

	xPict = 253
	yPict = 94
	PictWidth = 77
	PictHeight = 120

	ui_pict 6, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRound_TableChair2.png

	_yCurr = yPict - _infHeight - 3
	ui_infield{3} "chairh", xPict - 8, _yCurr, 90, _infHeight ui_tooltip `Высота Стула`

	_yCurr = yPict + 80
	ui_infield{3} "gs_seat_height", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Высота Сидения`

return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 14,
													gs_SymbolType_m		= gs_SymbolType_m,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													gs_2DSeat			= 0,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentation":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1   
	surfacesArray[SurfItemID]	= "bUniformSurfaces"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_top_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_leg_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_frame_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_back_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_seat_mat"
	SurfItemID = SurfItemID + 1

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k

	call "ui_object_3DRepresentation" parameters 	surfacesArray 		= surfacesArray,
													childFlagArray 		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													iSurfaceListID		= TABID_3DRepresentation


return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
