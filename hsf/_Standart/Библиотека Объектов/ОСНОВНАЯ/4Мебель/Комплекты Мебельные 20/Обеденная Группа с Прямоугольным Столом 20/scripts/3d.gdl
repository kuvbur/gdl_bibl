
IF gs_detlevel_3D_m = 0 THEN end
IF not(gs_shadow) THEN SHADOW OFF
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

pen gs_cont_pen

! --- Table -------------------------------------------------------------------

! --- Make Length stretchable --------------------------------------------------
unID = 1
hotspot 0, tablehalfd, tableh, unID, tablehalfw, 1+128, tablew :unID=unID+1
hotspot -1, tablehalfd, tableh, unID, tablehalfw, 3, tablew :unID=unID+1
hotspot tablehalfw, tablehalfd, tableh, unID, tablehalfw, 2, tablew :unID=unID+1

hotspot 0, tablehalfd, tableh, unID, tablehalfw, 1+128, tablew :unID=unID+1
hotspot 1, tablehalfd, tableh, unID, tablehalfw, 3, tablew :unID=unID+1
hotspot -tablehalfw, tablehalfd, tableh, unID, tablehalfw, 2, tablew :unID=unID+1

hotspot 0, -tablehalfd, tableh, unID, tablehalfw, 1+128, tablew :unID=unID+1
hotspot -1, -tablehalfd, tableh, unID, tablehalfw, 3, tablew :unID=unID+1
hotspot tablehalfw, tablehalfd, tableh, unID, tablehalfw, 2, tablew :unID=unID+1

hotspot 0, -tablehalfd, tableh, unID, tablehalfw, 1+128, tablew :unID=unID+1
hotspot 1, -tablehalfd, tableh, unID, tablehalfw, 3, tablew :unID=unID+1
hotspot -tablehalfw, -tablehalfd, tableh, unID, tablehalfw, 2, tablew :unID=unID+1

! --- Make Width stretchable ---------------------------------------------------
IF iLeftSideType = SIDETYPE_WHEELCHAIR AND iSizingType = SIZING_TOTAL_SPACE THEN

ELSE
	hotspot tablehalfw, 0, tableh, unID, tablehalfd, 1+128, tabled :unID=unID+1
	hotspot tablehalfw, -1, tableh, unID, tablehalfd, 3, tabled :unID=unID+1
	hotspot tablehalfw, tablehalfd, tableh, unID, tablehalfd, 2, tabled :unID=unID+1

	hotspot tablehalfw, 0, tableh, unID, tablehalfd, 1+128, tabled :unID=unID+1
	hotspot tablehalfw, 1, tableh, unID, tablehalfd, 3, tabled :unID=unID+1
	hotspot tablehalfw, -tablehalfd, tableh, unID, tablehalfd, 2, tabled :unID=unID+1

	hotspot -tablehalfw, 0, tableh, unID, tablehalfd, 1+128, tabled :unID=unID+1
	hotspot -tablehalfw, -1, tableh, unID, tablehalfd, 3, tabled :unID=unID+1
	hotspot -tablehalfw, tablehalfd, tableh, unID, tablehalfd, 2, tabled :unID=unID+1

	hotspot -tablehalfw, 0, tableh, unID, tablehalfd, 1+128, tabled :unID=unID+1
	hotspot -tablehalfw, 1, tableh, unID, tablehalfd, 3, tabled :unID=unID+1
	hotspot -tablehalfw, -tablehalfd, tableh, unID, tablehalfd, 2, tabled :unID=unID+1
ENDIF

! --- Middle hotspot stretchable------------------------------------------------
hotspot 0, 0, 0, 		unID, tableh, 1: unID = unID + 1
hotspot 0, 0, -1, 		unID, tableh, 3: unID = unID + 1
hotspot 0, 0, tableh,	unID, tableh, 2: unID = unID + 1

add -tablew/2, -tabled/2, 0

! --- Tabletop ----------------------------------------------------------------

material gs_top_mat
addz tableh-gs_top_thk
prism_ 4,gs_top_thk,
	0,0,15,
	tablew,0,15,
	tablew,tabled,15,
	0,tabled,15
del 1

IF tablew>tabled THEN GOSUB 2000
IF tablew<tabled OR ABS(tablew-tabled)<EPS THEN GOSUB 1000

! --- Leg ---------------------------------------------------------------------

material gs_leg_mat
addy tboy
addx tbo
gosub 80
del 1
addx tablew-gs_leg-tbo
gosub 80
addy tabled-gs_leg-tboy*2
gosub 80
addx -(tablew-gs_leg-tbo*2)
gosub 80
del 4

! --- Frame -------------------------------------------------------------------

material gs_frame_mat

addz tableh-gs_top_thk-.05
add 0,tbo+gs_leg/4,0
for i=1 to 2
	prism_ 5,.05,
	tbo+gs_leg,0,15,
	tablew-tbo-gs_leg,0,15,
	tablew-tbo-gs_leg,gs_leg/2,15,
	tbo+gs_leg,gs_leg/2,15,
	tbo+gs_leg,0,-1
	addy tabled-2*tbo-gs_leg
next i
del 3

for ii=1 to 2
	prism_ 5,.05,
	tbo+gs_leg/4,tbo+gs_leg,15,
	tbo+gs_leg*3/4,tbo+gs_leg,15,
	tbo+gs_leg*3/4,tabled-tbo-gs_leg,15,
	tbo+gs_leg/4,tabled-tbo-gs_leg,15,
	tbo+gs_leg/4,tbo+gs_leg,-1
	addx tablew-2*tbo-gs_leg
next ii
del 2

del top


! =============================================================================
! Chair Layout
! =============================================================================

IF iBottomSideType = SIDETYPE_WHEELCHAIR AND iSizingType = SIZING_CHAIR_DIST THEN WidForWheelchair = DistNextToWheelchair*2 + wheelchairw

chairHotspotUnId = 100
! --- Top ---------------------------------------------------------------------

If TopSide=1 THEN
	IF iBottomSideType < 2 THEN
		dist = RealDistH/2
	ELSE
		dist = (WidForWheelchair-chairw)/2
	ENDIF

	add -tablew/2+dist+addtablew*(iLeftSideType = SIDETYPE_WHEELCHAIR), tabled/2+ChairTableDist, 0
	For kk=1 to ChairNumHor
		GOSUB 500
		hotspot chairw/2, -ChairTableDist, 0, chairHotspotUnId ,  "ChairTableDist", 1+128		: chairHotspotUnId =chairHotspotUnId +1
		hotspot chairw/2, -1, 0, chairHotspotUnId, "ChairTableDist", 3						: chairHotspotUnId =chairHotspotUnId +1
		hotspot chairw/2, 0, 0, chairHotspotUnId, "ChairTableDist", 2							: chairHotspotUnId =chairHotspotUnId +1
		IF iBottomSideType = 2 AND kk = 1 THEN
			add chairw+dist+RealDistH/2, 0, 0
		ELSE
			add chairw+RealDistH, 0,0
		ENDIF
	NEXT kk
	DEL ChairNumHor
	DEL 1

ENDIF

! --- Bottom --------------------------------------------------------------
if bChairBottom then
	add -tablew/2+RealDistH/2+WidForWheelchair*(iBottomSideType = SIDETYPE_WHEELCHAIR)+addtablew*(iLeftSideType = SIDETYPE_WHEELCHAIR), -tabled/2-ChairTableDist, 0
	ChairNumHor = ChairNumHor - (iBottomSideType = 2)
	for kk=1 to ChairNumHor
		mul 1,-1,1
		gosub 500
		del 1
		hotspot chairw/2, ChairTableDist, 0, chairHotspotUnId ,  "ChairTableDist", 1+128	: chairHotspotUnId =chairHotspotUnId +1
		hotspot chairw/2, 1, 0, chairHotspotUnId, "ChairTableDist", 3						: chairHotspotUnId =chairHotspotUnId +1
		hotspot chairw/2, 0, 0, chairHotspotUnId, "ChairTableDist", 2						: chairHotspotUnId =chairHotspotUnId +1
		add chairw+RealDistH,0,0
	next kk
	del ChairNumHor
	del 1
endif


! --- Left ----------------------------------------------------------------
if bChairLeft then
	if iLeftSideType = SIDETYPE_CHAIR then
		add -tablew/2-ChairTableDist, -tabled/2+RealDistV/2, 0
		for kk=1 to ChairNumVert
			rotz 90
			gosub 500
			del 1
			hotspot 0, chairw/2+ChairTableDist, 0, chairHotspotUnId ,  "ChairTableDist", 1+128	: chairHotspotUnId =chairHotspotUnId +1
			hotspot 0, chairw/2+1, 0, chairHotspotUnId, "ChairTableDist", 3						: chairHotspotUnId =chairHotspotUnId +1
			hotspot 0, chairw/2, 0, chairHotspotUnId, "ChairTableDist", 2						: chairHotspotUnId =chairHotspotUnId +1
			add 0,chairw+RealDistV,0
		next kk
		del ChairNumVert
		del 1
	endif
endif

! --- Right ---------------------------------------------------------------
If RightSide=1 THEN

	add tablew/2+ChairTableDist, tabled/2-RealDistV/2, 0
	For kk=1 to ChairNumVert
		ROTz -90
		GOSUB 500
		DEL 1
		hotspot 0, -chairw/2-ChairTableDist, 0, chairHotspotUnId ,  "ChairTableDist", 1+128		: chairHotspotUnId =chairHotspotUnId +1
		hotspot 0, -chairw/2-1, 0, chairHotspotUnId, "ChairTableDist", 3						: chairHotspotUnId =chairHotspotUnId +1
		hotspot 0, -chairw/2, 0, chairHotspotUnId, "ChairTableDist", 2							: chairHotspotUnId =chairHotspotUnId +1
		add 0,-chairw-RealDistV,0
	NEXT kk
	DEL ChairNumVert
	DEL 1

ENDIF

END


! =============================================================================
! S U B R O U T I N E S
! =============================================================================

80:

	! --- Table/Texture Coordination ----------------------------------------------
	brick gs_leg,gs_leg,tableh-gs_top_thk
	roty 90

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body 1
	del 1

	return



500:
	! --- CHAIR / CIRCLE ----------------------------------------------------------

	if iChairType = CHAIRTYPE_ROUND then

		ADD chairw/2,chairw/2,0

		RESOL gs_resol

		MATERIAL gs_seat_mat
		ADDZ gs_seat_height-0.02
		CYLIND 0.02,chairw/2
		DEL 1

		H=0.45/40

		MATERIAL gs_frame_mat
		ADD chairw/2*COS(45),chairw/2*COS(45),0
		FOR K=1 TO 2
			FOR I=1 TO 2
				RULED 4,119,
						-H,-H,0,
						-H,H,0,
						H,H,0,
						H,-H,0,

						-H, -3*H-0.15/40, gs_seat_height-0.02,
						-H, -H-0.15/40, gs_seat_height-0.02,
						H,-H-0.15/40, gs_seat_height-0.02,
						H,-3*H-0.15/40, gs_seat_height-0.02

				gosub 2000

				ADDX -chairw*COS(45)
			NEXT I
			DEL 3
			MULY -1

			ADD chairw/2*COS(45),chairw/2*COS(45),0

		NEXT K
		DEL 3
		MATERIAL gs_back_mat
		ADD chairw/2*COS(45),chairw/2*COS(45)-2*H,gs_seat_height
		FOR I=1 TO 2
			PRISM_ 4,3*H,
					-H,-H,11,
					-H,H,11,
					H,H,11,
					H,-H,11
			ADDX -chairw*COS(45)
		NEXT I
		DEL 3


		ADD chairw/2*COS(45),chairw/2*COS(45)-2*H,gs_seat_height+3*H
		FOR I=1 TO 2
			RULED 4,111,
					-H,-H,0,
					-H,H,0,
					H,H,0,
					H,-H,0,

					-H,3*H,toth-3*H,
					-H,4*H,toth-3*H,
					H,4*H,toth-3*H,
					H,3*H,toth-3*H
			ADDX -chairw*COS(45)
		NEXT I

		DEL 3

		gosub 2000

		Beta=atn((toth-3*H)/(1.5*H))
		ADD -0,chairw/2*COS(45)+2*H,gs_seat_height+toth
		rotx Beta*2
		CUTPLANE 1,0,1,0
		DEL 2

		ADDZ gs_seat_height+toth-H
		ROTY 90

		RESOL INT(1.5 * gs_resol)
		REVOLVE 5,360,63,
				0,chairw/2-H/8+H,0,
				0,chairw/2+H,0,
				5*H,chairw/2+H,0,
				5*H,chairw/2-H/8+H,0,
				0,chairw/2-H/8+H,0
		DEL 2

		ADDZ gs_seat_height+toth-8*H
		ROTY 90
		xx=(8*H)/tan(Beta)

		REVOLVE 5,360,63,
				0,chairw/2-H/8+H-xx,0,
				0,chairw/2+H-xx,0,
				5*H,chairw/2+H-xx,0,
				5*H,chairw/2-H/8+H-xx,0,
				0,chairw/2-H/8+H-xx,0
		DEL 2

		CUTEND

		gosub 1000

		DEL 1

		Hotspot chairw/2,chairw,0

	ELSE

		! --- CHAIR / RECTANGLE -------------------------------------------------------

		H=0.03				!!! Chair Leg Size
		gs_seat_thk=0.03	!!!Seat Thickness


		ADD chairw/2,chaird/2,0

		! --- CHAIR_RECTANGLE/Simple ------------------------------------------

		IF gs_detlevel_3D_m = 1 THEN

			MATERIAL gs_seat_mat
			ADDZ gs_seat_height-gs_seat_thk
			PRISM_ 5, gs_seat_thk,		! Seat prism
					-chairw/2,-chaird/2,15,
					-chairw/2,chaird/2,15,
					chairw/2,chaird/2,15,
					chairw/2,-chaird/2,15,
					-chairw/2,-chaird/2,-1
			DEL 1

			gosub 1000

			MATERIAL gs_frame_mat

			ADD -chairw/2, -chaird/2,0
			BLOCK H,H, gs_seat_height-gs_seat_thk
			DEL 1

			ADD  chairw/2-h, -chaird/2, 0
			BLOCK H,H, gs_seat_height-gs_seat_thk
			DEL 1

			ADD chairw/2-h, chaird/2-h, 0
			BLOCK H, H, gs_seat_height-gs_seat_thk
			DEL 1

			ADD -chairw/2,  chaird/2-h, 0
			BLOCK H, H, gs_seat_height-gs_seat_thk
			DEL 1

			MATERIAL gs_back_mat
			ADD -chairw/2,  chaird/2-h, gs_seat_height
			BLOCK chairw, H, toth
			DEL 1

			gosub 1000

		ENDIF

		! --- CHAIR_RECTANGLE/Detailed ----------------------------------------

		IF gs_detlevel_3D_m = 2 THEN

			MULY -1

			MATERIAL gs_seat_mat
			ADDZ gs_seat_height-gs_seat_thk

			PRISM_ 8, gs_seat_thk,		! Seat prism
					-chairw/2+H,-chaird/2,15,
					-chairw/2+H,-chaird/2+H,15,
					-chairw/2,-chaird/2+H,15,
					-chairw/2,chaird/2,15,
					chairw/2,chaird/2,15,
					chairw/2,-chaird/2+H,15,
					chairw/2-H,-chaird/2+H,15,
					chairw/2-H,-chaird/2,15

			GOSUB 2000

			DEL 1

			MATERIAL gs_frame_mat

			FOR I= 1 TO 2
				ADD -chairw/2,-chaird/2,0
				BLOCK H,H,gs_seat_height+toth
				DEL 1

				ADD -chairw/2,chaird/2-H,0
				BLOCK H,H,gs_seat_height-gs_seat_thk
				DEL 1

				MULX -1

			NEXT I

			gosub 1000

			FOR I= 1 TO 2
				ADD -chairw/2+H/2,-chaird/2+H,gs_seat_height-gs_seat_height/8-gs_seat_thk
				BLOCK H/2,chaird-2*H,gs_seat_height/8
				DEL 1

				ADD -chairw/2+H/2,-chaird/2+H,0.5*gs_seat_height
				BLOCK H/2,chaird-2*H,gs_seat_height/8
				DEL 1
				MULX -1

			NEXT I

			gosub 2000

			MATERIAL gs_back_mat


			ADD -chairw/2+H,-chaird/2,gs_seat_height+toth-toth/6-toth/15
			BLOCK chairw-2*H,H/2,toth/6
			DEL 1


			ADD -chairw/2+H,-chaird/2,gs_seat_height+toth-toth/6-toth/15-toth/4
			BLOCK chairw-2*H,H/2,toth/6
			DEL 1

			gosub 2000

			del 5
		ENDIF

		del 1

	ENDIF

	return



1000:
	roty 90
	gosub 5000
	del 1
	return

2000:
	gosub 5000
	return


5000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body 1

	return
! ==============================================================================
! END of CHAIR
! ==============================================================================
