
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 					= - 1
_idxTab = 1
TABID_Layout				= _idxTab  : _idxTab = _idxTab + 1
TABID_Dimensions			= _idxTab  : _idxTab = _idxTab + 1
TABID_TableChair			= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions			= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Обеденного Прямоугольного Стопа`
ui_current_page gs_ui_current_page

ui_page TABID_Layout, TABID_Root, `Размещение`, "uiTab_Style_flat(1)"
	gosub "Layoutpage"

ui_page TABID_Dimensions, TABID_Root, `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "Dimensionspage"

ui_page TABID_TableChair, TABID_Root, `Стол и Стул`, "uiTab_TableChair_flat(1)"
	gosub "TableChairpage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentation"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_TableChair_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"Layoutpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	xPictTable = 180
	yPictTable = pageStartY + 85
	PictWidthTable = 95 
	PictHeightTable = 55
	PictWidthChair = 27
	PictDepthChair = 29
	dy = 22
	x1 = xPictTable - 13
	x2 = 0
	x3 = xPictTable + PictWidthTable + 3 + PictDepthChair + 3

	ui_pict	1, xPictTable , yPictTable , PictWidthTable , PictHeightTable , 1

	_yCurr = yPictTable - 58
	ui_infield{3} "TopSide", x1, _yCurr, 444 - x1, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Стулья Сверху`, 0,
					"", `Стулья Сверху`, 1

	if TopSide then
		ui_pict	2,	xPictTable + 12,									yPictTable - 3 - PictDepthChair,	PictWidthChair,	PictDepthChair
		ui_pict	2,	xPictTable + PictWidthTable - PictWidthChair - 12,	yPictTable - 3 - PictDepthChair,	PictWidthChair,	PictDepthChair
	endif

	_yCurr = yPictTable + 18
	ui_infield{3} "RightSide", x3, _yCurr, 444 - x3, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Стул Справа`, 0,
					"", `Стул Справа`, 1
	if RightSide then
		ui_pict 5, xPictTable + PictWidthTable + 3, yPictTable + 14, PictDepthChair, PictWidthChair
	endif

	ui_infield{3} "bChairLeft", x2, _yCurr, 444 - x3, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Стул Слева`, 0,
					"", `Стул Слева`, 1

	if bChairLeft then
		if iLeftSideType = SIDETYPE_CHAIR 		then ui_pict	4,	xPictTable -3-PictDepthChair,	yPictTable +14,	PictDepthChair,	PictWidthChair
		if iLeftSideType = SIDETYPE_WHEELCHAIR 	then ui_pict	7,	xPictTable -42,					yPictTable +10,	42,				35, 		1

		_yCurr = _yCurr + dy
		ui_infield{3}	"iLeftSideType", x2 + 15, _yCurr, 112, _infHeight,
				8, "", 3, 1,
				0, 0, 0, 0,
				1, stSideTypes[1], SIDETYPE_CHAIR,
				2, stSideTypes[2], SIDETYPE_WHEELCHAIR
	endif

	_yCurr = yPictTable + PictHeightTable + 3 + 42 + 3
	ui_infield{3} "bChairBottom", x1, _yCurr, 444 - x3, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Стулья Внизу`, 0,
					"", `Стулья Внизу`, 1

	if bChairBottom then
		ui_pict	3,	xPictTable + PictWidthTable - PictWidthChair - 12, yPictTable + PictHeightTable + 3,	PictWidthChair,	PictDepthChair

		if iBottomSideType = SIDETYPE_CHAIR 		then ui_pict	3,	xPictTable + 12,	yPictTable + PictHeightTable + 3,	PictWidthChair,	PictDepthChair
		if iBottomSideType = SIDETYPE_WHEELCHAIR 	then ui_pict	6,	xPictTable + 8,		yPictTable + PictHeightTable ,		35,				42, 		1

		_yCurr = _yCurr + dy
		ui_infield{3}	"iBottomSideType", x1 + 15, _yCurr, 112, _infHeight,
				8, "", 3, 1,
				0, 0, 0, 0,
				1, stSideTypes[1], SIDETYPE_CHAIR,
				2, stSideTypes[2], SIDETYPE_WHEELCHAIR
	endif
return

! ------------------------------------------------------------------------------
"Dimensionspage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	xPict = 126
	yPict = 80
	PictWidth = 222 
	PictHeight = 164
	_outfFlag = 0
	x1 = 0
	x2 = 230
	x3 = 390
	dy = 22

	_yCurr = pageStartY	
	if iSizingType = 2 then
		ui_infield{3} "NumChairsX", x3, _yCurr, 54, _infHeight ui_tooltip `Количество Стульев по X`
			_yCurr = _yCurr + _yOffset

		ui_outfield `Количество Стульев 1`, x2, _yCurr, 444 - x2 - 56, _outfHeight
		_yCurr = _yCurr - _yOffset

	endif
		_yCurr = _yCurr + _yOffset

	ui_style 0, 1
	ui_outfield `Размер`, x1, _yCurr, x2 - 2, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "iSizingType", x1 + 21, _yCurr, 191, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", stSizingTypes[1], 1,
			"", stSizingTypes[2], 2 ui_tooltip `Размер`

	if iSizingType = 2 then
		ui_infield{3} "NumChairsY", x3, _yCurr, 54, _infHeight ui_tooltip `Количество Стульев по Y`

		_yCurr = _yCurr + _yOffset
		ui_outfield `Количество Стульев 2`, x2, _yCurr, 444 - x2 - 56, _outfHeight

		_yCurr = _yCurr - _yOffset
	endif

	_yCurr = _yCurr + dy
	ui_separator x1, _yCurr, 444,_yCurr

	dimensionForm = ""
	 qq= REQUEST ("Working_length_unit", "", dimensionForm)

	if (iLeftSideType = 1 and iBottomSideType = 1) or not(bChairLeft) or not(bChairBottom)then
		xPict = 96 + 39
		yPict = 70 + 10
		PictWidth = 173
		PictHeight = 153
	endif

	if iLeftSideType = 2 and iBottomSideType = 1 and bChairLeft and bChairBottom then
		xPict = 96 + 11
		yPict = 70 + 6
		PictWidth = 229
		PictHeight = 161
	endif

	if iBottomSideType = 2  and bChairLeft and bChairBottom then
		xPict = 96
		yPict = 70
		PictWidth = 251
		PictHeight = 173
	endif

	x4 = xPict - 90 - 3
	x5 = xPict + PictWidth + 3

	if (iLeftSideType = 1 and iBottomSideType = 1) or not(bChairLeft) or not(bChairBottom)then
		ui_infield{3} "ChairTableDist", x5, yPict + dy * 4, 		90, _infHeight ui_tooltip `Расстояние Стул-Стол`
		ui_infield{3} "chaird", 		x5, yPict + dy * 5, 		90, _infHeight ui_tooltip `Глубина Стула`
		ui_infield{3} "chairw", xPict + 80, yPict + PictHeight + 3, 90, _infHeight ui_tooltip `Ширина Стула`
		if iSizingType = 2 then
			ui_pict 8, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_DisByChair1.png

			ui_infield{3} "DepPerChair", x5, 			yPict + dy, 			90, _infHeight ui_tooltip `Глубина на Стул`
			ui_infield{3} "WidPerChair", xPict + 80, 	yPict - _infHeight - 3, 90, _infHeight ui_tooltip `Ширина на Стул`

			ui_outfield str(dimensionForm, tabled), x5, yPict + dy * 3, 90, _outfHeight ui_tooltip `Ширина Стола`
		else
			ui_pict 12, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_TotalDis1.png

			ui_infield{3} "tabled", x5, 		yPict + dy * 3, 		90, _infHeight ui_tooltip `Ширина Стола`
			ui_infield{3} "MSRear", x5, 		yPict + dy * 6, 		90, _infHeight ui_tooltip `Расстояние за Стулом`
			ui_infield{3} "tablew", xPict + 45, yPict - _infHeight - 3, 90, _infHeight ui_tooltip `Длина Стола`
		endif
	endif

	if iLeftSideType = 2 and iBottomSideType = 1 and bChairLeft and bChairBottom then
		ui_infield{3} "wheelchairw", 		x4, 			yPict + dy * 4 - 10, 	90, _infHeight ui_tooltip `Ширина Кресла-Коляски`
		ui_infield{3} "ChairTableDist", 	x5, 			yPict + dy * 5 - 10, 	90, _infHeight ui_tooltip `Расстояние Стул-Стол`
		ui_infield{3} "chaird", 			x5, 			yPict + dy * 6 - 10, 	90, _infHeight ui_tooltip `Глубина Стула`
		ui_infield{3} "wheelchaird", 		xPict, 			yPict + PictHeight + 3, 90, _infHeight ui_tooltip `Глубина Кресла-Коляски`
		ui_infield{3} "AddSideSpaceSize", 	xPict + 93, 	yPict + PictHeight + 3, 90, _infHeight ui_tooltip `Дополнительное Пространство Сбоку от Кресла-Коляски`
		ui_infield{3} "chairw", 			xPict + 145, 	yPict - _infHeight - 3, 90, _infHeight ui_tooltip `Ширина Стула`
		if iSizingType = 2 then
			ui_pict 9, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_DisByChair2.png

			ui_infield{3} "WidPerChair", 		x5, yPict, 			90, _infHeight ui_tooltip `Ширина на Стул`
			ui_infield{3} "DepPerChair", 		x5, yPict + 30, 	90, _infHeight ui_tooltip `Глубина на Стул`
			ui_infield{3} "WidForWheelchair", 	x4, yPict + dy * 5, 90, _infHeight ui_tooltip `Ширина для Кресла-Коляски`
			ui_infield{3} "DepForWheelchair", 	x4, yPict + dy * 6, 90, _infHeight ui_tooltip `Глубина для Кресла-Коляски`

			ui_outfield str(dimensionForm, tabled), x5, yPict + 80, 90, _outfHeight ui_tooltip `Ширина Стола`
		else
			ui_pict 13, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_TotalDis2.png

			ui_infield{3} "DistNextToWheelchair", 	x4, yPict + dy * 3 - 10, 	90, _infHeight ui_tooltip `Расстояние возле Кресла-Коляски`
			ui_infield{3} "MSRearW", 				x4, yPict + dy * 7 - 5, 	90, _infHeight ui_tooltip `Расстояние за Креслом-Коляской`
			ui_infield{3} "tablew", 				x5, yPict + 2, 				90, _infHeight ui_tooltip `Ширина Стола`
			ui_infield{3} "MSRear", 				x5, yPict + dy * 7 - 10, 	90, _infHeight ui_tooltip `Расстояние за Стулом`
			ui_infield{3} "tabled", 				x5, yPict + dy * 4 - 10, 	90, _infHeight ui_tooltip `Ширина Стола`
			ui_infield{3} "MinDist",  xPict + 145 - 92, yPict - _infHeight - 3, 90, _infHeight ui_tooltip `Минимальное Расстояние Стул-Стул`
		endif
	endif

	if iLeftSideType = 1 and iBottomSideType = 2  and bChairLeft and bChairBottom then
		ui_infield{3} "wheelchaird", 	x4, yPict + dy * 6 - 10, 90, _infHeight ui_tooltip `Глубина Кресла-Коляски`
		ui_infield{3} "chaird", 		x5, yPict + dy * 6 - 10, 90, _infHeight ui_tooltip `Глубина Стула`
		ui_infield{3} "ChairTableDist", x5, yPict + dy * 5 - 10, 90, _infHeight ui_tooltip `Расстояние Стул-Стол`
		if iSizingType = 2 then
			ui_pict 10, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_DisByChair3.png

			ui_infield{3} "wheelchairw", 		x4, 			yPict + dy * 7 - 10, 	90, _infHeight ui_tooltip `Ширина Кресла-Коляски`
			ui_infield{3} "DepForWheelchair", 	x4, 			yPict + dy * 8 - 10, 	90, _infHeight ui_tooltip `Глубина для Кресла-Коляски`
			ui_infield{3} "WidPerChair", 		x5, 			yPict, 					90, _infHeight ui_tooltip `Ширина на Стул`
			ui_infield{3} "DepPerChair",		x5, 			yPict + 30, 			90, _infHeight ui_tooltip `Глубина на Стул`
			ui_infield{3} "AddSideSpaceSize", 	xPict, 			yPict + PictHeight + 3, 90, _infHeight ui_tooltip `Дополнительное Пространство Сбоку от Кресла-Коляски`
			ui_infield{3} "WidForWheelchair", 	xPict + 93, 	yPict + PictHeight + 3, 90, _infHeight ui_tooltip `Ширина для Кресла-Коляски`
			ui_infield{3} "chairw", 			xPict + 145, 	yPict - _infHeight - 3, 90, _infHeight ui_tooltip `Ширина Стула`

			ui_outfield str(dimensionForm, tabled), x5, yPict + 80, 90, _outfHeight ui_tooltip `Ширина Стола`
		else
			ui_pict 14, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_TotalDis3

			ui_infield{3} "MSRearW", 				  x4, yPict + dy * 7 - 10, 		90, _infHeight ui_tooltip `Расстояние за Креслом-Коляской`
			ui_infield{3} "AddSideSpaceSize", 		  x4, yPict + dy * 8 - 10, 		90, _infHeight ui_tooltip `Дополнительное Пространство Сбоку от Кресла-Коляски`
			ui_infield{3} "tablew", 			 	  x5, yPict + 2, 				90, _infHeight ui_tooltip `Ширина Стола`
			ui_infield{3} "tabled", 			 	  x5, yPict + dy * 4 - 10, 		90, _infHeight ui_tooltip `Глубина Стола`
			ui_infield{3} "MSRear", 			 	  x5, yPict + dy * 7 - 10, 		90, _infHeight ui_tooltip `Расстояние за Стулом`
			ui_infield{3} "MinDist", 		  xPict + 78, yPict - _infHeight - 3, 	90, _infHeight ui_tooltip `Минимальное Расстояние Стул-Стул`
			ui_infield{3} "chairw", 	 xPict + 78 + 92, yPict - _infHeight - 3, 	90, _infHeight ui_tooltip `Ширина Стула`
			ui_infield{3} "DistNextToWheelchair",  xPict, yPict + PictHeight + 3, 	90, _infHeight ui_tooltip `Расстояние возле Кресла-Коляски`
			ui_infield{3} "wheelchairw", 	  xPict + 92, yPict + PictHeight + 3, 	90, _infHeight ui_tooltip `Ширина Кресла-Коляски`
		endif
	endif

	if iLeftSideType = 2 and iBottomSideType = 2  and bChairLeft and bChairBottom then
		ui_infield{3} "wheelchairw", 	x4, yPict + dy * 4 - 10, 90, _infHeight ui_tooltip `Ширина Кресла-Коляски`
		ui_infield{3} "wheelchaird", 	x4, yPict + dy * 6 - 10, 90, _infHeight ui_tooltip `Глубина Кресла-Коляски`
		ui_infield{3} "ChairTableDist", x5, yPict + dy * 5 - 10, 90, _infHeight ui_tooltip `Расстояние Стул-Стол`
		ui_infield{3} "chaird", 		x5, yPict + dy * 6 - 10, 90, _infHeight ui_tooltip `Глубина Стула`
		if iSizingType = 2 then
			ui_pict 11, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_DisByChair4.png

			ui_infield{3} "DepForWheelchair", 			x4, yPict, 					90, _infHeight ui_tooltip `Глубина для Кресла-Коляски`
			ui_infield{3} "WidForWheelchair", 			x4, yPict + dy, 			90, _infHeight ui_tooltip `Ширина для Кресла-Коляски`
			ui_infield{3} "wheelchaird", 				x4, yPict + dy * 6 - 10, 	90, _infHeight ui_tooltip `Глубина Кресла-Коляски`
			ui_infield{3} "WidPerChair", 				x5, yPict, 					90, _infHeight ui_tooltip `Ширина на Стул`
			ui_infield{3} "DepPerChair", 				x5, yPict + 30, 			90, _infHeight ui_tooltip `Глубина на Стул`
			ui_infield{3} "AddSideSpaceSize", 		 xPict, yPict + PictHeight + 3, 90, _infHeight ui_tooltip `Дополнительное Пространство Сбоку от Кресла-Коляски`
			ui_infield{3} "WidForWheelchair",  xPict  + 93, yPict + PictHeight + 3, 90, _infHeight ui_tooltip `Ширина для Кресла-Коляски`
			ui_infield{3} "chairw", 		   xPict + 145, yPict - _infHeight - 3, 90, _infHeight ui_tooltip `Ширина Стула`

			ui_outfield str(dimensionForm, tabled), x5, yPict + 80, 90, _outfHeight ui_tooltip `Ширина Стола`
		else
			ui_pict 15, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_Dimensions_TotalDis4.png

			ui_infield{3} "DistNextToWheelchair", 			   x4, yPict + dy * 3 - 10, 	90, _infHeight ui_tooltip `Расстояние возле Кресла-Коляски`
			ui_infield{3} "MSRearW", 						   x4, yPict + dy * 7 - 10, 	90, _infHeight ui_tooltip `Расстояние за Креслом-Коляской`
			ui_infield{3} "tablew", 						   x5, yPict + 2, 				90, _infHeight ui_tooltip `Ширина Стола`
			ui_infield{3} "tabled", 						   x5, yPict + dy * 4 - 10,		90, _infHeight ui_tooltip `Глубина Стола`
			ui_infield{3} "MSRear", 						   x5, yPict + dy * 7 - 10, 	90, _infHeight ui_tooltip `Расстояние за Стулом`
			ui_infield{3} "chairw", 				   xPict + 78, yPict - _infHeight - 3, 	90, _infHeight ui_tooltip `Ширина Стула`
			ui_infield{3} "MinDist", 			  xPict + 78 + 92, yPict - _infHeight - 3, 	90, _infHeight ui_tooltip `Минимальное Расстояние Стул-Стул`
			ui_infield{3} "AddSideSpaceSize", 		   xPict - 25, yPict + PictHeight + 3, 	90, _infHeight ui_tooltip `Дополнительное Пространство Сбоку от Кресла-Коляски`
			ui_infield{3} "DistNextToWheelchair", xPict - 25 + 92, yPict + PictHeight + 3, 	90, _infHeight ui_tooltip `Расстояние возле Кресла-Коляски`
		endif
	endif

return

! ------------------------------------------------------------------------------
"TableChairpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	_outfFlag = 0
	x1 = 0
	x2 = 232
	dy = 24

	_yCurr = pageStartY	+ _yOffset
	ui_style 0, 1
	ui_outfield `Стол`, x1, _yCurr, 444 - x2, _outfHeight
	ui_outfield `Стул`, x2, _yCurr, 444 - x2, _outfHeight
	ui_style 0, 0

	ui_separator x2 - 10, 0, x2 - 10, 265

	_yCurr = _yCurr	+ dy
	ui_outfield `Тип`, x2, _yCurr, 444 - x2 - 92, _outfHeight

	_yCurr = _yCurr	- _yOffset
	ui_infield{3}	"iChairType", 444 - 90, _yCurr, 90, _infHeight,
			8, "", 3, 1,
			0, 0, 0, 0,
			1, _strChairType[1], CHAIRTYPE_RECTANGLE,
			2, _strChairType[2], CHAIRTYPE_ROUND ui_tooltip `Тип Стула`

	xPict = 7
	yPict = 118
	PictWidth = 113
	PictHeight = 106

	ui_pict 16, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_TableChair1.png

	_yCurr = yPict - _infHeight - 3
	ui_infield{3} "tableh", x1, _yCurr, 90, _infHeight ui_tooltip `Высота Стола`

	_yCurr = yPict - 4
	ui_infield{3} "gs_top_thk", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Толщина Столешницы`

	_yCurr = yPict + PictHeight + 3
	ui_infield{3} "gs_leg", xPict + 48, _yCurr, 90, _infHeight ui_tooltip `Толщина Ножек`

	xPict = 253
	yPict = 94
	PictWidth = 77
	PictHeight = 120

	ui_pict 17, xPict, yPict, PictWidth, PictHeight, 1	! ui_DiningTableRectangle_TableChair2.png

	_yCurr = yPict - _infHeight - 3
	ui_infield{3} "chairh", xPict - 8, _yCurr, 90, _infHeight ui_tooltip `Высота Стула`

	_yCurr = yPict + 80
	ui_infield{3} "gs_seat_height", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Высота Сидения`

return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 14,
													gs_SymbolType_m		= gs_SymbolType_m,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													gs_2DSeat			= 0,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentation":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1   
	surfacesArray[SurfItemID]	= "bUniformSurfaces"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_top_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_leg_mat"
	SurfItemID = SurfItemID + 1

	if TopSide or RightSide or bChairBottom or bChairLeft then
		surfacesArray[SurfItemID]	= "gs_frame_mat"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "gs_back_mat"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "gs_seat_mat"
		SurfItemID = SurfItemID + 1
	endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k

	call "ui_object_3DRepresentation" parameters 	surfacesArray 		= surfacesArray,
													childFlagArray 		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
