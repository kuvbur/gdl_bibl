
! ======================================================================
! SCALE
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

! --- 2D ----------------------------------------------------------------------

backrestAngle = atn(gs_seat_depth*0.2/gs_arm_height)
dr = gs_seat_depth*0.2-tan(backrestAngle)*gs_seat_height
X1 = (ZZYZX-gs_seat_height)*TAN(backrestAngle)
X2 = CushThk*COS(backrestAngle)

! Minimal space

if iLayoutType = LAYOUT_STRAIGHT then
	add2 -ArmWidth/2, -di
	MSType = 3
else
	if iPositioning = POSITIONING_CENTER then
		add2 0, -di/2
		MSType = 3
	else
		add2 (gs_seat_width-ArmWidth)/2, -di/2
		MSType = 3
	endif
endif

drawindex 10
call "fa_minimalspace" parameters all MSType = MSType,
								unID = 3000,
								A = A,
								B = B
del 1

PEN gs_cont_pen
FILL gs_fill_type
line_type 1

! ==============================================================================
! 2D HOTSPOTs
! ==============================================================================

if iLayoutType <> LAYOUT_CURVED then

	if iSeatArrangement = ARRANGE_STAGGEREDROWS then
		tempWidth  = - (gs_seat_width/2 + ArmWidth)
		tempWidth2 = gs_seat_width*(m) - (gs_seat_width)/2
		tempWidth3 = gs_seat_width*(m)
	else
		tempWidth  = - ArmWidth
		tempWidth2 = gs_seat_width*(m)
		tempWidth3 = gs_seat_width*(m)
	endif

	unID=1

	if iCalcMethod = CALCMETHOD_AREA then

		HOTSPOT2 -ArmWidth, -di, unID, A, 1+256 :	unID=unID+1
		HOTSPOT2 -1, -di, unID, A, 3 : unID=unID+1
		HOTSPOT2 A, -di, unID, A, 2 : unID=unID+1

		HOTSPOT2 -ArmWidth, B-di, unID, A, 1+256 :	unID=unID+1
		HOTSPOT2 -1, B-di, unID, A, 3 : unID=unID+1
		HOTSPOT2 A, B-di, unID, A, 2 : unID=unID+1

		HOTSPOT2 -ArmWidth, -di, unID, B, 1+256 :	unID=unID+1
		HOTSPOT2 -ArmWidth, -di-1, unID, B, 3 : unID=unID+1
		HOTSPOT2 -ArmWidth, -di+B, unID, B, 2 : unID=unID+1

		HOTSPOT2 A, -di, unID, B, 1+256 :	unID=unID+1
		HOTSPOT2 A, -di-1, unID, B, 3 : unID=unID+1
		HOTSPOT2 A, -di+B, unID, B, 2 : unID=unID+1

	endif

	unID=100

	! --- Start Position of seats -----------------------------------------------
	! if (Calculation Method=by No. of Seats then A=0 and B=0)

	ADD2 StartA,StartB
	HOTSPOT2 -ArmWidth, -di, unID :	unID=unID+1
	if iSeatArrangement = ARRANGE_STAGGEREDROWS THEN
		HOTSPOT2 gs_seat_width*(m-1), -di, unID :	unID=unID+1
	else
		HOTSPOT2 gs_seat_width*(m), -di, unID :	unID=unID+1
	endif

	! --- Seat Width -----------------------------------------------------------
	HOTSPOT2 -ArmWidth, 0, unID, gs_seat_width, 1+128 :	unID=unID+1
	HOTSPOT2 -1, 0, unID, gs_seat_width, 3+128 : unID=unID+1
	HOTSPOT2 gs_seat_width-ArmWidth, 0, unID, gs_seat_width, 2 : unID=unID+1

	! --- Seat Depth -----------------------------------------------------------
	HOTSPOT2 -ArmWidth, 0, unID, gs_seat_depth, 1+128 :	unID=unID+1
	HOTSPOT2 -ArmWidth, 1, unID, gs_seat_depth, 3+128 : unID=unID+1
	HOTSPOT2 -ArmWidth, -gs_seat_depth, unID, gs_seat_depth, 2 : unID=unID+1

	if n>1 then
		! --- Dist. between Rows -----------------------------------------------
		HOTSPOT2 tempWidth, 0, unID, di, 1+128 :	unID=unID+1
		HOTSPOT2 tempWidth, -1, unID, di, 3+128 : unID=unID+1
		HOTSPOT2 tempWidth, di, unID, di, 2 : unID=unID+1

		HOTSPOT2 tempWidth2, 0, unID, di, 1+128 :	unID=unID+1
		HOTSPOT2 tempWidth2, -1, unID, di, 3+128 : unID=unID+1
		HOTSPOT2 tempWidth2, di, unID, di, 2 : unID=unID+1
	else
		unID=unID+6
	endif
	del 1
else
	unID=1

	if iCalcMethod = CALCMETHOD_AREA then
		add2 gs_seat_width/2, gs_seat_depth

		HOTSPOT2 -ArmWidth, -di, unID, A, 1+256 :	unID=unID+1
		HOTSPOT2 -1, -di, unID, A, 3 : unID=unID+1
		HOTSPOT2 A, -di, unID, A, 2 : unID=unID+1

		HOTSPOT2 -ArmWidth, B-di, unID, A, 1+256 :	unID=unID+1
		HOTSPOT2 -1, B-di, unID, A, 3 : unID=unID+1
		HOTSPOT2 A, B-di, unID, A, 2 : unID=unID+1

		HOTSPOT2 -ArmWidth, -di, unID, B, 1+256 :	unID=unID+1
		HOTSPOT2 -ArmWidth, -di-1, unID, B, 3 : unID=unID+1
		HOTSPOT2 -ArmWidth, -di+B, unID, B, 2 : unID=unID+1

		HOTSPOT2 A, -di, unID, B, 1+256 :	unID=unID+1
		HOTSPOT2 A, -di-1, unID, B, 3 : unID=unID+1
		HOTSPOT2 A, -di+B, unID, B, 2 : unID=unID+1
		del 1
	endif
endif

! ==============================================================================
! 2D Plan Symbol
! ==============================================================================

if iLayoutType = LAYOUT_CURVED then
	if iPositioning = POSITIONING_SIDE then
		add2 A, -radrow

		for i= 1 to n
			if iSeatArrangement = ARRANGE_STAGGEREDROWS then
				if i%2 = 0  then
					m=m+1
				else
					m=m-1
				endif
			endif
			for j=1 to m
				if iSeatArrangement = ARRANGE_STAGGEREDROWS and i%2 = 1 then
					rot2 beta/2
				else
					rot2 0
				endif
				add2 -gs_seat_width/2, radrow+gs_seat_depth
				bLastChair = (j=1)
				if iCalcMethod = CALCMETHOD_AREA then
					if (1-COS((j-1)*beta))*radrow < i*di-gs_seat_depth then
						gosub "one chair"
					endif
				else
					gosub "one chair"
				endif
				if i = 1 and j= 1 then
					gosub "start hotspot1"
				endif
				if i = 1 and j= m then
					if iCalcMethod = CALCMETHOD_AREA then
						if (1-COS((j-1)*beta))*radrow < i*di-gs_seat_depth then
							gosub "start hotspot2"
						else
							unID = unID + 1
						endif
					else
						gosub "start hotspot2"
					endif
					if iCalcMethod = CALCMETHOD_SEATS then
						gosub "chair hotspots"
					else
						unID = unID + 6
					endif
				endif
				if iCalcMethod = CALCMETHOD_SEATS and i = 2 and j = m then
					gosub "rowdist hotspots"
				endif
				if j= 1 then
					HOTSPOT2 gs_seat_width+ArmWidth/2, 0, unID
				endif
				unID=unID+1
				if (1-COS((j-1)*beta))*radrow < i*di-gs_seat_depth and j = m then
					HOTSPOT2 -ArmWidth/2, 0, unID
				else
					unID = unID+1
				endif
				unID=unID+1
				del 2
				if not(bRadial) then
					beta = 2 * atn (((gs_seat_width)/2)/radrow)
				endif
				rot2 beta
			next j
			del j-1
			radrow = radrow + di
		next i
	else									! Start from Center Line
		add2 A/2, -radrow
		for i= 1 to n
			if iSeatArrangement = ARRANGE_STAGGEREDROWS then
				if i%2 = 0  then
					m=m+1
				else
					m=m-1
				endif
			endif
			if m%2 = 1 then
				j = 1
				bLastChair = 0
				add2 -gs_seat_width/2, radrow+gs_seat_depth
				if iCalcMethod = CALCMETHOD_AREA then
					if (1-COS((j-1)*beta))*radrow < i*di-gs_seat_depth then
						gosub "one chair"
					endif
				else
					gosub "one chair"
				endif
				del 1
			endif
			for k=1 to 2
				for j=1 to int(m/2)
					if iSeatArrangement = ARRANGE_STAGGEREDROWS and i%2 = 1 then
						rot2 beta/2
						if m%2 = 1 then
							add2 -gs_seat_width, radrow+gs_seat_depth
						else
							add2 -gs_seat_width/2, radrow+gs_seat_depth
						endif
					else
						if m%2 = 1 then
							rot2 beta
							add2 -gs_seat_width/2+(k=1 and j=1 and i<>1)*0.025, radrow+gs_seat_depth
						else
							rot2 beta/2
							add2 -gs_seat_width/2, radrow+gs_seat_depth
						endif
					endif
					bLastChair = ((k=1 and j=1 and m%2=0) or ((k=2 and j=1 and m%2=1)))
					if iCalcMethod = CALCMETHOD_AREA then
						if (1-COS((j-1)*beta))*radrow < i*di-gs_seat_depth then
							gosub "one chair"
						endif
					else
						gosub "one chair"
					endif
					if k = 1 and i = 1 and j = int(m/2) then
						add2 -gs_seat_width, 0
						gosub "start hotspot1"
						del 1
					endif
					if i = 1 and j = int(m/2) then
						if iCalcMethod = CALCMETHOD_AREA then
							if (1-COS((j-1)*beta))*radrow < i*di-gs_seat_depth then
								gosub "start hotspot2"
							else
								unID = unID + 1
							endif
						else
							gosub "start hotspot2"
						endif
						if iCalcMethod = CALCMETHOD_SEATS then
							gosub "chair hotspots"
						else
							unID = unID + 6
						endif
					endif
					if iCalcMethod = CALCMETHOD_SEATS and i = 2 and j = int(m/2) then
						gosub "rowdist hotspots"
					endif
					if j = int(m/2) then
						add2 -gs_seat_width, 0
						HOTSPOT2 gs_seat_width-ArmWidth/2, 0, unID
						del 1
					endif
					unID=unID+1
					if (1-COS((j-1)*beta))*radrow < i*di-gs_seat_depth and j= m then
						HOTSPOT2 -ArmWidth/2, 0, unID
					else
						unID = unID+1
					endif
					unID=unID+1
					del 2
					if not(bRadial) then
						beta = 2 * atn (((gs_seat_width)/2)/radrow)
					endif
					rot2 beta
				next j
				del j-1
				mul2 -1, 1
			next k
			del k-1
			radrow = radrow + di
		next i
	endif
else				! Straight layout
	add2 -ArmWidth/2, 0
	for i= 1 to n
		if iSeatArrangement = ARRANGE_STAGGEREDROWS then
			if i%2 = 0  then
				add2 0,0
				m=m+1
			else
				add2 StartA, 0
				m=m-1
			endif
		else
			add2 0,0
		endif

		HOTSPOT2 -ArmWidth/2, 0, unID :	unID=unID+1
		HOTSPOT2 gs_seat_width*m+ArmWidth/2, 0, unID :	unID=unID+1

		for j= 1 to m
			bLastChair = (j=m)
			gosub "one chair"
			add2 gs_seat_width, 0
		next j
		del m+1

		add2 0,di
	next i
	DEL n+1
endif
end

! ==============================================================================
! Subroutines
! ==============================================================================

"one chair":

	if det2D = 1 THEN	! --- 1/200 ---

		ADD2 0,-gs_seat_depth-dr
		! --- seat and Cushion -------------------------------------------------
		POLY2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			gs_seat_width, 0, 1,
			gs_seat_width, gs_seat_depth+X1+X2, 1,
			0, gs_seat_depth+X1+X2, 1,
			0, 0, -1
		DEL 1

	else

		if det2D = 2 THEN	! --- 1/100 ---

			ADD2 -ArmWidth/2,-gs_seat_depth
			! --- Arm --------------------------------------------------------------
			POLY2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 1,
				ArmWidth, 0, 1,
				ArmWidth, gs_seat_depth+0.1*gs_seat_depth, 1,
				0, gs_seat_depth+0.1*gs_seat_depth, 1,
				0, 0, -1
			DEL 1

			ADD2 ArmWidth/2,-gs_seat_depth-dr
			! --- seat and Cushion -------------------------------------------------
			POLY2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 1,
				gs_seat_width-ArmWidth, 0, 1,
				gs_seat_width-ArmWidth, gs_seat_depth+X1+X2, 1,
				0, gs_seat_depth+X1+X2, 1,
				0, 0, -1
			DEL 1

			ADD2 ArmWidth/2,0
			LINE2 0,0,gs_seat_width-ArmWidth, 0
			DEL 1

			if bLastChair then
				ADD2 -ArmWidth/2+gs_seat_width,-gs_seat_depth
				! --- Arm at last end ----------------------------------------------
				POLY2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
					0, 0, 1,
					ArmWidth, 0, 1,
					ArmWidth, gs_seat_depth+0.1*gs_seat_depth, 1,
					0, gs_seat_depth+0.1*gs_seat_depth, 1,
					0, 0, -1
				DEL 1
			endif
		else
			if det2D = 3 THEN	! --- 1/50 ---
				R1= 0.02
				RR= 0.04
				R2=0


				DRAWINDEX 20
				ADD2 ArmWidth/2,-gs_seat_depth-dr
				! --- Seat -------------------------------------------------------------
				POLY2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
					0, 0, 1,
					gs_seat_width-ArmWidth, 0, 1,
					gs_seat_width-ArmWidth, gs_seat_depth+dr, 1,
					0, gs_seat_depth+dr, 1
				DEL 1

				DRAWINDEX 30
				ADD2 ArmWidth/2,0
				! --- Cushion-----------------------------------------------------------
				POLY2_b 12, 1+2+4+64, gs_fill_pen, gs_back_pen,
					0, RR, 1,
					RR,RR,900,
					0,90,4001,
					gs_seat_width-ArmWidth-RR, 0, 1,
					gs_seat_width-ArmWidth-RR, RR, 900,
					0,90,4001,
					gs_seat_width-ArmWidth, X1+X2-dr-RR, 1,
					gs_seat_width-ArmWidth-RR, X1+X2-dr-RR, 900,
					0,90,4001,
					RR, X1+X2-dr, 1,
					RR, X1+X2-dr-RR, 900,
					0,90,4001
				DEL 1

				DRAWINDEX 40
				ADD2 -ArmWidth/2-0.02,-gs_seat_depth
				! --- Arm---------------------------------------------------------------
				POLY2_b 12, 1+2+4+64, gs_fill_pen, gs_back_pen,
					0, R1, 1,
					R1,R1,900,
					0,90,4001,
					ArmWidth+0.04-R1, 0, 1,
					ArmWidth+0.04-R1, R1, 900,
					0,90,4001,
					ArmWidth+0.04, gs_seat_depth-R1, 1,
					ArmWidth+0.04-R1, gs_seat_depth-R1, 900,
					0,90,4001,
					R1, gs_seat_depth, 1,
					R1, gs_seat_depth-R1, 900,
					0,90,4001

				DEL 1

				if bLastChair then
					ADD2 -ArmWidth/2-0.02+gs_seat_width,-gs_seat_depth
					! --- Arm at last end-----------------------------------------------
					POLY2_b 12, 1+2+4+64, gs_fill_pen, gs_back_pen,
						0, R1, 1,
						R1,R1,900,
						0,90,4001,
						ArmWidth+0.04-R1, 0, 1,
						ArmWidth+0.04-R1, R1, 900,
						0,90,4001,
						ArmWidth+0.04, gs_seat_depth-R1, 1,
						ArmWidth+0.04-R1, gs_seat_depth-R1, 900,
						0,90,4001,
						R1, gs_seat_depth, 1,
						R1, gs_seat_depth-R1, 900,
						0,90,4001
					DEL 1
				endif
			else
				add2 ArmWidth/2, -gs_seat_depth
				IF det2D = 4 THEN 	CALL "fa_ChairSymbol" PARAMETERS ALL gs_OrigoCentered = 0,		! --- Symbolic ---
																		A = gs_seat_width-ArmWidth,
																		B = gs_seat_depth+X1+X2,
																		calledAsMacro = 1
				del 1
			endif
		endif
	endif
return

"start hotspot1":
	unID=100
	! --- Start Position of seats -----------------------------------------------
	! if (Calculation Method=by No. of Seats then A=0 and B=0)

	add2 gs_seat_width, 0
	HOTSPOT2 0, -di, unID :	unID=unID+1
	del 1
return

"start hotspot2":
	HOTSPOT2 0, -di, unID :	unID=unID+1
return

"chair hotspots":
	! --- Seat Width -----------------------------------------------------------
	HOTSPOT2 -ArmWidth/2, 0, unID, gs_seat_width, 1+128 :	unID=unID+1
	HOTSPOT2 -1, 0, unID, gs_seat_width, 3+128 : unID=unID+1
	HOTSPOT2 gs_seat_width-ArmWidth/2, 0, unID, gs_seat_width, 2 : unID=unID+1

	! --- Seat Depth -----------------------------------------------------------
	rot2 -180
	HOTSPOT2 ArmWidth/2, gs_seat_depth, unID, gs_seat_depth, 1+256 :	unID=unID+1
	HOTSPOT2 ArmWidth/2, 1, unID, gs_seat_depth, 3+128 : unID=unID+1
	HOTSPOT2 ArmWidth/2, 0, unID, gs_seat_depth, 2+128 : unID=unID+1
	del 1

return

"rowdist hotspots":
	if n>1 then
		! --- Dist. between Rows -----------------------------------------------
		add2 0, -di
		HOTSPOT2 -ArmWidth/2, 0, unID, di, 1+128 :	unID=unID+1
		HOTSPOT2 -ArmWidth/2, -1, unID, di, 3+128 : unID=unID+1
		HOTSPOT2 -ArmWidth/2, di, unID, di, 2 : unID=unID+1
		del 1
	else
		unID=unID+3
	endif
return

