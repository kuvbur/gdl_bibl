
IF gs_detlevel_3D_m = 0 THEN END

IF gs_shadow = 0 THEN SHADOW OFF
IF gs_shadow = 1 THEN SHADOW ON

!!! *** 3D Feedback TO editing
IF GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40 THEN
	gs_detlevel_3D_m = 1
ENDIF

PEN gs_cont_pen


! === 3D HOTSPOTS ==============================================================

unID=1

HOTSPOT  A/2, -B/2, 0, unID           : unID=unID+1

IF iCalculationMethod =  CALCULATION_BYAREA THEN

	HOTSPOT  0,  0, 0, unID, A, 1+256 : unID=unID+1
	HOTSPOT -1,  0, 0, unID, A, 3     : unID=unID+1
	HOTSPOT  A,  0, 0, unID, A, 2     : unID=unID+1

	HOTSPOT  0, -B, 0, unID, A, 1+256 : unID=unID+1
	HOTSPOT -1, -B, 0, unID, A, 3     : unID=unID+1
	HOTSPOT  A, -B, 0, unID, A, 2     : unID=unID+1

	HOTSPOT  0,  0, 0, unID, B, 1+256 : unID=unID+1
	HOTSPOT  0,  1, 0, unID, B, 3     : unID=unID+1
	HOTSPOT  0, -B, 0, unID, B, 2     : unID=unID+1

	HOTSPOT  A,  0, 0, unID, B, 1+256 : unID=unID+1
	HOTSPOT  A,  1, 0, unID, B, 3     : unID=unID+1
	HOTSPOT  A, -B, 0, unID, B, 2     : unID=unID+1

ELSE

	HOTSPOT  0,  0, 0, unID, AA, 1+256, nColumn : unID=unID+1
	HOTSPOT -1,  0, 0, unID, AA, 3,     nColumn : unID=unID+1
	HOTSPOT  A,  0, 0, unID, AA, 2,     nColumn : unID=unID+1

	HOTSPOT  0, -B, 0, unID, AA, 1+256, nColumn : unID=unID+1
	HOTSPOT -1, -B, 0, unID, AA, 3,     nColumn : unID=unID+1
	HOTSPOT  A, -B, 0, unID, AA, 2,     nColumn : unID=unID+1

	HOTSPOT  0,  0, 0, unID, BB, 1+256, nRow    : unID=unID+1
	HOTSPOT  0,  1, 0, unID, BB, 3,     nRow    : unID=unID+1
	HOTSPOT  0, -B, 0, unID, BB, 2,     nRow    : unID=unID+1

	HOTSPOT  A,  0, 0, unID, BB, 1+256, nRow    : unID=unID+1
	HOTSPOT  A,  1, 0, unID, BB, 3,     nRow    : unID=unID+1
	HOTSPOT  A, -B, 0, unID, BB, 2,     nRow    : unID=unID+1

ENDIF


IF gs_detlevel_2D_m = 4 & (gs_SymbolType = `Окружность` | gs_SymbolType = `Круглый со Спинкой`) THEN

	! === Seat Width = Depth ===
	HOTSPOT  0,             -gs_seat_width / 2, gs_seat_height, unID, gs_seat_width, 1+128 : unID=unID+1
	HOTSPOT -1,             -gs_seat_width / 2, gs_seat_height, unID, gs_seat_width, 3+128 : unID=unID+1
	HOTSPOT  gs_seat_width, -gs_seat_width / 2, gs_seat_height, unID, gs_seat_width, 2     : unID=unID+4

ELSE

	! === Seat Width ===
	HOTSPOT  0,             -gs_seat_depth, gs_seat_height, unID, gs_seat_width, 1+128 : unID=unID+1
	HOTSPOT -1,             -gs_seat_depth, gs_seat_height, unID, gs_seat_width, 3+128 : unID=unID+1
	HOTSPOT  gs_seat_width, -gs_seat_depth, gs_seat_height, unID, gs_seat_width, 2     : unID=unID+1

	! === Seat Depth ===
	HOTSPOT  gs_seat_width,  0,             gs_seat_height, unID, gs_seat_depth, 1+128 : unID=unID+1
	HOTSPOT  gs_seat_width,  1,             gs_seat_height, unID, gs_seat_depth, 3+128 : unID=unID+1
	HOTSPOT  gs_seat_width, -gs_seat_depth, gs_seat_height, unID, gs_seat_depth, 2     : unID=unID+1

ENDIF


! === Seat Height ===
HOTSPOT  gs_seat_width / 2, 0,  0,              unID, gs_seat_height, 1+128 : unID=unID+1
HOTSPOT  gs_seat_width / 2, 0, -1,              unID, gs_seat_height, 3+128 : unID=unID+1
HOTSPOT  gs_seat_width / 2, 0,  gs_seat_height, unID, gs_seat_height, 2     : unID=unID+1

! === Chair Height ===
HOTSPOT  gs_seat_width / 2, 0,  0,      unID, chairh, 1+128 : unID=unID+1
HOTSPOT  gs_seat_width / 2, 0, -1,      unID, chairh, 3+128 : unID=unID+1
HOTSPOT  gs_seat_width / 2, 0,  chairh, unID, chairh, 2     : unID=unID+1

! === Dist. between Rows ===
IF nRow>1 THEN
	HOTSPOT 0,  0,       0, unID, distRow, 1+128 : unID=unID+1
	HOTSPOT 0,  1,       0, unID, distRow, 3+128 : unID=unID+1
	HOTSPOT 0, -distRow, 0, unID, distRow, 2     : unID=unID+1
ELSE
	unID=unID+3
ENDIF


! === 3D MODEL =================================================================

nColumn = nColumn

ADD 0, - gs_seat_depth, 0

FOR i = 1 TO nRow

	IF iSeatArrangement = ARRANGE_STAGGEREDROWS THEN
		IF i/2 <> INT(i/2) THEN
				ADD 0, 0, 0
				nColumn = nColumn + 1
				IF i = 1 THEN nColumn = nColumn - 1
			ELSE
				nColumn = nColumn - 1
				ADD (gs_seat_width + distChairH) / 2, 0, 0
		ENDIF
	ELSE
		nColumn = nColumn
		ADD 0, 0, 0
	ENDIF

	HOTSPOT 0,                                                  0, 0, unID : unID=unID+1
	HOTSPOT gs_seat_width * nColumn + distChairH * (nColumn-1), 0, 0, unID : unID=unID+1

	FOR j = 1 TO nColumn

		MATERIAL gs_seat_mat

		GOSUB 500

		ADD gs_seat_width + distChairH, 0, 0

	NEXT j
	DEL nColumn + 1

	ADD 0, -distRow, 0

NEXT i

DEL TOP


END


! ==============================================================================
! SUBROUTINES
! ==============================================================================


500:

toth=chairh-gs_seat_height


! === CHAIR / CIRCLE ===========================================================

if iChairType = CHAIRTYPE_ROUND then

	C = gs_seat_height
	ADD gs_seat_width/2, gs_seat_width/2, 0

	RESOL 2 * gs_resol

	MATERIAL gs_seat_mat

	ADDZ C - 0.02
	CYLIND 0.02, gs_seat_width/2
	DEL 1

	h = 0.45/40

	MATERIAL gs_frame_mat

	ADD gs_seat_width/2 * COS(45), gs_seat_width/2 * COS(45), 0

	FOR jj=1 TO 2
		FOR ii=1 TO 2
			RULED 4, 119,
				-h, -h, 0,
				-h,  h, 0,
				 h,  h, 0,
				 h, -h, 0,

				-h, -3*h - 0.15/40, C - 0.02,
				-h, -h - 0.15/40,   C - 0.02,
				 h, -h - 0.15/40,   C - 0.02,
				 h, -3*h - 0.15/40, C - 0.02

			GOSUB 2000

			ADDX -gs_seat_width * COS(45)
		NEXT ii
		DEL 3

		MULY -1
		ADD gs_seat_width/2 * COS(45), gs_seat_width/2 * COS(45), 0

	NEXT jj
	DEL 3

	MATERIAL gs_back_mat

	ADD gs_seat_width/2*COS(45),gs_seat_width/2*COS(45)-2*h,C
	FOR ii=1 TO 2
		PRISM_ 4, 3*h,
				-h, -h, 11,
				-h,  h, 11,
				 h,  h, 11,
				 h, -h, 11
		ADDX -gs_seat_width * COS(45)
	NEXT ii
	DEL 3

	ADD gs_seat_width/2 * COS(45), gs_seat_width/2 * COS(45) - 2*h, C + 3*h
	FOR ii=1 TO 2
		RULED 4, 111,
			-h, -h, 0,
			-h,  h, 0,
			 h,  h, 0,
			 h, -h, 0,

			-h, 3*h, toth - 3*h,
			-h, 4*h, toth - 3*h,
			 h, 4*h, toth-3*h,
			 h, 3*h, toth-3*h
		ADDX -gs_seat_width * COS(45)
	NEXT ii

	DEL 3

	GOSUB 2000

	Beta = ATN((toth - 3*h) / (1.5*h))
	ADD -0, gs_seat_width/2 * COS(45) + 2*h, C + toth
	ROTX Beta*2

	CUTPLANE 1, 0, 1, 0

		DEL 2

		ADDZ C + toth - h
		ROTY 90

		RESOL 3*gs_resol
		REVOLVE 5, 360, 63,
			0,   gs_seat_width/2 - h/8 + h, 0,
			0,   gs_seat_width/2 + h,       0,
			5*h, gs_seat_width/2 + h,       0,
			5*h, gs_seat_width/2 - h/8 + h, 0,
			0,   gs_seat_width/2 - h/8 + h, 0
		DEL 2

		ADDZ C + toth - 8*h
		ROTY 90
		dd = (8*h) / TAN(Beta)

		REVOLVE 5, 360, 63,
			0,   gs_seat_width/2 - h/8 + h - dd, 0,
			0,   gs_seat_width/2 + h - dd,       0,
			5*h, gs_seat_width/2 +h - dd,        0,
			5*h, gs_seat_width/2 - h/8 + h - dd, 0,
			0,   gs_seat_width/2 - h/8 + h - dd, 0
		DEL 2

	CUTEND

	GOSUB 1000

	DEL 1

	HOTSPOT gs_seat_width/2, gs_seat_width, 0

ELSE


! === CHAIR / RECTANGLE ========================================================

	C = gs_seat_height
	h = 0.03				!!! Chair Leg Size
	gs_seat_thk = 0.03		!!! Seat Thickness


	ADD gs_seat_width/2, gs_seat_depth/2, 0


	! --- CHAIR_RECTANGLE/Simple -----------------------------------------------

	IF gs_detlevel_3D_m = 1 THEN

		MATERIAL gs_seat_mat

		ADDZ gs_seat_height-gs_seat_thk

		PRISM_ 5, gs_seat_thk,		! Seat prism
			-gs_seat_width/2, -gs_seat_depth/2,  15,
			-gs_seat_width/2,  gs_seat_depth/2,  15,
			 gs_seat_width/2,  gs_seat_depth/2,  15,
			 gs_seat_width/2, -gs_seat_depth/2,  15,
			-gs_seat_width/2, -gs_seat_depth/2, -1
		DEL 1

		GOSUB 1000

		MATERIAL gs_frame_mat

		ADD -gs_seat_width/2, -gs_seat_depth/2, 0
		BLOCK h, h, gs_seat_height - gs_seat_thk
		DEL 1

		ADD gs_seat_width/2 - h, -gs_seat_depth/2, 0
		BLOCK h, h, gs_seat_height - gs_seat_thk
		DEL 1

		ADD gs_seat_width/2 - h, gs_seat_depth/2 - h, 0
		BLOCK h, h, gs_seat_height - gs_seat_thk
		DEL 1

		ADD -gs_seat_width/2, gs_seat_depth/2 - h, 0
		BLOCK h, h, gs_seat_height - gs_seat_thk
		DEL 1

		MATERIAL gs_back_mat

		ADD -gs_seat_width/2, gs_seat_depth/2 - h, gs_seat_height
		BLOCK gs_seat_width, h, toth
		DEL 1

		GOSUB 1000

	ENDIF


	! --- CHAIR_RECTANGLE/Detailed ---------------------------------------------

	IF gs_detlevel_3D_m = 2 THEN

		MULY -1

		MATERIAL gs_seat_mat

		ADDZ C-gs_seat_thk

		PRISM_ 8, gs_seat_thk,		! Seat prism
			-gs_seat_width/2 + h, -gs_seat_depth/2,     15,
			-gs_seat_width/2 + h, -gs_seat_depth/2 + h, 15,
			-gs_seat_width/2,     -gs_seat_depth/2 + h, 15,
			-gs_seat_width/2,      gs_seat_depth/2,     15,
			 gs_seat_width/2,      gs_seat_depth/2,     15,
			 gs_seat_width/2,     -gs_seat_depth/2 + h, 15,
			 gs_seat_width/2 - h, -gs_seat_depth/2 + h, 15,
			 gs_seat_width/2 - h, -gs_seat_depth/2,     15

		GOSUB 2000

		DEL 1

		MATERIAL gs_frame_mat

		FOR ii = 1 TO 2
			ADD -gs_seat_width/2, -gs_seat_depth/2, 0
			BLOCK h, h, C + toth
			DEL 1

			ADD -gs_seat_width/2, gs_seat_depth/2 - h, 0
			BLOCK h, h, C - gs_seat_thk
			DEL 1

			MULX -1
		NEXT ii

		GOSUB 1000

		FOR ii = 1 TO 2
			ADD -gs_seat_width/2 + h/2, -gs_seat_depth/2 + h, C - C/8 - gs_seat_thk
			BLOCK h/2, gs_seat_depth - 2*h, C/8
			DEL 1

			ADD -gs_seat_width/2 + h/2, -gs_seat_depth/2 + h, 0.5*C
			BLOCK h/2, gs_seat_depth - 2*h, C/8
			DEL 1

			MULX -1
		NEXT ii

		GOSUB 2000

		MATERIAL gs_back_mat

		ADD -gs_seat_width/2 + h, -gs_seat_depth/2, c + toth - toth/6 - toth/15
		BLOCK gs_seat_width - 2*h, h/2, toth/6
		DEL 1

		ADD -gs_seat_width/2 + h, -gs_seat_depth/2, c + toth - toth/6 - toth/15 - toth/4
		BLOCK gs_seat_width - 2*h, h/2, toth/6
		DEL 1

		GOSUB 2000

		DEL 5

	ENDIF

	DEL 1

	HOTSPOT gs_seat_width/2, gs_seat_depth, 0

ENDIF


RETURN


! ==============================================================================

1000:

	ROTY 90
	GOSUB 5000
	DEL 1

RETURN


! ==============================================================================

2000:

	GOSUB 5000

RETURN


! ==============================================================================

5000:

	VERT 0, 0, 0
	VERT 1, 0, 0
	VERT 0, 1, 0
	VERT 0, 0, 1

	COOR 2, -1, -2, -3, -4

	BASE

	BODY 1

RETURN


