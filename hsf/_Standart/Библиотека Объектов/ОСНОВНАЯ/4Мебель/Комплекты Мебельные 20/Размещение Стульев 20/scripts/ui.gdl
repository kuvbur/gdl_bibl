
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 					=-1
_idxTab = 1
TABID_StyleAndDimensions	= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation		= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions			= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Размещения Стульев`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Компоновка и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37

	x1 = 0
	x2 = 122
	x3 = 232
	x4 = 354
	dy = 24

	_yCurr = pageStartY
	ui_infield{3} "iCalculationMethod", x2, _yCurr, 90, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", stCalculationMethods[1], 1,
			"", stCalculationMethods[2], 2 ui_tooltip `Способ Расчета`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Способ Расчета`, x1, _yCurr, x2 - 2, _outfHeight
	ui_outfield `Количество Мест`, x3, _yCurr, x2 - 2, _outfHeight

	formatString = "%~"
	ui_outfield str(formatString , nChair), x4, _yCurr, 90, _outfHeight

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "iSeatArrangement", x2, _yCurr, 90, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Со Смещением`, 1,
			"", `Без Смещения`, 2 ui_tooltip `Расположение Мест`

	ui_infield{3} "gs_seat_height", x4, _yCurr, 90, _infHeight ui_tooltip `Высота Сидения`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Расположение Мест`, x1, _yCurr, x2 - 2, _outfHeight
	ui_outfield `Высота Сидения`, x3, _yCurr, x2 - 2, _outfHeight

	_yCurr = _yCurr + dy - _yOffset
	ui_separator x1, _yCurr, 444,_yCurr
	ui_separator 222, pageStartY, 222, _yCurr - 3

	xPict = 185
	yPict = 81
	PictWidth = 166
	PictHeight = 163

	if iCalculationMethod = CALCULATION_BYCHAIRNO then
		_yCurr = _yCurr + 6
		ui_infield{3} "nRow", x2 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Рядов`
		ui_infield{3} "nColumn", x4 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Мест в Ряду`
	
		_yCurr = _yCurr + _yOffset
		ui_outfield `Количество Рядов`, x1, _yCurr, x2 + 36 - 2, _outfHeight
		ui_outfield `Количество Мест в Ряду`, x3, _yCurr, x2 + 36 - 2, _outfHeight
	
		_yCurr = _yCurr + dy - _yOffset
		ui_separator x1, _yCurr, 444,_yCurr
	
		if iSeatArrangement = ARRANGE_STAGGEREDROWS then
			ui_pict 4, xPict, yPict, PictWidth, PictHeight, 1	! ui_ChairLayout_Layout_Staggered1.png
		else
			ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1	! ui_ChairLayout_Layout_Even1.png
		endif
	else
		_yCurr = yPict - _infHeight - 3
		ui_infield{3} "A", xPict + 25, _yCurr, 90, _infHeight ui_tooltip `Ширина Размещения`

		_yCurr = yPict + 107
		ui_infield{3} "B", xPict - 93, _yCurr, 90, _infHeight ui_tooltip `Глубина Размещения`

		if iSeatArrangement = ARRANGE_STAGGEREDROWS then
			ui_pict 5, xPict, yPict, PictWidth, PictHeight, 1	! ui_ChairLayout_Layout_Staggered2.png
		else
			ui_pict 3, xPict, yPict, PictWidth, PictHeight, 1	! ui_ChairLayout_Layout_Even2.png
		endif
	endif

	_yCurr = yPict +PictHeight + 2
	ui_infield{3} "LeftMinspace", xPict - 48, _yCurr, 90, _infHeight ui_tooltip `Минимальное Пространство Слева`
	ui_infield{3} "distChairH", xPict + 47, _yCurr, 90, _infHeight ui_tooltip `Расстояние Между Стульями по Горизонтали`
	ui_infield{3} "RightMinspace", xPict + 142, _yCurr, 90, _infHeight ui_tooltip `Минимальное Пространство Справа`

	_yCurr = yPict + 8
	ui_infield{3} "RearMinspace", xPict + 169, _yCurr, 90, _infHeight ui_tooltip `Минимальное Пространство Сзади`

	_yCurr = yPict + 8 + dy
	ui_infield{3} "distRow", xPict + 169, _yCurr, 90, _infHeight ui_tooltip `Расстояние между Рядами`

	_yCurr = yPict + 137
	ui_infield{3} "FrontMinspace", xPict + 169, _yCurr, 90, _infHeight ui_tooltip `Минимальное Пространство Спереди`

	xPict = 0
	yPict = 88
	PictWidth = 38 
	PictHeight = 40

	ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1	! ui_ChairLayout_LayoutAndDimensions1.png

	_yCurr = yPict + 5
	ui_infield{3} "gs_seat_depth", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Глубина Стула`

	_yCurr = yPict + PictHeight + 3
	ui_infield{3} "gs_seat_width", xPict, _yCurr, 90, _infHeight ui_tooltip `Ширина Стула`

return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 14,
													gs_SymbolType_m		= gs_SymbolType_m,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													gs_2DSeat			= 0,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1

	surfacesArray[SurfItemID]	= "gs_frame_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_back_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_seat_mat"
	SurfItemID = SurfItemID + 1

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters all	surfacesArray		= surfacesArray,
														childFlagArray		= childFlagArray,
														iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
