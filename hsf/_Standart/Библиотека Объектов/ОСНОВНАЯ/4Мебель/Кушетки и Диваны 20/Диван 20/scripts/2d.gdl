
! -----------------------------------------------------------------------------
! Scale
! -----------------------------------------------------------------------------
call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

! -----------------------------------------------------------------------------
! 2D symbol
! -----------------------------------------------------------------------------

if iStyle = 1 | iStyle = 5 then
	if det2D = 1 then	iSofaSymbolType = SOFASYMBOL_RECTSIMPLE	!! 1/200
	if det2D = 2 then   iSofaSymbolType = SOFASYMBOL_RECT3 !! 1/100
endif
if iStyle = 2 | iStyle = 4 then
	if det2D = 1 then	iSofaSymbolType = SOFASYMBOL_RECTSIMPLE	!! 1/200
	if det2D = 2 then   iSofaSymbolType = SOFASYMBOL_RECT2 !! 1/100
endif
if iStyle = 3 then
	if det2D = 1 then	iSofaSymbolType = SOFASYMBOL_RECTSIMPLE	!! 1/200
	if det2D = 2 then   iSofaSymbolType = SOFASYMBOL_RECT1 !! 1/100
endif
if iStyle = 6 | iStyle = 7 then
	if det2D = 1 then	iSofaSymbolType = SOFASYMBOL_CURVEDSIMPLE	!! 1/200
	if det2D = 2 then   iSofaSymbolType = SOFASYMBOL_CURVED !! 1/100
endif

if det2D = 3 then   iSofaSymbolType = 0 !! 1/50

call "fa_SofaSymbol" parameters ALL gs_SeatNum_m = nSeats, iSofaSymbolType = iSofaSymbolType, MSType = 3

! -----------------------------------------------------------------------------
! Sofa Types
! -----------------------------------------------------------------------------
pen gs_cont_pen
fill gs_fill_type
gosub iStyle


end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Sofa Style 1
! -----------------------------------------------------------------------------

1:

	! --- 1/50 ---
	if det2D = 3 then

		!! *** Symbol Lines

		add2 0.15, 0
		for ii = 1 TO nSeats
			add2 (a-0.30)/nSeats*(ii-1), 0
			if gs_2DSeat then 
				poly2_ 9, 1,
					(a - 0.30) / nSeats - 0.03, 0, 1,
					0.03, 0, 1,
					0, 0.03, 1001,
					0, b - yyy / tan (75) - 0.1972, 1,
					0.03, b - yyy / tan (75) - 0.1672, 1001,
					(a - 0.30) / nSeats - 0.03, b - yyy / tan(75) - 0.1672, 1,
					(a - 0.30) / nSeats, b-yyy/TAN(75) - 0.1972, 1001,
					(a - 0.30) / nSeats, 0.03, 1,
					(a - 0.30) / nSeats - 0.03, 0, 1001
			else
				poly2_B 7, 1, gs_fill_pen, gs_back_pen,
					(a - 0.30) / nSeats - 0.03, 0, 1,
					0.03, 0, 1,
					0, 0.03, 1000,
					0, b - yyy / tan (75) - 0.1672, 0,
					(a - 0.30) / nSeats, b - yyy / tan (75) - 0.1672, 0,
					(a - 0.30) / nSeats, 0.03, 1,
					(a - 0.30) / nSeats - 0.03, 0, 1000
			endif

			poly2_ 9, 1,
				(a - 0.30) / nSeats - 0.03, b - 0.04, 1,
				0.03, b - 0.04, 1,
				0, b - 0.07, 1001,
				0, b - yyy / tan (75) - 0.1372, 1,
				0.03, b - yyy / tan (75) - 0.1672, 1001,
				(a - 0.30) / nSeats - 0.03, b - yyy / tan (75) - 0.1672, 1,
				(a - 0.30) / nSeats, b - yyy / tan (75) - 0.1372, 1001,
				(a - 0.30) / nSeats, b - 0.07, 1,
				(a - 0.30) / nSeats - 0.03, b - 0.04, 1001
			poly2_ 5, 1,
				0, b-0.15, 1,
				0.03, b-0.15, 901,
				0.03, b-0.18, 3001,
				(a-0.30)/nSeats-0.03, b-0.18, 1,
				(a-0.30)/nSeats, b-0.15, 1001
			del 1
		next ii
		del 1

		for i = 1 TO 2
			add2 (a-0.15)*(i-1), 0
			poly2_ 9, 1,
				0.13, 0, 1,
				0.02, 0, 1,
				0, 0.02, 1001,
				0, b-0.02, 1,
				0.02, b, 1001,
				0.13, b, 1,
				0.15, b-0.02, 1001,
				0.15, 0.02, 1,
				0.13, 0, 1001
			del 1
		next i

		line2 0.15, b-0.02, a-0.15, b-0.02

		! --- Background Fill ---

		add2 0.15, 0
		for ii = 1 TO nSeats
			add2 (a-0.30)/nSeats*(ii-1), 0
			poly2_B 7, 2+64, gs_fill_pen, gs_back_pen,
				(a - 0.30) / nSeats - 0.03, 0, 1,
				0.03, 0, 1,
				0, 0.03, 1001,
				0, b - yyy / tan (75) - 0.1672, 1,
				(a - 0.30) / nSeats, b - yyy / tan (75) - 0.1672, 1,
				(a - 0.30) / nSeats, 0.03, 1,
				(a - 0.30) / nSeats - 0.03, 0, 1001

			poly2_B 5, 2+64, gs_fill_pen, gs_back_pen,
				(a - 0.30) / nSeats, b - 0.02, 1,
				0, b - 0.02, 1,
				0, b - yyy / tan (75) - 0.1672, 1,
				(a - 0.30) / nSeats, b - yyy / tan (75) - 0.1672, 1,
				(a - 0.30) / nSeats, b - 0.02, 1

			del 1
		next ii
		del 1


		for i = 1 TO 2
			add2 (a-0.15)*(i-1), 0
			poly2_B 9, 2+64, gs_fill_pen, gs_back_pen,
				0.13, 0, 1,
				0.02, 0, 1,
				0, 0.02, 1001,
				0, b-0.02, 1,
				0.02, b, 1001,
				0.13, b, 1,
				0.15, b-0.02, 1001,
				0.15, 0.02, 1,
				0.13, 0, 1001
			del 1
		next i

	endif
return

! -----------------------------------------------------------------------------
! Sofa Style 2
! -----------------------------------------------------------------------------

2:

	! --- 1/50 ---
	if det2D = 3 then

		bb = B - ((zzyzx - gs_seat_height + 0.02) * cos (70) + 0.17 - 0.02 * sin (5))
		bb1 = (arm_h - gs_seat_height + 0.02) * cos (70)
		RR = 0.05

		poly2_ 8, 1+4,
			0, RR, 1,
			0, b, 1,
			a, b, 1,
			a, RR, 1,
			a-0.15, RR, 1,
			a-0.15, bb, 0,
			0.15, bb, 1,
			0.15, RR, 1

		add2 0.15, 0
		for ii=1 TO nSeats
			add2 (a-0.30)/nSeats*(ii-1), 0

			if gs_2DSeat then
				poly2_ 9, 1+4,
					(a-0.30)/nSeats-RR, 0, 1,
					RR, 0, 1,
					0, RR, 1001,
					0, bb-RR, 1,
					RR, bb, 1001,
					(a-0.30)/nSeats-RR, bb, 1,
					(a-0.30)/nSeats, bb-RR, 1001,
					(a-0.30)/nSeats, RR, 1,
					(a-0.30)/nSeats-RR, 0, 1001
			else
				poly2_B 6, 1, gs_fill_pen, gs_back_pen,
					(a-0.30)/nSeats-RR, 0, 1,
					RR, 0, 1,
					0, RR, 1000,
					(a-0.30)/nSeats, RR, 1,
					(a-0.30)/nSeats-RR, RR, 901,
					(a-0.30)/nSeats-RR, 0, 3001
			endif

			del 1
		next ii
		del 1

		line2 0, b-0.155, a, b-0.155

		poly2_ 6, 1,
			0, bb+bb1-0.03, 1,
			0.03, bb+bb1-0.03, 901,
			0.03, bb+bb1, 3001,
			0.12, bb+bb1, 1,
			0.15, bb+bb1-0.03, 1001,
			0.15, bb-0.03, 1
		add2 a-0.15,0
		poly2_ 6, 1,
			0, bb-0.03, 1,
			0, bb+bb1-0.03, 1,
			0.03, bb+bb1-0.03, 901,
			0.03, bb+bb1, 3001,
			0.12, bb+bb1, 1,
			0.15, bb+bb1-0.03, 1001
		del 1

		! --- Background Fill ---

		poly2_B 4, 2+4+64, gs_fill_pen, gs_back_pen,
			0, RR, 1,
			0, b, 1,
			a, b, 1,
			a, RR, 1

		add2 0.15, 0
		for ii = 1 TO nSeats
			add2 (a-0.30)/nSeats*(ii-1), 0
			poly2_B 6, 2+4+64, gs_fill_pen, gs_back_pen,
				(a-0.30)/nSeats-RR, 0, 1,
				RR, 0, 1,
				0, RR, 1001,
				(a-0.30)/nSeats, RR, 1,
				(a-0.30)/nSeats-RR, RR, 901,
				(a-0.30)/nSeats-RR, 0, 3001
			del 1
		next ii
		del 1

	endif

return

! -----------------------------------------------------------------------------
! Sofa Style 3
! -----------------------------------------------------------------------------

3:
	!!!! ******************************** 1/50
	if det2D = 3 then

		poly2_ 8, 1+4,
			0, 0, 1,
			0.22, 0, 1,
			0.22, b-0.22, 1,
			a-0.22, b-0.22, 1,
			a-0.22, 0, 1,
			a, 0, 1,
			a, b, 1,
			0, b, 1

		line2 0.10, b-0.10, 0.22, b-0.22
		line2 a-0.10, b-0.10, a-0.22, b-0.22
		line2 0.22, 0, a-0.22, 0

		RR = 0.06

		if gs_2DSeat then 
			add2 0.22, 0
			for ii = 1 TO nSeats
				aa = (a-0.22*2)/nSeats
				bb = b-0.22
				add2 aa*(ii-1), 0
				poly2_ 9, 1+4,
					aa-RR, 0, 1,
					RR, 0, 1,
					0, RR, 1001,
					0, bb-RR, 1,
					RR, bb, 1001,
					aa-RR, bb, 1,
					aa, bb-RR, 1001,
					aa,RR, 1,
					aa-RR, 0, 1001
				del 1
			next ii
			del 1
		endif

		!!! *** Background Fill
		poly2_B      4, 2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			a, 0, 1,
			a, b, 1,
			0, b, 1

	endif

return

! -----------------------------------------------------------------------------
! Sofa Style 4
! -----------------------------------------------------------------------------

4:
	if det2D = 3 then

		thick1 = 0.05
		z0 = zzyzx*0.67
		q1 = (z0-thick1)*0.35
		x3 = 0.05
		x4 = thick1/cos(10)+(z0-thick1)*tan(10)

		poly2_ 4,5,0,0,1,a,0,1,a,b,1,0,b,1
		poly2 4,1,x3,0,x3,b-thick1,a-x3,b-thick1,a-x3,0
		line2 x3,b-thick1-q1,a-x3,b-thick1-q1
		line2 x4+q1/2,0,x4+q1/2,b-thick1-q1
		line2 a-x4-q1/2,0,a-x4-q1/2,b-thick1-q1

		if gs_2DSeat then
			for xx = 1 TO nSeats - 1
				add2 x4+q1/2+(a-x4*2-q1)/nSeats*xx,0
				line2 0,0,0,b-thick1-q1
				del 1
			next xx
		endif

		unID = 1
		for xx = 1 TO nSeats
			add2 x4+q1/2+(a-x4*2-q1)/nSeats*xx,0
			hotspot2 -(a-x4*2-q1)/nSeats/2,0, unID: unID=unID+1
			del 1
		next xx

		poly2_B 4, 2+4+64,  gs_fill_pen, gs_back_pen,
				0, 0, 1,
				a, 0, 1,
				a, b, 1,
				0, b, 1

	endif

return

! -----------------------------------------------------------------------------
! Sofa Style 5
! -----------------------------------------------------------------------------

5:

	if det2D = 3 then

		RR = 0.04

		add2 0.18, 0
		for ii=1 TO nSeats
			add2 (a-0.36)/nSeats*(ii-1), 0
			if gs_2DSeat then
				poly2_ 9, 1,
					(a-0.36)/nSeats-RR, 0, 0,
					RR, 0, 1,
					0, RR, 1001,
					0, b-yyy/TAN(75)-0.1672-RR, 1,
					RR, b-yyy/TAN(75)-0.1672, 1001,
					(a-0.36)/nSeats-RR, b-yyy/TAN(75)-0.1672, 1,
					(a-0.36)/nSeats, b-yyy/TAN(75)-0.1672-RR, 1001,
					(a-0.36)/nSeats, RR, 1,
					(a-0.36)/nSeats-RR, 0, 1001
			endif
			poly2_ 9, 1,
				(a-0.36)/nSeats-RR, b-0.04, 1,
				RR, b-0.04, 1,
				0, b-0.04-RR, 1001,
				0, b-yyy/TAN(75)-0.1672+RR, 1,
				RR, b-yyy/TAN(75)-0.1672, 1001,
				(a-0.36)/nSeats-RR, b-yyy/TAN(75)-0.1672, 1,
				(a-0.36)/nSeats, b-yyy/TAN(75)-0.1672+RR, 1001,
				(a-0.36)/nSeats, b-0.04-RR, 1,
				(a-0.36)/nSeats-RR, b-0.04, 1001
			poly2_ 5, 1,
				0, b-0.18+RR, 1,
				RR, b-0.18+RR, 901,
				RR, b-0.18, 3001,
				(a-0.36)/nSeats-RR, b-0.18, 1,
				(a-0.36)/nSeats, b-0.18+RR, 1001
			del 1
		next ii
		del 1

		if not(gs_2DSeat) then 
			add2 0.18, 0
			poly2_B 7, 1, gs_fill_pen, gs_back_pen,
					a-0.36-RR, 0, 1,
					RR, 0, 1,
					0, RR, 1000,
					0, b, 1,
					a-0.36, b, 0,
					a-0.36, RR, 1,
					a-0.36-RR, 0, 1001
		del 1
		endif

		line2 0.21, 0, a-0.21, 0
		line2 0.18/2, b, a-0.18/2, b
		line2 0.18/2, b, 0.18/2, b-0.04-RR
		line2 A-0.18/2, b, A-0.18/2, b-0.04-RR

		rect2 0, RR,0.18,b-0.04-RR
		rect2 A, RR,A-0.18,b-0.04-RR

		! --- Background Fill ---

		add2 0.18, 0
		poly2_B 7, 2+64, gs_fill_pen, gs_back_pen,
				a-0.36-RR, 0, 1,
				RR, 0, 1,
				0, RR, 1001,
				0, b, 1,
				a-0.36, b, 1,
				a-0.36, RR, 1,
				a-0.36-RR, 0, 1001
		del 1


		for i = 1 TO 2
			poly2_B 7, 2+64, gs_fill_pen, gs_back_pen,
					0, RR, 1,
					0.18, RR, 1,
					0.18, b, 1,
					0.18/2, b, 1,
					0.18/2, b-0.04-RR, 1,
					0, b-0.04-RR, 1,
					0, RR, -1

			mul2 -1, 1
			add2 -A, 0
		next i
		del top
	endif
return


! -----------------------------------------------------------------------------
! Sofa Style 6
! -----------------------------------------------------------------------------

6:

	if det2D = 3 then

		tt = 0.20           ! Thickness Arm
		er = 0.03           ! Edge Radius
		cr = 0.03           ! Corner Radius Arm
		cri = 0.25          ! Corner Radius In
		cro = cri+tt-2*er   ! Corner Radius Out

		add2 a/2, 0

		mm=1
		for ii = 1 TO 2
			poly2_ 10, 1,
				0, b-tt, 1,
				mm*(a/2-tt-cri+er), b-tt, 1,
				mm*(a/2-tt), b-tt-cri+er, 1001,
				mm*(a/2-tt), er+cr, 1,
				mm*(a/2-tt+er+cr), 0, 1001,
				mm*(a/2-er-cr), 0, 1,
				mm*a/2, er+cr, 1001,
				mm*a/2, b-cro-er, 1,
				mm*(a/2-cro-er), b, 1001,
				0, b, 1

			poly2_ 4, 1,
				mm*(a/2-tt), er+cr, 1,
				mm*(a/2-tt), 0.04, 1,
				mm*(a/2-tt-0.04), 0, 1001,
				0, 0, 1
		mm=-1
		next ii

		add2 tt-a/2, 0
		for ii=1 TO nSeats
			aa=(a-tt*2)/nSeats
			add2 aa*(ii-1), 0
			del 1
		next ii
		del 1


		if nSeats = 2 and gs_2DSeat then
			mm = 1
			gosub "sofastyle67_100"
			mm = -1
			gosub "sofastyle67_100"
		endif


		if nSeats = 3 and gs_2DSeat then
			add2 (a-0.20*2)/6, 0
			mm = 1
			gosub "sofastyle67_100"
			mm = -1
			gosub "sofastyle67_100"
			del 1

			add2 -(a-0.20*2)/6, 0
			mm = 1
			gosub "sofastyle67_100"
			mm = -1
			gosub "sofastyle67_100"
			del 1
		endif

		!!! *** Background Fill
		fill gs_fill_type
		mm = 1
		for ii=1 TO 2
			poly2_B 11, 2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 0,
				mm*(a/2-tt-0.04), 0, 0,
				mm*(a/2-tt), 0.04, 1000,
				mm*(a/2-tt), er+cr, 0,
				mm*(a/2-tt+er+cr), er+cr, 900,
				mm*(a/2-tt+er+cr), 0, 3000,
				mm*(a/2-er-cr), 0, 0,
				mm*a/2, er+cr, 1000,
				mm*a/2, b-cro-er, 0,
				mm*(a/2-cro-er), b, 1000,
				0, b, 0
		mm = -1
		next ii
		del 1
	endif

return

! -----------------------------------------------------------------------------
! Sofa Style 7
! -----------------------------------------------------------------------------

7:

	if det2D = 3 then

		tt = 0.25           ! Arm Thickness
		cri = 0.18          ! Corner Radius In

		add2 A/2, 0

		mm=1
		for ii = 1 TO 2
			poly2_ 8, 1,
				0, b-tt, 1,
				mm*(a/2-tt-cri), b-tt, 1,
				mm*(a/2-tt), b-tt-cri, 1001,
				mm*(a/2-tt), 0.03, 1,
				mm*a/2, 0.03, 1,
				mm*a/2, b-tt-cri, 1,
				mm*(a/2-tt-cri), b, 1001,
				0, b, 1
			poly2_ 3, 1,
				0, 0, 1,
				mm*(a/2-tt-0.04), 0, 1,
				mm*(a/2-tt), 0.04, 1001
		mm=-1
		next ii

		add2 tt-a/2, 0
		for ii = 1 TO nSeats
			aa=(a-tt*2)/nSeats
			add2 aa*(ii-1), 0
			del 1
		next ii
		del 1

		if nSeats = 2 and gs_2DSeat then
			mm=1
			gosub "sofastyle67_100"
			mm=-1
			gosub "sofastyle67_100"
		endif


		if nSeats = 3 and gs_2DSeat then
			add2 (a-0.25*2)/6, 0
			mm=1
			gosub "sofastyle67_100"
			mm=-1
			gosub "sofastyle67_100"
			del 1

			add2 -(a-0.25*2)/6, 0
			mm=1
			gosub "sofastyle67_100"
			mm=-1
			gosub "sofastyle67_100"
			del 1
		endif

		!!! *** Background Fill
		mm=1
		for ii = 1 TO 2
			poly2_B 8, 2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 0,
				mm*(a/2-tt-0.04), 0, 0,
				mm*(a/2-tt), 0.04, 1000,
				mm*(a/2-tt), 0.03, 0,
				mm*a/2, 0.03, 0,
				mm*a/2, b-tt-cri, 0,
				mm*(a/2-tt-cri), b, 1000,
				0, b, 0

			mm=-1
		next ii
		del 1
	endif

return



! -----------------------------------------------------------------------------
! details for Sofa Style 6 and 7
! -----------------------------------------------------------------------------

"sofastyle67_100":
	poly2_ 5, 1,
		mm*0.04, 0, 1,
		mm*0.04, 0.04, 901,
		0, 0.04, 3001,
		0, b-tt-0.04, 1,
		mm*0.04, b-tt, 1001
return




