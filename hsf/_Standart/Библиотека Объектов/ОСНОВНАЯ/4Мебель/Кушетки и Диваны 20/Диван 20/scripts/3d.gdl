
if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then shadow off
if gs_detlevel_3D_m = 1 then gs_resol = 4
if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then gs_detlevel_3D_m = 1
_leg_real_h = bLeg * leg_h

! -----------------------------------------------------------------------------
! 3D Hotspots  A and B
! -----------------------------------------------------------------------------

unID = 1

hotspot  0, 0, 0, unID, A, 1+256 : unID = unID + 1
hotspot -1, 0, 0, unID, A, 3 : unID = unID + 1
hotspot  A, 0, 0, unID, A, 2 : unID = unID + 1

hotspot  0, B, 0, unID, A, 1+256 : unID = unID + 1
hotspot -1, B, 0, unID, A, 3 : unID = unID + 1
hotspot  A, B, 0, unID, A, 2 : unID = unID + 1

hotspot 0, 0, 0, unID, B, 1+256 : unID = unID + 1
hotspot 0,-1, 0, unID, B, 3 : unID = unID + 1
hotspot 0, B, 0, unID, B, 2 : unID = unID + 1

hotspot A, 0, 0, unID, B, 1+256 : unID = unID + 1
hotspot A,-1, 0, unID, B, 3 : unID = unID + 1
hotspot A, B, 0, unID, B, 2 : unID = unID + 1

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

pen gs_cont_pen
gosub iStyle


end

! =============================================================================
! SUBRUTINES
!
! =============================================================================



! -----------------------------------------------------------------------------
! Sofa type 1
! -----------------------------------------------------------------------------

1:
	add a/2, b/2, 0

	hotspot a/2-0.15, -b/2+b-yyy/tan (75)-0.1672+cos (75)*(yyy/sin (75)), 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot a/2-0.15, -b/2+b-yyy/tan (75)-0.1672+cos (75)*(yyy/sin (75)), -1, unID, zzyzx, 3: unID = unID + 1
	hotspot a/2-0.15, -b/2+b-yyy/tan (75)-0.1672+cos (75)*(yyy/sin (75)), zzyzx, unID, zzyzx, 2: unID = unID + 1

	hotspot -a/2+0.15, -b/2+b-yyy/tan (75)-0.1672+cos (75)*(yyy/sin (75)), 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot -a/2+0.15, -b/2+b-yyy/tan (75)-0.1672+cos (75)*(yyy/sin (75)), -1, unID, zzyzx, 3: unID = unID + 1
	hotspot -a/2+0.15, -b/2+b-yyy/tan (75)-0.1672+cos (75)*(yyy/sin (75)), zzyzx, unID, zzyzx, 2: unID = unID + 1

	! hotspot editing !
	if gs_full_edit = 1 then

		hotspot -a/2+0.15, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot -a/2+0.15, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot -a/2+0.15, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot a/2-0.15, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot a/2-0.15, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot a/2-0.15, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot -a/2, -b/2, 0, unID, arm_h, 1+128: unID = unID + 1
		hotspot -a/2, -b/2, -1, unID, arm_h, 3: unID = unID + 1
		hotspot -a/2, -b/2, arm_h, unID, arm_h, 2: unID = unID + 1

		hotspot a/2, -b/2, 0, unID, arm_h, 1+128: unID = unID + 1
		hotspot a/2, -b/2, -1, unID, arm_h, 3: unID = unID + 1
		hotspot a/2, -b/2, arm_h, unID, arm_h, 2: unID = unID + 1
	endif

	add -a/2, -b/2, 0

	if bLeg then
	gosub 100 + iLegType
	endif

	material gs_sofa_mat
	resol gs_resol

	add 0.15, 0, _leg_real_h

	roty 90
	prism_ 8, a-0.30,
		-0.02, 0.02, 15,
		-gs_seat_height+0.15+_leg_real_h, 0.02, 15,
		-gs_seat_height+0.15+_leg_real_h,  b-yyy/tan (75)-0.1672, 15,
		-(gs_seat_height-_leg_real_h)+0.0875,  b-yyy/tan (75)-0.1672, 15,
		-(gs_seat_height-_leg_real_h)+0.0875,  b-yyy/tan (75)-0.03, 15,
		-arm_h+_leg_real_h+0.02, b-yyy/tan (75)-0.03+(arm_h-gs_seat_height+0.0675)/tan (75), 15,
		-arm_h+_leg_real_h+0.02, b-0.02, 15,
		-0.02, b-0.02, 15
	del 1

	roty -90
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=arm_h-_leg_real_h,
		cush_b=b,cush_h=0.15,r1=0.02,r2=0.01,gs_resol=gs_resol,cush_mat=gs_sofa_mat
	del 1

	addx a-0.15
	roty -90
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=arm_h-_leg_real_h,
		cush_b=b,cush_h=0.15,r1=0.02,r2=0.01,gs_resol=gs_resol,cush_mat=gs_sofa_mat
	del 3

	add 0.15, 0, gs_seat_height-0.15
	for ii = 1 to nSeats
		addx (a-0.30)/nSeats*(ii-1)
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.30)/nSeats,
			cush_b=b-yyy/tan (75)-0.1672,
			cush_h=0.15,r1=0.02,r2=0.01,gs_resol=gs_resol,cush_mat=gs_cush_mat

		add 0, b-yyy/tan (75)-0.03, 0.15-0.08750
		rotx 75
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.30)/nSeats,
			cush_b=yyy/sin (75),cush_h=0.15,
			r1=0.02,r2=0.01,gs_resol=gs_resol,cush_mat=gs_cush_mat
		del 3
	next ii
	del TOP

return


! -----------------------------------------------------------------------------
! Sofa type 2
! -----------------------------------------------------------------------------

2:
	bb = b-((zzyzx-gs_seat_height+0.02)*cos (70)+0.17-0.02*sin (5))

	add a/2, b/2, 0

	hotspot a/2, -b/2+(zzyzx-gs_seat_height+0.02)*cos (70)+bb, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot a/2, -b/2+(zzyzx-gs_seat_height+0.02)*cos (70)+bb, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot a/2, -b/2+(zzyzx-gs_seat_height+0.02)*cos (70)+bb, zzyzx, unID, zzyzx, 2: unID = unID + 1

	hotspot -a/2, -b/2+(zzyzx-gs_seat_height+0.02)*cos (70)+bb, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot -a/2, -b/2+(zzyzx-gs_seat_height+0.02)*cos (70)+bb, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot -a/2, -b/2+(zzyzx-gs_seat_height+0.02)*cos (70)+bb, zzyzx, unID, zzyzx, 2: unID = unID + 1

	! hotspot editing !
	if gs_full_edit = 1 then

		hotspot -a/2+0.15, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot -a/2+0.15, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot -a/2+0.15, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot a/2-0.15, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot a/2-0.15, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot a/2-0.15, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot -a/2, -b/2+0.02, 0, unID, arm_h, 1+128: unID = unID + 1
		hotspot -a/2, -b/2+0.02, -1, unID, arm_h, 3: unID = unID + 1
		hotspot -a/2, -b/2+0.02, arm_h, unID, arm_h, 2: unID = unID + 1

		hotspot a/2, -b/2+0.02, 0, unID, arm_h, 1+128: unID = unID + 1
		hotspot a/2, -b/2+0.02, -1, unID, arm_h, 3: unID = unID + 1
		hotspot a/2, -b/2+0.02, arm_h, unID, arm_h, 2: unID = unID + 1
	endif

	add -a/2, -b/2, 0

	if bLeg then
	gosub 100 + iLegType
	endif

	material gs_sofa_mat
	resol gs_resol

	if gs_detlevel_3D_m = 2 then
		add 0.0001, bb, 0
		roty 90
		extrude 7, 0, 0, a-0.0002, 1+2+4+16+32,
			-gs_seat_height+0.15, 0, 0,
			-(gs_seat_height-0.02), 0, 1,
			-zzyzx+0.03*sin (70), (zzyzx-gs_seat_height+0.02-0.03*cos (70))*cos (70), 1,
			-zzyzx, (zzyzx-gs_seat_height+0.02)*cos (70)+0.03, 1001,
			-zzyzx, (zzyzx-gs_seat_height+0.02)*cos (70)+0.15, 1,
			-zzyzx+0.02*cos (5), (zzyzx-gs_seat_height+0.02)*cos (70)+0.17-0.02*sin (5), 1001,
			-gs_seat_height+0.15, (zzyzx-gs_seat_height+0.02)*cos (70)+0.17-(zzyzx-gs_seat_height+0.15)*sin (5), 1
		del 2
	else
		add 0, bb, 0
		lin_ 0, (arm_h-gs_seat_height+0.02)*cos (70), arm_h,   0.15, (arm_h-gs_seat_height+0.02)*cos (70), arm_h
		lin_ a-0.15, (arm_h-gs_seat_height+0.02)*cos (70), arm_h,   a, (arm_h-gs_seat_height+0.02)*cos (70), arm_h
		lin_ 0.15, 0, gs_seat_height-0.02,   0.15, (arm_h-gs_seat_height+0.02)*cos (70), arm_h
		lin_ a-0.15, 0, gs_seat_height-0.02,   a-0.15, (arm_h-gs_seat_height+0.02)*cos (70), arm_h

		roty 90
		prism_ 7, a,
			-gs_seat_height+0.15, 0, 10,
			-(gs_seat_height-0.02), 0, 8,
			-(arm_h), (arm_h-gs_seat_height+0.02)*cos (70), 13,
			-zzyzx, (zzyzx-gs_seat_height+0.02)*cos (70), 15,
			-zzyzx, (zzyzx-gs_seat_height+0.02)*cos (70)+0.17, 15,
			-gs_seat_height+0.15, (zzyzx-gs_seat_height+0.02)*cos (70)+0.17-(zzyzx-gs_seat_height+0.15)*sin (5), 8,
			-gs_seat_height+0.15, 0, -1
		del 2

	endif

	if gs_detlevel_3D_m = 2 then

		add 0, bb+((zzyzx-gs_seat_height+0.02)*cos (70)+0.17-(zzyzx-gs_seat_height+0.15)*sin (5)),gs_seat_height-0.15
		cutplane -95
		del 1

		add 0, 0.02, _leg_real_h
		rotx 90
		extrude 17, 0, 0, -b, 1+2+4+16+32,
			a-0.02, 0, 1,
			0.02, 0, 1,
			0, 0.02, 1000,
			0, arm_h-_leg_real_h-0.021, 0,
			0, arm_h-_leg_real_h-0.02, 1,
			0.02, arm_h-_leg_real_h, 1001,
			0.13, arm_h-_leg_real_h, 1,
			0.15, arm_h-_leg_real_h-0.02, 1000,
			0.15, gs_seat_height-0.15-_leg_real_h, 0,
			a-0.15, gs_seat_height-0.15-_leg_real_h, 0,
			a-0.15, arm_h-_leg_real_h-0.021, 0,
			a-0.15, arm_h-_leg_real_h-0.02, 1,
			a-0.13, arm_h-_leg_real_h, 1001,
			a-0.02, arm_h-_leg_real_h, 1,
			a, arm_h-_leg_real_h-0.02, 1000,
			a, 0.02, 1,
			a-0.02, 0, 1000
		del 2

		cutend

	else

		add 0, bb, 0
		roty 90
		cutpolya 6, 2, 0,
			-gs_seat_height+0.15, 0, 8,
			-(gs_seat_height-0.02), 0, 8,
			-zzyzx, (zzyzx-gs_seat_height+0.02)*cos (70), 8,
			-zzyzx, 1.00, 8,
			-gs_seat_height+0.15, 1.00, 8,
			-gs_seat_height+0.15, 0, -1
		del 2

		add 0, bb+((zzyzx-gs_seat_height+0.02)*cos (70)+0.17-(zzyzx-(gs_seat_height+_leg_real_h)/2)*sin (5)), (gs_seat_height+_leg_real_h)/2
			cutplane -95
		del 1

		add 0, 0.02, _leg_real_h
		rotx 90
		prism_ 9, -b,
			0, 0, 15,
			0, arm_h-_leg_real_h, 15,
			0.15, arm_h-_leg_real_h, 15,
			0.15, gs_seat_height-0.15-_leg_real_h, 15,
			a-0.15, gs_seat_height-0.15-_leg_real_h, 15,
			a-0.15, arm_h-_leg_real_h, 15,
			a, arm_h-_leg_real_h, 15,
			a, 0, 15,
			0, 0, -1
		del 1
		del 1

		cutend: cutend

	endif

	add 0.15, 0, gs_seat_height-0.15
	for ii=1 to nSeats
		addx (a-0.30)/nSeats*(ii-1)
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.30)/nSeats,cush_b=bb,
			cush_h=0.15,r1=0.02,r2=0.01,gs_resol=gs_resol,cush_mat=gs_cush_mat
		del 1
	next ii

	del TOP

return

! -----------------------------------------------------------------------------
! Sofa type 3
! -----------------------------------------------------------------------------

3:

	add a/2,b/2,0

	hotspot a/2, -b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot a/2, -b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot a/2, -b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot -a/2, -b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	! hotspot editing

	hotspot -a/2+0.22, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
	hotspot -a/2+0.22, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
	hotspot -a/2+0.22, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

	hotspot a/2-0.22, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
	hotspot a/2-0.22, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
	hotspot a/2-0.22, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

	add -a/2, -b/2, 0
	addx 0.22
	material gs_leg_mat
	if bLeg then
	gosub 100 + iLegType
	endif

	material gs_sofa_mat
	resol gs_resol
	addz _leg_real_h
	if gs_detlevel_3D_m = 2 then
		zz=sqr((zzyzx-_leg_real_h)^2+0.06^2)
		xx=0.06*0.02/zz
		yy=(zzyzx-_leg_real_h)*0.02/zz
		tube 10, 6, 1+2+4+16+32,
			0, 0, 1,
			0, zzyzx-_leg_real_h-0.16, 1,
			0.16, zzyzx-_leg_real_h, 1001,
			0.22-0.02, zzyzx-_leg_real_h, 1,
			0.22-xx, zzyzx-_leg_real_h-yy, 1001,
			0.22-xx, zzyzx-_leg_real_h-yy, 1,
			0.16+xx, 0+yy, 1,
			0.16-0.02, 0, 1001,
			0.16-0.02, 0, 1,
			0, 0, 1,

			0, -0.1, 0, 0,
			0, 0, 0, 0,
			0, b-0.22, 0, 0,
			a-0.22*2, b-0.22, 0, 0,
			a-0.22*2, 0, 0, 0,
			a-0.22*2, -0.1, 0, 0

	else
		tube 5, 6, 1+2+4+16+32,
			0, 0, 0,
			0, zzyzx-_leg_real_h, 0,
			0.22, zzyzx-_leg_real_h, 0,
			0.16, 0, 0,
			0, 0, 0,

			0, -0.1, 0, 0,
			0, 0, 0, 0,
			0, b-0.22, 0, 0,
			a-0.22*2, b-0.22, 0, 0,
			a-0.22*2, 0, 0, 0,
			a-0.22*2, -0.1, 0, 0

	endif

	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.22*2,
			cush_b=b-0.22,cush_h=gs_seat_height-_leg_real_h-0.15,r1=0.02,r2=0.02,gs_resol=gs_resol,cush_mat=gs_sofa_mat

	addz gs_seat_height-_leg_real_h-0.15
	for ii = 1 to nSeats
		addx (a-0.22*2)/nSeats*(ii-1)
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.22*2)/nSeats,
				cush_b=b-0.22,cush_h=0.15,r1=0.02,r2=0.02,gs_resol=gs_resol,cush_mat=gs_cush_mat
		del 1
	next ii
	del TOP
return

! -----------------------------------------------------------------------------
! Sofa type 4
! -----------------------------------------------------------------------------

4:

	resol gs_resol

	thick1 = 0.0588
	thick2 = thick1/5
	z1 = gs_seat_height-0.1909-thick1
	z0 = zzyzx*0.67
	b0 = b-thick1
	q1 = (z0-thick1)*0.35
	x1 = (z0-thick1)*tan(10)*(z0-thick1)/(z0-thick1-thick1*sin(10))
	x2 = (z0-thick1-thick2)*tan(10)*(z0-thick1-thick2)/(z0-thick1-thick2-(thick1+thick2)*sin(10))
	x3 = thick1/cos(10)+(z0-thick1)*tan(10)-(z0-thick1)*tan(12)
	x4 = thick1/cos(10)+(z0-thick1)*tan(10)
	a1 = (a-x4*2-q1-thick2*(nSeats+1))/nSeats

	add a/2, b/2, 0

	hotspot a/2, b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot a/2, b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot a/2, b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	hotspot -a/2, b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot -a/2, b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	! hotspot editing !
	if gs_full_edit=1 then
		hotspot -a/2+x4+q1/2, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot -a/2+x4+q1/2, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot -a/2+x4+q1/2, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot a/2-x4-q1/2, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot a/2-x4-q1/2, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot a/2-x4-q1/2, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1
	endif

	add -a/2, -b/2, 0

	! --- SIMPLE 3D ---

	if gs_detlevel_3D_m = 1 then
		material gs_cush_mat
		roty -90
		addz -x4-q1/2
		prism_ 7,-a+2*(x4+q1/2),
		 z1,0,10,
		 gs_seat_height,0,15,
		 gs_seat_height,b*0.7,15,
		 zzyzx,b*0.7,10,
		 zzyzx,b,10,
		 z1,b,10,
		 z1,0,-1
		prism_ 5,x4+q1/2,
		 z1,0,15,
		 zzyzx,0,15,
		 zzyzx,b,14,
		 z1,b,14,
		 z1,0,-1
		addz -a+2*(x4+q1/2)
		prism_ 5,-x4-q1/2,
		 z1,0,15,
		 zzyzx,0,15,
		 zzyzx,b,11,
		 z1,b,11,
		 z1,0,-1
		del 3

		add 0.1, thick1*2, 0
		gosub 4040
		addx a-0.2
		gosub 4040
		addy b-thick1*4
		gosub 4040
		addx -a+0.2
		gosub 4040
		del 5
		end
	endif

	! --- DETAILED 3D ---

	rotx 90
	addy z1+z0
	material gs_back_mat
	prism_ 13,-b0,
	 0,0,15,
	 x3,0,15,
	 x4,thick1-z0,15,
	 a-thick1/cos(10)-(z0-thick1)*tan(10),thick1-z0,15,
	 a-thick1/cos(10)-(z0-thick1)*tan(10)+(z0-thick1)*tan(12),0,15,
	 a,0,14,
	 a-x1,-x1/tan(10),78,
	 a-thick1/cos(10)-(z0-thick1)*tan(10),thick1-z0,914,
	 a-thick1/cos(10)-(z0-thick1)*tan(10),-z0,3078,
	 x4,-z0,78,
	 x4,thick1-z0,914,
	 x1,-x1/tan(10),3078,
	 0,0,-1

	addz -b0
	prism_ 9,-thick1,
		0,0,15,
		a,0,11,
		a-x1,-x1/tan(10),75,
		a-thick1/cos(10)-(z0-thick1)*tan(10),thick1-z0,911,
		a-thick1/cos(10)-(z0-thick1)*tan(10),-z0,3075,
		x4,-z0,75,
		x4,thick1-z0,911,
		x1,-x1/tan(10),3075,
		0,0,-1
	del 1

	! --- TRIM ---

	material gs_trim_mat
	prism_ 13, thick2,
		 -thick2,0,11,
		 x3,0,15,
		 x4,thick1-z0,15,
		 a-thick1/cos(10)-(z0-thick1)*tan(10),thick1-z0,15,
		 a-thick1/cos(10)-(z0-thick1)*tan(10)+(z0-thick1)*tan(12),0,11,
		 a+thick2,0,15,
		 a+thick2-x2,-x2/tan(10),79,
		 a-thick1/cos(10)-(z0-thick1)*tan(10),thick1-z0,915,
		 a-(thick1+thick2)/cos(10)-(z0-thick1-thick2)*tan(10),-z0-thick2,3079,
		 (thick1+thick2)/cos(10)+(z0-thick1-thick2)*tan(10),-z0-thick2,79,
		 x4,thick1-z0,915,
		 -thick2+x2,-x2/tan(10),3079,
		 -thick2,0,-1

	rotx -90
	prism_ 9, thick2,
		-thick2,-thick2,15,
		-thick2,b0+thick1+thick2,15,
		a+thick2,b0+thick1+thick2,15,
		a+thick2,-thick2,14,
		a-x3,-thick2,15,
		a-x3,b0,15,
		x3,b0,15,
		x3,-thick2,14,
		-thick2,-thick2,-1
	del 3

	material gs_cush_mat
	for xx=1 to nSeats
		add x4+thick2/2+q1/2+(a-x4*2-q1-thick2)/nSeats*(xx-1)+thick2/2,b0-q1*1.001,z1!+thick1*1.0
		gosub 4010
		gosub 4050
		gosub 4020
		del 1
	next xx

	add x4+q1/2,b0-q1*1.001,z1!+thick1*1.0
	gosub 4030
	gosub 4050
	addx a-x4*2-q1!-thick2
	mulX -1
	gosub 4030
	gosub 4050
	del 3

	add x4+q1/4,thick1*2,0
	gosub 4040
	addx a-x4*2-q1/2
	gosub 4040
	addy b-thick1*4
	gosub 4040
	addx x4*2+q1/2-a
	gosub 4040
	del 6

return

! -----------------------------------------------------------------------------
! Sofa type 5
! -----------------------------------------------------------------------------

5:

	add a/2, b/2, 0
	hotspot a/2-0.18, b/2-(0.19/sin (75))+0.03, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot a/2-0.18, b/2-(0.19/sin (75))+0.03, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot a/2-0.18, b/2-(0.19/sin (75))+0.03, zzyzx, unID, zzyzx, 2: unID = unID + 1

	hotspot -a/2+0.18, b/2-(0.19/sin (75))+0.03, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot -a/2+0.18, b/2-(0.19/sin (75))+0.03, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot -a/2+0.18, b/2-(0.19/sin (75))+0.03, zzyzx, unID, zzyzx, 2: unID = unID + 1

	! hotspot editing !
	if gs_full_edit=1 then
		hotspot -a/2+0.18, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot -a/2+0.18, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot -a/2+0.18, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot a/2-0.18, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot a/2-0.18, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot a/2-0.18, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot -a/2, -b/2+0.02, 0, unID, arm_h, 1+128: unID = unID + 1
		hotspot -a/2, -b/2+0.02, -1, unID, arm_h, 3: unID = unID + 1
		hotspot -a/2, -b/2+0.02, arm_h, unID, arm_h, 2: unID = unID + 1

		hotspot a/2, -b/2+0.02, 0, unID, arm_h, 1+128: unID = unID + 1
		hotspot a/2, -b/2+0.02, -1, unID, arm_h, 3: unID = unID + 1
		hotspot a/2, -b/2+0.02, arm_h, unID, arm_h, 2: unID = unID + 1
	endif

	add -a/2, -b/2, 0

	material gs_leg_mat
	if bLeg then
	gosub 100 + iLegType
	endif

	material gs_sofa_mat
	resol gs_resol*2

	r2 = SQR(0.09^2-0.03^2)

	add 0, b-(zzyzx-_leg_real_h-0.08)/tan (75), _leg_real_h
	cutplane 255

	rotx -15
	cutpolya 5, 2, 0,
		0.075, -0.04, 15,
		a-0.075, -0.04, 15,
		a-0.075, 1.00, 15,
		0.075, 1.00, 15,
		0.075, -0.04, -1
	del 2

	mm=1
	gosub 5000

	mm=-1
	addx a
	gosub 5000
	del 1

	cutend
	cutend

	add 0.075, b-(zzyzx-_leg_real_h-0.08)/tan (75), _leg_real_h
	rotx -15
	addy -0.04
	BLOCK a-0.15, 0.04, (zzyzx-_leg_real_h-0.08)/sin (75)
	del 3

	add 0.18, 0, _leg_real_h
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.36,
		cush_b=b-(zzyzx-_leg_real_h-0.08)/tan (75),cush_h=gs_seat_height-_leg_real_h-0.15,r1=0.02,r2=0.01,
		gs_resol=gs_resol,cush_mat=gs_sofa_mat
	del 1

	add 0.18, 0, gs_seat_height-0.15
	for ii=1 to nSeats
		addx (a-0.36)/nSeats*(ii-1)
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.36)/nSeats,
			cush_b=b-yyy/tan (75)-0.1672,
			cush_h=0.15,r1=0.02,r2=0.01,gs_resol=gs_resol,cush_mat=gs_cush_mat

		add 0, b-yyy/tan (75)-0.02915, 0.15-0.0875
		rotx 75
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.36)/nSeats,
			cush_b=yyy/sin (75),cush_h=0.15,
			r1=0.02,r2=0.01,gs_resol=gs_resol,cush_mat=gs_cush_mat
		del 3
	next ii
	del TOP
return

! -----------------------------------------------------------------------------
! Sofa type 6
! -----------------------------------------------------------------------------

6:
	tt = 0.20           ! Thickness Arm
	hh = zzyzx-_leg_real_h     ! Height
	er = 0.03           ! Edge Radius
	cr = 0.03           ! Corner Radius Arm
	cri = 0.25          ! Corner Radius In
	cro = cri+tt-2*er   ! Corner Radius Out

	add a/2, b/2, 0
	hotspot a/2, -b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot a/2, -b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot a/2, -b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot -a/2, -b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	! hotspot editing !
	if gs_full_edit=1 then
		hotspot -a/2+0.2, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot -a/2+0.2, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot -a/2+0.2, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot a/2-0.2, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot a/2-0.2, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot a/2-0.2, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1
	endif

	add -a/2, -b/2, 0

	material gs_leg_mat
	if bLeg then
	gosub 100 + iLegType
	endif

	material gs_sofa_mat
	resol gs_resol

	addz _leg_real_h
	if gs_detlevel_3D_m = 2 then
		addx a/2
		mm=1
		gosub 6000
		gosub 6010

		mm=-1
		gosub 6000
		gosub 6010
		del 1
	else
		prism_ 9, hh,
			0, 0, 15,
			tt, 0, 15,
			tt, b-tt, 15,
			a-tt, b-tt, 15,
			a-tt, 0, 15,
			a, 0, 15,
			a, b, 15,
			0, b, 15,
			0, 0, -1
	endif

	addx 0.20

	gosub 6020
	addy -0.20
	r1=0.21
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.20*2,cush_b=b,
								 cush_h=gs_seat_height-0.15-_leg_real_h,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_sofa_mat
	del 1
	cutend

	gosub 6030
	r1=0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.20*2,cush_b=b-0.20,
								 cush_h=gs_seat_height-_leg_real_h-0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_sofa_mat
	cutend

	addz gs_seat_height-_leg_real_h-0.15
	if nSeats = 1 then
		gosub 6020
		addy -0.20
		r1=0.20
		r2=0.02
		if gs_detlevel_3D_m = 1 then
			r1=0
			r2=0
		endif
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.20*2,cush_b=b,
									 cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
		del 1
		cutend

		gosub 6030
		r1=0.02
		r2=0.02
		if gs_detlevel_3D_m = 1 then
			r1=0
			r2=0
		endif
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.20*2,cush_b=b-0.20,
									cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
		cutend
	endif


	if nSeats = 2 then
		i=2
		gosub 6050
		add a-0.20*2, 0, 0.15
		roty 180
		gosub 6050
		del 2
	endif


	if nSeats = 3 then
		i=3
		gosub 6050
		addx (a-0.20*2)/i
		r1=0.02
		r2=0.02
		if gs_detlevel_3D_m=1 then
			r1=0
			r2=0
		endif
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.20*2)/3,cush_b=b-0.20,
									 cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
		add 2*(a-0.20*2)/i, 0, 0.15
		roty 180
		gosub 6050
		del 3
	endif

	del TOP

return

! -----------------------------------------------------------------------------
! Sofa type 7
! -----------------------------------------------------------------------------

7:

	tt = 0.25          ! Arm Thickness
	cri = 0.18          ! Corner Radius In

	add a/2, b/2, 0

	hotspot a/2, -b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot a/2, -b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot a/2, -b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128: unID = unID + 1
	hotspot -a/2, -b/2, -1, unID, zzyzx, 3: unID = unID + 1
	hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2: unID = unID + 1

	! hotspot editing !
	if gs_full_edit = 1 then
		hotspot -a/2+0.25, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot -a/2+0.25, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot -a/2+0.25, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1

		hotspot a/2-0.25, -b/2, 0, unID, gs_seat_height, 1+128: unID = unID + 1
		hotspot a/2-0.25, -b/2, -1, unID, gs_seat_height, 3: unID = unID + 1
		hotspot a/2-0.25, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID = unID + 1
	endif

	add -a/2, -b/2, 0

	pen gs_cont_pen
	material gs_leg_mat
	if bLeg then
	gosub 100 + iLegType
	endif

	material gs_sofa_mat
	resol gs_resol*2

	addz _leg_real_h
	if gs_detlevel_3D_m = 2 then
		addx a/2
		mm=1
		gosub 7000
		mm=-1
		gosub 7000
		del 1
	else

	prism_ 9, zzyzx-_leg_real_h,
		0, 0, 15,
		tt, 0, 15,
		tt, b-tt, 15,
		a-tt, b-tt, 15,
		a-tt, 0, 15,
		a, 0, 15,
		a, b, 15,
		0, b, 15,
		0, 0, -1
	endif

	addx 0.25

	gosub 7020
	addy -0.25
	r1=cri-0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.25*2,cush_b=b,
								 cush_h=gs_seat_height-_leg_real_h-0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_sofa_mat
	del 1
	cutend

	gosub 7030
	r1=0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.25*2,cush_b=b-0.25,
								cush_h=gs_seat_height-_leg_real_h-0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_sofa_mat
	cutend


	addz gs_seat_height-_leg_real_h-0.15
	if nSeats = 1 then
		gosub 7020
		addy -0.25
		r1=cri-0.02
		r2=0.02
		if gs_detlevel_3D_m = 1 then
			r1=0
			r2=0
		endif
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.25*2,cush_b=b,
									cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
		del 1
		cutend

		gosub 7030
		r1=0.02
		r2=0.02
		if gs_detlevel_3D_m = 1 then
			r1=0
			r2=0
		endif
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.25*2,cush_b=b-0.25,
									cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
		cutend
	endif

	if nSeats = 2 then
		i=2
		gosub 7050
		add a-0.25*2, 0, 0.15
		roty 180
		gosub 7050
		del 2
	endif


	if nSeats = 3 then
		i=3
		gosub 7050
		addx (a-0.25*2)/i
		r1=0.02
		r2=0.02
		if gs_detlevel_3D_m = 1 then
			r1=0
			r2=0
		endif
		call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.25*2)/3,cush_b=b-0.25,
									 cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
		add 2*(a-0.25*2)/i, 0, 0.15
		roty 180
		gosub 7050
		del 3
	endif

	del TOP
return


! -----------------------------------------------------------------------------
! LEG TYPES  0, 1-5  (100, 101 - 105)
! -----------------------------------------------------------------------------

101:
	if iStyle = 1 then : xDiff = 0.0525 : yDiff = 0.02 : aLegPos = a - 0.105 : bLegPos = b - 0.04  : endif
	if iStyle = 2 then : xDiff = 0.0525 : yDiff = 0.02 : aLegPos = a - 0.105 : bLegPos = b - 0.085 : endif
	if iStyle = 3 then : xDiff = -0.1025 : yDiff = 0.00 : aLegPos = a - 0.235 : bLegPos = b - 0.06 : endif
	if iStyle = 5 then : xDiff = 0.090 : yDiff = 0.04 : aLegPos = a - 0.180 : bLegPos = b - (zzyzx-_leg_real_h-0.08)/tan (75)-0.06 : endif
	if iStyle = 6 then : xDiff = 0.110 : yDiff = 0.02 : aLegPos = a - 0.22 : bLegPos = b - 0.18 : endif
	if iStyle = 7 then : xDiff = 0.170 : yDiff = 0.03 : aLegPos = a - 0.34 : bLegPos = b - 0.26 : endif
	add xDiff, yDiff, 0
	call "fa_sofalegtyp" PARAMETERS sofaleg_typ_m = iLegType, a = aLegPos, b = bLegPos, gs_detlevel_3D_m=gs_detlevel_3D_m,
									gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
	del 1
return

102:
	if iStyle = 1 then : xDiff = 0.06 : yDiff = 0.04 : aLegPos = a - 0.12 : bLegPos = b - 0.08  : endif
	if iStyle = 2 then : xDiff = 0.06 : yDiff = 0.04 : aLegPos = a - 0.12 : bLegPos = b - 0.125 : endif
	if iStyle = 3 then : xDiff = -0.06 : yDiff = 0.02 : aLegPos = a - 0.32 : bLegPos = b - 0.10 : endif
	if iStyle = 5 then : xDiff = 0.105 : yDiff = 0.04 : aLegPos = a - 0.21 : bLegPos = b - (zzyzx-_leg_real_h-0.08)/tan (75)-0.06 : endif
	if iStyle = 6 then : xDiff = 0.140 : yDiff = 0.02 : aLegPos = a - 0.28 : bLegPos = b - 0.16 : endif
	if iStyle = 7 then : xDiff = 0.180 : yDiff = 0.03 : aLegPos = a - 0.36 : bLegPos = b - 0.27 : endif
	add xDiff, yDiff, 0
	call "fa_sofalegtyp" PARAMETERS sofaleg_typ_m = iLegType, a = aLegPos, b = bLegPos, _leg_real_h=_leg_real_h, gs_detlevel_3D_m=gs_detlevel_3D_m,
									gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
	del 1
return

103:
	if iStyle = 1 then : xDiff = 0.075 : yDiff = 0.075 : aLegPos = a - 0.15 : bLegPos = b - 0.15  : endif
	if iStyle = 2 then : xDiff = 0.075 : yDiff = 0.075 : aLegPos = a - 0.15 : bLegPos = b - 0.195 : endif
	if iStyle = 3 then : xDiff = -0.080 : yDiff = 0.060 : aLegPos = a - 0.28 : bLegPos = b - 0.18 : endif
	if iStyle = 5 then : xDiff = 0.120 : yDiff = 0.075 : aLegPos = a - 0.24 : bLegPos = b - (zzyzx-_leg_real_h-0.08)/tan (75)-0.128 : endif
	if iStyle = 6 then : xDiff = 0.140 : yDiff = 0.060 : aLegPos = a - 0.28 : bLegPos = b - 0.25 : endif
	if iStyle = 7 then : xDiff = 0.200 : yDiff = 0.060 : aLegPos = a - 0.40 : bLegPos = b - 0.32 : endif
	add xDiff, yDiff, 0
	call "fa_sofalegtyp" PARAMETERS sofaleg_typ_m = iLegType, a = aLegPos, b = bLegPos, _leg_real_h=_leg_real_h, gs_detlevel_3D_m=gs_detlevel_3D_m,
									gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
	del 1
return

104:
	if iStyle = 1 then : xDiff = 0.02 : yDiff = 0.075 : aLegPos = a - 0.04 : bLegPos = b - 0.15  : endif
	if iStyle = 2 then : xDiff = 0.02 : yDiff = 0.075 : aLegPos = a - 0.04 : bLegPos = b - 0.195 : endif
	if iStyle = 3 then : xDiff = -0.160 : yDiff = 0.040 : aLegPos = a - 0.12 : bLegPos = b - 0.140 : endif
	if iStyle = 4 then : xDiff = 0.070 : yDiff = 0.070 : aLegPos = a - 0.14 : bLegPos = b - (zzyzx-_leg_real_h-0.08)/tan (75)-0.12 : endif
	if iStyle = 5 then : xDiff = 0.070 : yDiff = 0.070 : aLegPos = a - 0.14 : bLegPos = b-(zzyzx-_leg_real_h-0.08)/tan (75)-0.12 : endif
	if iStyle = 6 then : xDiff = 0.080 : yDiff = 0.060 : aLegPos = a - 0.16 : bLegPos = b - 0.27 : endif
	if iStyle = 7 then : xDiff = 0.150 : yDiff = 0.060 : aLegPos = a - 0.30 : bLegPos = b - 0.32 : endif
	add xDiff, yDiff, 0
	call "fa_sofalegtyp" PARAMETERS sofaleg_typ_m = iLegType, a = aLegPos, b = bLegPos, _leg_real_h=_leg_real_h, gs_detlevel_3D_m=gs_detlevel_3D_m,
									gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
	del 1
return

105:
	if iStyle = 1 then : xDiff = 0.075 : yDiff = 0.085 : aLegPos = a - 0.15 : bLegPos = b - 0.17  : endif
	if iStyle = 2 then : xDiff = 0.075 : yDiff = 0.085 : aLegPos = a - 0.15 : bLegPos = b - 0.215 : endif
	if iStyle = 3 then : xDiff = -0.080 : yDiff = 0.080 : aLegPos = a - 0.28 : bLegPos = b - 0.220 : endif
	if iStyle = 5 then : xDiff = 0.130 : yDiff = 0.085 : aLegPos = a - 0.26 : bLegPos = b - (zzyzx-_leg_real_h-0.08)/tan (75)-0.145 : endif
	if iStyle = 6 then : xDiff = 0.130 : yDiff = 0.080 : aLegPos = a - 0.26 : bLegPos = b - 0.33 : endif
	if iStyle = 7 then : xDiff = 0.195 : yDiff = 0.080 : aLegPos = a - 0.39 : bLegPos = b - 0.40 : endif
	add xDiff, yDiff, 0
	call "fa_sofalegtyp" PARAMETERS sofaleg_typ_m = iLegType, a = aLegPos, b = bLegPos, leg_h=_leg_real_h, ur=0.055, lr=0.045,
									gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
	del 1
return


! -----------------------------------------------------------------------------
!  for Sofa Style 4     4010 - 4050
! -----------------------------------------------------------------------------

4010:     !!!!!! BACK PANEL !!!!!

	material gs_cush_mat
	addz gs_seat_height-z1-0.1909 !!!!!!!!!!!!!!!!!!
	if abs (xx-1) > eps AND abs (xx-nSeats) > eps then a01=a1 else a01 = a1+q1/2+x4-x3*1.1
	if nSeats = 1 then a01=a01+q1/2+x4-x3*1.1
	ROTZ 90
	rotx 90
	addz -q1/2.1
	if abs (xx-1) < eps then cutplane 168
	del 1
	addz a01-x4+x3*1.1-(q1/2+x4-x3*1.1)*not(nSeats-1)-thick2/2
	if abs (xx-nSeats) < eps then cutplane 12
	del 1
	mul q1/0.1285,(zzyzx-gs_seat_height+0.1909)/0.365,1
	if abs (xx-1) < eps then addz -q1/2+x3*1.1-x4
	prism_ 18,a01,
	 0.128,0.01825,79,
	 0.128,0.34675,79,
	 0.107748,0.344748,979,
	 0.0, 78.7097,4079,
	 0.10975,0.365,79,
	 0.054296,0.365,79,
	 0.056077,0.344962,979,
	 0.0, 69.8383,4079,
	 0.036653,0.350196,79,
	 -0.000015,0.131182,79,
	 -0.0,0.01825,79,
	 0.020239,0.020239,979,
	 0.0, 78.777,4079,
	 0.01825,0.0,79,
	 0.10975,0.0,79,
	 0.107999,0.020001,979,
	 0.0, 79.9949,4079,
	 0.128,0.01825,-1
	if abs (xx-1) < eps OR abs (xx-nSeats) < eps then cutend
	if abs (xx-nSeats) < eps and abs (nSeats-1) < eps then cutend
	del 3
	if abs (xx-1) < eps then del 1
	del 1 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
return

4020:       !!!!! SEAT PANEL !!!!!

	material gs_cush_mat
	addz gs_seat_height-z1-0.1909
	ROTZ 90
	rotx 90
	mul (b0-q1)/0.287,0.1909/0.121,1
	prism_ 17,a1,
		 -0.27875,-0.0,79,
		 -0.01825,-0.0,79,
		 -0.020239,0.020239,979,
		  0.0,78.777,4079,
		 -0.0,0.01825,79,
		 -0.0,0.10275,79,
		 -0.02,0.101,979,
		  0.0,80.0002,4079,
		 -0.01825,0.121,79,
		 -0.27875,0.121,79,
		 -0.276778,0.100778,979,
		  0.0,78.861,4079,
		 -0.297,0.10275,79,
		 -0.297,0.01825,79,
		 -0.277,0.02,979,
		  0.0,80.0002,4079,
		 -0.27875,-0.0,-1
	del 4
return

4030:        !!!!! SIDE PANEL !!!!!

	addz gs_seat_height-0.1909-z1
	rotx 90
	mul q1/2/0.102,(z0-thick1)/0.554,1
	prism_ 19,b0-q1*0.99,
		 -0.0,0.01825,79,
		  0.0,0.220419,79,
		 -0.013,0.264243,79,
		 -0.155273,0.544479,79,
		 -0.172098,0.534095,979,
		  0.0,58.9478,4079,
		 -0.172315,0.553865,79,
		 -0.202779,0.553865,79,
		 -0.201927,0.543914,979,
		  0.0,97.4037,4079,
		 -0.211685,0.541786,79,
		 -0.098027,0.007065,79,
		 -0.088294,0.010171,979,
		  0.0,68.6036,4079,
		 -0.088953,-0.000024,79,
		 -0.01825,-0.0,79,
		 -0.02,0.02,979,
		  0.0,80.0002,4079,
		 -0.0,0.01825,-1
	del 3
return

4040:			!!!!! LEG !!!!!

	material gs_leg_mat
	cone z1*0.18,thick1*0.4,thick1*0.3,90,90
	material gs_trim_mat
	addz z1*0.18
	cone z1*0.82,thick1*0.3,thick1*0.54,90,90
	del 1

return

4050:
	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	base
	body -1
	del 1
return

! -----------------------------------------------------------------------------
!  for Sofa Style 5     5000
! -----------------------------------------------------------------------------

5000:
	rotx -90
	if gs_detlevel_3D_m = 2 then
		addz 0.02
		extrude 6, 0, 0,  b, 1+2+4+16+32,
			mm*0.075, -_leg_real_h, 1,
			mm*0.18, -_leg_real_h, 0,
			mm*0.18, -(arm_h-0.09), 1,
			mm*0.06, -(arm_h-0.09-r2), 1000,
			mm*0.06, -0.015-_leg_real_h, 1,
			mm*0.075, -_leg_real_h, 1001
		del 1
	else
		prism_ 7, b,
			mm*0.06, -_leg_real_h, 15,
			mm*0.18, -_leg_real_h, 15,
			mm*0.18, -arm_h, 15,
			0, -arm_h, 15,
			0, -arm_h+0.09+r2, 15,
			mm*0.06, -arm_h+0.09+r2, 15,
			mm*0.06, -_leg_real_h, -1

	endif
	del 1
return

! -----------------------------------------------------------------------------
!  for Sofa Style 6     6000 - 6050
! -----------------------------------------------------------------------------

6000:
	tube 6, 35, 1+2,
		-0.0001*mm, 0.0, 0,
		0.0, 0.0, 1,
		er*mm, er, 1001,
		er*mm, hh-er, 1,
		0.0, hh, 1001,
		-0.0001*mm, hh, 0,

		-a/2*mm, b-er, 0, 0,
		0, b-er, 0, 0,
		(a/2-er-cro)*mm, b-er, 0, 0, !
		(a/2-er-cro+cro*sin (10))*mm, b-er-cro+cro*cos (10), 0, 0,
		(a/2-er-cro+cro*sin (20))*mm, b-er-cro+cro*cos (20), 0, 0,
		(a/2-er-cro+cro*sin (30))*mm, b-er-cro+cro*cos (30), 0, 0,
		(a/2-er-cro+cro*sin (40))*mm, b-er-cro+cro*cos (40), 0, 0,
		(a/2-er-cro+cro*sin (50))*mm, b-er-cro+cro*cos (50), 0, 0,
		(a/2-er-cro+cro*sin (60))*mm, b-er-cro+cro*cos (60), 0, 0,
		(a/2-er-cro+cro*sin (70))*mm, b-er-cro+cro*cos (70), 0, 0,
		(a/2-er-cro+cro*sin (80))*mm, b-er-cro+cro*cos (80), 0, 0,
		(a/2-er)*mm, b-er-cro, 0, 0, !
		(a/2-er)*mm, er+cr, 0, 0, !
		(a/2-er-cr+cr*cos (15))*mm, er+cr-cr*sin (15), 0, 0,
		(a/2-er-cr+cr*cos (30))*mm, er+cr-cr*sin (30), 0, 0,
		(a/2-er-cr+cr*cos (45))*mm, er+cr-cr*sin (45), 0, 0,
		(a/2-er-cr+cr*cos (60))*mm, er+cr-cr*sin (60), 0, 0,
		(a/2-er-cr+cr*cos (75))*mm, er+cr-cr*sin (75), 0, 0,
		(a/2-er-cr)*mm, er, 0, 0, !
		(a/2-tt+er+cr)*mm, er, 0, 0, !
		(a/2-tt+er+cr-cr*sin (15))*mm, er+cr-cr*cos (15), 0, 0,
		(a/2-tt+er+cr-cr*sin (30))*mm, er+cr-cr*cos (30), 0, 0,
		(a/2-tt+er+cr-cr*sin (45))*mm, er+cr-cr*cos (45), 0, 0,
		(a/2-tt+er+cr-cr*sin (60))*mm, er+cr-cr*cos (60), 0, 0,
		(a/2-tt+er+cr-cr*sin (75))*mm, er+cr-cr*cos (75), 0, 0,
		(a/2-tt+er)*mm, er+cr, 0, 0, !
		(a/2-tt+er)*mm, b-tt+er-cri, 0, 0, !
		(a/2-tt+er-cri+cri*cos (15))*mm, b-tt+er-cri+cri*sin (15), 0, 0,
		(a/2-tt+er-cri+cri*cos (30))*mm, b-tt+er-cri+cri*sin (30), 0, 0,
		(a/2-tt+er-cri+cri*cos (45))*mm, b-tt+er-cri+cri*sin (45), 0, 0,
		(a/2-tt+er-cri+cri*cos (60))*mm, b-tt+er-cri+cri*sin (60), 0, 0,
		(a/2-tt+er-cri+cri*cos (75))*mm, b-tt+er-cri+cri*sin (75), 0, 0,
		(a/2-tt+er-cri)*mm, b-tt+er, 0, 0, !
		0, b-tt+er, 0, 0,
		-a/2*mm, b-tt+er, 0, 0
return

6010:
	prism_ 34, hh,
		0, b-er, 8,
		(a/2-er-cro)*mm, b-er, 8,
		(a/2-er-cro+cro*sin (10))*mm, b-er-cro+cro*cos (10), 8,
		(a/2-er-cro+cro*sin (20))*mm, b-er-cro+cro*cos (20), 8,
		(a/2-er-cro+cro*sin (30))*mm, b-er-cro+cro*cos (30), 8,
		(a/2-er-cro+cro*sin (40))*mm, b-er-cro+cro*cos (40), 8,
		(a/2-er-cro+cro*sin (50))*mm, b-er-cro+cro*cos (50), 8,
		(a/2-er-cro+cro*sin (60))*mm, b-er-cro+cro*cos (60), 8,
		(a/2-er-cro+cro*sin (70))*mm, b-er-cro+cro*cos (70), 8,
		(a/2-er-cro+cro*sin (80))*mm, b-er-cro+cro*cos (80), 8,
		(a/2-er)*mm, b-er-cro, 8,
		(a/2-er)*mm, er+cr, 8,
		(a/2-er-cr+cr*cos (15))*mm, er+cr-cr*sin (15), 8,
		(a/2-er-cr+cr*cos (30))*mm, er+cr-cr*sin (30), 8,
		(a/2-er-cr+cr*cos (45))*mm, er+cr-cr*sin (45), 8,
		(a/2-er-cr+cr*cos (60))*mm, er+cr-cr*sin (60), 8,
		(a/2-er-cr+cr*cos (75))*mm, er+cr-cr*sin (75), 8,
		(a/2-er-cr)*mm, er, 8,
		(a/2-tt+er+cr)*mm, er, 8,
		(a/2-tt+er+cr-cr*sin (15))*mm, er+cr-cr*cos (15), 8,
		(a/2-tt+er+cr-cr*sin (30))*mm, er+cr-cr*cos (30), 8,
		(a/2-tt+er+cr-cr*sin (45))*mm, er+cr-cr*cos (45), 8,
		(a/2-tt+er+cr-cr*sin (60))*mm, er+cr-cr*cos (60), 8,
		(a/2-tt+er+cr-cr*sin (75))*mm, er+cr-cr*cos (75), 8,
		(a/2-tt+er)*mm, er+cr, 8,
		(a/2-tt+er)*mm, b-tt+er-cri, 8,
		(a/2-tt+er-cri+cri*cos (15))*mm, b-tt+er-cri+cri*sin (15), 8,
		(a/2-tt+er-cri+cri*cos (30))*mm, b-tt+er-cri+cri*sin (30), 8,
		(a/2-tt+er-cri+cri*cos (45))*mm, b-tt+er-cri+cri*sin (45), 8,
		(a/2-tt+er-cri+cri*cos (60))*mm, b-tt+er-cri+cri*sin (60), 8,
		(a/2-tt+er-cri+cri*cos (75))*mm, b-tt+er-cri+cri*sin (75), 8,
		(a/2-tt+er-cri)*mm, b-tt+er, 8,
		0, b-tt+er, 8,
		0, b-er, -1
return

6020:
	cutpolya 5, 2, 0,
		0, -b, 8,
		a, -b, 8,
		a, 0.06, 8,
		0, 0.06, 8,
		0, -b, -1
return

6030:
	cutpolya 5, 2, 0,
		0, 0.06, 8,
		a, 0.06, 8,
		a, b, 8,
		0, b, 8,
		0, 0.06, -1
return

6040:
	cutpolya 5, 2, 0,
		0, -0.10, 8,
		(a-0.20*2)/i-0.06, -0.10, 8,
		(a-0.20*2)/i-0.06, b, 8,
		0, b, 8,
		0, -0.10, -1
return

6050:
	addx (a-0.20*2)/i-0.06
	gosub 6040
	del 1
	gosub 6020
	addy -0.20
	r1=0.21
	r2=0.02
	if gs_detlevel_3D_m=1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.20*2)/i+0.20,cush_b=b,
								 cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
	del 1
	cutend

	gosub 6030
	r1=0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.20*2)/i+0.20,cush_b=b-0.20,
								 cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
	cutend
	cutend

	gosub 6040
	r1=0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.20*2)/i,cush_b=b-0.20,
								 cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
	cutend
return

! -----------------------------------------------------------------------------
!  for Sofa Style 7    7000 - 7050
! -----------------------------------------------------------------------------

7000:
	tube 10, 14, 1+2+32,
		mm*0.05, 0, 1,
		mm*0.02, 0, 1,
		0, 0.02, 1001,
		0, gs_seat_height-_leg_real_h, 1,
		mm*0.02, zzyzx-_leg_real_h-0.16, 1,
		mm*0.22, zzyzx-_leg_real_h, 1001,
		mm*0.23, zzyzx-_leg_real_h, 1,
		mm*0.2494, zzyzx-_leg_real_h-0.025, 1001,
		mm*0.15, 0.02, 1,
		mm*0.125, 0, 1001,

		-a/2*mm, b-0.25, 0, 0,
		0, b-0.25, 0, 0,
		(a/2-0.25-cri)*mm, b-0.25, 0, 0, !
		(a/2-0.25-cri+cri*sin (10))*mm, b-0.25-cri+cri*cos (10), 0, 0,
		(a/2-0.25-cri+cri*sin (20))*mm, b-0.25-cri+cri*cos (20), 0, 0,
		(a/2-0.25-cri+cri*sin (30))*mm, b-0.25-cri+cri*cos (30), 0, 0,
		(a/2-0.25-cri+cri*sin (40))*mm, b-0.25-cri+cri*cos (40), 0, 0,
		(a/2-0.25-cri+cri*sin (50))*mm, b-0.25-cri+cri*cos (50), 0, 0,
		(a/2-0.25-cri+cri*sin (60))*mm, b-0.25-cri+cri*cos (60), 0, 0,
		(a/2-0.25-cri+cri*sin (70))*mm, b-0.25-cri+cri*cos (70), 0, 0,
		(a/2-0.25-cri+cri*sin (80))*mm, b-0.25-cri+cri*cos (80), 0, 0,
		(a/2-0.25)*mm, b-0.25-cri, 0, 0, !
		(a/2-0.25)*mm, 0.03, 0, 0, !
		(a/2-0.25)*mm, 0, 0, 0
return

7020:
	cutpolya 5, 2, 0,
		0, -b, 8,
		a, -b, 8,
		a, 0.06, 8,
		0, 0.06, 8,
		0, -b, -1
return

7030:
	cutpolya 5, 2, 0,
		0, 0.06, 8,
		a, 0.06, 8,
		a, b, 8,
		0, b, 8,
		0, 0.06, -1
return

7040:
	cutpolya 5, 2, 0,
		0, -0.10, 8,
		(a-0.25*2)/i-0.06, -0.10, 8,
		(a-0.25*2)/i-0.06, b, 8,
		0, b, 8,
		0, -0.10, -1
return

7050:
	addx (a-0.25*2)/i-0.06
	gosub 7040
	del 1
	gosub 7020
	addy -0.25
	r1=cri-0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.25*2)/i+0.20,cush_b=b,
								cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
	del 1
	cutend

	gosub 7030
	r1=0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.25*2)/i+0.20,cush_b=b-0.25,
								cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
	cutend
	cutend

	gosub 7040
	r1=0.02
	r2=0.02
	if gs_detlevel_3D_m = 1 then
		r1=0
		r2=0
	endif
	call "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=(a-0.25*2)/i,cush_b=b-0.25,
								cush_h=0.15,r1=r1,r2=r2,gs_resol=gs_resol,cush_mat=gs_cush_mat
	cutend
return

