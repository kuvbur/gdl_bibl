
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 				= -1
_idxTab = 1
TABID_Style				= _idxTab  : _idxTab = _idxTab + 1
TABID_DimMinSpace		= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions		= _idxTab  : _idxTab = _idxTab + 1

pageStartY = 0
pageStartX = 0

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Дивана`
ui_current_page gs_ui_current_page

ui_page TABID_Style, TABID_Root, `Стиль и Ножки`, "uiTab_Dimension_flat(1)"
	gosub "StylePage"

ui_page TABID_DimMinSpace, TABID_Root, `Размеры и Минимальное Пространство`, "uiTab_Style_flat(1)"
	gosub "DimMinSpacePage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationPage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StylePage":
! ------------------------------------------------------------------------------
	_outfHeight = 15
	_yOffset = 4
	_yCurrent	= pageStartY
	dy			= 24
	ui_style 0, 1
	ui_outfield `Стиль Дивана`, pageStartX, _yCurrent + _yOffset, 213, _outfHeight
	ui_style 0, 0
	
	_yCurrent		= 21
	_popupWidth		= 135
	_popupHeight	= 96

	ui_infield{3} "iStyle", pageStartX + 78, _yCurrent, _popupWidth, _popupHeight,
		2, 1,
		8, 2,
		120, 90, 120, 90,
		1, "", 1,
		2, "", 2,
		3, "", 3,
		4, "", 4,
		5, "", 5,
		6, "", 6,
		7, "", 7 ui_tooltip `Стиль`
	
	_pictWidth	= 18
	_pictHeight	= 18
	_dySurface	= 27
	_surfWidth	= 193
	_surfHeight	= 25
	_xPict		= pageStartX
	_xSurface	= pageStartX + 20

	_yLegMatInf	= pageStartY + 7 * dy

	_yCurrent	= _yLegMatInf	

	if iStyle = 4 then 

		ui_pict 14, _xPict, _yCurrent + 4, _pictWidth, _pictHeight, 1 ui_tooltip `Покрытие Торца`
		
		ui_infield "gs_trim_mat", _xSurface, _yCurrent, _surfWidth, _surfHeight ui_tooltip `Торец`

		_yCurrent = _yCurrent + _dySurface

		ui_pict 13, _xPict, _yCurrent + 4, _pictWidth, _pictHeight, 1 ui_tooltip `Боковое Покрытие`
		
		ui_infield "gs_back_mat", _xSurface, _yCurrent, _surfWidth, _surfHeight ui_tooltip `Сторона`

		_yCurrent = _yCurrent + _dySurface

		ui_pict 15, _xPict, _yCurrent + 4, _pictWidth, _pictHeight, 1 ui_tooltip `Покрытие Подушки`
		
		ui_infield "gs_cush_mat", _xSurface, _yCurrent, _surfWidth, _surfHeight ui_tooltip `Подушка`

		_yCurrent = _yCurrent + _dySurface

	else

		ui_pict 11, _xPict, _yCurrent + 4, _pictWidth, _pictHeight, 1 ui_tooltip `Покрытие Дивана`
		
		ui_infield "gs_sofa_mat", _xSurface, _yCurrent, _surfWidth, _surfHeight ui_tooltip `Диван`

		_yCurrent = _yCurrent + _dySurface

		ui_pict 12, _xPict, _yCurrent + 4, _pictWidth, _pictHeight, 1 ui_tooltip `Покрытие Подушки`
		
		ui_infield "gs_cush_mat", _xSurface, _yCurrent, _surfWidth, _surfHeight ui_tooltip `Подушка`

		_yCurrent = _yCurrent + 2 * _dySurface

	endif

	x_separator = 222
	ui_separator x_separator, pageStartY, x_separator, 260

	call "ui_furniture_parts" parameters all 	pageStartX	= 231,
												pageStartY	= 0,
												bIsSofaLeg	= 1,
												stLegTypes	= stLegTypes,
												yLegMatInf	= _yLegMatInf

return

! ------------------------------------------------------------------------------
"DimMinSpacePage":
! ------------------------------------------------------------------------------
	_infHeight	= 19
	_outfHeight = 15
	_yOffset = 4
	_outFlag = 0

	x1 = 0
	x2 = 228
	y0 = pageStartY + _yOffset
	dy = 24

	ui_outfield `Количество Мест`, x1, y0, 		160, _outfHeight
	ui_infield "nSeats", 		163, y0 - _yOffset, 	52, _infHeight ui_tooltip `Количество Мест`

	if iStyle = STYLE_3 | iStyle = STYLE_4 | iStyle = STYLE_6 | iStyle = STYLE_7 then _outFlag = 4

	ui_outfield `Высота Подлокотника`, 	x2, y0, 		125, _outfHeight,  _outFlag
	ui_infield "arm_h", 		354, y0 - _yOffset, 	90, _infHeight ui_tooltip `Высота Подлокотника`

	_outFlag = 0

	y0 = y0 + dy

	ui_outfield `Высота Сидения`, 	x1, y0, 		124, _outfHeight
	ui_infield "gs_seat_height", 125, y0 - _yOffset, 	90, _infHeight ui_tooltip `Высота Сидения`

	if not(bLeg) or iStyle = STYLE_4 or iLegType = 1 then _outFlag = 4

	ui_outfield `Высота Ножек`, 	x2, y0, 		125, _outfHeight, _outFlag
	ui_infield "leg_h", 		354, y0 - _yOffset, 	90, _infHeight ui_tooltip `Высота Ножек`

	_outFlag = 0

	ui_separator 0, y0 + 21, 444, y0 + 21

	pageStartY = pageStartY + 45

	call "fa_minimalspace" parameters 	MSType 			= 3, 		! MINSPACE_DETCHAIR_NO_HOLECUT
										MSSide			= MSSide,
										pageStartY		= pageStartY
return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	pageStartY			= -1,
													iSymbolClass		= 4,
													iSofaSymbolType		= iSofaSymbolType,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													bShowSeatsIn2D		= 1,
													gs_2DSeat			= gs_2DSeat,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= TABID_2DRepresentation

return


! ------------------------------------------------------------------------------
"3DRepresentationPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
	SurfItemID = 1   
		if iStyle <> 4 then
			surfacesArray[SurfItemID]	= "gs_sofa_mat"
			SurfItemID = SurfItemID + 1
		endif
	
		surfacesArray[SurfItemID]	= "gs_cush_mat"
		SurfItemID = SurfItemID + 1
	
		if bLeg then
			surfacesArray[SurfItemID]	= "gs_leg_mat"
			SurfItemID = SurfItemID + 1
		endif
	
		if iStyle = 4 then
			surfacesArray[SurfItemID]	= "gs_back_mat"
			SurfItemID = SurfItemID + 1
		endif
	
		if iStyle = 4 then
			surfacesArray[SurfItemID]	= "gs_trim_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
		for k = 1 to vardim1(surfacesArray)
			childFlagArray[k]	= 0
		next k
	
	call "ui_object_3DRepresentation" parameters 	pageStartY			= 3,
													bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3DRepresentation
return


! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY 	= 0,
										listFieldHeight = 265,
										stDescHeader 	= "",
										stValueHeader 	= "",
										stParamNames 	= stParamNames
return

