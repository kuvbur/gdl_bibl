
! ======================================================================
! SCALE
! ======================================================================
call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

PEN gs_cont_pen

unID = 1


! --- 2D SYMBOL & MINIMAL SPACE---
DRAWINDEX 30

if det2D = 1 then call "fa_SofaSymbol" parameters	all	\					!! 1/200
													iSofaSymbolType = SOFASYMBOL_RECTSIMPLE,
													MSType = 4,
													MSFront = op_sofa * bedLength + MSFront

if det2D = 2 then call "fa_SofaSymbol" parameters	all	\					!! 1/100
													iSofaSymbolType = SOFASYMBOL_RECT3,
													MSType = 4,
													MSFront = op_sofa * bedLength + MSFront

if det2D = 3 then call "fa_SofaSymbol" parameters	all	\					!! 1/50
													iSofaSymbolType = 0,	!! hotspots & minimal space only
													MSType = 4,
													MSFront = op_sofa * bedLength + MSFront

if det2D = 4 then call "fa_SofaSymbol" parameters	all	\					!! Symbolic
													iSofaSymbolType = iSofaSymbolType,
													MSType = 4,
													MSFront = op_sofa * bedLength + MSFront

! --- 1/50 ---
IF det2D = 3 THEN

	! --- Symbol ---

	IF gs_2DSeat = 0 THEN
		mask=0
	ELSE
		mask=1
	ENDIF

	FILL gs_fill_type

	add2 wArmrest, 0
	! back
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, b, 1,
				0, -bt, 101,
				a-2*wArmrest, 0, 101,
				0, bt, 101

	! seats
	for i=1 to nSeats
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0, 0, 1,
				wSeat, 0, mask,
				wSeat, b-bt-b/4, 1,
				0, b-bt-b/4, mask,
				0, 0, -1
		add2 wSeat, 0
	next i

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			wArmrest, 0, 1,
			wArmrest, b, 1,
			0, b, 1
	del i-1
	del 1

	! left armrest
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			wArmrest, 0, 1,
			wArmrest, b, 1,
			0, b, 1


	add2 a/2, b-bt
	for i=1 to 2
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				a/2-wArmrest-wSeat, 0, mask,
				0, -b/4, 101,
				wSeat+wArmrest/2, 0, 101,
				0, b/4, 101
		mul2 -1, 1
	next i
	del i-1
	if nSeats=3 then
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				wSeat/2, 0, mask,
				0, -b/4, 101,
				-wSeat, 0, 100+mask,
				0, b/4, 101
	endif
	del 1

ENDIF

if op_sofa=1 then
	! Sofa Bed

	if det2D = 3 then
		ta = wArmrest
	else
		ta = 0.15		!override arm width
	endif
	
	fill gs_fill_type

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			a-ta, 0,0,
			a-ta, -bedLength,0,
			ta, -bedLength,0,
			ta, 0,1,
			a-ta, 0,-1

	line_type gs_dashed

	line2 a-ta, 0, a-ta, -bedLength
	line2 a-ta, -bedLength, ta, -bedLength
	line2 ta, -bedLength, ta, 0

	hotspot2 a-ta, 0,			unID	: unID=unID+1
	hotspot2 a-ta, -bedLength,	unID	: unID=unID+1
	hotspot2 ta, -bedLength,	unID	: unID=unID+1
	hotspot2 ta, 0,				unID	: unID=unID+1
endif

END
