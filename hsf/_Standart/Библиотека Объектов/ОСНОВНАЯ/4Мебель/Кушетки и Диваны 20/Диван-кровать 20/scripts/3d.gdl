
! ================================================================================
! Hotspots
! ================================================================================

unID=1

! A size
HOTSPOT 0,0,0,unID, A,1+256 : unID=unID+1
HOTSPOT -1,0,0,unID, A,3 : unID=unID+1
HOTSPOT A,0,0,unID, A,2 : unID=unID+1

HOTSPOT 0,B,0,unID, A,1+256 : unID=unID+1
HOTSPOT -1,B,0,unID, A,3 : unID=unID+1
HOTSPOT A,B,0,unID, A,2 : unID=unID+1

! B size
HOTSPOT 0,0,0,unID, B,1+256 : unID=unID+1
HOTSPOT 0,-1,0,unID, B,3 : unID=unID+1
HOTSPOT 0,B,0,unID, B,2 : unID=unID+1

HOTSPOT A,0,0,unID, B,1+256 : unID=unID+1
HOTSPOT A,-1,0,unID, B,3 : unID=unID+1
HOTSPOT A,B,0,unID, B,2 : unID=unID+1

ADD a/2,b/2,0

hotspot a/2, b/2, 0, unID, zzyzx, 1+128: unID=unID+1
hotspot a/2, b/2, -1, unID, zzyzx, 3: unID=unID+1
hotspot a/2, b/2, zzyzx, unID, zzyzx, 2: unID=unID+1

hotspot -a/2, b/2, 0, unID, zzyzx, 1+128: unID=unID+1
hotspot -a/2, b/2, -1, unID, zzyzx, 3: unID=unID+1
hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2: unID=unID+1

del 1

if gs_detlevel_3D_m = 0 then end
pen gs_cont_pen
resol gs_resol
if gs_shadow=0 then shadow off

! ================================================================================
! Legs
! ================================================================================
material gs_leg_mat
if iLegType = LEGTYPE_1 then k = 1
if iLegType = LEGTYPE_2 then k = 2
if iLegType = LEGTYPE_3 then k = 3
if iLegType = LEGTYPE_4 then k = 4
if iLegType = LEGTYPE_5 then k = 5
if not(bLeg) then k = 0
gosub 10+k

! ================================================================================
! Simple 3D representation
! ================================================================================
if gs_detlevel_3D_m = 1 then
	material gs_seat_mat
	resol 10
	add a/2, b/2, leg_h
	prism_	4, th,
			a/2-wArmrest, -b/2, 15,
			a/2-wArmrest, b/2-bt, 15,
			-a/2+wArmrest, b/2-bt, 15,
			-a/2+wArmrest, -b/2, 15
	prism_	4, t,
			a/2-wArmrest, b/2, 15,
			0, -bt, 115,
			-a+2*wArmrest, 0, 115,
			0, bt, 115
	addz th
	prism_	4, th/2,
			a/2-wArmrest, -b/2, 15,
			a/2-wArmrest, b/2-bt, 15,
			-a/2+wArmrest, b/2-bt, 15,
			-a/2+wArmrest, -b/2, 15
	addz th/2
	prism_	4, th/2,
			a/2-wArmrest, -b/2, 15,
			a/2-wArmrest, b/2-bt, 15,
			-a/2+wArmrest, b/2-bt, 15,
			-a/2+wArmrest, -b/2, 15
	addz th/2
	addx a/2-wArmrest-wSeat/2
	for i=1 to nSeats
			prism_	4, th,
			wSeat/2, -b/2, 15,
			wSeat/2, b/2-bt, 15,
			-wSeat/2, b/2-bt, 15,
			-wSeat/2, -b/2, 15
		addx -wSeat
	next i
	del i-1
	del 5

	add a/2, b/2, leg_h
	rotx 90
	addz -b/2
	for i=1 to 2
		prism_	4, b,
				a/2-wArmrest, 0, 79,
				0, hh-wArmrest/2, 179,
				wArmrest/2, -240, 2079,
				a/2-wArmrest/2, 0, 15
		mulx -1
	next i
	del i-1
	addz bt
	for i=1 to 2
		prism_	7, b/4,
				a/2-wArmrest, 3*th, 79,
				0, hh-wArmrest/2-3*th, 179,
				wArmrest/2, -60, 2079,
				wArmrest/2/2, 30, 2079,
				wArmrest/2, 120, 2079,
				a/2-wSeat-wArmrest, zzyzx-leg_h, 15,
				0, -(zzyzx-leg_h-3*th), 179
		mulx -1
	next i
	del i-1
	if nSeats=3 then
		prism_	4, b/4,
				-wSeat/2, 3*th, 79,
				0, (zzyzx-leg_h-3*th), 179,
				wSeat, 0, 179,
				0, -(zzyzx-leg_h-3*th), 179
	endif

	del 4

else

resol gs_resol

! ================================================================================
! Detailed 3D representation
! ================================================================================


	material gs_seat_mat

	add a/2, b/2, leg_h
	for j=1 to 2
		rotx 90
		!---------------------------------------------------armrests-------------
		for i=1 to 2
			hprism_	gs_seat_mat, gs_seat_mat, gs_seat_mat, gs_seat_mat,
					4, b/2, 0, r, 1,
					a/2-wArmrest, 0, 14,
					0, hh-wArmrest/2, 178,
					wArmrest/2, -240, 2014,
					a/2-wArmrest/2, 0, 14

			gosub 4000
			mulz -1
		next i
		del i-1


		addz -(b/2-bt-b/8)
		for i=1 to 2

		!-----------------------------------------------backrest cushions-------
			hprism_	gs_seat_mat, gs_seat_mat, gs_seat_mat, gs_seat_mat,
					11, b/8, 0, rr*0.9, 1,
					a/2-wArmrest-1.1*rr, 3*th, 12,
					a/2-wArmrest-rr, 3*th, 12,
					rr, 90, 2078,
					0, hh-wArmrest/2-3*th-rr, 178,
					wArmrest/2, -60, 2078,
					wArmrest/2/2, 30, 2078,
					wArmrest/2, 120, 2078,
					a/2-wSeat-wArmrest+1.1*rr, zzyzx-leg_h, 12,
					rr, 90, 2078,
					0, -(zzyzx-leg_h-3*th-2*rr), 178,
					rr, 90, 2078

			gosub 4000
			mulz -1
		next i
		del i-1
		del 1
		del 1
		mulx -1
	next j
	del j-1
	del 1




	!--------------------------------bottom panel(bottom thick cushion)-------
	add wArmrest, b/2-(b-bt)/2, leg_h

	call "fa_cushion" parameters	gs_detlevel_3D_m	= gs_detlevel_3D_m,			
									cush_a				= (a/2-wArmrest)*2,				
									cush_b				= b-bt,				
									cush_h				= th,		
									r1					= 0.02,				
									r2					= cushionRad,		
									gs_resol			= gs_resol,		
									cush_mat			= gs_seat_mat		

	del 1

	!------------------------------------------------------thin cushions-------
	add wArmrest , b/2 - (b-bt)/2 , leg_h + th	
	for i=1 to 2
		call "fa_cushion" parameters	gs_detlevel_3D_m	= gs_detlevel_3D_m,			
										cush_a				= (a/2-wArmrest)*2,				
										cush_b				= b-bt,					
										cush_h				= th/2,				
										r1					= 0.02,					
										r2					= cushionRad,			
										gs_resol			= gs_resol,			
										cush_mat			= gs_seat_mat		


		addz th/2
	next i
	del i-1
	del 1


	!-------------------------------------------------------top cushions------
	add a-wArmrest - wSeat,0,leg_h + th*2
	gosub 2000

	for i=1 to nSeats
		call "fa_cushion" parameters	gs_detlevel_3D_m	= gs_detlevel_3D_m,			
										cush_a				= wSeat,				
										cush_b				= b-bt,				
										cush_h				= th,		
										r1					= 0.02,				
										r2					= cushionRad,		
										gs_resol			= gs_resol,		
										cush_mat			= gs_seat_mat	
		addx -wSeat
	next i
	del i-1
	del 1


	!---------------------------------------------------------back panel------

	add a/2, b/2, leg_h
	gosub 2000
	prism_	4, t,
			a/2-wArmrest, b/2, 15,
			0, -bt, 115,
			-a+2*wArmrest, 0, 115,
			0, bt, 115
	del 1

	add a/2, b/2, leg_h
	!----------------------------middle backrest cushion for third seat-------
	if nSeats=3 then
		rotx 90
		addz -(b/2-bt-b/8)
		for i=1 to 2
			hprism_	gs_seat_mat, gs_seat_mat, gs_seat_mat, gs_seat_mat,
					9, b/8, 0, rr*0.9, 1,
					0.95*wSeat/2-1.1*rr, 3*th, 12,
					0.95*wSeat/2-rr, 3*th, 12,
					rr, 90, 2078,
					0, (zzyzx-leg_h-3*th-2*rr), 178,
					rr, 90, 2078,
					-0.95*wSeat+2*rr, 0, 178,
					rr, 90, 2078,
					0, -(zzyzx-leg_h-3*th-2*rr), 178,
					rr, 90, 2078
			mulz -1
		next i
		del i-1
		del 2
		gosub 2000
	endif
	del 1

endif

! ================================================================================
END	! END	! END	! END	! END	! END	! END	! END	! END	! END	! END
! ================================================================================

! --------------------------------------------------------------------------------
! Subroutines for Legtypes
! --------------------------------------------------------------------------------

10:
leg_h=0
RETURN

11:
_minusA = 0.32
_minusB = 0.08
ADD a/2- (a-_minusA)/2, b/2- (b-_minusB)/2, 0
CALL "fa_sofalegtyp" PARAMETERS sofaleg_typ_m=1, a=a-_minusA, b=b-_minusB, gs_detlevel_3D_m=gs_detlevel_3D_m,
gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
DEL 1
RETURN

12:
_minusA = 0.32
_minusB = 0.08
ADD a/2- (a-_minusA)/2,  b/2- (b-_minusB)/2, 0
CALL "fa_sofalegtyp" PARAMETERS sofaleg_typ_m=2, a=a-_minusA, b=b-_minusB, leg_h=leg_h, gs_detlevel_3D_m=gs_detlevel_3D_m,
gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
DEL 1
RETURN

13:
_minusA = 0.32
_minusB = 0.08
ADD a/2- (a-_minusA)/2,  b/2- (b-_minusB)/2, 0
CALL "fa_sofalegtyp" PARAMETERS sofaleg_typ_m=3, a=a-_minusA, b=b-_minusB, leg_h=leg_h, gs_detlevel_3D_m=gs_detlevel_3D_m,
gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
DEL 1
RETURN

14:
_minusA = 0.26
_minusB = 0.1
ADD a/2- (a-_minusA)/2,  b/2- (b-_minusB)/2, 0
CALL "fa_sofalegtyp" PARAMETERS sofaleg_typ_m=4, a=a-_minusA, b=b-_minusB, leg_h=leg_h, gs_detlevel_3D_m=gs_detlevel_3D_m,
gs_resol=gs_resol,gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
DEL 1
RETURN

15:
_minusA = 0.4
_minusB = 0.12
ADD a/2- (a-_minusA)/2,  b/2- (b-_minusB)/2, 0
CALL "fa_sofalegtyp" PARAMETERS sofaleg_typ_m=5, a=a-_minusA, b=b-_minusB, leg_h=leg_h, ur=wArmrest/8, lr=wArmrest/12,
gs_cont_pen=gs_cont_pen, gs_leg_mat=gs_leg_mat
DEL 1
RETURN


! --------------------------------------------------------------------------------
! Texture Subroutines
! --------------------------------------------------------------------------------


2000:
gosub 5000
return

4000:
rotx 90
gosub 5000
del 1
return


5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return



