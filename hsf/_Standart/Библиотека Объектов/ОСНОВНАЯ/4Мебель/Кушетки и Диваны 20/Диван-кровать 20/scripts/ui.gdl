
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 				= -1
_idxTab = 1
TABID_DimMinSpace		= _idxTab  : _idxTab = _idxTab + 1
TABID_Leg				= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions		= _idxTab  : _idxTab = _idxTab + 1

pageStartY = 0
pageStartX = 0

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Дивана-Кровати`
ui_current_page gs_ui_current_page

ui_page TABID_DimMinSpace, TABID_Root, `Размеры и Минимальное Пространство`, "uiTab_Dimension_flat(1)"
	gosub "DimMinSpacePage"

ui_page TABID_Leg, TABID_Root, `Ножки`, "uiTab_Leg_flat(1)"
	gosub "LegPage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationPage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Leg_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"DimMinSpacePage":
! ------------------------------------------------------------------------------
	_infHeight	= 19
	_outfHeight = 15
	_yOffset = 4
	_outFlag = 0

	x1 = 0
	x2 = 228
	y0 = pageStartY + _yOffset
	dy = 24

	ui_outfield `Количество Мест`, x1, y0, 		160, _outfHeight
	ui_infield "nSeats", 		163, y0 - _yOffset, 	52, _infHeight ui_tooltip `Количество Мест`

	y0 = y0 + dy

	ui_outfield `Высота Сидения`, 	x1, y0, 		124, _outfHeight
	ui_infield "gs_seat_height", 125, y0 - _yOffset, 	90, _infHeight ui_tooltip `Высота Сидения`

	if not(bLeg) or iLegType = 1 then _outFlag = 4

	ui_outfield `Высота Ножек`, 	x2, y0, 		125, _outfHeight, _outFlag
	ui_infield "leg_h", 		354, y0 - _yOffset, 	90, _infHeight ui_tooltip `Высота Ножек`

	_outFlag = 4

	ui_separator 0, y0 + 20, 444, y0 + 20

	xxPict = 128
	yyPict = pageStartY + 78

	ImageX = 183
	ImageY = 169

	ui_pict	2, xxPict, yyPict, ImageX, ImageY, 1 					! SofaBed_DimAndMinSpace.png

	ui_infield "A", 		xxPict+56, 			yyPict-23,		 	88, _infHeight ui_tooltip `Ширина Дивана` 
	ui_infield "MSSide",	xxPict+148, 		yyPict-23,			70, _infHeight ui_tooltip `Минимальное Пространство Сбоку`

	n = request("Working_length_unit", "", formatt)

	ui_outfield str(formatt, MSSide),	xxPict-5, 			yyPict-18,	50, _outfHeight, 2 ui_tooltip `Минимальное Пространство Сбоку`
	ui_infield "MSRear",				xxPict+ImageX+3, 	yyPict+6,	70, _infHeight ui_tooltip `Минимальное Пространство Сзади`
	ui_infield "B", 					xxPict+ImageX+3, 	yyPict+48, 	88, _infHeight ui_tooltip `Глубина Дивана`		
	ui_infield "bed_length",			xxPict-91, 			yyPict+48, 	88, _infHeight ui_tooltip `Длина Кровати`	
	ui_infield "MSFront",				xxPict+ImageX+3, 	yyPict+138, 70,	_infHeight ui_tooltip `Минимальное Пространство Спереди` 

return

! ------------------------------------------------------------------------------
"LegPage":
! ------------------------------------------------------------------------------
	_yCurrent 		= pageStartY
	dy				= 24
	_infieldHeight	= 19
	_outfieldHeight	= 15
	
	x1	= pageStartX
	x2	= pageStartX + 78
	x3	= 213
	x4	= 232
	x5	= 354
	xEnd = 444
	
	ui_style 0, 1
	ui_infield{3} "bLeg", x1, _yCurrent, 444, _infieldHeight,
					7, "",
					2,
					1, 20, 20, 20, 20,
					0, `Ножки`, 0,
					1, `Ножки`, 1
	ui_style 0, 0
	
	_yCurrent = 21
	
	UI_INFIELD{3}	"iLegType",	x2,	_yCurrent,	135,	96,
		2,1,8,2,
		120,90,120,90,							! ui_Sofaleg_Types(1)
		1, "", LEGTYPE_1,
		2, "", LEGTYPE_2,
		3, "", LEGTYPE_3,
		4, "", LEGTYPE_4,
		5, "", LEGTYPE_5 ui_tooltip `Стиль Ножек`
	
	
	_iFlag = 0
	if not(bLeg) or iLegType = 1 then _iFlag = 4

	ui_outfield `Высота Ножек`, x4, _yCurrent + 4, x5 - x4 - 2, _outfieldHeight, _iFlag
	ui_infield "leg_h", x5, _yCurrent, xEnd - x5, _infieldHeight ui_tooltip `Высота Ножек`
	
	_iFlag = 0

	_yCurrent = _yCurrent + 2 * dy

	ui_infield "gs_leg_mat", x4, _yCurrent, xEnd - x4, 25 ui_tooltip `Ножки`

return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 4,
													iSofaSymbolType		= iSofaSymbolType,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													bShowSeatsIn2D		= 1,
													gs_2DSeat			= gs_2DSeat,
													bShowOpenSofaBed	= 1,
													op_sofa				= op_sofa,
													gs_dashed			= gs_dashed,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= TABID_2DRepresentation,
													pageStartY			= - 1

return

! ------------------------------------------------------------------------------
"3DRepresentationPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_seat_mat"
		SurfItemID = SurfItemID + 1
		
		if bLeg then
			surfacesArray[2]	= "gs_leg_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
		for k = 1 to vardim1(surfacesArray)
			childFlagArray[k]	= 0
		next k

	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													pageStartY			= 3,
													iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY 	= 0,
										listFieldHeight = 265,
										stDescHeader 	= "",
										stValueHeader 	= "",
										stParamNames 	= stParamNames

return
