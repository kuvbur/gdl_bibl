
!! Sofa Set 01.gsm

! ======================================================================
! SCALE
! ======================================================================
call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

!!! ******************************** 2D

PEN gs_cont_pen


unID=1
IF iSofaUnitType = SOFAUNIT_END THEN HOTSPOT2 a,0, unID: unID = unID+1

HOTSPOT2 0,0, unID: unID=unID+1
HOTSPOT2 a/2,0, unID: unID=unID+1
HOTSPOT2 a/2,b/2, unID: unID=unID+1

CALL "Resize_A_B_ZZYZX" Parameters ALL

IF  iSofaUnitType = SOFAUNIT_STANDARD or iSofaUnitType = SOFAUNIT_POUFFE THEN
	!!! *** Minimal Space
	call "fa_minimalspace" parameters all MSType = 6, unID = 3000, MSSide = MSSide1
ENDIF

IF  iSofaUnitType = SOFAUNIT_END THEN
		POLY2_ 10, 2+4,
			-0.2, -0.5, 0,
			-0.2, b+0.2, 0,
			a, b+0.2, 0,
			a, -0.5, 0,
			-0.2, -0.5, -1,
			0, 0, 1,
			0, b, 1,
			a, b, 1,
			a, 0, 1,
			0, 0, -1

!!! *** Minimal Space
	call "fa_minimalspace" parameters all MSType = 6, unID = 3000, MSSide = 0, MSSide2 = MSSide
ENDIF

IF iSofaUnitType = SOFAUNIT_CORNER THEN
	POLY2_ 10, 2+4,
			0, 0, 0,
			0, b+0.2, 0,
			b+0.2, b+0.2, 0,
			b+0.2, 0, 0,
			0, 0, -1,
			0, 0, 1,
			0, b, 1,
			b, b, 1,
			b, 0, 1,
			0, 0, -1

!!! *** Minimal Space
	reqBMinSpace = 0
	if MSRear > EPS then
		call "fa_minimalspace" parameters MSType = 0,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen,
													MSFillBackPen,
													MSContLineType,
													MSContLinePen
		if reqBMinSpace <> 0 then
			FILL MSFillType
			PEN MSContLinePen
			line_type  MSContLineType
			POLY2_B 7, 1+2+4+64,MSFillPen,MSFillBackPen,
						0, B, 1,
						0, B+MSRear, 1,
						A+MSRear, B+MSRear, 1,
						A+MSRear, 0, 1,
						A, 0, 0,
						A, B, 0,
						0, B, -1
			PEN gs_cont_pen
			line_type 1
		endif
	endif
ENDIF


!!! ******************************** 1/50
IF det2D = 3 THEN

!!! *** Symbol Lines
IF iSofaUnitType = SOFAUNIT_STANDARD OR iSofaUnitType = SOFAUNIT_END THEN


	IF iSofaUnitType = SOFAUNIT_END THEN
		POLY2_ 9, 1,            !!! arm
			0.13, 0, 1,
			0.02, 0, 1,
			0, 0.02, 1001,
			0, b-0.02, 1,
			0.02, b, 1001,
			0.13, b, 1,
			0.15, b-0.02, 1001,
			0.15, 0.02, 1,
			0.13, 0, 1001
		aa=0.15
		LINE2 0.13, b, a, b
		LINE2 a, b, a, b-0.07

	ELSE
		aa=0
		LINE2 0, b, a, b
		LINE2 0, b, 0, b-0.07
		LINE2 a, b, a, b-0.07
		LINE2 0, b-yback1, 0, b-yback3
	ENDIF

	LINE2 a, b-yback1, a, b-yback3

	ADD2 aa, 0
		POLY2_ 9, 1,
			(a-aa)-0.03, 0, 1,
			0.03, 0, 1,
			0, 0.03, 1001,
			0, b-yback3, 1,
			0.03, b-yback2, 1001,
			(a-aa)-0.03, b-yback2, 1,
			(a-aa), b-yback3, 1001,
			(a-aa), 0.03, 1,
			(a-aa)-0.03, 0, 1001
		POLY2_ 9, 1,
			(a-aa)-0.03, b-0.04, 1,
			0.03, b-0.04, 1,
			0, b-0.07, 1001,
			0, b-yback1, 1,
			0.03, b-yback2, 1001,
			(a-aa)-0.03, b-yback2, 1,
			(a-aa), b-yback1, 1001,
			(a-aa), b-0.07, 1,
			(a-aa)-0.03, b-0.04, 1001
		POLY2_ 5, 1,
			0, b-0.15, 1,
			0.03, b-0.15, 901,
			0.03, b-0.18, 3001,
			(a-aa)-0.03, b-0.18, 1,
			(a-aa), b-0.15, 1001
	DEL 1

ENDIF

IF iSofaUnitType = SOFAUNIT_CORNER THEN

	POLY2_ 9, 1,
		b-yback3, 0, 1,
		0.03, 0, 1,
		0, 0.03, 1001,
		0, b-yback3, 1,
		0.03, b-yback2, 1001,
		b-yback3, b-yback2, 1,
		b-yback2, b-yback3, 1001,
		b-yback2, 0.03, 1,
		b-yback3, 0, 1001
	POLY2_ 11, 1,
		b-0.04, b-0.04, 1,
		0.03, b-0.04, 1,
		0, b-0.07, 1001,
		0, b-yback1, 1,
		0.03, b-yback2, 1001,
		b-yback2, b-yback2, 1,
		b-yback2, 0.03, 1,
		b-yback1, 0, 1001,
		b-0.07, 0, 1,
		b-0.04, 0.03, 1001,
		b-0.04, b-0.04, 1
	POLY2_ 6, 1,
		0, b-0.15, 1,
		0.03, b-0.15, 901,
		0.03, b-0.18, 3001,
		b-0.18, b-0.18, 1,
		b-0.18, 0.03, 1,
		b-0.15, 0, 1001

		LINE2 0, b, b, b
		LINE2 b, b, b, 0
		LINE2 0, b, 0, b-0.07
		LINE2 b-0.07, 0, b, 0
		LINE2 b-yback1, 0, b-yback3, 0
		LINE2 0, b-yback1, 0, b-yback3
		LINE2 b-yback2, b-yback2, b-0.04, b-0.04
ENDIF


IF iSofaUnitType = SOFAUNIT_POUFFE THEN

	POLY2_ 9, 1,
		a-0.03, 0, 1,
		0.03, 0, 1,
		0, 0.03, 1001,
		0, b-0.03, 1,
		0.03, b, 1001,
		a-0.03, b, 1,
		a, b-0.03, 1001,
		a, 0.03, 1,
		a-0.03, 0, 1001
ENDIF

!!! *** Background Fill

FILL gs_fill_type

IF iSofaUnitType = SOFAUNIT_STANDARD THEN
	POLY2_B 7, 2+4+64, gs_fill_pen, gs_back_pen,
		a-0.03, 0, 1,
		0.03, 0, 1,
		0, 0.03, 1001,
		0, b, 1,
		a, b, 1,
		a, 0.03, 1,
		a-0.03, 0, 1001
ENDIF

IF iSofaUnitType = SOFAUNIT_END THEN
	POLY2_B 13, 2+4+64, gs_fill_pen, gs_back_pen,
		0.12, 0, 1,
		0.02, 0, 1,
		0, 0.02, 1001,
		0, b-0.02, 1,
		0.02, b, 1001,
		a, b, 1,
		a, 0.03, 1,
		a-0.03, 0, 1001,
		0.18, 0, 1,
		0.15, 0.03, 1001,
		0.15, 0.02, 1,
		0.13, 0.02, 901,
		0.13, 0, 3001
ENDIF

IF iSofaUnitType = SOFAUNIT_CORNER THEN
	POLY2_B 5, 2+4+64, gs_fill_pen, gs_back_pen,
		b, 0, 1,
		0.03, 0, 1,
		0, 0.03, 1001,
		0, b, 1,
		b, b, 1
ENDIF

IF iSofaUnitType = SOFAUNIT_POUFFE THEN
	POLY2_B 9, 2+4+64, gs_fill_pen, gs_back_pen,
		a-0.03, 0, 1,
		0.03, 0, 1,
		0, 0.03, 1001,
		0, b-0.03, 1,
		0.03, b, 1001,
		a-0.03, b, 1,
		a, b-0.03, 1001,
		a, 0.03, 1,
		a-0.03, 0, 1001
ENDIF
endif

!!! ******************************** 1/100
IF det2D = 2 THEN

rect2 0, 0, a, b

GOSUB 10

	IF iSofaUnitType = SOFAUNIT_STANDARD THEN
		line2 0, b - yback2, a, b - yback2
	endif

	IF iSofaUnitType = SOFAUNIT_END THEN
		line2 0.15, b - yback2, a, b - yback2
		line2 0.15, 0, 0.15, b
	endif

	IF iSofaUnitType = SOFAUNIT_CORNER THEN
		line2 0, b - yback2, a - yback2, b - yback2
		line2 a - yback2, 0, a - yback2, b - yback2
	endif

endif
!!! ******************************** 1/200
IF det2D = 1 THEN

	rect2 0,0,a,b
GOSUB 10

endif
end

10:

	FILL gs_fill_type
	POLY2_B 5, 2+64,gs_fill_pen,gs_back_pen,
		0,0,0,
		a,0,0,
		a,b,0,
		0,b,0,
		0,0,0
RETURN

