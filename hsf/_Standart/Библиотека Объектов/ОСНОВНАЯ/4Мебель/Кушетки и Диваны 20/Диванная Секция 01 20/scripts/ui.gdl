
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 				= -1
_idxTab = 1
TABID_Style				= _idxTab  : _idxTab = _idxTab + 1
TABID_DimMinSpace		= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions		= _idxTab  : _idxTab = _idxTab + 1

pageStartY = 0
pageStartX = 0

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Диванной Секции`
ui_current_page gs_ui_current_page

ui_page TABID_Style, TABID_Root, `Стиль и Ножки`, "uiTab_Style_flat(1)"
	gosub "StylePage"

ui_page TABID_DimMinSpace, TABID_Root, `Размеры и Минимальное Пространство`, "uiTab_Dimension_flat(1)"
	gosub "DimMinSpacePage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationPage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StylePage":
! ------------------------------------------------------------------------------
	x1 = pageStartX
	x2 = x1 + 78
	
	_outfieldHeight		= 15
	_infieldHeight 		= 19
	_yOffset 			= 4

	dy 					= 24

	_yCurrent = pageStartY
	ui_style 0, 1
	ui_outfield `Тип Секции`, x1, _yCurrent + _yOffset, 213, _outfieldHeight
	ui_style 0, 0

	_yCurrent = _yCurrent + 21
	ui_infield{3} "iSofaUnitType", x2, _yCurrent, 135, 96,
				2,"ui_sofaset1(14)",4,1,
				120,90,120,90,
				1,`Рядная`, SOFAUNIT_STANDARD,
				2,`Торцевая`, SOFAUNIT_END,
				3,`Угловая`, SOFAUNIT_CORNER,
				4,`Пуф`, SOFAUNIT_POUFFE ui_tooltip `Тип Секции`

	_pictWidth	= 18
	_pictHeight	= 18
	_dySurface	= 27
	_surfWidth	= 193
	_surfHeight	= 25
	_xPict		= pageStartX
	_xSurface	= pageStartX + 20
	
	_yMatInf	= pageStartY + 7 * dy

	_yCurrent	= _yMatInf	

	ui_pict 12, _xPict, _yCurrent + 4, _pictWidth, _pictHeight, 1 ui_tooltip `Покрытие Подушки` !Sofa_StyleAndLeg_Surface2.png 
		
	ui_infield "gs_cush_mat", _xSurface, _yCurrent, _surfWidth, _surfHeight ui_tooltip `Подушка`

	_yCurrent = _yCurrent + _dySurface

	if iSofaunitType <> 4 then

		ui_pict 11, _xPict, _yCurrent + 4, _pictWidth, _pictHeight, 1 ui_tooltip `Покрытие Дивана` !Sofa_StyleAndLeg_Surface1.png 
		
		ui_infield "gs_sofa_mat", _xSurface, _yCurrent, _surfWidth, _surfHeight ui_tooltip `Диван`
	endif

	_yCurrent = _yCurrent + _dySurface
	x_separator = pageStartX + 222	
	ui_separator x_separator, pageStartY, x_separator, _yCurrent
	
	call "ui_furniture_parts" parameters all 	pageStartX	= 231,
												pageStartY	= 0,
												bIsSofaLeg	= 1,
												iLegType	= iLegType,
												yLegMatInf	= _yMatInf

return

! ------------------------------------------------------------------------------
"DimMinSpacePage":
! ------------------------------------------------------------------------------
	x1 = 0
	x2 = 228
	_infHeight	= 19
	_outfHeight = 15
	_yOffset = 4
	y0 = pageStartY + _yOffset
	dy = 24

	ui_outfield `Высота Сидения`, 		x1, 	y0, 			124, 	_outfHeight
	ui_infield "gs_seat_height", 	125, 	y0 - _yOffset, 	90, 	_infHeight ui_tooltip `Высота Сидения`

	if iSofaUnitType <> SOFAUNIT_POUFFE then
		ui_outfield `Высота Подлокотника`, 	x2, 	y0, 			125, 	_outfHeight
		ui_infield "arm_h", 		354, 	y0 - _yOffset, 	90, 	_infHeight ui_tooltip `Высота Подлокотника`
		y0 = y0 + 20

		if bLeg & iLegType <> 1 then

			y0 = y0 + 4

			ui_outfield `Высота Ножек`, 	x2, 	y0, 			125,	_outfHeight
			ui_infield "leg_h", 		354, 	y0 - _yOffset, 	90,		_infHeight ui_tooltip `Высота Ножек`
	
			y0 = y0 + 20
		endif
	else
		if bLeg & iLegType <> 1 then
			ui_outfield `Высота Ножек`, 	x2, y0, 		125, _outfHeight
			ui_infield "leg_h", 		354, y0 - _yOffset, 	90, _infHeight ui_tooltip `Высота Ножек`
		endif
		
		y0 = y0 + 20
	endif

	ui_separator 0, y0, 444, y0

	if iSofaUnitType = SOFAUNIT_STANDARD then

		call "fa_minimalspace" parameters 	MSType	= 6 		! MINSPACE_SOFASET_DIFF_SIDES

	else
		if iSofaUnitType = SOFAUNIT_POUFFE then
	
			xxPict = 145
			yyPict = pageStartY + 86
		
			ImageX = 125
			ImageY = 134

			ui_pict	1, xxPict, yyPict, ImageX, ImageY, 1					! SofaSet01Pouffe.png
		endif

		if iSofaUnitType = SOFAUNIT_END then
	
			xxPict = 160
			yyPict = pageStartY + 86
		
			ImageX = 105
			ImageY = 134
		
			ui_pict	2, xxPict, yyPict, ImageX, ImageY, 1					! SofaSet01End.png
	
		endif	
	
		if iSofaUnitType = SOFAUNIT_CORNER then
	
			xxPict = 185
			yyPict = pageStartY + 106
		
			ImageX = 95
			ImageY = 105
		
			ui_pict	3, xxPict, yyPict, ImageX, ImageY, 1					! SofaSet01CornerL.png
	
		endif

		if iSofaUnitType = SOFAUNIT_POUFFE then
			ui_infield "MSSide2",		xxPict-53,	 	yyPict-23, 			70, _infHeight ui_tooltip `Минимальное Пространство Слева` 
		endif
	
		if iSofaUnitType = SOFAUNIT_END then
			ui_infield "MSSide",		xxPict-53,	 	yyPict-23, 			70, _infHeight ui_tooltip `Минимальное Пространство по Бокам` 
		endif
	
		ui_infield "A", xxPict+2+21 * (iSofaUnitType <> SOFAUNIT_CORNER),		yyPict-23, 			70, 19 ui_tooltip `Ширина Дивана` 	 
	
		if iSofaUnitType = SOFAUNIT_POUFFE then	
			ui_infield "MSSide1",		xxPict+115, 	yyPict-23, 			70, _infHeight ui_tooltip `Минимальное Пространство Справа`
		endif
	
		ui_infield "MSRear",		xxPict+ImageX+3,	yyPict+5, 			70, _infHeight ui_tooltip `Минимальное Пространство Сзади` 
		ui_infield "B", 			xxPict+ImageX+3,	yyPict+48, 			70, _infHeight ui_tooltip `Глубина Дивана` 	
	
		if iSofaUnitType <> SOFAUNIT_CORNER then	
			ui_infield "MSFront",		xxPict+ImageX+3,yyPict+102, 		70, _infHeight ui_tooltip `Минимальное Пространство Спереди` 
		else	
			n = request("Working_length_unit", "", formatt)
	
			ui_outfield str(formatt, MSRear),	xxPict+55, 	yyPict+ImageY+3,	50, _outfHeight, 2 ui_tooltip `Минимальное Пространство Сзади`
		endif
	endif

return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 10,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= TABID_2DRepresentation,
													pageStartY			= - 1

return

! ------------------------------------------------------------------------------
"3DRepresentationPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
	SurfItemID = 1   
		if iSofaUnitType <> 4 then
			surfacesArray[SurfItemID]	= "gs_sofa_mat"
			SurfItemID = SurfItemID + 1
		endif
	
		surfacesArray[SurfItemID]	= "gs_cush_mat"
		SurfItemID = SurfItemID + 1
	
		if bLeg then
			surfacesArray[SurfItemID]	= "gs_leg_mat"
			SurfItemID = SurfItemID + 1
		endif
	
	dim childFlagArray[]
		for k = 1 to vardim1(surfacesArray)
			childFlagArray[k]	= 0
		next k

	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													pageStartY			= 3,
													iSurfaceListID		= TABID_3DRepresentation
return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY 	= 0,
										listFieldHeight = 265,
										stDescHeader 	= "",
										stValueHeader 	= "",
										stParamNames 	= stParamNames
return
