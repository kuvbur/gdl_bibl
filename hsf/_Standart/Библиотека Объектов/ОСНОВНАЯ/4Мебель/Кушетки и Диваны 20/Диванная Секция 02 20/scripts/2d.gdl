
!! Sofa Set 02.gsm

! ======================================================================
! SCALE
! ======================================================================
call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

!!! ******************************** 2D

PEN gs_cont_pen


bb=b/0.844
rr=radius_i+b
alpha=(ASN(a/2/rr))*2
rrr=radius_i+b+MSRear


unID=1
!!! *********************************************************** Symbol lines
IF iSofaUnitType2 = SOFAUNIT_STANDARD THEN

	HOTSPOT2 0, 0: unID=unID+1
	HOTSPOT2 a/2, 0: unID=unID+1
	HOTSPOT2 a/2, b/2: unID=unID+1

	CALL "Resize_A_B_ZZYZX" Parameters ALL

	POLY2_ 5, 1,
		0, 0, 1,
		0, b, 1,
		a, b, 1,
		a, 0, 1,
		0, 0, -1
!!! ******************************** 1/50
	IF det2D = 3 THEN
	LINE2 0, 0.606*bb, a, 0.606*bb
	endif
	IF det2D = 3 or det2D = 2 THEN
	LINE2 0, 0.486*bb, a, 0.486*bb
	endif
ENDIF


IF iSofaUnitType2 = SOFAUNIT_CURVA THEN
!	unID = 10
	drawindex 20

	HOTSPOT2 0, 0: unID=unID+1
	HOTSPOT2 0, b: unID=unID+1
	HOTSPOT2 rr*SIN(alpha), -radius_i+rr*COS(alpha): unID=unID+1
	HOTSPOT2 radius_i*SIN(alpha), -radius_i+radius_i*COS(alpha): unID=unID+1

	POLY2_ 7, 1,
		0, 0, 1,
		0, b, 1,
		0, -radius_i, 901,
		rr*SIN(alpha), -radius_i+rr*COS(alpha), 3001,
		radius_i*SIN(alpha), -radius_i+radius_i*COS(alpha), 1,
		0, -radius_i, 901,
		0, 0, 3001

	!!! ******************************** 1/50
	IF det2D = 3 THEN
	POLY2_ 3, 1,
		0, 0.606*bb, 1,
		0, -radius_i, 901,
		(rr-0.606*bb)*SIN(alpha), -radius_i+(rr-0.606*bb)*COS(alpha), 3001
	endif
	IF det2D = 3 or det2D = 2 THEN
	POLY2_ 3, 1,
		0, 0.486*bb, 1,
		0, -radius_i, 901,
		(rr-0.486*bb)*SIN(alpha), -radius_i+(rr-0.486*bb)*COS(alpha), 3001
	endif
ENDIF


IF iSofaUnitType2 = SOFAUNIT_CURVB THEN
!	unID = 20
	HOTSPOT2 0, 0: unID=unID+1
!		HOTSPOT2 0, rr: unID=unID+1
	HOTSPOT2 0, b: unID=unID+1
	HOTSPOT2 radius_i*SIN(alpha), rr-radius_i*COS(alpha): unID=unID+1
	HOTSPOT2 rr*SIN(alpha), rr-rr*COS(alpha): unID=unID+1

	POLY2_ 7, 1,
		0, 0, 1,
		0, b, 1,
		0, rr, 901,
		radius_i*SIN(alpha), rr-radius_i*COS(alpha), 3001,
		rr*SIN(alpha), rr-rr*COS(alpha), 1,
		0, rr, 901,
		0, 0, 3001

	!!! ******************************** 1/50
	IF det2D = 3 THEN
	POLY2_ 3, 1,
		0, 0.606*bb, 1,
		0, rr, 901,
		(rr-0.606*bb)*SIN(alpha), rr-(rr-0.606*bb)*COS(alpha), 3001
	endif

	IF det2D = 3 or det2D = 2 THEN
	POLY2_ 3, 1,
		0, 0.486*bb, 1,
		0, rr, 901,
		(rr-0.486*bb)*SIN(alpha), rr-(rr-0.486*bb)*COS(alpha), 3001
	endif
ENDIF



!!! ********************************************* Background Fill
FILL gs_fill_type

IF iSofaUnitType2 = SOFAUNIT_STANDARD THEN
	POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
		0, 0, 0,
		0, b, 0,
		a, b, 0,
		a, 0, 0,
		0, 0, -1
ENDIF

IF iSofaUnitType2 = SOFAUNIT_CURVA THEN
	POLY2_B 7, 2+64, gs_fill_pen, gs_back_pen,
		0, 0, 0,
		0, b, 0,
		0, -radius_i, 900,
		rr*SIN(alpha), -radius_i+rr*COS(alpha), 3000,
		radius_i*SIN(alpha), -radius_i+radius_i*COS(alpha), 0,
		0, -radius_i, 900,
		0, 0, 3000
ENDIF

IF iSofaUnitType2 = SOFAUNIT_CURVB THEN
	POLY2_B 7, 2+64, gs_fill_pen, gs_back_pen,
		0, 0, 0,
		0, b, 0,
		0, rr, 900,
		radius_i*SIN(alpha), rr-radius_i*COS(alpha), 3000,
		rr*SIN(alpha), rr-rr*COS(alpha), 0,
		0, rr, 900,
		0, 0, 3000
ENDIF


!!! ************************************************* Minimal Space
IF iSofaUnitType2 = SOFAUNIT_STANDARD THEN
	call "fa_minimalspace" parameters all MSType = 6, unID = 3000, MSSide = MSSide1
else
	drawindex 10
	reqBMinSpace = 0
	if MSFront > EPS or MSRear > EPS then
		call "fa_minimalspace" parameters all MSType = 0,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen, MSFillBackPen,
													MSContLineType,
													MSContLinePen
	endif

	if reqBMinSpace <> 0 then
		FILL MSFillType
		PEN MSContLinePen
		line_type  MSContLineType

		IF iSofaUnitType2 = SOFAUNIT_CURVA THEN
			if MSFront >= radius_i then
				POLY2_B 5, 1+2+4+64, MSFillPen, MSFillBackPen,
						0, 		-radius_i,	1,
						radius_i*SIN(alpha), -radius_i+radius_i*COS(alpha),0,
						0, 		-radius_i, 	900,
						0, 		0,			3001,
						0, 		-radius_i,	-1

				HOTSPOT2  (rrr)*SIN(alpha), -radius_i+(rrr)*COS(alpha): unID=unID+1
				HOTSPOT2 0, -radius_i: unID=unID+1
			else
				if MSFront > EPS then
					POLY2_B 8, 1+2+4+64, MSFillPen, MSFillBackPen,
							0, 		0,			1,
							0, 		-MSFront, 	1,
							0, 		-radius_i, 	900,
							(radius_i-MSFront)*SIN(alpha), 	-radius_i+(radius_i-MSFront)*COS(alpha), 3001,
							radius_i*SIN(alpha), 			-radius_i+radius_i*COS(alpha),			 0,
							0, 		-radius_i, 	900,
							0, 		0, 			3000,
							0, 		0, 			-1

					HOTSPOT2 (radius_i-MSFront)*SIN(alpha), -radius_i+(radius_i-MSFront)*COS(alpha), unID=unID+1
					HOTSPOT2 0, -MSFront: unID=unID+1
				endif
			endif

			if MSRear > EPS then
				POLY2_B 8, 1+2+4+64, MSFillPen, MSFillBackPen,
						0, 		b, 			1,
						0, 		b+MSRear,	1,
						0, 		-radius_i, 	900,
						(rrr)*SIN(alpha), -radius_i+(rrr)*COS(alpha),3001,
						rr*SIN(alpha), -radius_i+rr*COS(alpha),0,
						0, 		-radius_i, 	900,
						0, 		b, 			3000,
						0, 		b, 			-1

				HOTSPOT2  (rrr)*SIN(alpha), -radius_i+(rrr)*COS(alpha): unID=unID+1
				HOTSPOT2  0, b+MSRear: unID=unID+1
			endif
		ENDIF

		IF iSofaUnitType2 = SOFAUNIT_CURVB THEN
			if MSRear >= radius_i then
				POLY2_B 4, 1+2+4+64, MSFillPen, MSFillBackPen,
						radius_i*SIN(alpha), rr-radius_i*COS(alpha),0,
						0, 		rr, 	900,
						0, 		0, 		3001,
						0, 		rr, 	1

				HOTSPOT2  0, rr: unID=unID+1
			else
				if MSRear > EPS then
					POLY2_B 8, 1+2+4+64, MSFillPen, MSFillBackPen,
							0, 		b, 			1,
							0, 		b+MSRear, 	1,
							0, 		rr, 		900,
							(radius_i-MSRear)*SIN(alpha), 	rr-(radius_i-MSRear)*COS(alpha), 3001,
							radius_i*SIN(alpha), 			rr-radius_i*COS(alpha), 		 0,
							0, 		rr, 		900,
							0, 		b, 			3000,
							0,		b,			-1

					HOTSPOT2  0, b+MSRear: unID=unID+1
					HOTSPOT2  (radius_i-MSRear)*SIN(alpha), rr-(radius_i-MSRear)*COS(alpha) : unID=unID+1
				endif
			endif

			if MSFront > EPS then
				POLY2_B 6, 1+2+4+64,MSFillPen, MSFillBackPen,
						rr*SIN(alpha), rr-rr*COS(alpha), 0,
						0, 			   rr, 				 900,
						0, 			   0, 				 3001,
						0, 			   -MSFront, 		 1,
						0, 			   rr, 				 900,
						(rr+MSFront)*SIN(alpha), rr-(rr+MSFront)*COS(alpha), 3001

				HOTSPOT2  0, -MSFront: unID=unID+1
				HOTSPOT2  (rr+MSFront)*SIN(alpha), rr-(rr+MSFront)*COS(alpha): unID=unID+1
			endif
		endif
	endif
endif



