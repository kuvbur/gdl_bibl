
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 				= -1
_idxTab = 1
TABID_Style				= _idxTab  : _idxTab = _idxTab + 1
TABID_DimMinSpace		= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions		= _idxTab  : _idxTab = _idxTab + 1

pageStartY = 0
pageStartX = 0

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Диванной Секции`
ui_current_page gs_ui_current_page

ui_page TABID_Style, TABID_Root, `Стиль и Ножки`, "uiTab_Style_flat(1)"
	gosub "StylePage"

ui_page TABID_DimMinSpace, TABID_Root, `Размеры и Минимальное Пространство`, "uiTab_Dimension_flat(1)"
	gosub "DimMinSpacePage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationPage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================


! ------------------------------------------------------------------------------
"StylePage":
! ------------------------------------------------------------------------------

	x1 = pageStartX
	x2 = x1 + 78
	x3 = x1 + 123
	x4 = x3 + 90
	
	_outfieldHeight		= 15
	_infieldHeight 		= 19
	_yOffset = 4

	dy 					= 24
	_yCurrent			= pageStartY

	ui_style 0, 1
	ui_outfield `Тип Секции`, pageStartX, pageStartY + _yOffset, 213, _outfieldHeight
	ui_style 0, 0
	
	_yCurrent = _yCurrent + 21
	UI_INFIELD{3} "iSofaUnitType2", 	x2, _yCurrent, 135, 96,
				2,"ui_sofaset2(14)",3,1,
				120,90,120,90,
				1,`Рядная`, SOFAUNIT_STANDARD,
				2,`Скошенная А`, SOFAUNIT_CURVA,
				3,`Скошенная В`, SOFAUNIT_CURVB ui_tooltip `Тип Секции`
	
	_yCurrent = pageStartY + 5 * dy
	ui_outfield `Высота Сидения`, 		x1, _yCurrent + _yOffset, 	x3 - x1 - 3, 	_outfieldHeight
	ui_infield "gs_seat_height", 	x3, _yCurrent, 				x4 - x3, 		_infieldHeight ui_tooltip `Высота Сидения`

	_yCurrent = _yCurrent + dy
	_iFlag = 0 
	if iSofaUnitType2 = 1 then _iFlag = 4

	ui_outfield `Внутренний Радиус`, 	x1, _yCurrent + _yOffset, 	x3 - x1 - 3, 	_outfieldHeight, _iFlag
	ui_infield "radius_i",  		x3, _yCurrent, 				x4 - x3, 		_infieldHeight ui_tooltip `Внутренний Радиус`

	_iFlag = 0

	_yLegMatInf	= pageStartY + 8 * dy

	_yCurrent = _yLegMatInf
	ui_infield "gs_sofa_mat", x1, _yCurrent, 213, 25 ui_tooltip `Диван`

	_yCurrent = _yCurrent + dy + 3
	x_separator = pageStartX + 222	
	ui_separator x_separator, pageStartY, x_separator, _yCurrent
	
	call "ui_furniture_parts" parameters all 	pageStartX	= 231,
												pageStartY	= 0,
												bIsSofaLeg	= 1,
												iLegType	= iLegType,
												yLegMatInf	= _yLegMatInf

return

! ------------------------------------------------------------------------------
"DimMinSpacePage":
! ------------------------------------------------------------------------------
	_infHeight	= 19
	_outfHeight = 15
	_yOffset	= 4
	_outFlag 	= 0

	x1 = 0
	x2 = 228
	y0 = pageStartY + _yOffset

	ui_outfield `Высота Сидения`, 		x1, 	y0, 			124, 	_outfHeight
	ui_infield "gs_seat_height", 	125, 	y0 - _yOffset, 	90, 	_infHeight ui_tooltip `Высота Сидения`


	if not(bLeg) or iLegType = 1 then _outFlag = 4

	ui_outfield `Высота Ножек`, 	x2, 	y0, 			125, 	_outfHeight, _outFlag
	ui_infield "leg_h", 		354, 	y0 - _yOffset, 	90, 	_infHeight ui_tooltip `Высота Ножек`

	_outFlag = 0

	ui_separator 0, y0 + 20, 444, y0 + 20

	if iSofaUnitType2 = SOFAUNIT_STANDARD then

		call "fa_minimalspace" parameters 	MSType	= 6 		! MINSPACE_SOFASET_DIFF_SIDES

	else
		xxPict = 200
		yyPict = pageStartY + 51

		if iSofaUnitType2 = SOFAUNIT_CURVA then
		
			ImageX = 126
			ImageY = 183
		
			ui_pict	1, xxPict, yyPict, ImageX, ImageY, 1					! SofaSet02CurvedA.png
	
			ui_infield "A", 			xxPict+43,		yyPict-21, 			70, _infHeight ui_tooltip `Ширина Дивана` 
			ui_infield "MSRear",		xxPict-73,		yyPict+13, 			70, _infHeight ui_tooltip `Минимальное Пространство Сзади` 
			ui_infield "B", 			xxPict-73,		yyPict+50, 			70, _infHeight ui_tooltip `Глубина Дивана`
			ui_infield "MSFront",		xxPict-73,		yyPict+108, 		70, _infHeight ui_tooltip `Минимальное Пространство Спереди`
			ui_infield "radius_i",		xxPict+10,		yyPict+ImageY+3, 	70, _infHeight ui_tooltip `Внутренний Радиус` 
		endif
	
		if iSofaUnitType2 = SOFAUNIT_CURVB then

			ImageX = 122
			ImageY = 179
		
			ui_pict	2, xxPict, yyPict, ImageX, ImageY, 1					! SofaSet02CurvedB.png
	
			ui_infield "MSRear",		xxPict-73,		yyPict+37, 			70, _infHeight ui_tooltip `Минимальное Пространство Сзади` 
			ui_infield "B", 			xxPict-73,		yyPict+74, 			70, _infHeight ui_tooltip `Глубина Дивана`
			ui_infield "MSFront",		xxPict-73,		yyPict+132, 		70, _infHeight ui_tooltip `Минимальное Пространство Спереди`
			ui_infield "radius_i",		xxPict+10,		yyPict-22,		 	70, _infHeight ui_tooltip `Внутренний Радиус` 
			ui_infield "A", 			xxPict+40,		yyPict+ImageY+2,	70, _infHeight ui_tooltip `Ширина Дивана` 
		endif
	endif

return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 10,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= TABID_2DRepresentation,
													pageStartY			= - 1

return

! ------------------------------------------------------------------------------
"3DRepresentationPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_sofa_mat"
		SurfItemID = SurfItemID + 1

		if bLeg then
			surfacesArray[SurfItemID]	= "gs_leg_mat"
			SurfItemID = SurfItemID + 1
		endif
	
	dim childFlagArray[]
		for k = 1 to vardim1(surfacesArray)
			childFlagArray[k]	= 0
		next k

	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													pageStartY			= 3,
													iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY 	= 0,
										listFieldHeight = 265,
										stDescHeader 	= "",
										stValueHeader 	= "",
										stParamNames 	= stParamNames

return
