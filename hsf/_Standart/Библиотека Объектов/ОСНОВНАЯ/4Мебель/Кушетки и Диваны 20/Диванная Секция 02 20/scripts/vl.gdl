
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0
values{2} "iSofaUnitType2" 	SOFAUNIT_STANDARD, `Рядная`,
							SOFAUNIT_CURVA, `Скошенная А`,
							SOFAUNIT_CURVB, `Скошенная В`
values{2} "iLegType" 	LEGTYPE_1, `Стиль 1`,
						LEGTYPE_2, `Стиль 2`,
						LEGTYPE_3, `Стиль 3`,
						LEGTYPE_4, `Стиль 4`,
						LEGTYPE_5, `Стиль 5`
VALUES "gs_resol" RANGE [3,)

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16

values "A"              range [0.09, )
values "zzyzx"          range [gs_seat_height+0.2, )
values "gs_seat_height" range [leg_h+0.2, )
values "leg_h"          range [0,)

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 0
	if gs_detlevel_3D = `Простой` then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Детальный` then gs_detlevel_3D_m = 2
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = `Откл.`
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = `Детальный`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

IF gs_detlevel_3D_m=1 THEN
	LOCK "gs_resol"
	HIDEPARAMETER "gs_resol"
ENDIF
IF gs_detlevel_3D_m=0 THEN
	LOCK "gs_resol", "gs_shadow"
	HIDEPARAMETER "gs_resol", "gs_shadow"
ENDIF

IF iSofaUnitType2 = SOFAUNIT_STANDARD THEN
	LOCK "radius_i"
	HIDEPARAMETER "radius_i"
ENDIF

if not(bLeg) then
	leg_h = 0
	lock "iLegType", "leg_h"
	hideparameter "iLegType", "leg_h"
endif

IF iLegType = LEGTYPE_1 THEN
	leg_h=0.08
	PARAMETERS leg_h=leg_h
	LOCK "leg_h"
	HIDEPARAMETER "leg_h"
ENDIF

IF abs(leg_h) < EPS THEN
	LOCK "gs_leg_mat"
	HIDEPARAMETER "gs_leg_mat"
ENDIF

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19


!!! ******************************** Minimal Space

IF iSofaUnitType2 <> SOFAUNIT_STANDARD THEN
	HIDEPARAMETER "MSSide1","MSSide2"
	LOCK "MSSide1","MSSide2"
ENDIF

if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif
if MSRear < 0 then
	MSRear = 0
	parameters MSRear = MSRear
endif
if MSSide1 < 0 then
	MSSide1 = 0
	parameters MSSide1 = MSSide1
endif
if MSSide2 < 0 then
	MSSide2 = 0
	parameters MSSide2 = MSSide2
endif


IF GLOB_MODPAR_NAME = "iSofaUnitType2" THEN
	IF iSofaUnitType2 = SOFAUNIT_STANDARD THEN
		PARAMETERS MSSide1=0, MSSide2=0
		MSSide1=0
		MSSide2=0
	endif

endif
