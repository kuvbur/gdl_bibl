
! Designer Sofa 01


if gs_detlevel_3D_m=0 then end

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

IF gs_shadow=0 THEN SHADOW OFF


	unID=1

	!!! *** X Dimension
	HOTSPOT 0,0,0,unID, A,1+256 : unID=unID+1
	HOTSPOT -1,0,0,unID, A,3 : unID=unID+1
	HOTSPOT A,0,0,unID, A,2 : unID=unID+1

	HOTSPOT 0,B,0,unID, A,1+256 : unID=unID+1
	HOTSPOT -1,B,0,unID, A,3 : unID=unID+1
	HOTSPOT A,B,0,unID, A,2 : unID=unID+1

	!!! *** X Dimension
	HOTSPOT 0,0,0,unID, B,1+256 : unID=unID+1
	HOTSPOT 0,-1,0,unID, B,3 : unID=unID+1
	HOTSPOT 0,B,0,unID, B,2 : unID=unID+1

	HOTSPOT A,0,0,unID, B,1+256 : unID=unID+1
	HOTSPOT A,-1,0,unID, B,3 : unID=unID+1
	HOTSPOT A,B,0,unID, B,2 : unID=unID+1

ADD a/2,b/2,0

	HOTSPOT a/2, b/2, 0, unID, zzyzx, 1+128: unID=unID+1
	HOTSPOT a/2, b/2, -1, unID, zzyzx, 3: unID=unID+1
	HOTSPOT a/2, b/2, zzyzx, unID, zzyzx, 2: unID=unID+1

	HOTSPOT -a/2, b/2, 0, unID, zzyzx, 1+128: unID=unID+1
	HOTSPOT -a/2, b/2, -1, unID, zzyzx, 3: unID=unID+1
	HOTSPOT -a/2, b/2, zzyzx, unID, zzyzx, 2: unID=unID+1

xx=0.035
yy=0.03

! HOTSPOT editing ! DETAILED 3D
IF gs_full_edit=1 THEN
	HOTSPOT -a/2+0.15+xx, -b/2+yy, 0, unID, gs_seat_height, 1+128: unID=unID+1
	HOTSPOT -a/2+0.15+xx, -b/2+yy, -1, unID, gs_seat_height, 3: unID=unID+1
	HOTSPOT -a/2+0.15+xx, -b/2+yy, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

	HOTSPOT a/2-0.15-xx, -b/2+yy, 0, unID, gs_seat_height, 1+128: unID=unID+1
	HOTSPOT a/2-0.15-xx, -b/2+yy, -1, unID, gs_seat_height, 3: unID=unID+1
	HOTSPOT a/2-0.15-xx, -b/2+yy, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1
ENDIF

pen gs_cont_pen
SET MATERIAL gs_frame_mat

ADD -a/2,-b/2,0

IF gs_detlevel_3D_m=1 THEN
MATERIAL gs_seat_mat

PRISM_ 9,zzyzx,
		0,0,15,
		0.185,0,15,
		0.185,b-0.1,15,
		a-0.185,b-0.1,15,
		a-0.185,0,15,
		a,0,15,
		a,b,15,
		0,b,15,
		0,0,-1

PRISM_ 5,gs_seat_height,
		0.185,0.03,15,
		a-0.185,0.03,15,
		a-0.185,b-0.1,15,
		0.185,b-0.1,15,
		0.185,0.03,-1
DEL 1
END
ENDIF

RESOL gs_resol

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!! ***** main legs ***** !!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

for i1=1 to 2
	 ADD 0.0875,0.0125,0
			EXTRUDE 2,0,0,zzyzx-0.15-0.05,1+4+16,
				0,0,901,
				0.0125,360,4001

		ADDZ zzyzx-0.15-0.05
		ROTZ 180
			ADDX 0.0375
			ROTZ 90
				REVOLVE 2,90,8,
					0,0.0375,901,
					0.0125,360,4001
			del 2

		ADD 0.0375, 0, 0.0375
		ROTY 90
		ROTZ -90
			ADDx 0.0375
			ROTZ 90
				REVOLVE 2,90,8,
					0,0.0375,901,
					0.0125,360,4001
			del 2

		ADD 0.0375, 0, 0.0375
		ROTY 90
				EXTRUDE 2,0,0,b-0.1,4,
				0,0,901,
				0.0125,360,4001

		ADDZ b-0.1


			ADDx 0.0375
			ROTZ 90
				REVOLVE 2,90,1+2+4+8,
					0,0.0375,901,
					0.0125,360,4001
			del 2
  del 9
mulx -1
addx -a
next i1
del 4



	ADD 0.05, b-0.0125,zzyzx-0.15-0.0125
	ROTY 90
				EXTRUDE 2,0,0,a-0.1,4,
				0,0,901,
				0.0125,360,4001
	del 2


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!! ***** back legs ***** !!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

ADD 0.025+0.005+0.15, b-0.0125, 0

	addz zzyzx-0.15-0.0125
	roty 90
		ARMC 0.0125,0.0125,zzyzx-0.15-0.0125,0,0,90
	del 2
ADDX a-2*(0.025+0.005+0.15)

	addz zzyzx-0.15-0.0125
	roty 90
		ARMC 0.0125,0.0125,zzyzx-0.15-0.0125,0,0,90
	del 2

del 2

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!! ***** skinny bit ***** !!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

for i2=1 to 2
	ADD 0.0875,0.0125,0

	ADDZ leg_h+0.2
	ROTY -90
			EXTRUDE 2,0,0,0.0675,4,
				0,0,901,
				0.005,360,4001

	ADDZ 0.0675
	ROTZ 90
				ADDx 0.015
				ROTZ 90
					REVOLVE 2,90,1+2+4+8,
						0,0.015,901,
						0.005,360,4001
				del 2

	ADD 0.015, 0, 0.015
	ROTY 90
					EXTRUDE 2,0,0,b-0.06,4,
					0,0,901,
					0.005,360,4001

	ADDZ b-0.06

				ADDx 0.015
				ROTZ 90
					REVOLVE 2,90,1+2+4+8,
						0,0.015,901,
						0.005,360,4001
				del 2
	del 8


mulx -1
addx -a
next i2
del 4

ADD 0.02, b-0.0175, leg_h+0.2
ROTY 90
			EXTRUDE 2,0,0,a-0.04,4,
				0,0,901,
				0.005,360,4001
del 2




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!! ***** angled bottom bit ***** !!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! angled bottom bit

	add 0.025,0.025,leg_h
		MATERIAL gs_frame_mat
		PRISM_ 10,0.025,
			0,0,15,
			a-0.05,0,15,
			a-0.05,b-0.05,15,
			0,b-0.05,15,
			0,0,-1,
			0.005,0.005,15,
			a-0.05-0.005,0.005,15,
			a-0.05-0.005,b-0.05-0.005,15,
			0.005,b-0.05-0.005,15,
			0.005,0.005,-1

	del 1

10:
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!! ***** cushions ***** !!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! cushions

ADD 0.03,0.025,leg_h

ROTY -90
ADD 0, 0.005,- 0.15
CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=zzyzx-leg_h,
cush_b=b-0.06,cush_h=0.15,r1=0.02,r2=0.02,gs_resol=gs_resol,cush_mat=gs_seat_mat
DEL 2

ADD 0.155, b-0.055, 0
ROTX 90
CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.06-2*0.15-2*0.005,
cush_b=zzyzx-leg_h,cush_h=0.1,r1=0.02,r2=0.02,gs_resol=gs_resol,cush_mat=gs_seat_mat
DEL 2

ADD a-0.06,0.005,0
ROTY -90
CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=zzyzx-leg_h,
cush_b=b-0.06,cush_h=0.15,r1=0.02,r2=0.02,gs_resol=gs_resol,cush_mat=gs_seat_mat
DEL 2

ADD 0.155,0.005,0
CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.06-2*0.15-2*0.005,
cush_b=b-2*0.03-0.1-0.005,cush_h=gs_seat_height-leg_h-0.15,r1=0.02,r2=0.02,gs_resol=gs_resol,cush_mat=gs_seat_mat

ADDZ gs_seat_height-leg_h-0.15
CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,cush_a=a-0.06-2*0.15-2*0.005,
cush_b=b-2*0.03-0.1-0.005,cush_h=0.15,r1=0.02,r2=0.02,gs_resol=gs_resol,cush_mat=gs_seat_mat
DEL 2

DEL 1
DEL 2

END
