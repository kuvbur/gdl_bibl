
IF gs_detlevel_3D_m = 0 THEN END

IF not(gs_shadow) THEN SHADOW OFF
bDetailed = (gs_detlevel_3D_m = 2)

pen gs_cont_pen

IF bDetailed THEN
	RESOL gs_resol
ELSE
	RESOL 4
ENDIF

matraceThk = 0.15
frameRadius = 0.03
legRadius = 0.025
unID = 1


! --- Hotspots -----------------------------------------------------------------

HOTSPOT 0, 0, 0, unID : unID = unID + 1
HOTSPOT a, 0, 0, unID : unID = unID + 1
HOTSPOT 0, b, 0, unID : unID = unID + 1
HOTSPOT a, b, 0, unID : unID = unID + 1
HOTSPOT 0, 0, zzyzx, unID : unID = unID + 1
HOTSPOT a, 0, zzyzx, unID : unID = unID + 1
HOTSPOT 0, b, zzyzx, unID : unID = unID + 1
HOTSPOT a, b, zzyzx, unID : unID = unID + 1

HOTSPOT 0, 0, 0, unID, gs_seat_height, 1 + 128 : unID = unID + 1
HOTSPOT 0, 0, gs_seat_height, unID, gs_seat_height, 2 : unID = unID + 1
HOTSPOT 0, 0, -1, unID, gs_seat_height, 3 : unID = unID + 1

HOTSPOT a, 0, 0, unID, gs_seat_height, 1 + 128 : unID = unID + 1
HOTSPOT a, 0, gs_seat_height, unID, gs_seat_height, 2 : unID = unID + 1
HOTSPOT a, 0, -1, unID, gs_seat_height, 3 : unID = unID + 1

HOTSPOT 0, b, 0, unID, gs_seat_height, 1 + 128 : unID = unID + 1
HOTSPOT 0, b, gs_seat_height, unID, gs_seat_height, 2 : unID = unID + 1
HOTSPOT 0, b, -1, unID, gs_seat_height, 3 : unID = unID + 1

HOTSPOT a, b, 0, unID, gs_seat_height, 1 + 128 : unID = unID + 1
HOTSPOT a, b, gs_seat_height, unID, gs_seat_height, 2 : unID = unID + 1
HOTSPOT a, b, -1, unID, gs_seat_height, 3 : unID = unID + 1

HOTSPOT 0, b*11/12, 0, unID, zzyzx, 1 + 128 : unID = unID + 1
HOTSPOT 0, b*11/12, zzyzx, unID, zzyzx, 2 : unID = unID + 1
HOTSPOT 0, b*11/12, -1, unID, zzyzx, 3 : unID = unID + 1

HOTSPOT a, b*11/12, 0, unID, zzyzx, 1 + 128 : unID = unID + 1
HOTSPOT a, b*11/12, zzyzx, unID, zzyzx, 2 : unID = unID + 1
HOTSPOT a, b*11/12, -1, unID, zzyzx, 3 : unID = unID + 1


! --- Legs ---------------------------------------------------------------------

MATERIAL gs_leg_mat

ADD a/2, b/2, 0
FOR i = 1 TO 2
	FOR ii = 1 TO 2
		ADD a/2 - 2 * frameRadius, b*27/70, 0
		CYLIND gs_seat_height - matraceThk - frameRadius * 2, legRadius
		DEL 1

		MULY -1
	NEXT ii
	DEL 2

	MULX -1
NEXT i
DEL 3


! --- Frame --------------------------------------------------------------------

MATERIAL gs_frame_mat

IF bDetailed THEN
	PUT 3 * frameRadius, -frameRadius, 0,
		frameRadius, -frameRadius, 1,
		frameRadius, frameRadius, 1001,
		3 * frameRadius, frameRadius, 0,
		3 * frameRadius, -frameRadius, -1
ELSE
	PUT 3 * frameRadius, -frameRadius, 0,
		0, -frameRadius, 0,
		0, frameRadius, 0,
		3 * frameRadius, frameRadius, 0,
		3 * frameRadius, -frameRadius, -1
ENDIF

TUBE (NSP/3), 7, 1+2,

	GET(NSP),

	0, b, gs_seat_height - matraceThk - frameRadius, 0,
	0, 0, gs_seat_height - matraceThk - frameRadius, 0,
	a, 0, gs_seat_height - matraceThk - frameRadius, 0,
	a, b, gs_seat_height - matraceThk - frameRadius, 0,
	0, b, gs_seat_height - matraceThk - frameRadius, 0,
	0, 0, gs_seat_height - matraceThk - frameRadius, 0,
	a, 0, gs_seat_height - matraceThk - frameRadius, 0

rotz 90
gosub 1000
del 1


! --- Bands --------------------------------------------------------------------

IF bDetailed THEN
	MATERIAL gs_matrace_mat
	nBands = 11
	distB = b/35
	ADDZ gs_seat_height - matraceThk - frameRadius * 2
	ROTX -90
	ADDZ 2 * distB
	FOR i = 1 TO nBands
		EXTRUDE 8, 0, 0, distB,
			16+32,
			3 * frameRadius+0.001, -2 * frameRadius-0.001, 1,
			3 * frameRadius+0.001, 0.001, 1,
			frameRadius, 0.001, 1,
			frameRadius, -2 * frameRadius-0.001, 1001,
			a-frameRadius, -2 * frameRadius-0.001, 1,
			a-frameRadius, 0.001, 1001,
			a - 3 * frameRadius-0.001, 0.001, 1,
			a - 3 * frameRadius-0.001, -2 * frameRadius-0.001, 1

		ADDZ 3 * distB
	NEXT i
	DEL nBands + 3
ENDIF


! --- Matrace ------------------------------------------------------------------

ADDZ gs_seat_height - matraceThk + 0.001
CALL "fa_barcelonaCushion" PARAMETERS a 			= a,
					b					= b,
					zzyzx				= matraceThk - 0.001,
					waveZ				= 0.03,
					cushAng				= 0,
					cutAngle			= 0,
					sectX				= 6,
					sectY				= 12,
					mat					= gs_matrace_mat,
					gs_detlevel_3D_m	= gs_detlevel_3D_m

DEL 1


! --- Cushion ------------------------------------------------------------------

MATERIAL gs_cushion_mat

IF bDetailed THEN
	cushionRadius = cushionRadius + 0.006
	ADD 0, b * 11/12, zzyzx - cushionRadius
	rrr = cushionRadius / 10

	REVOLVE 9, 360, 1+2,
		rrr, cushionRadius/3, 601,
		rrr, cushionRadius/3 + rrr, 901,
		0, cushionRadius/3 + rrr, 3001,
		0, cushionRadius - rrr, 1,
		rrr, cushionRadius, 1001,
		a-rrr, cushionRadius, 1,
		a, cushionRadius-rrr, 1001,
		a, cushionRadius/3+rrr, 1,
		a-rrr, cushionRadius/3, 1001

	DEL 1
ELSE
	RESOL 8
	ADD 0, b*11/12, zzyzx - cushionRadius
	ROTY 90
	CYLIND a, cushionRadius
	DEL 2
ENDIF

end


1000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

return

