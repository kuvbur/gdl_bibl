
!!!! Handling overhungs in case of end panels !!!


DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

	pen gs_cont_pen

	unID=1

	HOTSPOT2 0,b,unID: unID=unID+1
	HOTSPOT2 a/2,b,unID: unID=unID+1

	IF cab_size=0 THEN

		CALL "Resize_A_B_ZZYZX" Parameters ALL

		unID=100
		!!! *** Front Overhang of Cabinet
		HOTSPOT2 left_cab,0,unID, front_cab, 1+128 :unID=unID+1
		HOTSPOT2 left_cab,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,front_cab,unID, front_cab, 2: unID=unID+1

		HOTSPOT2 A-right_cab,0,unID, front_cab, 1+128 :unID=unID+1
		HOTSPOT2 A-right_cab,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,front_cab,unID, front_cab, 2: unID=unID+1

		!!! *** Rear Overhang of Cabinet
		HOTSPOT2 left_cab,B,unID, rear_cab, 1+128 :unID=unID+1
		HOTSPOT2 left_cab,B+1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

		HOTSPOT2 A-right_cab,B,unID, rear_cab, 1+128 :unID=unID+1
		HOTSPOT2 A-right_cab,B+1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

	ELSE

		A=a-(left_cab+right_cab)
		B=b-(front_cab+rear_cab)

		ADD2 left_cab,front_cab
		CALL "Resize_A_B_ZZYZX" Parameters ALL
		DEL 1

		A=a+(left_cab+right_cab)
		B=b+(front_cab+rear_cab)

		unID=100

		!!! *** Hotspots on bounding box
		HOTSPOT2 0,B,unID :unID=unID+1
		HOTSPOT2 0,0,unID :unID=unID+1
		HOTSPOT2 A,0,unID :unID=unID+1
		HOTSPOT2 A,B,unID :unID=unID+1

		!! *** Front Overhang of Cabinet
		HOTSPOT2 left_cab+a/2,0,unID, front_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+a/2,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+a/2,front_cab,unID, front_cab, 2: unID=unID+1

		!! *** Rear Overhang of Cabinet
		HOTSPOT2 left_cab+a/2,front_cab+(B-front_cab-rear_cab),unID, rear_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+a/2,front_cab+(B-front_cab-rear_cab)-1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+a/2,front_cab+(B-front_cab-rear_cab)+rear_cab,unID, rear_cab, 2: unID=unID+1

	ENDIF

	PEN gs_cont_pen

	!!! *** Symbol Types
	LINE2 0,0,a,0
	If iCabSymbolType2D <> 1 Then

		IF iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 Then
			LINE2 0,0.025,a,0.025
		ENDIF

		IF iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 Then
				LINE2 0,0,cos(15)*a,-sin(15)*a
				arc2 0,0,a,-15,0
		Endif

		If dob_face = 1 Then

			add2 0,b
			mul2 1,-1

			IF iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 Then LINE2 0,0.025,a,0.025
			IF iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 Then
				LINE2 a,0,a-cos(15)*a,-sin(15)*a
				arc2 a,0,a,180,195
			Endif

			Del 2
		Endif

	Endif

	!!! *** Cabinet
	side_vis2D_m = side_vis2D_m+1
	IF side_vis2D_m=1 THEN
		m=0
		n=0
	ENDIF

	IF side_vis2D_m=2 THEN
		m=0
		n=1
	ENDIF

	IF side_vis2D_m=3 THEN
		m=1
		n=0
	ENDIF

	IF side_vis2D_m=4 THEN
		m=1
		n=1
	ENDIF

	DRAWINDEX 10
	IF bs_h>0 and dob_face=0 THEN LINE2 0,b-0.02,a,b-0.02		!!(Backsplash)
	!!! Fill of Cabinet
	FILL gs_fill_type

	POLY2_B 5, 1+2+64,gs_fill_pen,gs_back_pen,
			0,0,n,
			0,b,1,
			a,b,m,
			a,0,0,
			0,0,-1

	LINE2 0, 0, 0, front_cab - 0.02 + sinkCornerRadiusOut + sinkCornerDistY * (gs_sink_type_m = 2)
	LINE2 A, 0, A, front_cab - 0.02 + sinkCornerRadiusOut + sinkCornerDistY * (gs_sink_type_m = 2)

	HOTLINE2 0,front_cab,0,b
	HOTLINE2 0,b,a,b
	HOTLINE2 a,b,a,front_cab
	HOTLINE2 a,front_cab,0,front_cab

	HOTLINE2 0,front_cab+sink_d-0.02,a,front_cab+sink_d-0.02


	! --- Belfast Sink ---
	DRAWINDEX 50
	ADD2 0,front_cab-0.02
		CALL "fa_belfast_sink" PARAMETERS ALL	a					= a - left_cab - right_cab,
												b					= sink_d,
												zzyzx				= sink_h,
												sinkWallThkSide		= counterWidthOnSide,
												sinkWallThkMiddle	= counterWidthOnSide,
												sinkCornerDistY		= sinkCornerDistY
	DEL 1

	! --- Minimal Space ---
	DRAWINDEX 10
	reqBMinSpace = 0
	call "fa_minimalspace" parameters all MSType = 20, unID = 3000,
							returned_parameters reqBMinSpace,
												MSFillType,
												MSFillPen,
												MSFillBackPen,
												MSContLineType,
												MSContLinePen

ENDIF


! Show MEP Connections -------------------------------------------------------------[

isavailable = 0
isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)
isMEPEnabled = (isavailable OR isInArchiCAD = 0)
if isMEPEnabled then
	bShowMEP = 1
	reqBMep = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showMepConnect", reqBMep)
	if success > 0 then
		bShowMEP = reqBMep
	endif
else
	bShowMEP = 0
endif

! Show MEP Connections -------------------------------------------------------------]

! --- start of modifying to Shaft

IF bShowMEP THEN

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 12,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 12

	DEL 1
ENDIF

! --- end of modifying to Shaft
