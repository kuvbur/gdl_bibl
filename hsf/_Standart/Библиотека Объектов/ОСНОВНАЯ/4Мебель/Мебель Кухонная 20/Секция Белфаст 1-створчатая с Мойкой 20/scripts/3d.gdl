
IF gs_detlevel_3D_m=0 then END
IF not(gs_shadow) THEN SHADOW OFF

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1


! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

! Show MEP Connections -------------------------------------------------------------[

isavailable = 0
isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)
isMEPEnabled = (isavailable OR isInArchiCAD = 0)
if isMEPEnabled then
	bShowMEP = 1
	reqBMep = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showMepConnect", reqBMep)
	if success > 0 then
		bShowMEP = reqBMep
	endif
else
	bShowMEP = 0
endif

! Show MEP Connections -------------------------------------------------------------]

IF bShowMEP THEN

	IF useSysMat THEN
		toe_mat			= sMat
		count_mat		= sMat
		bs_mat			= sMat
		gs_cabinet_mat	= sMat
		doorglas_mat	= sMat
		doorframe_mat	= sMat
		doorframe_mat_end	= sMat
		gs_sink_mat		= sMat
		gs_tap_mat		= sMat
		gs_knob_mat		= sMat
		gs_ShelfMat		= sMat
		DrawerMat		= sMat
		gs_con_mat		= sMat
	ENDIF

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 16
ENDIF

! --- end of modifying to Shaft


IF gs_detlevel_3D_m=1 THEN gs_resol = 4
RESOL gs_resol



unID=1

!!!! Handling overhungs in case of end panels !!!
if dob_face then rear_cab = rear_cab
if rear_panel then rear_cab = rear_cab + 0.02
if left_panel then left_cab = 0.02	!! (no left overhang)
if right_panel then right_cab = 0.02	!! (no right overhang)


PEN gs_cont_pen
MATERIAL gs_cabinet_mat
ADD 0.02,front_cab+fr_th1,zzyzx-0.02-sink_h-count_th
	PRISM_ 5,0.02,
			0,0,15,
			a-0.04,0,11,
			a-0.04,b-0.02-front_cab-rear_cab-fr_th1-((fr_th1-0.02)*dob_face),15,
			0,b-0.02-front_cab-rear_cab-fr_th1-((fr_th1-0.02)*dob_face),11,
			0,0,-1
DEL 1

ADD 0,0,zzyzx-sink_h-count_th
ROTY 90

CUTPOLYA 7,1,0,
		-1,0,15,
		0,0,15,
		0,sink_d+front_cab-0.02,15,
		-sink_h,sink_d+front_cab-0.02,15,
		-sink_h,sink_d+front_cab-0.02,15,
		-1,sink_d+front_cab,15,
		-1,0,-1

DEL 1
DEL 1
!!!!****************************************** CALL GENERAL BASE CABINET

	CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx,
			gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=dob_face, count_th=count_th,
			front_cab=front_cab, rear_cab=rear_cab, left_cab=left_cab,
			right_cab=right_cab, toe_h=toe_h, side_vis_m=side_vis_m+1,
			sink=0, shelf_num=shelf_num, gs_ShelfMat=gs_ShelfMat,
			gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
			toe_mat=toe_mat, gs_cabinet_mat=gs_cabinet_mat, drawer_h=sink_h,
			gs_toe_type_m=gs_toe_type_m,toe_d = toe_d,fr_th1=fr_th1,  ToeHtps3D=1,
			basecab_unID=unID,
			returned_parameters unID


!!!!****************************************** CALL COUNTERTOP and SINK

CUTEND

CUTPOLYA 5,1,0,
		counterWidthOnSide + left_cab, 0, 15,
		counterWidthOnSide + left_cab, sink_d + front_cab - 0.02 - counterWidthOnSide, 15,
		a - counterWidthOnSide - right_cab, sink_d + front_cab - 0.02 - counterWidthOnSide, 15,
		a - counterWidthOnSide - right_cab, 0, 15,
		counterWidthOnSide + left_cab, 0, -1

CALL "fa_counter_sink" PARAMETERS ALL	side_vis_m		= side_vis_m + 1,
										bBelfastSink	= 1

CUTEND

ADD left_cab,front_cab-0.02,zzyzx-sink_h-count_th
	CALL "fa_belfast_sink" PARAMETERS ALL	a					= a - left_cab - right_cab,
											b					= sink_d,
											zzyzx				= sink_h,
											sinkWallThkSide		= counterWidthOnSide,
											sinkWallThkMiddle	= counterWidthOnSide,
											sinkCornerDistY		= sinkCornerDistY,
											count_th			= count_th * (gs_sink_type_m <> 3 and gs_sink_type_m <> 5 and gs_sink_type_m <> 6)
DEL 1

ADDZ zzyzx-sink_h-count_th

DEL 1




!!!!!****************************************** CALL DRAWER and DOOR PANEL

if dob_face=1 then
		MULY -1
		ADD left_cab,-b+rear_cab+fr_th1,ZZYZX-count_th-sink_h		!*** DRAWER
		GOSUB 10
		DEL 2
endif

FOR ii=1 TO (dob_face)+1									!*** double face
	ADD left_cab,front_cab+fr_th1,toe_h

	if gs_door_type_m > 0 then GOSUB 20						!*** FRONT DOOR

	DEL 1

	door_open=0


	MULy -1
	ADDy -front_cab
	ADD	0,-B+rear_cab,0
	mulx -1
	addx -A
NEXT ii
DEL (dob_face+1)*5


!!!!!****************************************** CALL END PANELS



	IF rear_panel=1 and dob_face=0 then
			door_w=a-left_cab-right_cab
			door_h=ZZYZX-toe_h-count_th

		ADD door_w+left_cab,b-rear_cab,toe_h
		ROTZ 180

			GOSUB 40
		DEL 2

	ENDIF


	IF left_panel=1 then	!*** LEFT END PANEL
		door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face) : door_h=ZZYZX-toe_h-count_th
		ADD left_cab,door_w+front_cab+fr_th1,toe_h
		ROTZ -90
			GOSUB 40
		DEL 2
	ENDIF

	IF right_panel=1 then
		door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face): door_h=ZZYZX-toe_h-count_th
		ADD a-right_cab,front_cab+fr_th1,toe_h
		ROTZ 90
			GOSUB 40
		DEL 2
	ENDIF

END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!! *** RETURN *** !!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!****************************************** DRAWER
10:

cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=a-left_cab-right_cab, door_h=sink_h, gs_door_type_m=1,gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,door_ang=0,
			doorframe_mat=doorframe_mat, gs_knob_type_m=0,fr_th1=fr_th1,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
		returned_parameters cabdoortyp_unID

unID = cabdoortyp_unID


RETURN

!!!!!****************************************** FRONT DOOR
20:

cabdoortyp_unID=unID


	CALL "fa_cabdoortyp" PARAMETERS ALL	door_w=a-left_cab-right_cab,cabdoortyp_unID=cabdoortyp_unID,
		gs_bevel=gs_bevel,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

RETURN

!!!!!****************************************** END PANELS
40:
		!! on rear panel there is no glas and grilles and knobs!!
		gs_knob_type_m=0
		If iEndPanelType = 1 	then gs_door_type_m=1
		If iEndPanelType = 2 	then gs_door_type_m=2
		If iEndPanelType = 4 	then gs_door_type_m=11
		If iEndPanelType = 7 	then gs_door_type_m=4
		If iEndPanelType = 9 	then gs_door_type_m=6
		If iEndPanelType = 99 	then gs_door_type_m=10
		gs_full_edit=0

	door_ang=0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS ALL cabdoortyp_unID=cabdoortyp_unID,
		fr_th1=0.02,gs_cust_p=gs_cust_p_end,doorframe_w=doorframe_w_end,
		doorframe_mat=doorframe_mat_end,doorglas_mat=doorframe_mat_end,
		gs_bevel=gs_bevel,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

RETURN
