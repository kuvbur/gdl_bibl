
!! Shelf Base End.gsm

pen gs_cont_pen
reqBMinSpace = 0
call "fa_minimalspace" parameters all MSType = 0, unID = 3000,
							returned_parameters reqBMinSpace,
												MSFillType,
												MSFillPen,
												MSFillBackPen,
												MSContLineType,
												MSContLinePen


! 2D Hotspot

a=a+front_cab
b=b+front_cab

	unID=1

	IF cab_size=1 THEN
		a=a-front_cab
		b=b-front_cab
		ADD2 0,front_cab
	ENDIF

	HOTSPOT2 0, B, unID, A, 1+256	:	unID=unID+1
	HOTSPOT2 -0.001, B, unID, A, 3+128	:	unID=unID+1
	HOTSPOT2 A, B, unID, A, 2	:	unID=unID+1

	HOTSPOT2 0, 0, unID, A, 1+256	:	unID=unID+1
	HOTSPOT2 -0.001, 0, unID, A, 3	:	unID=unID+1
	HOTSPOT2 A, 0, unID, A, 2+128	:	unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+256	:	unID=unID+1
	HOTSPOT2 0, -0.001, unID, B, 3+128	:	unID=unID+1
	HOTSPOT2 0, B, unID, B, 2	:	unID=unID+1

	HOTSPOT2 A, 0, unID, B, 1+128	:	unID=unID+1
	HOTSPOT2 A, -0.001, unID, B, 3	:	unID=unID+1
	HOTSPOT2 A, B, unID, B, 2	:	unID=unID+1


	IF cab_size=1 THEN
		a=a+front_cab
		b=b+front_cab
		DEL 1
	ENDIF

	IF cab_size=0 THEN
		HOTSPOT2 0, front_cab,unID: unID=unID+1
		HOTSPOT2 A-front_cab, B,unID: unID=unID+1
	ELSE
		HOTSPOT2 0, 0,unID: unID=unID+1
		HOTSPOT2 A, B,unID: unID=unID+1
	ENDIF




	a=a-front_cab
	b=b-front_cab

! END of 2D Hotspot

ADD2 0,front_cab

IF side_vis2D_m=0 THEN 2
IF side_vis2D_m=1 THEN 1
LINE2 -0, b,   a+front_cab, b

1:
IF side_vis2D_m=2 THEN 2
LINE2 0, -front_cab,   0, b

2:
IF bs_h<=0 OR side_vis2D_m<2 THEN 3
LINE2 0, b-0.02,   a+front_cab, b-0.02

3:

if end_typ_m = END_CHAMFERED then goto 10:
if end_typ_m = END_TRIANGLE then goto 20:
if end_typ_m = END_ROUND then goto 30:


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!! *** CHAMFERED *** !!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
10:


fill gs_fill_type

poly2_b 5, 2+4+64,gs_fill_pen,gs_back_pen,
	0,-front_cab,1,
	a/2+xx2,-front_cab,1,
	a+front_cab,b/2-yy2,1,
	a+front_cab,b,1,
	0,b,1

	HOTSPOT2 a/2, 0,unID: unID=unID+1
	HOTSPOT2 a, b/2,unID: unID=unID+1

if front_cab>0 then
	HOTSPOT2 a/2+xx2, -front_cab,unID: unID=unID+1
	HOTSPOT2 a+front_cab, b/2-yy2,unID: unID=unID+1
endif

LINE2 0, -front_cab,   a/2+xx2, -front_cab
LINE2 a/2+xx2, -front_cab,   a+front_cab, b/2-yy2
LINE2 a+front_cab, b/2-yy2,   a+front_cab, b

HOTLINE2 0, -front_cab,   a/2+xx2, -front_cab
HOTLINE2 a/2+xx2, -front_cab,   a+front_cab, b/2-yy2
HOTLINE2 a+front_cab, b/2-yy2,   a+front_cab, b
HOTLINE2 a+front_cab,b,0,b
HOTLINE2 0,b,0,-front_cab


!!! *** Minimal Space
unID = 3000
IF reqBMinSpace <> 0 THEN
		FILL MSFillType
		line_type MSContLineType
		pen MSContLinePen

		POLY2_B 9, 1+2+4+64,MSFillPen,MSFillBackPen,
				a+front_cab,			b,					0,
				a+front_cab,			b/2-yy2,			0,
				a/2+xx2,				-front_cab,			0,
				0,						-front_cab,			1,
				0,						-MSFront-front_cab,	1,
				a/2+xx2+xx3,			-MSFront-front_cab,	1,
				a+front_cab+MSFront,	b/2-yy2-yy3,		1,
				a+MSFront+front_cab,	b,					1,
				a+front_cab,			b,					-1

	HOTSPOT2 0, -MSFront-front_cab,unID: unID=unID+1
	HOTSPOT2 a/2+xx2+xx3, -MSFront-front_cab,unID: unID=unID+1
	HOTSPOT2 a+front_cab+MSFront, b/2-yy2-yy3,unID: unID=unID+1
	HOTSPOT2 a+MSFront+front_cab, b,unID: unID=unID+1
ENDIF
END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!! *** TRIANGLE *** !!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

20:

LINE2 0, -front_cab,   0.02+xx2, -front_cab
LINE2 0.02+xx2, -front_cab,   a+front_cab, b-0.02-yy2
LINE2 a+front_cab, b-0.02-yy2,   a+front_cab, b

HOTLINE2 0.02+xx2, -front_cab,   a+front_cab, b-0.02-yy2
HOTLINE2 0,-front_cab,0,b
HOTLINE2 a+front_cab,b,0,b

fill gs_fill_type
	poly2_b 5, 2+4+64,gs_fill_pen,gs_back_pen,
		0,-front_cab,1,
		0.02+xx2, -front_cab,1,
		a+front_cab, b-0.02-yy2,1,
		a+front_cab,b,1,
		0,b,1

	HOTSPOT2 0.02, 0,unID: unID=unID+1
	HOTSPOT2 a, b-0.02,unID: unID=unID+1

if front_cab>0 then
	HOTSPOT2 0.02+xx2, -front_cab,unID: unID=unID+1
	HOTSPOT2 a+front_cab, b-0.02-yy2,unID: unID=unID+1
endif

!!! *** Minimal Space
unID = 3000
IF reqBMinSpace <> 0 THEN
		FILL MSFillType
		line_type MSContLineType
		pen MSContLinePen

		POLY2_B 9, 1+2+4+64,MSFillPen,MSFillBackPen,
				a+front_cab,			b,					1,
				a+front_cab,			b-0.02-yy2,			1,
				0.02+xx2,				-front_cab,			1,
				0,						-front_cab,			1,
				0,						-MSFront-front_cab,	1,
				0.02+xx2+xx3,			-MSFront-front_cab,	1,
				a+front_cab+MSFront,	b-0.02-yy2-yy3,		1,
				a+MSFront+front_cab,	b,					1,
				a+front_cab,			b,					-1

	HOTSPOT2 0, -MSFront-front_cab,unID: unID=unID+1
	HOTSPOT2 0.02+xx2+xx3, -MSFront-front_cab,unID: unID=unID+1
	HOTSPOT2 a+front_cab+MSFront, b-0.02-yy2-yy3,unID: unID=unID+1
	HOTSPOT2 a+MSFront+front_cab, b,unID: unID=unID+1
ENDIF
END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!! *** ROUND *** !!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
30:

LINE2 0, -front_cab,   a-RR, -front_cab
ARC2 a-RR,RR,   RR+front_cab, 270, 360
LINE2 a+front_cab, RR,   a+front_cab, b

HOTLINE2 a+front_cab, RR,   a+front_cab, b
HOTLINE2 a+front_cab,b,0,b
HOTLINE2 0,b,0,-front_cab

fill gs_fill_type

poly2_b 7, 2+4+64,gs_fill_pen,gs_back_pen,
	0,-front_cab,1,
	a-RR, -front_cab,1,
	a-RR,RR,900,
	0,90,4000,
	a+front_cab, RR,1,
	a+front_cab,b,1,
	0,b,1

	HOTSPOT2 a,  RR,unID: unID=unID+1
	HOTSPOT2 a-RR, 0,unID: unID=unID+1
	HOTSPOT2 a-RR, -front_cab,unID: unID=unID+1

if front_cab>0 then
	HOTSPOT2 a+front_cab,  RR,unID: unID=unID+1
endif

!!! *** Minimal Space
unID = 3000
IF reqBMinSpace <> 0 THEN
		FILL MSFillType
		line_type MSContLineType
		pen MSContLinePen
		POLY2_B 13, 1+2+4+64,MSFillPen,MSFillBackPen,
				0,-front_cab,1,
				a-RR, -front_cab,1,
				a-RR,RR,900,
				0,90,4001,
				a+front_cab, RR,1,
				a+front_cab,b,1,
				a+front_cab+MSFront,  B,1,
				a+front_cab+MSFront,  RR,1,
				a-RR,RR,900,
				0,-90,4001,
				a-RR, -MSFront-front_cab,1,
				0, -MSFront-front_cab,1,
				0,-front_cab,-1

	HOTSPOT2 0, -MSFront-front_cab,unID: unID=unID+1
	HOTSPOT2 a-RR, -MSFront-front_cab,unID: unID=unID+1
	HOTSPOT2 a+front_cab+MSFront,  RR,unID: unID=unID+1
	HOTSPOT2 a+front_cab+MSFront,  B,unID: unID=unID+1
ENDIF

DEL 1



end
