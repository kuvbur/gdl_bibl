
IF gs_detlevel_3D_m=0 then END
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

IF gs_detlevel_3D_m=1 THEN RESOL 4
IF gs_detlevel_3D_m=2 THEN RESOL gs_resol

if gs_detlevel_3D_m = 1 then 
	if gs_count_type_m # 0 then gs_count_type_m = 1
endif

IF not(gs_shadow) THEN SHADOW OFF


pen gs_cont_pen

c=zzyzx


ADDY front_cab

side_vis2D_m = side_vis_m

ADDZ toe_h+0.02
MATERIAL gs_cabinet_mat
PRISM_ 7, zzyzx-count_th-toe_h-0.04,             !! SIDES
		0, 0, 10,
		0.02, 0, 15,
		0.02, b-0.02, 15,
		a, b-0.02, 10,
		a, b, 10,
		0, b, 10,
		0, 0, -1

gosub 1000
del 1


IF bs_h>0 & gs_count_type_m # 0 THEN
	ADDZ zzyzx

	CPRISM_ bs_mat, bs_mat, bs_mat, 5, bs_h,              !! BACK SPLASH
			0, b, 13+2*((side_vis2D_m=1) OR (side_vis2D_m=3)),
			a+front_cab+temp_front_cab, b, 15,
			a+front_cab+temp_front_cab, b-0.02, 15,
			0, b-0.02, 8+7*((side_vis2D_m=1) OR (side_vis2D_m=3)),
			0, b, -1

	del 1
ENDIF

!!! 3D Hotspot !!!

unID=1

HOTSPOT 0, toe_d, 0, unID : unID=unID+1
HOTSPOT a-toe_d, b, 0, unID : unID=unID+1
HOTSPOT 0, b, 0, unID : unID=unID+1

hotspot a+front_cab, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a+front_cab, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a+front_cab, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, 0-front_cab, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, 0-front_cab, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, 0-front_cab, zzyzx, unID, zzyzx, 2 : unID=unID+1

if end_typ_m = END_CHAMFERED then
	HOTSPOT A/2-xx1, toe_d, 0, unID : unID=unID+1
	HOTSPOT A-toe_d, B/2+yy1, 0, unID : unID=unID+1
ENDIF

unID=100
If bs_h>0 Then
	HOTSPOT a+front_cab, b, zzyzx+bs_h, unID : unID=unID+1
	HOTSPOT 0, b, zzyzx+bs_h, unID : unID=unID+1
EndIf

unID=200
If toe_h>0 Then
	hotspot 0, toe_d, 0, unID, toe_h, 1+128 : unID=unID+1
	hotspot 0, toe_d, -1, unID, toe_h, 3 : unID=unID+1
	hotspot 0, toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot a-toe_d, b, 0, unID, toe_h, 1+128 : unID=unID+1
	hotspot a-toe_d, b, -1, unID, toe_h, 3 : unID=unID+1
	hotspot a-toe_d, b, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot 0, b, 0, unID, toe_h, 1+128 : unID=unID+1
	hotspot 0, b, -1, unID, toe_h, 3 : unID=unID+1
	hotspot 0, b, toe_h, unID, toe_h, 2 : unID=unID+1
EndIf

if end_typ_m = END_CHAMFERED then goto 10:
if end_typ_m = END_TRIANGLE then goto 20:
if end_typ_m = END_ROUND then goto 30:


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!! *** CHAMFERED *** !!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
10:


	MATERIAL toe_mat

	if gs_toe_type_m = 1 then

		EXTRUDE 6, 0, 0, toe_h, 55,               !! TOE
				0, toe_d, 0,
				a/2-xx1, toe_d, 0,
				a-toe_d, b/2+yy1, 0,
				a-toe_d, b, 0,
				0, b, 0,
				0, toe_d, -1

		gosub 1000

	else

		MATERIAL toe_mat

		r_leg=0.02

		add r_leg, r_leg+toe_d,0
		gosub 50

		add r_leg, B-r_leg,0
		gosub 50

		add a-r_leg-toe_d, B-r_leg,0
		gosub 50
	endif


	ADDZ toe_h+0.02

	ADDZ -0.02
	CPRISM_  gs_cabinet_mat, gs_cabinet_mat ,gs_cabinet_mat, 8, 0.02,      !! BOTTOM SHELF
			0, 0, 11,
			0.02, 0, 13,
			a/2, 0, 15,
			a, b/2, 15,
			a, b-0.02, 9,
			a, b, 11,
			0, b, 11,
			0, 0, -1

	gosub 3000

	ADDZ (c-count_th-toe_h-0.02)/(shelf_num+1)

	MATERIAL gs_ShelfMat
	FOR x=1 TO shelf_num
		EXTRUDE 6, 0, 0, 0.02, 55,              !! SHELVES
			0.02, 0, 0,
			a/2, 0, 0,
			a, b/2, 0,
			a, b-0.02, 0,
			0.02, b-0.02, 0,
			0.02, 0, -1
		gosub 3000
		ADDZ (c-count_th-toe_h-0.02)/(shelf_num+1)
	NEXT x

	MATERIAL gs_cabinet_mat
	PRISM_ 8, 0.02,
			0, 0, 14,
			0.02, 0, 13,
			a/2, 0, 15,
			a, b/2, 15,
			a, b-0.02, 12,
			a, b, 14,
			0, b, 14,
			0, 0, -1

	gosub 3000


	DEL x

	ADDZ zzyzx-count_th-toe_h
		if gs_count_type_m = 1 then
			CPRISM_ count_mat, count_mat, count_mat, 6, count_th,                  !! COUNTER
					0, -front_cab, 13+2*((side_vis2D_m=1) OR (side_vis2D_m=3)),
					a/2+xx2, -front_cab, 15,
					a+front_cab, b/2-yy2, 15,
					a+front_cab, b, 8+7*(side_vis2D_m>1),
					0, b, 8+5*((side_vis2D_m=1) OR (side_vis2D_m=3))+2*(side_vis2D_m=3),
					0, -front_cab, -1
		endif
		if gs_count_type_m = 2 then

			CPRISM_ count_mat, count_mat, count_mat, 6, count_th,                  !! COUNTER
				0, -front_cab+rr4, 13+2*((side_vis2D_m=1) OR (side_vis2D_m=3)),
				a/2+xx2-xx4, -front_cab+rr4, 15,
				a+front_cab-rr4, b/2-yy2+yy4, 15,
				a+front_cab-rr4, b, 8+7*(side_vis2D_m>1),
				0, b, 8+5*((side_vis2D_m=1) OR (side_vis2D_m=3))+2*(side_vis2D_m=3),
				0, -front_cab+rr4, -1

			RESOL gs_resol
			addz count_th
			MATERIAL count_mat
			TUBE 6,6, 3,
					0,0,1,
					0,-count_th,1,
					-rr4,-count_th,1,
					-rr4,-rr4,1,
					0,-rr4,901,
					rr4,-90,4001,

					-1, -front_cab+rr4,0,0,
					0, -front_cab+rr4,0,0,
					a/2+xx2-xx4, -front_cab+rr4,0,0,
					a+front_cab-rr4, b/2-yy2+yy4,0,0,
					a+front_cab-rr4, b,0,0,
					a+front_cab-rr4, b+1,0,0

			del 1
			RESOL gs_resol*4
		endif
	del 1

gosub 3000

del top

end



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!! *** TRIANGLE *** !!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

20:
	addz toe_h

	PRISM_  6, 0.02,            !! BOTTOM Cabinet
			0, 0, 11,
			0.02, 0, 13,
			a, b-0.02, 9,
			a, b, 11,
			0, b, 11,
			0, 0, -1

	gosub 3000

	addz zzyzx-toe_h-0.02-count_th
	PRISM_ 6, 0.02,                     !! TOP Cabinet
			0, 0, 14,
			0.02, 0, 13,
			a, b-0.02, 12,
			a, b, 14,
			0, b, 14,
			0, 0, -1

	gosub 3000


	addz 0.02
	if gs_count_type_m = 1 then
		CPRISM_ count_mat, count_mat, count_mat, 6, count_th,                  !! COUNTER
				0, -front_cab, 13+2*((side_vis2D_m=1) OR (side_vis2D_m=3)),
				0.02+xx2, -front_cab, 15,
				a+front_cab, b-0.02-yy2, 15,
				a+front_cab, b, 8+7*(side_vis2D_m>1),
				0, b, 8+5*((side_vis2D_m=1) OR (side_vis2D_m=3))+2*(side_vis2D_m=3),
				0, -front_cab, -1
	endif
	if gs_count_type_m = 2 then
		CPRISM_ count_mat, count_mat, count_mat, 6, count_th,
				0, -front_cab+rr4, 13+2*((side_vis2D_m=1) OR (side_vis2D_m=3)),
				0.02+xx2-xx4, -front_cab+rr4, 15,
				a+front_cab-rr4, b-0.02-yy2+yy4, 15,
				a+front_cab-rr4, b, 8+7*(side_vis2D_m>1),
				0, b, 8+5*((side_vis2D_m=1) OR (side_vis2D_m=3))+2*(side_vis2D_m=3),
				0, -front_cab+rr4, -1

		RESOL gs_resol
		addz count_th
		MATERIAL count_mat
		TUBE 6,6, 3,
				0,0,1,
				0,-count_th,1,
				-rr4,-count_th,1,
				-rr4,-rr4,1,
				0,-rr4,901,
				rr4,-90,4001,

				-1, -front_cab+rr4,0,0,
				0, -front_cab+rr4,0,0,
				0.02+xx2-xx4, -front_cab+rr4,0,0,
				a+front_cab-rr4, b-0.02-yy2+yy4,0,0,
				a+front_cab-rr4, b,0,0,
				a+front_cab-rr4, b+1,0,0
		del 1
		RESOL gs_resol*4
	endif
	del 1

	del 2

	gosub 3000

	if gs_toe_type_m = 1 then
		CPRISM_ toe_mat, toe_mat, toe_mat, 4, toe_h,                  !! Toe
				0, toe_d, 15,
				a-toe_d, b, 15,
				0, b, 15,
				0, toe_d, -1
		gosub 3000
	else
		MATERIAL toe_mat

		r_leg=0.02

		add r_leg, r_leg+toe_d,0
		gosub 50

		add r_leg, B-r_leg,0
		gosub 50

		add a-r_leg-toe_d, B-r_leg,0
		gosub 50
	endif

!!!!!!!!!!!!!!!!!! *** Shelves/ Triangle *** !!!!!!!!!!!!!
ADDZ (zzyzx-toe_h-0.02-count_th)/(shelf_num+1)+toe_h

MATERIAL gs_ShelfMat
FOR x=1 TO (shelf_num)
	EXTRUDE 4, 0, 0, 0.02, 55,
		0.02, 0, 0,
		a, b-0.02, 0,
		0.02, b-0.02, 0,
		0.02, 0, -1
	ADDZ (zzyzx-toe_h-0.02-count_th)/(shelf_num+1)
NEXT x
del shelf_num
del 1

gosub 3000

end

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!! *** ROUND *** !!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
30:

	RESOL gs_resol*4
	addz toe_h
	PRISM_  10, 0.02,            !! BOTTOM of Cabinet
			0, 0, 11,
			0.02, 0, 13,
			a-RR,0,13,
			a-RR,RR,900,
			0,90,4000,
			a,RR,13,
			a, b-0.02, 9,
			a, b, 11,
			0, b, 11,
			0, 0, -1

	gosub 3000

	addz zzyzx-toe_h-count_th-0.02
	PRISM_ 10, 0.02,                     !! TOP of Cabinet
			0, 0, 14,
			0.02, 0, 13,
			a-RR,0,13,
			a-RR,RR,900,
			0,90,4000,
			a,RR,13,
			a, b-0.02, 12,
			a, b, 14,
			0, b, 14,
			0, 0, -1

	gosub 3000


	!!! Curved Resolution
	S=90/(gs_resol)

	IF gs_count_type_m = 1 THEN
		FOR k=0 TO 90 STEP S
			PUT a-RR+(RR+front_cab)*sin(k),RR-(RR+front_cab)*cos(k),79
		NEXT k
	ENDIF
	IF gs_count_type_m = 2 THEN
		FOR k=0 TO 90 STEP S
			PUT a-RR+(RR+front_cab-rr4)*sin(k),RR-(RR+front_cab-rr4)*cos(k),79
		NEXT k
	ENDIF


	addz 0.02
	IF gs_count_type_m = 1 THEN
		CPRISM_ count_mat, count_mat, count_mat, 8+NSP/3, count_th,         !! COUNTER
				0, -front_cab, 13+2*((side_vis2D_m=1) OR (side_vis2D_m=3)),
				0.02, -front_cab, 13,
				a-RR,-front_cab,13,
				GET(NSP),
				a+front_cab,RR,13,
				a+front_cab, b-0.02, 13,
				a+front_cab, b, 8+7*((side_vis2D_m=2) OR (side_vis2D_m=3)),
				0, b, 8+7*((side_vis2D_m=1) OR (side_vis2D_m=3)),
				0, -front_cab, -1

	ENDIF
	IF gs_count_type_m = 2 THEN
		CPRISM_ count_mat, count_mat, count_mat, 8+NSP/3, count_th,         !! COUNTER
				0, -front_cab+rr4, 13+2*((side_vis2D_m=1) OR (side_vis2D_m=3)),
				0.02, -front_cab+rr4, 13,
				a-RR,-front_cab+rr4,13,
				GET(NSP),
				a+front_cab-rr4,RR,13,
				a+front_cab-rr4, b-0.02, 13,
				a+front_cab-rr4, b, 8+7*((side_vis2D_m=2) OR (side_vis2D_m=3)),
				0, b, 8+7*((side_vis2D_m=1) OR (side_vis2D_m=3)),
				0, -front_cab+rr4, -1


		FOR k=0 TO 90 STEP S
			PUT a-RR+(RR+front_cab-rr4)*sin(k),RR-(RR+front_cab-rr4)*cos(k),0,0
		NEXT k

		RESOL gs_resol
		addz count_th
		MATERIAL count_mat
		TUBE 6,8+NSP/4, 3,
				0,0,1,
				0,-count_th,1,
				-rr4,-count_th,1,
				-rr4,-rr4,1,
				0,-rr4,901,
				rr4,-90,4001,

				-1, -front_cab+rr4,0,0,
				0, -front_cab+rr4,0,0,
				0.02, -front_cab+rr4, 0,0,
				a-RR,-front_cab+rr4,0,0,
				GET(NSP),
				a+front_cab-rr4,RR,0,0,
				a+front_cab-rr4, b-0.02, 0,0,
				a+front_cab-rr4, b, 0,0,
				a+front_cab-rr4, b+1, 0,0
		del 1

		RESOL gs_resol*4

	endif
	del 1


	gosub 3000

	del 2


	if toe_h>0 then
		IF gs_toe_type_m = 1 then

		CPRISM_ toe_mat, toe_mat, toe_mat, 10, toe_h,         !! TOE
			  0, toe_d, 15,
			  0.02, toe_d, 13,
			  a-RR,toe_d,13,
			  a-RR,RR,900,
			  0,90,4000,
			  a-toe_d,RR,13,
		  a-toe_d, b-0.02, 13,
			  a-toe_d, b, 15,
			  0, b, 15,
			  0, toe_d, -1

		gosub 3000

	else

		MATERIAL toe_mat

		r_leg=0.02

		add r_leg, r_leg+toe_d,0
		gosub 50

		add r_leg, B-r_leg,0
		gosub 50

		add a-r_leg-toe_d, B-r_leg,0
		gosub 50
	endif
endif

!!!!!!!!!!!!!!!!!! *** Shelves/ Round *** !!!!!!!!!!!!!
ADDZ (zzyzx-toe_h-0.02-count_th)/(shelf_num+1)+toe_h

MATERIAL gs_ShelfMat
FOR x=1 TO (shelf_num)
	PRISM_ 8, 0.02,                     !! TOP Cabinet
		0.02, 0, 15,
		a-RR,0,13,
		a-RR,RR,900,
		0,90,4000,
		a,RR,13,
		a, b-0.02, 12,
		0.02, b-0.02, 12,
		0.02, 0, -1
	ADDZ (zzyzx-toe_h-0.02-count_th)/(shelf_num+1)
NEXT x
del shelf_num
del 1

gosub 3000


40:

DEL TOP


END


50:

if gs_toe_type_m = 2 then
	RESOL gs_resol
	cylind toe_h, r_leg
	RESOL gs_resol*4
endif
if gs_toe_type_m = 3 then
	prism_ 4,toe_h,
			-r_leg,-r_leg,15,
			r_leg,-r_leg,15,
			r_leg,r_leg,15,
			-r_leg,r_leg,15
endif
gosub 1000
del 1

RETURN


1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

3000:
rotz 90
gosub 5000
del 1
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

