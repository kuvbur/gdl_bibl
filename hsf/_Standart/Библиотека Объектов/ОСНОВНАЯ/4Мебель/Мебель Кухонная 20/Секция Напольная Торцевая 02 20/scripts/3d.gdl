
IF gs_detlevel_3D_m=0 then END
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

IF gs_detlevel_3D_m=1 THEN RESOL 4
IF gs_detlevel_3D_m=2 THEN RESOL gs_resol

if gs_detlevel_3D_m = 1 then 
	if gs_count_type_m # 0 then gs_count_type_m = 1
endif

IF not(gs_shadow) THEN SHADOW OFF


pen gs_cont_pen

c=zzyzx

ADDY front_cab
ADDZ toe_h+0.02
MATERIAL gs_cabinet_mat
PRISM_ 5, zzyzx-count_th-toe_h-0.04,             !! SIDES
	0, 0, 15,
	0.02, 0, 15,
	0.02, b-2*front_cab, 15,
	0, b-2*front_cab, 15,
	0, 0, -1

IF DivPanel=1 THEN
	For k=1 to shelf_num+1
		Addy (B-2*front_cab)/2-0.01
		PRISM_ 5, (c-count_th-toe_h-0.04-shelf_num*0.02)/(shelf_num+1),             !! SIDES
			0.02, 0, 15,
			a/2, 0, 15,
			a/2, 0.02, 15,
			0.02, 0.02, 15,
			0.02, 0, -1
		DEL 1
		Addz ((c-count_th-toe_h-0.04-shelf_num*0.02)/(shelf_num+1))+0.02
	NEXT k
	DEL shelf_num+1
ENDIF

gosub 1000
del 1
DEL 1

if end_typ_m = END_CHAMFERED then gosub "ChamferedEndType"
if end_typ_m = END_ROUND then gosub "RoundEndType"

! ================================================================================
END ! END  ! END ! END ! END ! END ! END ! END ! END ! END ! END ! END ! END ! END
! ================================================================================


! ================================================================================
 "ChamferedEndType":
! ================================================================================

	MATERIAL toe_mat

	if gs_toe_type_m = 1 then

		EXTRUDE 7, 0, 0, toe_h, 55,               !! TOE
				0, front_cab+toe_d, 0,
				a/2-xx2-xx1, front_cab+toe_d, 0,
				a-front_cab-toe_d, b/4+yy2+yy1, 0,
				a-front_cab-toe_d, b*3/4-yy2-yy1, 0,
				a/2-xx2-xx1, b-front_cab-toe_d, 0,
				0, b-front_cab-toe_d, 0,
				0, front_cab+toe_d, -1

		gosub 1000

	else

		MATERIAL toe_mat


		r_leg=0.02

		add r_leg, r_leg+toe_d+front_cab,0
		gosub "ToeType"

		add r_leg, B-r_leg-front_cab-toe_d,0
		gosub "ToeType"

		add a-r_leg-toe_d-front_cab, B/2,0
		gosub "ToeType"
	endif


	ADDZ toe_h+0.02

	ADDZ -0.02

	CPRISM_  gs_cabinet_mat, gs_cabinet_mat ,gs_cabinet_mat,
			9, 0.02,      !! BOTTOM SHELF
			0, front_cab, 11,
			0.02, front_cab, 13,
			a/2-xx2, front_cab, 15,
			a-front_cab, b/4+yy2, 15,
			a-front_cab, b*3/4-yy2, 15,
			a/2-xx2, b-front_cab, 15,
			0.02, b-front_cab, 13,
			0, b-front_cab, 11,
			0, front_cab, -1

	gosub 3000

	ADDZ (c-count_th-toe_h-0.02)/(shelf_num+1)

	MATERIAL gs_ShelfMat
	FOR x=1 TO shelf_num
		EXTRUDE 7, 0, 0, 0.02, 55,              !! SHELVES
				0.02, front_cab, 0,
				a/2-xx2, front_cab, 0,
				a-front_cab, b/4+yy2, 0,
				a-front_cab, b*3/4-yy2, 0,
				a/2-xx2, b-front_cab, 0,
				0.02, b-front_cab, 0,
				0.02, front_cab, -1
		gosub 3000
		ADDZ (c-count_th-toe_h-0.02)/(shelf_num+1)
	NEXT x

	MATERIAL gs_cabinet_mat
	PRISM_ 9, 0.02,
			0, front_cab, 14,
			0.02, front_cab, 13,
			a/2-xx2, front_cab, 15,
			a-front_cab, b/4+yy2, 15,
			a-front_cab, b*3/4-yy2, 15,
			a/2-xx2, b-front_cab, 15,
			0.02, b-front_cab, 13,
			0, b-front_cab, 14,
			0, front_cab, -1

	gosub 3000


	DEL x

	ADDZ zzyzx-count_th-toe_h
	if gs_count_type_m = 1 then
		CPRISM_ count_mat, count_mat, count_mat, 7, count_th,                  !! COUNTER
				0, 0, 13 + 2*(side_vis_m - 2),
				a/2, 0, 15,
				a, b/4, 15,
				a, b*3/4, 15,
				a/2, b, 15,
				0, b, 8 + 7*(side_vis_m - 2),
				0, 0, -1
	endif
	IF gs_count_type_m = 2 Then
		CPRISM_ count_mat, count_mat, count_mat, 7, count_th,                  !! COUNTER
			0, rr4, 13,
			a/2-xx4, rr4, 15,
			a-rr4, b/4+yy4, 15,
			a-rr4, b*3/4-yy4, 15,
			a/2-xx4, b-rr4, 15,
			0, b-rr4, 8 + 7*(side_vis_m - 2),
			0, rr4, -1

		RESOL gs_resol
		addz count_th
		MATERIAL count_mat

		TUBE 6,8, 3 + 16*(side_vis_m - 2) + 32*(side_vis_m - 2),

				0,0,1,
				0,-count_th,1,
				-rr4,-count_th,1,
				-rr4,-rr4,1,
				0,-rr4,901,
				rr4,-90,4001,

				-1, rr4,0,0,
				0, rr4,0,0,
				a/2-xx4, rr4,0,0,
				a-rr4, b/4+yy4,0,0,
				a-rr4, b*3/4-yy4,0,0,
				a/2-xx4, b-rr4,0,0,
				0, b-rr4,0,0,
				-1, b-rr4,0,0

		del 1
		RESOL gs_resol*4
	endif
	del 1

	gosub 3000

	del top


	! --- 3D Hotspot ---

	unID=1

	if gs_count_type_m=0 then
		TEMP=1
	else
		TEMP=0
	endif

	!!! *** ZZYZX
	hotspot 0,front_cab*TEMP,  0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0,front_cab*TEMP,  -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0,front_cab*TEMP,  zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot a/2-xx2*TEMP, front_cab*TEMP, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a/2-xx2*TEMP, front_cab*TEMP, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a/2-xx2*TEMP, front_cab*TEMP, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot a-front_cab*TEMP, b/4+yy2*TEMP, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a-front_cab*TEMP, b/4+yy2*TEMP, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a-front_cab*TEMP, b/4+yy2*TEMP, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot a-front_cab*TEMP, b*3/4-yy2*TEMP, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a-front_cab*TEMP, b*3/4-yy2*TEMP, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a-front_cab*TEMP, b*3/4-yy2*TEMP, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot a/2-xx2*TEMP, b-front_cab*TEMP, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a/2-xx2*TEMP, b-front_cab*TEMP, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a/2-xx2*TEMP, b-front_cab*TEMP, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot 0, b-front_cab*TEMP, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, b-front_cab*TEMP, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, b-front_cab*TEMP, zzyzx, unID, zzyzx, 2 : unID=unID+1

	if gs_toe_type_m <> 0 then
		unID=100

		hotspot 0, front_cab+toe_d, 0, unID, toe_h, 1 : unID=unID+1
		hotspot 0, front_cab+toe_d, -1, unID, toe_h, 3 : unID=unID+1
		hotspot 0, front_cab+ toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

		hotspot 0, b-front_cab-toe_d, 0, unID, toe_h, 1 : unID=unID+1
		hotspot 0, b-front_cab-toe_d, -1, unID, toe_h, 3 : unID=unID+1
		hotspot 0, b-front_cab-toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

		hotspot a-front_cab-toe_d, b/4+yy2+yy1, 0, unID, zzyzx, 1 : unID=unID+1
		hotspot a-front_cab-toe_d, b/4+yy2+yy1, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot a-front_cab-toe_d, b/4+yy2+yy1, toe_h, unID, zzyzx, 2 : unID=unID+1

		hotspot a-front_cab-toe_d, b*3/4-yy2-yy1, 0, unID, zzyzx, 1 : unID=unID+1
		hotspot a-front_cab-toe_d, b*3/4-yy2-yy1, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot a-front_cab-toe_d, b*3/4-yy2-yy1, toe_h, unID, zzyzx, 2 : unID=unID+1

	else
		unID=200

		HOTSPOT 0,front_cab,0, unID	: unID=unID+1
		HOTSPOT a/2-xx2,front_cab,0, unID	: unID=unID+1
		HOTSPOT a-front_cab,b/4+yy2,0, unID	: unID=unID+1
		HOTSPOT a-front_cab,b*3/4-yy2,0, unID	: unID=unID+1
		HOTSPOT a/2-xx2,b-front_cab,0, unID	: unID=unID+1
		HOTSPOT 0,b-front_cab,0, unID	: unID=unID+1
	endif

return

! ================================================================================
"RoundEndType":
! ================================================================================

	RESOL gs_resol*4
	addz toe_h
	PRISM_  13, 0.02,            !! BOTTOM of Cabinet
			0, front_cab, 11,
			0.02, front_cab, 79,
			a-RR,front_cab,79,
			a-RR,RR,900,
			0,90,4000,
			a-front_cab,RR,79,
			a-front_cab,B-RR,79,
			a-RR,B-RR,900,
			0,90,4000,
			a-RR,B-front_cab,79,
			0.02, b-front_cab, 9,
			0, b-front_cab, 11,
			0, front_cab, -1


	gosub 3000

	addz zzyzx-toe_h-count_th-0.02
	PRISM_ 13, 0.02,                     !! TOP of Cabinet
			0, front_cab, 15,
			0.02, front_cab, 79,
			a-RR,front_cab,79,
			a-RR,RR,900,
			0,90,4000,
			a-front_cab,RR,79,
			a-front_cab,B-RR,79,
			a-RR,B-RR,900,
			0,90,4000,
			a-RR,B-front_cab,79,
			0.02, b-front_cab, 79,
			0, b-front_cab, 14,
			0, front_cab, -1

	gosub 3000


	!!! Curved Resolution
	S=90/(gs_resol)

	IF gs_count_type_m = 1 THEN
		FOR k=0 TO 90 STEP S
			PUT a-RR+(RR)*sin(k),RR-(RR)*cos(k),79
		NEXT k
		FOR k=90 TO 180 STEP S
			PUT a-RR+(RR)*sin(k),B-RR-(RR)*cos(k),79
		NEXT k
	ENDIF
	IF gs_count_type_m = 2 THEN
		FOR k=0 TO 90 STEP S
			PUT a-RR+(RR-rr4)*sin(k),RR-(RR-rr4)*cos(k),79
		NEXT k
		FOR k=90 TO 180 STEP S
			PUT a-RR+(RR-rr4)*sin(k),B-RR-(RR-rr4)*cos(k),79
		NEXT k
	ENDIF


	addz 0.02


	IF gs_count_type_m = 1 THEN
		CPRISM_ count_mat, count_mat, count_mat, 7+NSP/3, count_th,         !! COUNTER
				0, 0, 13,
				a-RR,0,79,
				GET(NSP/2),
				a,RR,79,
				a, b-RR, 79,
				GET(NSP),
				a-RR, b,79,
				0, b, 8+7*(side_vis_m - 2),
				0, 0, -1
	ENDIF
	IF gs_count_type_m = 2 THEN
		CPRISM_ count_mat, count_mat, count_mat, 7+NSP/3, count_th,         !! COUNTER
				0, rr4, 13,
				a-RR, rr4, 13,
				GET(NSP/2),
				a-rr4,RR,13,
				a-rr4, b-RR, 13,
				GET(NSP),
				a-RR, b-rr4, 13,
				0, b-rr4, 8+7*(side_vis_m - 2),
				0, rr4, -1


		FOR k=0 TO 90 STEP S
			PUT a-RR+(RR-rr4)*sin(k),RR-(RR-rr4)*cos(k),0,0
		NEXT k

		FOR k=90 TO 180 STEP S
			PUT a-RR+(RR-rr4)*sin(k),B-RR-(RR-rr4)*cos(k),0,0
		NEXT k

		RESOL gs_resol
		addz count_th
		MATERIAL count_mat
		TUBE 6,9+NSP/4, 3+16*(side_vis_m - 2) + 32*(side_vis_m - 2),
				0,0,1,
				0,-count_th,1,
				-rr4,-count_th,1,
				-rr4,-rr4,1,
				0,-rr4,901,
				rr4,-90,4001,

				-1, rr4,0,0,
				0, rr4,0,0,
				0.02, rr4, 0,0,
				a-RR,rr4,0,0,
				GET(NSP/2),
				a-rr4,RR,0,0,
				a-rr4,B-RR,0,0,
				GET(NSP),
				a-RR,B-rr4,0,0,
				0,B-rr4,0,0,
				-1,B-rr4,0,0

		del 1

		RESOL gs_resol*4

	ENDIF
	del 1


	gosub 3000

	del 2


	IF gs_toe_type_m = 1 Then

	CPRISM_ toe_mat, toe_mat, toe_mat, 13, toe_h,         !! TOE
			0, front_cab+toe_d, 13+2 * (side_vis_m - 2),
			0.02, front_cab+toe_d, 79,
			a-RR,front_cab+toe_d,79,
			a-RR,RR,900,
			0,90,4000,
			a-front_cab-toe_d,RR,79,
			a-front_cab-toe_d,B-RR,79,
			a-RR,B-RR,900,
			0,90,4000,
			a-RR,B-front_cab-toe_d,79,
			0.02, b-front_cab-toe_d, 79,
			0, b-front_cab-toe_d, 8+7*(side_vis_m - 2),
			0, front_cab+toe_d, -1

	gosub 3000

	Else

		MATERIAL toe_mat

		r_leg=0.02

		add r_leg, r_leg+toe_d+front_cab,0
			gosub "ToeType"

		add r_leg, B-r_leg-front_cab-toe_d,0
			gosub "ToeType"

		add a-r_leg-toe_d-front_cab, B/2,0
			gosub "ToeType"
	endif


!!!!!!!!!!!!!!!!!! *** Shelves/ Round *** !!!!!!!!!!!!!
ADDZ (zzyzx-toe_h-0.02-count_th)/(shelf_num+1)+toe_h

MATERIAL gs_ShelfMat
	FOR x=1 TO (shelf_num)
	PRISM_ 11, 0.02,
		0.02, front_cab, 79,
		a-RR,front_cab,79,
		a-RR,RR,900,
		0,90,4000,
		a-front_cab,RR,79,
		a-front_cab,B-RR,79,
		a-RR,B-RR,900,
		0,90,4000,
		a-RR,B-front_cab,79,
		0.02, b-front_cab, 79,
		0.02, front_cab, -1

	ADDZ (zzyzx-toe_h-0.02-count_th)/(shelf_num+1)
	NEXT x
	del shelf_num
del 1

gosub 3000

DEL TOP


!!! 3D Hotspot !!!

unID=1


	IF gs_count_type_m=0 THEN
		TEMP=1
		ELSE
		TEMP=0
	ENDIF

	!!! *** ZZYZX
	hotspot 0,front_cab*TEMP,  0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0,front_cab*TEMP,  -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0,front_cab*TEMP,  zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot a-front_cab*TEMP, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a-front_cab*TEMP, b/2, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a-front_cab*TEMP, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot 0, b-front_cab*TEMP, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, b-front_cab*TEMP, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, b-front_cab*TEMP, zzyzx, unID, zzyzx, 2 : unID=unID+1

if gs_toe_type_m <> 0 then
	unID=100

	hotspot 0, front_cab+toe_d, 0, unID, toe_h, 1 : unID=unID+1
	hotspot 0, front_cab+toe_d, -1, unID, toe_h, 3 : unID=unID+1
	hotspot 0, front_cab+ toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot 0, b-front_cab-toe_d, 0, unID, toe_h, 1 : unID=unID+1
	hotspot 0, b-front_cab-toe_d, -1, unID, toe_h, 3 : unID=unID+1
	hotspot 0, b-front_cab-toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot a-front_cab-toe_d, b/2, 0, unID, zzyzx, 1 : unID=unID+1
	hotspot a-front_cab-toe_d, b/2, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a-front_cab-toe_d, b/2, toe_h, unID, zzyzx, 2 : unID=unID+1

else
	unID=200

	HOTSPOT 0,front_cab,0, unID	: unID=unID+1
	HOTSPOT a-front_cab,b/2,0, unID	: unID=unID+1
	HOTSPOT 0,b-front_cab,0, unID	: unID=unID+1

endif


return

! ================================================================================
"ToeType":
! ================================================================================


	if gs_toe_type_m = 2 then
		RESOL gs_resol
		cylind toe_h, r_leg
		RESOL gs_resol*4
	endif
	if gs_toe_type_m = 3 then
		prism_ 4,toe_h,
				-r_leg,-r_leg,15,
				r_leg,-r_leg,15,
				r_leg,r_leg,15,
				-r_leg,r_leg,15
	endif
	gosub 1000
	del 1

	return

! ================================================================================
! Texture Subroutines
! ================================================================================

1000:
	roty 90
	gosub 5000
	del 1
	return

2000:
	gosub 5000
	return

3000:
	rotz 90
	gosub 5000
	del 1
	return

5000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return
