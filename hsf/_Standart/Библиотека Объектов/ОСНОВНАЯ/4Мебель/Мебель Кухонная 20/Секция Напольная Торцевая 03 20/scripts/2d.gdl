
! very similar 2D look to Shelf Wall Chamfered

!=== Minimal Space ===
reqBMinSpace = 0
call "fa_minimalspace" parameters all MSType = 0, unID = 3000,
						returned_parameters reqBMinSpace,
											MSFillType,
											MSFillPen,
											MSFillBackPen,
											MSContLineType,
											MSContLinePen

IF reqBMinSpace <> 0 THEN
	drawindex 10
	FILL MSFillType
	line_type MSContLineType
	pen MSContLinePen
	POLY2_B 5, 1+2+4+64,MSFillPen,MSFillBackPen,
			0,	0,			1,
			0, 	-MSFront, 	1,
			A, 	B-depth_cab-FrontY*cab_size-MSFront, 	1,
			A, 	B-depth_cab-FrontY*cab_size, 			0,
			0, 	0, 	-1

ENDIF

pen gs_cont_pen
fill gs_fill_type
line_type gs_line_type


poly2_b 5, 1+2+64,gs_fill_pen,gs_back_pen,
		0,0,1,
		a,b-depth_cab-FrontY*cab_size,0+1*((side_vis2D_m=2) OR (side_vis2D_m=3)),
		a,b,1,
		0,b,0+1*((side_vis2D_m=1) OR (side_vis2D_m=3)),
		0,0,1

IF bs_h > 0 THEN
	LINE2 0,B-0.02,A,B-0.02
ENDIF

IF cab_size THEN
	a=a-right_cab
	b=b-front_cab
	ADD2 0,front_cab
ENDIF

unID=1

HOTSPOT2 0, B, unID, A, 1+256	:	unID=unID+1
HOTSPOT2 -0.001, B, unID, A, 3+128	:	unID=unID+1
HOTSPOT2 A, B, unID, A, 2	:	unID=unID+1

HOTSPOT2 0, 0, unID, A, 1+256	:	unID=unID+1
HOTSPOT2 -0.001, 0, unID, A, 3	:	unID=unID+1
HOTSPOT2 A, 0, unID, A, 2+128	:	unID=unID+1

HOTSPOT2 0, 0, unID, B, 1+256	:	unID=unID+1
HOTSPOT2 0, -0.001, unID, B, 3+128	:	unID=unID+1
HOTSPOT2 0, B, unID, B, 2	:	unID=unID+1

HOTSPOT2 A, 0, unID, B, 1+128	:	unID=unID+1
HOTSPOT2 A, -0.001, unID, B, 3	:	unID=unID+1
HOTSPOT2 A, B, unID, B, 2	:	unID=unID+1

HOTSPOT2 A, b, unID, depth_cab, 1+128			:	unID=unID+1
HOTSPOT2 A, b+1, unID, depth_cab, 3				:	unID=unID+1
HOTSPOT2 A, b-depth_cab, unID, depth_cab, 2	:	unID=unID+1

IF cab_size THEN
	HOTSPOT2 0, -front_cab,unID: unID=unID+1
	HOTSPOT2 A+right_cab, B-depth_cab-FrontY,unID: unID=unID+1
	HOTSPOT2 A+right_cab, B,unID: unID=unID+1
ELSE
	HOTSPOT2 0, front_cab, unID: unID=unID+1
	HOTSPOT2 A-right_cab, B-depth_cab+FrontY, unID: unID=unID+1
	HOTSPOT2 A-right_cab, B, unID: unID=unID+1
ENDIF

IF reqBMinSpace <> 0 THEN
	unID = 3000
	IF cab_size THEN
		HOTSPOT2 0, -front_cab-MSFront,unID: unID=unID+1
	else
		HOTSPOT2 0, -MSFront,unID: unID=unID+1
	endif
	HOTSPOT2 a,B-depth_cab-FrontY*cab_size-MSFront,unID: unID=unID+1
ENDIF

