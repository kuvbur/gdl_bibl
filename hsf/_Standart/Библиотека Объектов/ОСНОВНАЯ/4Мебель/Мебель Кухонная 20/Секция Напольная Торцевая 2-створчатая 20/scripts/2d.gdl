
reqBMinSpace = 0
call "fa_minimalspace" parameters all MSType = 0, unID = 3000,
						returned_parameters reqBMinSpace,
											MSFillType,
											MSFillPen,
											MSFillBackPen,
											MSContLineType,
											MSContLinePen
unID=1

PEN gs_cont_pen
if iEndCabinetStyle = CABEND_ANGULAR then
	IF cab_size=0 THEN					! angular end

		!!! *** Cabinet Width
		HOTSPOT2 0,B,unID, A, 1+256 :unID=unID+1
		HOTSPOT2 -1,B,unID, A, 3: unID=unID+1
		HOTSPOT2 A,B,unID, A, 2: unID=unID+1

		!!! *** Cabinet Depth
		HOTSPOT2 0,0,unID, B, 1+256 :unID=unID+1
		HOTSPOT2 0,-1,unID, B, 3: unID=unID+1
		HOTSPOT2 0,B,unID, B, 2: unID=unID+1

		HOTSPOT2 A,0,unID, B, 1+128 :unID=unID+1
		HOTSPOT2 A,-1,unID, B, 3: unID=unID+1
		HOTSPOT2 A,B,unID, B, 2: unID=unID+1

		HOTSPOT2 left_cab, 0,unID: unID=unID+1
		HOTSPOT2 left_cab+amx, amy,unID: unID=unID+1
		HOTSPOT2  A, B-right_cab,unID: unID=unID+1

		HOTSPOT2 0, front_cab,unID: unID=unID+1
		HOTSPOT2 left_cab, front_cab,unID: unID=unID+1
		HOTSPOT2 left_cab+amxc, front_cab+amxc*TAN(fi),unID: unID=unID+1
		HOTSPOT2  A-front_cab, B-right_cab,unID: unID=unID+1
		HOTSPOT2  A-front_cab, B,unID: unID=unID+1
	ELSE

		a=a-(left_cab+front_cab)
		b=b-(right_cab+front_cab)

		!!! *** Cabinet Width
		HOTSPOT2 0,B,unID, A, 1+256 :unID=unID+1
		HOTSPOT2 -1,B,unID, A, 3: unID=unID+1
		HOTSPOT2 A,B,unID, A, 2: unID=unID+1

		!!! *** Cabinet Depth
		HOTSPOT2 0,0,unID, B, 1+256 :unID=unID+1
		HOTSPOT2 0,-1,unID, B, 3: unID=unID+1
		HOTSPOT2 0,B,unID, B, 2: unID=unID+1

		HOTSPOT2 A,0,unID, B, 1+128 :unID=unID+1
		HOTSPOT2 A,-1,unID, B, 3: unID=unID+1
		HOTSPOT2 A,B,unID, B, 2: unID=unID+1

		HOTSPOT2 -left_cab,0,unID: unID=unID+1

		HOTSPOT2 amx-(0.002*sqr(2)), amy-front_cab+(0.002*sqr(2)),unID: unID=unID+1

		HOTSPOT2 A,B+right_cab,unID: unID=unID+1

		HOTSPOT2 -left_cab,-front_cab,unID: unID=unID+1
		HOTSPOT2 0,-front_cab,unID: unID=unID+1
		HOTSPOT2 amx, amy-front_cab,unID: unID=unID+1
		HOTSPOT2 A+front_cab,B,unID: unID=unID+1
		HOTSPOT2 A+front_cab,B+right_cab,unID: unID=unID+1
		HOTSPOT2 -left_cab,B+right_cab,unID: unID=unID+1

		a=a+left_cab+front_cab
		b=b+right_cab+front_cab
	ENDIF

	ADD2 a/2-left_cab*cab_size, b/2-front_cab*cab_size

	FILL gs_fill_type
	POLY2_B 7, 2+64, gs_fill_pen, gs_back_pen,
			-a/2, 				-b/2, 			1,
			-a/2+left_cab, 		-b/2, 			1,
			-a/2+left_cab+amx, 	-b/2+amy, 		1,
			a/2, 				b/2-right_cab, 	1,
			a/2, 				b/2, 			1,
			-a/2, 				b/2, 			1,
			-a/2, 				-b/2, 			-1

	HOTLINE2 -a/2, -b/2,-a/2+left_cab, -b/2
	HOTLINE2 -a/2+left_cab, -b/2,-a/2+left_cab+amx, -b/2+amy
	HOTLINE2 -a/2+left_cab+amx, -b/2+amy,a/2, b/2-right_cab
	HOTLINE2 a/2, b/2-right_cab,a/2, b/2
	HOTLINE2 a/2, b/2,-a/2, b/2
	HOTLINE2 -a/2, b/2,-a/2, -b/2

	POLY2_ 7, 1,
			-a/2, 				-b/2, 			1,
			-a/2+left_cab, 		-b/2, 			1,
			-a/2+left_cab+amx, 	-b/2+amy, 		1,
			a/2, 				b/2-right_cab, 	1,
			a/2, 				b/2, 			0,
			-a/2, 				b/2, 			0,
			-a/2, 				-b/2, 			-1
	side_vis2D_m = side_vis2D_m+1
	IF side_vis2D_m=2 OR side_vis2D_m=4 THEN LINE2 -a/2, -b/2, -a/2, b/2
	IF side_vis2D_m=3 OR side_vis2D_m=4 THEN LINE2 -a/2,  b/2,  a/2, b/2

	IF side_vis2D_m=3 OR side_vis2D_m=4 THEN
		IF bs_h>0 AND ABS(right_cab)<EPS THEN
			LINE2	-a/2, b/2-0.02, a/2-0.02*TAN(45-fi), b/2-0.02
		ENDIF
	ENDIF

	if iCabSymbolType2D <> 1 then
		x1 = -a/2+left_cab+amx
		y1 = -b/2+amy
		dx = 0.025/COS(22.5)
		x2 = x1-dx*SIN(45)
		y2 = y1+dx*SIN(45)
		if iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 then
				LINE2 -a/2+left_cab, -b/2+dx, x2, y2
				LINE2 x1, y1, x2, y2
				LINE2 x2, y2, a/2-dx, b/2-right_cab
		endif

		if  iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 then
			doorlen1 = SQR((x1-(-a/2+left_cab))^2+(y1-(-b/2))^2)
			add2 -a/2+left_cab, -b/2
			rot2 fi-15
			arc2 0,0,doorlen1,0,15
			LINE2 0,0,doorlen1,0
			del 2

			doorlen2 = SQR((x1-(a/2))^2+(y1-(b/2-right_cab))^2)
			add2 a/2, b/2-right_cab
			rot2 -120+fi
			arc2 0,0,doorlen1,-15,0
			LINE2 0,0,doorlen2,0
			del 2
		endif
	endif

	IF reqBMinSpace <> 0 then
		FILL MSFillType
		pen MSContLinePen
		line_type MSContLineType

		POLY2_B 11, 1+2+4+64,MSFillPen,MSFillBackPen,
				-a/2, 			-b/2-MSFront,1,
				-a/2+left_cab, -b/2-MSFront,1,
				-a/2+left_cab+amxms, -b/2-MSFront+amyms,1,
				 a/2+MSFront, 	b/2-right_cab,	1,
				 a/2+MSFront, 	b/2,			1,
				 a/2, 			b/2,			1,
				 a/2, 			b/2-right_cab,	1,
				-a/2+left_cab+amx, -b/2+amy,	1,
				-a/2+left_cab, 	-b/2,			1,
				-a/2, 			-b/2,			1,
				-a/2, 			-b/2-MSFront,	-1

		HOTSPOT2 -a/2, -b/2-MSFront,unID: unID=unID+1
		HOTSPOT2 -a/2+left_cab+amxms, -b/2-MSFront+amyms,unID: unID=unID+1
		HOTSPOT2  a/2+MSFront, b/2,unID: unID=unID+1

		PEN gs_cont_pen
		line_type 1
	ENDIF

	del 1
ELSE
	unID=100
	!*********************************** radius end

	if left_cab<eps and left_panel=0 then
		if right_cab<eps and right_panel=0 then
			y1=0
			x1=0
		else
			y1=right_panel*0.02+right_cab
			x1=0
		endif
	else
		if right_cab<eps and right_panel=0 then
			y1=0
			x1=left_panel*0.02+left_cab
		else
			y1=right_panel*0.02+right_cab
			x1=left_panel*0.02+left_cab
		endif
	endif
	IF cab_size=1 THEN
		HOTSPOT2 0,B+front_cab
		HOTSPOT2 0,front_cab
		HOTSPOT2 A,B+front_cab

		!************* A=cabinet size

		POLY2_ 7, 1,
			0, front_cab+B+y1, 0,
			x1+A+front_cab, front_cab+B+y1, 1,
			x1+A+front_cab, front_cab+B, 1,
			x1+A+front_cab, B-1000, 800,
			x1, 0, 1001,
			0, 0, 0,
			0, front_cab+B+y1, -1

		HOTLINE2 0, 0, 0, front_cab+B+y1
		HOTLINE2 0, front_cab+B+y1, x1+A+front_cab, front_cab+B+y1
		side_vis2D_m = side_vis2D_m + 1
		IF side_vis2D_m=2 OR side_vis2D_m=4 THEN LINE2 0, 0, 0, front_cab+B+y1
		IF side_vis2D_m=3 OR side_vis2D_m=4 THEN LINE2 0, front_cab+B+y1,
												x1+A+front_cab, front_cab+B+y1

		!*** cabinet depth
		A=A+front_cab
		HOTSPOT2 x1,front_cab+B, unID, A, 1+128 :unID=unID+1
		HOTSPOT2 -1000,front_cab+B,unID, A, 3: unID=unID+1
		HOTSPOT2 x1+A,front_cab+B,unID, A, 2: unID=unID+1
		A=A-front_cab

		HOTSPOT2 0,	0,unID, B, 1+256 :unID=unID+1
		HOTSPOT2 0,-1000,unID, B, 3: unID=unID+1
		HOTSPOT2 0, B+front_cab,unID, B, 2: unID=unID+1

		if x1>0 or front_cab>0 then hotspot2 0, 0, unID : unID=unID+1
		if y1>0 or front_cab>0 then hotspot2 0, front_cab+B+y1, unID : unID=unID+1
		if x1>0 or y1>0 or front_cab>0 then
			hotspot2 x1+A+front_cab, front_cab+B+y1, unID : unID=unID+1
		endif

		FILL gs_fill_type

		POLY2_B 7, 2+4+64, gs_fill_pen, gs_back_pen,
			0, front_cab+B+y1, 0,
			x1+A+front_cab, front_cab+B+y1, 1,
			x1+A+front_cab, front_cab+B, 1,
			x1+A+front_cab, B-1000, 800,
			x1, 0, 1001,
			0, 0, 0,
			0, front_cab+B+y1, -1

		if iCabSymbolType2D <> 1 then
			if iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 then
				add2 0, front_cab+B
				rot2 -90
				arc2 0, 0, A+front_cab-0.025, 0, 90
				del 2
			endif

			if  iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 then
				calcA = A+front_cab
				len = SQR((calcA-(calcA/SIN(45))*SIN(30))^2+(-(calcA/SIN(45))*COS(30)+calcA)^2)
				alpha = 180-2*ATN((len/2)/(len/10))
				rr = (len/2)/SIN(alpha)

				add2 calcA, front_cab+B
				rot2 -90+15
				arc2 0, -calcA, calcA, 0, 90
				add2 calcA, -calcA
				rot2 alpha-142.5
				arc2 0, -rr, rr, alpha*2, 90
				del 4
			endif
		endif


		IF reqBMinSpace <> 0 then
			FILL MSFillType
			pen MSContLinePen
			line_type MSContLineType
			POLY2_B 11, 1+2+4+64,MSFillPen,MSFillBackPen,
					x1+A+front_cab, front_cab+B+y1, 	1,
					x1+A+front_cab, front_cab+B, 		1,
					x1+A+front_cab, B-1000, 			800,
					x1, 			0, 					1001,
					0, 				0, 					1,
					0, 				-MSFront, 			1,
					x1, 			-MSFront, 			1,
					x1+1000, 		-MSFront, 			800,
					x1+A+front_cab+MSFront, front_cab+B, 1001,
					x1+A+front_cab+MSFront, front_cab+B+y1, 1,
					x1+A+front_cab, front_cab+B+y1,		-1

			HOTSPOT2 x1+A+front_cab+MSFront, front_cab+B,unID: unID=unID+1
			HOTSPOT2 x1+A+front_cab+MSFront, front_cab+B+y1,unID: unID=unID+1
			HOTSPOT2 x1, -MSFront, unID: unID=unID+1
			HOTSPOT2 0, -MSFront, unID: unID=unID+1
			HOTSPOT2 x1+(A+front_cab+MSFront)*sin(45),
				front_cab+B-(A+front_cab+MSFront)*sin(45), unID: unID=unID+1
			PEN gs_cont_pen
			line_type 1
		ENDIF
	else
		HOTSPOT2 0,B
		HOTSPOT2 0,front_cab
		HOTSPOT2 A-front_cab,B

		!************* A=counter size

		POLY2_ 7, 1,
			0, B, 0,
			A, B, 1,
			A, B-y1, 1,
			A, B-1000, 800,
			x1, 0, 1001,
			0, 0, 0,
			0, B, -1


		HOTLINE2 0, 0, 0, B
		HOTLINE2 0, B,  A, B

		!*** cabinet depth

		HOTSPOT2 0,0,unID, B, 1+256 :unID=unID+1
		HOTSPOT2 0,-1000,unID, B, 3: unID=unID+1
		HOTSPOT2 0, B,unID, B, 2: unID=unID+1

		!*** cabinet depth

		HOTSPOT2 0,B, unID, A, 1+128 :unID=unID+1
		HOTSPOT2 -1000,B,unID, A, 3: unID=unID+1
		HOTSPOT2 A,B,unID, A, 2: unID=unID+1

		if x1>0 then hotspot2 x1, 0, unID : unID=unID+1
		if y1>0 then hotspot2 A, B-y1, unID : unID=unID+1
		if x1>0 or y1>0 then hotspot2 x1, B-y1, unID : unID=unID+1

		FILL gs_fill_type

		POLY2_B 7, 2+4+64, gs_fill_pen, gs_back_pen,
			0, B, 1,
			A, B, 1,
			A, B-y1, 1,
			A, B-1000, 800,
			x1, 0, 1001,
			0, 0, 1,
			0, B, -1
		side_vis2D_m = side_vis2D_m + 1
		IF side_vis2D_m=2 OR side_vis2D_m=4 THEN LINE2 0, 0, 0, B
		IF side_vis2D_m=3 OR side_vis2D_m=4 THEN LINE2 0, B,  A, B

		if iCabSymbolType2D <> 1 then
			if iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 then
				add2 0, B
				rot2 -90
				arc2 0, 0, A-0.025, 0, 90
				del 2
			endif

			if  iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 then
				len = SQR((A-(A/SIN(45))*SIN(30))^2+(-(A/SIN(45))*COS(30)+A)^2)
				alpha = 180-2*ATN((len/2)/(len/10))
				rr = (len/2)/SIN(alpha)
				add2 A, B
				rot2 -90+15
				arc2 0, -B, A, 0, 90
				add2 A, -A
				rot2 alpha-142.5
				arc2 0, -rr, rr, alpha*2, 90
				del 4
			endif
		endif

		IF reqBMinSpace <> 0 then
			FILL MSFillType
			pen MSContLinePen
			line_type MSContLineType
			POLY2_B 11, 1+2+4+64,MSFillPen,MSFillBackPen,
					A, 			B, 			1,
					A, 			B-y1, 		1,
					A, 			B-1000, 	800,
					x1, 		0, 			1001,
					0, 			0, 			1,
					0, 			-MSFront, 	1,
					x1, 		-MSFront, 	1,
					x1+1000, 	-MSFront, 	800,
					A+MSFront,	B-y1, 		1001,
					A+MSFront,	B, 			1,
					A, 			B, 			-1

			HOTSPOT2 0, -MSFront, unID: unID=unID+1
			HOTSPOT2 x1, -MSFront, unID: unID=unID+1
			HOTSPOT2 A+MSFront, B-y1, unID: unID=unID+1
			HOTSPOT2 A+MSFront, B, unID: unID=unID+1
			HOTSPOT2 x1+(A+MSFront)*sin(45), B-y1-(A+MSFront)*sin(45), unID: unID=unID+1
			PEN gs_cont_pen
			line_type 1
		ENDIF

	endif
ENDIF

END
