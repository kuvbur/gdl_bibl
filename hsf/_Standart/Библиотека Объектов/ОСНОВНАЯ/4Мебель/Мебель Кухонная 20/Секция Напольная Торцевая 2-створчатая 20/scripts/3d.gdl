
IF gs_detlevel_3D_m=0 then END
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

IF gs_detlevel_3D_m=1 THEN gs_resol = 4
RESOL gs_resol

IF not(gs_shadow) THEN SHADOW OFF



PEN gs_cont_pen


if iEndCabinetStyle = CABEND_RADIUSED then goto 100

!*********************************** Angular End *************************************
!*************************************************************************************

ADD a/2-left_cab*cab_size, b/2-front_cab*cab_size, 0

!! 3D hotspots !!
unID=1

!!! Countertop Height
hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2+left_cab+amx, -b/2+amy, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2+left_cab+amx, -b/2+amy, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2+left_cab+amx, -b/2+amy, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

!! End of 3D hotspots !!

!!******************************** CABINET

MATERIAL toe_mat

!!******************************** TOE
IF toe_h>0 THEN
unID=100
	hotspot -a/2+left_cab, -b/2+front_cab+fr_th1+toe_d, 0, unID, toe_h, 1 : unID=unID+1
	hotspot -a/2+left_cab, -b/2+front_cab+fr_th1+toe_d, -1, unID, toe_h, 3 : unID=unID+1
	hotspot -a/2+left_cab, -b/2+front_cab+fr_th1+toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot -a/2+left_cab+amxt, -b/2+front_cab+fr_th1+toe_d+amyt, 0, unID, toe_h, 1 : unID=unID+1
	hotspot -a/2+left_cab+amxt, -b/2+front_cab+fr_th1+toe_d+amyt, -1, unID, toe_h, 3 : unID=unID+1
	hotspot -a/2+left_cab+amxt, -b/2+front_cab+fr_th1+toe_d+amyt, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot a/2-front_cab-fr_th1-toe_d,  b/2-right_cab, 0, unID, toe_h, 1 : unID=unID+1
	hotspot a/2-front_cab-fr_th1-toe_d,  b/2-right_cab, -1, unID, toe_h, 3 : unID=unID+1
	hotspot a/2-front_cab-fr_th1-toe_d,  b/2-right_cab, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot -a/2+left_cab, b/2-right_cab, 0, unID, toe_h, 1 : unID=unID+1
	hotspot -a/2+left_cab, b/2-right_cab, -1, unID, toe_h, 3 : unID=unID+1
	hotspot -a/2+left_cab, b/2-right_cab, toe_h, unID, toe_h, 2 : unID=unID+1

	if gs_toe_type_m = 1 then
		PRISM_ 5, toe_h,
			-a/2, -b/2+front_cab+fr_th1+toe_d, 15,
			-a/2+amxt, -b/2+amyt+front_cab+fr_th1+toe_d, 15,
			 a/2-front_cab-fr_th1-toe_d,  b/2, 15,
			-a/2, b/2, 15,
			-a/2, -b/2+front_cab+fr_th1+toe_d, -1

		Gosub 1000
	else
		r_leg=0.02

		Add -a/2+r_leg, -b/2+front_cab+fr_th1+toe_d+r_leg+r_leg*COS(fi),0
		GOSUB 20

		Add -a/2+amxt-r_leg, -b/2+front_cab+fr_th1+toe_d+amyt+r_leg,0
		GOSUB 20

		Add a/2-front_cab-fr_th1-toe_d-r_leg-r_leg*COS(fi),  b/2-right_cab-r_leg,0
		GOSUB 20

		Add -a/2+left_cab+r_leg, b/2-right_cab-r_leg,0
		GOSUB 20
	Endif

endif

if gs_toe_type_m = 0 then
unID=200
	hotspot -a/2, -b/2+front_cab, 0, unID : unID=unID+1
	hotspot a/2-front_cab,  b/2, 0, unID : unID=unID+1
	hotspot -a/2, b/2, 0, unID, toe_h, 0, unID : unID=unID+1
endif

!!******************************** CORPUS
ADD -a/2, -b/2+front_cab+fr_th1, 0
ROTZ fi
CUTPLANE 1, 0, 1, 1
DEL 2

ADD a/2-front_cab-fr_th1, b/2, 0
ROTZ fi+45
CUTPLANE 1, 0, 1, 1
DEL 2

ADD -a/2, -b/2, toe_h

CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx-toe_h,ToeHtps3D=0,
		gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=0, count_th=count_th,
		front_cab=0, rear_cab=0, left_cab=0,right_cab=0,
		toe_h=0, side_vis_m=side_vis_m+1,fr_th1=0,
		sink=0, shelf_num=shelf_num, gs_ShelfMat=gs_ShelfMat,
		gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
		toe_mat=toe_mat, gs_cabinet_mat=gs_cabinet_mat, drawer=0,
		basecab_unID=unID,
		returned_parameters unID


DEL 1
CUTEND
CUTEND

!!******************************** COUNTER
side_vis_m = side_vis_m+1
IF count_th>0 THEN
	MATERIAL count_mat
	ADDZ zzyzx-count_th
	IF gs_count_type_m = 1 Then
		PRISM_ 7, count_th,
			-a/2, -b/2, 13+2*((side_vis_m=2) OR (side_vis_m=4)),
			-a/2+left_cab, -b/2, 15,
			-a/2+left_cab+amx, -b/2+amy, 15,
			a/2, b/2-right_cab,15,
			a/2, b/2,8+2*(ABS(right_cab)<EPS)+7*((side_vis_m>=3) AND (right_cab>0))+5*((side_vis_m>=3) AND (ABS(right_cab)<EPS)),
			-a/2, b/2, 8+7*(side_vis_m=4)+5*(side_vis_m=2),
			-a/2, -b/2, -1
	Endif
	IF gs_count_type_m = 2 Then
		PRISM_ 7, count_th,
			-a/2, -b/2+rr4, 13+2*((side_vis_m=2) OR (side_vis_m=4)),
			-a/2+left_cab, -b/2+rr4, 8,
			-a/2+left_cab+amx-rr4, -b/2+amy+rr4, 8,
			a/2-rr4, b/2-right_cab,8,
			a/2-rr4, b/2,8+2*(ABS(right_cab)<EPS)+7*((side_vis_m>=3) AND (right_cab>0))+5*((side_vis_m>=3) AND (ABS(right_cab)<EPS)),
			-a/2, b/2, 8+7*(side_vis_m=4)+5*(side_vis_m=2),
			-a/2, -b/2+rr4, -1
		addz count_th
		TUBE 6,7, 3,
				0,0,1,
				0,-count_th,1,
				-rr4,-count_th,1,
				-rr4,-rr4,1,
				0,-rr4,901,
				rr4,-90,4001,

				-a/2-1, -b/2+rr4,0,0,
				-a/2, -b/2+rr4,0,0,
				-a/2+left_cab, -b/2+rr4,0,0,
				-a/2+left_cab+amx-rr4, -b/2+amy+rr4,0,0,
				a/2-rr4, b/2-right_cab,0,0,
				a/2-rr4, b/2,0,0,
				a/2-rr4, b/2+1,0,0

		del 1
	Endif
	DEL 1
ENDIF
! --- END OF COUNTER ---


! --- BACK SPLASH ---
MATERIAL bs_mat
IF bs_h>0 AND ABS(right_cab)<EPS AND gs_count_type_m # 0 then
	ADDZ zzyzx
	PRISM_ 5, bs_h,
		-a/2, b/2-0.02, 15-bl,
		 a/2-0.02*TAN(45-fi), b/2-0.02, 15,
		 a/2, b/2, 15,
		-a/2, b/2, 15-cl,
		-a/2, b/2-0.02, -1
	DEL 1
endif


! --- Door ---

if gs_door_type_m > 0 then
	ADD -a/2+left_cab, -b/2+front_cab+fr_th1, toe_h
	ROTZ fi

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang, gs_resol=gs_resol,
			door_w=ambcl, door_h=zzyzx-toe_h-count_th, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,knob_size=knob_size,
			doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorglas_mat,gs_knob_p_m=gs_knob_p_m,
			gs_knob_type_m=gs_knob_type_m,gs_bevel=gs_bevel,
			gs_cust_p=gs_cust_p,fr_th1=fr_th1,
			knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
			cabdoortyp_unID=cabdoortyp_unID,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	DEL 2

	ADD a/2-front_cab-fr_th1, b/2-right_cab, toe_h
	ROTZ 45+fi
	MULX -1

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang, gs_resol=gs_resol,
			door_w=ambcr, door_h=zzyzx-toe_h-count_th, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,knob_size=knob_size,
			doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorglas_mat,gs_knob_p_m=gs_knob_p_m,
			gs_knob_type_m=gs_knob_type_m, gs_bevel=gs_bevel,
			gs_cust_p=gs_cust_p,fr_th1=fr_th1,
			knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
			cabdoortyp_unID=cabdoortyp_unID,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	DEL 3
endif

del 1


END

100:

!**************************** Radius End ****************************
!********************************************************************

gs_res_2=gs_res_2*4
resol gs_res_2


!************************* toe

MATERIAL toe_mat

IF toe_h>0 THEN

	IF cab_size=1 THEN

		!************* A=cabinet size

		prism_ 5, toe_h,
			x1, front_cab+b, 15,
			x1+a-front_cab-toe_d, front_cab+b, 79,
			x1, front_cab+b, 900,
			x1, front_cab+toe_d, 3015,
			x1, front_cab+b, -1

		IF toe_h>0 THEN
		unID=500
			hotspot 0, front_cab+fr_th1+toe_d, 0, unID, toe_h, 1 : unID=unID+1
			hotspot 0, front_cab+fr_th1+toe_d, -1, unID, toe_h, 3 : unID=unID+1
			hotspot 0, front_cab+fr_th1+toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

			hotspot 0, front_cab+b, 0, unID, toe_h, 1 : unID=unID+1
			hotspot 0, front_cab+b, -1, unID, toe_h, 3 : unID=unID+1
			hotspot 0, front_cab+b, toe_h, unID, toe_h, 2 : unID=unID+1

			hotspot a-(fr_th1+toe_d), front_cab+b, 0, unID, toe_h, 1 : unID=unID+1
			hotspot a-(fr_th1+toe_d), front_cab+b, -1, unID, toe_h, 3 : unID=unID+1
			hotspot a-(fr_th1+toe_d), front_cab+b, toe_h, unID, toe_h, 2 : unID=unID+1
		ENDIF
	ELSE

		!************* A=counter size

		prism_ 5, toe_h,
			x1, B-y1, 15,
			A-front_cab-fr_th1-toe_d, B-y1, 79,
			A-front_cab-fr_th1-toe_d, B-1000, 800,
			x1, front_cab+fr_th1+toe_d, 1015,
			x1, B-y1, -1

		IF toe_h>0 THEN
		unID=500
			hotspot 0, front_cab+fr_th1+toe_d, 0, unID, toe_h, 1 : unID=unID+1
			hotspot 0, front_cab+fr_th1+toe_d, -1, unID, toe_h, 3 : unID=unID+1
			hotspot 0, front_cab+fr_th1+toe_d, toe_h, unID, toe_h, 2 : unID=unID+1

			hotspot 0, b, 0, unID, toe_h, 1 : unID=unID+1
			hotspot 0, b, -1, unID, toe_h, 3 : unID=unID+1
			hotspot 0, b, toe_h, unID, toe_h, 2 : unID=unID+1

			hotspot a-(front_cab+fr_th1+toe_d), b, 0, unID, toe_h, 1 : unID=unID+1
			hotspot a-(front_cab+fr_th1+toe_d), b, -1, unID, toe_h, 3 : unID=unID+1
			hotspot a-(front_cab+fr_th1+toe_d), b, toe_h, unID, toe_h, 2 : unID=unID+1
		ELSE
			hotspot 0, 0, 0, unID : unID=unID+1
			hotspot a,  b, 0, unID : unID=unID+1
			hotspot -a/2, b/2, 0, unID, toe_h, 0, unID : unID=unID+1
		ENDIF

	ENDIF

ENDIF

!************************* cabinet

MATERIAL gs_cabinet_mat

addz toe_h

IF gs_detlevel_3D_m=1 THEN

	!************************ simple 3D

	IF cab_size=1 THEN

		!************* A=cabinet size

		prism_ 5, zzyzx-toe_h-count_th,
			x1, front_cab+b, 15,
			x1+a-fr_th1, front_cab+b, 79,
			x1, front_cab+b, 900,
			x1, front_cab+0.02, 3015,
			x1, front_cab0+b, -1

	ELSE

		!************* A=counter size

		prism_ 5, zzyzx-toe_h-count_th,
			x1, B-y1, 15,
			A-front_cab-fr_th1, B-y1, 79,
			A-front_cab, B-1000, 800,
			x1, front_cab+fr_th1, 1015,
			x1, B-y1, -1

	ENDIF


ELSE

	!************************ detailed 3D

	IF cab_size=1 THEN

		!************* A=cabinet size

		a0=a-fr_th1
		b0=a-fr_th1

	ELSE

		!************* A=counter size

		a0=a-x1-front_cab-fr_th1
		b0=a-x1-front_cab-fr_th1

	ENDIF

	add x1, front_cab+fr_th1, 0

	alph=2*0.02*90/a0/pi
	dx=a0*cos(alph)
	dy=a0*sin(alph)

	prism_ 10, 0.02,
		0, B0, 11,
		A0-0.0001, B0, 15,
		A0, B0, 64+11,
		0, B0, 900,
		dx, B0-dy, 3079,
		0, B0, 900,
		dy, b0-dx, 3009,
		0, B0, 900,
		0, 0, 3011,
		0, B0, -1

	addz 0.02

	prism_ 12, zzyzx-0.02*2-toe_h-count_th,
		0, B0, 10,
		A0-0.0001, B0, 15,
		A0, B0, 64+10,
		0, B0, 900,
		dx, B0-dy, 3079,
		dx-0.0001, B0-dy, 15,
		0.02, B0-0.02, 15,
		dy, b0-dx+0.0001, 15,
		dy, b0-dx, 64+10,
		0, B0, 900,
		0, 0, 3010,
		0, B0, -1

	del 1

	addz zzyzx-0.02-toe_h-count_th

	prism_ 10, 0.02,
		0, B0, 14,
		A0-0.0001, B0, 15,
		A0, B0, 64+14,
		0, B0, 900,
		dx, B0-dy, 3079,
		0, B0, 900,
		dy, b0-dx, 3012,
		0, B0, 900,
		0, 0, 3014,
		0, B0, -1

	del 1

	if shelf_num>0 then

		dz=(zzyzx-2*0.02-toe_h-count_th-shelf_num*0.02)/(shelf_num+1)

		for i=1 to shelf_num

			addz dz
			MATERIAL gs_ShelfMat
			prism_ 5, 0.02,
				0.02, B0-0.02, 15,
				dx, B0-dy, 79,
				0, B0, 900,
				dy, B0-dx, 3015,
				0.02, B-0.02, -1

		next i

		del shelf_num

	endif

	del 1

ENDIF

del 1



!************************* counter

material count_mat

addz zzyzx-count_th

IF cab_size=1 THEN

	!************* A=cabinet size

	!!! Curved Resolution
	S=90/(gs_resol)

		IF gs_count_type_m = 1 THEN
			FOR k=0 TO 90 STEP S
			PUT a-RR+(RR+front_cab)*sin(k),RR-(RR+front_cab)*cos(k),79
			NEXT k
		ENDIF
		IF gs_count_type_m = 2 THEN
			FOR k=0 TO 90 STEP S
			PUT a-RR+(RR+front_cab-rr4)*sin(k),RR-(RR+front_cab-rr4)*cos(k),79
			NEXT k
		ENDIF


	Addy front_cab
		IF gs_count_type_m = 1 THEN
			CPRISM_ count_mat, count_mat, count_mat, 8+NSP/3, count_th,         !! COUNTER
				0, -front_cab, 13+2*((d=1) OR (d=3)),
				0.02, -front_cab, 13,
				a-RR,-front_cab,13,
				GET(NSP),
				a+front_cab,RR,13,
				a+front_cab, b-0.02, 13,
				a+front_cab, b, 8+7*((d=2) OR (d=3)),
				0, b, 8+7*((d=1) OR (d=3)),
				0, -front_cab, -1

		ENDIF
		IF gs_count_type_m = 2 THEN
			CPRISM_ count_mat, count_mat, count_mat, 6+NSP/3, count_th,         !! COUNTER
				0, -front_cab+rr4, 13+2*((d=1) OR (d=3)),
				a-RR,-front_cab+rr4,13,
				GET(NSP),
				a+front_cab-rr4,RR,13,
				a+front_cab-rr4, b, 8+7*((d=2) OR (d=3)),
				0, b, 8+7*((d=1) OR (d=3)),
				0, -front_cab+rr4, -1


			FOR k=0 TO 90 STEP S
			PUT a-RR+(RR+front_cab-rr4)*sin(k),RR-(RR+front_cab-rr4)*cos(k),0,0
			NEXT k

			RESOL gs_resol
			addz count_th
			MATERIAL count_mat
			TUBE 6,6+NSP/4, 3,
					0,0,1,
					0,-count_th,1,
					-rr4,-count_th,1,
					-rr4,-rr4,1,
					0,-rr4,901,
					rr4,-90,4001,

					-1, -front_cab+rr4,0,0,
					0, -front_cab+rr4,0,0,
					a-RR,-front_cab+rr4,0,0,
					GET(NSP),
					a+front_cab-rr4,RR,0,0,
					a+front_cab-rr4, b, 0,0,
					a+front_cab-rr4, b+1, 0,0
			del 1

			RESOL gs_resol*4

		ENDIF
	del 1


	del 1

	!!! Countertop Height
	unID=600
	hotspot 0, front_cab+B+y1, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, front_cab+B+y1, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, front_cab+B+y1, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot x1+A+front_cab, front_cab+B+y1, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot x1+A+front_cab, front_cab+B+y1, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot x1+A+front_cab, front_cab+B+y1, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot 0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, 0, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

ELSE

	!************* A=counter size
	!!! Curved Resolution
	S=90/(gs_resol)

		IF gs_count_type_m = 1 THEN
			FOR k=0 TO 90 STEP S
			PUT a-RR+(RR)*sin(k),RR-(RR)*cos(k),79
			NEXT k
		ENDIF
		IF gs_count_type_m = 2 THEN
			FOR k=0 TO 90 STEP S
			PUT a-RR+(RR-rr4)*sin(k),RR-(RR-rr4)*cos(k),79
			NEXT k
		ENDIF


		IF gs_count_type_m = 1 THEN

			CPRISM_ count_mat, count_mat, count_mat, 6+NSP/3, count_th,         !! COUNTER
				0, 0, 13+2*((d=1) OR (d=3)),
				a-RR,0,13,
				GET(NSP),
				a,RR,13,
				a, b, 8+7*((d=2) OR (d=3)),
				0, b, 8+7*((d=1) OR (d=3)),
				0, 0, -1

		ENDIF
		IF gs_count_type_m = 2 THEN
			CPRISM_ count_mat, count_mat, count_mat, 6+NSP/3, count_th,         !! COUNTER
				0, 0+rr4, 13+2*((d=1) OR (d=3)),
				a-RR,0+rr4,13,
				GET(NSP),
				a-rr4,RR,13,
				a-rr4, b, 8+7*((d=2) OR (d=3)),
				0, b, 8+7*((d=1) OR (d=3)),
				0, -0+rr4, -1


			FOR k=0 TO 90 STEP S
			PUT a-RR+(RR-rr4)*sin(k),RR-(RR-rr4)*cos(k),0,0
			NEXT k

			RESOL gs_resol
			addz count_th
			MATERIAL count_mat
			TUBE 6,6+NSP/4, 3,
					0,0,1,
					0,-count_th,1,
					-rr4,-count_th,1,
					-rr4,-rr4,1,
					0,-rr4,901,
					rr4,-90,4001,

					-1, rr4,0,0,
					0, rr4,0,0,
					a-RR,rr4,0,0,
					GET(NSP),
					a-rr4,RR,0,0,
					a-rr4, b, 0,0,
					a-rr4, b+1, 0,0
			del 1

			RESOL gs_resol*4

		ENDIF

	del 1

	!!! Countertop Height
	unID=600
	hotspot 0, B, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, B, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot A, B, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot A, B, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot A, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot 0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, 0, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

ENDIF


!********************************** doors for radius end

if cab_size=1 then

	!************* A=cabinet size
	if gs_door_type_m > 0 then
		ADD x1, front_cab+fr_th1, toe_h
		rotx 90

		cabdoorrad_unID = unID
		CALL "fa_cabdoortyp_rad" PARAMETERS a=a, gs_detlevel_3D_m=gs_detlevel_3D_m,
				gs_full_edit=gs_full_edit, door_ang=door_ang, gs_res_2=gs_res_2,
				door_w=(2*(a-fr_th1)*pi)/4, door_h=zzyzx-toe_h-count_th, gs_door_type_m=gs_door_type_m,
				gs_shadow=gs_shadow, doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
				vert_gnum=vert_gnum, hor_gnum=hor_gnum,
				doorframe_mat=doorframe_mat, gs_bevel=gs_bevel,
				gs_knob_mat=gs_knob_mat,knob_pl_y=knob_pl_y,gs_knob_type_m=gs_knob_type_m,
				knob_size=knob_size,doorglas_mat=doorglas_mat,  gs_resol=gs_resol,
				gs_cust_p=gs_cust_p,fr_th1=fr_th1,
				cabdoorrad_unID=cabdoorrad_unID,
			returned_parameters cabdoorrad_unID

		unID = cabdoorrad_unID

		del 2
	endif
else

	!************* A=counter size
	if gs_door_type_m > 0 then
		a00=a-front_cab

		ADD x1, front_cab+fr_th1, toe_h
		rotx 90

		cabdoorrad_unID = unID
		CALL "fa_cabdoortyp_rad" PARAMETERS a=a00, gs_detlevel_3D_m=gs_detlevel_3D_m,
				gs_full_edit=gs_full_edit, door_ang=door_ang, gs_res_2=gs_res_2,
				door_w=(2*(a00-fr_th1)*pi)/4, door_h=zzyzx-toe_h-count_th, gs_door_type_m=gs_door_type_m,
				gs_shadow=gs_shadow, doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
				vert_gnum=vert_gnum, hor_gnum=hor_gnum,
				doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,knob_pl_y=knob_pl_y,
				doorglas_mat=doorglas_mat, gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
				gs_cust_p=gs_cust_p,knob_size=knob_size,
				fr_th1=fr_th1,gs_bevel=gs_bevel,
				cabdoorrad_unID=cabdoorrad_unID,
				returned_parameters cabdoorrad_unID

		unID = cabdoorrad_unID


		del 2
	endif
endif


end

!! LEGS - START
20:

if gs_toe_type_m = 2 then

	cylind toe_h, r_leg
endif

if gs_toe_type_m = 3 then
	prism_ 4,toe_h,
			-r_leg,-r_leg,15,
			r_leg,-r_leg,15,
			r_leg,r_leg,15,
			-r_leg,r_leg,15
endif
gosub 1000
del 1

RETURN
!! LEGS - END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4


base

body -1

return
