

! --- Oriented View ------------------------------ [

angleViewRot = 0
if dt then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]

DrawingNum = 1
if DrawingNum = 1 then	! Top

	!!!! Handling overhungs in case of end panels !!!

	pen gs_cont_pen

	CALL "fa_counter_sink" PARAMETERS ALL gs_sink_type_m=0, side_vis2D_m = side_vis2D_m + 1

	if cab_size=0 then
		CALL "Resize_A_B_ZZYZX" Parameters ALL
	ELSE
		A=a-(left_cab+right_cab)
		B=b-(front_cab+rear_cab)

		ADD2 left_cab,front_cab
		CALL "Resize_A_B_ZZYZX" Parameters ALL
		DEL 1

		A=a+(left_cab+right_cab)
		B=b+(front_cab+rear_cab)
	endif

	DEL top

! ======================================================================
! Dishwasher Symbol
! ======================================================================

if b2DSymbol then
	ADD2 a/2,b/2
	pen gs_symbol_pen
	aorb = a
	if a > b then aorb = b

	if iDwSymbolType = 1 then
		rot2 30
		CIRCLE2 0,0,aorb/8
		LINE2 0,aorb/8,aorb/2,aorb/8
		LINE2 0,-aorb/8,-aorb/2,-aorb/8
		del 1
	endif

	if iDwSymbolType = 2 then
		circle2 0, 0, aorb/2.7
		circle2 0, 0, aorb/20
		line2 aorb/2.7*1.414213/2, aorb/2.7*1.414213/2, a/2, b/2
		line2 -aorb/2.7*1.414213/2, aorb/2.7*1.414213/2, -a/2, b/2
		line2 aorb/2.7*1.414213/2, -aorb/2.7*1.414213/2, a/2, -b/2
		line2 -aorb/2.7*1.414213/2, -aorb/2.7*1.414213/2, -a/2, -b/2

		fill gs_symbol_fill_type
		poly2_b 7, 1+2+64, gs_symbol_pen, gs_back_pen,
				aorb/20, 0, 1,
				0, 0,    900,
				0, 180,  4001,
				-aorb/20, 0,  1,
				0, 0,  900,
				0, 180,4001,
				aorb/20, 0, 1
	endif

	if iDwSymbolType = 3 then
		radSymb = aorb/3
		circle2 0, 0, radSymb
		line2 0, -radSymb*2/3, 0, radSymb*2/3
		line2 -radSymb*2/3.5, radSymb*1/3, radSymb*2/3.5, radSymb*1/3
		line2 -radSymb*2/3.5, radSymb*1/3, -radSymb*2/3.5, radSymb*2/3
		line2 radSymb*2/3.5, radSymb*1/3, radSymb*2/3.5, radSymb*2/3
		add2 0, -b/2
			fill gs_fill_type
			POLY2_B 5, 1+2+64,gs_fill_pen,gs_back_pen,
					-a/3, 0, 1,
					-a/3, -0.05, 1,
					a/3, -0.05, 1,
					a/3, 0, 1,
					-a/3, 0, -1
		del 1
	endif
	del 1
endif

! ======================================================================
! 2D TEXT
! ======================================================================

	unID = 200
	if dt then
		nTrans = 0

		HOTSPOT2 a/2+txtx,	b/2+txty,	unID,  txtx,  2    	: unID = unID + 1	! moving
		HOTSPOT2 a/2,		b/2+txty,	unID,  txtx,  1+128 : unID = unID + 1	! base
		HOTSPOT2 -1,		b/2+txty,	unID,  txtx,  3    	: unID = unID + 1	! reference

		HOTSPOT2 a/2+txtx,	b/2,		unID,  txty,  1+128 : unID = unID + 1
		HOTSPOT2 a/2+txtx,	b/2+txty,	unID,  txty,  2    	: unID = unID + 1
		HOTSPOT2 a/2+txtx, 	-1,			unID,  txty,  3    	: unID = unID + 1

		pen tpen
		define style textstyle	fontType, 10/GLOB_SCALE*fsz, 5,
								gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
		style textstyle
		add2 a/2+txtx,b/2+txty
		nTrans = nTrans + 1

		! Text Rotation - Readable / Always Horizontal / Align with Symbol
		! ----------------------------------------------------------------------
		if typeTextRotation_m = 2 then								! Readable
			rot2 180 * (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps))	! 1/0
			nTrans = nTrans + 1
		else
			if typeTextRotation_m = 1 then							! Horizontal
				rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED))	! 1/-1
				nTrans = nTrans + 1
			endif
		endif
		! ----------------------------------------------------------------------

		text2 0,0,txt

		del nTrans
	else
		HOTSPOT2 a/2,b/2, unID
	endif
endif
DEL TOP

! Show MEP Connections -------------------------------------------------------------[

isavailable = 0
isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)
isMEPEnabled = (isavailable OR isInArchiCAD = 0)
if isMEPEnabled then
	bShowMEP = 1
	reqBMep = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showMepConnect", reqBMep)
	if success > 0 then
		bShowMEP = reqBMep
	endif
else
	bShowMEP = 0
endif

! Show MEP Connections -------------------------------------------------------------]

! --- start of modifying to Shaft

if bShowMEP then

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	if DrawingNum = 2 then PROJECT2 -3, 90, 2	! Bottom
	if DrawingNum = 3 then PROJECT2 4, 270, 2	! Front
	if DrawingNum = 4 then PROJECT2 4, 90, 2	! Back
	if DrawingNum = 5 then PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	if DrawingNum = 6 then PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 12

	DEL 1
endif

! --- end of modifying to Shaft
