
call "FM_types" parameters all

values "A" range (0, )
values "B" range (0, )

values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

values{2} "iControlPanelType" 	CONTROLPANEL_STYLE1, `Стиль 1`,
								CONTROLPANEL_STYLE2, `Стиль 2`,
								CONTROLPANEL_NONE, `Нет`

PARAMETERS door_w=a

DIM stDWSymbolTypes[3]
	stDWSymbolTypes[1]=`Стиль 1`
	stDWSymbolTypes[2]=`Стиль 2`
	stDWSymbolTypes[3]=`Стиль 3`

values "gs_dw_symbol_type" stDwSymbolTypes
values "iDwSymbolType" 1, 2, 3

if GLOB_MODPAR_NAME = "gs_dw_symbol_type" then
	for i = 1 to vardim1(stDWSymbolTypes)
		if gs_dw_symbol_type = stDWSymbolTypes[i] then iDwSymbolType = i
	next i
	parameters iDwSymbolType = iDwSymbolType
else
	gs_dw_symbol_type = stDWSymbolTypes[max(1, min(iDwSymbolType,vardim1(stDWSymbolTypes)))]
	parameters gs_dw_symbol_type = gs_dw_symbol_type
endif

if not(b2DSymbol) then
	lock "gs_dw_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type"
	hideparameter "gs_dw_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type"
endif
if b2DSymbol and iDwSymbolType <> 2 then
	lock "gs_symbol_fill_type"
	hideparameter "gs_symbol_fill_type"
endif

parameters fsz = abs(fsz)
values "fsz" range (0,]

if tpen=0 then parameters tpen=86

!!! *************************************************
! --- LOCK PARAMETERS ---

if rear_panel=1 then
	LOCK "dob_face"
	HIDEPARAMETER "dob_face"
endif

if not(dt) then
	lock	"txt", "tpen", "fontType", "fsz", "fsz_point",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation"
	hideparameter	"txt", "tpen", "fontType", "fsz", "fsz_point",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation"
endif

if iControlPanelType = CONTROLPANEL_NONE then
	LOCK "ControlPanelMat","HandleMat","HandleMat2","SwitchMat"
	HIDEPARAMETER "ControlPanelMat","HandleMat","HandleMat2","SwitchMat"
endif

if iControlPanelType = CONTROLPANEL_STYLE2 then
	LOCK "HandleMat","HandleMat2","SwitchMat"
	HIDEPARAMETER "HandleMat","HandleMat2","SwitchMat"
endif

if iControlPanelType = CONTROLPANEL_STYLE1 then
	LOCK "gs_knob_type", "gs_knob_type_m", "gs_knob_mat", "knob_size"
	HIDEPARAMETER "gs_knob_type", "gs_knob_type_m", "gs_knob_mat", "knob_size"
endif

! --- Minimal Space ---
values "MSFront" range [0, )
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]
values "MSRear" range [0, )
values "RMSSide" range (-a/2,]
values "RMSSide2" range (-a/2,]

if not(dob_face) then
	lock			"MSRear",
					"RMSSide",
					"RMSSide2"
	hideparameter	"MSRear",
					"RMSSide",
					"RMSSide2"
endif

!!! *************************************************

Dot_in_mm = 25.4 / 72


! --- Synhronized mm and point in marker width
if GLOB_MODPAR_NAME = "fsz" then
	parameters fsz_point = fsz / Dot_in_mm
	fsz_point = fsz / Dot_in_mm
endif
if GLOB_MODPAR_NAME = "fsz_point" then
	parameters fsz = fsz_point * Dot_in_mm
	fsz = fsz_point * Dot_in_mm
endif


	PARAMETERS MEP_NumberConnections = 2

dim DimConName[2]
	DimConName[1] = `Подключение Холодной Воды`
	DimConName[2] = `Подключение Канализации`

CALL "Kitchen Cabinets UI macro" parameters all \
									pageIds = pageIds,
									DimConName = DimConName,
									CornCabType = 0,
									bEdgeBevel = 1,
									CabinetType = 0,
									bCounterOverhang = 1,
									bMirror = 1,

									CornerEdgeSizes = 0,
									bToe = 1,
									bShelf = 0,
									bShowSurfaces = 1,

									bPossibleUndermounted = 0,
									PossibleSinkTypes = 0,
									bSidePanels = 1,
									bDrainSymb = 0,

									nDoors = 1,

									bDoubleFacadeEdit = 0,
									bOneRearPanelEdit = 1,
									bHorKnobPosition = 0,
									bVerKnobPosition =0,
									bPositionType = 0,

									bCabSymbol = 0,
									bPossibleSinknTap = 0,
									bApplianceEdit = 0,
									bSymbLineEdit = 0,
									bMinSpaceEdit = 1,
									bShadowEdit = 1,
									bHotspotEdit = 1,

									bEnableBackSplash	= 1,
									bEnableDoorpanel	= 1,
									bEnableEndpanel		= 1,
									bDoubleDoor			= 0,
									bEnableKnobPlacing	= 0,
									bEnableShelf		= 0,
									bEnableSink			= 0,
									isTallCabinet		= 0,

									bKnobParams = 1

VALUES "gs_ui_current_page" 1, 2, 3, 4, 5, 6, 7, 8


