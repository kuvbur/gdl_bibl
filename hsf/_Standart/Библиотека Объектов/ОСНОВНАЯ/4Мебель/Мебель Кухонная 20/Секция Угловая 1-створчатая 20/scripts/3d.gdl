
if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

PEN gs_cont_pen
IF gs_detlevel_3D_m=1 THEN gs_resol = 4
RESOL gs_resol

IF not(gs_shadow) THEN SHADOW OFF

if cab_size=0 then
	depth_cabl=depth_cabl
	depth_cabr=depth_cabr
endif
if cab_size=1 then
	depth_cabl=depth_cabl+front_cab+lrear_cab
	depth_cabr=depth_cabr+front_cab+rrear_cab
endif

ADD -(lrear_cab)*cab_size,-(left_cab)*cab_size, 0


!******************************** 3D HOTSPOTS
!!! at base

unID=1

if gs_toe_type_m = 0 Then
	HOTSPOT lrear_cab, left_cab, 0, unID : unID=unID+1
	HOTSPOT depth_cabl-front_cab, left_cab, 0, unID : unID=unID+1
	HOTSPOT depth_cabl-front_cab, b-depth_cabr+front_cab+fr_th1, 0, unID : unID=unID+1
	HOTSPOT  a-right_cab, b-depth_cabr+front_cab, 0, unID : unID=unID+1
	HOTSPOT  a-right_cab, b-rrear_cab, 0, unID : unID=unID+1
	HOTSPOT lrear_cab, b-rrear_cab, 0, unID : unID=unID+1
endif

!!! cabinet height
unID=10
hotspot 0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot depth_cabl, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot depth_cabl, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot depth_cabl, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot depth_cabl,  b-depth_cabr, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot depth_cabl,  b-depth_cabr, -1, unID, zzyzx, 3 : unID=unID+1
hotspot depth_cabl,  b-depth_cabr, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a, b-depth_cabr+fr_th1, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a, b-depth_cabr+fr_th1, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a, b-depth_cabr+fr_th1, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, b, zzyzx, unID, zzyzx, 2 : unID=unID+1
!! End of 3D hotspots !!


!!!! Handling overhangs in case of end panels !!!
if dob_face then rrear_cab = rrear_cab
if lrear_panel then lrear_cab = lrear_cab + 0.02
if rrear_panel then rrear_cab = rrear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02

bLeftSideVisible  = (side_vis_m=1) OR (side_vis_m=3)
bRightSideVisible = (side_vis_m=2) OR (side_vis_m=3)

!******************************** TOE

!! Toe !!
unID=100

If toe_h>0 Then

	hotspot lrear_cab, left_cab, 0, unID, toe_h, 1 : unID=unID+1
	hotspot lrear_cab, left_cab, -1, unID, toe_h, 3 : unID=unID+1
	hotspot lrear_cab, left_cab, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot depth_cabl-toe_d-front_cab, left_cab, 0, unID, toe_h, 1 : unID=unID+1
	hotspot depth_cabl-toe_d-front_cab, left_cab, -1, unID, toe_h, 3 : unID=unID+1
	hotspot depth_cabl-toe_d-front_cab, left_cab, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot depth_cabl-toe_d-front_cab, b-depth_cabr+toe_d+front_cab+fr_th1, 0, unID, toe_h, 1 : unID=unID+1
	hotspot depth_cabl-toe_d-front_cab, b-depth_cabr+toe_d+front_cab+fr_th1, -1, unID, toe_h, 3 : unID=unID+1
	hotspot depth_cabl-toe_d-front_cab, b-depth_cabr+toe_d+front_cab+fr_th1, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, 0, unID, toe_h, 1 : unID=unID+1
	hotspot a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, -1, unID, toe_h, 3 : unID=unID+1
	hotspot a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot a-right_cab, b-rrear_cab-(fr_th1*dob_face), 0, unID, toe_h, 1 : unID=unID+1
	hotspot a-right_cab, b-rrear_cab-(fr_th1*dob_face), -1, unID, toe_h, 3 : unID=unID+1
	hotspot a-right_cab, b-rrear_cab-(fr_th1*dob_face), toe_h, unID, toe_h, 2 : unID=unID+1

	hotspot lrear_cab, b-rrear_cab-(fr_th1*dob_face), 0, unID, toe_h, 1 : unID=unID+1
	hotspot lrear_cab, b-rrear_cab-(fr_th1*dob_face), -1, unID, toe_h, 3 : unID=unID+1
	hotspot lrear_cab, b-rrear_cab-(fr_th1*dob_face), toe_h, unID, toe_h, 2 : unID=unID+1

	MATERIAL toe_mat
	if gs_toe_type_m = 1 Then
		PRISM_ 7, toe_h,
			lrear_cab, left_cab, 8+7*(bLeftSideVisible OR (left_cab>0)),
			depth_cabl-toe_d-front_cab, left_cab, 13+2*(bLeftSideVisible OR (left_cab>0)),
			depth_cabl-toe_d-front_cab, b-depth_cabr+toe_d+front_cab+fr_th1, 15,
			a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, 8+7*(bRightSideVisible OR (right_cab>0)),
			a-right_cab, b-rrear_cab-(fr_th1*dob_face), 13+2*(bRightSideVisible OR (right_cab>0)),
			lrear_cab, b-rrear_cab-(fr_th1*dob_face), 15,
			lrear_cab, left_cab, -1
		gosub 2000
	Else
		r_leg=0.02

		add lrear_cab+r_leg, left_cab+r_leg,0
		gosub "legs"
		del 1

		add depth_cabl-toe_d-front_cab-r_leg, left_cab+r_leg,0
		gosub "legs"
		del 1

		add a-right_cab-r_leg, b-depth_cabr+toe_d+front_cab+r_leg+fr_th1,0
		gosub "legs"
		del 1

		add a-right_cab-r_leg, b-rrear_cab-r_leg-(fr_th1*dob_face),0
		gosub "legs"
		del 1

		add lrear_cab+r_leg, b-rrear_cab-r_leg-(fr_th1*dob_face),0
		gosub "legs"
		del 1
	Endif
EndIf
!! End of toe !!


!******************************** CABINET

ADDZ toe_h
MATERIAL gs_cabinet_mat

			if gs_detlevel_3D_m=2 AND zzyzx-count_th-toe_h>0.041 then

	PRISM_ 11, 0.02,
		lrear_cab, left_cab, 11,
		depth_cabl-front_cab, left_cab, 11,
		depth_cabl-front_cab, b-depth_cabr+front_cab+fr_th1, 11,
		depth_cabl-front_cab+0.07, b-depth_cabr+front_cab+fr_th1, 13,
		a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 9,
		a-right_cab, b-depth_cabr+front_cab+fr_th1, 11,
		a-right_cab, b-rrear_cab-(fr_th1*dob_face), 11,
		a-right_cab-0.02, b-rrear_cab-(fr_th1*dob_face), 9+4*dob_face,
		lrear_cab+0.02, b-rrear_cab-(fr_th1*dob_face), 9,
		lrear_cab, b-rrear_cab-(fr_th1*dob_face), 11,
		lrear_cab, left_cab, -1

	ADDZ 0.02
		IF dob_face=0 THEN		!! Side Panels
			PRISM_ 15, zzyzx-count_th-toe_h-0.04,
				lrear_cab, left_cab, 10,
				depth_cabl-front_cab, left_cab, 10,
				depth_cabl-front_cab, b-depth_cabr+front_cab+fr_th1, 10,
				depth_cabl-front_cab+0.07, b-depth_cabr+front_cab+fr_th1, 15,
				depth_cabl-front_cab+0.07, b-depth_cabr+front_cab+0.02+fr_th1, 15,
				depth_cabl-front_cab-0.02, b-depth_cabr+front_cab+0.02+fr_th1, 15,
				depth_cabl-front_cab-0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, b-0.02-rrear_cab, 15,
				a-0.02-right_cab, b-0.02-rrear_cab, 15,
				a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
				a-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
				a-right_cab, b-rrear_cab, 10,
				lrear_cab, b-rrear_cab, 10,
				lrear_cab, left_cab, -1
		ENDIF

		IF dob_face=1 THEN			!!! Side Panel
			PRISM_ 11, zzyzx-count_th-toe_h-0.04,
				lrear_cab, left_cab, 10,
				depth_cabl-front_cab, left_cab, 10,
				depth_cabl-front_cab, b-depth_cabr+front_cab+fr_th1, 10,
				depth_cabl-front_cab+0.07, b-depth_cabr+front_cab+fr_th1, 15,
				depth_cabl-front_cab+0.07, b-depth_cabr+front_cab+0.02+fr_th1, 15,
				depth_cabl-front_cab-0.02, b-depth_cabr+front_cab+0.02+fr_th1, 15,
				depth_cabl-front_cab-0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, b-rrear_cab-(fr_th1*dob_face), 10,
				lrear_cab, b-rrear_cab-(fr_th1*dob_face), 10,
				lrear_cab, left_cab, -1

			PRISM_ 5, zzyzx-count_th-toe_h-0.04,
				a-0.02-right_cab, b-rrear_cab-(fr_th1*dob_face), 15,
				a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
				a-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
				a-right_cab, b-rrear_cab-(fr_th1*dob_face), 10,
				a-0.02-right_cab, b-rrear_cab-(fr_th1*dob_face), -1
		ENDIF

		ADDZ zzyzx-count_th-toe_h-0.04
		PRISM_ 11, 0.02,			! Top Panel
			lrear_cab, left_cab, 14,
			depth_cabl-front_cab, left_cab, 14,
			depth_cabl-front_cab, b-depth_cabr+front_cab+fr_th1, 14,
			depth_cabl-front_cab+0.07, b-depth_cabr+front_cab+fr_th1, 13,
			a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 12,
			a-right_cab, b-depth_cabr+front_cab+fr_th1, 14,
			a-right_cab, b-rrear_cab-(fr_th1*dob_face), 14,
			a-right_cab-0.02, b-rrear_cab-(fr_th1*dob_face), 14-dob_face,
			lrear_cab+0.02, b-rrear_cab-(fr_th1*dob_face), 12,
			lrear_cab, b-rrear_cab-(fr_th1*dob_face), 14,
			lrear_cab, left_cab, -1
	DEL 2

ELSE

	EXTRUDE 7, 0, 0, zzyzx-count_th-toe_h, 55,        !!! Simple cab
		lrear_cab, left_cab, 0,
		depth_cabl-front_cab, left_cab, 0,
		depth_cabl-front_cab, b-depth_cabr+front_cab+fr_th1, 0,
		a-right_cab, b-depth_cabr+front_cab+fr_th1, 0,
		a-right_cab, b-rrear_cab-(fr_th1*dob_face), 0,
		lrear_cab, b-rrear_cab-(fr_th1*dob_face), 0,
		lrear_cab, left_cab, -1

ENDIF
DEL 1


!******************************** COUNTER

ADDZ zzyzx-count_th
if count_th>0 then
	if gs_count_type_m = 1 Then
		CPRISM_ count_mat, count_mat, count_mat, 7, count_th,
			0, 0, 8+7*bLeftSideVisible,
			depth_cabl, 0, 13+2*bLeftSideVisible,
			depth_cabl, b-depth_cabr, 15,
			a, b-depth_cabr, 8+7*bRightSideVisible,
			a, b, 13+2*bRightSideVisible,
			0, b, 15,
			0, 0, -1
	Endif
	if gs_count_type_m = 2 Then
		rr=min (count_th*4/5, 0.02)

		CPRISM_ count_mat, count_mat, count_mat, 7, count_th,
			0, 0, 8+7*bLeftSideVisible,
			depth_cabl-rr, 0, 13+2*bLeftSideVisible,
			depth_cabl-rr, b-depth_cabr+rr, 15,
			a, b-depth_cabr+rr, 8+7*bRightSideVisible,
			a, b-rr, 13+2*bRightSideVisible,
			0, b-rr, 15,
			0, 0, -1

		add 0,b,count_th
		rotx -90
		roty 90
		If dob_face = 1 Then
			CPRISM_ count_mat, count_mat, count_mat, 6, a,
					rr,0,8,
					rr,count_th,12+1*bLeftSideVisible,
					0,count_th,14+1*bLeftSideVisible,
					0,rr,14+1*bLeftSideVisible+64,
					rr,rr,900,
					rr,90,4014+64
		Else
			CPRISM_ count_mat, count_mat, count_mat,4, a,
					rr,0,8,
					rr,count_th,12+1*bLeftSideVisible,
					0,count_th,14+1*bLeftSideVisible,
					0,0,14+1*bLeftSideVisible+64
		Endif
		del 3

		add depth_cabl,0,count_th
		rotx -90
		mulx -1

		addz b-depth_cabr
		roty -45
		cutplane
		del 2

		CPRISM_ count_mat, count_mat, count_mat, 6, b-depth_cabr+rr,
				rr,0,8,
				rr,count_th,12+1*bLeftSideVisible,
				0,count_th,14+1*bLeftSideVisible,
				0,rr,14+1*bLeftSideVisible+64,
				rr,rr,900,
				rr,90,4014+64

		cutend
		del 3

		add a,b-depth_cabr,count_th
		rotz 90
		rotx -90

		add rr,0,a-depth_cabl+rr
		roty -45
		cutplane
		del 2

		CPRISM_ count_mat, count_mat, count_mat, 6, a-depth_cabl+rr,
				rr,0,8,
				rr,count_th,12+1*bLeftSideVisible,
				0,count_th,14+1*bLeftSideVisible,
				0,rr,14+1*bLeftSideVisible+64,
				rr,rr,900,
				rr,90,4014+64
		cutend
		del 3

	Endif
ENDIF
DEL 1


if bs_h<=0 then 5

if bs_h > 0 & gs_count_type_m # 0 then
	ADDZ zzyzx
	if dob_face=0 then
		CPRISM_  bs_mat, bs_mat, bs_mat, 7, bs_h,
				0, 0, 8+7*bLeftSideVisible,
				0.02, 0, 13+2*bLeftSideVisible,
				0.02, b-0.02, 15,
				a, b-0.02, 8+7*bRightSideVisible,
				a, b, 13+2*bRightSideVisible,
				0, b, 15,
				0, 0, -1
	endif

	if dob_face=1 then
		CPRISM_  bs_mat, bs_mat, bs_mat, 5, bs_h,
			0, 0, 8+7*bLeftSideVisible,
			0.02, 0, 13+2*bLeftSideVisible,
			0.02, b, 15,
			0, b, 15,
			0, 0, -1
	endif
	DEL 1
endif

5:
IF zzyzx-count_th-toe_h<0.18 THEN END

IF gs_detlevel_3D_m=2 THEN

	! --- SHELVES ---

	 MATERIAL gs_cabinet_mat

	IF bDrawer THEN
		ADDZ zzyzx-count_th-drawer_h-0.01
			IF dob_face=0 THEN aa=0.02
			If dob_face=1 THEN aa=0
			PRISM_ 7,0.02,
			   lrear_cab+0.02, left_cab+0.02, 15,
			   depth_cabl-front_cab-0.02, left_cab+0.02, 15,
			   depth_cabl-front_cab-0.02, b-depth_cabr+front_cab+0.02+fr_th1, 15,
			   a-0.02-right_cab, b-depth_cabr+front_cab+0.02+fr_th1, 15,
			   a-0.02-right_cab, b-0.02*(1-dob_face)-rrear_cab-(fr_th1*dob_face), 15,
			   lrear_cab+0.02, b-0.02*(1-dob_face)-rrear_cab-(fr_th1*dob_face), 15,
			   lrear_cab+0.02, left_cab+0.02, -1
		DEL 1
	ENDIF


	IF shelf_num>0 AND A>0.04 THEN
	FOR ii=1 TO shelf_num
		ADDZ toe_h+((zzyzx-count_th-toe_h-0.02-drawer_h+bDrawer*0.01)/(shelf_num+1))*(ii)
		MATERIAL gs_ShelfMat
		PRISM_ 7,0.02,
		   lrear_cab+0.02, left_cab+0.02, 15,
		   depth_cabl-front_cab-0.02, left_cab+0.02, 15,
		   depth_cabl-front_cab-0.02, b-depth_cabr+front_cab+0.02+fr_th1, 15,
		   a-0.02-right_cab, b-depth_cabr+front_cab+0.02+fr_th1, 15,
		   a-0.02-right_cab, b-0.02*(1-dob_face)-rrear_cab-(fr_th1*dob_face), 15,
		   lrear_cab+0.02, b-0.02*(1-dob_face)-rrear_cab-(fr_th1*dob_face), 15,
		   lrear_cab+0.02, left_cab+0.02, -1
		DEL 1
	NEXT ii
	ENDIF

ENDIF
!! END OF SHELVES !!



cc=zzyzx-count_th-toe_h
ADDZ toe_h

!******************************** END PANELS
accessno=2
!! on rear panel there is no glas and grilles and knobs!!
If iEndPanelType = 1 	then gs_door_type_mr=1
If iEndPanelType = 2 	then gs_door_type_mr=2
If iEndPanelType = 4 	then gs_door_type_mr=11
If iEndPanelType = 7 	then gs_door_type_mr=4
If iEndPanelType = 9 	then gs_door_type_mr=6
If iEndPanelType = 99 then gs_door_type_mr=10

IF lrear_panel=1 THEN
	aa=(b-left_cab-rrear_cab-(fr_th1*dob_face))
	ADD lrear_cab, b-rrear_cab-(fr_th1*dob_face), 0
	ROTZ -90

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=0,
			door_w=aa, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,
			cabdoortyp_unID=cabdoortyp_unID,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	DEL 2
ENDIF

IF rrear_panel=1 AND dob_face=0 THEN
	aa=(a-right_cab-lrear_cab)/2
	ADD a-right_cab, b-rrear_cab, 0
	ROTZ 180

	FOR HI=1 TO 2

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=0,
			door_w=aa, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,
			cabdoortyp_unID=cabdoortyp_unID,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	ADDX aa
	NEXT HI
	DEL 2

	DEL 2
ENDIF

IF left_panel=1 THEN
	ADD lrear_cab, left_cab, 0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=0,
			door_w=depth_cabl-front_cab-lrear_cab, door_h=cc,
			gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,
			cabdoortyp_unID=cabdoortyp_unID,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	DEL 1
ENDIF

if right_panel=1 then
	ADD a-right_cab, b-depth_cabr+front_cab+fr_th1, 0
	ROTZ 90

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=0,
			door_w=depth_cabr-front_cab-rrear_cab-fr_th1-(fr_th1*dob_face), door_h=cc,
			gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,
			cabdoortyp_unID=cabdoortyp_unID,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	DEL 2
endif
DEL 1


!----- drawer
!----- accessno=1
cc = zzyzx - count_th - toe_h - drawer_h

if bDrawer then

	aa = a - depth_cabl - 0.07 + doorOffset/2 + front_cab - right_cab
	add depth_cabl - front_cab + 0.07 - doorOffset/2 , b - depth_cabr + front_cab + fr_th1, toe_h + cc

	cabdoortyp_unID = unID
	call "fa_cabdoortyp" parameters 	gs_detlevel_3D_m	= gs_detlevel_3D_m,
										gs_door_type_m 		= _iDrawerType, 
										gs_cust_p			= gs_cust_p,
										gs_bevel			= gs_bevel,
										door_w				= aa, 
										door_h				= drawer_h, 
										fr_th1				= fr_th1,
										doorframe_w			= doorframe_w, 
										door_ang			= 0,
										vert_gnum			= 0, 
										hor_gnum			= 0,
										gs_knob_type_m		= 0, 
										gs_cont_pen			= gs_cont_pen,
										doorframe_mat		= DrawerMat, 
										gs_knob_mat			= gs_knob_mat,
										doorglas_mat		= doorglas_mat, 
										gs_shadow			= gs_shadow,
										gs_full_edit		= 0, 
										cabdoortyp_unID		= cabdoortyp_unID,
										returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	add aa / 2, -0.02, drawer_h / 2
	ROTY 90
	gosub 6000
	del 1 + 2

	if dob_face then
		aa = (a - right_cab - lrear_cab) / 2
		add lrear_cab + 2 * aa, b - rrear_cab - (fr_th1 * dob_face), toe_h + cc
		rotz 180

		for hi = 1 to 2
			cabdoortyp_unID = unID
			call "fa_cabdoortyp" parameters 	gs_detlevel_3D_m	= gs_detlevel_3D_m,
												gs_door_type_m 		= _iDrawerType, 
												gs_bevel			= gs_bevel,
												gs_cust_p			= gs_cust_p,
												door_ang			= door_ang2,
												door_w				= aa, 
												door_h				= drawer_h, 
												fr_th1				= fr_th1,
												vert_gnum			= 0, 
												hor_gnum			= 0,
												doorframe_w			= doorframe_w, 
												gs_knob_type_m		= 0,
												gs_cont_pen			= gs_cont_pen,
												doorframe_mat		= DrawerMat, 
												gs_knob_mat			= gs_knob_mat,
												doorglas_mat		= doorglas_mat,
												gs_shadow			= gs_shadow,
												gs_full_edit		= 0, 
												cabdoortyp_unID		= cabdoortyp_unID,
												returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			add aa / 2, -0.02, drawer_h / 2
			roty 90
			if gs_knob_type_m <> 0 and gs_detlevel_3D_m = 2 then
				gosub 6000
			endif
			del 2

			mulx -1
			addx -2 * aa
		next hi
		del 4

		del 2
	endif
endif

if gs_door_type_m > 0 then
	aa = a - depth_cabl - 0.07 + doorOffset/2 + front_cab - right_cab
	add depth_cabl - front_cab + 0.07 - doorOffset/2, b - depth_cabr + front_cab + fr_th1, toe_h

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang,
			door_w=aa, door_h=cc, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,fr_th1=fr_th1,
			doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorglas_mat,gs_knob_p_m=gs_knob_p_m,
			gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
			gs_cust_p=gs_cust_p,knob_size=knob_size,knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID


	DEL 1


	IF dob_face=1 THEN
		aa=(a-right_cab-lrear_cab)/2
		ADD lrear_cab+2*aa, b-rrear_cab-(fr_th1*dob_face), toe_h
		ROTZ 180

		FOR HI=1 TO 2
			cabdoortyp_unID=unID
			CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_full_edit=gs_full_edit, door_ang=door_ang,
					door_w=aa, door_h=cc, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
					doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
					vert_gnum=vert_gnum, hor_gnum=hor_gnum,fr_th1=fr_th1,
					doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
					doorglas_mat=doorglas_mat,gs_knob_p_m=gs_knob_p_m,
					gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
					gs_cust_p=gs_cust_p,knob_size=knob_size,knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
					gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
					gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
					cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
					returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			MULX -1
			ADDX -2*aa
		next HI
		del 4

		del 2
	endif
endif

!! End of door !!


END


"legs":
	if gs_toe_type_m = 2 then
		cylind toe_h, r_leg
	Endif
	if gs_toe_type_m = 3 then
		prism_ 4,toe_h,
				-r_leg,-r_leg,15,
				r_leg,-r_leg,15,
				r_leg,r_leg,15,
				-r_leg,r_leg,15
	endif

	gosub 1000
RETURN


1000:
		roty 90
		gosub 5000
		del 1
		return

2000:
		gosub 5000
		return

5000:

		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4

		base

		body -1

		return

6000:
	if gs_detlevel_3D_m=2 then

		if gs_knob_type_m = 9 then
			reqBKnobs = 0
			success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
			if (success = 0 or not(reqBKnobs)) and gs_cust_knob<>"" then
				ADDz -(left_cab+right_cab)/2
				rotx 90
				rotz 90
				addx -gs_customHandleLength / 2
				addy -gs_customHandleHeight / 2
				call "Knob_Collection" PARAMETERS gs_ptype	= gs_cust_knob,
													a		= gs_customHandleLength,
													b		= gs_customHandleHeight,
													zzyzx	= gs_customHandleDepth
				del 5
			endif
		else
			if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
				ADDz 0
				addy -fr_th1+0.02
				!!! knob on top drawer
				CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
						gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
						gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
						gs_knob_mat=gs_knob_mat
				del 2
			else
				if gs_knob_type_m=8 then
					roty 180
					Addz knob_size/2
					addy -fr_th1+0.02
						CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
								gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
								gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
								gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 3
				else
					Addz knob_size/2
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				endif
			endif
		endif
	endif
return

