
if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

PEN gs_cont_pen
IF gs_detlevel_3D_m=1 THEN gs_resol = 4
RESOL gs_resol

IF not(gs_shadow) THEN SHADOW OFF

if cab_size=0 then
	depth_cabl=depth_cabl
	depth_cabr=depth_cabr
endif
if cab_size=1 then
	depth_cabl=depth_cabl+front_cab+lrear_cab
	depth_cabr=depth_cabr+front_cab+rrear_cab
endif

ADD -(lrear_cab)*cab_size,-(left_cab)*cab_size, 0


		!******************************** 3D HOTSPOTS
		!!! at base

		unID=1

		if gs_toe_type_m = 0 Then
			HOTSPOT lrear_cab, left_cab, 0, unID : unID=unID+1
			HOTSPOT depth_cabl-front_cab, left_cab, 0, unID : unID=unID+1
			HOTSPOT depth_cabl-front_cab, b-depth_cabr+front_cab, 0, unID : unID=unID+1
			HOTSPOT  a-right_cab, b-depth_cabr+front_cab, 0, unID : unID=unID+1
			HOTSPOT  a-right_cab, b-rrear_cab, 0, unID : unID=unID+1
			HOTSPOT lrear_cab, b-rrear_cab, 0, unID : unID=unID+1
		endif

		!!! cabinet height
		unID=10
		hotspot 0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
		hotspot 0, 0, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot 0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

		hotspot depth_cabl, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
		hotspot depth_cabl, 0, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot depth_cabl, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

		hotspot depth_cabl,  b-depth_cabr, 0, unID, zzyzx, 1+128 : unID=unID+1
		hotspot depth_cabl,  b-depth_cabr, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot depth_cabl,  b-depth_cabr, zzyzx, unID, zzyzx, 2 : unID=unID+1

		hotspot a, b-depth_cabr, 0, unID, zzyzx, 1+128 : unID=unID+1
		hotspot a, b-depth_cabr, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot a, b-depth_cabr, zzyzx, unID, zzyzx, 2 : unID=unID+1

		hotspot a, b, 0, unID, zzyzx, 1+128 : unID=unID+1
		hotspot a, b, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot a, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

		hotspot 0, b, 0, unID, zzyzx, 1+128 : unID=unID+1
		hotspot 0, b, -1, unID, zzyzx, 3 : unID=unID+1
		hotspot 0, b, zzyzx, unID, zzyzx, 2 : unID=unID+1
		!! End of 3D hotspots !!


		!!!! Handling overhangs in case of end panels !!!
		if dob_face then lrear_cab = lrear_cab
		if lrear_panel then lrear_cab = lrear_cab + 0.02
		if rrear_panel then rrear_cab = rrear_cab + 0.02
		if left_panel then left_cab = left_cab +0.02
		if right_panel then right_cab = right_cab +0.02

		bLeftSideVisible  = (side_vis_m=1) OR (side_vis_m=3)
		bRightSideVisible = (side_vis_m=2) OR (side_vis_m=3)

			!******************************** TOE

			!! Toe !!
			unID=100

			If toe_h>0 Then

				hotspot lrear_cab+(fr_th1*dob_face), left_cab, 0, unID, toe_h, 1 : unID=unID+1
				hotspot lrear_cab+(fr_th1*dob_face), left_cab, -1, unID, toe_h, 3 : unID=unID+1
				hotspot lrear_cab+(fr_th1*dob_face), left_cab, toe_h, unID, toe_h, 2 : unID=unID+1

				hotspot depth_cabl-toe_d-front_cab-fr_th1, left_cab, 0, unID, toe_h, 1 : unID=unID+1
				hotspot depth_cabl-toe_d-front_cab-fr_th1, left_cab, -1, unID, toe_h, 3 : unID=unID+1
				hotspot depth_cabl-toe_d-front_cab-fr_th1, left_cab, toe_h, unID, toe_h, 2 : unID=unID+1

				hotspot depth_cabl-toe_d-front_cab-fr_th1, b-depth_cabr+toe_d+front_cab+fr_th1, 0, unID, toe_h, 1 : unID=unID+1
				hotspot depth_cabl-toe_d-front_cab-fr_th1, b-depth_cabr+toe_d+front_cab+fr_th1, -1, unID, toe_h, 3 : unID=unID+1
				hotspot depth_cabl-toe_d-front_cab-fr_th1, b-depth_cabr+toe_d+front_cab+fr_th1, toe_h, unID, toe_h, 2 : unID=unID+1

				hotspot a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, 0, unID, toe_h, 1 : unID=unID+1
				hotspot a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, -1, unID, toe_h, 3 : unID=unID+1
				hotspot a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, toe_h, unID, toe_h, 2 : unID=unID+1

				hotspot a-right_cab, b-rrear_cab, 0, unID, toe_h, 1 : unID=unID+1
				hotspot a-right_cab, b-rrear_cab, -1, unID, toe_h, 3 : unID=unID+1
				hotspot a-right_cab, b-rrear_cab, toe_h, unID, toe_h, 2 : unID=unID+1

				hotspot lrear_cab, b-rrear_cab, 0, unID, toe_h, 1 : unID=unID+1
				hotspot lrear_cab, b-rrear_cab, -1, unID, toe_h, 3 : unID=unID+1
				hotspot lrear_cab, b-rrear_cab, toe_h, unID, toe_h, 2 : unID=unID+1

				MATERIAL toe_mat
				if gs_toe_type_m = 1 then
					PRISM_ 7, toe_h,
							lrear_cab+(fr_th1*dob_face), left_cab, 8+7*(bLeftSideVisible OR (left_cab>0)),
							depth_cabl-toe_d-front_cab-fr_th1, left_cab, 13+2*(bLeftSideVisible OR (left_cab>0)),
							depth_cabl-toe_d-front_cab-fr_th1, b-depth_cabr+toe_d+front_cab+fr_th1, 15,
							a-right_cab, b-depth_cabr+toe_d+front_cab+fr_th1, 8+7*(bRightSideVisible OR (right_cab>0)),
							a-right_cab, b-rrear_cab, 13+2*(bRightSideVisible OR (right_cab>0)),
							lrear_cab+(fr_th1*dob_face), b-rrear_cab, 15,
							lrear_cab+(fr_th1*dob_face), left_cab, -1
					gosub 1000
				else
					r_leg=0.02

					add lrear_cab+r_leg+(fr_th1*dob_face), left_cab+r_leg,0
					GOSUB 20

					Add depth_cabl-toe_d-front_cab-r_leg-fr_th1, left_cab+r_leg,0
					GOSUB 20

					add depth_cabl-toe_d-front_cab-r_leg-fr_th1, b-depth_cabr+toe_d+front_cab+r_leg+fr_th1,0
					GOSUB 20

					add a-right_cab-r_leg, b-depth_cabr+toe_d+front_cab+r_leg+fr_th1,0
					GOSUB 20

					add a-right_cab-r_leg, b-rrear_cab-r_leg,0
					GOSUB 20

					add lrear_cab+r_leg+(fr_th1*dob_face), b-rrear_cab-r_leg,0
					GOSUB 20

				endif


			endIf


			!******************************** CABINET

			ADDZ toe_h
			MATERIAL gs_cabinet_mat

			if gs_detlevel_3D_m=2 AND zzyzx-count_th-toe_h>0.041 then

				PRISM_ 13, 0.02,		! Bottom Panel
						lrear_cab+(fr_th1*dob_face), left_cab, 11,
						depth_cabl-front_cab-fr_th1, left_cab, 11,
						depth_cabl-front_cab-fr_th1, 0.02+left_cab, 13,
						depth_cabl-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 9+(1-door)*4,
						depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 11+(1-door)*4,
						depth_cabl+0.01-front_cab, b-depth_cabr+front_cab+fr_th1, 13,
						a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 9,
						a-right_cab, b-depth_cabr+front_cab+fr_th1, 11,
						a-right_cab, b-rrear_cab, 11,
						lrear_cab+(fr_th1*dob_face), b-rrear_cab, 11,
						lrear_cab+(fr_th1*dob_face), b-rrear_cab-0.02, 9+dob_face*4,
						lrear_cab+(fr_th1*dob_face), left_cab+0.02, 9,
						lrear_cab+(fr_th1*dob_face), left_cab, -1

				ADDZ 0.02
				if dob_face=0 then		!! Side Panels
					PRISM_ 11, zzyzx-count_th-toe_h-0.04,
							lrear_cab+(fr_th1*dob_face), left_cab, 10,
							depth_cabl-front_cab-fr_th1, left_cab, 10,
							depth_cabl-front_cab-fr_th1, 0.02+left_cab, 15,
							lrear_cab+0.02, 0.02+left_cab, 15,
							lrear_cab+0.02, b-0.02-rrear_cab, 15,
							a-0.02-right_cab, b-0.02-rrear_cab, 15,
							a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
							a-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
							a-right_cab, b-rrear_cab, 10,
							lrear_cab+(fr_th1*dob_face), b-rrear_cab, 10,
							lrear_cab+(fr_th1*dob_face), left_cab, -1
				endif

				if dob_face=1 then		!! Side Panels
					PRISM_ 5, zzyzx-count_th-toe_h-0.04,
						lrear_cab+(fr_th1*dob_face), left_cab, 10,
						depth_cabl-front_cab-fr_th1, left_cab, 10,
						depth_cabl-front_cab-fr_th1, 0.02+left_cab, 15,
						lrear_cab+(fr_th1*dob_face), 0.02+left_cab, 10,
						lrear_cab+(fr_th1*dob_face), left_cab, -1

					PRISM_ 7, zzyzx-count_th-toe_h-0.04,

						lrear_cab+(fr_th1*dob_face), b-0.02-rrear_cab, 15,
						a-0.02-right_cab, b-0.02-rrear_cab, 15,
						a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
						a-right_cab, b-depth_cabr+front_cab+fr_th1, 10,
						a-right_cab, b-rrear_cab, 10,
						lrear_cab+(fr_th1*dob_face), b-rrear_cab, 10,
						lrear_cab+(fr_th1*dob_face), b-0.02-rrear_cab, -1
				endif


				if door=1 OR bDrawer then
					if door=0 then ADDZ zzyzx-count_th-drawer_h-toe_h-0.02

					if door=1 then hh=zzyzx-count_th-toe_h-0.04
					if door=0 then hh=drawer_h-0.01

					PRISM_ 8, hh,
						depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
						depth_cabl-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 10,
						depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 10,
						depth_cabl+0.01-front_cab, b-depth_cabr+front_cab+fr_th1, 15,
						depth_cabl+0.01-front_cab, b-depth_cabr+0.02+front_cab+fr_th1, 15,
						depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr+0.02+front_cab+fr_th1, 15,
						depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
						depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, -1
					if door=0 then DEL 1
				endif

				ADDZ zzyzx-count_th-toe_h-0.04
				PRISM_ 13, 0.02,			!! top panel
						lrear_cab+(fr_th1*dob_face), left_cab, 14,
						depth_cabl-front_cab-fr_th1, left_cab, 14,
						depth_cabl-front_cab-fr_th1, 0.02+left_cab, 13,
						depth_cabl-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 12+(1-MAX(door,bDrawer)),
						depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 14+(1-MAX(door,bDrawer)),
						depth_cabl+0.01-front_cab, b-depth_cabr+front_cab+fr_th1, 13,
						a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 12,
						a-right_cab, b-depth_cabr+front_cab+fr_th1, 14,
						a-right_cab, b-rrear_cab, 14,
						lrear_cab+(fr_th1*dob_face), b-rrear_cab, 14,
						lrear_cab+(fr_th1*dob_face), b-rrear_cab-0.02, 12+dob_face,
						lrear_cab+(fr_th1*dob_face), left_cab+0.02, 12,
						lrear_cab+(fr_th1*dob_face), left_cab, -1

				DEL 2

			else

				EXTRUDE 7, 0, 0, zzyzx-count_th-toe_h, 55,        !!! Simple cab
					lrear_cab+(fr_th1*dob_face), left_cab, 0,
					depth_cabl-front_cab-fr_th1, left_cab, 0,
					depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 0,
					a-right_cab, b-depth_cabr+front_cab+fr_th1, 0,
					a-right_cab, b-rrear_cab, 0,
					lrear_cab+(fr_th1*dob_face), b-rrear_cab, 0,
					lrear_cab+(fr_th1*dob_face), left_cab, -1

			endif

		DEL 1

		gosub 1000

		!******************************** COUNTER

		ADDZ zzyzx-count_th
		if count_th>0 then
			if gs_count_type_m = 1 Then
				CPRISM_ count_mat, count_mat, count_mat, 7, count_th,
					0, 0, 8+7*bLeftSideVisible,
					depth_cabl, 0, 13+2*bLeftSideVisible,
					depth_cabl, b-depth_cabr, 15,
					a, b-depth_cabr, 8+7*bRightSideVisible,
					a, b, 13+2*bRightSideVisible,
					0, b, 15,
					0, 0, -1
			Endif
			if gs_count_type_m = 2 Then
				rr=min (count_th*4/5, 0.02)

				CPRISM_ count_mat, count_mat, count_mat, 7, count_th,
					0+rr, 0, 8+7*bLeftSideVisible,
					depth_cabl-rr, 0, 8+2*bLeftSideVisible,
					depth_cabl-rr, b-depth_cabr+rr, 8,
					a, b-depth_cabr+rr, 8+7*bRightSideVisible,
					a, b, 13+2*bRightSideVisible,
					0+rr, b, 8,
					0+rr, 0, -1

				addz count_th
				rotx -90
				If dob_face = 1 Then
					CPRISM_ count_mat, count_mat, count_mat, 6, b+rrear_cab+left_cab,
							rr,0,8,
							rr,count_th,12+1*bLeftSideVisible,
							0,count_th,14+1*bLeftSideVisible,
							0,rr,14+1*bLeftSideVisible+64,
							rr,rr,900,
							rr,90,4014+64
				Else
					CPRISM_ count_mat, count_mat, count_mat,4, b,
							rr,0,8,
							rr,count_th,12+1*bLeftSideVisible,
							0,count_th,14+1*bLeftSideVisible,
							0,0,14+1*bLeftSideVisible+64
				Endif
				del 2

				add depth_cabl,0,count_th
				rotx -90
				mulx -1

				addz b-depth_cabr
				roty -45
				cutplane
				del 2

				CPRISM_ count_mat, count_mat, count_mat, 6, b-depth_cabr+rr,
						rr,0,8,
						rr,count_th,12+1*bLeftSideVisible,
						0,count_th,14+1*bLeftSideVisible,
						0,rr,14+1*bLeftSideVisible+64,
						rr,rr,900,
						rr,90,4014+64

				cutend
				del 3

				add a,b-depth_cabr,count_th
				rotz 90
				rotx -90

				addz a-depth_cabl
				roty -45
				cutplane
				del 2

				CPRISM_ count_mat, count_mat, count_mat, 6, a-depth_cabl+rr,
						rr,0,8,
						rr,count_th,12+1*bLeftSideVisible,
						0,count_th,14+1*bLeftSideVisible,
						0,rr,14+1*bLeftSideVisible+64,
						rr,rr,900,
						rr,90,4014+64
				cutend
				del 3

			Endif
		endif
		DEL 1


		if bs_h<=0 then 5


		ADDZ zzyzx

			if bs_h > 0 & gs_count_type_m # 0 then
				if dob_face=0 then
					CPRISM_  bs_mat, bs_mat, bs_mat, 7, bs_h,
						0, 0, 8+7*bLeftSideVisible,
						0.02, 0, 13+2*bLeftSideVisible,
						0.02, b-0.02, 15,
						a, b-0.02, 8+7*bRightSideVisible,
						a, b, 13+2*bRightSideVisible,
						0, b, 15,
						0, 0, -1
				endif
	
				if dob_face=1 then
					CPRISM_  bs_mat, bs_mat, bs_mat, 5, bs_h,
						0, b-0.02, 15,
						a, b-0.02, 8+7*bRightSideVisible,
						a, b, 13+2*bRightSideVisible,
						0, b, 15,
						0, b-0.02, -1
				endif
			endif

		DEL 1

		5:
		if zzyzx-count_th-toe_h<0.18 then END

		if gs_detlevel_3D_m=2 then


		!******************************** SHELVES
		 MATERIAL gs_cabinet_mat

		if bDrawer then

			if door=1 then
			CUTPOLYA 8, 1, 0,
				depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
				depth_cabl-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
				depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 15,
				depth_cabl+0.01-front_cab, b-depth_cabr+front_cab+fr_th1, 15,
				depth_cabl+0.01-front_cab, b-depth_cabr+0.02+front_cab+fr_th1, 15,
				depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr+0.02+front_cab+fr_th1, 15,
				depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
				depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, -1
			endif

			ADDZ zzyzx-count_th-drawer_h-0.01
			PRISM_ 9,0.02,
				lrear_cab+0.02*(1-dob_face)+(fr_th1*dob_face), left_cab+0.02, 15,
				depth_cabl-front_cab-fr_th1, left_cab+0.02, 15,
				depth_cabl-front_cab-fr_th1, b-depth_cabr-0.03+front_cab+fr_th1, 9+(1-MAX(door,bDrawer))*4,
				depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 11+(1-MAX(door,bDrawer))*4,
				depth_cabl+0.01-front_cab, b-depth_cabr+front_cab+fr_th1, 13,
				a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 15,
				a-0.02-right_cab, b-0.02-rrear_cab, 15,
				lrear_cab+0.02*(1-dob_face)+(fr_th1*dob_face), b-0.02-rrear_cab, 15,
				lrear_cab+0.02*(1-dob_face)+(fr_th1*dob_face), left_cab+0.02, -1
			DEL 1

			if door=1 then CUTEND

		endif


		if shelf_num>0 AND A>0.04 then

			if door=1 then
				CUTPOLYA 8, 1, 0,
					depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
					depth_cabl-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
					depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 15,
					depth_cabl+0.01-front_cab, b-depth_cabr+front_cab+fr_th1, 15,
					depth_cabl+0.01-front_cab, b-depth_cabr+0.02+front_cab+fr_th1, 15,
					depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr+0.02+front_cab+fr_th1, 15,
					depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, 15,
					depth_cabl-0.02-front_cab-fr_th1, b-depth_cabr-0.01+front_cab, -1

			endif

			shelf_z=(zzyzx-(count_th+(shelf_num+1)*0.02+toe_h+drawer_h))/(shelf_num+1)

			FOR ii=1 TO shelf_num

				ADDZ toe_h+shelf_z
					MATERIAL gs_ShelfMat
					PRISM_ 7,0.02,
					   lrear_cab+0.02*(1-dob_face)+(fr_th1*dob_face), left_cab+0.02, 15,
					   depth_cabl-front_cab-fr_th1, left_cab+0.02, 15,
					   depth_cabl-front_cab-fr_th1, b-depth_cabr+front_cab+fr_th1, 15,
					   a-0.02-right_cab, b-depth_cabr+front_cab+fr_th1, 15,
					   a-0.02-right_cab, b-0.02-rrear_cab, 15,
					   lrear_cab+0.02*(1-dob_face)+(fr_th1*dob_face), b-0.02-rrear_cab, 15,
					   lrear_cab+0.02*(1-dob_face)+(fr_th1*dob_face), left_cab+0.02, -1
				DEL 1

			addz shelf_z+0.02
			NEXT ii
			DEL shelf_num

			if door=1 then CUTEND

		endif

		endif
		!! END OF SHELVES !!



		cc=zzyzx-count_th-toe_h
		ADDZ toe_h

		!******************************** END PANELS
		accessno=2
		If iEndPanelType = 1 	then gs_door_type_mr=1
		If iEndPanelType = 2 	then gs_door_type_mr=2
		If iEndPanelType = 4 	then gs_door_type_mr=11
		If iEndPanelType = 7 	then gs_door_type_mr=4
		If iEndPanelType = 9	then gs_door_type_mr=6
		If iEndPanelType = 99 then gs_door_type_mr=10

		if lrear_panel=1 AND dob_face=0 then
			aa=(b-left_cab-rrear_cab)/2
			ADD lrear_cab, b-rrear_cab, 0
			ROTZ -90

			FOR HI=1 TO 2
			cabdoortyp_unID=unID
			CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_full_edit=0,
					door_w=aa, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
					doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
					doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
					doorglas_mat=doorframe_mat_end,
					gs_knob_type_m=0,
					gs_cust_p=gs_cust_p_end,fr_th1=0.02,
					cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
					returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			ADDX aa
			NEXT HI
			DEL 2

			DEL 2
		endif

		if rrear_panel=1 then
			aa=(a-right_cab-lrear_cab-(fr_th1*dob_face))/2
			ADD a-right_cab, b-rrear_cab, 0
			ROTZ 180

			FOR HI=1 TO 2

			cabdoortyp_unID=unID
			CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_full_edit=0,
					door_w=aa, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
					doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
					doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
					doorglas_mat=doorframe_mat_end,
					gs_knob_type_m=0,
					gs_cust_p=gs_cust_p_end,fr_th1=0.02,
					cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
					returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			ADDX aa
			NEXT HI
			DEL 2

			DEL 2
		endif

		if left_panel=1 then
			ADD lrear_cab+(fr_th1*dob_face), left_cab, 0

			cabdoortyp_unID=unID
			CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_full_edit=0,
					door_w=depth_cabl-(fr_th1*dob_face)-fr_th1-lrear_cab-front_cab, door_h=cc,
					gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
					doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
					doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
					doorglas_mat=doorframe_mat_end,
					gs_knob_type_m=0,
					gs_cust_p=gs_cust_p_end,fr_th1=0.02,
					cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
					returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			DEL 1
		endif

		if right_panel=1 then
			ADD a-right_cab, b-depth_cabr+front_cab+fr_th1, 0
			ROTZ 90

			cabdoortyp_unID=unID
			CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_full_edit=0,
					door_w=depth_cabr-front_cab-rrear_cab-fr_th1, door_h=cc,
					gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
					doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
					doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
					doorglas_mat=doorframe_mat_end,
					gs_knob_type_m=0,
					gs_cust_p=gs_cust_p_end,fr_th1=0.02,
					cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
					returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			DEL 2
		endif
		DEL 1


		!--- drawers
		!--- left drawer
		if bDrawer then
			aa = b - depth_cabr + front_cab - left_cab - 0.01 + doorOffset/2

			add depth_cabl - front_cab - fr_th1, left_cab, toe_h + cc - drawer_h
			rotz 90
			addx aa
			mulx -1

			cabdoortyp_unID = unID
			call "fa_cabdoortyp" parameters	gs_detlevel_3D_m	= gs_detlevel_3D_m,
											gs_door_type_m 		= _iDrawerType,
											gs_cust_p			= gs_cust_p,
											gs_bevel			= gs_bevel,
											fr_th1				= fr_th1,
											doorframe_w			= doorframe_w,
											door_ang			= 0,
											door_w				= aa,
											door_h				= drawer_h,
											vert_gnum			= 0,
											hor_gnum			= 0,
											gs_knob_type_m		= 0,
											gs_cont_pen			= gs_cont_pen,
											doorframe_mat		= DrawerMat,
											gs_knob_mat			= gs_knob_mat,
											doorglas_mat		= doorglas_mat,
											gs_shadow			= gs_shadow,
											gs_full_edit		= 0,
											cabdoortyp_unID		= cabdoortyp_unID,
											returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			del 1

			if gs_knob_type_m <> 0 and gs_detlevel_3D_m = 2 then
				add -aa / 2, -0.02, drawer_h / 2
				roty -90 
					gosub 6000
				del 1
				del 1
			endif

			del 3

			!--- right drawer
			aa = a - depth_cabl + front_cab - right_cab - 0.01 + doorOffset/2
			add depth_cabl - front_cab + 0.01 -doorOffset/2 , b - depth_cabr + front_cab + fr_th1, toe_h + cc -drawer_h

			cabdoortyp_unID = unID
			call "fa_cabdoortyp" parameters 	gs_detlevel_3D_m	= gs_detlevel_3D_m,
												gs_door_type_m 		= _iDrawerType, 
												gs_cust_p			= gs_cust_p,
												gs_bevel			= gs_bevel,
												door_ang			= 0,
												door_w				= aa, 
												door_h				= drawer_h, 
												doorframe_w			= doorframe_w, 
												gs_cont_pen			= gs_cont_pen,
												fr_th1				= fr_th1,
												doorframe_mat		= DrawerMat, 
												gs_knob_mat			= gs_knob_mat,
												doorglas_mat		= doorglas_mat, 
												vert_gnum			= 0, 
												hor_gnum			= 0,
												gs_knob_type_m		= 0, 
												gs_shadow			= gs_shadow,
												gs_full_edit		= 0,
												cabdoortyp_unID		= cabdoortyp_unID,
												returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

			if gs_knob_type_m <> 0 and gs_detlevel_3D_m = 2 then
				add aa / 2, -0.02, drawer_h / 2
				roty -90
					gosub 6000
				del 1
				del 1
			endif

			del 1

			if dob_face then
				aa = ( b - left_cab - rrear_cab) / 2
				add lrear_cab + fr_th1, b - rrear_cab, toe_h + cc -drawer_h
				rotz -90

				for hi = 1 to 2

					cabdoortyp_unID = unID
					call "fa_cabdoortyp" parameters 	gs_detlevel_3D_m	= gs_detlevel_3D_m,
														gs_door_type_m 		= _iDrawerType, 
														gs_cust_p			= gs_cust_p,
														gs_bevel			= gs_bevel,
														door_ang			= 0,
														fr_th1				= fr_th1,
														doorframe_w			= doorframe_w, 
														door_w				= aa,
														door_h				= drawer_h, 
														vert_gnum			= 0, 
														hor_gnum			= 0,
														gs_knob_type_m		= 0, 
														gs_cont_pen			= gs_cont_pen,
														doorframe_mat		= DrawerMat, 
														gs_knob_mat			= gs_knob_mat,
														doorglas_mat		= doorglas_mat, 
														gs_shadow			= gs_shadow,
														gs_full_edit		= 0, 
														cabdoortyp_unID		= cabdoortyp_unID,
														returned_parameters cabdoortyp_unID

					unID = cabdoortyp_unID

					if gs_knob_type_m <> 0 then
						add aa / 2, -0.02, drawer_h / 2
						roty -90
							gosub 6000
						del 1
						del 1
					endif

					mulx -1
					addx -2 * aa
				next hi
				del 4

				del 2
			endif

		endif
		!! end of drawer !!


		!******************************** DOOR
		if door then
			if gs_bHingedDoubleDoor then

				leftDoorWidth 	= b - depth_cabr + front_cab - left_cab - doorOffset/2
				rightDoorWidth 	= a - depth_cabl + front_cab - right_cab - doorOffset/2

				! --- Hinged Door Opening Side = Left
				if gs_hingedDoorSide_m = 1 then				
	
					leftKnob 	= 0
					rightKnob 	= gs_knob_type_m
	
					add depth_cabl - front_cab - fr_th1, left_cab + doorOffset/2, toe_h
					rotz - door_ang

					rotz 90
					cabdoortyp_unID = unID
					!--- left side
					call "fa_cabdoortyp" parameters gs_detlevel_3D_m = gs_detlevel_3D_m,	
							gs_full_edit 				= 0,
							door_ang 					= 0,
							door_w 						= leftDoorWidth,
							door_h						= cc-drawer_h, 
							gs_door_type_m				= gs_door_type_m, 
							gs_shadow					= gs_shadow,
							doorframe_w					= doorframe_w, 
							gs_cont_pen					= gs_cont_pen,
							vert_gnum					= vert_gnum, 
							hor_gnum					= hor_gnum,
							fr_th1						= fr_th1,
							doorframe_mat				= doorframe_mat, 
							gs_knob_mat					= gs_knob_mat,
							doorglas_mat				= doorglas_mat,
							knob_size					= knob_size,
							gs_knob_type_m				= leftKnob, 
							gs_resol					= gs_resol,
							gs_cust_p					= gs_cust_p,
							gs_knob_p_m					= gs_knob_p_m,
							knob_pl_x					= knob_pl_x,
							knob_pl_y					= knob_pl_y,
							cabdoortyp_unID				= cabdoortyp_unID,
							gs_bevel					= gs_bevel,
							gs_cust_knob 				= gs_cust_knob,
							gs_customHandleLength 		= gs_customHandleLength,
							gs_customHandleHeight 		= gs_customHandleHeight, 
							gs_customHandleDepth 		= gs_customHandleDepth,
							doorOffset					= 0,
							returned_parameters cabdoortyp_unID

					unID = cabdoortyp_unID
					del 1

					add fr_th1, b - depth_cabr + front_cab +  fr_th1 - left_cab - doorOffset/2, 0
					cabdoortyp_unID = unID
					!--- right side
					call "fa_cabdoortyp" parameters gs_detlevel_3D_m = gs_detlevel_3D_m,	
							gs_full_edit				= 0,
							door_ang					= 0,
							door_w						= rightDoorWidth, 
							door_h						= cc-drawer_h, 
							gs_door_type_m				= gs_door_type_m, 
							gs_shadow					= gs_shadow,
							doorframe_w					= doorframe_w, 
							gs_cont_pen					= gs_cont_pen,
							vert_gnum					= vert_gnum, 
							hor_gnum					= hor_gnum,
							fr_th1						= fr_th1,
							doorframe_mat				= doorframe_mat, 
							gs_knob_mat					= gs_knob_mat,
							doorglas_mat				= doorglas_mat,
							knob_size					= knob_size,
							gs_knob_type_m				= rightKnob, 
							gs_resol					= gs_resol,
							gs_cust_p					= gs_cust_p,
							gs_knob_p_m					= gs_knob_p_m,
							knob_pl_x					= knob_pl_x,
							knob_pl_y					= knob_pl_y,
							cabdoortyp_unID				= cabdoortyp_unID,
							gs_bevel					= gs_bevel,
							gs_cust_knob 				= gs_cust_knob, 
							gs_customHandleLength 		= gs_customHandleLength,
							gs_customHandleHeight 		= gs_customHandleHeight, 
							gs_customHandleDepth 		= gs_customHandleDepth,
							doorOffset					= 0,
							returned_parameters cabdoortyp_unID

					unID = cabdoortyp_unID

					del 1

					del 2
				else						
				! --- Hinged Door Opening Side = Right

					rightKnob = 0
					leftKnob = gs_knob_type_m

					add a - right_cab, b - depth_cabr + front_cab + fr_th1, toe_h
					rotz door_ang

					add -(a - depth_cabl + fr_th1 + front_cab - right_cab), -fr_th1, 0
					rotz 90
					mulx -1
					cabdoortyp_unID = unID
					!--- left side
					call "fa_cabdoortyp" parameters gs_detlevel_3D_m = gs_detlevel_3D_m,	
							gs_full_edit 				= 0,
							door_ang 					= 0,
							door_w 						= leftDoorWidth,
							door_h						= cc-drawer_h, 
							gs_door_type_m				= gs_door_type_m, 
							gs_shadow					= gs_shadow,
							doorframe_w					= doorframe_w, 
							gs_cont_pen					= gs_cont_pen,
							vert_gnum					= vert_gnum, 
							hor_gnum					= hor_gnum,
							fr_th1						= fr_th1,
							doorframe_mat				= doorframe_mat, 
							gs_knob_mat					= gs_knob_mat,
							doorglas_mat				= doorglas_mat,
							knob_size					= knob_size,
							gs_knob_type_m				= leftKnob, 
							gs_resol					= gs_resol,
							gs_cust_p					= gs_cust_p,
							gs_knob_p_m					= gs_knob_p_m,
							knob_pl_x					= knob_pl_x,
							knob_pl_y					= knob_pl_y,
							cabdoortyp_unID				= cabdoortyp_unID,
							gs_bevel					= gs_bevel,
							gs_cust_knob 				= gs_cust_knob,
							gs_customHandleLength 		= gs_customHandleLength,
							gs_customHandleHeight 		= gs_customHandleHeight, 
							gs_customHandleDepth 		= gs_customHandleDepth,
							doorOffset					= 0,
							returned_parameters cabdoortyp_unID

					unID = cabdoortyp_unID
					del 3

					add -rightDoorWidth - doorOffset/2, 0, 0
					cabdoortyp_unID = unID
					!--- right side
					call "fa_cabdoortyp" parameters gs_detlevel_3D_m = gs_detlevel_3D_m,	
							gs_full_edit				= 0,
							door_ang					= 0,
							door_w						= rightDoorWidth, 
							door_h						= cc-drawer_h, 
							gs_door_type_m				= gs_door_type_m, 
							gs_shadow					= gs_shadow,
							doorframe_w					= doorframe_w, 
							gs_cont_pen					= gs_cont_pen,
							vert_gnum					= vert_gnum, 
							hor_gnum					= hor_gnum,
							fr_th1						= fr_th1,
							doorframe_mat				= doorframe_mat, 
							gs_knob_mat					= gs_knob_mat,
							doorglas_mat				= doorglas_mat,
							knob_size					= knob_size,
							gs_knob_type_m				= rightKnob, 
							gs_resol					= gs_resol,
							gs_cust_p					= gs_cust_p,
							gs_knob_p_m					= gs_knob_p_m,
							knob_pl_x					= knob_pl_x,
							knob_pl_y					= knob_pl_y,
							cabdoortyp_unID				= cabdoortyp_unID,
							gs_bevel					= gs_bevel,
							gs_cust_knob 				= gs_cust_knob, 
							gs_customHandleLength 		= gs_customHandleLength,
							gs_customHandleHeight 		= gs_customHandleHeight, 
							gs_customHandleDepth 		= gs_customHandleDepth,
							doorOffset					= 0,
							returned_parameters cabdoortyp_unID

					unID = cabdoortyp_unID

					del 1

					del 2
				endif

			else
				leftDoorWidth 	= b - depth_cabr + front_cab - left_cab - 0.01 + doorOffset/2
				rightDoorWidth 	= a - depth_cabl + front_cab - right_cab - 0.01 + doorOffset/2
				add depth_cabl - front_cab - fr_th1, left_cab, toe_h
				rotz 90
				addx leftDoorWidth
				mulx -1

				cabdoortyp_unID=unID
				call "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
						gs_full_edit=gs_full_edit, door_ang=door_ang,
						door_w=leftDoorWidth, door_h=cc-drawer_h, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
						doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
						vert_gnum=vert_gnum, hor_gnum=hor_gnum,fr_th1=fr_th1,
						doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
						doorglas_mat=doorglas_mat,knob_size=knob_size,
						gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
						gs_cust_p=gs_cust_p,gs_knob_p_m=gs_knob_p_m,
						knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
						cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
						gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
						gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
						returned_parameters cabdoortyp_unID

				unID = cabdoortyp_unID
				del 4


				add depth_cabl - front_cab + 0.01 - doorOffset/2, b - depth_cabr + front_cab + fr_th1, toe_h

				cabdoortyp_unID=unID
				CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m,
						gs_full_edit=gs_full_edit,door_ang=door_ang,
						door_w=rightDoorWidth,door_h=cc-drawer_h,
						gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
						doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
						vert_gnum=vert_gnum, hor_gnum=hor_gnum,fr_th1=fr_th1,
						doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
						doorglas_mat=doorglas_mat,knob_size=knob_size,
						gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
						gs_cust_p=gs_cust_p,gs_knob_p_m=gs_knob_p_m,
						knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
						cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
						gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
						gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
						returned_parameters cabdoortyp_unID

				unID = cabdoortyp_unID

				del 1
			endif

			if dob_face=1 then
				aa=(b-left_cab-rrear_cab)/2
				ADD lrear_cab+fr_th1, b-rrear_cab, toe_h
				ROTZ -90

				FOR HI=1 TO 2
					cabdoortyp_unID=unID
					CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_full_edit=1, door_ang=door_ang,
							door_w=aa, door_h=cc-drawer_h, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
							doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
							vert_gnum=vert_gnum, hor_gnum=hor_gnum,
							doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
							doorglas_mat=doorglas_mat,fr_th1=fr_th1,
							gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
							gs_cust_p=gs_cust_p,gs_knob_p_m=gs_knob_p_m,gs_bevel=gs_bevel,
							knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
							cabdoortyp_unID=cabdoortyp_unID,
							gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
							gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
							returned_parameters cabdoortyp_unID

					unID = cabdoortyp_unID

					MULX -1
					ADDX -2*aa
				NEXT HI
				DEL 4

				DEL 2
			endif

		endif
		!! End of door !!

DEL 1

END


!! LEGS - START
20:

	if gs_toe_type_m = 2 Then
		cylind toe_h, r_leg
	Endif

	if gs_toe_type_m = 3 Then
		prism_ 4,toe_h,
				-r_leg,-r_leg,15,
				r_leg,-r_leg,15,
				r_leg,r_leg,15,
				-r_leg,r_leg,15
	Endif
	gosub 1000
	del 1

	RETURN
	!! LEGS - END


1000:
	roty 90
	gosub 5000
	del 1
	return

	2000:
	gosub 5000
	return

	5000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return

6000:
	if gs_detlevel_3D_m=2 then

		if gs_knob_type_m = 9 then
			reqBKnobs = 0
			success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
			if (success = 0 or not(reqBKnobs)) and gs_cust_knob<>"" then
				ADDz -(left_cab+right_cab)/2
				rotx 90
				rotz -90
				addx -gs_customHandleLength / 2
				addy -gs_customHandleHeight / 2
				call "Knob_Collection" PARAMETERS gs_ptype	= gs_cust_knob,
													a		= gs_customHandleLength,
													b		= gs_customHandleHeight,
													zzyzx	= gs_customHandleDepth
				del 5
			endif
		else
			if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
				ADDz 0
				addy -fr_th1+0.02
				!!! knob on top drawer
				CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
						gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
						gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
						gs_knob_mat=gs_knob_mat
				del 2
			else
				if gs_knob_type_m=8 then

					Addz knob_size/2
					addy -fr_th1+0.02
						CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
								gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
								gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
								gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				else
					Addz knob_size/2
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				endif
			endif
		endif
	endif
return

