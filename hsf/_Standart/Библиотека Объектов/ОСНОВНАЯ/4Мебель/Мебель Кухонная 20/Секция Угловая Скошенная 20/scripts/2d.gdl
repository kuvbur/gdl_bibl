
DrawingNum = 1
if DrawingNum = 1 then	! Top
	! Handling overhungs in case of end panels !!!

	pen gs_cont_pen

	! 2D Hotspot
	if cab_size=1 then
		a=a-(lrear_cab+right_cab)
		b=b-(left_cab+rrear_cab)
	endif

	unID=1

	HOTSPOT2 0, B, unID, A, 1+256	:	unID=unID+1
	HOTSPOT2 -0.001, B, unID, A, 3+128	:	unID=unID+1
	HOTSPOT2 A, B, unID, A, 2	:	unID=unID+1

	HOTSPOT2 0, 0, unID, A, 1+256	:	unID=unID+1
	HOTSPOT2 -0.001, 0, unID, A, 3	:	unID=unID+1
	HOTSPOT2 A, 0, unID, A, 2+128	:	unID=unID+1

	HOTSPOT2 0, 0, unID, B, 1+256	:	unID=unID+1
	HOTSPOT2 0, -0.001, unID, B, 3+128	:	unID=unID+1
	HOTSPOT2 0, B, unID, B, 2	:	unID=unID+1

	HOTSPOT2 A, 0, unID, B, 1+128	:	unID=unID+1
	HOTSPOT2 A, -0.001, unID, B, 3	:	unID=unID+1
	HOTSPOT2 A, B, unID, B, 2	:	unID=unID+1

	HOTSPOT2 0, 0, unID, depth_cab, 1+128	:	unID=unID+1
	HOTSPOT2 -0.001, 0, unID, depth_cab, 3	:	unID=unID+1
	HOTSPOT2 depth_cab, 0, unID, depth_cab, 2	:	unID=unID+1

	HOTSPOT2 A, b, unID, depth_cab, 1+128	:	unID=unID+1
	HOTSPOT2 A, b+1, unID, depth_cab, 3	:	unID=unID+1
	HOTSPOT2 A, b-depth_cab, unID, depth_cab, 2	:	unID=unID+1

	if cab_size=0 then
		unID=100
		HOTSPOT2 depth_cab-front_cab, left_cab,unID: unID=unID+1
		HOTSPOT2 a-right_cab, b-depth_cab+front_cab,unID: unID=unID+1
		HOTSPOT2 lrear_cab, b-rrear_cab,unID: unID=unID+1
		HOTSPOT2 a-right_cab, b-rrear_cab,unID: unID=unID+1
		HOTSPOT2 lrear_cab, left_cab,unID: unID=unID+1
	else
		unID=200
		HOTSPOT2 -lrear_cab, -left_cab,unID: unID=unID+1
		HOTSPOT2 depth_cab+front_cab, -left_cab,unID: unID=unID+1
		HOTSPOT2 A+right_cab, B-depth_cab-front_cab,unID: unID=unID+1
		HOTSPOT2 A+right_cab, B+rrear_cab,unID: unID=unID+1
		HOTSPOT2 -lrear_cab, B+rrear_cab,unID: unID=unID+1
	endif

	if cab_size=1 then
		a=a+(lrear_cab+right_cab)
		b=b+(left_cab+rrear_cab)
	endif
	! END of 2D Hotspot

	PEN gs_cont_pen
	LINE_TYPE gs_line_type

	a=a-lrear_cab-right_cab
	b=b-rrear_cab-left_cab
	if cab_size then
		depth_cabl=depth_cab+front_cab+lrear_cab
		depth_cabr=depth_cab+front_cab+rrear_cab
	else
		depth_cabl=depth_cab
		depth_cabr=depth_cab
	endif

	alfa=ATN((b+rrear_cab-depth_cabr+front_cab)/(a+lrear_cab-depth_cabl+front_cab))

	ADD2 lrear_cab*(1-cab_size), left_cab*(1-cab_size)

	LINE2 -lrear_cab, -left_cab,   -lrear_cab, b+rrear_cab
	LINE2 -lrear_cab, b+rrear_cab,   a+right_cab, b+rrear_cab

	if left_cab>0 AND right_cab>0 then
		LINE2 depth_cabl-lrear_cab, -left_cab,   depth_cabl-lrear_cab, -front_cab*(TAN(45-alfa/2))
		LINE2 depth_cabl-lrear_cab, -front_cab*(TAN(45-alfa/2)), a+front_cab*(TAN(alfa/2)), b-depth_cabr+rrear_cab
		LINE2 a+right_cab, b-depth_cabr+rrear_cab, a+front_cab*(TAN(alfa/2)), b-depth_cabr+rrear_cab
		lx=depth_cabl-lrear_cab
		ly=-front_cab*(TAN(45-alfa/2))
		rx=a+front_cab*(TAN(alfa/2))
		ry=b-depth_cabr+rrear_cab
	endif

	if ABS(left_cab)<EPS AND right_cab>0 then
		LINE2 depth_cabl-lrear_cab, 0, a+front_cab*(1-1/(TAN(alfa))), b-depth_cabr+rrear_cab
		LINE2 a+right_cab, b-depth_cabr+rrear_cab, a+front_cab*(1-1/(TAN(alfa))), b-depth_cabr+rrear_cab
		lx=depth_cabl-lrear_cab
		ly=0
		rx=a+front_cab*(1-1/(TAN(alfa)))
		ry=b-depth_cabr+rrear_cab
	endif

	if left_cab>0 AND ABS(right_cab)<EPS then
		LINE2 depth_cabl-lrear_cab, -left_cab,   depth_cabl-lrear_cab, -front_cab*(1-1/(TAN(90-alfa)))
		LINE2 depth_cabl-lrear_cab, -front_cab*(1-1/(TAN(90-alfa))), a, b-depth_cabr+rrear_cab
		lx=depth_cabl-lrear_cab
		ly=-front_cab*(1-1/(TAN(90-alfa)))
		rx=a
		ry=b-depth_cabr+rrear_cab
	endif

	if ABS(left_cab)<EPS AND ABS(right_cab)<EPS then
		LINE2 depth_cabl-lrear_cab, 0, a, b-depth_cabr+rrear_cab
		lx=depth_cabl-lrear_cab
		ly=0
		rx=a
		ry=b-depth_cabr+rrear_cab
	endif

	DRAWINDEX 10
	FILL gs_fill_type

	POLY2_B 8, 2+64, gs_fill_pen, gs_back_pen,
			-lrear_cab, b+rrear_cab, 1,
			a+right_cab, b+rrear_cab, 1,
			a+right_cab, b-depth_cabr+rrear_cab, 1,
			rx, ry, 1,
			lx, ly, 1,
			depth_cabl-lrear_cab, -left_cab, 1,
			-lrear_cab, -left_cab, 1,
			-lrear_cab, b+rrear_cab, -1

	HOTLINE2 -lrear_cab, b+rrear_cab,a+right_cab, b+rrear_cab
	HOTLINE2 a+right_cab, b+rrear_cab,a+right_cab, b-depth_cabr+rrear_cab
	HOTLINE2 a+right_cab, b-depth_cabr+rrear_cab,rx, ry
	HOTLINE2 rx, ry,lx, ly
	HOTLINE2 lx, ly,depth_cabl-lrear_cab, -left_cab
	HOTLINE2 depth_cabl-lrear_cab, -left_cab,-lrear_cab, -left_cab
	HOTLINE2 -lrear_cab, -left_cab,-lrear_cab, b+rrear_cab

	ADD2 depth_cabl-lrear_cab+(rx-lx)/2, ly+(ry-ly)/2
	ROT2 ATN((ry-ly)/(rx-lx))
	if gs_sink_type_m=1 then			!!SINK 1
		sink_a=0.4
		sink_b=0.4
		sink_z=0.2
	endif
	if gs_sink_type_m=2 then			!!SINK 2
		sink_a=0.55
		sink_b=0.55
		sink_z=0.2
	endif
	ADD2 -sink_a/2, front_cab+3"

	call "fa_sinktyp" parameters all	a				= sink_a,
										b				= sink_b,
										zzyzx			= sink_z,
										bEnableTapEdit	= 1,
										bMovableDrain	= 0

	if gs_sink_type_m = 1 then
		HOTARC2 sink_a/2,sink_b/2,sink_a/2,0,180
		HOTARC2 sink_a/2,sink_b/2,sink_a/2,180,360
	endif
	if gs_sink_type_m = 2 then
		HOTLINE2 0,0,sink_a,0
		HOTLINE2 sink_a,0,sink_a,sink_b
		HOTLINE2 sink_a,sink_b,0,sink_b
		HOTLINE2 0,0,0,sink_b
	endif
	del 1
	del 2
	side_vis2D_m = side_vis2D_m +1
	if side_vis2D_m=1 then 15
	if side_vis2D_m=2 then 10
	LINE2 a+right_cab, b+rrear_cab,   a+right_cab, b-depth_cabr+rrear_cab

	10:
		if side_vis2D_m=3 then 15
		LINE2 -lrear_cab, -left_cab,  depth_cabl-lrear_cab, -left_cab

	15:
		if not(bs_h<=0) then
			line2 0.02-lrear_cab, b-0.02+rrear_cab,   a+right_cab, b-0.02+rrear_cab
			line2 0.02-lrear_cab, b-0.02+rrear_cab,   0.02-lrear_cab, -left_cab
		endif

		reqBMinSpace = 0

		call "fa_minimalspace" parameters all MSType = 0, unID = 3000,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen,
													MSFillBackPen,
													MSContLineType,
													MSContLinePen

		if reqBMinSpace <> 0 then

			FILL MSFillType
			pen MSContLinePen
			line_type MSContLineType

			alpha=atn((ry-ly)/(rx-lx))
			lcab_side=sin(alpha)*left_cab
			rcab_side=cos(alpha)*right_cab

			POLY2_B 8, 1+2+4+64,MSFillPen,MSFillBackPen,
				a+right_cab, b-depth_cabr+rrear_cab,1,
				rx, ry,1,
				lx, ly,1,
				depth_cabl-lrear_cab, -left_cab,1,

				depth_cabl-lrear_cab-MSSide2/cos(alpha), -left_cab,1,
				lx+sin(alpha)*MSFront-cos(alpha)*lcab_side-cos(alpha)*MSSide2, ly-cos(alpha)*MSFront-sin(alpha)*lcab_side-sin(alpha)*MSSide2,1,

				rx+sin(alpha)*MSFront+cos(alpha)*rcab_side+cos(alpha)*MSSide, ry-cos(alpha)*MSFront+sin(alpha)*rcab_side+sin(alpha)*MSSide,1,
				a+right_cab, b-depth_cabr+rrear_cab+MSSide/sin(alpha),1

			HOTSPOT2 depth_cabl-lrear_cab-MSSide2/cos(alpha), -left_cab,		unID: unID=unID+1
			HOTSPOT2 lx+sin(alpha)*MSFront-cos(alpha)*lcab_side-cos(alpha)*MSSide2, ly-cos(alpha)*MSFront-sin(alpha)*lcab_side-sin(alpha)*MSSide2,	unID: unID=unID+1
			HOTSPOT2 rx+sin(alpha)*MSFront+cos(alpha)*rcab_side+cos(alpha)*MSSide, ry-cos(alpha)*MSFront+sin(alpha)*rcab_side+sin(alpha)*MSSide,		unID: unID=unID+1
			HOTSPOT2 a+right_cab, b-depth_cabr+rrear_cab+MSSide/sin(alpha),		unID: unID=unID+1

		endif

	del 1
	PEN gs_cont_pen
	LINE_TYPE gs_line_type

	ADD2 lrear_cab*(1-cab_size), left_cab*(1-cab_size)
	if iCabSymbolType2D <> 1 then
		if iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 then
				LINE2 	rx, ry+0.025,	lx-0.025, ly
				LINE2 	lx-0.025, -left_cab,	lx-0.025, ly
				LINE2 	rx, ry+0.025,	a+right_cab, ry+0.025
		endif
		if iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 then
				poly2_ 4,1+4,
					rx,ry,1,
					lx,ly,901,
					0,-15,4001,
					lx,ly,1
		endif
	endif
	del 1
endif


! Show MEP Connections -------------------------------------------------------------[

isavailable = 0
isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)
isMEPEnabled = (isavailable OR isInArchiCAD = 0)
if isMEPEnabled then
	bShowMEP = 1
	reqBMep = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showMepConnect", reqBMep)
	if success > 0 then
		bShowMEP = reqBMep
	endif
else
	bShowMEP = 0
endif

! Show MEP Connections -------------------------------------------------------------]

! start of modifying to Shaft

IF bShowMEP THEN

	PEN gs_cont_pen

	! OTHER VIEWS

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	if DrawingNum = 2 then PROJECT2 -3, 90, 2	! Bottom
	if DrawingNum = 3 then PROJECT2 4, 270, 2	! Front
	if DrawingNum = 4 then PROJECT2 4, 90, 2	! Back
	if DrawingNum = 5 then PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	if DrawingNum = 6 then PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left

	! CONNECTIONS

	CALL "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 12,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 12
	del 1
endif

! end of modifying to Shaft
