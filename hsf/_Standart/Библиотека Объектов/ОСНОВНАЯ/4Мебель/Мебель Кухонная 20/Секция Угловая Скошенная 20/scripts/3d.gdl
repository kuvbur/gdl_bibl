
if gs_detlevel_3D_m=0 then end
if not(gs_shadow) then SHADOW OFF

! start of modifying to Shaft

! CONNECTIONS

! Show MEP Connections -------------------------------------------------------------[

isavailable = 0
isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)
isMEPEnabled = (isavailable OR isInArchiCAD = 0)
if isMEPEnabled then
	bShowMEP = 1
	reqBMep = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showMepConnect", reqBMep)
	if success > 0 then
		bShowMEP = reqBMep
	endif
else
	bShowMEP = 0
endif

! Show MEP Connections -------------------------------------------------------------]

IF bShowMEP THEN

	if useSysMat then
		toe_mat			= sMat
		count_mat		= sMat
		bs_mat			= sMat
		gs_cabinet_mat	= sMat
		doorglas_mat	= sMat
		doorframe_mat	= sMat
		doorframe_mat_end	= sMat
		gs_sink_mat		= sMat
		gs_tap_mat		= sMat
		gs_knob_mat		= sMat
		gs_ShelfMat		= sMat
		DrawerMat		= sMat
		gs_con_mat		= sMat
	endif

	CALL "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 16
endif

! end of modifying to Shaft

if gs_detlevel_3D_m=1 then gs_resol = 12   !keep the resol in syncron with the "fa_sinktyp" simple resol
RESOL gs_resol

PEN gs_cont_pen

if cab_size then
	depth_cabl=depth_cab+front_cab+lrear_cab
	depth_cabr=depth_cab+front_cab+rrear_cab
else
	depth_cabl=depth_cab
	depth_cabr=depth_cab
endif

ADD a/2-(lrear_cab)*cab_size, b/2-(left_cab)*cab_size, 0

! 3D HOTSPOTS
! at base

unID=1

if gs_toe_type_m = 0 then
	HOTSPOT -a/2+lrear_cab, -b/2+left_cab, 0, unID : unID=unID+1
	HOTSPOT -a/2+depth_cabl-front_cab-fr_th1, -b/2+left_cab, 0, unID : unID=unID+1
	HOTSPOT  a/2-right_cab, b/2-depth_cabr+front_cab+fr_th1, 0, unID : unID=unID+1
	HOTSPOT  a/2-right_cab, b/2-rrear_cab, 0, unID : unID=unID+1
	HOTSPOT -a/2+lrear_cab, b/2-rrear_cab, 0, unID : unID=unID+1
endif

! cabinet height
unID=10

hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2+depth_cabl, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2+depth_cabl, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2+depth_cabl, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2-depth_cabr, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2-depth_cabr, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2-depth_cabr, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1
! End of 3D hotspots

! Handling overhangs in case of end panels
!lrear_cab = 0	! no overhang
!rrear_cab = 0
!left_cab = 0
!right_cab = 0

if lrear_panel then lrear_cab = lrear_cab + 0.02
if rrear_panel then rrear_cab = rrear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02

a=a-lrear_cab-right_cab
b=b-rrear_cab-left_cab

ADD -a/2+lrear_cab/2-right_cab/2, -b/2-rrear_cab/2+left_cab/2, 0

! CABINET
if ABS(a-depth_cab)<EPS then a=depth_cab+0.001

unID=100
corncab_unID=unID

CALL "fa_gen_corncabinet" parameters a=a, b=b, zzyzx=zzyzx,
		gs_detlevel_3D_m=gs_detlevel_3D_m,
		dob_face=0, gs_shadow=gs_shadow, ldepth_cab=depth_cabl-lrear_cab-front_cab,
		rdepth_cab=depth_cabr-rrear_cab-front_cab, count_th=count_th,
		front_cab=front_cab, right_cab=right_cab, left_cab=left_cab, lrear_cab=lrear_cab,
		rrear_cab=rrear_cab, toe_h=toe_h,
		bs_h=bs_h, gs_sink_type_m=gs_sink_type_m, gs_tap_type_m=gs_tap_type_m, gs_resol = gs_resol,
		side_vis_m=side_vis_m+1,fr_th1=fr_th1,
		gs_cont_pen=gs_cont_pen,
		toe_mat=toe_mat, gs_cabinet_mat=gs_cabinet_mat, count_mat=count_mat, bs_mat=bs_mat,
		gs_sink_mat=gs_sink_mat, gs_tap_mat=gs_tap_mat,
		gs_toe_type_m=gs_toe_type_m,gs_count_type_m=gs_count_type_m,toe_d = (toe_d*sin(45)),
		corncab_unID=corncab_unID, sunder=sunder,
		tapOffset=tapOffset,
		returned_parameters corncab_unID

unID = corncab_unID

if bDrawer AND zzyzx-count_th-toe_h<0.31 then END
if not(bDrawer) AND zzyzx-count_th-toe_h<0.18 then END

aa=SQR((a-(depth_cabl-lrear_cab)+front_cab+fr_th1-0.02)^2+(b-(depth_cabr-rrear_cab)+front_cab+fr_th1-0.02)^2): accessno=1
alpha2=ATN((b-(depth_cabr-rrear_cab)+front_cab+fr_th1)/(a-(depth_cabl-lrear_cab)+front_cab+fr_th1))

if gs_detlevel_3D_m=2 then
	! SHELVES
	material gs_cabinet_mat

	if bDrawer then
		ADDZ zzyzx-count_th-drawer_h-0.01
		aa=0.02

		if gs_sink_type_m=0 then

		PRISM_ 6,0.02,
			   aa, 0.02, 15,
			   depth_cabl-lrear_cab+0.02/TAN(alpha2)-front_cab-fr_th1, 0.02, 15,
			   a-0.02, b-depth_cabr-0.02/TAN(90-alpha2)+front_cab+rrear_cab+fr_th1, 15,
			   a-0.02, b-0.02, 15,
			   aa, b-0.02, 15,
			   aa, 0.02, -1
		endif
		del 1
	endif

	if shelf_num>0 AND A>0.04 then
		for ii=1 to shelf_num
			ADDZ toe_h+((zzyzx-count_th-toe_h-0.02-drawer_h+bDrawer*0.01)/(shelf_num+1))*(ii)
			aa=0.02

				material gs_ShelfMat
				PRISM_ 6,0.02,
				   aa, 0.02, 15,
				   depth_cabl-lrear_cab+0.02/TAN(alpha2)-front_cab-fr_th1, 0.02, 15,
				   a-0.02, b-depth_cabr-0.02/TAN(90-alpha2)+front_cab+rrear_cab+fr_th1, 15,
				   a-0.02, b-0.02, 15,
				   aa, b-0.02, 15,
				   aa, 0.02, -1

			del 1
		next ii
	endif
endif

! END PANELS

cc=zzyzx-count_th-toe_h
ADDZ toe_h

accessno=2
! on rear panel there is no glas and grilles and knobs!!

if iEndPanelType = 1 	then gs_door_type_mr=1
if iEndPanelType = 2 	then gs_door_type_mr=2
if iEndPanelType = 4 	then gs_door_type_mr=11
if iEndPanelType = 7 	then gs_door_type_mr=4
if iEndPanelType = 9 	then gs_door_type_mr=6
if iEndPanelType = 99 then gs_door_type_mr=10

if lrear_panel=1 then
	ADD 0, b, 0
	ROTZ -90

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=b, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
			gs_knob_type_m=0, gs_resol=gs_resol,
			gs_cust_p=gs_cust_p_end,fr_th1=0.02,
			cabdoortyp_unID=cabdoortyp_unID,
	returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	del 2
endif

if rrear_panel=1 then
	ADD a, b, 0
	ROTZ 180

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=a, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
			gs_knob_type_m=0, gs_resol=gs_resol,
			gs_cust_p=gs_cust_p_end,fr_th1=0.02,
			cabdoortyp_unID=cabdoortyp_unID,
	returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	del 2
endif

if left_panel=1 then

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
		door_w=depth_cabl-front_cab-lrear_cab-fr_th1, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
		doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
		doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
		doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
		gs_knob_type_m=0, gs_resol=gs_resol,
		gs_cust_p=gs_cust_p_end,fr_th1=0.02,
		cabdoortyp_unID=cabdoortyp_unID,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
endif

if right_panel=1 then
	ADD a, b-depth_cabr+front_cab+rrear_cab+fr_th1, 0
	ROTZ 90

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=depth_cabr-front_cab-rrear_cab-fr_th1, door_h=cc, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorframe_mat_end,gs_bevel=gs_bevel,
			gs_knob_type_m=0, gs_resol=gs_resol,
			gs_cust_p=gs_cust_p_end,fr_th1=0.02,
			cabdoortyp_unID=cabdoortyp_unID,
	returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	del 2
endif
del 1

!--- Door/Drawer
aa = SQR ((a - (depth_cabl - lrear_cab) + front_cab + fr_th1 - 0.02)^2 + (b - (depth_cabr - rrear_cab) + front_cab + fr_th1 - 0.02)^2) : accessno = 1

if bDrawer then

	cc = zzyzx - count_th - toe_h - drawer_h

	add depth_cabl - front_cab - fr_th1 - lrear_cab, 0, zzyzx - count_th - drawer_h
	rotz  alpha2
	addx 0.02 * SQR(2) / 2

	cabdoortyp_unID = unID
	call "fa_cabdoortyp" parameters gs_detlevel_3D_m	= gs_detlevel_3D_m,
									gs_door_type_m 		= _iDrawerType,
									gs_cust_p			= gs_cust_p,
									gs_bevel			= gs_bevel,
									fr_th1				= fr_th1,
									doorframe_w			= doorframe_w,
									door_w				= aa,
									door_h				= drawer_h,
									vert_gnum			= 0,
									hor_gnum			= 0,
									gs_knob_type_m		= 0,
									gs_cont_pen			= gs_cont_pen,
									doorglas_mat		= doorglas_mat,
									doorframe_mat		= DrawerMat,
									gs_shadow			= gs_shadow,
									gs_full_edit		= 0,
									cabdoortyp_unID		= cabdoortyp_unID,
									returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	if gs_knob_type_m <> 0 then
		add  aa / 2, -0.02, drawer_h / 2
		roty 90
		gosub "knobs"
		del 2
	endif
	del  3
endif

!--- Door


if gs_door_type_m > 0 then
	ADD depth_cabl-front_cab-fr_th1-lrear_cab,0,toe_h
	ROTZ alpha2
	ADDx 0.02*SQR(2)/2

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang,
			door_w=aa, door_h=cc, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,
			doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorglas_mat,fr_th1=fr_th1,
			gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
			gs_cust_p=gs_cust_p,gs_knob_p_m=gs_knob_p_m,knob_size=knob_size,
			knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	del 3
endif

END


"knobs":
	if gs_detlevel_3D_m=2 then
		if gs_knob_type_m = 9 then
			reqBKnobs = 0
			success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
			if (success = 0 or not(reqBKnobs)) and gs_cust_knob<>"" then
				ADDz -(left_cab+right_cab)/2
				rotx 90
				rotz 90
				addx -gs_customHandleLength / 2
				addy -gs_customHandleHeight / 2
				call "Knob_Collection" PARAMETERS gs_ptype	= gs_cust_knob,
													a		= gs_customHandleLength,
													b		= gs_customHandleHeight,
													zzyzx	= gs_customHandleDepth
				del 5
			endif
		else
			if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
				ADDz 0
				addy -fr_th1+0.02
				! knob on top drawer
				CALL "fa_knobtyp" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
						gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
						gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
						gs_knob_mat=gs_knob_mat
				del 2
			else
				if gs_knob_type_m=8 then
					roty 180
					Addz knob_size/2
					addy -fr_th1+0.02
					gosub "knobmacro"
					del 3
				else
					Addz knob_size/2
					addy -fr_th1+0.02
					gosub "knobmacro"
					del 2
				endif
			endif
		endif
	endif
return

"knobmacro":
	CALL "fa_knobtyp" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
			gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
			gs_knob_mat=gs_knob_mat,knob_size=knob_size
return
