
IF gs_detlevel_3D_m=0 then END
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

IF gs_detlevel_3D_m=1 THEN gs_resol = 4
RESOL gs_resol

IF not(gs_shadow) THEN SHADOW OFF


! --- Handling overhungs in case of end panels ---

if rear_panel then rear_cab = rear_cab + 0.02
if rear_panel then rear_cab = rear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02


unID=1

PEN gs_cont_pen

dob_face = 0

! --- CALL DRAWERS ---

MATERIAL gs_cabinet_mat

IF gs_door_type_m > EPS THEN
	for i=1 to drawer_num

		if iKnobVertPos = KNOB_VERTPOS_TOP		then knob_h = (gs_drawerHeights[i])-min(gs_drawerHeights[i]/2, 0.13/2)
		if iKnobVertPos = KNOB_VERTPOS_MIDDLE	then knob_h = (gs_drawerHeights[i])/2
		if iKnobVertPos = KNOB_VERTPOS_BOTTOM	then knob_h = min(gs_drawerHeights[i]/2, 0.13/2)

		add left_cab, front_cab+fr_th1, toe_h

		cabdoortyp_unID=unID
		CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m, gs_full_edit = 0,
				door_w = a - left_cab - right_cab, door_h = gs_drawerHeights[i], gs_door_type_m = gs_door_type_m, gs_shadow = gs_shadow,
				doorframe_w = doorframe_w, gs_cont_pen = gs_cont_pen,fr_th1 = fr_th1,
				doorframe_mat = doorframe_mat, doorglas_mat = doorglas_mat, gs_knob_type_m = 0,gs_cust_p=gs_cust_p,
				cabdoortyp_unID = cabdoortyp_unID, gs_bevel = gs_bevel, vert_gnum = vert_gnum, hor_gnum = hor_gnum,
				returned_parameters cabdoortyp_unID

				unID = cabdoortyp_unID

		if gs_knob_type_m<>0 AND gs_detlevel_3D_m=2 then
			add a/2-(left_cab+right_cab)/2,  -fr_th1, knob_h
!			hotspot 0,         -fr_th1, 0, unID, knob_pl_x, 1+128, knob_pl_x	: unID=unID+1
!			hotspot -1,        -fr_th1, 0, unID, knob_pl_x, 3, knob_pl_x		: unID=unID+1
!			hotspot knob_pl_x, -fr_th1, 0, unID, knob_pl_x, 2, knob_pl_x		: unID=unID+1

			for j = 1 to 1 + gs_bDoubleKnob
				if gs_bDoubleKnob then
					if j = 1 then
						addx a/2 - gs_knobDistFromSide
					else
						addx -a/2 + gs_knobDistFromSide
					endif
				else
					addx knob_pl_x
				endif

				ROTY 90
				if gs_knob_type_m = 9 then
					reqBKnobs = 0
					success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
					if (success = 0 or not(reqBKnobs)) and gs_cust_knob<>"" then
						ADDz -(left_cab+right_cab)/2
						rotx 90
						rotz 90
						addx -gs_customHandleLength / 2
						addy -gs_customHandleHeight / 2
						call "Knob_Collection" PARAMETERS gs_ptype	= gs_cust_knob,
															a		= gs_customHandleLength,
															b		= gs_customHandleHeight,
															zzyzx	= gs_customHandleDepth
						del 5
					endif
				else
					if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 then
						CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
								gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
								gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
								gs_knob_mat=gs_knob_mat
					else
						if gs_knob_type_m=8 then
							roty 180
							addz knob_size/2
						else
							addz knob_size/2
						endif
						CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
								gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
								gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
								gs_knob_mat=gs_knob_mat,knob_size=knob_size
						if gs_knob_type_m=8 then
							del 2
						else
							del 1
						endif
					endif
				endif
				del 2
			next j
			del 1
		endif
		del 1

		addz toe_h+0.02
		IF i>1 THEN
			prism_	5, -0.02,
				left_cab+0.02,front_cab+fr_th1, 15,
				left_cab+0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,front_cab+fr_th1, 15,
				left_cab+0.02,front_cab+fr_th1, -1
		ENDIF
		del 1
		addz gs_drawerHeights[i]

	next i
	del drawer_num
endif

! --- CALL GENERAL BASE CABINET -----------------------------------------------

if not(isFrom14) then unID=100
CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx,
		gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=dob_face, count_th=count_th,
		front_cab=front_cab, rear_cab=rear_cab, left_cab=left_cab,
		right_cab=right_cab, toe_h=toe_h, side_vis_m=side_vis_m+1,
		sink=0, shelf_num=0, gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
		toe_mat=toe_mat, gs_cabinet_mat=gs_cabinet_mat, drawer_h=0.13,
		gs_toe_type_m=gs_toe_type_m,toe_d = toe_d, ToeHtps3D=1,
		fr_th1=fr_th1,
		basecab_unID=unID,
		returned_parameters unID


! --- CALL COUNTERTOP and SINK ------------------------------------------------

CALL "fa_counter_sink" PARAMETERS ALL dob_face		= 0,
									side_vis_m		= side_vis_m + 1,
									countsink_unID	= unID,
									gs_sink_type_m	= 0,
						returned_parameters unID



! --- CALL END PANELS ---------------------------------------------------------

IF rear_panel=1 then
	door_w=a-left_cab-right_cab
	door_h=ZZYZX-toe_h-count_th
	ADD door_w+left_cab,b-rear_cab,toe_h
	ROTZ 180
		GOSUB 40
	DEL 2
ENDIF

IF left_panel=1 then										! LEFT END PANEL
	door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face)
	door_h=ZZYZX-toe_h-count_th
	ADD left_cab,door_w+front_cab+fr_th1,toe_h
	ROTZ -90
		GOSUB 40
	DEL 2
ENDIF

IF right_panel=1 then										! RIGHT END PANEL
	door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face)
	door_h=ZZYZX-toe_h-count_th
	ADD a-right_cab,front_cab+fr_th1,toe_h
	ROTZ 90
		GOSUB 40
	DEL 2
ENDIF
END

! ===============================================================================
! =================================== RETURN ====================================
! ===============================================================================

! --- FRONT DOOR_LEFT ---------------------------------------------------------
20:
	CALL "fa_cabdoortyp" PARAMETERS ALL
RETURN

! --- END PANELS --------------------------------------------------------------
40:
	! on rear panel there is no glas and grilles and knobs!!
	gs_knob_type_m=0
	If iEndPanelType = 1 	then gs_door_type_m=1
	If iEndPanelType = 2 	then gs_door_type_m=2
	If iEndPanelType = 4 	then gs_door_type_m=11
	If iEndPanelType = 7 	then gs_door_type_m=4
	If iEndPanelType = 9 	then gs_door_type_m=6
	If iEndPanelType = 99 	then gs_door_type_m=10
	gs_full_edit=0
	door_ang=0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS ALL cabdoortyp_unID=cabdoortyp_unID,
		fr_th1=0.02,gs_cust_p=gs_cust_p_end,doorframe_w=doorframe_w_end,
		doorframe_mat=doorframe_mat_end,doorglas_mat=doorframe_mat_end,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

RETURN
