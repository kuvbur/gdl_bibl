
ui_current_page gs_ui_current_page

dim pageNames[7]
	pageNames[1] = ""
	pageNames[2] = `Столешница`
	pageNames[3] = `Ящик`
	pageNames[4] = ""
	pageNames[5] = ""
	pageNames[6] = ""
	pageNames[7] = `Распределение Ящиков`

dim pageIcos[7]
	pageIcos[1] = ""
	pageIcos[2] = "uiTab_FurnitureCounter_flat(1)"
	pageIcos[3] = "uiTab_Drawer_flat(1)"
	pageIcos[4] = ""
	pageIcos[5] = ""
	pageIcos[6] = ""
	pageIcos[7] = "uiTab_Drawer_flat(1)"

iPageIDRoot 		=-1
iPageIDCounter		= 2
iPageIDDrawer		= 3
iPageIDDistribution	= 7

pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

CALL "Kitchen Cabinets UI macro" parameters all \
									pageIds = pageIds,
									pageNames = pageNames,
									pageIcos = pageIcos,
									CornCabType = 0,
									bEdgeBevel = 1,
									CabinetType = 0,
									bCounterOverhang = 1,
									bMirror = 1,

									CornerEdgeSizes = 0,
									bToe = 1,
									bShelf = 0,
									bShowSurfaces = 1,

									bDoubleFacadeEdit = 0,
									bOneRearPanelEdit = 1,
									bHorKnobPosition = 1,
									bVerKnobPosition = 0,
									bStVerKnobPosition = 1,
									bPositionType = 0,
									bEnableDoubleKnobs = 1,
									gs_bDoubleKnob	= gs_bDoubleKnob,

									nDoors = 0,

									bCabSymbol = 0,
									bPossibleSinknTap = 0,
									bApplianceEdit = 0,
									bSymbLineEdit = 0,
									bShadowEdit = 1,
									bHotspotEdit = 1,

									bEnableBackSplash	= 1,

									bWideCab = 0,
									bSideSpace = 0


UI_PAGE iPageIDCounter, iPageIDRoot, `Столешница`, "uiTab_FurnitureCounter_flat(1)"
	gosub "counterPage"

UI_PAGE iPageIDDrawer, iPageIDRoot, `Ящик`, "uiTab_Drawer_flat(1)"
	gosub "drawerPage"

UI_PAGE iPageIDDistribution, iPageIDRoot, `Распределение Ящиков`, "uiTab_Drawer_flat(1)"
	gosub "distributionPage"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"counterPage":
! ------------------------------------------------------------------------------

	XX=80
	YY=pageStartY + 45
	PX=105
	CX= 70

	UI_STYLE 0,1
	UI_OUTFIELD	`Свесы Столешницы:`, 5,	pageStartY + 5,	140,13
	UI_STYLE 0,0

	UI_PICT	"ui_CabTopView(13)",	XX,	YY,	PX,	PX,1

	UI_INFIELD	"rear_cab",		XX-CX-2,	YY,			CX,	18 UI_TOOLTIP `Выступ Сзади`
	UI_INFIELD	"front_cab",	XX-CX-2,	YY+PX-18,	CX,	18 UI_TOOLTIP `Выступ Спереди`

	IF SYMB_MIRRORED THEN
		UI_INFIELD	"left_cab_mirror",		XX-CX/4,	YY+PX+3,	CX,	18 UI_TOOLTIP `Выступ Слева`
		UI_INFIELD	"right_cab_mirror",		XX+PX-CX/2,	YY+PX+3,	CX,	18 UI_TOOLTIP `Выступ Справа`
	ELSE
		UI_INFIELD	"left_cab",		XX-CX/4,	YY+PX+3,	CX,	18 UI_TOOLTIP `Выступ Слева`
		UI_INFIELD	"right_cab",	XX+PX-CX/2,	YY+PX+3,	CX,	18 UI_TOOLTIP `Выступ Справа`
	ENDIF

	UI_STYLE 0,1
	UI_OUTFIELD	`Торец Столешницы:`, 250, pageStartY + 5, 140, 13
	UI_STYLE 0,0

	call "Kitchen Cabinets UI macro" parameters bCounterInfieldOnly = 1,
												counterInfieldXPos	= 270,
												counterInfieldYPos	= pageStartY + 25

	UI_STYLE 0,1
	UI_OUTFIELD	`Покрытия`, 250, pageStartY + 55, 140, 13
	UI_STYLE 0,0

	UI_OUTFIELD	`Столешница`, 		270, pageStartY + 75,  140, 13
	UI_INFIELD	"count_mat", 	270, pageStartY + 95,  130, 22
	UI_OUTFIELD	`Фартук`, 	270, pageStartY + 125, 140, 13
	UI_INFIELD	"bs_mat", 		270, pageStartY + 145, 130, 22



return

! ------------------------------------------------------------------------------
"drawerPage":
! ------------------------------------------------------------------------------

	UI_STYLE 0,1
	UI_OUTFIELD	`Стиль Ящиков`, 5,	pageStartY + 5,	140,13
	UI_STYLE 0,0

	UI_INFIELD{3}	"gs_door_type",	30,	pageStartY + 20,	150,	90,
		2,"ui_FurnitureCabDoortyp(1)",20,5,
		65,98,60,80,
		2,	`Стиль 1`,		`Стиль 1`,
		3,	`Стиль 2`,		`Стиль 2`,
		4,	`Стиль 3`,		`Стиль 3`,
		12,	`Стиль 4`,		`Стиль 4`,
		13,	`Стиль 5`,		`Стиль 5`,
		10,	`Стиль 6`,		`Стиль 6`,
		5,	`Стиль 7`,		`Стиль 7`,
		6,	`Стиль 8`,		`Стиль 8`,
		7,	`Стиль 9`,		`Стиль 9`,
		8,	`Стиль 10`,		`Стиль 10`,
		11,	`Спец. панель`, `Спец. панель`,
		1,	`Нет`, 		`Нет`

	UI_INFIELD{3} "gs_bevel", 5, pageStartY + 130, 100, 18,
		7, "", 2, 1, 0, 0, 0, 0,
		1, `Фаска Торца`, 0,
		2, `Фаска Торца`, 1

	UI_OUTFIELD `Имя спец. панели`, 	5, 		pageStartY + 155, 120, 18
	UI_INFIELD "gs_cust_p",				130,	pageStartY + 155, 90, 18
	UI_OUTFIELD `Ширина Обвязки`, 	5, 		pageStartY + 180, 120, 18
	UI_INFIELD "doorframe_w", 			130,	pageStartY + 180, 90, 18


	UI_STYLE 0,1
	UI_OUTFIELD	`Покрытия`,		235,	pageStartY + 5,	100,	13
	UI_STYLE 0,0

	UI_OUTFIELD	`Верхний Ящик`,		235,	pageStartY + 27,	100,	13
	UI_OUTFIELD	`Другие Ящики`,	235,	pageStartY + 49,	100,	13
	UI_OUTFIELD	`Стекло`,			235,	pageStartY + 71,	100,	13

	UI_INFIELD	"DrawerMat",		338,	pageStartY + 27 - 4,		100,	22
	UI_INFIELD	"doorframe_mat",	338,	pageStartY + 49 - 2,		100,	22
	UI_INFIELD	"doorglas_mat",		338,	pageStartY + 71 - 2,		100,	22


return

! ------------------------------------------------------------------------------
"distributionPage":
! ------------------------------------------------------------------------------

	y1 = pageStartY + 8
	y2 = pageStartY + 45
	y3 = pageStartY + 185

	x1 = 5
	x2 = 145
	x3 = 240
	x4 = 365

	UI_OUTFIELD `Количество Ящиков`, x1, y1, x2-x1-1, 18
	UI_INFIELD "drawer_num", x2, y1, 65, 18

	UI_OUTFIELD `Схема`, x1, y2, 100, 18
	UI_INFIELD{3} "gs_drawerScheme_m", 110, y2, 100, 130,
			2, 1,			!drawerScheme
			5, 1,
			89, 123, 89, 123,
			1, `Схема 1`, 1,
			2, `Схема 2`, 2,
			3, `Схема 3`, 3,
			4, `Схема 4`, 4,
			5, `Спец.`, 5

	UI_OUTFIELD `Толщина Спереди`, x1, y3, x2-x1-1, 18
	UI_INFIELD "fr_th1", x2, y3, 65, 18

	UI_SEPARATOR 222, pageStartY + 5, 222, pageStartY + 225

	UI_OUTFIELD `Высота Ящиков`,  x3, y1, 150, 18

	IF gs_drawerScheme_m = 2 THEN
		UI_OUTFIELD `Верхний Ящик`, x3, y2, x4-x3-1, 18
		UI_INFIELD "gs_topDrawerH", x4, y2, 65, 18
	ENDIF

	IF gs_drawerScheme_m = 3 THEN
		UI_OUTFIELD `Нижний Ящик`,  x3, y2, x4-x3-1, 18
		UI_INFIELD "gs_bottomDrawerH", x4, y2, 65, 18
	ENDIF

	IF gs_drawerScheme_m = 4 THEN
		UI_OUTFIELD `Верхний Ящик`, x3, y2, x4-x3-1, 18
		UI_INFIELD "gs_topDrawerH", x4, y2, 65, 18
		UI_OUTFIELD `Нижний Ящик`,  x3, y2+25, x4-x3-1, 18
		UI_INFIELD "gs_bottomDrawerH", x4, y2+25, 65, 18
	ENDIF

	IF gs_drawerScheme_m = 5 THEN
		IF drawer_num = 1 THEN
			UI_PICT 2, x3, y2, 109, 135, 1			!drawer1
			y0 = 58
		ENDIF
		IF drawer_num = 2 THEN
			UI_PICT 3, x3, y2, 109, 135, 1			!drawer2
			y0 = 30
			dy = 58
		ENDIF
		IF drawer_num = 3 THEN
			UI_PICT 4, x3, y2, 109, 135, 1			!drawer3
			y0 = 15
			dy = 45
		ENDIF
		IF drawer_num = 4 THEN
			UI_PICT 5, x3, y2, 109, 135, 1			!drawer4
			y0 = 10
			dy = 34
		ENDIF
		IF drawer_num = 5 THEN
			UI_PICT 6, x3, y2, 109, 135, 1			!drawer5
			y0 = 5
			dy = 27
		ENDIF
		IF drawer_num = 6 THEN
			UI_PICT 7, x3, y2, 109, 135, 1			!drawer6
			y0 = 3
			dy = 22
		ENDIF

		y = y2 + y0


		IF drawer_num > 5 THEN
			UI_INFIELD "gs_drawerH_6", x4, y, 65, 18
			y = y + dy
		ENDIF
		IF drawer_num > 4 THEN
			UI_INFIELD "gs_drawerH_5", x4, y, 65, 18
			y = y + dy
		ENDIF
		IF drawer_num > 3 THEN
			UI_INFIELD "gs_drawerH_4", x4, y, 65, 18
			y = y + dy
		ENDIF
		IF drawer_num > 2 THEN
			UI_INFIELD "gs_drawerH_3", x4, y, 65, 18
			y = y + dy
		ENDIF
		IF drawer_num > 1 THEN
			UI_INFIELD "gs_drawerH_2", x4, y, 65, 18
			y = y + dy
		ENDIF

		UI_INFIELD "gs_drawerH_1", x4, y, 65, 18
	ENDIF

return

