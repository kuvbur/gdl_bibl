
unID=1

if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

if gs_detlevel_3D_m=1 THEN gs_resol = 4
RESOL gs_resol

if gs_shadow=0 THEN SHADOW OFF


!!!! Handling overhungs in case of end panels !!!
if dob_face then rear_cab = rear_cab
if rear_panel then rear_cab = rear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02


if bDrawer and iOvenDoorStyle = OVENDOOR_NONE THEN							! --- Shelf between Cabinet and Drawer

	PEN gs_cont_pen
	MATERIAL gs_cabinet_mat
	ADD 0,0,zzyzx-count_th-drawer_h-0.01

		PRISM_	5, 0.02,
				left_cab+0.02,front_cab+fr_th1, 15,
				left_cab+0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,front_cab+fr_th1, 15,
				left_cab+0.02,front_cab+fr_th1, -1
	DEL 1

ENDIF

if DrawerOven = 1 and iOvenDoorStyle <> OVENDOOR_NONE THEN						! --- Shelf between Cabinet and Drawer

	PEN gs_cont_pen
	MATERIAL gs_cabinet_mat
	ADD 0,0,toe_h+drawer_h-0.01
		PRISM_	5, 0.02,
				left_cab+0.02,front_cab+fr_th1, 15,
				left_cab+0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,front_cab+fr_th1, 15,
				left_cab+0.02,front_cab+fr_th1, -1
	DEL 1

ENDIF

! --- CALL GENERAL BASE CABINET ---

sink=0

CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx,
		gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=dob_face, count_th=count_th,
		front_cab=front_cab, rear_cab=rear_cab, left_cab=left_cab,
		right_cab=right_cab, toe_h=toe_h, side_vis_m=side_vis_m+1,
			sink=sink, shelf_num=shelf_num, gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
		drawer_h=drawer_h,fr_th1=fr_th1,
		toe_d=toe_d,gs_toe_type_m=gs_toe_type_m,ToeHtps3D=1,
		gs_ShelfMat=gs_ShelfMat,toe_mat=toe_mat, gs_cabinet_mat=gs_cabinet_mat,
		basecab_unID=unID,
		returned_parameters unID



! --- CALL COOKTOP ---

CALL "fa_CounterCooktop" PARAMETERS ALL hCountertop = ZZYZX, side_vis_m=side_vis_m+1, CountCooktop_unID = 300

unID = 400
rgtmod = 0
bckmod = 0
lftmod =0
hotspot a+rgtmod, b+bckmod, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a+rgtmod, b+bckmod, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a+rgtmod, b+bckmod, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -lftmod, b+bckmod, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -lftmod, b+bckmod, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -lftmod, b+bckmod, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a+rgtmod, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a+rgtmod, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a+rgtmod, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -lftmod, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -lftmod, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -lftmod, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1


! --- CALL DRAWER, DOOR PANEL, OVEN DOOR ---

if iOvenDoorStyle = OVENDOOR_NONE THEN	!!! ***Oven=0 than Door

	FOR ii=1 TO (dob_face)+1										! --- double face
		if bDrawer then
			ADD left_cab,front_cab+fr_th1,ZZYZX-count_th-drawer_h	! --- DRAWER
			GOSUB 10
			ADD a/2,-0.02,drawer_h/2
			ROTY 90
			if gs_detlevel_3D_m=2 then GOSUB 100					! --- Knob
			DEL 3
		ENDIF

		ADD left_cab,front_cab+fr_th1,toe_h							! --- FRONT DOORS
		door_w=a-left_cab-right_cab
		if gs_door_type_m > 0 then GOSUB 20										! --- FRONT LEFT DOOR
		DEL 1

		door_open=0

		ADDY front_cab
		ROTZ 180
		ADD	-A-left_cab+right_cab,-B+front_cab,0
	NEXT ii
	DEL (dob_face+1)*3

ELSE		!!! *** Oven

	if DrawerOven=1 then
		ADD left_cab,front_cab+fr_th1,toe_h							! ---  DRAWER
		GOSUB 10
		ADD a/2,-0.02,drawer_h/2
		ROTY 90
		if gs_detlevel_3D_m=2 then GOSUB 100						! --- Knob
		DEL 3
	ENDIF

	ADD	0,front_cab, toe_h+drawer_h
	CALL "fa_MicOvenDoor" PARAMETERS ALL  ApplType_m=2, b=fr_th1, ApplStyle_m = iOvenDoorStyle,
				ZZYZX=ZZYZX-count_th-drawer_h-toe_h,SwitchMat=OvenSwitchMat
	DEL 1

	!if dob_face=1 THEN
	!	door_w=a-left_cab-right_cab: door_h=ZZYZX-toe_h-count_th-drawer_h
	!	ADD door_w+left_cab,b-front_cab-fr_th1,toe_h+drawer_h
	!	ROTZ 180
	!	GOSUB 20
	!	DEL 2
	!
	!	if DrawerOven=1 then
	!		ADD door_w+left_cab,b-front_cab-fr_th1,toe_h
	!		ROTZ 180
	!		GOSUB 10
	!		ADD a/2,-0.02,drawer_h/2
	!		ROTY 90
	!		if gs_detlevel_3D_m=2 then GOSUB 100					! --- Knob
	!		DEL 2
	!		DEL 2
	!	ENDIF
	!ENDIF

ENDIF

! -- CALL END PANELS ---

if rear_panel=1 and dob_face=0 then
	door_w=a-left_cab-right_cab: door_h=ZZYZX-toe_h-count_th
	ADD door_w+left_cab,b-rear_cab,toe_h
	ROTZ 180
	GOSUB 40
	DEL 2
ENDIF

	if left_panel=1 then	!*** LEFT END PANEL
	door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face) : door_h=ZZYZX-toe_h-count_th
	ADD left_cab,door_w+front_cab+fr_th1,toe_h
	ROTZ -90
	GOSUB 40
	DEL 2
ENDIF

if right_panel=1 then
	door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face): door_h=ZZYZX-toe_h-count_th
	ADD a-right_cab,front_cab+fr_th1,toe_h
	ROTZ 90
	GOSUB 40
	DEL 2
ENDIF


END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!! *** RETURN *** !!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!****************************************** DRAWER
10:
	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=a-left_cab-right_cab, door_h=drawer_h, gs_door_type_m=1,gs_shadow=gs_shadow,
			doorframe_w=DrawerMat, gs_cont_pen=gs_cont_pen,
			doorframe_mat=DrawerMat, gs_knob_type_m=0,fr_th1=fr_th1,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID

	RETURN

!!!!!****************************************** FRONT DOOR
20:


	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS ALL cabdoortyp_unID=cabdoortyp_unID,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	RETURN

!!!!!****************************************** END PANELS
40:
		!! on rear panel there is no glas and grilles and knobs!!
		gs_knob_type_m=0
		If iEndPanelType = 1 	then gs_door_type_m=1
		If iEndPanelType = 2 	then gs_door_type_m=2
		If iEndPanelType = 4 	then gs_door_type_m=11
		If iEndPanelType = 7 	then gs_door_type_m=4
		If iEndPanelType = 9 	then gs_door_type_m=6
		If iEndPanelType = 99 	then gs_door_type_m=10

		gs_full_edit=0
		door_ang=0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS ALL cabdoortyp_unID=cabdoortyp_unID,
		fr_th1=0.02,gs_cust_p=gs_cust_p_end,doorframe_w=doorframe_w_end,
		doorframe_mat=doorframe_mat_end,doorglas_mat=doorframe_mat_end,
		gs_bevel=gs_bevel,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

	RETURN

!!!!!****************************************** KNOBS/Drawer
100:

	if gs_detlevel_3D_m=2 THEN

		if gs_knob_type_m = 9 then
			reqBKnobs = 0
			success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
			if (success = 0 or not(reqBKnobs)) and gs_cust_knob<>"" then
				ADDz -(left_cab+right_cab)/2
				rotx 90
				rotz 90
				addx -gs_customHandleLength / 2
				addy -gs_customHandleHeight / 2
				call "Knob_Collection" PARAMETERS gs_ptype	= gs_cust_knob,
													a		= gs_customHandleLength,
													b		= gs_customHandleHeight,
													zzyzx	= gs_customHandleDepth
				del 5
			endif
		else
			if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
				ADDz 0
				addy -fr_th1+0.02
				!!! knob on top drawer
				CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
						gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
						gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
						gs_knob_mat=gs_knob_mat
				del 2
			else
				if gs_knob_type_m=8 then
					roty 180
					Addz knob_size/2
					addy -fr_th1+0.02
						CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
								gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
								gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
								gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 3
				else
					Addz knob_size/2
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				endif
			endif
		endif
	ENDIF

	RETURN






