

if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

if gs_detlevel_3D_m=1 then gs_resol = 4
RESOL gs_resol

if not(gs_shadow) then SHADOW OFF



unID=1
addz topHeight
hotspot a, b, 0, unID, zzyzx, 1 : unID=unID+1
hotspot a, b, 1, unID, zzyzx, 3 : unID=unID+1
hotspot a, b, -zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, b, 0, unID, zzyzx, 1 : unID=unID+1
hotspot 0, b, 1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, b, -zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a, 0, 0, unID, zzyzx, 1 : unID=unID+1
hotspot a, 0, 1, unID, zzyzx, 3 : unID=unID+1
hotspot a, 0, -zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, 0, 0, unID, zzyzx, 1 : unID=unID+1
hotspot 0, 0, 1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, 0, -zzyzx, unID, zzyzx, 2 : unID=unID+1
del 1

addz topHeight-zzyzx

!!!!!****************************************** CALL GENERAL BASE CABINET
CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx,
		gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=0, count_th=count_th,
		front_cab=front_cab, rear_cab=rear_cab, left_cab=left_cab,
		right_cab=right_cab, toe_h=0, side_vis_m=side_vis_m+1,fr_th1=fr_th1,
		sink=0, shelf_num=0, gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
		toe_mat=0, gs_cabinet_mat=gs_cabinet_mat, drawer_h=drawer_h,
		basecab_unID=unID, ToeHtps3D=0,
		returned_parameters unID


!!!!!****************************************** CALL COUNTERTOP and SINK

CALL "fa_counter_sink" PARAMETERS a=a, b=b, zzyzx=zzyzx,
		gs_detlevel_3D_m=gs_detlevel_3D_m, gs_full_edit=0, dob_face=0, count_th=count_th,
		front_cab=front_cab, rear_cab=rear_cab, left_cab=left_cab,
		right_cab=right_cab, bs_h=bs_h, side_vis_m=side_vis_m+1,gs_sink_type_m=0,
		count_mat=count_mat,bs_mat=bs_mat,gs_count_type_m=gs_count_type_m,
		gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
		zzyzx_edit = 0,
		countsink_unID=unID,
		returned_parameters unID


!!!!!****************************************** CALL DRAWER and DOOR PANEL

if bDrawer AND gs_door_type_m > EPS then
	ADD left_cab,front_cab+fr_th1,ZZYZX-count_th-drawer_h		!*** DRAWER
	GOSUB 10
	ADD a/2,-0.02,drawer_h/2
	ROTY 90
	if gs_detlevel_3D_m=2 then GOSUB 100	!*** Knob
	DEL 3
endif

rrear_cab = 0
HOTSPOT left_cab,front_cab,0, unID: unID=unID+1
HOTSPOT a-right_cab,front_cab,0, unID: unID=unID+1
HOTSPOT left_cab,b-rrear_cab,0, unID: unID=unID+1
HOTSPOT a-right_cab,b-rrear_cab,0, unID: unID=unID+1
del 1
END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!! *** RETURN *** !!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!****************************************** DRAWER
10:

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=a-left_cab-right_cab, door_h=drawer_h, gs_door_type_m=gs_door_type_m,gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,fr_th1=fr_th1,
			doorframe_mat=doorframe_mat, gs_knob_type_m=0, gs_cust_p=gs_cust_p,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel, vert_gnum = vert_gnum, hor_gnum = hor_gnum,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID

RETURN

! --- KNOBS/Drawer ---
100:


	if gs_knob_type_m = 9 then
		reqBKnobs = 0
		success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
		if (success = 0 or not(reqBKnobs)) and gs_cust_knob<>"" then
			ADDz -(left_cab+right_cab)/2
			rotx 90
			rotz 90
			addx -gs_customHandleLength / 2
			addy -gs_customHandleHeight / 2
			call "Knob_Collection" PARAMETERS gs_ptype	= gs_cust_knob,
												a		= gs_customHandleLength,
												b		= gs_customHandleHeight,
												zzyzx	= gs_customHandleDepth
			del 5
		endif
	else
		if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
			ADDz -(left_cab+right_cab)/2
			addy -fr_th1+0.02
			!!! knob on top drawer
			CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
					gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
					gs_knob_mat=gs_knob_mat
			del 2
		else
			if gs_knob_type_m=8 then
				roty 180
				ADDz knob_size/2+(left_cab+right_cab)/2
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
				del 3
			else
					ADDz knob_size/2-(left_cab+right_cab)/2
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
						del 2
			endif
		endif
	endif
RETURN
