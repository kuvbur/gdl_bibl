


! ================================================================
! Detail Level
! ================================================================

if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

if gs_detlevel_3D_m=1 then RESOL 4
if gs_detlevel_3D_m=2 then RESOL gs_resol

! ================================================================
! 3D Hotspots
! ================================================================
addz placementHeight

unID=1

hotspot 0,0,0, unID : unID=unID+1
hotspot a,0,0, unID : unID=unID+1
hotspot 0,b,0, unID : unID=unID+1
hotspot a,b,0, unID : unID=unID+1

hotspot a, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, b, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, b, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot 0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot 0, 0, -1, unID, zzyzx, 3 : unID=unID+1
hotspot 0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

if gs_door_bw = 0 then		! asymmetric door widths are enabled
	if gs_full_edit=1 then
		! Door Panel Streching
		if gs_door_type_m > 0 then
			hotspot left_cab+0,		front_cab,toe_h+door_h,unID,door_w,1+128 : unID=unID+1
			hotspot left_cab-1,		front_cab,toe_h+door_h,unID,door_w,3 : unID=unID+1
			hotspot left_cab+door_w,	front_cab,toe_h+door_h,unID,door_w,2 : unID=unID+1
		endif
	endif
endif
del 1

! ================================================================
!Base Babinet
! ================================================================

addz placementHeight
pen gs_cont_pen
material gs_cabinet_mat
if not(gs_shadow) then SHADOW OFF

! ----- Simple Geometry -------------------------------------------
! -----------------------------------------------------------------

if gs_detlevel_3D_m=1 then
	addy fr_th1
	mulID = 0
	if right_panel and left_panel then
		addx 0.02 : mulID = mulID + 1
	endif
	if not(right_panel) and left_panel then
		addx 0.02 : mulID = mulID + 1
	endif

	PRISM_ 5,zzyzx,
			0,	0,			15,
			a - (left_panel* 0.02) - (right_panel * 0.02),	0,			15,
			a - (left_panel* 0.02) - (right_panel * 0.02),	b - fr_th1 - (dob_face * fr_th1),	15,
			0,	b - fr_th1 - (dob_face * fr_th1),	15,
			0,	0,			-1
	del 1
	if mulID > 0 then
		del mulID
	endif
else
! ----- Detailed Geometry -----------------------------------------
! -----------------------------------------------------------------
	CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx,
			gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=dob_face, count_th=0,
			front_cab=0, rear_cab=rear_cab, left_cab=left_cab,fr_th1=fr_th1,
			right_cab=right_cab, toe_h=0, side_vis_m=0,
			sink=0, shelf_num=shelf_num, gs_ShelfMat=gs_ShelfMat,
			gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
			toe_mat=0, gs_cabinet_mat=gs_cabinet_mat, drawer=0,
			ToeHtps3D=0,
			basecab_unID=unID,
			returned_parameters unID
endif

! ================================================================
! Door Panels
! ================================================================

for ii=1 to (dob_face)+1							!*** double face
	ADD left_cab,front_cab+fr_th1,toe_h				
	if gs_door_type_m > 0 and (abs(door_w)>EPS)then GOSUB "FrontDoorLeft"
	! in case door_w = 0 ,the left door won't be called. Result: single-door cabinet	
	ADDx a-right_cab-left_cab
	mulx -1
	if gs_door_type_m > 0 and (abs(door_w - (a-right_cab-left_cab))>EPS) then GOSUB "FrontDoorRight"
	! in case  door_w = A (the left door is as wide as the object itself) ,the right door won't be called. Result: single-door cabinet						
	del 1
	del 2

	ADDY front_cab
	ROTZ 180
	ADD	-A+right_cab-left_cab,-B+rear_cab,0
next ii
del (dob_face+1)*3

! ================================================================
! End Panels
! ================================================================
if rear_panel=1 then
	door_w=a-door_w-left_cab-right_cab
	door_h=ZZYZX-toe_h-count_th
	ADD door_w+left_cab,b-rear_cab,toe_h
	ROTZ 180
	GOSUB "EndPanels"
	door_w=a-door_w-left_cab-right_cab
	ADDX -door_w
	GOSUB "EndPanels"
	del 3
endif

if left_panel=1 then
	door_w=b-fr_th1-rear_cab-(fr_th1*dob_face): door_h=ZZYZX-toe_h-count_th		!*** LEFT END PANEL
	ADD left_cab,door_w+fr_th1,toe_h
	ROTZ -90
	GOSUB "EndPanels"
	del 2
endif

if right_panel=1 then
	door_w=b-fr_th1-rear_cab-(fr_th1*dob_face): door_h=ZZYZX-toe_h-count_th
	ADD a-right_cab,fr_th1,toe_h
	ROTZ 90
	GOSUB "EndPanels"
	del 2
endif

del 1

! ================================================================
END	! END	! END	! END	! END	! END	! END	! END	! END	!
! ================================================================


! -----------------------------------------------------------------
"FrontDoorLeft":
! -----------------------------------------------------------------

	if gs_door_bw=0 then
		door_w=door_w
	else
		door_w=(a-left_cab-right_cab)/2
	endif

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
		gs_full_edit=gs_full_edit,door_ang=door_ang,door_w=door_w,door_h=door_h,
		gs_door_type_m=gs_door_type_m,gs_cust_p=gs_cust_p,doorframe_w=doorframe_w,
		vert_gnum=vert_gnum,hor_gnum=hor_gnum,gs_cont_pen=gs_cont_pen,
		gs_shadow=gs_shadow,doorglas_mat=doorglas_mat,doorframe_mat=doorframe_mat,
		fr_th1=fr_th1,gs_bevel=gs_bevel,
		gs_knob_type_m=gs_knob_type_m,gs_knob_p_m=gs_knob_p_m,knob_size=knob_size,
		knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
		gs_knob_mat=gs_knob_mat,
		cabdoortyp_unID=cabdoortyp_unID,
		gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
		gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	return

! -----------------------------------------------------------------
"FrontDoorRight":
! -----------------------------------------------------------------

	if gs_door_bw=1 then
		door_w=(a-left_cab-right_cab)/2
	else
		door_w=a-door_w-left_cab-right_cab
	endif


	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
		gs_full_edit=gs_full_edit,door_ang=door_ang,door_w=door_w,door_h=door_h,
		gs_door_type_m=gs_door_type_m,gs_cust_p=gs_cust_p,doorframe_w=doorframe_w,
		vert_gnum=vert_gnum,hor_gnum=hor_gnum,gs_cont_pen=gs_cont_pen,
		gs_shadow=gs_shadow,doorglas_mat=doorglas_mat,doorframe_mat=doorframe_mat,
		fr_th1=fr_th1,gs_bevel=gs_bevel,
		gs_knob_type_m=gs_knob_type_m,gs_knob_p_m=gs_knob_p_m,knob_size=knob_size,
		knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
		gs_knob_mat=gs_knob_mat,
		cabdoortyp_unID=cabdoortyp_unID,
		gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
		gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	return

! -----------------------------------------------------------------
"EndPanels":
! -----------------------------------------------------------------

	! on rear panel there is no glas and grilles and knobs!!
	door_ang=0
	gs_knob_type_m=0
	If iEndPanelType = 1 	then gs_door_type_m=1
	If iEndPanelType = 2 	then gs_door_type_m=2
	If iEndPanelType = 4 	then gs_door_type_m=11
	If iEndPanelType = 7 	then gs_door_type_m=4
	If iEndPanelType = 9 	then gs_door_type_m=6
	If iEndPanelType = 99 	then gs_door_type_m=10
	gs_full_edit=0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS ALL cabdoortyp_unID=cabdoortyp_unID,
		fr_th1=0.02,gs_cust_p=gs_cust_p_end,doorframe_w=doorframe_w_end,
		doorframe_mat=doorframe_mat_end,doorglas_mat=doorframe_mat_end,
		gs_bevel=gs_bevel,
		returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	return
