

toe_h = 0
rrear_cab =	0
lrear_cab = 0
left_cab = 0
right_cab = 0
count_th = 0

if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

if gs_detlevel_3D_m=1 then RESOL 4
if gs_detlevel_3D_m=2 then RESOL gs_resol

if gs_shadow=0 then SHADOW OFF

! --- END PANELS OPTION ---
if dob_face then lrear_cab = lrear_cab
if rrear_panel then rrear_cab = rrear_cab + 0.02
if lrear_panel then lrear_cab = lrear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02

PEN gs_cont_pen


unID=1

ADD a/2, b/2, placementHeight

! 3D hotspots
hotspot -a/2, -b/2, 0, unID : unID=unID+1
hotspot -a/2+depth_cabl, -b/2, 0, unID : unID=unID+1
hotspot -a/2+depth_cabl,  b/2-depth_cabr+fr_th1, 0, unID : unID=unID+1
hotspot  a/2, b/2-depth_cabr, 0, unID : unID=unID+1
hotspot  a/2, b/2, 0, unID : unID=unID+1
hotspot -a/2, b/2, 0, unID : unID=unID+1

hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2+depth_cabl, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2+depth_cabl, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2+depth_cabl, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2+depth_cabl,  b/2-depth_cabr+fr_th1, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2+depth_cabl,  b/2-depth_cabr+fr_th1, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2+depth_cabl,  b/2-depth_cabr+fr_th1, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2-depth_cabr, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2-depth_cabr, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2-depth_cabr, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1
! End of 3D hotspots

ADD -a/2, -b/2, 0

! CABINET

material gs_cabinet_mat

if gs_detlevel_3D_m=2 then

	PRISM_ 11, 0.02,
			lrear_cab, left_cab, 11,
			depth_cabl, left_cab, 11,
			depth_cabl, b-depth_cabr+fr_th1, 11,
			depth_cabl+0.05, b-depth_cabr+fr_th1, 13,
			a-0.02-right_cab, b-depth_cabr+fr_th1, 9,
			a-right_cab, b-depth_cabr+fr_th1, 11,
			a-right_cab, b-rrear_cab-(fr_th1*dob_face), 11,
			a-right_cab-0.02, b-rrear_cab-(fr_th1*dob_face), 9+4*dob_face,
			lrear_cab+0.02, b-rrear_cab-(fr_th1*dob_face), 9,
			lrear_cab, b-rrear_cab-(fr_th1*dob_face), 11,
			lrear_cab, left_cab, -1
	ADDZ 0.02
	if dob_face=0 then
		PRISM_ 15, zzyzx-0.04,
				lrear_cab, left_cab, 10,
				depth_cabl, left_cab, 10,
				depth_cabl, b-depth_cabr+fr_th1, 10,
				depth_cabl+0.05, b-depth_cabr+fr_th1, 15,
				depth_cabl+0.05, b-depth_cabr+fr_th1+0.02, 15,
				depth_cabl-0.02, b-depth_cabr+fr_th1+0.02, 15,
				depth_cabl-0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, b-0.02-rrear_cab, 15,
				a-0.02-right_cab, b-0.02-rrear_cab, 15,
				a-0.02-right_cab, b-depth_cabr+fr_th1, 10,
				a-right_cab, b-depth_cabr+fr_th1, 10,
				a-right_cab, b-rrear_cab, 10,
				lrear_cab, b-rrear_cab, 10,
				lrear_cab, left_cab, -1
	endif

	if dob_face=1 then
		PRISM_ 11, zzyzx-0.04,
				lrear_cab, left_cab, 10,
				depth_cabl, left_cab, 10,
				depth_cabl, b-depth_cabr+fr_th1, 10,
				depth_cabl+0.05, b-depth_cabr+fr_th1, 15,
				depth_cabl+0.05, b-depth_cabr+fr_th1+0.02, 15,
				depth_cabl-0.02, b-depth_cabr+fr_th1+0.02, 15,
				depth_cabl-0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, 0.02+left_cab, 15,
				lrear_cab+0.02, b-rrear_cab-(fr_th1*dob_face), 10,
				lrear_cab, b-rrear_cab-(fr_th1*dob_face), 10,
				lrear_cab, left_cab, -1

		PRISM_ 5, zzyzx-0.04,
				a-0.02-right_cab, b-rrear_cab-(fr_th1*dob_face), 15,
				a-0.02-right_cab, b-depth_cabr+fr_th1, 10,
				a-right_cab, b-depth_cabr+fr_th1, 10,
				a-right_cab, b-rrear_cab-(fr_th1*dob_face), 10,
				a-0.02-right_cab, b-rrear_cab-(fr_th1*dob_face), -1
	endif

	ADDZ zzyzx-0.04
	PRISM_ 11, 0.02,
			lrear_cab, left_cab, 14,
			depth_cabl, left_cab, 14,
			depth_cabl, b-depth_cabr+fr_th1, 14,
			depth_cabl+0.05, b-depth_cabr+fr_th1, 13,
			a-0.02-right_cab, b-depth_cabr+fr_th1, 12,
			a-right_cab, b-depth_cabr+fr_th1, 14,
			a-right_cab, b-rrear_cab-(fr_th1*dob_face), 14,
			a-right_cab-0.02, b-rrear_cab-(fr_th1*dob_face), 14-dob_face,
			lrear_cab+0.02, b-rrear_cab-(fr_th1*dob_face), 12,
			lrear_cab, b-rrear_cab-(fr_th1*dob_face), 14,
			lrear_cab, left_cab, -1
	del 2
else
	EXTRUDE 7, 0, 0, zzyzx, 55,        !!! Simple cab
		lrear_cab, left_cab, 0,
		depth_cabl, left_cab, 0,
		depth_cabl, b-depth_cabr+fr_th1, 0,
		a-right_cab, b-depth_cabr+fr_th1, 0,
		a-right_cab, b-rrear_cab-(fr_th1*dob_face), 0,
		lrear_cab, b-rrear_cab-(fr_th1*dob_face), 0,
		lrear_cab, left_cab, -1
endif

gosub 1000

if gs_detlevel_3D_m=2 then

! SHELVES
 material gs_cabinet_mat

	if shelf_num>0 AND A>0.04 then
		for ii=1 to shelf_num
			ADDZ ((zzyzx-0.02)/(shelf_num+1))*(ii)
			MATERIAl  gs_ShelfMat
			PRISM_ 7,0.02,
			   lrear_cab+0.02, left_cab+0.02, 15,
			   depth_cabl-0.02, left_cab+0.02, 15,
			   depth_cabl-0.02, b-depth_cabr+fr_th1+0.02, 15,
			   a-0.02-right_cab, b-depth_cabr+fr_th1+0.02, 15,
			   a-0.02-right_cab, b-0.02*(1-dob_face)-rrear_cab-(fr_th1*dob_face), 15,
			   lrear_cab+0.02, b-0.02*(1-dob_face)-rrear_cab-(fr_th1*dob_face), 15,
			   lrear_cab+0.02, left_cab+0.02, -1
			del 1
		next ii
	endif
endif

ADDZ toe_h
! END PANELS

accessno=2
! on rear panel there is no glas and grilles and knobs
If iEndPanelType = 1 	then gs_door_type_mr=1
If iEndPanelType = 2 	then gs_door_type_mr=2
If iEndPanelType = 4 	then gs_door_type_mr=11
If iEndPanelType = 7 	then gs_door_type_mr=4
If iEndPanelType = 9 	then gs_door_type_mr=6
If iEndPanelType = 99 then gs_door_type_mr=10

if lrear_panel=1 then
	aa=(b-left_cab-rrear_cab-(fr_th1*dob_face))
	ADD lrear_cab, b-rrear_cab-(fr_th1*dob_face), 0
	ROTZ -90

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m	= gs_detlevel_3D_m,
			gs_full_edit	= 0,
			door_w 			= aa,
			door_h			= zzyzx,
			gs_door_type_m	= gs_door_type_mr,
			gs_shadow		= gs_shadow,
			doorframe_w		= doorframe_w_end,
			gs_cont_pen		= gs_cont_pen,
			doorframe_mat	= doorframe_mat_end,
			gs_knob_mat		= gs_knob_mat,
			doorglas_mat	= doorframe_mat_end,
			gs_knob_type_m	= 0,
			gs_cust_p		= gs_cust_p_end,
			gs_bevel		= gs_bevel,
			cabdoortyp_unID	= cabdoortyp_unID,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	del 2
endif

if rrear_panel=1 and dob_face=0 then
	aa=(a-right_cab-lrear_cab)/2
	ADD a-right_cab, b-rrear_cab, 0
	ROTZ 180

	for HI=1 to 2
		cabdoortyp_unID=unID
		CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m	= gs_detlevel_3D_m,
				gs_full_edit		= 0,
				door_w				= aa,
				door_h				= zzyzx,
				gs_door_type_m		= gs_door_type_mr,
				gs_shadow			= gs_shadow,
				doorframe_w			= doorframe_w_end,
				gs_cont_pen			= gs_cont_pen,
				doorframe_mat		= doorframe_mat_end,
				gs_knob_mat			= gs_knob_mat,
				doorglas_mat		= doorframe_mat_end,
				gs_knob_type_m		= 0,
				gs_cust_p			= gs_cust_p_end,
				gs_bevel			= gs_bevel,
				cabdoortyp_unID		= cabdoortyp_unID,
				returned_parameters cabdoortyp_unID

				unID = cabdoortyp_unID
		ADDX aa
	next HI
	del 2
	del 2
endif

if left_panel=1 then
	ADD lrear_cab, left_cab, 0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m	= gs_detlevel_3D_m,
            gs_full_edit	= 0,
            door_w			= depth_cabl-front_cab-lrear_cab,
            door_h			= zzyzx,
            gs_door_type_m	= gs_door_type_mr,
            gs_shadow		= gs_shadow,
            doorframe_w		= doorframe_w_end,
            gs_cont_pen		= gs_cont_pen,
            doorframe_mat	= doorframe_mat_end,
            gs_knob_mat		= gs_knob_mat,
            doorglas_mat	= doorframe_mat_end,
            gs_knob_type_m	= 0,
            gs_cust_p		= gs_cust_p_end,
            gs_bevel		= gs_bevel,
            cabdoortyp_unID	= cabdoortyp_unID,
            returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	del 1
endif

if right_panel=1 then
	ADD a-right_cab, b-depth_cabr+fr_th1, 0
	ROTZ 90

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m	= gs_detlevel_3D_m,
			gs_full_edit	= 0,
			door_w			= depth_cabr-fr_th1-rrear_cab-(fr_th1*dob_face),
			door_h			= zzyzx,
			gs_door_type_m	= gs_door_type_mr,
			gs_shadow		= gs_shadow,
			doorframe_w		= doorframe_w_end,
			gs_cont_pen		= gs_cont_pen,
			doorframe_mat	= doorframe_mat_end,
			gs_knob_mat		= gs_knob_mat,
			doorglas_mat	= doorframe_mat_end,
			gs_knob_type_m	= 0,
			gs_cust_p		= gs_cust_p_end,
			gs_bevel		= gs_bevel,
			cabdoortyp_unID	= cabdoortyp_unID,
			returned_parameters cabdoortyp_unID
	unID = cabdoortyp_unID
	del 2
endif


! --- DOOR ---------------------------------------------------------------------
!accessno = 1
cc = zzyzx - count_th - toe_h

if gs_door_type_m <> 0 then
	aa = a - depth_cabl - right_cab - 0.05 + doorOffset/2
	add depth_cabl + 0.05 - doorOffset/2, b - depth_cabr + fr_th1, 0

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m,
            gs_full_edit			= gs_full_edit,
            door_ang				= door_ang,
            door_w					= aa,
            door_h					= cc,
            gs_door_type_m			= gs_door_type_m,
            gs_shadow				= gs_shadow,
            doorframe_w				= doorframe_w,
            gs_cont_pen				= gs_cont_pen,
            vert_gnum				= vert_gnum,
            hor_gnum				= hor_gnum,
            fr_th1					= fr_th1,
            doorframe_mat			= doorframe_mat,
            doorglas_mat			= doorglas_mat,
            gs_resol				= gs_resol,
            gs_cust_p				= gs_cust_p,
            gs_knob_type_m			= gs_knob_type_m,
            gs_knob_p_m				= gs_knob_p_m,
            knob_size				= knob_size,
            knob_pl_x				= knob_pl_x,
            knob_pl_y				= knob_pl_y,
            gs_knob_mat				= gs_knob_mat,
            gs_bevel				= gs_bevel,
            cabdoortyp_unID			= cabdoortyp_unID,
            gs_cust_knob 			= gs_cust_knob,
            gs_customHandleLength 	= gs_customHandleLength,
            gs_customHandleHeight	= gs_customHandleHeight,
            gs_customHandleDepth 	= gs_customHandleDepth,
            returned_parameters cabdoortyp_unID
	unID = cabdoortyp_unID
	del 1
	if dob_face=1 then
		aa=(a-right_cab-lrear_cab)/2
		ADD lrear_cab+2*aa, b-rrear_cab-(fr_th1*dob_face), toe_h
		ROTZ 180
		for HI=1 to 2
			cabdoortyp_unID=unID
			CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m			= gs_detlevel_3D_m,
                    gs_full_edit				= gs_full_edit,
                    door_ang					= door_ang,
                    door_w						= aa,
                    door_h						= cc,
                    gs_door_type_m				= gs_door_type_m,
                    gs_shadow					= gs_shadow,
                    doorframe_w					= doorframe_w,
                    gs_cont_pen					= gs_cont_pen,
                    vert_gnum					= vert_gnum,
                    hor_gnum					= hor_gnum,
                    doorframe_mat				= doorframe_mat,
                    gs_knob_mat					= gs_knob_mat,
                    doorglas_mat				= doorglas_mat,
                    fr_th1						= fr_th1,
                    gs_knob_type_m				= gs_knob_type_m,
                    gs_knob_p_m					= gs_knob_p_m,
                    gs_resol					= gs_resol,
                    gs_cust_p					= gs_cust_p,
                    knob_size					= knob_size,
                    knob_pl_x					= knob_pl_x,
                    knob_pl_y					= knob_pl_y,
                    cabdoortyp_unID				= cabdoortyp_unID,
                    gs_bevel					= gs_bevel,
                    gs_cust_knob				= gs_cust_knob,
                    gs_customHandleLength		= gs_customHandleLength,
                    gs_customHandleHeight		= gs_customHandleHeight,
                    gs_customHandleDepth		= gs_customHandleDepth,
				returned_parameters cabdoortyp_unID

				unID = cabdoortyp_unID
			MULX -1
			ADDX -2*aa
		next HI
		del 4
		del 2
	endif
endif
! --- End of door --------------------------------------------------------------

del 1

END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4


base

body -1

return

