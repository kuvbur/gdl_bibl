
unID=1

PEN gs_cont_pen
LINE_TYPE gs_line_type
FILL gs_fill_type

if iEndCabinetStyle = CABEND_ANGULAR then
	ADD2 a/2,b/2

	! angular end
	POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
			-a/2, -b/2, 1,
			-a/2+amx, -b/2+amy, 1,
			a/2, b/2, 1,
			-a/2, b/2, 1,
			-a/2, -b/2, -1

	hotline2 -a/2, -b/2,-a/2+amx, -b/2+amy
	hotline2 -a/2+amx, -b/2+amy,a/2, b/2
	hotline2 a/2, b/2,-a/2, b/2
	hotline2 -a/2, b/2,-a/2, -b/2

	hotspot2 -a/2, b/2, unID : unID=unID+1
	hotspot2 -a/2, -b/2, unID : unID=unID+1
	hotspot2 -a/2+amx, -b/2+amy, unID : unID=unID+1
	hotspot2  a/2, b/2, unID : unID=unID+1

	poly2_ 5, 1,
			-a/2, -b/2, 1,
			-a/2+amx, -b/2+amy, 1,
			a/2, b/2, 0,
			-a/2, b/2, 0,
			-a/2, -b/2, -1
	side_vis2D_m = side_vis2D_m+1
	if side_vis2D_m=2 OR side_vis2D_m=4 then LINE2 -a/2, -b/2, -a/2, b/2
	if side_vis2D_m=3 OR side_vis2D_m=4 then LINE2 -a/2,  b/2,  a/2, b/2

	if iCabSymbolType2D <> 1 then
		x1 = -a/2+left_cab+amx
		y1 = -b/2+amy
		dx = 0.025/COS(22.5)
		x2 = x1-dx*SIN(45)
		y2 = y1+dx*SIN(45)
		if iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 then
				LINE2 -a/2+left_cab, -b/2+dx, x2, y2
				LINE2 x1, y1, x2, y2
				LINE2 x2, y2, a/2-dx, b/2-right_cab
		endif

		if  iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 then
			doorlen1 = SQR((x1-(-a/2+left_cab))^2+(y1-(-b/2))^2)
			add2 -a/2+left_cab, -b/2
			rot2 fi-15
			arc2 0,0,doorlen1,0,15
			LINE2 0,0,doorlen1,0
			del 2

			doorlen2 = SQR((x1-(a/2))^2+(y1-(b/2-right_cab))^2)
			add2 a/2, b/2-right_cab
			rot2 -120+fi
			arc2 0,0,doorlen1,-15,0
			LINE2 0,0,doorlen2,0
			del 2
		endif
	endif
	del 1
else
	! radius end
	POLY2_B 5, 2+64, gs_fill_pen, gs_back_pen,
			0, B, 0,
			A, B, 1,
			0, B, 900,
			0, 0, 3000,
			0, B, -1
	hotline2 0, 0, 0, B
	hotline2 0, B,  A, B
	hotspot2 0, B, unID : unID=unID+1
	hotspot2 0, 0, unID : unID=unID+1
	hotspot2 A, B, unID : unID=unID+1

	poly2_ 5, 1,
			0, B, 0,
			A, B, 1,
			0, B, 900,
			0, 0, 3000,
			0, B, -1
	if side_vis2D_m=2 OR side_vis2D_m=4 then LINE2 0, 0, 0, B
	if side_vis2D_m=3 OR side_vis2D_m=4 then LINE2 0, B,  A, B

	if  iCabSymbolType2D <> 1 then
		if iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 then
			add2 0, B
			rot2 -90
			arc2 0, 0, A-0.025, 0, 90
			del 2
		endif

		if  iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 then
			len = SQR((A-(A/SIN(45))*SIN(30))^2+(-(A/SIN(45))*COS(30)+A)^2)
			alpha = 180-2*ATN((len/2)/(len/10))
			rr = (len/2)/SIN(alpha)
			add2 A, B
			rot2 -90+15
			arc2 0, -B, A, 0, 90
			add2 A, -A
			rot2 alpha-142.5
			arc2 0, -rr, rr, alpha*2, 90
			del 4
		endif
	endif
endif

END
