

toe_h = 0
count_th = 0

if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

if gs_detlevel_3D_m=1 then RESOL 4
if gs_detlevel_3D_m=2 then RESOL gs_resol

if gs_shadow=0 then SHADOW OFF

PEN gs_cont_pen

unID=1
addz placementHeight
ADD a/2,b/2,0

! 3D hotspots
HOTSPOT -a/2, -b/2, 0, unID : unID=unID+1
HOTSPOT -a/2+amx, -b/2+amy, 0, unID : unID=unID+1
HOTSPOT  a/2,  b/2, 0, unID : unID=unID+1
HOTSPOT -a/2,  b/2, 0, unID : unID=unID+1

hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2+amx, -b/2+amy, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2+amx, -b/2+amy, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2+amx, -b/2+amy, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1
! End of 3D hotspots


! CABINET
del 1

MATERIAL gs_cabinet_mat
ADD left_cab, fr_th1, 0
ROTZ fi
	if iEndCabinetStyle = CABEND_ANGULAR then CUTPLANE 1, 0, 1, 1
del 2

ADD a-fr_th1, b, 0
ROTZ fi+45
	if iEndCabinetStyle = CABEND_ANGULAR then CUTPLANE 1, 0, 1, 1
del 2

if iEndCabinetStyle = CABEND_ANGULAR then

	! angular end

	if gs_detlevel_3D_m=1 then
		PRISM_ 5,zzyzx,
				0,0,15,
				A,0,15,
				A,B,15,
				0,B,15,
				0,0,-1
	else			! Cabinet
		PRISM_ 5,0.02,
				0,0,11,
				A,0,11,
				A,B,11,
				0,B,11,
				0,0,-1
		ADDZ ZZYZX-0.02
		PRISM_ 5,0.02,
				0,0,14,
				A,0,14,
				A,B,14,
				0,B,14,
				0,0,-1
		del 1
		ADDz 0.02
		PRISM_ 7,ZZYZX-0.02-0.02,
				0,0,15,
				0.02,0,15,
				0.02,B-0.02,15,
				A,B-0.02,15,
				A,B,10,
				0,B,10,
				0,0,-1
		del 1

		gosub 1000

		!  Shelves
		if shelf_num>0 then

			dz=(zzyzx-2*0.02-shelf_num*0.02)/(shelf_num+1)

			for i=1 to shelf_num
				addz dz+0.02
				MATERIAL gs_ShelfMat
				PRISM_ 5,0.02,
						0.02,0,15,
						A,0,15,
						A,B-0.02,15,
						0.02,B-0.02,15,
						0.02,0,-1
			next i

			del shelf_num
		endif
	endif
else
	! radius end
	RESOL gs_res_2

	if gs_detlevel_3D_m=1 then
		prism_ 5, zzyzx,
				0, B, 15,
				A, B, 79,
				0, B, 900,
				0, 0, 3015,
				0, B, -1
	else
		alph=2*0.02*90/a/pi
		dx=a*cos(alph)
		dy=a*sin(alph)

		ADDy fr_th1
		prism_ 10, 0.02,
				0, B-fr_th1, 11,
				A-0.0001-fr_th1, B-fr_th1, 15,
				A-fr_th1, B-fr_th1, 64+11,
				0, B-fr_th1, 900,
				dx, B-dy-fr_th1, 3079,
				0, B-fr_th1, 900,
				dy, b-dx-fr_th1, 3009,
				0, B-fr_th1, 900,
				0, 0, 3011,
				0, B-fr_th1, -1
		addz 0.02
		prism_ 12, zzyzx-0.02*2,
				0, B-fr_th1, 10,
				A-0.0001-fr_th1, B-fr_th1, 15,
				A-fr_th1, B-fr_th1, 64+10,
				0, B-fr_th1, 900,
				dx-fr_th1, B-dy-fr_th1, 3079,
				dx-0.0001-fr_th1, B-dy-fr_th1, 15,
				0.02, B-0.02-fr_th1, 15,
				dy, b-dx+0.0001, 15,
				dy, b-dx, 64+10,
				0, B-fr_th1, 900,
				0, 0, 3010,
				0, B-fr_th1, -1
		del 1
		addz zzyzx-0.02
		prism_ 10, 0.02,
				0, B-fr_th1, 14,
				A-0.0001-fr_th1, B-fr_th1, 15,
				A-fr_th1, B-fr_th1, 64+14,
				0, B-fr_th1, 900,
				dx-fr_th1, B-dy-fr_th1, 3079,
				0, B-fr_th1, 900,
				dy, b-dx-fr_th1, 3012,
				0, B-fr_th1, 900,
				0, 0, 3014,
				0, B-fr_th1, -1
		del 1

		gosub 1000

		if shelf_num>0 then

			dz=(zzyzx-2*0.02-shelf_num*0.02)/(shelf_num+1)

			for i=1 to shelf_num
				addz dz
				MATERIAL gs_ShelfMat
				prism_ 5, 0.02,
						0.02, B-0.02-fr_th1, 15,
						dx-fr_th1, B-dy-fr_th1, 79,
						0, B-fr_th1, 900,
						dy, B-dx-fr_th1, 3015,
						0.02, B-0.02-fr_th1, -1
			next i
			del shelf_num
		endif
	del 1
	endif

endif

RESOL gs_resol

if iEndCabinetStyle = CABEND_ANGULAR then CUTEND
if iEndCabinetStyle = CABEND_ANGULAR then CUTEND
! End of cabinet

if iEndCabinetStyle = CABEND_RADIUSED then goto 999

! Door
if gs_door_type_m > 0 then
	ADD 0, fr_th1, toe_h
	ROTZ fi

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang,
			door_w=ambcl, door_h=zzyzx-toe_h-count_th, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,
			doorframe_mat=doorframe_mat,doorglas_mat=doorglas_mat,
			gs_resol=gs_resol, gs_cust_p=gs_cust_p,fr_th1=fr_th1,
			gs_knob_type_m=gs_knob_type_m,gs_knob_p_m=gs_knob_p_m,knob_size=knob_size,
			knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
			gs_knob_mat=gs_knob_mat,gs_bevel=gs_bevel,
			cabdoortyp_unID=cabdoortyp_unID,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
			returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID
	del 2

	ADD a-fr_th1, b, toe_h
	ROTZ 45+fi
	MULX -1
	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang,
			door_w=ambcl, door_h=zzyzx-toe_h-count_th, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,
			doorframe_mat=doorframe_mat,doorglas_mat=doorglas_mat,
			gs_resol=gs_resol, gs_cust_p=gs_cust_p,fr_th1=fr_th1,
			gs_knob_type_m=gs_knob_type_m,gs_knob_p_m=gs_knob_p_m,knob_size=knob_size,
			knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
			gs_knob_mat=gs_knob_mat,gs_bevel=gs_bevel,
			cabdoortyp_unID=cabdoortyp_unID,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
			returned_parameters cabdoortyp_unID

			unID = cabdoortyp_unID
	del 3
endif
!del 1

END

999:

! doors for radius end

!ADD -a/2+left_cab, -b/2+front_cab, toe_h
ADDy fr_th1
rotx 90
if gs_door_type_m > 0 then
	cabdoorrad_unID = unID
	CALL "fa_cabdoortyp_rad" PARAMETERS a=a, gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang, gs_res_2=gs_res_2,
			door_w=(2*(a-fr_th1)*pi)/4, door_h=zzyzx-toe_h-count_th, gs_door_type_m=gs_door_type_m,
			gs_shadow=gs_shadow, doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,fr_th1=fr_th1,
			doorframe_mat=doorframe_mat, gs_knob_mat=gs_knob_mat,
			doorglas_mat=doorglas_mat, gs_knob_type_m=gs_knob_type_m, gs_resol=gs_resol,
			gs_cust_p=gs_cust_p,knob_size=knob_size,
			knob_pl_y=knob_pl_y,gs_bevel=gs_bevel,
			cabdoorrad_unID=cabdoorrad_unID,
		returned_parameters cabdoorrad_unID

	unID = cabdoorrad_unID
Endif

del 3

END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4


base

body -1

return
