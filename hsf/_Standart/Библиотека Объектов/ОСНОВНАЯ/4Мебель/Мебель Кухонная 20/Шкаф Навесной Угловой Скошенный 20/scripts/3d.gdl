

if gs_detlevel_3D_m=0 then END
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

if gs_detlevel_3D_m=1 then RESOL 4
if gs_detlevel_3D_m=2 then RESOL gs_resol

if gs_shadow=0 then SHADOW OFF

addz placementHeight

unID=1

! --- END PANELS OPTION ---
front_cab = 0
rrear_cab = 0
lrear_cab = 0
left_cab = 0
right_cab = 0
if rrear_panel then rrear_cab = rrear_cab + 0.02
if lrear_panel then lrear_cab = lrear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02

pen gs_cont_pen

ADD a/2,b/2,0

! --- 3D hotspots --------------------------------------------------------------

hotspot -a/2, -b/2, 0, unID : unID=unID+1
hotspot -a/2+depth_cab, -b/2, 0, unID : unID=unID+1
hotspot  a/2,  b/2-depth_cab, 0, unID : unID=unID+1
hotspot  a/2,  b/2, 0, unID : unID=unID+1
hotspot -a/2,  b/2, 0, unID : unID=unID+1

hotspot -a/2, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2+depth_cab, -b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2+depth_cab, -b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2+depth_cab, -b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2-depth_cab, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2-depth_cab, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2-depth_cab, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot -a/2, b/2, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot -a/2, b/2, -1, unID, zzyzx, 3 : unID=unID+1
hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2 : unID=unID+1

! --- End of 3D hotspots -------------------------------------------------------

ADD -a/2, -b/2, 0

!  --- CABINET -----------------------------------------------------------------
if ABS(a-depth_cab)<EPS then a=depth_cab+0.001
	ADD lrear_panel*lrear_cab,left_panel*left_cab,0
	corncab_unID=unID
	CALL "fa_gen_corncabinet" PARAMETERS a=a-(lrear_panel*lrear_cab+right_panel*right_cab),
		b=b-(left_panel*left_cab+rrear_panel*rrear_cab), zzyzx=zzyzx,
		gs_detlevel_3D_m=gs_detlevel_3D_m,
		dob_face=0, gs_shadow=gs_shadow, ldepth_cab=depth_cab-lrear_cab-front_cab,
		rdepth_cab=depth_cab-rrear_cab-front_cab, count_th=0,
		front_cab=0, left_cab=left_cab, right_cab=right_cab,
		lrear_cab=lrear_cab, rrear_cab=rrear_cab,
		toe_h=0,
		bs_h=0, gs_sink_type_m=0, gs_tap_type_m=0,
		side_vis_m=0, gs_cont_pen=gs_cont_pen,
		gs_cabinet_mat=gs_cabinet_mat,fr_th1=fr_th1,
		corncab_unID=corncab_unID,
		returned_parameters basecab_unID
	del 1
	unID = corncab_unID

	alpha=ATN((b-depth_cab)/(a-depth_cab))

	if gs_detlevel_3D_m=2 then
		! SHELVES
		MATERIAL gs_cabinet_mat

		if shelf_num>0 AND A>0.04 then
		count_th = 0
		for ii=1 to shelf_num
			ADDZ ((zzyzx-count_th-0.02)/(shelf_num+1))*(ii)
			aa = 0.02

			MATERIAL  gs_ShelfMat
			PRISM_ 6,0.02,
				   aa+lrear_cab, 0.02+left_cab, 15,
				   depth_cab+0.02/TAN(alpha)-fr_th1, 0.02+left_cab, 15,
				   a-0.02-right_cab, b-depth_cab-0.02/TAN(alpha)+fr_th1, 15,
				   a-0.02-right_cab, b-0.02-rrear_cab, 15,
				   aa+lrear_cab, b-0.02-rrear_cab, 15,
				   aa+lrear_cab, 0.02+left_cab, -1
			del 1
		next ii
	endif
endif

! --- END PANELS ---------------------------------------------------------------
accessno=2
! --- on rear panel there is no glas and grilles and knobs ---
If iEndPanelType = 1 	then gs_door_type_mr=1
If iEndPanelType = 2 	then gs_door_type_mr=2
If iEndPanelType = 4 	then gs_door_type_mr=11
If iEndPanelType = 7 	then gs_door_type_mr=4
If iEndPanelType = 9 	then gs_door_type_mr=6
If iEndPanelType = 99	then gs_door_type_mr=10

if lrear_panel=1 then
	ADD lrear_cab, b-rrear_cab, 0
	ROTZ -90
	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=b-left_cab-rrear_cab, door_h=zzyzx, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorglas_mat=doorframe_mat_end,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,fr_th1=0.02,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	del 2
endif

if rrear_panel=1 then
	ADD a-right_cab, b-rrear_cab, 0
	ROTZ 180

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=a-lrear_cab-right_cab, door_h=zzyzx, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorglas_mat=doorframe_mat_end,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,fr_th1=0.02,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	del 2
endif

if left_panel=1 then
	ADD lrear_cab, left_cab, 0
	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=depth_cab-lrear_cab-fr_th1, door_h=zzyzx, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorglas_mat=doorframe_mat_end,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,fr_th1=0.02,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	del 1
endif

if right_panel=1 then
	ADD a-right_cab, b-depth_cab+fr_th1, 0
	ROTZ 90

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=depth_cab-rrear_cab-fr_th1, door_h=zzyzx, gs_door_type_m=gs_door_type_mr, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w_end, gs_cont_pen=gs_cont_pen,
			doorglas_mat=doorframe_mat_end,
			doorframe_mat=doorframe_mat_end, gs_knob_mat=gs_knob_mat,
			gs_knob_type_m=0,
			gs_cust_p=gs_cust_p_end,fr_th1=0.02,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			returned_parameters cabdoortyp_unID

	unID = cabdoortyp_unID
	del 2
endif

! --- DOOR ---

del TOP

addz placementHeight

alpha2=ATN((b-depth_cab-(left_cab*left_panel))/(a-depth_cab-(right_cab*right_panel)))

aa=SQR((a-depth_cab+fr_th1-0.02-(right_cab*right_panel))^2+(b-depth_cab+fr_th1-0.02-(left_cab*left_panel))^2): accessno=1

ADD depth_cab-fr_th1,left_cab*left_panel,0
ROTZ alpha2
ADDx 0.02*SQR(2)/2

if gs_door_type_m > 0 then

	cabdoortyp_unID=unID
	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
			gs_full_edit=gs_full_edit, door_ang=door_ang,
			door_w=aa, door_h=zzyzx, gs_door_type_m=gs_door_type_m, gs_shadow=gs_shadow,
			doorframe_w=doorframe_w, gs_cont_pen=gs_cont_pen,
			doorglas_mat=doorglas_mat,gs_cust_p=gs_cust_p,
			vert_gnum=vert_gnum, hor_gnum=hor_gnum,
			doorframe_mat=doorframe_mat,fr_th1=fr_th1, knob_size=knob_size,
			gs_knob_p_m=gs_knob_p_m,gs_knob_mat=gs_knob_mat,
			gs_knob_type_m=gs_knob_type_m,
			knob_pl_x=knob_pl_x,knob_pl_y=knob_pl_y,
			cabdoortyp_unID=cabdoortyp_unID,gs_bevel=gs_bevel,
			gs_cust_knob = gs_cust_knob, gs_customHandleLength = gs_customHandleLength,
			gs_customHandleHeight = gs_customHandleHeight, gs_customHandleDepth = gs_customHandleDepth,
			returned_parameters cabdoortyp_unID
	unID = cabdoortyp_unID
endif
del 4

END

