
VALUES "A" range (0, )
VALUES "B" range (0, )

values "ApplType" st_ApplType
values "ApplType_m" APPLTYPE_MIC_OVEN,
					APPLTYPE_SINGLE_OVEN,
					APPLTYPE_COMB_OVEN,
					APPLTYPE_DOUBLE_OVEN,
					APPLTYPE_NONE

if GLOB_MODPAR_NAME = "ApplType" then
	ApplType_m = APPLTYPE_MIC_OVEN
	if ApplType = st_ApplType[2]		then ApplType_m = APPLTYPE_SINGLE_OVEN
	if ApplType = st_ApplType[3]		then ApplType_m = APPLTYPE_COMB_OVEN
	if ApplType = st_ApplType[4]		then ApplType_m = APPLTYPE_DOUBLE_OVEN
	if ApplType = st_ApplType[5]		then ApplType_m = APPLTYPE_NONE
	parameters ApplType_m = ApplType_m
else
	ApplType = st_ApplType[1]
	if ApplType_m = APPLTYPE_SINGLE_OVEN	then ApplType = st_ApplType[2]
	if ApplType_m = APPLTYPE_COMB_OVEN		then ApplType = st_ApplType[3]
	if ApplType_m = APPLTYPE_DOUBLE_OVEN	then ApplType = st_ApplType[4]
	if ApplType_m = APPLTYPE_NONE			then ApplType = st_ApplType[5]
	parameters ApplType = ApplType
endif

values "OvenDoorStyle" st_OvenDoorStyles
values "ApplStyle_m" APPLSTYLE_1, APPLSTYLE_2

if GLOB_MODPAR_NAME = "OvenDoorStyle" then
	ApplStyle_m = APPLSTYLE_1
	if OvenDoorStyle = st_OvenDoorStyles[2]	then ApplStyle_m = 2
	parameters ApplStyle_m = ApplStyle_m
else
	OvenDoorStyle = st_OvenDoorStyles[1]
	if ApplStyle_m = APPLSTYLE_2 then OvenDoorStyle = st_OvenDoorStyles[2]
	parameters OvenDoorStyle = OvenDoorStyle
endif

values "door_ang" range [0,180]


! --- Minimal Space ---
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]
IF UpCabinet=1 THEN
	values "low_door_h" range [toe_h+0.1,ZZYZX-ApplHeight-0.1]
	values "ApplHeight" range [toe_h+0.1,ZZYZX-low_door_h-0.1]
ELSE
	values "ApplHeight" range [0.1,ZZYZX-toe_h-0.1]
ENDIF

! --- Mic-Oven Doors ---

IF GLOB_MODPAR_NAME="ApplType_m" or GLOB_MODPAR_NAME="ApplType" then

	IF ApplType_m = APPLTYPE_MIC_OVEN THEN
		ApplHeight = 0.4
		PARAMETERS ApplHeight = ApplHeight
	ENDIF

	IF ApplType_m = APPLTYPE_SINGLE_OVEN THEN
		ApplHeight = 0.5
		PARAMETERS ApplHeight = ApplHeight
	ENDIF

	IF ApplType_m = APPLTYPE_COMB_OVEN THEN
		ApplHeight = 0.8
		PARAMETERS ApplHeight = ApplHeight
	ENDIF

	IF ApplType_m = APPLTYPE_DOUBLE_OVEN THEN
		ApplHeight = 0.9
		PARAMETERS ApplHeight = ApplHeight
	ENDIF

ENDIF

IF UpCabinet=1 THEN
	up_door_h = zzyzx-low_door_h-ApplHeight
	PARAMETERS up_door_h = up_door_h
	LOCK "up_door_h"
ELSE
	low_door_h = zzyzx-ApplHeight
	PARAMETERS low_door_h = low_door_h
	LOCK "up_door_h","low_door_h"
ENDIF


! --- LOCK PARAMETERS ---

IF low_door=0 THEN
	LOCK "hor_gnum","vert_gnum"
	HIDEPARAMETER "hor_gnum","vert_gnum"
ENDIF

IF up_door=0 THEN
	LOCK "hor_gnum2","vert_gnum2"
	HIDEPARAMETER "hor_gnum2","vert_gnum2"
ENDIF

IF UpCabinet=0 THEN
	LOCK "up_door","up_door_h","vert_gnum2","hor_gnum2","knob_pl_x_up", "knob_pl_y_up"
	HIDEPARAMETER "up_door","up_door_h","vert_gnum2","hor_gnum2","knob_pl_x_up", "knob_pl_y_up"
ENDIF

IF OvenDoorStyle=`Стиль 1` THEN
	HIDEPARAMETER "SwitchMat"
	LOCK "SwitchMat"
ENDIF

IF  ApplType_m = APPLTYPE_NONE THEN
	HIDEPARAMETER "OvenDoorStyle","BaseMat","PanelMat","GlassMat","HandleMat","SwitchMat","Appl2D","ApplLineType"
	LOCK "OvenDoorStyle","BaseMat","PanelMat","GlassMat","HandleMat","SwitchMat","Appl2D","ApplLineType"
ENDIF

IF Appl2D=0 THEN
	HIDEPARAMETER "ApplLineType"
	LOCK "ApplLineType"
ENDIF


CALL "Kitchen Cabinets UI macro" parameters all \
									pageIds = pageIds,
									CornCabType = 0,
									bEdgeBevel = 2,
									CabinetType = 2,
									bCounterOverhang = 0,
									bMirror = 1,

									bToe = 1,
									bShelf= 1,
									bShowSurfaces = 1,

									bMoreDoorPanels = 0,
									bPossibleDrawer = 0,
									bPossibleDrawerNum = 0,
									nDoors = 1,

									bDoubleFacadeEdit = 0,
									bOneRearPanelEdit = 1,
									bHorKnobPosition = 1,
									bVerKnobPosition =1,
									bPositionType = 1,

									bCabSymbol = 0,
									bPossibleSinknTap = 0,
									bApplianceEdit = 1,
									bSymbLineEdit = 0,
									bShadowEdit = 1,
									bHotspotEdit = 1,

									bEnableBackSplash	= 0,
									bEnableDoorpanel	= 1,
									bEnableEndpanel		= 1,
									bDoubleDoor			= 0,
									bEnableKnobPlacing	= 1,
									bEnableShelf		= 1,
									bEnableSink			= 0,
									isTallCabinet		= 1,

									bKnobParams = 1

VALUES "gs_ui_current_page" 1, 2, 3, 4, 5, 6


