
IF gs_shadow=0 THEN SHADOW OFF

if iDetlevel3D = 0 then end

resol gs_resol * (iDetlevel3D = 2) + 8 * (iDetlevel3D = 1)
PEN gs_cont_pen

!!! Posts
addz legHeight*(bLegTip)
for vp=1 to 2
	for hp=1 to 2
	material frameMat
	prism_ 4, zzyzx-gridHeight-legHeight*(bLegTip),
		0, 0, 15,
		frameWidth, 0, 15,
		frameWidth, frameDepth, 15,
		0, frameDepth, 15
	mulx -1
	addx -a
	next hp
	del 4
muly -1
addy -b
next vp
del 4
del 1
!!! End Of Posts



!!! Top Frame
addz zzyzx-gridHeight
	material frameMat
	prism_ 10, gridHeight,
		0, 0, 15,
		a, 0, 15,
		a, b, 15,
		0, b, 15,
		0, 0, -1,

		frameWidth, frameDepth, 15,
		a-frameWidth, frameDepth, 15,
		a-frameWidth, b-frameDepth, 15,
		frameWidth, b-frameDepth, 15,
		frameWidth, frameDepth, -1
del 1
!!! End Of Top Frame



!!! Horizontal Grids
add frameWidth+horGridDist, 0, zzyzx-gridHeight
for hg=1 to horGridNum
	for hgg=1 to verGridNum+1
	material frameMat
	prism_ 4, gridHeight,
		0, frameDepth, 15,
		gridWidth, frameDepth, 15,
		gridWidth, frameDepth+verGridDist, 15,
		0, frameDepth+verGridDist, 15
	addy gridWidth+verGridDist
	next hgg
	del verGridNum+1
addx horGridDist+gridWidth
next hg
del horGridNum
del 1
!!! End Of Horizontal Grids



!!! Vertical Grids
add 0, frameDepth+verGridDist, zzyzx-gridHeight
for vg=1 to verGridNum
	material frameMat
	prism_ 4, gridHeight,
		frameWidth, 0, 15,
		a-frameWidth, 0, 15,
		a-frameWidth, gridWidth, 15,
		frameWidth, gridWidth, 15
addy verGridDist+gridWidth
next vg
del verGridNum
del 1
!!! End Of Vertical Grids



!!! Umbrella Holder
addz umbHoldElev
for huh=1 to 2
	material frameMat
	prism_ 4, umbHoldHeight,
		frameWidth, 0, 15,
		a-frameWidth, 0, 15,
		a-frameWidth, frameDepth, 15,
		frameWidth, frameDepth, 15
muly -1
addy -b
next huh
del 4

for vuh=1 to 2
	material frameMat
	prism_ 4, umbHoldHeight,
		0, frameDepth, 15,
		frameWidth, frameDepth, 15,
		frameWidth, b-frameDepth, 15,
		0, b-frameDepth, 15
mulx -1
addx -a
next vuh
del 4
del 1

addz umbHoldElev+umbHoldThk
for vuc=1 to moduleNumVer
	if vuc=1 or vuc=moduleNumVer then
		umbHoldDepth	= (b - 2 * frameDepth - (moduleNumVer - 1) * gridWidth) / moduleNumVer - umbHoldThk - umbHoldThk * (moduleNumVer = 1)
	else
		umbHoldDepth	= (b - 2 * frameDepth - (moduleNumVer - 1) * gridWidth) / moduleNumVer
	endif

	for huc=1 to moduleNumHor
		if huc=1 or huc=moduleNumHor then
			umbHoldWidth	= (a - 2 * frameWidth - (moduleNumHor - 1) * gridWidth) / moduleNumHor - umbHoldThk - umbHoldThk * (moduleNumHor = 1)
		else
			umbHoldWidth	= (a - 2 * frameWidth - (moduleNumHor - 1) * gridWidth) / moduleNumHor
		endif

		cutform 4, 1, 1+16,
			0, 0, -1, 0,
			frameWidth+umbHoldThk, frameDepth+umbHoldThk, 1+2+4+8+16,
			frameWidth+umbHoldThk+umbHoldWidth, frameDepth+umbHoldThk, 1+2+4+8+16,
			frameWidth+umbHoldThk+umbHoldWidth, frameDepth+umbHoldThk+umbHoldDepth, 1+2+4+8+16,
			frameWidth+umbHoldThk, frameDepth+umbHoldThk+umbHoldDepth, 1+2+4+8+16
	addx umbHoldWidth+gridWidth
	next huc
	del moduleNumHor
addy umbHoldDepth+gridWidth
next vuc
del moduleNumVer
del 1

addz umbHoldElev
material umbHoldMat
prism_ 4, umbHoldHeight,
	frameWidth, frameDepth, 15,
	a-frameWidth, frameDepth, 15,
	a-frameWidth, b-frameDepth, 15,
	frameWidth, b-frameDepth, 15
del 1

for vuc=1 to moduleNumVer
	for huc=1 to moduleNumHor
		cutend
	next huc
next vuc
!!! End Of Umbrella Holder



!!! Side Cover
if iCaseType=2 then
	for vp=1 to 2
		for hp=1 to 2
		material frameMat
		cutpolya 4, 1, 0,
			0, 0, 15,
			frameWidth, 0, 15,
			frameWidth, frameDepth, 15,
			0, frameDepth, 15
		mulx -1
		addx -a
		next hp
		del 4
	muly -1
	addy -b
	next vp
	del 4

	addz umbHoldElev+umbHoldHeight
	for louv=1 to coverLouverNum
		material coverMat
		tube 6, 7, 1+2+16+32,
			coverThk, 0, 0,
			coverThk, coverLouverHeightR, 0,
			coverCutDepth, coverLouverHeightR, 0,
			0, coverLouverHeightR-coverCutDepth, 0,
			0, coverCutDepth, 0,
			coverCutDepth, 0, 0,

			a, b, 0, 0,
			0, b, 0, 0,
			0, 0, 0, 0,
			a, 0, 0, 0,
			a, b, 0, 0,
			0, b, 0, 0,
			0, 0, 0, 0
	addz coverLouverHeightR
	next louv
	del coverLouverNum
	del 1

	for vp=1 to 2
		for hp=1 to 2
			cutend
		next hp
	next vp
endif
!!! End Of Side Cover



!!! Legs
if iLegTipType = 1 then
	add frameWidth/2, frameDepth/2, 0

	for vp=1 to 2
		for hp=1 to 2
		material legTipMat
		cone legHeight/2, min(frameWidth, frameDepth)/2, min(frameWidth, frameDepth)/2, 90, 90
		addz legHeight/2
		cone legHeight/2, min(frameWidth, frameDepth)/4, min(frameWidth, frameDepth)/4, 90, 90
		del 1
		mulx -1
		addx -a+frameWidth
		next hp
		del 4
	muly -1
	addy -b+frameDepth
	next vp
	del 4

	del 1
endif

if iLegTipType = 2 then
	mulz -1
	add frameWidth/2, frameDepth/2, -legHeight

	for vp=1 to 2
		for hp=1 to 2
		material legTipMat
		cone legHeight, min(frameWidth, frameDepth)/2, min(frameWidth, frameDepth)/4, 90, 90
		mulx -1
		addx -a+frameWidth
		next hp
		del 4
	muly -1
	addy -b+frameDepth
	next vp
	del 4

	del 2
endif
!!! End Of Legs






