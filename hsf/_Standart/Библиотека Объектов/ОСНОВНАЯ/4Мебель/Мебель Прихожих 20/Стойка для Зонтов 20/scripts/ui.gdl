
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 							=-1
_idxTab = 1
TABID_Style							= _idxTab  : _idxTab = _idxTab + 1
TABID_Dimensions					= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces		= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions					= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Стойки для Зонтов`
ui_current_page gs_ui_current_page

UI_PAGE TABID_Style, TABID_Root, `Стиль`, "uiTab_Style_flat(1)"
	gosub "StylePage"

UI_PAGE TABID_Dimensions, TABID_Root, `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "DimensionsPage"

UI_PAGE TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

UI_PAGE TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ------------------------------------------------------------------------------
!Subroutines
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"StylePage":
! ------------------------------------------------------------------------------
	pageStartY 	= 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_outfFlag	= 0
	x1 = 0
	x2 = 123
	x3 = 231
	x4 = 354
	dy = 24

	_yCurr = pageStartY + _yOffset
	ui_style 0, 1
	ui_outfield `Тип Стойки`, x1, _yCurr, x3 - 22, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + _outfHeight + 6

	ui_infield{3} "iCaseType", x1 + 1, _yCurr, 212, 96,
		4, 1,	! ui_UmbrellaStand_CaseStyle.png
		2, 1,
		100, 100, 90,90,
		1,  stCaseType[1], 1,
		2,  stCaseType[2], 2

	_yCurr = _yCurr + 96 + 5
	ui_infield "coverLouverHeight", x2, _yCurr, 90, _infHeight ui_tooltip `Высота Планок`

	_yCurr = _yCurr + _yOffset
	if iCaseType = 1 then _outfFlag = 4	! Frame
	ui_outfield `Высота Планок`, x1, _yCurr, x2 - 2, _outfHeight, _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr - _yOffset + 2*dy
	
	ui_infield "frameMat", x1 + 20, _yCurr, 193, 25 ui_tooltip `Покрытие Обвязки`
	ui_pict 3, x1, _yCurr + 3, 18, 18, 1 ui_tooltip `Покрытие Обвязки`	! ui_UmbrellaStand_FrameMat.png

	dy = 27

	_yCurr = _yCurr + dy
	if iCaseType = 2 then	! Cased
		ui_infield "coverMat", x1 + 20, _yCurr, 193, 25 ui_tooltip `Боковое Покрытие`
		ui_pict 4, x1, _yCurr + 3, 18, 18, 1 ui_tooltip `Боковое Покрытие`	! ui_UmbrellaStand_SideMat.png
	else
		ui_infield "umbHoldMat", x1 + 20, _yCurr, 193, 25 ui_tooltip `Боковое Покрытие`
		ui_pict 5, x1, _yCurr + 3, 18, 18, 1 ui_tooltip `Боковое Покрытие`	! ui_UmbrellaStand_TrayMat.png
	endif

	_yCurr = _yCurr + dy

	ui_separator 222, x1, 222, _yCurr

	dy = 24
	_yCurr = pageStartY
	ui_style 0, 1
	ui_infield{3} "bLegTip", x3, _yCurr, 444 - x3 - 2, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Опора Ножек`, 0,
					"", `Опора Ножек`, 1
	ui_style 0, 0

	_yCurr = _yCurr + _infHeight + 6

	ui_infield{3} "iLegTipType", x3 + 1, _yCurr, 212, 96,
		4, 2,	! ui_UmbrellaStand_LegTipStyle.png
	
		2, 1,
		100, 100, 90,90,
		1,  stLegTipType[1], 1,
		2,  stLegTipType[2], 2

	_yCurr = _yCurr + 96 + 5
	ui_infield "legHeight", x4, _yCurr, 90, _infHeight ui_tooltip `Высота`

	_yCurr = _yCurr + _yOffset
	if not(bLegTip) then _outfFlag = 4
	ui_outfield `Высота`, x3, _yCurr, x2 - 2, _outfHeight, _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr - _yOffset + 2*dy
	
	ui_infield "legTipMat", x3, _yCurr, 213, 25 ui_tooltip `Покрытие Опор Ножек`

return


! ------------------------------------------------------------------------------
"DimensionsPage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_yOffset = 4
	_outfFlag = 0
	xPict = 182
	yPict = 36
	PictWidth = 81 
	PictHeight = 91

	x1 = 0
	x2 = 159
	x3 = 231
	x4 = 390
	x5 = xPict - 93
	x6 = xPict + PictWidth + 3
	dy = 24

	_yCurr = pageStartY
	ui_infield "moduleNumHor", x2, _yCurr, 54, _infHeight ui_tooltip `Количество Модулей 1`
	ui_infield "moduleNumVer", x4, _yCurr, 54, _infHeight ui_tooltip `Количество Модулей 2`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Количество Модулей по X`, x1, _yCurr, x2 - 2, _outfHeight, _outfFlag
	ui_outfield `Количество Модулей по Y`, x3, _yCurr, x2 - 2, _outfHeight, _outfFlag

	_yCurr = _yCurr + dy - _yOffset
	ui_separator x1, _yCurr, 444, _yCurr

	ui_pict 7, xPict, yPict, PictWidth, PictHeight, 1	! ui_UmbrellaStand_Dimensions2.png

	_yCurr = yPict - 2
	ui_infield "gridHeight", x6, _yCurr, 90, _infHeight ui_tooltip `Высота Решетки`

	_yCurr = yPict + 36
	ui_infield "zzyzx", x5, _yCurr, 90, _infHeight ui_tooltip `Высота`

	_yCurr = yPict + 51
	ui_infield "umbHoldHeight", x6, _yCurr, 90, _infHeight ui_tooltip `Высота Поддона Стойки`

	_yCurr = yPict + 51 + dy
	ui_infield "umbHoldElev", x6, _yCurr, 90, _infHeight ui_tooltip `Возвышение Поддона Стойки`

	yPict = 176
	PictHeight = 67

	ui_pict 6, xPict, yPict, PictWidth, PictHeight, 1	! ui_UmbrellaStand_Dimensions1.png

	_yCurr = yPict - _infHeight - 3
	ui_infield "moduleWidth", xPict + 5, _yCurr, 90, _infHeight ui_tooltip `Ширина Модуля`

	_yCurr = _yCurr - _outfHeight - 2
	ui_outfield `1 Модуль по X`, xPict + 5, _yCurr, 90, _outfHeight, _outfFlag + 2

	_yCurr = yPict + 23
	ui_infield "moduleDepth", x5, _yCurr, 90, _infHeight ui_tooltip `Глубина Модуля`

	_yCurr = _yCurr - _outfHeight - 2
	ui_outfield `1 Модуль по Y`, x5, _yCurr, 90, _outfHeight, _outfFlag + 2

	_yCurr = yPict + 44
	ui_infield "frameDepth", x6, _yCurr, 90, _infHeight ui_tooltip `Глубина Обвязки`

	_yCurr = yPict + PictHeight + 3
	ui_infield "frameDepth", x6 - 10, _yCurr, 90, _infHeight ui_tooltip `Глубина Обвязки`
	ui_infield "gridWidth", xPict - 19, _yCurr, 90, _infHeight ui_tooltip `Ширина Решетки`

return


! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "iDetlevel3D"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"

		if iDetlevel3D = 2 and bLegTip then	! Detailed
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
		endif

		if iDetlevel3D <> 0 then	! Off
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "frameMat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if iCaseType = 2 then	! Covered
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "coverMat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "umbHoldMat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if bLegTip then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "legTipMat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
