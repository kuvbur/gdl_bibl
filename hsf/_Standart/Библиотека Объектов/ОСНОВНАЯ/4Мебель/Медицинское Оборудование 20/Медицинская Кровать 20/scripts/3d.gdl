
if not(gs_shadow) then shadow off
if gs_detlevel_3D_m = 2 then	! Detailed
	resol gs_resol
else
	if gs_detlevel_3D_m = 1 then ! Simple
		resol 3
	else
		END	! Off
	endif
endif

pen gs_cont_pen

call "Resize_A_B_ZZYZX" Parameters ALL

!-----Frame-----
addz ZZYZX-0.2
add -0.02, 0, 0.03
rotx -90
S_size=B
gosub 10
addx A+0.02
gosub 10
del 3

add 0, -0.02, -0.01
rotz 90
rotx 90
S_size=A
gosub 10
addx B+0.02
gosub 10
del 5

add 0, 0.4, 0.15
roty 90
rotz 90
muly 2
mulx 1.5
gosub 10
del 5

add 0, B-0.4, 0.15
roty 90
rotz 90
muly 2
mulx 1.5
gosub 10
del 5

S_size=0.7
add 0.1, 0.4, 0.2
if abs(zzyzx-0.65)>EPS then
	rotx -acs((zzyzx-0.4)/0.6)
else
	rotx -65
endif
gosub 10
addx A-0.2
gosub 10
del 3

add 0.1, B-0.37, 0.2
rotz 180
if abs(zzyzx-0.65)>EPS then
	rotx -acs((zzyzx-0.4)/0.6)
else
	rotx -65
endif
gosub 10
addx -A+0.2
gosub 10
del 4



!-----wheel----
add 0, 0.4, 0.05

gosub 30
addx A
gosub 30
del 1
addy B-0.8
gosub 30
addx A
gosub 30
del 3


addz ZZYZX-0.2

!-----tubes-----
add -0.01, -0.01, -0.02
bow_h=0.5
gosub 20
addy B+0.02
bow_h=0.7
gosub 20
del 2


! ----- Mattress -----
material gs_matrace_mat
rotx -90

if gs_positionBed_m = 2 then	! Reclined
	TUBE 6, 4, 1+2+16+32,
		0, 0, 1,
		A, 0, 1,
		A, -0.16, 1,
		A-0.02, -0.2, 1,
		0.02, -0.2, 1,
		0, -0.16, 1,

		0, 0, -1, 0,
		0, 0, 0, 0,
		0, 0, B, 0,
		0, 0, B+1, 0
else							! Sit Up
	TUBE 6, 7, 1+2+16+32,
		0, 0, 1,
		A, 0, 1,
		A, -0.16, 1,
		A-0.02, -0.2, 1,
		0.02, -0.2, 1,
		0, -0.16, 1,

		0, 0.2, -1, 0,
		0, 0.1, 0.03, 0,
		0, -0.1,  B/3, 0,
		0, 0, B/2, 0,
		0, 0, 3*B/5, 0,
		0, -0.3, B-0.03, 0,
		0, -0.5, B+1, 0
endif
del 1

! ----- Pillow -----
if bPillow and gs_detlevel_3D_m = 2 THEN	! Detailed
	ADD a/2, b-0.32, 0.20
	if gs_positionBed_m = 1 then	! Sit Up
		rotx 22
		addz 0.19
	endif

	call "fa_pillow" PARAMETERS a					= a*0.60,
								gs_detlevel_3D_m	= 2,
								gs_resol			= gs_resol,
								pillow_mat			= gs_pillow_mat
	del 1

	if gs_positionBed_m = 1 then del 2
endif


! ----- Safety Bar -----
if gs_detlevel_3D_m = 2 then	! Detailed
	if gs_positionBar_m = BAR_SIDE1 | gs_positionBar_m = BAR_BOTHSIDE then		! Side 1 or Both Sides
		addx -0.03
		gosub 40
		del 1
	endif
	if gs_positionBar_m = BAR_SIDE2 | gs_positionBar_m = BAR_BOTHSIDE then		! Side 2 or Both Sides
		addx A+0.03
		gosub 40
		del 1
	endif
endif


del 1



END
! ======================================================================
! SubRoutines
! ======================================================================
!-----frame materials-----
10:
	material gs_frame_mat
	prism 4, S_size,
		0, 0,
		0.02, 0,
		0.02, 0.04,
		0, 0.04
	return

!-----tubes-----
20:
	material gs_frame_mat

	cylind bow_h, 0.02
	addz bow_h
	elbow 0.1, 90, 0.02
	add 0.1, 0, 0.1
	roty 90
	cylind A-0.18, 0.02
	addz A-0.18
	elbow 0.1, 90, 0.02
	add 0.1, 0, 0.1
	roty 90
	cylind bow_h, 0.02
	del 6

	add 0.04, 0, 0.1
	rotx 90
	material gs_board_mat
	prism 4, 0.01,
		0, 0,
		A-0.06, 0,
		A-0.06, bow_h-0.1,
		0, bow_h-0.1
	del 2

	return

!-----weels-----
30:
	if gs_detlevel_3D_m = 1 then resol 5

	material gs_frame_mat
	cylind 0.2, 0.02
	add -0.015, 0.02, 0
	roty 90
	material gs_wheel_mat
	cylind 0.03, 0.05
	del 2
	return

!-----sidor-----
40:
	!resol 6
	material gs_frame_mat
	addy 0.3
	cylind 0.4, 0.01
	addy B/2-0.3
	cylind 0.4, 0.01
	del 1
	addy B-0.6
	cylind 0.4, 0.01
	del 2
	add 0, 0.28, 0.41
	rotx -90
	cylind B-0.56, 0.01
	add 0, 0.13, 0.02
	cylind B-0.6, 0.005
	addy 0.13
	cylind B-0.6, 0.005
	del 4
	return
