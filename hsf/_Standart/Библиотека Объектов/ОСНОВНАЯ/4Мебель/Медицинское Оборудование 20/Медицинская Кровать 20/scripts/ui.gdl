
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_Style						= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Медицинской Кровати`
ui_current_page gs_ui_current_page

ui_page TABID_Style, TABID_Root, `Стиль`, "uiTab_Style_flat(1)"
	gosub "Style"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalSpacepage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"Style":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 1
	x1 = 0
	x2 = 126
	x3 = 228
	x4 = 387
	x5 = 444 - 103
	_dy = 24

	_yCurr = pageStartY

	ui_infield{3} "bSafetyBar", x1, _yCurr + _yOffset, x2 - 2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Защитное Ограждение`, 0,
					"", `Защитное Ограждение`, 1

	if bSafetyBar then
		ui_infield{3} "gs_positionBar_m", x2, _yCurr, x3 - x2 - 13, _infHeight,
						8, "",
						2, 0,
						0, 0, 0, 0,
						"", stBarPosition[2],	BAR_SIDE1,
						"", stBarPosition[3],	BAR_SIDE2,
						"", stBarPosition[4], 	BAR_BOTHSIDE
	else
		ui_infield{3} "gs_positionBar_m", x2, _yCurr, x3 - x2 - 13, _infHeight,
						8, "",
						2, 0,
						0, 0, 0, 0,
						"", stBarPosition[1],	BAR_NONE
	endif

	ui_infield{3} "bPillow", x3, _yCurr + _yOffset, 444 - x3, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Подушка`, 0,
					"", `Подушка`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "gs_positionBed_m", 0, _yCurr, 444, 265 - _yCurr,
		1, 1, 2, 1,	! ui_HospitalBed_StyleAndDimensions.png
		100, 110,
		90, 90,
		1, `Сидячее`, 	2,
		2, `Лежачее`, 	1 ui_tooltip `Положение Кровати`

return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 20,	! SYMBOLCLAS_MEDICAL
													bUniformFill		= bUniformFill,
													iSymbolType			= iSymbolType,
													stSymbolTypes		= stSymbolType2D,
													iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
 	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_frame_mat"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "gs_board_mat"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "gs_wheel_mat"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "gs_matrace_mat"
		SurfItemID = SurfItemID + 1

		if bPillow then
			surfacesArray[SurfItemID]	= "gs_pillow_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"MinimalSpacepage":
! ------------------------------------------------------------------------------
	call "fa_minimalspace" parameters MSType = 2		!MINSPACE_SIMPLE_BED

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return

