
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

values "a" range [0.4, ]
values "b" range [1.2, ]
values "zzyzx" range [0.4, 1.0]

! ----- General Settings -----
values "gs_positionBed" stBedPosition		! Sit Up(1), Reclined(2)
values "gs_positionBed_m" 1, 2
if GLOB_MODPAR_NAME = "gs_positionBed" then
	gs_positionBed_m = 1
	for i = 1 to vardim1(stBedPosition)
		if gs_positionBed = stBedPosition[i] then	gs_positionBed_m = i
	next i
	parameters gs_positionBed_m = gs_positionBed_m
else
	gs_positionBed_m = max(min(gs_positionBed_m,2),1)
	parameters gs_positionBed = stBedPosition[gs_positionBed_m]
endif

values{2} "gs_positionBar_m"	BAR_NONE, stBarPosition[1],
								BAR_SIDE1, stBarPosition[2],
								BAR_SIDE2, stBarPosition[3],
								BAR_BOTHSIDE, stBarPosition[4]

if GLOB_MODPAR_NAME = "bSafetyBar" then
	if not(bSafetyBar) then
		gs_positionBar_m = BAR_NONE
	else
		gs_positionBar_m = BAR_BOTHSIDE
	endif
	parameters gs_positionBar_m = gs_positionBar_m
endif

if not(bSafetyBar) | gs_positionBar_m = BAR_NONE then
	lock "gs_positionBar_m"
	hideparameter "gs_positionBar_m"
endif

if not(bPillow) then
	lock "gs_pillow_mat"
	hideparameter "gs_pillow_mat"
endif

! ----- 2D Symbol Type -----
values "styp" stSymbolType2D		! Type 1(1),Type 2(2),Type 3(3),Type 4(4)
values "iSymbolType" 1, 2, 3, 4, 5
if GLOB_MODPAR_NAME = "styp" then
	iSymbolType = 1
	for i = 1 to vardim1(stSymbolType2D)
		if styp = stSymbolType2D[i] then	iSymbolType = i
	next i
	parameters iSymbolType = iSymbolType
else
	iSymbolType = max(min(iSymbolType,5),1)
	parameters styp = stSymbolType2D[iSymbolType]
endif

! ----- 3D Representation -----
values "gs_detlevel_3D" stDetlevel3D		! Off(0), Simple(1), Detailed(2)
values "gs_detlevel_3D_m" 0, 1, 2
if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	for i = 1 to vardim1(stDetlevel3D)
		if gs_detlevel_3D = stDetlevel3D[i] then	gs_detlevel_3D_m = 3-i
	next i
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D_m = max(min(gs_detlevel_3D_m,2),0)
	parameters gs_detlevel_3D = stDetlevel3D[3-gs_detlevel_3D_m]
endif

values "gs_resol" range [3, )

if gs_detlevel_3D_m = 1 then	! Simple
	lock "gs_resol"
else
	if gs_detlevel_3D_m = 0 then ! Off
		lock "gs_resol", "gs_shadow"
	endif
endif

! ----- Minimal Space -----

values "MSFront" range [0, ]
values "MSSideAdd" range [0, ]
values "MSLength" range [0, ]
values "MSLengthAdd" range [0, ]
values "MSSide" range [0, ]
values "MSSide2" range [0, ]


!!! ******************************** Different fills
if GLOB_MODPAR_NAME = "fillFor" then
	iFillFor = 1
	for i = 1 to vardim1(stFillFor)
		if fillFor = stFillFor[i] then	iFillFor = i
	next i
	parameters iFillFor = iFillFor
else
	iFillFor = max(min(iFillFor,3),1)
	parameters fillFor = stFillFor[iFillFor]
endif

if fillPenUniform=0 then parameters fillPenUniform=19
if fillPenPillow=0 then parameters fillPenPillow=19
if fillPenBlanket=0 then parameters fillPenBlanket=19
if gs_fill_pen=0 then parameters gs_fill_pen=19

if iSymbolType = 1 or iSymbolType = 2 or iSymbolType = 5 then
	if iSymbolType = 1 or iSymbolType = 5 then
		values "fillFor" stFillFor
		values "iFillFor" 1, 2, 3
	endif
	if iSymbolType = 2 then
		values "fillFor" stFillFor[1], stFillFor[2]
		values "iFillFor" 1, 2
		hideparameter "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
		lock 	      "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	endif
else
	values "fillFor" stFillFor[1]
	values "iFillFor" 1
	lock		  "fillFor", "iFillFor"
	hideparameter "fillFor", "iFillFor"
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
endif

if bUniformFill then
	lock "fillFor", "iFillFor"
	hideparameter "fillFor", "iFillFor"
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", "gs_fill_type", "gs_fill_pen", "gs_back_pen"
else
	hideparameter "fillTypeUniform", "fillPenUniform", "backPenUniform"
	lock 	      "fillTypeUniform", "fillPenUniform", "backPenUniform"
endif
