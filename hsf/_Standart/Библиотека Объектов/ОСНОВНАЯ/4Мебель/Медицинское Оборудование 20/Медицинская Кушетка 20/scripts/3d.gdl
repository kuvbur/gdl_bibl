

if not(gs_shadow) then shadow off
if gs_detlevel_3D_m = 2 then	! Detailed
	resol gs_resol
else
	if gs_detlevel_3D_m = 1 then ! Simple
		resol 3
		gs_resol = 6
	else
		END	! Off
	endif
endif

pen gs_cont_pen


!-----Definitions-----
unID = 1
mattressHeight = 0.05
mattresHead = 0.65			! Mattress Head Part Length
legPozY = 0.2				! Leg Pozition
legRubberOversize = 0.002	! Leg Rubber OverSize
stepHeight = 0.2
bedPositionAngle = 15
reducedLength = b-1.9

	define material	"builtInMatBlackPlastic" 0,
	  0.333333,   0.313725,   0.294118,
	 0.15,  0.15,  0.45,     0,      5,     0,
	  0.784314,   0.784314,   0.784314,
	         0,          0,          0,     0

call "Resize_A_B_ZZYZX" Parameters ALL


! ======================================================================
! Rectangular
! ======================================================================
if gs_leg_type_m = 1 then
	!-----Frame-----
	material gs_leg_mat
	addz ZZYZX-mattressHeight-leg_w
	prism_ 15, leg_w,
		0, 0, 15,
		a, 0, 15,
		a, b, 15,
		0, b, 15,
		0, 0, -1,
		leg_w, leg_w, 15,
		a-leg_w, leg_w, 15,
		a-leg_w, b-mattresHead-leg_w/2, 15,
		leg_w, b-mattresHead-leg_w/2, 15,
		leg_w, leg_w, -1,
		leg_w, b-mattresHead+leg_w/4, 15,
		a-leg_w, b-mattresHead+leg_w/4, 15,
		a-leg_w, b-leg_w, 15,
		leg_w, b-leg_w, 15,
		leg_w, b-mattresHead+leg_w/4, -1
	del 1
	roty 90
	rotz 90
	add 0, ZZYZX-0.7, a/2
	gosub 120
	del 3

	hotspot 0, legPozY, 0, unID : unID = unID + 1
	hotspot a, legPozY, 0, unID : unID = unID + 1
	hotspot a, b-legPozY, 0, unID : unID = unID + 1
	hotspot 0, b-legPozY, 0, unID : unID = unID + 1

	!-----Leg----
	legWidthHalf = leg_w/2
	legh = ZZYZX-mattressHeight-leg_w
	add leg_w/2, legPozY, 0
	gosub 10
	add a-leg_w, 0, 0
	gosub 10
	del 2
	add leg_w/2, b-legPozY, 0
	gosub 10
	add a-leg_w, 0, 0
	gosub 10
	del 2

	!-----Brace----
	addz stepHeight-leg_w/2
	if not(bStep) then
		prism 12,leg_w/2,
			leg_w, legPozY-leg_w/4,
			a-leg_w, legPozY-leg_w/4,
			a-leg_w, legPozY+leg_w/4,
			a/2+leg_w/4, legPozY+leg_w/4,
			a/2+leg_w/4, b-legPozY-leg_w/4,
			a-leg_w, b-legPozY-leg_w/4,
			a-leg_w, b-legPozY+leg_w/4,
			leg_w, b-legPozY+leg_w/4,
			leg_w, b-legPozY-leg_w/4,
			a/2-leg_w/4, b-legPozY-leg_w/4,
			a/2-leg_w/4, legPozY+leg_w/4,
			leg_w, legPozY+leg_w/4
	else
		prism 4,leg_w/2,
			leg_w, legPozY-leg_w/4,
			a-leg_w, legPozY-leg_w/4,
			a-leg_w, legPozY+leg_w/4,
			leg_w, legPozY+leg_w/4

		prism 4,leg_w/2,
			leg_w, b-legPozY-leg_w/4,
			a-leg_w, b-legPozY-leg_w/4,
			a-leg_w, b-legPozY+leg_w/4,
			leg_w, b-legPozY+leg_w/4

		prism 4,leg_w/2,
			leg_w/4, legPozY+leg_w/2,
			leg_w/4, b-legPozY-leg_w/2,
			leg_w*0.75, b-legPozY-leg_w/2,
			leg_w*0.75, legPozY+leg_w/2

		!prism 4,leg_w/2,					! Other Side
		!	a-leg_w/4, legPozY+leg_w/2,
		!	a-leg_w/4, b-legPozY-leg_w/2,
		!	a-leg_w*0.75, b-legPozY-leg_w/2,
		!	a-leg_w*0.75, legPozY+leg_w/2

		add leg_w/4-stepWidth, stepOffset, 0
		!add a-leg_w/4, stepOffset, 0		! Other Side
		gosub 20
		del 1
	endif
	del 1


	! ----- Mattress -----
	mattressReduc = 0.01
	mattressR = 0.02
	mattressLenght = b-mattresHead-2*mattressReduc
	mattressWidht = a - 2*mattressReduc
	add mattressReduc, mattressReduc, ZZYZX
	gosub 100
	del 1

	if gs_positionBed_m = 1 then	! Sit Up
		mattressLenght = mattresHead-mattressReduc
		add mattressReduc, b-mattresHead-mattressReduc, ZZYZX
		rotx bedPositionAngle
		gosub 100
		gosub 110
		del 2
	else							! Reclined
		mattressLenght = mattresHead-mattressReduc
		add mattressReduc, b-mattresHead, ZZYZX
		gosub 100
		gosub 110
		del 1
	endif

else
! ======================================================================
! Round
! ======================================================================

	!-----Leg----
	uFrameRadius = 0.1
	legRadiusHalf = leg_d/2
	uFrameHeight = ZZYZX-mattressHeight-legRadiusHalf
	uFrameLenght = b-legPozY/4-2*legRadiusHalf
	rotz 90
	add legPozY/4+legRadiusHalf, -2*legRadiusHalf, 0
	gosub 50
	del 1
	add legPozY/4+legRadiusHalf, -a+2*legRadiusHalf, 0
	gosub 50
	del 2

	!-----Brace----
	rotx 90
	roty 90
	add legPozY/4+leg_d/2, stepHeight-leg_d/4, leg_d
	cylind a-leg_d*2, leg_d/4
	del 1

	add b-leg_d/2, stepHeight-leg_d/4, leg_d
	cylind a-leg_d*2, leg_d/4
	del 1

	add b-mattresHead, ZZYZX-mattressHeight-legRadiusHalf, leg_d
	cylind a-leg_d*2, leg_d/4
	del 1

	add b-leg_d/2-0.15, ZZYZX-mattressHeight-legRadiusHalf-0.1, leg_d
	cylind a-leg_d*2, leg_d/4
	del 1

	add b-leg_d/2, ZZYZX-mattressHeight-legRadiusHalf-0.1, leg_d
	cylind a-leg_d*2, leg_d/4
	del 1

	add uFrameRadius+legPozY/4+0.05, ZZYZX-mattressHeight-legRadiusHalf, leg_d
	cylind a-leg_d*2, leg_d/4
	del 2
	if not(bStep) then
		add a/2, stepHeight-leg_d/4, -b+leg_d/2
		cylind b-legPozY/4-leg_d, leg_d/4
		del 1
	else
		add leg_d, stepHeight-leg_d/4, -b+leg_d/2
		!add a-leg_d, stepHeight-leg_d/4, -b+leg_d/2		! Other Side
		cylind b-legPozY/4-leg_d, leg_d/4
		del 1

		rotx -90
		add leg_d*1.5-stepWidth, stepOffset, stepHeight-leg_d/2
		!add a-leg_d, stepOffset, stepHeight-leg_d/2		! Other Side
		gosub 20
		del 2
	endif
	del 1

	!-----Leg-----
	if gs_detlevel_3D_m = 2 then	! Detailed
		material gs_leg_mat
		roty 90
		rotz 90
		addy ZZYZX-0.70
		prism 5, 0.005,
		  -0.002,         0.45,
		  -0.002,         0.44,
		   0.028,         0.41,
			0.05,         0.41,
			0.05,         0.45
		addz 0.005
		prism_ 6, a-0.01,
			0.05,         0.45, 15,
			0.05,        0.415, 15,
			0.028,       0.415, 15,
		   0.003,         0.44, 15,
		   0.003,         0.45, 7,
			0.05,         0.45, 1
		addz a-0.01
		prism 5, 0.005,
		  -0.002,         0.45,
		  -0.002,         0.44,
		   0.028,         0.41,
			0.05,         0.41,
			0.05,         0.45
		del 3
		del 2
	endif

	addz ZZYZX-0.70
	rotx 90
	roty 90
	addz a/2-leg_d/4
	if gs_positionBed_m = 1 then	! Sit Up
		prism 4, leg_d/2,
			reducedLength+1.7688731719, 0.420932986239,
			reducedLength+1.677289458868, 0.7627280564176,
			reducedLength+1.696607975394, 0.7679044373196,
			reducedLength+1.788191688425, 0.426109367141
	else							! Reclined
		prism 4, leg_d/2,
			reducedLength+1.822014941988, 0.3208473767421,
			reducedLength+1.692130389044,         0.65,
			reducedLength+1.713631192606,         0.65,
			reducedLength+1.840618897827, 0.3281885501568
	endif
	del 4

	add leg_d-0.005, b-0.15-leg_d*0.75, ZZYZX-mattressHeight-(0.1+leg_d/2+leg_d/4)
	prism 4, 0.1+leg_d/2,
		0, 0,
		0, 0.02,
		0.005, 0.02,
		0.005, 0
	add leg_d,0,leg_d/8
	prism 4, leg_d/4,
		0, 0.01,
		0, 0.16,
		0.005, 0.16,
		0.005, 0.01
	del 2
	add a-leg_d, b-0.15-leg_d*0.75, ZZYZX-mattressHeight-(0.1+leg_d/2+leg_d/4)
	prism 4, 0.1+leg_d/2,
		0, 0,
		0, 0.02,
		0.005, 0.02,
		0.005, 0
	add -leg_d,0,leg_d/8
	prism 4, leg_d/4,
		0, 0.01,
		0, 0.16,
		0.005, 0.16,
		0.005, 0.01
	del 2

	! ----- Mattress -----
	mattressReduc = 0
	mattressR = 0.04
	mattressLenght = b-mattresHead-2*mattressReduc
	mattressWidht = a - 2*mattressReduc
	add mattressReduc, mattressReduc, ZZYZX
	gosub 100
	del 1

	if gs_positionBed_m = 1 then	! Sit Up
		mattressLenght = mattresHead-mattressReduc
		add mattressReduc, b-mattresHead-mattressReduc, ZZYZX
		rotx bedPositionAngle
		gosub 100
		del 2
	else							! Reclined
		mattressLenght = mattresHead-mattressReduc
		add mattressReduc, b-mattresHead, ZZYZX
		gosub 100
		del 1
	endif
endif



END
! ======================================================================
! SubRoutines
! ======================================================================
!-----Rectangular Leg-----
10:
	if gs_detlevel_3D_m = 2 then	! Detailed
		material "builtInMatBlackPlastic"
		prism 4, legWidthHalf*2,
			-legWidthHalf-legRubberOversize, -legWidthHalf-legRubberOversize,
			-legWidthHalf-legRubberOversize,  legWidthHalf+legRubberOversize,
			 legWidthHalf+legRubberOversize,  legWidthHalf+legRubberOversize,
			 legWidthHalf+legRubberOversize, -legWidthHalf-legRubberOversize

		addz legWidthHalf*2
		material gs_leg_mat
		prism 4, legh-legWidthHalf*2,
			-legWidthHalf, -legWidthHalf,
			-legWidthHalf,  legWidthHalf,
			 legWidthHalf,  legWidthHalf,
			 legWidthHalf, -legWidthHalf
		del 1
	else
		material gs_leg_mat
		prism 4, legh,
			-legWidthHalf, -legWidthHalf,
			-legWidthHalf,  legWidthHalf,
			 legWidthHalf,  legWidthHalf,
			 legWidthHalf, -legWidthHalf
	endif
	return


!-----Step-----
20:
	material gs_leg_mat
	if gs_leg_type_m = 1 then		! Rectangular
		prism_ 10, leg_w/2,
			0, 0, 15,
			stepWidth, 0, 15,
			stepWidth, stepLenght, 15,
			0, stepLenght, 15,
			0, 0, -1,
			leg_w/2, leg_w/2, 15,
			stepWidth-leg_w/2, leg_w/2, 15,
			stepWidth-leg_w/2, stepLenght-leg_w/2, 15,
			leg_w/2, stepLenght-leg_w/2, 15,
			leg_w/2, leg_w/2, -1

		addz -stepHeight+leg_w/2
		legWidthHalf = leg_w/4
		legh = stepHeight-leg_w/2
		add legWidthHalf, legWidthHalf, 0
		gosub 10
		addx stepWidth-leg_w/2
		gosub 10
		del 2
		add legWidthHalf, stepLenght-legWidthHalf, 0
		gosub 10
		addx stepWidth-leg_w/2
		gosub 10
		del 2

		del 1
	else	! Round
		uFrameRadius = 0.05
		legRadiusHalf = leg_d/4
		uFrameHeight = stepHeight-legRadiusHalf
		uFrameLenght = stepWidth-legRadiusHalf
		add 0, 0.02+legRadiusHalf, -stepHeight+leg_d/2
		gosub 50
		del 1
		add 0, stepLenght-0.02-legRadiusHalf, -stepHeight+leg_d/2
		gosub 50
		del 1
	endif

	material gs_step_mat
	if gs_leg_type_m = 1 then addz leg_w/2
	if gs_leg_type_m = 2 then addz leg_d/2
	prism 4, 0.01,
		0, 0,
		stepWidth, 0,
		stepWidth, stepLenght,
		0, stepLenght
	del 1

	material "builtInMatBlackPlastic"
	addz -stepHeight+leg_w/2
	if gs_leg_type_m = 1 then		! Rectangular
		hotspot 0, 0, 0, unID : unID = unID + 1
		hotspot stepWidth, 0, 0, unID : unID = unID + 1
		hotspot stepWidth, stepLenght, 0, unID : unID = unID + 1
		hotspot 0, stepLenght, 0, unID : unID = unID + 1
	else		! Round
		hotspot 0, 0.02+legRadiusHalf, 0, unID : unID = unID + 1
		hotspot stepWidth, 0.02+legRadiusHalf, 0, unID : unID = unID + 1
		hotspot stepWidth, stepLenght-0.02-legRadiusHalf, 0, unID : unID = unID + 1
		hotspot 0, stepLenght-0.02-legRadiusHalf, 0, unID : unID = unID + 1
	endif

	hotspot 0, 0, stepHeight, unID : unID = unID + 1
	hotspot stepWidth, 0, stepHeight, unID : unID = unID + 1
	hotspot stepWidth, stepLenght, stepHeight, unID : unID = unID + 1
	hotspot 0, stepLenght, stepHeight, unID : unID = unID + 1
	del 1

	return


! ----- U frame -----
50:
	rotx 90
	material gs_leg_mat

	iRes = int(gs_resol/2)
	iAlpha = 90/iRes

	put  0, -1, 0, 0
	put  0, 0.01, 0, 0
	put  0, uFrameHeight-uFrameRadius ,   0,0
	for i=1 to iRes-1
		put uFrameRadius-cos(i*iAlpha)*uFrameRadius, uFrameHeight-uFrameRadius+sin(i*iAlpha)*uFrameRadius, 0, 0
	next i
	put uFrameRadius , uFrameHeight,0,0
	put uFrameLenght-uFrameRadius , uFrameHeight,0,0
	for i=1 to iRes-1
		put uFrameLenght-uFrameRadius+sin(i*iAlpha)*uFrameRadius, uFrameHeight-uFrameRadius+cos(i*iAlpha)*uFrameRadius, 0,0
	next i
	put uFrameLenght, uFrameHeight-uFrameRadius, 0, 0
	put uFrameLenght, 0.01, 0, 0
	put uFrameLenght, -1, 0, 0

	tube 2, NSP/4, 1+2+16+32+128,
		0, 0, 901,
		legRadiusHalf , 360, 4001,
		GET (NSP)

	del 1
	material "builtInMatBlackPlastic"
	mul -1, 1, 1
	cylind 0.01, legRadiusHalf
	del 1
	addx uFrameLenght
	cylind 0.01, legRadiusHalf
	del 1
	material gs_leg_mat
	return


! ----- Mattress -----
100:
	addz -mattressHeight
	material gs_matrace_mat
	if gs_detlevel_3D_m = 2 and GLOB_CONTEXT<>4 and GLOB_CONTEXT<>24  then	! Detailed
		cprism_ gs_matrace_mat, gs_leg_mat, gs_matrace_mat,
			5, mattressHeight,
			mattressR, mattressR, 8+64,
			mattressWidht -mattressR, mattressR, 8+64,
			mattressWidht -mattressR, mattressLenght-mattressR, 8+64,
			mattressR, mattressLenght-mattressR, 8+64,
			mattressR, mattressR, -1

		put mattressR, 0, 0
		put 0, 0, 1
		put 0, mattressHeight-mattressR, 1
		profilNumber = 3
		iRes = int(gs_resol/2)
		iAlpha = 90/iRes
		for i=1 to iRes-1
			put mattressR-cos(i*iAlpha)*mattressR, mattressHeight-mattressR+sin(i*iAlpha)*mattressR, 1
			profilNumber = profilNumber + 1
		next i
		put mattressR, mattressHeight, 0
		profilNumber = profilNumber + 1

		if gs_leg_type_m = 1 then		! Rectangular
			put  mattressWidht , mattressLenght, 0, 0
			put  0, mattressLenght, 0, 0
			put  0, 0, 0, 0
			put  mattressWidht , 0, 0, 0
			put  mattressWidht , mattressLenght, 0, 0
			put  0, mattressLenght, 0, 0
			put  0, 0, 0, 0
		else							! Round
			iRes = int(gs_resol/2)
			iAlpha = 90/iRes

			put  0, 0, 0, 0
			put  mattressR, 0, 0, 0
			put  mattressWidht -mattressR, 0, 0, 0
			for i=1 to iRes-1
				put  mattressWidht -mattressR+sin(i*iAlpha)*mattressR, mattressR-cos(i*iAlpha)*mattressR, 0, 0
			next i
			put mattressWidht , mattressR, 0, 0
			put mattressWidht , mattressLenght-mattressR, 0, 0
			for i=1 to iRes-1
				put mattressWidht -mattressR+cos(i*iAlpha)*mattressR, mattressLenght-mattressR+sin(i*iAlpha)*mattressR, 0, 0
			next i
			put mattressWidht -mattressR, mattressLenght, 0, 0
			put mattressR, mattressLenght, 0, 0
			for i=1 to iRes-1
				put  mattressR-sin(i*iAlpha)*mattressR, mattressLenght-mattressR+cos(i*iAlpha)*mattressR, 0, 0
			next i
			put 0, mattressLenght-mattressR, 0, 0
			put 0, mattressR, 0, 0
			for i=1 to iRes-1
				put  mattressR-cos(i*iAlpha)*mattressR, mattressR-sin(i*iAlpha)*mattressR, 0, 0
			next i
			put  mattressR, 0,   0, 0
			put  mattressWidht -mattressR, 0,   0, 0
		endif

		tube profilNumber, (NSP-3*profilNumber)/4, 1+2+128,
			GET (NSP)

	else		! Simple
		prism 4, mattressHeight,
			0, 0,
			mattressWidht , 0,
			mattressWidht , mattressLenght,
			0, mattressLenght

	endif

	del 1
	return


!-----HeadFrame-----
110:
	material gs_leg_mat
	add -mattressReduc, 0, -leg_w-mattressHeight
	prism_ 15, leg_w,
		leg_w, leg_w/4, 15,
		a-leg_w, leg_w/4, 15,
		a-leg_w, mattresHead-leg_w, 15,
		leg_w, mattresHead-leg_w, 15,
		leg_w, leg_w/4, -1,
		2*leg_w, leg_w*1.125, 15,
		a-leg_w*2, leg_w*1.125, 15,
		a-leg_w*2, mattresHead/2-leg_w/2, 15,
		2*leg_w, mattresHead/2-leg_w/2, 15,
		2*leg_w, leg_w*1.125, -1,
		2*leg_w, mattresHead/2+leg_w/2, 15,
		a-leg_w*2, mattresHead/2+leg_w/2, 15,
		a-leg_w*2, mattresHead-leg_w*2, 15,
		2*leg_w, mattresHead-leg_w*2, 15,
		2*leg_w, mattresHead/2+leg_w/2, -1
	del 1
	return


!-----Curbly-----
120:
	if gs_detlevel_3D_m = 2 then	! Detailed
		material gs_leg_mat
		addz -leg_w/2
		prism 4, leg_w,
			0,         0.61,
			reducedLength+1.25, 0.5006391705926,
			reducedLength+1.24651377029, 0.4607913826689,
			-0.003486229709906, 0.5701522120763
		del 1

		addz -leg_w/4
		if gs_positionBed_m = 1 then	! Sit Up
			prism 4, leg_w/2,
				reducedLength+1.214428480247, 0.4850399754247,
				reducedLength+1.225751407244, 0.4685538805498,
				reducedLength+1.569958969636, 0.7049614185566,
				reducedLength+1.564468225504, 0.7254531546286
		else							! Reclined
			prism 4, leg_w/2,
				reducedLength+1.223072894824, 0.4693308865328,
				reducedLength+1.21520548623, 0.4877184878453,
				reducedLength+1.573165249759, 0.6408768945226,
				reducedLength+1.573165249759, 0.6191231054774
		endif
		del 1

		roty -90
		rotz -90
		add -0.5901, 0, 0.0017
		roty -5
		prism_ 2, 0.01,
		   0, 0,      979,
		   0.005, 360,   4079
		addz 0.01
		prism_ 9, 0.005,
			0.07070889466518, 0.009974841770859,      79,
			-3.72965547335E-016,        0.015,      79,
			-3.545341104028E-016,            0,    979,
			 0,          180,   4079,
			-3.361026734705E-016,       -0.015,      79,
			0.07070889466518, -0.009974841770859,      79,
			0.07, 2.676930536017E-017,    979,
			0, 171.86984636,   4079,
			0.07070889466518, 0.009974841770859,      1
		addz 0.005
		prism_ 2, 0.05,
		   0.07, 0,      979,
		   0.0075, 360,   4079
		del 6
	endif

	return

