
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Медицинской Кушетки`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalSpacepage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	x1 = 0
	x2 = 126
	x3 = 228
	x4 = 387
	x5 = 444 - 103
	_dy = 24

	_yCurr = pageStartY

	ui_outfield `Положение Кровати`, x1, _yCurr + _yOffset, x2 - 2, _outfHeight

	ui_infield{3} "gs_positionBed_m", x2, _yCurr, x3 - x2 - 13, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", stBedPosition[1], 1,
			"", stBedPosition[2], 2 ui_tooltip `Положение Кровати`

	ui_infield{3} "bStep", x3, _yCurr, 444 - x3, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Табуретка`, 0,
					"", `Табуретка`, 1

	_yCurr = _yCurr + _dy
	if gs_leg_type_m = 1 then
		ui_outfield `Толщина Ножек`, x1, _yCurr + _yOffset, x2 - 2, _outfHeight
		ui_infield "leg_w", x2, _yCurr, x3 - x2 - 13, _infHeight
	else
		ui_outfield `Диаметр Ножек`, x1, _yCurr + _yOffset, x2 - 2, _outfHeight
		ui_infield "leg_d", x2, _yCurr, x3 - x2 - 13, _infHeight
	endif

	_yCurr = _yCurr + _dy
	ui_infield{3} "gs_leg_type_m", 0, _yCurr, 444, 265 - _yCurr,
		1, 1, 2, 1,	! ui_ExaminationTable_StyleAndDimensions.png
		100, 110,
		90, 90,
		1, `с Прямоугольными Ножками`, 	1,
		2, `с Круглыми Ножками`, 		2 ui_tooltip `Тип Ножек`

return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 2,	! SYMBOLCLAS_BED
													bUniformFill		= bUniformFill,
													iSymbolType			= iSymbolType,
													stSymbolTypes		= stSymbolType2D,
													bExaminationTable	= 1,
													iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
 	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_leg_mat"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "gs_matrace_mat"
		SurfItemID = SurfItemID + 1

		if bStep then
			surfacesArray[SurfItemID]	= "gs_step_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"MinimalSpacepage":
! ------------------------------------------------------------------------------

	call "fa_minimalspace" parameters MSType = 2		!MINSPACE_SIMPLE_BED

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return

