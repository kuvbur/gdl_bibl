
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

values "a" range [0.2, ]
values "b" range [1.0, ]
values "zzyzx" range [0.4, ]
values "leg_d" range (0, ]
values "leg_w" range (0, ]

! ----- General Settings -----
values "gs_positionBed" stBedPosition		! Sit Up(1), Reclined(2)
values "gs_positionBed_m" 1, 2
if GLOB_MODPAR_NAME = "gs_positionBed" then
	gs_positionBed_m = 1
	for i = 1 to vardim1(stBedPosition)
		if gs_positionBed = stBedPosition[i] then	gs_positionBed_m = i
	next i
	parameters gs_positionBed_m = gs_positionBed_m
else
	gs_positionBed_m = max(min(gs_positionBed_m,2),1)
	parameters gs_positionBed = stBedPosition[gs_positionBed_m]
endif

! ----- Leg -----
values "gs_leg_type" stLegTypes		! Rectangular(1), Round(2)
values "gs_leg_type_m" 1, 2
if GLOB_MODPAR_NAME = "gs_leg_type" then
	gs_leg_type_m = 1
	for i = 1 to vardim1(stLegTypes)
		if gs_leg_type = stLegTypes[i] then	gs_leg_type_m = i
	next i
	parameters gs_leg_type_m = gs_leg_type_m
else
	gs_leg_type_m = max(min(gs_leg_type_m,2),1)
	parameters gs_leg_type = stLegTypes[gs_leg_type_m]
endif

if gs_leg_type_m=1 then
	hideparameter "leg_d"
else
	hideparameter "leg_w"
endif

if not(bStep) then
	lock "gs_step_mat"
	hideparameter "gs_step_mat"
endif

! ----- 2D Symbol Type -----
values "styp" stSymbolType2D		! Type 1(1),Type 2(2),Type 3(3),Type 4(4)
values "iSymbolType" 1, 2, 3, 4
if GLOB_MODPAR_NAME = "styp" then
	iSymbolType = 1
	for i = 1 to vardim1(stSymbolType2D)
		if styp = stSymbolType2D[i] then	iSymbolType = i
	next i
	parameters iSymbolType = iSymbolType
else
	iSymbolType = max(min(iSymbolType,4),1)
	parameters styp = stSymbolType2D[iSymbolType]
endif

! ----- 3D Representation -----
values "gs_detlevel_3D" stDetlevel3D		! Off(0), Simple(1), Detailed(2)
values "gs_detlevel_3D_m" 0, 1, 2
if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	for i = 1 to vardim1(stDetlevel3D)
		if gs_detlevel_3D = stDetlevel3D[i] then	gs_detlevel_3D_m = 3-i
	next i
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D_m = max(min(gs_detlevel_3D_m,2),0)
	parameters gs_detlevel_3D = stDetlevel3D[3-gs_detlevel_3D_m]
endif

values "gs_resol" range [3, )

if gs_detlevel_3D_m = 1 then	! Simple
	lock "gs_resol"
else
	if gs_detlevel_3D_m = 0 then ! Off
		lock "gs_resol", "gs_shadow"
	endif
endif

! ----- Minimal Space -----
values "MSFront" range [0, ]
values "MSSideAdd" range [0, ]
values "MSLength" range [0, ]
values "MSLengthAdd" range [0, ]
values "MSSide" range [0, ]
values "MSSide2" range [0, ]

!!! ******************************** Different fills
if GLOB_MODPAR_NAME = "fillFor" then
	iFillFor = 1
	for i = 1 to vardim1(stFillFor)
		if fillFor = stFillFor[i] then	iFillFor = i
	next i
	parameters iFillFor = iFillFor
else
	iFillFor = max(min(iFillFor,4),1)
	parameters fillFor = stFillFor[iFillFor]
endif

if fillPenUniform=0 then parameters fillPenUniform=19
if fillPenStep=0 then parameters fillPenStep=19
if fillPenPillow=0 then parameters fillPenPillow=19
if fillPenBlanket=0 then parameters fillPenBlanket=19
if gs_fill_pen=0 then parameters gs_fill_pen=19

if iSymbolType = 1 or iSymbolType = 2 then
	if iSymbolType = 1 then
		if bStep then
			values "fillFor" stFillFor
			values "iFillFor" 1, 2, 3, 4
		else
			values "fillFor" stFillFor[1], stFillFor[2], stFillFor[3]
			values "iFillFor" 1, 2, 3
		endif
	endif
	if iSymbolType = 2 then
		if bStep then
			values "fillFor" stFillFor[1], stFillFor[2], stFillFor[4]
			values "iFillFor" 1, 2, 4
		else
			values "fillFor" stFillFor[1], stFillFor[2]
			values "iFillFor" 1, 2
		endif
		hideparameter "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
		lock 	      "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	endif
else
	if bStep then
		values "fillFor" stFillFor[1], stFillFor[4]
		values "iFillFor" 1, 4
	else
		values "fillFor" stFillFor[1]
		values "iFillFor" 1
	endif
	lock		  "fillFor", "iFillFor"
	hideparameter "fillFor", "iFillFor"
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
endif

if not(bStep) then
	hideparameter "fillTypeStep", "fillPenStep", "backPenStep"
	lock "fillTypeStep", "fillPenStep", "backPenStep"
endif

if bUniformFill then
	lock "fillFor", "iFillFor"
	hideparameter "fillFor", "iFillFor"
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", "gs_fill_type", "gs_fill_pen", "gs_back_pen", "fillTypeStep", "fillPenStep", "backPenStep"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", "gs_fill_type", "gs_fill_pen", "gs_back_pen", "fillTypeStep", "fillPenStep", "backPenStep"
else
	hideparameter "fillTypeUniform", "fillPenUniform", "backPenUniform"
	lock 	      "fillTypeUniform", "fillPenUniform", "backPenUniform"
endif
