
CALL "Resize_A_B_ZZYZX" Parameters ALL

unID = 1
pen gs_cont_pen

hotspot2 a/2, b, unID : unID = unID + 1
hotspot2 a/2, b/2, unID : unID = unID + 1

fill gs_fill_type
drawindex 20
poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
	0, 0, 1,
	a, 0, 1,
	a, b, 1,
	0, b, 1,
	0, 0, -1

if iDetlevel2D = 2 then	! Detailed
	if bTable then				! Overbed Table
		overbedTableRadius = 0.05
		overbedTableplusSize = 0.05
		add2 overbedTableplusSize, reducerB/2
		poly2_b 14, 1+2+4+64, gs_fill_pen, gs_back_pen,
			overbedTableRadius, 0, 1,
			realA-overbedTableRadius, 0, 1,
			realA-overbedTableRadius, overbedTableRadius, 901,
			realA, overbedTableRadius, 3001,
			realA, b-reducerB-overbedTableRadius, 1,
			realA-overbedTableRadius, b-reducerB-overbedTableRadius, 901,
			realA-overbedTableRadius, b-reducerB, 3001,
			overbedTableRadius, b-reducerB, 1,
			overbedTableRadius, b-reducerB-overbedTableRadius, 901,
			0, b-reducerB-overbedTableRadius, 3001,
			0, overbedTableRadius, 1,
			overbedTableRadius, overbedTableRadius, 901,
			overbedTableRadius, 0, 3001,
			overbedTableRadius, 0, -1
		del 1
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			realA+overbedTableplusSize, b/2+0.02, 1,
			realA+overbedTableplusSize, b/2-0.02, 1,
			realA+overbedTableplusSize+0.04, b/2-0.02, 1,
			realA+overbedTableplusSize+0.04, b/2+0.02, 1,
			realA+overbedTableplusSize, b/2+0.02, -1
	else
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			tableOffsetx, tableOffsety, 1,
			realA-tableOffsetx, tableOffsety, 1,
			realA-tableOffsetx, b-tableOffsety, 1,
			tableOffsetx, b-tableOffsety, 1,
			tableOffsetx, tableOffsety, -1
	endif
endif


! === Minimal Space ====================================== [
if MSFront> EPS then
	reqBMinSpace = 0
	drawindex 10
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showBMinSpace", reqBMinSpace)
	if success > 0 and reqBMinSpace then
		call "fa_minimalspace" parameters all 	MSType 	= 1,
												unID 	= 3000,
							returned_parameters reqBMinSpace,
												MSFillType,
												MSFillPen,
												MSFillBackPen,
												MSContLineType,
												MSContLinePen
	endif
	if reqBMinSpace <> 0 then
		fill MSFillType
		line_type MSContLineType
		pen MSContLinePen
		unID = 3050
		if bDouble and MSFront > EPS then		! Two-sided
			poly2_b 5, 2+4+64,MSFillPen,MSFillBackPen,
					0,      b,      1,
					a,      b,      1,
					a,      b+MSFront,    1,
					0,      b+MSFront,    1,
					0,      b,      1

			hotspot2  0,	 b+MSFront,	unID : unID = unID + 1
			hotspot2  a,	 b+MSFront,	unID : unID = unID + 1

			line2 0, b, 0, b+MSFront
			line2 a, b, a, b+MSFront
			line2 0, b+MSFront, a, b+MSFront
		endif
	endif
endif

! === Minimal Space ====================================== ]
