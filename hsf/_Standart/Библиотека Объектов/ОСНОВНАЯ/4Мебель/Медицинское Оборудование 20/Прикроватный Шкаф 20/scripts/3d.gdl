

if not(gs_shadow) then shadow off
if iDetlevel3D = 2 then	! Detailed
	resol gs_resol
else
	if iDetlevel3D = 1 then ! Simple
		resol 3
		gs_resol = 6
	else
		END	! Off
	endif
endif

pen gs_cont_pen


!-----Definitions-----
unID = 1
wheelPoz = 0.035
bottomHeight = 0.035													! Cube Bottom Height
shelvHeight = 0.015														! One shelves Height
shelvTotalHeight = baseHeight-drawerHeigth-2*panelWidth-bottomHeight	! Shelves summa height
bottleRackColumn = 0.025												! Bottle Rack column
overbedTableOffset = 0.02												! Overbed table


!-----Hotspot-----
call "Resize_A_B_ZZYZX" Parameters ALL

hotspot a/2, b, ZZYZX, unID : unID = unID + 1
hotspot a/2, b, 0, unID 	: unID = unID + 1


!-----Leg-----
gosub 25
!-----Base-----
gosub 30
!-----Top-----
gosub 60
!-----Overbed Table-----
if bTable then
	gosub 210
endif
!-----Bottle Rack-----
if bBottle then
	rotz 90
	add b/2, -realA, ZZYZX-topHeight
	gosub 200
	del 2
endif


END
! ======================================================================
! SubRoutines
! ======================================================================
!-----Wheel-----
10:
	if iDetlevel3D = 1 then resol 5		! Simple
	rotz wheelAlpha
	add -0.0225, -0.03, 0.03
	rotx -17
	addx -0.0025
	material gs_wheel_mat
	revolve 2,360,1+2+128,
	0,0.03,1,
	0.05,0.03,1
	del 1

	material gs_leg_mat
		revolve 5,214,1+2+16+32+128,
		0,0.035,1,
		0.045,0.035,1,
		0.045,0.031,1,
		0,0.031,1,
		0,0.035,-1
	del 2

	addz 0.040
	cylind 0.03,0.005
	del 2
	if iDetlevel3D = 1 then resol 3		! Simple
	return


!-----LegTop-----
15:
	rotx 90
	roty 90
	add 0, 0.005, -0.02
	prism_ 13, 0.04,
		b/2-0.02, 0.055, 15,
		b/2-0.02, 0.075, 15,
		b/2-0.02-eps*2, 0.075, 79,
		0.085-sin(13)*0.005, 0.095-cos(13)*0.005, 1079,
		0.085, 0.095, 915,
		0, -77, 4015,
		b-0.08, 0.095, 79,
		b-0.085, 0.095, 979,
		0, -77, 4079,
		b/2+0.02+2*eps, 0.075, 1079,
		b/2+0.02, 0.075, 15,
		b/2+0.02, 0.055, 15,
		b/2-0.02, 0.055, -1
	del 3
	return


!-----Leg-----
20:
	addz wheelHeight
	material gs_leg_mat

	if iDetlevel3D = 1 then resol 6		! Simple
	addy wheelPoz
	cylind 0.01, wheelPoz
	addy b-2*wheelPoz
	cylind 0.01, wheelPoz
	del 2
	if iDetlevel3D = 1 then resol 3		! Simple

	if iDetlevel3D = 2 then		! Detailed
		muly -1
		addy -b
		cutpolya 8, 2, 0,
			-0.05, wheelpoz+0.02, 69,
			-0.02, wheelpoz+0.02, 71,
			0, wheelpoz+0.02, 905,
			0, 180, 4005,
			0.05, wheelpoz+0.02, 5,
			0.05, 0, 5,
			-0.05, 0, 5,
			-0.05, wheelpoz+0.02, 1
		del 2

		cutpolya 8, 2, 0,
			-0.05, wheelpoz+0.02, 69,
			-0.02, wheelpoz+0.02, 71,
			0, wheelpoz+0.02, 905,
			0, 180, 4005,
			0.05, wheelpoz+0.02, 5,
			0.05, 0, 5,
			-0.05, 0, 5,
			-0.05, wheelpoz+0.02, 1
	endif

	rotx 90
	roty 90
	add 0, 0.005, -0.02
	prism_ 14, 0.04,
		b/2-0.02, 0.055, 15,
		b/2-0.02, 0.035, 15,
		b/2-0.02-eps*2, 0.035, 79,
		wheelpoz, 0.015, 1079,
		wheelpoz+0.0038, 0.0017, 979,
		0, 81.1852, 4079,
		wheelpoz-0.01, 0, 15,
		b-wheelpoz+0.01, 0, 79,
		b-wheelpoz-0.0038, 0.0017, 979,
		0, 81.1852, 4079,
		b/2+0.02+2*eps, 0.035, 1079,
		b/2+0.02, 0.035, 15,
		b/2+0.02, 0.055, 15,
		b/2-0.02, 0.055, 1
	del 3

	if iDetlevel3D = 2 then		! Detailed
		cutend
		cutend
	endif

	if not(bLegOversize) then
		addz 0.06
		prism 4, 0.025,
			-0.02, b/2-0.02,
			 0.02, b/2-0.02,
			 0.02, b/2+0.02,
			-0.02, b/2+0.02
		del 1
		addx- 0.1
		rotx 90
		roty 90
		addy 0.044
		cutpolya 4, 2, 0,
			b/2-0.015, 0, 7,
			b/2-0.015, 0.035, 7,
			b/2+0.015, 0.035, 7,
			b/2+0.015, 0, 7
		del 3
		gosub 15
		cutend
		del 1
	else
		gosub 15
	endif

	del 1
	addy wheelPoz+0.025
	wheelAlpha=0
	gosub 10
	addy b-2*(wheelPoz+0.025)
	wheelAlpha=180
	gosub 10
	del 2
	return


!-----LegControl-----
25:
	addx wheelPoz
	bLegOversize=1
	gosub 20
	del 1

	addx a-wheelPoz
	if bTable then
		bLegOversize = 0
	else
		bLegOversize = 1
	endif
	gosub 20
	del 1

	material gs_leg_mat
	addz wheelHeight+0.044
	prism 4, 0.035,
		wheelPoz +0.02, b/2-0.015,
		a-wheelPoz-0.02, b/2-0.015,
		a-wheelPoz-0.02, b/2+0.015,
		wheelPoz +0.02, b/2+0.015
	del 1
	return


!-----Base-----
30:
	material gs_cabinet_mat
	add 0, reducerB/2, 0.17

	group "base"
		rotx 90
		if bDoor then
			cutpolya 5, 1, -b+reducerB+panelWidth-bDouble,
				panelDepth, -bottomHeight, 7,
				realA-panelDepth, -bottomHeight, 7,
				realA-panelDepth, baseHeight-panelWidth,7,
				panelDepth, baseHeight-panelWidth, 7,
				panelDepth, -bottomHeight, 1
		else
			cutpolya 5, 1, -b+reducerB+panelWidth-bDouble,
				panelDepth, -bottomHeight, 7,
				realA-panelDepth, -bottomHeight, 7,
				realA-panelDepth, baseHeight-2*panelWidth-drawerHeigth,7,
				panelDepth, baseHeight-2*panelWidth-drawerHeigth, 7,
				panelDepth, -bottomHeight, 1

			cutpolya 5, 1, -b+reducerB+panelWidth-bDouble,
				panelDepth, baseHeight-panelWidth-drawerHeigth, 7,
				realA-panelDepth, baseHeight-panelWidth-drawerHeigth, 7,
				realA-panelDepth, baseHeight-panelWidth,7,
				panelDepth, baseHeight-panelWidth, 7,
				panelDepth, baseHeight-panelWidth-drawerHeigth, 7
		endif
		del 1

		if iDetlevel3D = 2 then		! Detailed
			tableRadius = 0.01
			prism_  14, baseHeight,
				tableRadius, 0, 79,
				realA-tableRadius, 0, 79,
				realA-tableRadius, tableRadius, 979,
				realA, tableRadius, 3079,
				realA, b-reducerB-tableRadius, 79,
				realA-tableRadius, b-reducerB-tableRadius, 979,
				realA-tableRadius, b-reducerB, 3079,
				tableRadius, b-reducerB, 79,
				tableRadius, b-reducerB-tableRadius, 979,
				0, b-reducerB-tableRadius, 3079,
				0, tableRadius, 79,
				tableRadius, tableRadius, 979,
				tableRadius, 0, 3079,
				tableRadius, 0, -1
		else			! Simple
			block realA, b-reducerB, baseHeight
		endif

		if not(bDoor) then cutend
		cutend
	endgroup

	group "cutter"
		gosub 100
	endgroup

	placegroup subgroup ("base", "cutter")
	killgroup "base"
	killgroup "cutter"

	material gs_knob_mat
	rotz -90
	roty -90
	add ZZYZX-0.3, 0.01, reducerB-towelRadius
	cylind b-2*reducerB, 0.005
	del 3


	material gs_cabinet_mat

	if not(bDouble) then
		if bDoor then
			lin_ panelDepth, b-reducerB-panelWidth, 0, panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth
			lin_ realA-panelDepth, b-reducerB-panelWidth, 0, realA-panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth
			lin_ realA-panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth, panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth
		else
			lin_ panelDepth, b-reducerB-panelWidth, 0, panelDepth, b-reducerB-panelWidth, baseHeight-2*panelWidth-drawerHeigth
			lin_ realA-panelDepth, b-reducerB-panelWidth, 0, realA-panelDepth, b-reducerB-panelWidth, baseHeight-2*panelWidth-drawerHeigth
			lin_ realA-panelDepth, b-reducerB-panelWidth, baseHeight-2*panelWidth-drawerHeigth, panelDepth, b-reducerB-panelWidth, baseHeight-2*panelWidth-drawerHeigth

			lin_ realA-panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth-drawerHeigth, panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth-drawerHeigth

			lin_ panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth, panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth-drawerHeigth
			lin_ realA-panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth, realA-panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth-drawerHeigth
			lin_ realA-panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth, panelDepth, b-reducerB-panelWidth, baseHeight-panelWidth
		endif
	endif

	! Bottom shelves
	prism 4, bottomHeight,
		panelDepth, panelWidth+panelEps,
		realA-panelDepth, panelWidth+panelEps,
		realA-panelDepth, b-reducerB-panelWidth-panelEps*bDouble,
		panelDepth,  b-reducerB-panelWidth-panelEps*bDouble

	! Shelves
	if not(bDoor) then
		sHeigth = (shelvTotalHeight-(iShelv*shelvHeight)) / (iShelv+1)
		for i= 1 to iShelv
			addz bottomHeight + sHeigth * i
			prism 4, shelvHeight,
				panelDepth, panelWidth+panelEps,
				realA-panelDepth, panelWidth+panelEps,
				realA-panelDepth, b-reducerB-panelWidth-panelEps*bDouble,
				panelDepth,  b-reducerB-panelWidth-panelEps*bDouble
			del 1
			addz shelvHeight
		next i
		del iShelv
	else
		addz baseHeight-2*panelWidth-drawerHeigth
		prism 4, panelWidth,
			panelDepth, panelWidth+panelEps,
			realA-panelDepth, panelWidth+panelEps,
			realA-panelDepth, b-reducerB-panelWidth-panelEps*bDouble,
			panelDepth,  b-reducerB-panelWidth-panelEps*bDouble
		del 1

		drawerH = drawerHeightBottom
		add panelDepth+panelEps, 0, panelEps
		doorMat = gs_door_mat
		knobNumber = 1
		gosub 50

		material gs_knob_mat
		addz drawerH+panelEps
		prism 4, 0.007,
			0, 0,
			drawerWidth, 0,
			drawerWidth, panelWidth+panelEps,
			0, panelWidth+panelEps
		del 1

		if bDouble then
			muly -1
			addy -b+reducerB
			gosub 50

			material gs_knob_mat
			addz drawerH+panelEps
			prism 4, 0.007,
				0, 0,
				drawerWidth, 0,
				drawerWidth, panelWidth+panelEps,
				0, panelWidth+panelEps
			del 3
		endif
		del 1
	endif

	! Drawer
	if bDrawer then
		drawerH = drawerHeightTop
		add panelDepth+panelEps, 0, baseHeight-panelWidth-drawerHeigth+panelEps
		doorMat = gs_drawer_mat
		knobNumber = 2
		gosub 50
		if bDouble then
			muly -1
			addy -b+reducerB
			gosub 50
			del 2
		endif
		del 1
	endif

	del 1
	return


!-----Front-----
50:
	material doorMat
	prism 4, drawerH,
		0, 0,
		drawerWidth, 0,
		drawerWidth, panelWidth,
		0, panelWidth

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4
	body 1

	! --- Knob ---
	if bKnob then
		add drawerWidth/2, 0, drawerH - drawerHeigth/2-panelEps + 0.005

		hotspot  knob_pl[knobNumber][1], 0,  0, unID, knob_pl[knobNumber][2], 1+128					: unID=unID+1
		hotspot  knob_pl[knobNumber][1], 0, -1, unID, knob_pl[knobNumber][2], 3 					: unID=unID+1
		hotspot  knob_pl[knobNumber][1], 0, knob_pl[knobNumber][2], unID, knob_pl[knobNumber][2],2 	: unID=unID+1

		hotspot  0, 0, knob_pl[knobNumber][2], unID, knob_pl[knobNumber][1], 1+128					: unID=unID+1
		hotspot -1, 0, knob_pl[knobNumber][2], unID, knob_pl[knobNumber][1], 3 						: unID=unID+1
		hotspot  knob_pl[knobNumber][1], 0, knob_pl[knobNumber][2], unID, knob_pl[knobNumber][1],2 	: unID=unID+1

		add	knob_pl[knobNumber][1], 0, knob_pl[knobNumber][2]

		_knob_size=0.2

		if iKnobStyle = 4 or iKnobStyle = 5 or iKnobStyle = 6 or iKnobStyle = 7 then
			roty 90
			addz _knob_size/2
			nTrans = 2
		endif

		if iKnobStyle = 8 then
			roty 270
			addz _knob_size/2
			nTrans = nTrans + 2
		endif

		CALL "fa_knobtyp" PARAMETERS 	gs_detlevel_3D_m	= iDetlevel3D,
										gs_knob_type_m		= iKnobStyle,
										gs_resol			= gs_resol,
										gs_shadow			= gs_shadow,
										gs_cont_pen			= gs_cont_pen,
										gs_knob_mat			= gs_knob_mat,
										knob_size			= _knob_size

		del nTrans

		del 2
	endif
	return


!-----Top-----
60:
	material gs_cabinet_mat
	tableB = reducerB - 0.02
	add 0, tableB/2, ZZYZX-topHeight
	if iDetlevel3D = 2 then		! Detailed
		tableRadius = 0.01
		beta = atn(0.005/tableOffsetx)

		cutpolya 4, 2, 0,
			tableOffsetx, tableOffsety, 7,
			realA-tableOffsetx, tableOffsety, 7,
			realA-tableOffsetx, b-tableB-tableOffsety, 7,
			tableOffsetx, b-tableB-tableOffsety, 7

		fprism_  gs_cabinet_mat, gs_cabinet_mat, gs_cabinet_mat, gs_cabinet_mat,
			14, topHeight, beta, 0.005,
			tableRadius, 0, 79,
			realA-tableRadius, 0, 79,
			realA-tableRadius, tableRadius, 979,
			realA, tableRadius, 3079,
			realA, b-tableB-tableRadius, 79,
			realA-tableRadius, b-tableB-tableRadius, 979,
			realA-tableRadius, b-tableB, 3079,
			tableRadius, b-tableB, 79,
			tableRadius, b-tableB-tableRadius, 979,
			0, b-tableB-tableRadius, 3079,
			0, tableRadius, 79,
			tableRadius, tableRadius, 979,
			tableRadius, 0, 3079,
			tableRadius, 0, -1

		cutend
		prism_  5, topHeight-0.005,
			tableOffsetx, tableOffsety, 15,
			realA-tableOffsetx, tableOffsety, 15,
			realA-tableOffsetx, b-tableB-tableOffsety, 15,
			tableOffsetx, b-tableB-tableOffsety, 15,
			tableOffsetx, tableOffsety, -1

	else			! Simple
		block realA, b-tableB, topHeight
	endif
	del 1
	return


!-----Towels-----
100:
	towelRadius = 0.02
	towelDepth = 0.04
	towelHeigth = baseHeight-2*panelWidth
	uLenght = b-2*reducerB-2*towelDepth-2*towelRadius
	iRes = int(gs_resol/2)
	iAlpha = 90/iRes

	rotz -90
	add -uLenght/2-(b-reducerB)/2, -towelRadius, panelWidth

	put  0, -1, 0, 0
	put  0, 0, 0, 0
	for i=1 to iRes-1
		put towelRadius-cos(i*iAlpha)*towelRadius, sin(i*iAlpha)*towelRadius, 0, 0
	next i
	put towelRadius , towelRadius,0,0
	put uLenght-towelRadius , towelRadius,0,0
	for i=1 to iRes-1
		put uLenght-towelRadius+sin(i*iAlpha)*towelRadius, cos(i*iAlpha)*towelRadius, 0,0
	next i
	put uLenght, 0, 0, 0
	put uLenght, -1, 0, 0

	tube 6, NSP/4, 1+2+16+32+128,
		0, 0, 1,
		0, towelDepth, 901,
		towelDepth,	towelDepth, 3001,
		towelDepth, towelHeigth-towelDepth, 1,
		0, towelHeigth-towelDepth, 901,
		0 , towelHeigth, 3001,
		GET (NSP)

	del 2
	return


!-----Bottle Rack-----
200:
	material gs_bottlerack_mat
	rotx 90
	if iDetlevel3D = 2 then	! Detailed
		toler 0.001
	else
		toler 0.01 ! Simple
	endif

	!board
	prism_ 7, 0.002,
	   -0.18,            0,      15,
	   -0.18, -0.3916364982832,      79,
		   0,      -0.2075,    979,
		   0, 88.69832493657,   4015,
		0.18, -0.3916364982832,      15,
		0.18,            0,      15,
	   -0.18,            0,      1

	group "baseVer2"
		addx -0.03
		cutpolya 21, 2, 0,
			0.075, -9.184850993605E-018,      7,
			0.165, -2.020667218593E-017,      7,
			0.1650053657122, -0.211338878857,      5,
			0.1176453524063, -0.211338878857,    905,
			0, -37.92697759127,   4005,
			0.1550026828561, -0.24044902695,      5,
			0.1923600133059, -0.2695591750431,   905,
			0, 37.92697759127,   4005,
			0.145, -0.2695591750431,      5,
			0.145, -0.3450054295484,      5,
			0.1200026828561, -0.3450027147747,    905,
			0, -179.9875550562,   4005,
			0.09500536571218, -0.3450054295494,      5,
			0.09500536571218, -0.2695591750431,      5,
			0.04764535240626, -0.2695591750431,    905,
			0, 37.92697759127,   4005,
			0.08500268285609, -0.24044902695,      5,
			0.1223600133059, -0.211338878857,    905,
			0, -37.92697759127,   4005,
			0.075, -0.211338878857,      7,
			0.075, -9.184850993605E-018,      1
		del 1

		mulx -1
		addx -0.03
		cutpolya 21, 2, 0,
			0.075, -9.184850993605E-018,      7,
			0.165, -2.020667218593E-017,      7,
			0.1650053657122, -0.211338878857,      5,
			0.1176453524063, -0.211338878857,    905,
			0, -37.92697759127,   4005,
			0.1550026828561, -0.24044902695,      5,
			0.1923600133059, -0.2695591750431,   905,
			0, 37.92697759127,   4005,
			0.145, -0.2695591750431,      5,
			0.145, -0.3450054295484,      5,
			0.1200026828561, -0.3450027147747,    905,
			0, -179.9875550562,   4005,
			0.09500536571218, -0.3450054295494,      5,
			0.09500536571218, -0.2695591750431,      5,
			0.04764535240626, -0.2695591750431,    905,
			0, 37.92697759127,   4005,
			0.08500268285609, -0.24044902695,      5,
			0.1223600133059, -0.211338878857,    905,
			0, -37.92697759127,   4005,
			0.075, -0.211338878857,      7,
			0.075, -9.184850993605E-018,      1
		del 2

		rotx 90
		cutpolya 4, 2, 0,
			-bottleRackColumn, 0.01, 7,
			 bottleRackColumn, 0.01, 7,
			 bottleRackColumn, 1, 7,
			-bottleRackColumn, 1, 7
		del 1

		addz 0.002
		prism_ 7, 0.05,
		   -0.16, 3.469446951954E-018,      15,
		   -0.16, -0.3830170931847,      79,
			   0,      -0.2075,    979,
			   0, 84.70410953288,   4079,
			0.16, -0.3830170931847,      79,
			0.16, 3.469446951954E-018,      15,
		   -0.16, 3.469446951954E-018,      1
		del 1

		cutend
		cutend
		cutend
	endgroup

	group "glassRack2"
		addz 0.002
		addx -0.03
		prism_ 5, 0.04,
			0.075, -0.39,      15,
			0.075, 0,      15,
			0.1650053657122, 0,      15,
			0.1650053657122, -0.39,      15,
			0.075, -0.39,      1
		del 1
		mulx -1
		addx -0.03
		prism_ 5, 0.04,
			0.075, -0.39,      15,
			0.075, 0,      15,
			0.1650053657122, 0,      15,
			0.1650053657122, -0.39,      15,
			0.075, -0.39,      1
		del 2
	endgroup

	group "depthCutter"
		rotx 90
		roty 90
		add 0, 0.002, -0.5
		prism_   9, 1,
			-0.445, 0,      77,
			 0,            0,      79,
			-0.05, 0,    979,
			 0,           90,   4079,
			-0.05,         0.05,      77,
			-0.395,         0.05,      77,
			-0.395, 	0,    979,
			 0,           90,   4079,
			-0.445, 0,      1
		del 3
	endgroup

	base2 = isectgroup ("baseVer2", "depthCutter")
	placegroup subgroup (base2, "glassRack2")
	killgroup base2
	killgroup "glassRack2"
	killgroup "baseVer2"
	killgroup "depthCutter"
	del 1
	return


!-----Overhead Table-----
210:
	material gs_cabinet_mat
	addz 0.17
	prism 4, ZZYZX-0.17+0.02,
		realA+0.02, b/2-0.02,
		realA+0.06, b/2-0.02,
		realA+0.06, b/2+0.02,
		realA+0.02, b/2+0.02
	del 1

	add realA+0.04, b/2, ZZYZX+0.02
	cylind 0.03, 0.02
	addz 0.03
	cylind tableHeight-0.09-ZZYZX, 0.01
	del 1
	addz tableHeight-0.06-ZZYZX
	cylind 0.025, 0.015
	del 2


	roty 90
	add -0.25, b/2, realA
	cylind 0.02, 0.015
	del 2

	material gs_table_mat
	overbedTabRadius = 0.05
	flangeWidth = 0.005
	overbedTableplusSize = 0.06
	add	0, reducerB/2, tableHeight-0.02
	if iDetlevel3D = 2 then		! Detailed
		hprism_  gs_table_mat, gs_table_mat, gs_table_mat, gs_table_mat,
			28, 0.02, 0, flangeWidth/2-EPS, 1,
			overbedTabRadius, 0, 79,
			realA-overbedTabRadius+overbedTableplusSize, 0, 79,
			realA-overbedTabRadius+overbedTableplusSize, overbedTabRadius, 979,
			realA+overbedTableplusSize, overbedTabRadius, 3079,
			realA+overbedTableplusSize, b-reducerB-overbedTabRadius, 79,
			realA-overbedTabRadius+overbedTableplusSize, b-reducerB-overbedTabRadius, 979,
			realA-overbedTabRadius+overbedTableplusSize, b-reducerB, 3079,
			overbedTabRadius, b-reducerB, 79,
			overbedTabRadius, b-reducerB-overbedTabRadius, 979,
			0, b-reducerB-overbedTabRadius, 3079,
			0, overbedTabRadius, 79,
			overbedTabRadius, overbedTabRadius, 979,
			overbedTabRadius, 0, 3079,
			overbedTabRadius, 0, -1,

			overbedTabRadius, flangeWidth, 79,
			realA-overbedTabRadius+overbedTableplusSize, flangeWidth, 79,
			realA-overbedTabRadius+overbedTableplusSize, overbedTabRadius, 979,
			realA+overbedTableplusSize-flangeWidth, overbedTabRadius, 3079,
			realA+overbedTableplusSize-flangeWidth, b-reducerB-overbedTabRadius, 79,
			realA-overbedTabRadius+overbedTableplusSize, b-reducerB-overbedTabRadius, 979,
			realA-overbedTabRadius+overbedTableplusSize, b-reducerB-flangeWidth, 3079,
			overbedTabRadius, b-reducerB-flangeWidth, 79,
			overbedTabRadius, b-reducerB-overbedTabRadius, 979,
			flangeWidth, b-reducerB-overbedTabRadius, 3079,
			flangeWidth, overbedTabRadius, 79,
			overbedTabRadius, overbedTabRadius, 979,
			overbedTabRadius, flangeWidth, 3079,
			overbedTabRadius, flangeWidth, -1

		addz 0.01
		prism_  14, 0.005,
			overbedTabRadius, flangeWidth, 79,
			realA-overbedTabRadius+overbedTableplusSize, flangeWidth, 79,
			realA-overbedTabRadius+overbedTableplusSize, overbedTabRadius, 979,
			realA+overbedTableplusSize-flangeWidth, overbedTabRadius, 3079,
			realA+overbedTableplusSize-flangeWidth, b-reducerB-overbedTabRadius, 79,
			realA-overbedTabRadius+overbedTableplusSize, b-reducerB-overbedTabRadius, 979,
			realA-overbedTabRadius+overbedTableplusSize, b-reducerB-flangeWidth, 3079,
			overbedTabRadius, b-reducerB-flangeWidth, 79,
			overbedTabRadius, b-reducerB-overbedTabRadius, 979,
			flangeWidth, b-reducerB-overbedTabRadius, 3079,
			flangeWidth, overbedTabRadius, 79,
			overbedTabRadius, overbedTabRadius, 979,
			overbedTabRadius, flangeWidth, 3079,
			overbedTabRadius, flangeWidth, -1
		del 1

		material gs_cabinet_mat
		addz 0.005
		wO = 0.01
		iRes = gs_resol
		iAlpha = 90/iRes

		put  wO, 1, 0, 0
		put  wO, overbedTabRadius, 0, 0
		for i=1 to iRes-1
			put overbedTabRadius-cos(i*iAlpha)*(overbedTabRadius-wO), overbedTabRadius-sin(i*iAlpha)*(overbedTabRadius-wO), 0, 0
		next i
		put overbedTabRadius , wO,0,0
		put realA+overbedTableplusSize-overbedTabRadius , wO, 0, 0
		for i=1 to iRes-1
			put realA+overbedTableplusSize-overbedTabRadius+sin(i*iAlpha)*(overbedTabRadius-wO), overbedTabRadius-cos(i*iAlpha)*(overbedTabRadius-wO), 0,0
		next i
		put realA+overbedTableplusSize-wO, overbedTabRadius, 0, 0
		put realA+overbedTableplusSize-wO, b-reducerB-overbedTabRadius, 0, 0
		for i=1 to iRes-1
			put realA+overbedTableplusSize-overbedTabRadius+cos(i*iAlpha)*(overbedTabRadius-wO), b-reducerB-overbedTabRadius+sin(i*iAlpha)*(overbedTabRadius-wO), 0,0
		next i
		put realA+overbedTableplusSize-overbedTabRadius, b-reducerB-wO, 0, 0
		put overbedTabRadius, b-reducerB-wO, 0, 0
		for i=1 to iRes-1
			put overbedTabRadius-sin(i*iAlpha)*(overbedTabRadius-wO), b-reducerB-overbedTabRadius+cos(i*iAlpha)*(overbedTabRadius-wO), 0, 0
		next i
		put wO, b-reducerB-overbedTabRadius, 0, 0
		put wO, overbedTabRadius, 0, 0
		put wO, -1, 0, 0

		tube 2, NSP/4, 1+2+16+32+128,
			0, 0, 901,
			0.005 , 360, 4001,
			GET (NSP)
		del 1
	else							! Simple
		prism  4, 0.02,
			0, 0,
			realA+overbedTableplusSize, 0,
			realA+overbedTableplusSize, b-reducerB,
			0, b-reducerB
	endif
	del 1

	hotspot realA+overbedTableplusSize, b/2, 0, unID, tableHeight,1+128 		: unID=unID+1
	hotspot realA+overbedTableplusSize, b/2, -1, unID, tableHeight,3 			: unID=unID+1
	hotspot realA+overbedTableplusSize, b/2, tableHeight, unID, tableHeight,2 	: unID=unID+1

	hotspot 0, reducerB/2, tableHeight, unID : unID = unID + 1
	hotspot realA+overbedTableplusSize, reducerB/2, tableHeight, unID : unID = unID + 1
	hotspot realA+overbedTableplusSize, b-reducerB/2, tableHeight, unID : unID = unID + 1
	hotspot 0, b-reducerB/2, tableHeight, unID : unID = unID + 1

	return
