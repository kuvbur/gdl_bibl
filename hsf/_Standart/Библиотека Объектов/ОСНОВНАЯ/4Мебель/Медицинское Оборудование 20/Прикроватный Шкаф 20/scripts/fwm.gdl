
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "ACA8B2C2-91B4-4456-8580-D56018DF4ABA"
	_startID	= "8A59FDCF-7BC9-4B9A-8576-1527E6A2C88F"
	_endID		= "0E0A44E8-53EF-43B7-83DF-DA381E4858AC"
gosub "newparameters_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! newparameters
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		
		bHas =	DELETED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m) |\
				DELETED_PAR_VALUE("gs_knob_type",	_gs_knob_type)
		
		! If Knob Style is None, than Knob is turned off.
		if _gs_knob_type_m = 0 then
			bKnob = 0
		else
			bKnob = 1
			iKnobStyle = _gs_knob_type_m
		endif
		
		parameters	iKnobStyle	= iKnobStyle,
					bKnob		= bKnob
				
		bHas =	DELETED_PAR_VALUE("gs_detlevel_3D", _gs_detlevel_3D) |\
				DELETED_PAR_VALUE("gs_detlevel_3D_m", _gs_detlevel_3D_m)
		
		if _gs_detlevel_3D_m = 2 then	! Detailed
			iDetlevel3D = 2
		else
			if _gs_detlevel_3D_m = 1 then	! Simple
				iDetlevel3D = 1
			else
				iDetlevel3D = 0	! Off
			endif
		endif
		
		parameters iDetlevel3D = iDetlevel3D
		
		bHas =	DELETED_PAR_VALUE("gs_detlevel_2D", _gs_detlevel_2D) |\
				DELETED_PAR_VALUE("gs_detlevel_2D_m", _gs_detlevel_2D_m)
		
		if _gs_detlevel_2D_m = 2 then	! Detailed
			iDetlevel2D = 2
		else	! Simple
			iDetlevel2D = 1
		endif
		
		parameters iDetlevel2D = iDetlevel2D


		bHas =	STORED_PAR_VALUE ("bTable", _bTable) |\
				STORED_PAR_VALUE ("bDrawer", _bDrawer) |\
				STORED_PAR_VALUE ("bDoor", _bDoor) |\
				STORED_PAR_VALUE ("bBottle", _bBottle)
				
		if _bTable = 0 and _bDrawer = 0 and _bDoor = 0 and _bBottle = 0 then
			iCabinetStyles = 1			! Type 1
		else
			if _bTable = 1 and _bDrawer = 0 and _bDoor = 1 and _bBottle = 0  then
				iCabinetStyles = 2		! Type 2
			else
				if _bTable = 1 and _bDrawer = 1 and _bDoor = 1 and _bBottle = 1  then
					iCabinetStyles = 3	! Type 3
				else
					iCabinetStyles = 4	! Custom
				endif
			endif
		endif
	
		parameters iCabinetStyles = iCabinetStyles
		
		actualGuid = _endID
	endif
return

