
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Прикроватного Шкафа`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacespage"

ui_page TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalSpacepage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37
	x1 = 0
	x2 = 230
	x3 = 353
	x4 = 387
	x5 = 444 - 103
	_dy = 24

	_yCurr = pageStartY
	ui_style 0, 1
	ui_outfield `Преднастроенные Параметры`, 	x1, _yCurr + _yOffset, x2 - 2, _outfHeight
	ui_outfield `Стиль`, 	x2, _yCurr + _yOffset, 444 - x2, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + _dy
	ui_infield{3} "iCabinetStyles", 0, _yCurr, 222, 265 - _yCurr,
		1, 2, 4, 1,	! ui_BedsideCabinet_StyleAndDimensions.png
		100, 110,
		90, 90,
		1, `Тип 1`, 1,
		2, `Тип 2`, 2,
		3, `Тип 3`, 3,
		4, `Спец.`, 4

	_yCurr = pageStartY + _dy + 8
	ui_infield{3} "bDouble", x2, _yCurr, 444 - x2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Двусторонний`, 0,
					"", `Двусторонний`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bTable", x2, _yCurr, 444 - x2 - 92, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Поворотный Стол`, 0,
					"", `Поворотный Стол`, 1

	ui_infield "tableHeight", x3, _yCurr, 444 - x3, _infHeight ui_tooltip `Высота Поворотного Стола`

	_yCurr = _yCurr + _dy
	ui_infield{3} "bBottle", x2, _yCurr, 444 - x2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Держатель Бутылок`, 0,
					"", `Держатель Бутылок`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bDrawer", x2, _yCurr, 444 - x2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Ящик`, 0,
					"", `Ящик`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bDoor", x2, _yCurr, 444 - x2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Двери`, 0,
					"", `Двери`, 1

	_yCurr = _yCurr + _dy
	ui_infield "iShelv", x4, _yCurr, 444 - x4, _infHeight
	ui_outfield `Кол-во Полок`, x2, _yCurr + _yOffset, x4 - x2 - 2, _outfHeight, 4 * bDoor

	_yCurr = _yCurr + _dy
	ui_infield{3} "bKnob", x2, _yCurr, x5 - x2 - 2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Ручка`, 0,
					"", `Ручка`, 1

	ui_infield{3} "iKnobStyle", x5, _yCurr, 103, 90,
		2, 3, 12, 3,	! ui_BedsideCabinet_StyleAndDimensions_Knob.png
		90, 90, 90, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,	4,
		5, ``,	5,
		6, ``,	6,
		7, ``,	7,
		8, ``,	8 ui_tooltip `Стиль Ручек`

return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacespage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "iDetlevel2D"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel2D_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "iDetlevel3D"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"
		_idx = _idx + 1

		if iDetlevel3D = 2 then						! Detailed
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
			_idx = _idx + 1
		endif

		if iDetlevel3D <> 0 then						! Off
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
			_idx = _idx + 1
		endif

		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cabinet_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if bTable then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_table_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bDrawer then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_drawer_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bDoor then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_door_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if bKnob and (bDrawer or bDoor) then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_knob_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_leg_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if bBottle then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_bottlerack_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_wheel_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

	
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return

! ------------------------------------------------------------------------------
"MinimalSpacepage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight 	= 19
	_infWidth	= 90
	PictWidth = 95 
	PictHeight = 156
	xPict = 222 - PictWidth / 2
	yPict = 60


	ui_pict 1,	xPict , yPict, PictWidth, PictHeight, 1	! ui_BedsideCab_MS.png

	ui_infield "A",				xPict + 3,				yPict  - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Ширина`
	ui_infield "B",				xPict + PictWidth + 3,	yPict  + 40,				_infWidth,	_infHeight  ui_tooltip `Длина`
	ui_infield "MSFront",		xPict + PictWidth + 3,	yPict  + 110,				_infWidth,	_infHeight  ui_tooltip `Минимальное Пространство Спереди`

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return

