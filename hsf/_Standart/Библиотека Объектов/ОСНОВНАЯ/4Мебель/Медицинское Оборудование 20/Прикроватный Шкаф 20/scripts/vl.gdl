
dim stDetlevel3D[3]
	stDetlevel3D[1] = `Детальный`
	stDetlevel3D[2] = `Простой`
	stDetlevel3D[3] = `Откл.`

dim stDetlevel2D[2]
	stDetlevel2D[1] = `Детальный`
	stDetlevel2D[2] = `Простой`

dim st_knobtypes[8]
	st_knobtypes[1]=`Стиль 1`
	st_knobtypes[2]=`Стиль 2`
	st_knobtypes[3]=`Стиль 3`
	st_knobtypes[4]=`Стиль 4`
	st_knobtypes[5]=`Стиль 5`
	st_knobtypes[6]=`Стиль 6`
	st_knobtypes[7]=`Стиль 7`
	st_knobtypes[8]=`Стиль 8`

hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

values "a" range [0.35, ]
values "b" range [0.35, ]
values "zzyzx" range [0.5, ]
values "tableHeight" range [zzyzx+0.1, ]
values "iShelv" range [1, 5]
values{2} "iCabinetStyles" 	1, `Тип 1`,
							2, `Тип 2`,
							3, `Тип 3`,
							4, `Спец.`

if bDoor then
	lock "iShelv"
	hideparameter "iShelv"
else
	lock "gs_door_mat"
	hideparameter "gs_door_mat"
endif

if not(bDrawer) then
	lock "gs_drawer_mat"
	hideparameter "gs_drawer_mat"
endif

if not(bDrawer) then
	lock "gs_drawer_mat"
	hideparameter "gs_drawer_mat"
endif

if not(bTable) then
	lock "gs_table_mat", "tableHeight"
	hideparameter "gs_table_mat", "tableHeight"
endif

if not(bBottle) then
	lock "gs_bottlerack_mat"
	hideparameter "gs_bottlerack_mat"
endif

! ----- Knob -----
values{2} "iKnobStyle" 	1,	st_knobtypes[1],
						2,	st_knobtypes[2],
						3,	st_knobtypes[3],
						4,	st_knobtypes[4],
						5,	st_knobtypes[5],
						6,	st_knobtypes[6],
						7,	st_knobtypes[7],
						8,	st_knobtypes[8]

if not(bKnob) or (not(bDoor) and not(bDrawer))then
	if not(bDoor) and not(bDrawer) then
		lock "bKnob"
		hideparameter "bKnob"
	endif
	if not(bDoor) then
		lock "gs_door_mat"
		hideparameter "gs_door_mat"
	endif
	if not(bDrawer) then
		lock "gs_drawer_mat"
		hideparameter "gs_drawer_mat"
	endif
	lock "gs_knob_mat", "iKnobStyle"
	hideparameter "gs_knob_mat", "iKnobStyle"
endif

knobSizeBig=0.07
knobSizeSmall=0.02
dim knobOffset[8][2]
knobOffset[1][1] = knobSizeSmall
knobOffset[1][2] = knobSizeSmall
knobOffset[2][1] = knobSizeSmall
knobOffset[2][2] = knobSizeSmall
knobOffset[3][1] = knobSizeSmall
knobOffset[3][2] = knobSizeSmall
knobOffset[4][1] = knobSizeBig
knobOffset[4][2] = knobSizeSmall
knobOffset[5][1] = knobSizeBig*1.5
knobOffset[5][2] = knobSizeSmall
knobOffset[6][1] = knobSizeBig*1.5
knobOffset[6][2] = knobSizeSmall
knobOffset[7][1] = knobSizeBig*1.5
knobOffset[7][2] = knobSizeSmall
knobOffset[8][1] = knobSizeBig*1.5
knobOffset[8][2] = knobSizeSmall

if (GLOB_MODPAR_NAME = "knob_pl" or GLOB_MODPAR_NAME = "iKnobStyle") and iKnobStyle<>0 then
	for knobNumber=1 to 2
		if knob_pl[knobNumber][1] > drawerWidth/2-knobOffset[iKnobStyle][1] then
			parameters knob_pl[knobNumber][1] = drawerWidth/2-knobOffset[iKnobStyle][1]
		endif
		if knob_pl[knobNumber][1] < -drawerWidth/2+knobOffset[iKnobStyle][1] then
			parameters knob_pl[knobNumber][1] = -drawerWidth/2+knobOffset[iKnobStyle][1]
		endif
		if knob_pl[knobNumber][2] > drawerHeightTop/2-knobOffset[iKnobStyle][2] then
			parameters knob_pl[knobNumber][2] = drawerHeightTop/2-knobOffset[iKnobStyle][2]
		endif

		! Door
		if knob_pl[1][2] < -drawerHeightBottom+drawerHeightTop/2+knobOffset[iKnobStyle][2] then
			parameters knob_pl[1][2] = -drawerHeightBottom+drawerHeightTop/2+knobOffset[iKnobStyle][2]
		endif

		! Top Drawer
		if knob_pl[2][2] < -drawerHeightTop/2+knobOffset[iKnobStyle][2] then
			parameters knob_pl[2][2] = -drawerHeightTop/2+knobOffset[iKnobStyle][2]
		endif

	next knobNumber
endif


! ----- 3D Representation -----
values{2} "iDetlevel3D" 	2,	stDetlevel3D[1],
							1,	stDetlevel3D[2],
							0,	stDetlevel3D[3]

values "gs_resol" range [3, )

if iDetlevel3D = 1 then	! Simple
	lock "gs_resol"
else
	if iDetlevel3D = 0 then ! Off
		lock "gs_resol", "gs_shadow"
	endif
endif

! ----- 2D Representation -----
values{2} "iDetlevel2D" 1, stDetlevel2D[2],
						2, stDetlevel2D[1]

! ----- Minimal Space -----
values "MSFront" range [0, ]

! ----- Presets -----
if GLOB_MODPAR_NAME = "iCabinetStyles" then
	if iCabinetStyles = 1 then
		bTable 	= 0
		bDrawer = 0
		bDoor 	= 0
		bBottle = 0
	endif
	if iCabinetStyles = 2 then
		bTable 	= 1
		bDrawer = 0
		bDoor 	= 1
		bBottle = 0
	endif
	if iCabinetStyles = 3 then
		bTable 	= 1
		bDrawer = 1
		bDoor 	= 1
		bBottle = 1
	endif

	parameters 	bTable 	= bTable,
				bDrawer = bDrawer,
				bDoor 	= bDoor,
				bBottle = bBottle
endif

if 	GLOB_MODPAR_NAME = "bTable" |\
	GLOB_MODPAR_NAME = "bDrawer " |\
	GLOB_MODPAR_NAME = "bDoor" |\
	GLOB_MODPAR_NAME = "bBottle" then
	iCabinetStyles = 4
	parameters iCabinetStyles = iCabinetStyles
endif
