
unID = 1
hotspot2 0, 0, unID :unID = unID + 1
hotspot2 A, 0, unID :unID = unID + 1
hotspot2 A, B, unID :unID = unID + 1
hotspot2 0, B, unID :unID = unID + 1
hotspot2 A/2, B/2, unID :unID = unID + 1

pen gs_cont_pen
fill gs_fill_type
poly2_B 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, B, 1,
		A, B, 1,
		A, 0, 1,
		0, 0, 1,
		0, B, -1


! ---------------------------------------------------------------------------------
! letter pad
! ---------------------------------------------------------------------------------

ADD2 0, B
if gs_detlevel_2D_m = 1 then
	ADD2 sdx, -sdy
	rect2 0, 0, 14 * xSmall + 13 * spaceX, - (ySmall + 4 * yNorm + yBottom + 5 * spaceY)
	DEL 1
else
	!detailed
	ADD2 sdx, - sdy
	nButtons = 14						! letter pad 1st row
	gosub "buttonSmalla"
	ADD2 0, -ySmall - spaceY - yNorm
	nButtons = 13						! letter pad 2nd row
	gosub "buttonNormal"
	ADD2 13 * (xNorm + spaceX), 0
	gosub "delTab"
	DEL 1
	ADD2 0, - yNorm - spaceY
	gosub "delTab"						! letter pad 3rd row
	ADD2 (14 * (xSmall + spaceX)- 13 * (xNorm + spaceX)), 0
	nButtons = 12
	gosub "buttonNormal"
	DEL 1
	ADD2 0, -yNorm - spaceY					! letter pad 4th row
	rect2 0, 0, xCaps, yNorm
	ADD2 xCaps + spaceX, 0
	nButtons = 12
	gosub "buttonNormal"
	ADD2 12 * (xNorm + spaceX), 0
	k = 14 * (xSmall + spaceX) - ( 12 * (xNorm + spaceX)) - xCaps - 2 * spaceX
	poly2 7, 1,
			0, 0,
			k, 0,
			k, 2 * yNorm + spaceY,
			k - xNorm, 2 * yNorm + spaceY,
			k - xNorm, yNorm + spaceY,
			0, yNorm + spaceY,
			0, 0
	DEL 6
	ADD2 sdx,  - sdy - ySmall - 4 * yNorm - 4 * spaceY							!letter pad 5th row
	rect2 0, 0, A / 20.47, yNorm
	ADD2 A / 20.47 + spaceX, 0
	nButtons = 11
	gosub "buttonNormal"
	ADD2 11 * (xNorm + spaceX), 0
	m = 14 * (xSmall + spaceX) - (A / 20.47 + spaceX) - 11 * (xNorm + spaceX) - spaceX
	rect2 0, 0, m, yNorm
	DEL 2
	ADD2 0,  - yNorm - 2 * spaceY							!letter pad 6th row
	gosub "control"
	rect2 0, 0, A / 20, yBottom
	ADD2 A / 20 + spaceX, 0
	gosub "control"
	DEL 3
	ADD2 A / 20.47 + 3 * xNorm + 4 * spaceX, 0
	rect2 0, 0, 6 * xNorm + 5 * spaceX, yBottom
	ADD2 6 * xNorm + 6 * spaceX, 0
	gosub "control"
	p = 2 * xNorm + m - 2 * o
	rect2 0, 0, p, yBottom
	ADD2 p + spaceX, 0
	gosub "control"
	DEL 5
endif


! ---------------------------------------------------------------------------------
! positioning pad
! ---------------------------------------------------------------------------------

if gs_detlevel_2D_m = 1 then
	! simple
	ADD2 14 * xSmall + 13 * spaceX + spaceC + sdx, - 3 * yNorm - 4 * spaceY
	rect2 0, 0, 3 * xNorm + 2 * spaceX, 3 * yNorm + 2 * spaceY
	DEL 1
else
	!detailed
	ADD2 14 * (xSmall + spaceX) + spaceC - spaceX, yBottom + 2 * yNorm + 3 * spaceY
	nButtons = 3
	for j = 1 to 2
		gosub "buttonNormal"
		ADD2 0, yNorm + spaceY
	next j
	gosub "buttonSmallb"
	DEL j

endif

! ---------------------------------------------------------------------------------
! arrows
! ---------------------------------------------------------------------------------

if gs_detlevel_2D_m = 1 then
	! simple
	ADD2 14 * xSmall + 13 * spaceX + spaceC + sdx, - (sdy + ySmall + 4 * yNorm + yBottom + 5 * spaceY)
	poly2_B 9, 1, gs_fill_pen, gs_back_pen,
		0, 0, 1,
		0, yNorm + spaceY, 1,
		xNorm, yNorm + spaceY, 1,
		xNorm, 2 * yNorm + spaceY, 1,
		2 * (xNorm + spaceX), 2 * yNorm + spaceY, 1,
		2 * (xNorm + spaceX), yNorm + spaceY, 1,
		3 * xNorm + 2 * spaceX, yNorm + spaceY, 1,
		3 * xNorm + 2 * spaceX, 0, 1,
		0, 0, -1
	DEL 1
else
	! detailed
	ADD2  14 * xSmall + 13 * spaceX + spaceC, 0
	nButtons = 3
	gosub "buttonNormal"
	ADD2 xNorm + spaceX, yNorm + spaceY
	nButtons = 1
	gosub "buttonNormal"
	DEL 2
endif


! ---------------------------------------------------------------------------------
! numpad
! ---------------------------------------------------------------------------------

if gs_detlevel_2D_m = 1 then
	! simple
	ADD2 A - sdx, -sdy
	rect2 0, 0, -4 * xNorm - 3 * spaceX, - (ySmall + 4 * yNorm + yBottom + 5 * spaceY)
	DEL 2
else
	! detailed
	ADD2 A - 3 * spaceX - 4 * xNorm - 2 * sdx , 0
	rect2 0, 0,  2 * xNorm + spaceX, yBottom
	ADD2 2 * xNorm + 2 * spaceX, 0
	rect2 0, 0, xNorm, yBottom
	ADD2 xNorm + spaceX, 0
	rect2 0, 0, xNorm, yBottom + yNorm + spaceY
	DEL 2
	ADD2 0, yBottom + spaceY
	nButtons = 3
	gosub "buttonNormal"
	ADD2 0, yNorm + spaceY
	nButtons = 4
	for j = 1 to 3
		gosub "buttonNormal"
		ADD2 0, yNorm + spaceY
	next j
	gosub "buttonSmallb"
	DEL 9
endif


end


! ---------------------------------------------------------------------------------
! subroutines
! ---------------------------------------------------------------------------------

"buttonNormal":
	for i = 1 to nButtons
		rect2 0, 0,  xNorm, yNorm
		ADD2 xNorm + spaceX, 0
	next i
	del nButtons
return

"buttonSmalla":
	for i = 1 to nButtons
		rect2 0, 0, xSmall, -ySmall
		ADD2 xSmall + spaceX, 0
	next i
	del nButtons
return


"buttonSmallb":
	for i = 1 to nButtons
		rect2 0, 0,  xNorm, ySmall
		ADD2 xNorm + spaceX, 0
	next i
	del nButtons
return


"delTab":										! del and tab
	for i = 1 to nButtons
	rect2 0, 0, (14 * (xSmall + spaceX)- 13 * (xNorm + spaceX)- spaceX), yNorm
next i
return

"control":
	o = (A / 20.47 + 3 * xNorm + spaceX - A / 20) / 2
	rect2 0, 0, o, yBottom
	ADD2 o + spaceX, 0
return
