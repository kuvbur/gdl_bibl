
if gs_detlevel_3D_m = 0 then end


unID = 1
hotspot 0, 				0, 0, unID, copier_width, 1+256 	:unID = unID + 1
hotspot -0.001, 		0, 0, unID, copier_width, 3+128 	:unID = unID + 1
hotspot copier_width, 	0, 0, unID, copier_width, 2 		:unID = unID + 1

hotspot 0, 				copier_depth, 0, unID, copier_width, 1+256 	:unID = unID + 1
hotspot -0.001, 		copier_depth, 0, unID, copier_width, 3+128 	:unID = unID + 1
hotspot copier_width, 	copier_depth, 0, unID, copier_width, 2 		:unID = unID + 1

hotspot 0, 0, 				0, unID, copier_depth, 1+256	:unID = unID + 1
hotspot 0, -0.001, 			0, unID, copier_depth, 3+128 	:unID = unID + 1
hotspot 0, copier_depth, 	0, unID, copier_depth, 2 		:unID = unID + 1

hotspot copier_width, 0, 			0, unID, copier_depth, 1+256	:unID = unID + 1
hotspot copier_width, -0.001, 		0, unID, copier_depth, 3+128 	:unID = unID + 1
hotspot copier_width, copier_depth, 0, unID, copier_depth, 2 		:unID = unID + 1

hotspot 0, 0, 0, 		unID, zzyzx, 1+256 	:unID = unID + 1
hotspot 0, 0, -0.001, 	unID, zzyzx, 3+128 	:unID = unID + 1
hotspot 0, 0, zzyzx, 	unID, zzyzx, 2 		:unID = unID + 1

hotspot copier_width, 0, 0, 		unID, zzyzx, 1+256 	:unID = unID + 1
hotspot copier_width, 0, -0.001, 	unID, zzyzx, 3+128 	:unID = unID + 1
hotspot copier_width, 0, zzyzx, 	unID, zzyzx, 2 		:unID = unID + 1

hotspot copier_width, copier_depth, 0, 		unID, zzyzx, 1+256 	:unID = unID + 1
hotspot copier_width, copier_depth, -0.001, unID, zzyzx, 3+128 	:unID = unID + 1
hotspot copier_width, copier_depth, zzyzx, 	unID, zzyzx, 2 		:unID = unID + 1

hotspot 0, copier_depth, 0, 		unID, zzyzx, 1+256 	:unID = unID + 1
hotspot 0, copier_depth, -0.001, 	unID, zzyzx, 3+128 	:unID = unID + 1
hotspot 0, copier_depth, zzyzx, 	unID, zzyzx, 2 		:unID = unID + 1


xeroxWidth 	= 0.57
xeroxDepth 	= 0.65
xeroxHeight = 1.075
xeroxToeH	= 0.055

addz xeroxToeH
mul mainLength/0.57, B/0.650, (zzyzx-xeroxToeH)/(1.075-xeroxToeH)

material body_mat

drawerFrontWidth 	= xeroxWidth
drawerFrontDepth 	= 0.02
handleWidth 		= 0.32
halfHandleWidth		= handleWidth/2
handleDepth 		= 0.02
handleHeight 		= 0.03

DIM drawerFrontHeight_dim[4]
	drawerFrontHeight_dim[1] = 0.275
	drawerFrontHeight_dim[2] = 0.075
	drawerFrontHeight_dim[3] = 0.09
	drawerFrontHeight_dim[4] = 0.375

drawersHeight = drawerFrontHeight_dim[1]+drawerFrontHeight_dim[2]+drawerFrontHeight_dim[3]+drawerFrontHeight_dim[4]+0.03

DIM handlePos_dim[4]
	handlePos_dim[1] = 0.125+handleHeight
	handlePos_dim[2] = 0.008+handleHeight
	handlePos_dim[3] = 0.008+handleHeight
	handlePos_dim[4] = drawerFrontHeight_dim[4]

resol gs_resol*4
if gs_detlevel_3D_m = 2 then
	group "handle_01"
		for i = 1 to 4
			handlePos		= handlePos_dim[i]
			gosub "drawer handle"
			addz drawerFrontHeight_dim[i]+0.01
		next i
		del 4
	endgroup

	add drawerFrontWidth/2, drawerFrontDepth, drawersHeight
	rotx 90
	cutpolya 7, 2, 0,
		-0.15, 0, 79,
		0, 0.4375, 979,
		0, 37.8492888321, 4079,
		0.15, 0, 79,
		0.15, 0.175, 79,
		-0.15, 0.175, 79,
		-0.15, 0, -1
	del 1
	cutplane -90
	cutplane 0
	mul 1, 0.03/0.21, 0.30/0.21
	sphere 0.19
	del 1
	cutend
	cutend
	cutend
	del 1

	group "drawerfront_01"
		for i = 1 to 4
			if i < 4 then
				drawerFrontHeight	= drawerFrontHeight_dim[i]
			else
				drawerFrontHeight	= drawerFrontHeight_dim[i]-0.0025
			endif
			gosub "drawer front"
			addz drawerFrontHeight_dim[i]+0.01
		next i
		del 4
	endgroup

	result_01 = subgroup("drawerfront_01", "handle_01")
	placegroup result_01
	killgroup "drawerfront_01"
	killgroup "handle_01"
	killgroup result_01
else
	for i = 1 to 4
		drawerFrontHeight	= drawerFrontHeight_dim[i]
		gosub "drawer front"
		addz drawerFrontHeight_dim[i]+0.01
	next i
	del 4
endif

chamfering 	= 0.002
boxSizeX 	= xeroxWidth
boxSizeY 	= xeroxDepth-drawerFrontDepth
boxSizeZ 	= drawerFrontHeight_dim[1]+0.005

addy drawerFrontDepth
gosub "box"

addz boxSizeZ
boxSizeZ 	= drawerFrontHeight_dim[2]+drawerFrontHeight_dim[3]+0.02
gosub "box"

addz boxSizeZ
boxSizeZ 	= drawerFrontHeight_dim[4]+0.005
gosub "box"
del 3



! --- top of mainbox

resol gs_resol*4

addz drawersHeight
addx xeroxWidth/2
cutpolya 13, 2, 0,
	-0.15, 0.13, 79,
	0.05625, 0.065, 979,
	0, 34.98464905022, 4079,
	-0.15, 0, 79,
	0, 1.12, 979,
	0, 15.25629933716, 4079,
	0.15, 0, 79,
	-0.05625, 0.065, 979,
	0, 34.98464905022, 4079,
	0.15, 0.13, 79,
	0, -0.99, 979,
	0, 15.25629933716, 4079,
	-0.15, 0.13, -1
del 1

rotz 90
rotx 90
addz xeroxWidth/2
if gs_detlevel_3D_m = 2 then
	for i = -1 to 1 step 2
		mulz i
		fprism_ body_mat, body_mat, body_mat, body_mat,
				7, xeroxWidth/2, 45, 0.002,
				0, 0, 14,
				0, 0.025, 14,
				0.15, 0.025, 14,
				0.16, 0.085, 14,
				xeroxDepth, 0.085, 14,
				xeroxDepth, 0, 14,
				0, 0, -1
		del 1
	next i
else
	addz -xeroxWidth/2
	prism_ 7, xeroxWidth,
			0, 0, 15,
			0, 0.025, 15,
			0.15, 0.025, 15,
			0.16, 0.085, 15,
			xeroxDepth, 0.085, 15,
			xeroxDepth, 0, 15,
			0, 0, -1
	del 1
endif
del 3
cutend

del 1


! --- display

material panel_mat
add xeroxWidth/2, 0, drawersHeight-0.025

group "panel"
	prism_ 13, 0.05,
		-0.15, 0.13, 79,
		0.05625, 0.065, 979,
		0, 34.98464905022, 4079,
		-0.15, 0, 79,
		0, 1.12, 979,
		0, 15.25629933716, 4079,
		0.15, 0, 79,
		-0.05625, 0.065, 979,
		0, 34.98464905022, 4079,
		0.15, 0.13, 79,
		0, -0.99, 979,
		0, 15.25629933716, 4079,
		-0.15, 0.13, -1
endgroup

group "cutting shape"
	add 0, 0.35, 0.025
	rotx 90
	prism_ 9, 0.50,
		-0.20, 0, 79,
		-0.15, 0, 79,
		0, 0.4375, 979,
		0, 37.8492888321, 4079,
		0.15, 0, 79,
		0.20, 0, 79,
		0.20, -0.175, 79,
		-0.20, -0.175, 79,
		-0.20, 0, -1
	del 2
endgroup

result_02 = subgroup("panel", "cutting shape")
placegroup result_02

del 1


if gs_detlevel_3D_m = 2 then

	! --- screenglass

	material screenglass_mat
	add xeroxWidth/2-0.10, 0.025, drawersHeight+0.025
	block 0.20, 0.09, 0.001
	del 1


	! --- buttons

	add xeroxWidth/2+0.1725, 0.025, drawersHeight+0.025
	material body_mat
	resol gs_resol
	xx = 0
	yy = 0
	for j = 1 to 4
		for i = 1 to 3
			add xx, yy, 0
			cone 0.005, 0.006, 0.005, 90, 90
			del 1
			xx = xx + 0.02
		next i
		yy = yy + 0.02
		xx = 0
	next j

	add 0.065, 0.055, 0
	muly 0.5
	cone 0.005, 0.01, 0.009, 90, 90
	del 2

	add 0.095, 0.055, 0
	muly 0.5
	cone 0.005, 0.01, 0.009, 90, 90
	del 2

	material but_mat
	add 0.08, 0, 0
	muly 0.5
	cone 0.005, 0.02, 0.019, 90, 90
	del 2

	material but_mat2
	add 0.08, 0.03, 0
	muly 0.5
	cone 0.005, 0.01, 0.009, 90, 90
	del 3
endif


material body_mat

add 0, 0.18, drawersHeight + 0.085

width = xeroxDepth - 0.16 - 0.04
group "box01"
	cutplane 180
	rotx 90
	if gs_detlevel_3D_m = 2 then
		addz -width/2
		for i = -1 to 1 step 2
			mulz i
			fprism_ body_mat, body_mat, body_mat, body_mat,
					5, width/2, 45, 0.005,
					0, -0.005, 14,
					0.24, -0.005, 14,
					0.24, 0.09, 14,
					0.01, 0.09, 14,
					0, -0.005, -1
			del 1
		next i
		del 1
	else
		addz -width
		prism_ 5, width,
				0, -0.005, 15,
				0.24, -0.005, 15,
				0.24, 0.09, 15,
				0.01, 0.09, 15,
				0, -0.005, -1
		del 1
	endif
	del 1
	cutend
endgroup

group "box02"
	cutplane 180
	for j = 0 to 1
		add 0.22, (width-0.03)*j, 0
		rotx 90
		if gs_detlevel_3D_m = 2 then
			addz -0.015
			for i = -1 to 1 step 2
				mulz i
				fprism_ body_mat, body_mat, body_mat, body_mat,
						5, 0.015, 45, 0.005,
						0.32, -0.005, 14,
						0.32, 0.02, 14,
						0, 0.02, 14,
						0, -0.005, 14,
						0.32, -0.005, -1
				del 1
			next i
			del 1
		else
			addz -0.03
			prism_ 5, 0.03,
					0.32, -0.005, 15,
					0.32, 0.02, 15,
					0, 0.02, 15,
					0, -0.005, 15,
					0.32, -0.005, -1
			del 1
		endif
		del 2
	next j
	cutend
endgroup

result_03 = addgroup("box01", "box02")
placegroup result_03

killgroup "box01"
killgroup "box02"
killgroup result_03

ptray_ang = 12
frameHeight = 0.12
add 0.24, 0.065, 0
bFrame = 0
gosub "paper tray"
del 2


! --- paper tray

if not(bFinisher1) then
	add 0.57, 0.195, 0.51
	bFrame = 1
	gosub "paper tray"
	del 1
endif

add 0, 0.195, 0.65
mulx -1
bFrame = 1
gosub "small tray"
del 2
del 3

fronty = 0.02
backy = 0.02
length = mainLength
gosub "legs"


! ------------------
! --- Finisher 1 ---
! ------------------

if bFinisher1 then
	hotspot mainLength+finisher1Length, 0, 0, 		unID :unID = unID + 1
	hotspot mainLength+finisher1Length, B, 0, 		unID :unID = unID + 1

	material body_mat
	add mainLength, 0, xeroxToeH
	mul 1, B/0.650, (zzyzx-xeroxToeH)/(1.075-xeroxToeH)
	height = 0.845
	if gs_detlevel_3D_m = 2 then
		addz height/2
		for i = -1 to 1 step 2
			mulz i
			fprism_ body_mat, body_mat, body_mat, body_mat,
					13, height/2, 45, 0.0025,
					0.65, 0.003, 14,
					0.647, 0, 14,
					0.215, 0, 14,
					0.21, 0.003, 14,
					0.21, 0.07, 14,
					0.2, 0.08, 14,
					0.003, 0.08, 14,
					0, 0.083, 14,
					0, 0.647, 14,
					0.003, 0.65, 14,
					0.647, 0.65, 14,
					0.65, 0.647, 14,
					0.65, 0.003, -1
			del 1
		next i
		del 1
	else
		prism_ 7, height,
			0.65, 0, 15,
			0.21, 0, 15,
			0.21, 0.08, 15,
			0, 0.08, 15,
			0, 0.65, 15,
			0.65, 0.65, 15,
			0.65, 0, -1
	endif

	addz height
	group "box201"
		width = 0.15
		addy width
		rotx 90
		if gs_detlevel_3D_m = 2 then
			addz width/2
			for i = -1 to 1 step 2
				mulz i
				fprism_ body_mat, body_mat, body_mat, body_mat,
						7, width/2, 45, 0.0025,
						0.42, 0, 14,
						0.65, 0, 14,
						0.65, 0.1173216013874, 14,
						0.6473387290641, 0.1203024171518, 14,
						0.43, 0.145, 14,
						0.42, 0.145, 14,
						0.42, 0, -1
				del 1
			next i
			del 1
		else
			prism_ 6, width,
				0.42, 0, 15,
				0.65, 0, 15,
				0.65, 0.12, 15,
				0.43, 0.145, 15,
				0.42, 0.145, 15,
				0.42, 0, -1
		endif
		del 2

		width = 0.10
		addy 0.65
		rotx 90
		if gs_detlevel_3D_m = 2 then
			addz width/2
			for i = -1 to 1 step 2
				mulz i
				fprism_ body_mat, body_mat, body_mat, body_mat,
						7, width/2, 45, 0.0025,
						0.42, 0, 14,
						0.65, 0, 14,
						0.65, 0.1173216013874, 14,
						0.6473387290641, 0.1203024171518, 14,
						0.43, 0.145, 14,
						0.42, 0.145, 14,
						0.42, 0, -1
				del 1
			next i
			del 1
		else
			prism_ 6, width,
				0.42, 0, 15,
				0.65, 0, 15,
				0.65, 0.12, 15,
				0.43, 0.145, 15,
				0.42, 0.145, 15,
				0.42, 0, -1
		endif
		del 2

		width = 0.57
		addy 0.08+width
		rotx 90
		if gs_detlevel_3D_m = 2 then
			addz width/2
			for i = -1 to 1 step 2
				mulz i
				fprism_ body_mat, body_mat, body_mat, body_mat,
						5, width/2, 45, 0.0025,
						0, 0.025, 14,
						0.24, 0.025, 14,
						0.24, 0, 14,
						0, 0, 14,
						0, 0.025, -1
				del 1
			next i
			del 1
		else
			prism_ 5, width,
				0, 0.025, 15,
				0.24, 0.025, 15,
				0.24, 0, 15,
				0, 0, 15,
				0, 0.025, -1
		endif
		del 2
	endgroup


	group "box202"
		width = 0.65
		addy width
		rotx 90
		if gs_detlevel_3D_m = 2 then
			addz width/2
			for i = -1 to 1 step 2
				mulz i
				fprism_ body_mat, body_mat, body_mat, body_mat,
						7, width/2, 45, 0.0025,
						0.21, 0, 14,
						0.43, 0, 14,
						0.43, 0.1425, 14,
						0.4275, 0.145, 14,
						0.41, 0.145, 14,
						0.21, 0.06, 14,
						0.21, 0, -1
				del 1
			next i
			del 1
		else
			prism_ 6, width,
				0.21, 0, 15,
				0.43, 0, 15,
				0.43, 0.145, 15,
				0.41, 0.145, 15,
				0.21, 0.06, 15,
				0.21, 0, -1
		endif
		del 2
	endgroup

	result_04 = addgroup("box201", "box202")
	placegroup result_04

	killgroup "box201"
	killgroup "box202"
	killgroup result_04


	! --- paper tray

	add 0.43, 0.19, 0
	bFrame = 0
	gosub "paper tray"
	del 1
	del 1

	add 0.65, 0.19, 0.70
	frameHeight = 0.12
	bFrame = 1
	gosub "paper tray"
	del 1

	if not(bFinisher2) then
		frameHeight = 0.12
		add 0.65, 0.19, 0.20
		bFrame = 1
		gosub "paper tray"
		del 1
	endif
	del 2


	addx mainLength
	fronty = 0.10
	backy = 0.02
	length = finisher1Length
	gosub "legs"
	del 1
else
	bFinisher2 = 0
endif


! ------------------
! --- Finisher 2 ---
! ------------------

if bFinisher2 then
	hotspot mainLength+finisher1Length+finisher2Length, 0, 0, 		unID :unID = unID + 1
	hotspot mainLength+finisher1Length+finisher2Length, B, 0, 		unID :unID = unID + 1

	material body_mat
	add mainLength+finisher1Length, 0, xeroxToeH
	mul 1, B/0.650, (zzyzx-xeroxToeH)/(1.075-xeroxToeH)
	height = 0.32
	if gs_detlevel_3D_m = 2 then
		addz height/2
		for i = -1 to 1 step 2
			mulz i
			fprism_ body_mat, body_mat, body_mat, body_mat,
					15, height/2, 45, 0.0025,
					0, 0.6475, 14,
					0, 0.0325, 14,
					0.0025, 0.03, 14,
					0.0425, 0.03, 14,
					0.045, 0.0275, 14,
					0.045, 0.002624923872167, 14,
					0.04762180638428, 0.0001278929943551, 14,
					0.2476218063843, 0.009883990555331, 14,
					0.25, 0.01238102143314, 14,
					0.25, 0.6376189785669, 14,
					0.2476218063843, 0.6401160094447, 14,
					0.04506086700485, 0.6499970308778, 14,
					0.04493906062056, 0.65, 14,
					0.0025, 0.65, 14,
					0, 0.6475, -1
			del 1
		next i
		del 1
	else
		prism_ 8, height,
			0, 0.65, 15,
			0.045, 0.65, 15,
			0.25, 0.64, 15,
			0.25, 0.01, 15,
			0.045, 0, 15,
			0.045, 0.03, 15,
			0, 0.03, 15,
			0, 0.65, -1
	endif
	add 0.25, 0.195, 0.20
	bFrame = 1
	gosub "paper tray"
	del 1
	del 2

	addx mainLength+finisher1Length
	fronty = 0.05
	backy = 0.02
	length = finisher2Length
	gosub "legs"
	del 1
endif


! ----------------------------
! --- Feeder High Capacity ---
! ----------------------------

if bFeeder then
	hotspot -feederLength, 0, 0, unID :unID = unID + 1
	hotspot -feederLength, B, 0, unID :unID = unID + 1

	material body_mat
	addz xeroxToeH
	mul 1, B/0.650, (zzyzx-xeroxToeH)/(1.075-xeroxToeH)
	height = 0.55
	if gs_detlevel_3D_m = 2 then
		addz height/2
		for i = -1 to 1 step 2
			mulz i
			fprism_ body_mat, body_mat, body_mat, body_mat,
					17, height/2, 45, 0.0025,
					0, 0.04214479739192, 14,
					0, 0.6475, 14,
					-0.0025, 0.65, 14,
					-0.3725, 0.65, 14,
					-0.375, 0.6475, 14,
					-0.375, 0.1220864408835, 14,
					-0.3729472135955, 0.1196267661083, 14,
					-0.2670527864045, 0.1003732338917, 14,
					-0.265, 0.09791355911648, 14,
					-0.265, 0.0025, 14,
					-0.2625, 0, 14,
					-0.0675, 0, 14,
					-0.065, 0.0025, 14,
					-0.065, 0.02785520260808, 14,
					-0.06288014296064, 0.03032613185221, 14,
					-0.002119857039365, 0.03967386814779, 14,
					0, 0.04214479739192, -1
			del 1
		next i
		del 1
	else
		prism_ 9, height,
			-0.265, 0, 15,
			-0.065, 0, 15,
			-0.065, 0.03, 15,
			0, 0.04, 15,
			0, 0.65, 15,
			-0.375, 0.65, 15,
			-0.375, 0.12, 15,
			-0.265, 0.1, 15,
			-0.265, 0, -1
	endif
	del 2

	addx -feederLength
	fronty = 0.125
	backy = 0.02
	length = feederLength
	gosub "legs"
	del 1
endif
del top

end


"drawer front":
	if gs_detlevel_3D_m = 2 then
		addy drawerFrontDepth
		rotx 90
		if i = 1 then
			ax = (0.28/0.57)*drawerFrontWidth
			ay = (0.215/0.275)*drawerFrontHeight
			fprism_ body_mat, body_mat, body_mat, body_mat,
					5, drawerFrontDepth, 45, 0.002,
					0, 0, 15,
					ax, 0, 15,
					ax, ay, 15,
					0, ay, 15,
					0, 0, -1

			ax2 = ax+0.008
			ay2 = ay+0.008
			fprism_ body_mat, body_mat, body_mat, body_mat,
					7, drawerFrontDepth, 45, 0.002,
					0, ay2, 15,
					ax2, ay2, 15,
					ax2, 0, 15,
					drawerFrontWidth, 0, 15,
					drawerFrontWidth, drawerFrontHeight, 15,
					0, drawerFrontHeight, 15,
					0, ay2, -1
		else
			fprism_ body_mat, body_mat, body_mat, body_mat,
					5, drawerFrontDepth, 45, 0.002,
					0, 0, 15,
					drawerFrontWidth, 0, 15,
					drawerFrontWidth, drawerFrontHeight, 15,
					0, drawerFrontHeight, 15,
					0, 0, -1
		endif
		del 2
	else
		block drawerFrontWidth, drawerFrontDepth, drawerFrontHeight
	endif
return

"drawer handle":
	add drawerFrontWidth/2, 0, handlePos
	cutplane 0
	if i = 4 then
		mulz 0.30/0.21
		addy drawerFrontDepth
		rotx 90
		cone drawerFrontDepth, 0.19, 0.21, 90, 90
		del 3
	else
		mul 0.5, handleDepth/handleWidth, handleHeight/handleWidth
		sphere handleWidth
		del 1
	endif
	cutend
	del 1
return

"box":
	if gs_detlevel_3D_m = 2 then
		addz boxSizeZ/2
		for j = -1 to 1 step 2
			mulz j
			fprism_ body_mat, body_mat, body_mat, body_mat,
					5, boxSizeZ/2, 45, chamfering,
					0, 0, 14,
					boxSizeX, 0, 14,
					boxSizeX, boxSizeY, 14,
					0, boxSizeY, 14,
					0, 0, -1
			del 1
		next j
		del 1
	else
		block boxSizeX, boxSizeY, boxSizeZ
	endif
return

"paper tray":
	if bFrame and gs_detlevel_3D_m = 2 then
		add 0, -0.02, frameHeight-0.02
		roty 90
		prism_ 10, 0.005,
			0, 0, 15,
			frameHeight, 0, 15,
			frameHeight, 0.36, 15,
			0, 0.36, 15,
			0, 0, -1,
			0.02, 0.02, 15,
			frameHeight-0.02, 0.02, 15,
			frameHeight-0.02, 0.34, 15,
			0.02, 0.34, 15,
			0.02, 0.02, -1
		del 2
	endif

	rotz -90
	cutplane 90
	del 1
	if gs_detlevel_3D_m = 2 then
		roty -ptray_ang
		prism_ 7, 0.0075,
			0, 0, 11,
			0.28, 0, 75,
			0.32, 0.04, 1075,
			0.32, 0.28, 75,
			0.28, 0.32, 1075,
			0, 0.32, 11,
			0, 0, -1

		addz 0.0075

		put 0, 0, 14
		put 0.28, 0, 78
		put 0.32, 0.04, 1078
		put 0.32, 0.28, 78
		put 0.28, 0.32, 1078
		put 0, 0.32, 14
		put 0, 0, -1

		ax = 0.05
		ay = 0.05
		wid = 0.22/5
		for i = 1 to 3
			put ax, ay, 15
			put ax, ay+0.04, 15
			put ax+0.22, ay+0.04, 15
			put ax+0.22, ay, 15
			put ax, ay, -1
			ay = ay + wid*2
		next i

		prism_ NSP/3, 0.0075,
			GET(NSP)
		del 2
	else
		roty -ptray_ang
		prism_ 5, 0.015,
			0, 0, 15,
			0.32, 0, 15,
			0.32, 0.32, 15,
			0, 0.32, 15,
			0, 0, -1
		del 1
	endif
	cutend
return

"small tray":
	if bFrame and gs_detlevel_3D_m = 2 then
		add 0, -0.02, frameHeight-0.02
		roty 90
		prism_ 10, 0.005,
			0, 0, 15,
			frameHeight, 0, 15,
			frameHeight, 0.36, 15,
			0, 0.36, 15,
			0, 0, -1,
			0.02, 0.02, 15,
			frameHeight-0.02, 0.02, 15,
			frameHeight-0.02, 0.34, 15,
			0.02, 0.34, 15,
			0.02, 0.02, -1
		del 2
	endif

	rotz -90
	cutplane 90
	del 1
	roty -ptray_ang
	if gs_detlevel_3D_m = 2 then
		prism_ 7, 0.015,
			0, 0, 15,
			0.17, 0, 79,
			0.21, 0.04, 1079,
			0.21, 0.28, 79,
			0.17, 0.32, 1079,
			0, 0.32, 15,
			0, 0, -1
	else
		prism_ 5, 0.015,
			0, 0, 15,
			0.21, 0, 15,
			0.21, 0.32, 15,
			0, 0.32, 15,
			0, 0, -1
	endif
	cutend
return

"legs":
	add 0.0475, fronty, 0.0275
	material leg_mat
	rotx -90
	cylind 0.04, 0.0275
	del 2

	add 0.0475, b-0.04-backy, 0.0275
	material leg_mat
	rotx -90
	cylind 0.04, 0.0275
	del 2

	add length-0.0475, fronty, 0.0275
	material leg_mat
	rotx -90
	cylind 0.04, 0.0275
	del 2

	add length-0.0475, b-0.04-backy, 0.0275
	material leg_mat
	rotx -90
	cylind 0.04, 0.0275
	del 2
return
