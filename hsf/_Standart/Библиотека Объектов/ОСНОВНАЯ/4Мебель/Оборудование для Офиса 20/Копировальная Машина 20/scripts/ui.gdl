
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Копира`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacespage"

ui_page TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalSpacepage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================


! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	x1 = 0
	x2 = 230
	x3 = 353
	x4 = 387
	x5 = 444 - 90
	_dy = 24

	_yCurr = pageStartY
	ui_style 0, 1
	ui_outfield `Преднастроенные Параметры`, 	x1, _yCurr + _yOffset, x2 - 2, _outfHeight
	ui_outfield `Стиль и Размеры`, 	x2, _yCurr + _yOffset, 444 - x2, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + _dy
	ui_infield{3} "iCopierStyle", 0, _yCurr, 222, 265 - _yCurr,
		1, 1, 8, 2,	! ui_Copier_StyleAndDimensions.png
		100, 110,
		90, 90,
		1, stCopierStyleValues[PRESET_COPIER],				1,
		2, stCopierStyleValues[PRESET_FINISHER],			2,
		3, stCopierStyleValues[PRESET_EXTFINISHER],			3,
		4, stCopierStyleValues[PRESET_FEEDER],				4,
		5, stCopierStyleValues[PRESET_FEEDERFINISHER],		5,
		6, stCopierStyleValues[PRESET_FEEDEREXTFINISHER],	6 ui_tooltip `Преднастроенные Стили Копира`

	_yCurr = pageStartY + _dy + 8
	ui_outfield `Ширина Копира`, x2, _yCurr + _yOffset, x5 - x2 - 2, _outfHeight
	ui_infield{3} "copier_width", x5, _yCurr, 90, _infHeight ui_tooltip `Ширина Копира`

	_yCurr = _yCurr + _dy
	ui_infield{3} "bFeeder", x2, _yCurr, 444 - x2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Повышенная Производительность`, 0,
					"", `Повышенная Производительность`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bFinisher1", x2, _yCurr, 444 - x2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Финишер 1`, 0,
					"", `Финишер 1`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bFinisher2", x2, _yCurr, x5 - x2 - 2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Финишер 2`, 0,
					"", `Финишер 2`, 1

return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacespage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"
		_idx = _idx + 1

		if gs_detlevel_3D_m = 2 then						! Detailed
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
			_idx = _idx + 1
		endif

		if gs_detlevel_3D_m <> 0 then						! Off
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
			_idx = _idx + 1
		endif

		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "body_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "panel_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "screenglass_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "but_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "but_mat2"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "leg_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return

! ------------------------------------------------------------------------------
"MinimalSpacepage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight 	= 19
	_infWidth	= 90
	PictWidth = 185 
	PictHeight = 183
	xPict = (444 - PictWidth) / 2
	yPict = (265 - PictHeight) / 2

	ui_pict 2,	xPict , yPict, PictWidth, PictHeight, 1	! ui_Plotter_MS.png

	ui_infield "MSFront",	xPict + PictWidth + 3,	yPict  + 142,	_infWidth,	_infHeight  ui_tooltip `Минимальное Пространство Спереди`
	ui_infield "MSRear",	xPict + PictWidth + 3,	yPict  + 9,	_infWidth,	_infHeight  ui_tooltip `Минимальное Пространство Сзади`

	ui_infield "MSSide",	xPict - 30,	yPict  - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Минимальное Пространство по Бокам`

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
