
call "FM_types" parameters all

hideparameter all "A", "B", "ZZYZX"

values{2} "iCopierStyle"	1, stCopierStyleValues[1],
							2, stCopierStyleValues[2],
							3, stCopierStyleValues[3],
							4, stCopierStyleValues[4],
							5, stCopierStyleValues[5],
							6, stCopierStyleValues[6]

values "gs_resol" range [3,)

values{2} "gs_detlevel_3D_m" 	2, `Детальный`,
								1, `Простой`,
								0, `Откл.`

if gs_detlevel_3D_m = 1 then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

if gs_detlevel_3D_m = 0 then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif

if gs_cont_pen = 0 then parameters gs_cont_pen  =4
if gs_fill_pen = 0 then parameters gs_fill_pen = 19

if bFinisher1 = 0 then
	lock "bFinisher2"
	hideparameter "bFinisher2"
endif

A = copier_width + finisher1Length*bFinisher1 + finisher2Length*(bFinisher2 and bFinisher1) + feederLength*bFeeder
parameters A = A
parameters B = B
lock "A", "B"

! Minimal Space

if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif
if MSSide < 0 then
	MSSide = 0
	parameters MSSide = MSSide
endif
if MSRear < 0 then
	MSRear = 0
	parameters MSRear = MSRear
endif

! ==============================================================================
! 		Presets
! ==============================================================================

if	GLOB_MODPAR_NAME = "bFeeder" |		\
	GLOB_MODPAR_NAME = "bFinisher1" |	\
	GLOB_MODPAR_NAME = "bFinisher2" then
	
	if not(bFeeder)	& not(bFinisher1) 					then iCopierStyle = PRESET_COPIER
	if not(bFeeder)	& bFinisher1 	 & not(bFinisher2)	then iCopierStyle = PRESET_FINISHER
	if not(bFeeder)	& bFinisher1 	 & bFinisher2		then iCopierStyle = PRESET_EXTFINISHER
	if bFeeder 		& not(bFinisher1) 					then iCopierStyle = PRESET_FEEDER
	if bFeeder 		& bFinisher1 	 & not(bFinisher2)	then iCopierStyle = PRESET_FEEDERFINISHER
	if bFeeder 		& bFinisher1 	 & bFinisher2		then iCopierStyle = PRESET_FEEDEREXTFINISHER

	parameters iCopierStyle = iCopierStyle

else
	if GLOB_MODPAR_NAME = "iCopierStyle" then

	if	iCopierStyle = PRESET_COPIER |		\
		iCopierStyle = PRESET_FINISHER |	\
		iCopierStyle = PRESET_EXTFINISHER then
			bFeeder = 0
	else	bFeeder = 1

	if	iCopierStyle = PRESET_COPIER |		\
		iCopierStyle = PRESET_FEEDER then
			bFinisher1 = 0
	else	bFinisher1 = 1

	if	iCopierStyle = PRESET_COPIER |		\
		iCopierStyle = PRESET_FINISHER |	\
		iCopierStyle = PRESET_FEEDER |		\
		iCopierStyle = PRESET_FEEDERFINISHER then
			bFinisher2 = 0
	else	bFinisher2 = 1

	parameters 	bFeeder = bFeeder,
				bFinisher1 = bFinisher1,
				bFinisher2 = bFinisher2
	endif
endif
