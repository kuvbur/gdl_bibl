
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_BaseJoint					= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Модульной Офисной Перегородки`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_BaseJoint, TABID_Root, `Опоры и Соединения`, "uiTab_Leg_flat(1)"
	gosub "BaseJointpage"

ui_page TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacespage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Leg_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================


! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY 	= 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_dy 		= 24	
	x1 = 0
	x2 = 159
	x3 = 230
	x4 = 354

	_yCurr = pageStartY
	ui_outfield `Количество Панелей`, 	x1, _yCurr + _yOffset, 	x2 - 2, _outfHeight
	ui_infield "nScreen", 			x2, _yCurr,				54,	_infHeight 	ui_tooltip `Количество Панелей`

	ui_outfield `Высота Экрана`, 	x3, _yCurr + _yOffset, 	x4 - x3 - 2, 	_outfHeight
	ui_infield "screenHeight", 		x4, _yCurr,				90,				_infHeight 	ui_tooltip `Высота Экрана`

	_yCurr = _yCurr + _dy
	ui_infield{3} "bFrame", x1, _yCurr, x3 - 2, _infHeight,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Каркас`, 0,
		1, `Каркас`, 1

	ui_outfield `Ширина Рамы`, 	x3, _yCurr + _yOffset, 	x4 - x3 - 2, 	_outfHeight, 4 * not(bFrame)
	ui_infield "frameWidth", 	x4, _yCurr,				90,				_infHeight 	ui_tooltip `Ширина Рамы`

	_yCurr = _yCurr + _dy
	ui_separator 0, _yCurr, 444, _yCurr

	_yCurr = _yCurr + 5
	ui_outfield `Количество Модулей`, 	x1, _yCurr + _yOffset, 	x4 - x3 - 2, 	_outfHeight
	ui_infield "nModul", 		x2, _yCurr,				54,				_infHeight 	ui_tooltip `Количество Модулей`


	ui_infield{3} "bUniform", x3, _yCurr, 444 - x3, _infHeight,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Одинаковая Высота Панелей`, 0,
		1, `Одинаковая Высота Панелей`, 1

	xPict = 42
	yPict = 109
	PictWidth = 76 
	PictHeight = 109
	_dy = PictHeight / nModul

	ui_pict 10 + nModul, xPict, yPict, PictWidth, PictHeight, 1		! ui_office_panel_0n.png
	ui_infield "modulWidth", xPict - 3, yPict + PictHeight + 3,	90, _infHeight 	ui_tooltip `Ширина Модуля (Ширина Панели)`

	if nModul = 4 then _yCurr = yPict + 3
	if nModul = 3 then _yCurr = yPict + 8
	if nModul = 2 then _yCurr = yPict + 14
	if nModul = 1 then _yCurr = yPict + 35
	
	if nModul > 3 then
		ui_infield "moduleHeight4", xPict + PictWidth + 3, 		_yCurr - 3,	90,		_infHeight	ui_tooltip `Высота Панели 4`
		ui_infield "moduleMat4", 	xPict + PictWidth + 112, 	_yCurr - 5, 210, 	25  		ui_tooltip `Покрытие Модуля 4`
		_yCurr = _yCurr + _dy
	endif
	
	if nModul > 2 then
		ui_infield "moduleHeight3", xPict + PictWidth + 3, 		_yCurr - 3,	90,		_infHeight	ui_tooltip `Высота Панели 3`
		ui_infield "moduleMat3", 	xPict + PictWidth + 112, 	_yCurr - 5, 210, 	25  		ui_tooltip `Покрытие Модуля 3`
		_yCurr = _yCurr + _dy
	endif
	
	if nModul > 1 then
		ui_infield "moduleHeight2", xPict + PictWidth + 3, 		_yCurr - 3,	90,		_infHeight	ui_tooltip `Высота Панели 2`
		ui_infield "moduleMat2", 	xPict + PictWidth + 112, 	_yCurr - 5, 210, 	25  		ui_tooltip `Покрытие Модуля 2`
		_yCurr = _yCurr + _dy
	endif
	
	ui_infield "moduleHeight1", xPict + PictWidth + 3, 		_yCurr - 3,	90,		_infHeight	ui_tooltip `Высота Панели 1`
	ui_infield "moduleMat1", 	xPict + PictWidth + 112, 	_yCurr - 5, 210, 	25  		ui_tooltip `Покрытие Модуля 1`

return

! ------------------------------------------------------------------------------
"BaseJointpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	x1 =  0
	x2 = 76
	x3 = 222
	x4 = 232
	x5 = 444 - 54
	_dy = 24
	
	! --- Left Side ---
	_yCurr = pageStartY
	ui_style 0, 1
	ui_infield{3} "bBase", x1, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Основание`, 0,
					"", `Основание`, 1
	ui_style 0, 0

	_yCurr = _yCurr + _dy

	ui_infield{3} "iToe", x2, _yCurr, 135, 96,
		2, 1, 8, 2,	! ui_OfficePartitionPanel_Toe.png
		120, 90, 120, 90,
		1, stToeStyle[1],	1,
		2, stToeStyle[2],	2,
		3, stToeStyle[3], 	3,
		4, stToeStyle[4], 	4,
		5, stToeStyle[5], 	5,
		6, stToeStyle[6],	6 ui_tooltip `Стиль Основания`

	_yCurr = _yCurr + 96 + 5
	if iToe = 6 then
		ui_outfield `Смещение`, 	x1, _yCurr + _yOffset, 	x2, _outfHeight
	else
		ui_outfield `Высота`, 	x1, _yCurr + _yOffset, 	x2, _outfHeight, 4 * (iToe = 1)
	endif
	ui_infield "toeHeight", 	x2 + 45, _yCurr, 		90,	_infHeight 	ui_tooltip `Высота Основания`

	_yCurr = _yCurr + 45

	ui_pict 3, 	x1, 		_yCurr + 3, 18, 		18, 1	! ui_OfficePartitionPanel_BaseIcon.png
	ui_infield "toe_mat", 		x1 + 20, 	_yCurr, 	213 - 20, 	25 ui_tooltip `Покрытие Опоры`

	_yCurr = _yCurr + 30

	ui_pict 4, 	x1, 		_yCurr + 3, 18, 		18, 1	! ui_OfficePartitionPanel_WheelIcon.png
	ui_infield "wheelMat", 		x1 + 20, 	_yCurr, 	213 - 20, 	25 ui_tooltip `Покрытие Ролика`

	_yCurr = _yCurr + 30
	ui_separator x3, 0,	x3,	_yCurr

	! --- Right Side ---

	_yCurr = pageStartY
	ui_style 0, 1
	ui_outfield `Соединения`, 	x4, _yCurr + _yOffset, 444 - x4, 	_outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + _dy

	ui_infield{3} "bJointL", x4, _yCurr, 444 - x4, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Соединение со Стороны 1`, 0,
					"", `Соединение со Стороны 1`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bToeL", x4, _yCurr, 444 - x4, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Опора под Соединением`, 0,
					"", `Опора под Соединением`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bJointR", x4, _yCurr, 444 - x4, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Соединение со Стороны 2`, 0,
					"", `Соединение со Стороны 2`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "bToeR", x4, _yCurr, 444 - x4, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Опора под Соединением`, 0,
					"", `Опора под Соединением`, 1
	xPict = 230
	yPict = 179
	PictWidth = 196
	PictHeight = 24

	ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1		! ui_OfficePartitionPanel_ToeAndJoint.png
	ui_outfield `Сторона 1`, 	xPict + 17, 			yPict - _outfHeight - 2, 80, _outfHeight
	ui_outfield `Сторона 2`, 	xPict + PictWidth / 2,	yPict - _outfHeight - 2, 80, _outfHeight, 1

	ui_infield "angleLeft",		xPict, 					yPict + PictHeight + 3, 54,	_infHeight 	ui_tooltip `Угол Соединения со Стороны 1`
	ui_infield "angleRight", 	xPict + PictWidth - 54, yPict + PictHeight + 3, 54,	_infHeight 	ui_tooltip `Угол Соединения со Стороны 2`

return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacespage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_2D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel2D_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"
		_idx = _idx + 1

		if gs_detlevel_3D_m = 2 then						! Detailed
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
			_idx = _idx + 1
		endif

		if gs_detlevel_3D_m <> 0 then						! Off
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
			_idx = _idx + 1
		endif

		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "fmat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "edgeMat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		if (bJointR and bToeR) or (bJointL and bToeL) or (not(bJointR) and not(bJointL)) then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "toe_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
			if iToe = 4 then
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "wheelMat"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
			endif
		endif

	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray



return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return
