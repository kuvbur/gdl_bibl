
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_RepresentationAndSurfaces	= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Монитора`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_RepresentationAndSurfaces, TABID_Root, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacespage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	x1 =  0
	x2 = 222
	x3 = 232
	x4 = 444 - 90
	x5 = 444 - 54
	_dy = 24
	
	ui_style 0, 1
	_yCurr = pageStartY
	ui_outfield `Стиль`, 				x1, _yCurr + _yOffset , x2 - 2, 		_outfHeight
	ui_outfield `Размеры Экрана`, 	x3, _yCurr + _yOffset , 444 - x3 - 2, 	_outfHeight
	ui_style 0, 0
	
	ui_infield{3} "iMonitorType", x1, 26, 211,100,
		4, 1, 	! ui_LCDMonitor_StyleAndDimensions.png
		2, 1,
		100, 100, 90,90,
		1,  "", 1,
		2,  "", 2 ui_tooltip `Тип Монитора`

	_yCurr = pageStartY + _dy
	ui_outfield `Соотношение`, 				x3, _yCurr + _yOffset, 	x4 - x3 - 2,	_outfHeight
	ui_infield{3} "iScreenRatio", 	x4, _yCurr, 			90, 			_infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `4:3`, 		1,
			"", `16:9`, 	2,
			"", `2.39:1`, 	3,
			"", `Специальный`, 	4 ui_tooltip `Соотношение`

	_yCurr = _yCurr + _dy
	ui_outfield `Выбор Диагонали`, x3, _yCurr + _yOffset, 	x4 - x3 - 2,	_outfHeight, 4 * (iScreenRatio = 4)
	ui_infield{3} "iScreenSize", 	x4, _yCurr, 			90, 			_infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `15 дюймов`, 	1,
			"", `17 дюймов`, 	2,
			"", `19 дюймов`, 	3,
			"", `21 дюйм`, 	4,
			"", `23 дюйма`, 	5,
			"", `25 дюймов`, 	6,
			"", `27 дюймов`, 	7,
			"", `Специальный`, 	8 ui_tooltip `Преднастроенная Диагональ Экрана`

	_yCurr = _yCurr + _dy
	ui_outfield `Размер Диагонали`, 	x3, _yCurr + _yOffset, 	x4 - x3 - 2, 	_outfHeight, 4 * (iScreenSize <> 8 or iScreenRatio = 4)
	ui_infield "screenSize", 		x4, _yCurr, 			90,				_infHeight ui_tooltip `Размер Диагонали Экрана`


	_yCurr = _yCurr + _dy
	ui_outfield `Высота`, 		x3, _yCurr + _yOffset, 	x4 - x3 - 2, 	_outfHeight, 4 * (iScreenRatio <> 4)
	ui_infield "screenHeight", 	x4, _yCurr, 			90,				_infHeight ui_tooltip `Высота Экрана`

	_yCurr = _yCurr + _dy
	ui_outfield `Ширина`, 		x3, _yCurr + _yOffset, 	x4 - x3 - 2, 	_outfHeight, 4 * (iScreenRatio <> 4)
	ui_infield "screenWidth", 	x4, _yCurr, 			90,				_infHeight ui_tooltip `Ширина Экрана`

	_yCurr = _yCurr + _dy
	ui_outfield `Наклон Монитора`, 	x3, _yCurr + _yOffset, 	x5 - x3 - 2, 	_outfHeight, 4 * (iMonitorType = 1)
	ui_infield "displayAng", 		x5, _yCurr, 			54,				_infHeight ui_tooltip `Наклон Монитора`

	_yCurr = _yCurr + _dy
	ui_infield{3} "pic", x3, _yCurr, 444 - x3, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Показ Изображения`, 0,
					"", `Показ Изображения`, 1

	_yCurr = _yCurr + _dy
	ui_infield{3} "gs_picture_custom", x3, _yCurr, 444 - x3, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Спец. Изображение`, 0,
					"", `Спец. Изображение`, 1

	_yCurr = _yCurr + _dy
	ui_infield "gs_picture_name", x3 + 18, _yCurr, 444 - x3 - 18, _infHeight ui_tooltip `Имя Специального Изображения`


	_yCurr = _yCurr + _dy
	ui_separator x2, 0,	x2,	_yCurr

return

! ------------------------------------------------------------------------------
"RepresentationAndSurfacespage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"
		_idx = _idx + 1

		if gs_detlevel_3D_m = 2 then						! Detailed
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
			_idx = _idx + 1
		endif

		if gs_detlevel_3D_m <> 0 then						! Off
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
			_idx = _idx + 1
		endif

		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1

		if iMonitorType = 1 then
			paramNameReprArray 	[_idx] = "box_mat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
	
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "feetmat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

			if not(pic) then
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "dispmat"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
			endif
		else
			paramNameReprArray 	[_idx] = "frameMat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
	
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "standMat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

			if not(pic) then
				_idx = _idx + 1
				paramNameReprArray 	[_idx] = "screenMat"
				childFlagReprArray 	[_idx] = 1
				iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
			endif
		endif
	
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return

