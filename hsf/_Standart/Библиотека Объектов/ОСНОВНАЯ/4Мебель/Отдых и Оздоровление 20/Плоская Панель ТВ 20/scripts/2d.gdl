
pen gs_cont_pen
fill gs_fill_type

unID = 1

hotspot2 a/2, b/2, unID : unID = unID + 1
hotspot2 0, b, unID : unID = unID + 1
hotspot2 a, b, unID : unID = unID + 1
hotspot2 0, 0, unID : unID = unID + 1
hotspot2 a, 0, unID : unID = unID + 1

if iSupporterType = SUPPORTER_CEILING then
	hotspot2 A / 2, supporterPosition, unID : unID = unID + 1
endif

if iSupporterType = SUPPORTER_MOTORIZED then
	add2 A / 2, -(mountDepth / 2 - B - mountWallThk * 2)

	hotspot2 -mountWidth / 2, -mountDepth / 2, unID : unID = unID + 1
	hotspot2 -mountWidth / 2 - mountWallThk, mountDepth / 2, unID : unID = unID + 1
	hotspot2 mountWidth / 2 + mountWallThk, mountDepth / 2, unID : unID = unID + 1
	hotspot2 mountWidth / 2, -mountDepth / 2, unID : unID = unID + 1

	del 1
endif

if iSupporterType = SUPPORTER_WALL then
	hotspot2 A / 2, supporterPosition, unID : unID = unID + 1
endif

if gs_detlevel_2D_m = DETLEVEL_2D_SIMPLE then

	if bSupporter then		
		gosub iSupporterType
	endif

	poly2_b 4, 1+2+4+64,gs_fill_pen, gs_back_pen,
		0,0,15,
		a,0,15,
		a,b,15,
		0,b,15
endif

if gs_detlevel_2D_m = DETLEVEL_2D_DETAILED then

	if bSupporter then		
		gosub iSupporterType
	endif

	poly2_b 6, 1+2+4+64,gs_fill_pen, gs_back_pen,
		0,0,15,
		a,0,15,
		a,(b/2),15,
		a-(a/4),b,15,
		(a/4),b,15,
		0,(b/2),15

	add2 a-(a/15),(b/5)
	rect2 0,0,-(a/15),(b/8)
	add2 -(a/12),0
	rect2 0,0,-(a/15),(b/8)
	add2 -(a/12),0
	rect2 0,0,-(a/30),(b/8)
	del 3
	line2 0, b/2, a, b/2
endif

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================

! -----------------------------------------------------------------------------
1: ! Rectangular
! -----------------------------------------------------------------------------
	add2 A / 2, B / 2
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-supporterBaseWidth / 2, -supporterBaseDepth / 2, 1,
		-supporterBaseWidth / 2, supporterBaseDepth / 2, 1,
		supporterBaseWidth / 2, supporterBaseDepth / 2, 1,
		supporterBaseWidth / 2, -supporterBaseDepth / 2, 1,
		-supporterBaseWidth / 2, -supporterBaseDepth / 2, -1

	del 1
return

! -----------------------------------------------------------------------------
2: ! Oval
! -----------------------------------------------------------------------------
	add2 A / 2, supporterPosition
	mul2 1, supporterBaseRatioY
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, 0, 979,
		supporterBaseRad, 360, 4079

	del 2
return

! -----------------------------------------------------------------------------
3: ! Feet
! -----------------------------------------------------------------------------
	add2 A / 2, supporterPosition

	for i = 1 to 3
		rot2 (i-1) * 120
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-footWidth / 2, 0, 1,
			-footWidth / 2, footRad, 1,
			footWidth / 2, footRad, 1,
			footWidth / 2, 0, 1,
			-footWidth / 2, 0, -1

		del 1
	next i

	del 1
return

! -----------------------------------------------------------------------------
4: ! Designer
! -----------------------------------------------------------------------------
	add2 A / 2, B/ 1.1
	rot2 180

	poly2_b 5, 1+2+4+64,gs_fill_pen, gs_back_pen,
		r1,0,1,
		0,0,901,
		0,180,4001,
		0,t,901,
		0,2*alfa,4001

	del 2
return

! -----------------------------------------------------------------------------
5: ! Wall Mount
! -----------------------------------------------------------------------------
	add2 A / 2, B
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-backPlateWidth, 0, 1,
		-backPlateWidth, backPlateThk, 1,
		backPlateWidth, backPlateThk, 1,
		backPlateWidth, 0, 1,
		-backPlateWidth, 0, -1

	add2 0, backPlateThk
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-joinRad, 0, 1,
		-joinRad, armLength * 2, 1,
		joinRad, armLength * 2, 1,
		joinRad, 0, 1,
		-joinRad, 0, -1

	add2 0, armLength
	poly2_b 3, 1+2+64, gs_fill_pen, gs_back_pen,
		joinRad, 0, 1,
		0, 0, 901,
		joinRad, 180, 4001

	add2 0, armLength
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-supporterRad * 1.2, 0, 1,
		-supporterRad * 1.2, supporterBaseHeight, 1,
		supporterRad * 1.2, supporterBaseHeight, 1,
		supporterRad * 1.2, 0, 1,
		-supporterRad * 1.2, 0, -1

	del 4
return

! -----------------------------------------------------------------------------
6: ! Ceiling Mount
! -----------------------------------------------------------------------------
	add2 A / 2, B
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-backPlateWidth, 0, 1,
		-backPlateWidth, backPlateThk, 1,
		backPlateWidth, backPlateThk, 1,
		backPlateWidth, 0, 1,
		-backPlateWidth, 0, -1

	del 1

	add2 A / 2, supporterPosition
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-supporterBaseWidth / 2, -supporterBaseWidth / 2, 1,
		-supporterBaseWidth / 2, supporterBaseWidth / 2, 1,
		supporterBaseWidth / 2, supporterBaseWidth / 2, 1,
		supporterBaseWidth / 2, -supporterBaseWidth / 2, 1,
		-supporterBaseWidth / 2, -supporterBaseWidth / 2, -1

	del 1
return

! -----------------------------------------------------------------------------
7: ! Motorized Mount
! -----------------------------------------------------------------------------

	add2 A / 2, -(mountDepth / 2 - B - mountWallThk * 2)
	poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-mountWidth / 2, -mountDepth / 2, 1,
		-mountWidth / 2, 0, 1,
		-mountWidth / 2 - mountWallThk, 0, 1,
		-mountWidth / 2 - mountWallThk, mountDepth / 2, 1,
		mountWidth / 2 + mountWallThk, mountDepth / 2, 1,
		mountWidth / 2 + mountWallThk, 0, 1,
		mountWidth / 2, 0, 1,
		mountWidth / 2, -mountDepth / 2, 1,
		-mountWidth / 2, -mountDepth / 2, -1

	del 1
return

