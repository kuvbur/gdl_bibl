
if gs_detlevel_3D_m = DETLEVEL_3D_OFF then end

pen gs_cont_pen
resol gs_resol

unID = 1

if iScreenRatio <> SCREEN_CUST then
	hotspot A, 0, bottomSupporterHeight, unID :unID=unID+1
	hotspot A, B, bottomSupporterHeight, unID :unID=unID+1
	hotspot 0, B, bottomSupporterHeight, unID :unID=unID+1
	hotspot 0, 0, bottomSupporterHeight, unID :unID=unID+1

	hotspot A, 0, zzyzx - topSupporterHeight, unID :unID=unID+1
	hotspot A, B, zzyzx - topSupporterHeight, unID :unID=unID+1
	hotspot 0, B, zzyzx - topSupporterHeight, unID :unID=unID+1
	hotspot 0, 0, zzyzx - topSupporterHeight, unID :unID=unID+1

	hotspot sideFrameWidth, 0, bottomSupporterHeight+bottomFrameWidth , unID, screenSize, 1+128 :unID=unID+1
	hotspot 0, 0, bottomSupporterHeight, unID, screenSize, 3+128 :unID=unID+1
	hotspot A-sideFrameWidth , 0, zzyzx - topFrameWidth - topSupporterHeight, unID, screenSize, 2 :unID=unID+1
else
	hotspot 0, 0, bottomSupporterHeight, unID, A, 1+256 :unID=unID+1
	hotspot -1, 0, bottomSupporterHeight, unID, A, 3+128 :unID=unID+1
	hotspot A, 0, bottomSupporterHeight, unID, A, 2 :unID=unID+1

	hotspot 0, B, bottomSupporterHeight, unID, A, 1+256 :unID=unID+1
	hotspot -1, B, bottomSupporterHeight, unID, A, 3+128 :unID=unID+1
	hotspot A, B, bottomSupporterHeight, unID, A, 2 :unID=unID+1

	hotspot 0, 0, zzyzx - topSupporterHeight, unID, A, 1+256 :unID=unID+1
	hotspot -1, 0, zzyzx - topSupporterHeight, unID, A, 3+128 :unID=unID+1
	hotspot A, 0, zzyzx - topSupporterHeight, unID, A, 2 :unID=unID+1

	hotspot 0, B, zzyzx - topSupporterHeight, unID, A, 1+256 :unID=unID+1
	hotspot -1, B, zzyzx - topSupporterHeight, unID, A, 3+128 :unID=unID+1
	hotspot A, B, zzyzx - topSupporterHeight, unID, A, 2 :unID=unID+1

	hotspot 0, 0, bottomSupporterHeight, unID, B, 1+256 :unID=unID+1
	hotspot 0, -1, bottomSupporterHeight, unID, B, 3+128 :unID=unID+1
	hotspot 0, B, bottomSupporterHeight, unID, B, 2 :unID=unID+1

	hotspot A, 0, bottomSupporterHeight, unID, B, 1+256 :unID=unID+1
	hotspot A, -1, bottomSupporterHeight, unID, B, 3+128 :unID=unID+1
	hotspot A, B, bottomSupporterHeight, unID, B, 2 :unID=unID+1

	hotspot 0, 0, -topSupporterHeight, unID, zzyzx, 1+128 :unID=unID+1
	hotspot 0, 0, -1, unID, zzyzx, 3+128 :unID=unID+1
	hotspot 0, 0, zzyzx - topSupporterHeight, unID, zzyzx, 2 :unID=unID+1

	hotspot A, 0, -topSupporterHeight, unID, zzyzx, 1+128 :unID=unID+1
	hotspot A, 0, -1, unID, zzyzx, 3+128 :unID=unID+1
	hotspot A, 0, zzyzx - topSupporterHeight, unID, zzyzx, 2 :unID=unID+1

	hotspot 0, B, -topSupporterHeight, unID, zzyzx, 1+128 :unID=unID+1
	hotspot 0, B, -1, unID, zzyzx, 3+128 :unID=unID+1
	hotspot 0, B, zzyzx - topSupporterHeight, unID, zzyzx, 2 :unID=unID+1

	hotspot A, B, -topSupporterHeight, unID, zzyzx, 1+128 :unID=unID+1
	hotspot A, B, -1, unID, zzyzx, 3+128 :unID=unID
	hotspot A, B, zzyzx - topSupporterHeight, unID, zzyzx, 2 :unID=unID+1
endif

if iSupporterType = SUPPORTER_CEILING then
	hotspot A / 2, supporterPosition, ZZYZX, unID : unID = unID + 1
endif

if iSupporterType = SUPPORTER_MOTORIZED then
	add A / 2, -(mountDepth / 2 - B - mountWallThk * 2), ZZYZX + mountHeight

	hotspot -mountWidth / 2, -mountDepth / 2, 0, unID : unID = unID + 1
	hotspot -mountWidth / 2 - mountWallThk, mountDepth / 2, 0, unID : unID = unID + 1
	hotspot mountWidth / 2 + mountWallThk, mountDepth / 2, 0, unID : unID = unID + 1
	hotspot mountWidth / 2, -mountDepth / 2, 0, unID : unID = unID + 1

	del 1
endif

if iSupporterType = SUPPORTER_WALL then
	hotspot A / 2, supporterPosition, ZZYZX / 2 - supporterRad * 2, unID : unID = unID + 1
endif


! --- DEFINE MATERIALS ---
define empty_fill "emptyFill"

screenSizeX = A - 2 * sideFrameWidth
screenSizeY = ZZYZX - topFrameWidth - bottomFrameWidth - bottomSupporterHeight - topSupporterHeight

if gs_picture_custom and gs_picture_name <> "" then
	define texture "custPict" gs_picture_name, screenSizeX, screenSizeY, 0, 0
	define material "custPict" 20,
		0.8, 0.8, 0.8,
		0.58, 0.85, 0.2, 0,
		50,
		0,
		0.878401, 0.513481, 0.412253,
		0, 0, 0,
		0,
		ind(fill, "emptyFill"), 1,
		ind(texture, "custPict")
else
	define texture "defPict" 1, screenSizeX, screenSizeY, 0, 0
	define material "defPict" 20,
		0.8, 0.8, 0.8,
		0.58, 0.85, 0.2, 0,
		50,
		0,
		0.878401, 0.513481, 0.412253,
		0, 0, 0,
		0,
		ind(fill, "emptyFill"), 1,
		ind(texture, "defPict")
endif
! --- END OF DEFINE MATERIALS ---

if gs_shadow=0 then shadow off

if gs_detlevel_3D_m = DETLEVEL_3D_DETAILED then
	muly -1

	! supporter
	! --------------------------------------------
	if bSupporter then		
		material pd_mat
		gosub iSupporterType
	endif

	! screen
	! --------------------------------------------
	addz bottomSupporterHeight
	rotx 90
	if bPictureOnScreen then
		if gs_picture_custom and gs_picture_name <> "" then
			material "custPict"
		else
			material "defPict"
		endif
	else
		material sc_mat
	endif
	prism_ 4, 0.001,
		sideFrameWidth, bottomFrameWidth,15,
		sideFrameWidth, ZZYZX-topFrameWidth-topSupporterHeight-bottomSupporterHeight,15,
		A-sideFrameWidth, ZZYZX-topFrameWidth-topSupporterHeight-bottomSupporterHeight,15,
		A-sideFrameWidth, bottomFrameWidth,15

	base
	add sideFrameWidth, bottomFrameWidth, 0

	vert 0, 0, 0
	vert 1 - 2 * SYMB_MIRRORED, 0, 0	! Correcting the Texture mapping for Picture in case of mirrored object
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2+256, -1, -2, -3, -4
	body 1
	del 1

	! frame and back
	! --------------------------------------------

	cutform 4, 1, 1+16,
		0, 0, 1, 0.001,
		sideFrameWidth, bottomFrameWidth,15,
		sideFrameWidth, ZZYZX-topFrameWidth-topSupporterHeight-bottomSupporterHeight,15,
		A-sideFrameWidth, ZZYZX-topFrameWidth-topSupporterHeight-bottomSupporterHeight,15,
		A-sideFrameWidth, bottomFrameWidth,15

	material fr_mat
	fprism_ fr_mat,fr_mat,fr_mat,fr_mat,
			4,b,15,(b/2),
			0,0,15,
			0,zzyzx-topSupporterHeight-bottomSupporterHeight,15,
			a,zzyzx-topSupporterHeight-bottomSupporterHeight,15,
			a,0,15
	cutend

	material 10
	addx (a/2)
	addy (bottomFrameWidth * 0.25)
	prism_ 5, -0.0008,
		0,0,15,
		(a/100),(bottomFrameWidth * 0.25),15,
		(a/10),(bottomFrameWidth * 0.5),15,
		-(a/10),(bottomFrameWidth * 0.5),15,
		-(a/100),(bottomFrameWidth * 0.25),15
	del 2

	! buttons
	! --------------------------------------------
	material bt_mat
	addy zzyzx-topSupporterHeight-bottomSupporterHeight
	addz (b/5)
	addx (a/15)
	block (a/15), 0.0016,(b/8)
	addx (a/14)
	block (a/15), 0.0016,(b/8)
	addx (a/14)
	block (a/30), 0.0016,(b/8)
	del 8


else
	muly -1

	! supporter
	! --------------------------------------------
	if bSupporter then		
		material pd_mat
		gosub iSupporterType
	endif

	! screen
	! --------------------------------------------
	addz bottomSupporterHeight
	rotx 90
	if bPictureOnScreen then
		material "pictmat"
		if gs_picture_custom and gs_picture_name <> "" then
			material "custPict"
		else
			material "defPict"
		endif
	else
		material sc_mat
	endif
	prism_ 4, -0.0003,
		sideFrameWidth, bottomFrameWidth,15,
		sideFrameWidth, ZZYZX-topFrameWidth-topSupporterHeight-bottomSupporterHeight,15,
		A-sideFrameWidth, ZZYZX-topFrameWidth-topSupporterHeight-bottomSupporterHeight,15,
		A-sideFrameWidth, bottomFrameWidth,15
	base
	add sideFrameWidth, bottomFrameWidth, 0
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2+256, -1, -2, -3, -4
	body 1
	del 1

	! frame and back
	! --------------------------------------------
	material fr_mat
	prism_	6,b,
			0,(zzyzx/44),15,
			0,zzyzx-topSupporterHeight-bottomSupporterHeight,15,
			a,zzyzx-topSupporterHeight-bottomSupporterHeight,15,
			a,(zzyzx/44),15,
			a-sideFrameWidth,0,15,
			sideFrameWidth,0,15
	del 2
endif

! =============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end 
! =============================================================================

! =============================================================================
! Subroutines
! =============================================================================

! -----------------------------------------------------------------------------
1: ! Rectangular
! -----------------------------------------------------------------------------
	add A / 2, -B / 2, 0
	prism_ 5, supporterBaseHeight,
		-supporterBaseWidth / 2, -supporterBaseDepth / 2, 15,
		-supporterBaseWidth / 2, supporterBaseDepth / 2, 15,
		supporterBaseWidth / 2, supporterBaseDepth / 2, 15,
		supporterBaseWidth / 2, -supporterBaseDepth / 2, 15,
		-supporterBaseWidth / 2, -supporterBaseDepth / 2, -1

	del 1

	addz bottomSupporterHeight
	roty 90

	cutpolya 6, 1, 0,
		0, 0, 79,
		-ZZYZX / 2, 0, 79,
		-ZZYZX / 2, -B, 79,
		-(B / 2 / tan(15)), -B, 79,
		0, -B / 2, 79,
		0, 0, -1

	del 2

	add A / 2, -supporterPosition, supporterBaseHeight
	prism_ 2, bottomSupporterHeight,
		0, 0, 979,
		supporterRad, 360, 4079

	addz bottomSupporterHeight

	prism_ 5, supporterRad * 2,
		-supporterRad * 2, -supporterRad, 15,
		-supporterRad * 2, B / 2, 15,
		supporterRad * 2, B / 2, 15,
		supporterRad * 2, -supporterRad, 15,
		-supporterRad * 2, -supporterRad, -1

	del 2

	cutend
return

! -----------------------------------------------------------------------------
2: ! Oval
! -----------------------------------------------------------------------------
	add A / 2, -B / 2, 0
	muly supporterBaseRatioY

	fprism_	pd_mat, pd_mat, pd_mat, pd_mat,
			2, supporterBaseHeight, 30, supporterBaseHeight / 2,
			0, 0, 979,
			supporterBaseRad, 360, 4079

	del 2

	addz bottomSupporterHeight
	roty 90

	cutpolya 6, 1, 0,
		0, 0, 79,
		-ZZYZX / 2, 0, 79,
		-ZZYZX / 2, -B, 79,
		-(B / 2 / tan(15)), -B, 79,
		0, -B / 2, 79,
		0, 0, -1

	del 2

	add A / 2, -supporterPosition, supporterBaseHeight
	prism_ 2, bottomSupporterHeight,
		0, 0, 979,
		supporterRad, 360, 4079

	addz bottomSupporterHeight

	prism_ 5, supporterRad * 2,
		-supporterRad * 2, -supporterRad, 15,
		-supporterRad * 2, B / 2, 15,
		supporterRad * 2, B / 2, 15,
		supporterRad * 2, -supporterRad, 15,
		-supporterRad * 2, -supporterRad, -1

	del 2
	cutend
return

! -----------------------------------------------------------------------------
3: ! Feet
! -----------------------------------------------------------------------------
	add A / 2, -B / 2, 0
	for i = 1 to 3
		rotz (i-1) * 120
		addy supporterRad * 2

		cutpolya 5, 1, 0,
			supporterRad * cos(30), supporterRad * sin(30), 13,
			-supporterRad * cos(30), supporterRad * sin(30), 13,
			-cos(30), sin(30), 15,
			cos(30), sin(30), 15,
			supporterRad * cos(30), supporterRad * sin(30), -1

		del 2
	next i

	prism_ 2, supporterBaseHeight,
		0, 0, 979,
		footRad, 360, 4079

	del 1

	for i = 1 to 3
		cutend
	next i

	addz bottomSupporterHeight
	roty 90

	cutpolya 6, 1, 0,
		0, 0, 79,
		-ZZYZX / 2, 0, 79,
		-ZZYZX / 2, -B, 79,
		-(B / 2 / tan(15)), -B, 79,
		0, -B / 2, 79,
		0, 0, -1

	del 2

	add A / 2, -supporterPosition, supporterBaseHeight
	prism_ 2, bottomSupporterHeight,
		0, 0, 979,
		supporterRad, 360, 4079

	addz bottomSupporterHeight

	prism_ 5, supporterRad * 2,
		-supporterRad * 2, -supporterRad, 15,
		-supporterRad * 2, B / 2, 15,
		supporterRad * 2, B / 2, 15,
		supporterRad * 2, -supporterRad, 15,
		-supporterRad * 2, -supporterRad, -1

	del 2
	cutend
return

! -----------------------------------------------------------------------------
4: ! Designer
! -----------------------------------------------------------------------------
	add A / 2, -r1 / 3.36, 0
	prism_ 7, supporterBaseHeight,
		   r1,0,79,
		   0,0,915,
		   0,179.5,4015,
		   0,0.5,4079,
		   0,t,979,
		   0,2*alfa*0.999,4015,
		   r1,0,15
	del 1

	addz supporterBaseHeight
	addx (a/3)
	prism_ 4, bottomSupporterHeight - supporterBaseHeight,
		0,-(b/2)*0.1,15,
		0,-(b/2)*0.9,15,
		(a/3),-(b/2)*0.9,15,
		(a/3),-(b/2)*0.1,15

	del 2
return

! -----------------------------------------------------------------------------
5: ! Wall Mount
! -----------------------------------------------------------------------------
	add A / 2, -B - backPlateThk, ZZYZX / 2
	rotx -90

	prism_ 5, backPlateThk,
		-backPlateWidth / 2, -backPlateHeight / 2, 15,
		-backPlateWidth / 2, backPlateHeight / 2, 15,
		backPlateWidth / 2, backPlateHeight / 2, 15,
		backPlateWidth / 2, -backPlateHeight / 2, 15,
		-backPlateWidth / 2, -backPlateHeight / 2, -1

	for i = 1 to 2
		add 0, backPlateHeight / 2, backPlateThk / 2
		prism_ 5, backPlateThk / 2,
			-backPlateWidth, backPlateThk * 2, 15,
			-backPlateWidth, 0, 15,
			backPlateWidth, 0, 15,
			backPlateWidth, backPlateThk * 2, 15,
			-backPlateWidth, backPlateThk * 2, -1
		del 1
		muly -1
	next i
	del 2

	roty -90
	for i = 1 to 2
		addz backPlateWidth * 0.9

		prism_ 5, backPlateThk,
			0, backPlateHeight * 0.75, 15,
			backPlateThk, backPlateHeight, 15,
			backPlateThk, -backPlateHeight, 15,
			0, -backPlateHeight * 0.75, 15,
			0, backPlateHeight * 0.75, -1

		del 1
		mulz -1
	next i
	del 3

	roty 180
	rotz 180
	add 0, supporterRad, armLength
	rotx 90
	prism_ 2, supporterRad * 4,
		0, 0, 979,
		joinRad, 360, 4079

	cutpolya 2, 1, 0,
		0, 0, 979,
		joinRad, 360, 4079

	del 2

	for i = 1 to 2
		prism_ 5, armLength,
			-supporterRad, -supporterRad, 15,
			-supporterRad, supporterRad, 15,
			supporterRad, supporterRad, 15,
			supporterRad, -supporterRad, 15,
			-supporterRad, -supporterRad, -1

		add 0, -supporterRad * 2, armLength
	next i

	del 2
	cutend

	add 0, -supporterRad * 2, armLength * 2

	prism_ 5, supporterBaseHeight,
		-supporterRad * 1.2, -backPlateHeight / 2, 15,
		-supporterRad * 1.2, backPlateHeight / 2, 15,
		supporterRad * 1.2, backPlateHeight / 2, 15,
		supporterRad * 1.2, -backPlateHeight / 2, 15,
		-supporterRad * 1.2, -backPlateHeight / 2, -1

	del 5

return

! -----------------------------------------------------------------------------
6: ! Ceiling Mount
! -----------------------------------------------------------------------------
	add A / 2, -supporterPosition, (ZZYZX - topSupporterHeight) / 2
	addz -supporterRad
	prism_ 2, armLength,
		0, 0, 979,
		supporterRad, 360, 4079

	cutpolya 2, 1, 0,
		0, 0, 979,
		supporterRad, 360, 4079

	del 1
	rotx -90
	prism_ 5, joinLength + supporterRad,
		-supporterRad, -supporterRad, 15,
		-supporterRad, supporterRad, 15,
		supporterRad, supporterRad, 15,
		supporterRad, -supporterRad, 15,
		-supporterRad, -supporterRad, -1

	cutend
	addz joinLength + supporterRad
	prism_ 5, backPlateThk,
		-backPlateWidth / 2, -backPlateHeight / 2, 15,
		-backPlateWidth / 2, backPlateHeight / 2, 15,
		backPlateWidth / 2, backPlateHeight / 2, 15,
		backPlateWidth / 2, -backPlateHeight / 2, 15,
		-backPlateWidth / 2, -backPlateHeight / 2, -1

	for i = 1 to 2
		add 0, backPlateHeight / 2, backPlateThk / 2
		prism_ 5, backPlateThk / 2,
			-backPlateWidth, backPlateThk * 2, 15,
			-backPlateWidth, 0, 15,
			backPlateWidth, 0, 15,
			backPlateWidth, backPlateThk * 2, 15,
			-backPlateWidth, backPlateThk * 2, -1
		del 1
		muly -1
	next i
	del 2

	roty -90
	for i = 1 to 2
		addz backPlateWidth * 0.9

		prism_ 5, backPlateThk,
			0, backPlateHeight * 0.75, 15,
			backPlateThk, backPlateHeight, 15,
			backPlateThk, -backPlateHeight, 15,
			0, -backPlateHeight * 0.75, 15,
			0, backPlateHeight * 0.75, -1

		del 1
		mulz -1
	next i
	del 3
	del 3

	add A / 2, -supporterPosition, ZZYZX - supporterBaseHeight
	prism_ 5,supporterBaseHeight,
		-supporterBaseWidth / 2, -supporterBaseWidth / 2, 15,
		-supporterBaseWidth / 2, supporterBaseWidth / 2, 15,
		supporterBaseWidth / 2, supporterBaseWidth / 2, 15,
		supporterBaseWidth / 2, -supporterBaseWidth / 2, 15,
		-supporterBaseWidth / 2, -supporterBaseWidth / 2, -1

	del 1
return

! -----------------------------------------------------------------------------
7: ! Motorized Mount
! -----------------------------------------------------------------------------
	add A / 2, -B, 0
	rotx 90

	prism_ 5, mountWallThk / 2,
		-A / 2 - mountOffset * 0.8, -mountOffset * 0.8, 15,
		-A / 2 - mountOffset * 0.8, ZZYZX + mountTopOffset * 0.8, 15,
		A / 2 + mountOffset * 0.8, ZZYZX + mountTopOffset * 0.8, 15,
		A / 2 + mountOffset * 0.8, -mountOffset * 0.8, 15,
		-A / 2 - mountOffset * 0.8, -mountOffset * 0.8, -1

	addz mountWallThk / 2

	prism_ 5, mountWallThk / 2,
		-A / 2 - mountOffset, -mountOffset, 15,
		-A / 2 - mountOffset, ZZYZX + mountTopOffset, 15,
		A / 2 + mountOffset, ZZYZX + mountTopOffset, 15,
		A / 2 + mountOffset, -mountOffset, 15,
		-A / 2 - mountOffset, -mountOffset, -1

	del 3

	add A / 2, mountDepth / 2 - B - mountWallThk * 2, ZZYZX

	prism_ 14, mountHeight,
		-mountWidth / 2 - mountWallThk, -mountDepth / 2, 15,
		-mountWidth / 2 - mountWallThk, 0, 15,
		-mountWidth / 2, 0, 15,
		-mountWidth / 2, mountDepth / 2, 15,
		mountWidth / 2, mountDepth / 2, 15,
		mountWidth / 2, 0, 15,
		mountWidth / 2 + mountWallThk, 0, 15,
		mountWidth / 2 + mountWallThk, -mountDepth / 2, 15,
		-mountWidth / 2 - mountWallThk, -mountDepth / 2, -1,

		-mountWidth / 2 + mountWallThk, -mountDepth / 2 + mountWallThk, 11,
		-mountWidth / 2 + mountWallThk, mountDepth / 2 - mountWallThk, 11,
		mountWidth / 2 - mountWallThk, mountDepth / 2 - mountWallThk, 11,
		mountWidth / 2 - mountWallThk, -mountDepth / 2 + mountWallThk, 11,
		-mountWidth / 2 + mountWallThk, -mountDepth / 2 + mountWallThk, -1

	addz mountHeight - mountWallThk

	prism_ 5, mountWallThk,
		-mountWidth / 2 + mountWallThk, -mountDepth / 2 + mountWallThk, 11,
		-mountWidth / 2 + mountWallThk, mountDepth / 2 - mountWallThk, 11,
		mountWidth / 2 - mountWallThk, mountDepth / 2 - mountWallThk, 11,
		mountWidth / 2 - mountWallThk, -mountDepth / 2 + mountWallThk, 11,
		-mountWidth / 2 + mountWallThk, -mountDepth / 2 + mountWallThk, -1

	del 2
return

