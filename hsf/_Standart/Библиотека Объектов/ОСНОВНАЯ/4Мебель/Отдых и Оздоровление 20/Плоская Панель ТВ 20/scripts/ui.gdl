
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_ROOT 			=-1
_idxTab = 1
TABID_STYLE			= _idxTab  : _idxTab = _idxTab + 1
TABID_DIMENSIONS	= _idxTab  : _idxTab = _idxTab + 1
TABID_APPEARANCE	= _idxTab  : _idxTab = _idxTab + 1
TABID_LISTING		= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Плоской Панели Телевизора`
ui_current_page gs_ui_current_page

ui_page TABID_STYLE, TABID_ROOT, `Стиль`, "uiTab_Style_flat(1)"
	gosub "Stylepage"

ui_page TABID_DIMENSIONS, TABID_ROOT, `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "DimensionsPage"

ui_page TABID_APPEARANCE, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

ui_page TABID_LISTING, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================


! -----------------------------------------------------------------------------
"Stylepage":
! -----------------------------------------------------------------------------
	! Sizes and Positions
	pageStartY	= 2
	yCurr	= pageStartY
	diffY	= 24

	inFldWdt	= 440
	boolWdt		= 220

	inFldHgt	= 240

	xPos1		= 2

	picWdt		= 90
	picHgt		= 90

	cellWdt		= inFldWdt/4 - 6
	cellHgt		= inFldHgt/2 - 3
! ------------------------------------------------------------------------------

	ui_style 0, 0

	ui_infield{3}	"bSupporter",		xPos1,	yCurr,		boolWdt,	19,
		7, "", 0, 0,
		19, 19, 19, 19,
		"", `Основание`, 0,
		"", `Основание`, 1

	yCurr	= yCurr + diffY

	ui_infield{3}	"iSupporterType", xPos1, yCurr, inFldWdt, inFldHgt,
		1, 7, 7, 2,
		cellWdt, cellHgt, picWdt, picHgt,
		1, stSupporterType[1], SUPPORTER_RECTANGULAR,
		2, stSupporterType[2], SUPPORTER_OVAL,
		3, stSupporterType[3], SUPPORTER_FEET,
		4, stSupporterType[4], SUPPORTER_DESIGNER,
		5, stSupporterType[5], SUPPORTER_WALL,
		6, stSupporterType[6], SUPPORTER_CEILING,
		7, stSupporterType[7], SUPPORTER_MOTORIZED

return


! -----------------------------------------------------------------------------
"DimensionsPage":
! ------------------------------------------------------------------------------
	! Sizes and Positions
	pageStartY 			= 2
	yCurr	= pageStartY
	dy		= 24

	infieldHeight		= 19
	outfieldHeight		= 15
	fieldToPic			= 3
	fieldToField		= 5

	endX = 442
	endY = 266

	outX	= 110
	outXs	= 60
	outXl	= 130
	outX2	= 120
	inX		= 90
	inXs	= 25
	inXl	= 130
	inX2	= 90
	boolX	= outX2 + 2 + inX2

	picX	= 175
	picY	= pageStartY + 98

	xPos1	= 2
	xPos2	= xPos1 + outX2 + 2

	xPos3	= 442 - boolX
	xPos4	= xPos3 + 25
! ------------------------------------------------------------------------------

	ui_style 0, 0

	ui_outfield		`Пропорции Экрана`,			xPos1,	yCurr + 4,	outX2,	outfieldHeight
	ui_infield{3}	"iScreenRatio",			xPos2,	yCurr,		inX2,	infieldHeight,
		8, "", 0, 0,
		0, 0, 0, 0,
		"", stScreenRatio[1], SCREEN_43,
		"", stScreenRatio[2], SCREEN_169,
		"", stScreenRatio[3], SCREEN_239,
		"", stScreenRatio[4], SCREEN_CUST

	yCurr = yCurr + dy

	ui_infield{3}	"bUniformFrameWidths",	xPos1,	yCurr,		boolX,	infieldHeight,
		7, "", 0, 0,
		0, 0, 0, 0,
		"",`Одинаковая Ширина Обрамления`, 0,
		"",`Одинаковая Ширина Обрамления`, 1


	yCurr = pageStartY

	ui_infield{3}	"bPictureOnScreen",		xPos3,	yCurr,		boolX,	infieldHeight,
		7, "", 0, 0,
		0, 0, 0, 0,
		"",`Изобр. на Экране`, 0,
		"",`Изобр. на Экране`, 1

	yCurr = yCurr + dy

	ui_infield{3}	"gs_picture_custom",	xPos3,	yCurr,		boolX,	infieldHeight,
		7, "", 0, 0,
		0, 0, 0, 0,
		"",`Спец. Изображение`, 0,
		"",`Спец. Изображение`, 1

	yCurr = yCurr + dy

	ui_infield{3}	"gs_picture_name",		xPos4,	yCurr,		boolX - 25,	infieldHeight

	yCurr = yCurr + dy


	! --------------------------------------------------------------------------
	ui_separator	xPos1,	yCurr,	442, yCurr


	! --------------------------------------------------------------------------

	if bSupporter then

		if iSupporterType = SUPPORTER_RECTANGULAR | iSupporterType = SUPPORTER_OVAL \
			| iSupporterType = SUPPORTER_FEET | iSupporterType = SUPPORTER_DESIGNER then

			ui_pict 2, picX, picY, 176, 137, 1

			ui_infield "screenSize", picX + 95 - inX / 2, picY - infieldHeight - 2, inX, infieldHeight ui_tooltip `Размер Диагонали Экрана`
			ui_infield "screenHeight", picX - fieldToPic - inX, picY + 48, inX, infieldHeight ui_tooltip `Высота Экрана`
			ui_infield "supporterOffset", picX - fieldToPic - inX, picY + 93 + infieldHeight + fieldToField, inX, infieldHeight ui_tooltip `Смещение Крепления Выше Обрамления`
			ui_infield "screenWidth", picX + 95 - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Экрана`

			if bUniformFrameWidths then
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY - 2, inX, infieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", topFrameWidth)
				ui_outfield outStr, picX - fieldToPic - inX, picY + 94 + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`
			else
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY - 2, inX, infieldHeight ui_tooltip `Ширина Обрамления Вверху`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Ширина Обрамления Сбоку`

				ui_infield "bottomFrameWidth", picX - fieldToPic - inX, picY + 94, inX, infieldHeight ui_tooltip `Ширина Обрамления Внизу`
				ui_infield "sideFrameWidth", picX - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Обрамления Сбоку`
			endif

		endif

		if iSupporterType = SUPPORTER_CEILING then

			ui_pict 5, picX, picY, 176, 137, 1

			ui_infield "screenSize", picX + 95 - inX / 2, picY - infieldHeight - 2, inX, infieldHeight ui_tooltip `Размер Диагонали Экрана`
			ui_infield "supporterOffset", picX - fieldToPic - inX, picY - 2, inX, infieldHeight ui_tooltip `Смещение Подставки Ниже Обрамления`
			ui_infield "screenHeight", picX - fieldToPic - inX, picY + 65, inX, infieldHeight ui_tooltip `Высота Экрана`
			ui_infield "screenWidth", picX + 95 - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Экрана`

			if bUniformFrameWidths then
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY - 2 + dy, inX, infieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", topFrameWidth)
				ui_outfield outStr, picX - fieldToPic - inX, picY + 113 + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`
			else
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY - 2 + dy, inX, infieldHeight ui_tooltip `Ширина Обрамления Вверху`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Ширина Обрамления Сбоку`

				ui_infield "bottomFrameWidth", picX - fieldToPic - inX, picY + 113, inX, infieldHeight ui_tooltip `Ширина Обрамления Внизу`
				ui_infield "sideFrameWidth", picX - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Обрамления Сбоку`
			endif

		endif

		if iSupporterType = SUPPORTER_MOTORIZED then

			ui_pict 6, picX, picY, 176, 137, 1

			ui_infield "screenSize", picX + 95 - inX / 2, picY - infieldHeight - 2, inX, infieldHeight ui_tooltip `Размер Диагонали Экрана`
			ui_infield "screenHeight", picX - fieldToPic - inX, picY + 67, inX, infieldHeight ui_tooltip `Высота Экрана`
			ui_infield "screenWidth", picX + 95 - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Экрана`		

			if bUniformFrameWidths then
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY + 15, inX, infieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", topFrameWidth)
				ui_outfield outStr, picX - fieldToPic - inX, picY + 115 + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`
			else
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY + 15, inX, infieldHeight ui_tooltip `Ширина Обрамления Вверху`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Ширина Обрамления Сбоку`

				ui_infield "bottomFrameWidth", picX - fieldToPic - inX, picY + 115, inX, infieldHeight ui_tooltip `Ширина Обрамления Внизу`
				ui_infield "sideFrameWidth", picX - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Обрамления Сбоку`
			endif

		endif

		if iSupporterType = SUPPORTER_WALL then
			ui_pict 4, picX, picY, 180, 137, 1

			ui_infield "screenSize", picX + 3, picY - infieldHeight - 2, inX, infieldHeight ui_tooltip `Размер Диагонали Экрана`
			ui_infield "supporterOffset", picX + 3 + inX + fieldToField, picY - infieldHeight, inX, infieldHeight ui_tooltip `Расстояние от Стены`
			ui_infield "screenHeight", picX - fieldToPic - inX, picY + 58, inX, infieldHeight ui_tooltip `Высота Экрана`
			ui_infield "screenWidth", picX + 95 - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Экрана`



			if bUniformFrameWidths then
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY + 6, inX, infieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", topFrameWidth)
				ui_outfield outStr, picX - fieldToPic - inX, picY + 108 + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`
			else
				ui_infield "topFrameWidth", picX - fieldToPic - inX, picY + 6, inX, infieldHeight ui_tooltip `Ширина Обрамления Вверху`

				outStr = str("%3.0mm", sideFrameWidth)
				ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Ширина Обрамления Сбоку`

				ui_infield "bottomFrameWidth", picX - fieldToPic - inX, picY + 108, inX, infieldHeight ui_tooltip `Ширина Обрамления Внизу`
				ui_infield "sideFrameWidth", picX - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Обрамления Сбоку`
			endif

		endif

	else

		ui_pict 3, picX, picY, 176, 137, 1

		ui_infield "screenSize", picX + 95 - inX / 2, picY - infieldHeight - 2, inX, infieldHeight ui_tooltip `Размер Диагонали Экрана`
		ui_infield "screenHeight", picX - fieldToPic - inX, picY + 48, inX, infieldHeight ui_tooltip `Высота Экрана`
		ui_infield "screenWidth", picX + 95 - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Экрана`

		if bUniformFrameWidths then
			ui_infield "topFrameWidth", picX - fieldToPic - inX, picY - 2, inX, infieldHeight ui_tooltip `Толщина Обрамления`

			outStr = str("%3.0mm", sideFrameWidth)
			ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

			outStr = str("%3.0mm", topFrameWidth)
			ui_outfield outStr, picX - fieldToPic - inX, picY + 94 + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`

			outStr = str("%3.0mm", sideFrameWidth)
			ui_outfield outStr, picX, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Толщина Обрамления`
		else
			ui_infield "topFrameWidth", picX - fieldToPic - inX, picY - 2, inX, infieldHeight ui_tooltip `Ширина Обрамления Вверху`

			outStr = str("%3.0mm", sideFrameWidth)
			ui_outfield outStr, picX + 170, picY + 137 + fieldToPic + 4, inXs, outfieldHeight ui_tooltip `Ширина Обрамления Сбоку`

			ui_infield "bottomFrameWidth", picX - fieldToPic - inX, picY + 94, inX, infieldHeight ui_tooltip `Ширина Обрамления Внизу`
			ui_infield "sideFrameWidth", picX - inX / 2, picY + 137 + fieldToPic, inX, infieldHeight ui_tooltip `Ширина Обрамления Сбоку`
		endif

	endif

return

! -----------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! -----------------------------------------------------------------------------
	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_2D"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel2D_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"

		if gs_detlevel_3D_m = DETLEVEL_3D_DETAILED then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_resol"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_resol_flat(1)"
		endif

		if gs_detlevel_3D_m <> DETLEVEL_3D_OFF then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
		endif

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "sc_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "pd_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "fr_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "bt_mat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"

	file_dependence "ui_list_detailLevel2D_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_object_Representation" parameters all	listFieldID  	= TABID_APPEARANCE,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray

return


! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_LISTING,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames

return

