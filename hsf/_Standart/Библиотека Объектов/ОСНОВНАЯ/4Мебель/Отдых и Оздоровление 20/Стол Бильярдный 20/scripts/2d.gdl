
!! Billiard Table.gsm

CALL "Resize_A_B_ZZYZX" Parameters ALL

HOTSPOT2 	0,0	!! Deafault Hotspot
HOTSPOT2 	0,b/2
HOTSPOT2 	a/2,b
HOTSPOT2 	a,b/2
HOTSPOT2 	a/2,0
HOTSPOT2 	a/2,b/2

! Minimal space
reqBMinSpace = 0
if MSFront > EPS then
	call "fa_minimalspace" parameters all MSType = 4,
									unID = 3000,
									MSSide = MSFront,
									MSRear = MSFront,
				returned_parameters reqBMinSpace,
									MSFillType,
									MSFillPen,
									MSFillBackPen,
									MSContLineType,
									MSContLinePen
endif
if reqBMinSpace <> 0 then
	FILL MSFillType
	line_type MSContLineType
	pen MSContLinePen
	add2 a/2, b/2
	for i = 1 to 2
		for j = 1 to 2
			POLY2_B		6, 2+64, MSFillPen, MSFillBackPen,
						A/2, pocketCornerPosY, 1,
						A/2, B/2, 1,
						pocketCornerPosX, B/2, 1,
						pocketCornerPosX, pocketCornerPosY, 900,
						0, -90, 4000,
						A/2, pocketCornerPosY, -1
			mul2 1, -1
		next j
		del j-1
		mul2 -1,1
	next i
	del i-1
	del 1
endif

line_type 1
PEN gs_cont_pen

add2 a/2, b/2
for i = 1 to 2
	circle2 pocketCornerPosX, pocketCornerPosY, pocketRadius
	circle2 pocketCornerPosX, -pocketCornerPosY, pocketRadius
	circle2 pocketMiddlePosX, 0, pocketRadius
	arc2    pocketMiddlePosX, 0,    railWidth-pocketRadius,     -90,      90
	add2 pocketCornerPosX, pocketCornerPosY
	gosub "corner"
	del 1
	mul2 1, -1
	add2 pocketCornerPosX, pocketCornerPosY
	gosub "corner"
	del 2
	mul2 -1, 1
next i
del i-1
for i = 1 to 2
	line2 pocketMiddlePosX, pocketRadius, pocketMiddlePosX, surfaceLength/2-pocketString+cushionWidth
	line2 pocketMiddlePosX, -pocketRadius, pocketMiddlePosX, -(surfaceLength/2-pocketString+cushionWidth)
	line2 surfaceWidth/2+railWidth, surfaceLength/2-pocketString+cushionWidth, surfaceWidth/2+railWidth, -(surfaceLength/2-pocketString+cushionWidth)
	mul2 -1, 1
next i
del i-1

for i = 1 to 2
	line2 surfaceWidth/2-pocketString, surfaceLength/2+cushionWidth, -(surfaceWidth/2-pocketString), surfaceLength/2+cushionWidth
	line2 surfaceWidth/2-pocketString, surfaceLength/2+railWidth, -(surfaceWidth/2-pocketString), surfaceLength/2+railWidth
	line2 surfaceWidth/2-pocketString, surfaceLength/2, -(surfaceWidth/2-pocketString), surfaceLength/2

	line2 surfaceWidth/2-pocketString, surfaceLength/2, surfaceWidth/2-pocketString+cushionWidth, surfaceLength/2+cushionWidth
	line2 -(surfaceWidth/2-pocketString), surfaceLength/2, -(surfaceWidth/2-pocketString+cushionWidth), surfaceLength/2+cushionWidth

	mul2 1, -1
next i
del i-1

for i = 1 to 2
	for j = 1 to 2
		line2 surfaceWidth/2, surfaceLength/2-pocketString, surfaceWidth/2, pocketRadius+cushionWidth
		line2 surfaceWidth/2, pocketRadius+cushionWidth, surfaceWidth/2+cushionWidth, pocketRadius
		line2 surfaceWidth/2, surfaceLength/2-pocketString, surfaceWidth/2+cushionWidth, surfaceLength/2-pocketString+cushionWidth
		mul2 -1, 1
	next j
	del j -1
	mul2  1, -1
next i
del i-1

FILL gs_fill_type

POLY2_B      13, 2+64,      gs_fill_pen, gs_back_pen,
		pocketCornerPosX,	B/2,				1,
		pocketCornerPosX,	pocketCornerPosY,	901,
		0,					-90,				4001,
		A/2,				-pocketCornerPosY,	1,
		pocketCornerPosX,	-pocketCornerPosY,	901,
		0,					-90,				4000,
		-pocketCornerPosX,	-B/2,				1,
		-pocketCornerPosX,	-pocketCornerPosY,	901,
		0,					-90,				4001,
		-A/2,				pocketCornerPosY,	1,
		-pocketCornerPosX,	pocketCornerPosY,	901,
		0,					-90,				4001,
		pocketCornerPosX,	B/2,				-1
end


"corner":
POLY2_B      15, 1,      gs_fill_pen, gs_back_pen,
		-(pocketString+pocketRadMod),	cushionWidth-pocketRadMod,	1,
		-(pocketString+pocketRadMod-cushionWidth),	cushionWidth-pocketRadMod,	1,
		-pocketRadMod,					pocketRadMod,	1,
		0,								0,				900,
		0,								-180,			4001,
		pocketRadMod,					-pocketRadMod,	1,
		cushionWidth-pocketRadMod,		-(pocketString+pocketRadMod-cushionWidth),	1,
		cushionWidth-pocketRadMod,		-(pocketString+pocketRadMod),	1,
		railWidth-pocketRadMod,			-(pocketString+pocketRadMod),	1,
		railWidth-pocketRadMod,			0,		1,
		0,								0,		900,
		0,								90,		4001,
		0,								railWidth-pocketRadMod,	1,
		-(pocketString+pocketRadMod),	railWidth-pocketRadMod,	1,
		-(pocketString+pocketRadMod),	cushionWidth-pocketRadMod,	1

return
