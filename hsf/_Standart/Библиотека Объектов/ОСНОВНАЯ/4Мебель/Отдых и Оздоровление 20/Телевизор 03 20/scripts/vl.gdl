
call "FM_types" parameters all

!Hide all parameters from UI
hideparameter all  "A", "B", "ZZYZX"

values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_SIMPLE, `Простой`,
								DLEV3D_OFF, `Откл.`
values{2} "iDet2D" 	DETLEVEL_2D_DETAILED, `Детальный`,
					DETLEVEL_2D_SIMPLE, `Простой`
values "gs_resol" RANGE [3,)
values "hSupporter" range [0, ]
values "hTv" range [0, ]
values "ZZYZX" range (hSupporter, ]
values "screenSize" range (0,)
values "A" range (2*frameThickness + 0.06, )

values{2} "iScreenRatio"	SCREEN_43,		"4:3",
							SCREEN_169,		"16:9",
							SCREEN_239,		"2.39:1",
							SCREEN_CUST,	`Специальные`

if not (gs_cont_pen) then parameters gs_cont_pen=8

if not(bSupporter) then
	lock "pd_mat"
	hideparameter "pd_mat"
	lock "sl_mat"
	hideparameter "sl_mat"
endif

if iScreenRatio <> SCREEN_CUST then
	if iScreenRatio	= SCREEN_43		then screenRatioRate = 4/3
	if iScreenRatio	= SCREEN_169	then screenRatioRate = 16/9
	if iScreenRatio	= SCREEN_239	then screenRatioRate = 2.39

	parameters screenRatioRate = screenRatioRate
	lock "screenRatioRate"
endif

if GLOB_MODPAR_NAME= "ZZYZX" then
	parameters hSupporter = ZZYZX-hTv
endif

if GLOB_MODPAR_NAME="hSupporter" or GLOB_MODPAR_NAME="hTv" then
	parameters ZZYZX = hSupporter + hTv
endif

if not (gs_picture_custom) then hideparameter "gs_picture_name"

if gs_detlevel_3D_m = DLEV3D_SIMPLE then hideparameter "gs_resol"
if gs_detlevel_3D_m = DLEV3D_OFF then hideparameter "gs_resol", "gs_shadow"

if bUnitSizing then
	lock "A"
	lock "ZZYZX"
	lock "htv"
	A = screenSize * cos(atn(1/screenRatioRate)) + 2*frameThickness
	htv = screenSize * sin(atn(1/screenRatioRate)) + topFrameThickness + bottomFrameThickness
	ZZYZX = htv + hSupporter
	parameters A = A , htv = htv, ZZYZX = ZZYZX
else
	screenSize = sqr((A - 2*frameThickness)^2 + (ZZYZX - topFrameThickness - bottomFrameThickness - hSupporter)^2)
	screenRatioRate = (A - 2*frameThickness) / (ZZYZX - frameThickness - bottomFrameThickness - hSupporter)
	lock "screenSize"
	lock "screenRatioRate"
	lock "iScreenRatio"
	parameters screenSize = screenSize, screenRatioRate = screenRatioRate
endif


if not(bUsePicture) then
	lock	"gs_picture_custom",
			"gs_picture_name"
else
	if not(gs_picture_custom) then
		lock	"gs_picture_name"
	endif
endif
