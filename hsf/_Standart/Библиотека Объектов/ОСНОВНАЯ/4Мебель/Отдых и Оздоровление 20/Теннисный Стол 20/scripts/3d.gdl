
!! Table Tennis.gsm

pen gs_cont_pen

if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_detlevel_3D_m = DLEV3D_SIMPLE then net = 0

IF gs_shadow=0 then SHADOW OFF


vv=0.02

for y=1 to 2

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!legs!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
a=2.754
b=1.525

	addx -a/4
	for x=1 to 2
		material legmat
		prism_  4, 0.762,
				-a/4+0.2, -b/2+0.1, 15,
				0, 0.05, 115,
				0.05, 0, 115,
				0, -0.05, 115
		prism_  4, 0.762,
				a/4-0.25, -b/2+0.1, 15,
				0, 0.05, 115,
				0.05, 0, 115,
				0, -0.05, 115
		addz 0.3
		prism_  4, 0.05,
				-a/4+0.25, -b/2+0.1125, 15,
				a/2-0.5, 0, 115,
				0, 0.025, 115,
				-a/2+0.5, 0, 115
		del 1                   !!!!addz 0.3!!!!

		material fmat
		addz 0.762-0.03
		prism_  4, 0.03,
				-0.05, -0.025, 15,
				0.05, -0.025, 15,
				0.05, 0, 8,
				-0.05,0 ,13
		roty 90
		addz -0.02
		prism_  4, 0.04,
				0, 0, 15,
				0.762-0.33-0.032, b/2-0.15+0.0125-0.01, 115,
				-0.018, 0, 115,
				0, 0.025, 15
		addz -0.01
		prism_  4, 0.06,
				0.762-0.33-0.032+0.025, b/2-0.15+0.0125-0.01, 15,
				0, 0.01, 115,
				-0.025-0.018, 0, 115,
				0, -0.01, 115
		del 2                       !!!!roty 90, addz -0.02!!!!
		del 1                       !!!!addz-0.01!!!!
		del 1                       !!!!add -a/4,0,0.762-0.03!!!!

		rotz 180
	next x
	del 2                       !!!!2*rotz 180!!!!
	del 1                       !!!!addx -a/4!!!!


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!table!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	addz 0.762
	material tbmat
	prism_  4, 0.025,
			-a/2, -b/2, 15,
			a/2, 0, 115,
			0, b, 115,
			-a/2, 0, 115
	addz 0.025
	prism_  8, 0.002,
			-a/2+vv, -b/2+vv, 15,
			0, -b/2+vv, 15,
			0, b/2-vv, 15,
			-a/2+vv, b/2-vv, 15,
			-a/2+vv, vv/2, 15,
			-0.05, vv/2, 15,
			-0.05, -vv/2, 15,
			-a/2+vv, -vv/2, 15
	material lmat
	prism_  12, 0.002,
			-a/2, -b/2, 15,
			0, -b/2, 15,
			0, -b/2+vv, 15,
			-a/2+vv, -b/2+vv, 15,
			-a/2+vv, -vv/2, 15,
			-0.05, -vv/2, 15,
			-0.05, vv/2, 15,
			-a/2+vv, vv/2, 15,
			-a/2+vv, b/2-vv, 15,
			0, b/2-vv, 15,
			0, b/2, 15,
			-a/2, b/2, 15
	del 2                       !!!!addz 0.762, addz 0.025!!!!


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!net!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if net=1 then

		material netholdmat
		add 0, -b/2, 0.789
		rotz -90

		rotx 90
		addz -0.01
		prism_  9, 0.02,
				-0.04, -0.06, 15,
				0.05, -0.06, 15,
				0.12, -0.01, 15,
				0.12, 0.01, 15,
				-0.05, 0.01, 15,
				-0.06, 0, 15,
				0, 0, 15,
				0, -0.04, 15,
				-0.04, -0.04, 15
		del 2
		add 0.1, 0, 0.01
		RESOL gs_resol
		cylind 0.15, 0.005
		del 1
		add -0.02, 0, -0.027-0.0025
		cylind 0.0025, 0.01
		addz -0.0105
		RESOL 1.5*gs_resol
		cylind 0.0105, 0.0025
		addz -0.04
		cylind 0.02, 0.0025
		addz -0.005
		cylind 0.005, 0.005
		addz -0.007
		cylind 0.007, 0.01
		del 5                       !!!!6*add!!!!

		del 1                       !!!!rotz 90!!!!
		del 1                       !!!!add 0, -b/2, 0.789!!!!
	endif


rotz 180
next y
del 2                       !!!!rotz 180!!!!


	addz 0.809
	IF gs_detlevel_3D_m = DLEV3D_DETAILED then
		IF net THEN
		material netmat

			prism_  4, 0.13,
					-0.001, -b/2-0.095, 15,
					0.001, -b/2-0.095, 15,
					0.001, b/2+0.095, 15,
					0.001,  b/2+0.095, 15
		ENDIF
	ENDIF
	del 1                       !!!!addz 0.799!!!!


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!area, wall!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if area=1 then
	addz -0.05
	material areamat
	prism_  4, 0.05,
			-areaLength/2, -areaWidth/2, 15,
			 areaLength/2, -areaWidth/2, 15,
			 areaLength/2,  areaWidth/2, 15,
			-areaLength/2,  areaWidth/2, 15
	del 1                   !!!!addz -0.05!!!!
	if wall=1 then
		material wallmat
		wv=0.025
		prism_  10, 1,
				-areaLength/2, -areaWidth/2, 15,
				 areaLength/2, -areaWidth/2, 15,
				 areaLength/2,  areaWidth/2, 15,
				-areaLength/2,  areaWidth/2, 15,
				-areaLength/2, -areaWidth/2, -1,

				-areaLength/2+wv, -areaWidth/2+wv, 15,
				 areaLength/2-wv, -areaWidth/2+wv, 15,
				 areaLength/2-wv,  areaWidth/2-wv, 15,
				-areaLength/2+wv,  areaWidth/2-wv, 15,
				-areaLength/2+wv, -areaWidth/2+wv, -1
	endif
endif


