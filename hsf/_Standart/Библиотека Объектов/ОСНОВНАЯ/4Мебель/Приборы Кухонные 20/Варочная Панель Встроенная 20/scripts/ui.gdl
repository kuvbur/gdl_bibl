
ui_dialog `Параметры Варочной Панели`
ui_current_page gs_ui_current_page

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT									= -1
_idxTab = 1

TABID_Style								= _idxTab  : _idxTab = _idxTab + 1
TABID_Dimensions						= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation					= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentationSurfaces			= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace						= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions						= _idxTab  : _idxTab = _idxTab + 1


! =============================================================================
! Display actual tabpages
! =============================================================================

ui_page TABID_Style, TABID_ROOT, `Стиль`, "uiTab_Style_flat(1)"
	gosub "StylePage"

ui_page TABID_Dimensions, TABID_ROOT, `Размеры и Столешница`, "uiTab_Dimension_flat(1)"
	gosub "DimensionsPage"

ui_page TABID_2DRepresentation, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

ui_page TABID_3DRepresentationSurfaces, TABID_ROOT, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationSurfacesPage"

ui_page TABID_MinimalSpace, TABID_ROOT, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalPage"

ui_page TABID_Descriptions, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Cooktops_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StylePage":
! ------------------------------------------------------------------------------
	pageStartY 	= 5
	x0 			= 0

	ui_infield{3} "CooktopType_m", x0, pageStartY, 444, 260,
		1, 5,												! CooktopBultIn_OB_CooktopType_Matrix.png
		13, 4,
		105, 105, 90, 90,
		1, "", 1,
		2, "", 2,
		3, "", 3,
		4, "", 4,
		5, "", 5,
		6, "", 6,
		7, "", 7,
		8, "", 8,
		9, "", 9,
		10, "", 10,
		11, "", 11,
		12, "", 12,
		13, "", 13

return

! ------------------------------------------------------------------------------
"DimensionsPage":
! ------------------------------------------------------------------------------

	pageStartY	= 4
	x0			= 0  			! start Position X Coordinate
	x1			= x0 + 60
	x2			= x1 + 66
	x3			= x2 + 29
	x4			= x3 + 31
	x5			= x4 + 45
	x6			= x5 + 67
	x7			= x6 + 58
	xEnd		= 444
	_yCurr		= pageStartY   	! start Position Y Coordinate
    _dy			= 22
	_picy		= 98
	y1			= pageStartY + 95
	yEnd		= 266
	_infHeight	= 19
	_infWidth 	= 88
	_outfHeight	= 15


	ui_outfield `Высота Столешницы`,	x0, _yCurr,		x2 - x0 - 1, _outfHeight

	ui_infield	"hCountertop",		x2, _yCurr - 4,	_infWidth,	_infHeight ui_tooltip `Высота Столешницы`

	ui_infield{3} "dob_face", x5, _yCurr - 4, xEnd - x5, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Двусторонняя`, 0,
					"", `Двусторонняя`, 1

	_yCurr = _yCurr + _dy

	ui_outfield `Толщина`,	x0, _yCurr,		x2 - x0 - 1, _outfHeight

	ui_infield	"count_th",		x2, _yCurr - 4,	_infWidth,	_infHeight ui_tooltip `Толщина Столешницы`

	ui_infield{3} "bBacksplash", x5, _yCurr - 4, x7 - x5 - 1, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Фартук`, 0,
					"", `Фартук`, 1

	ui_infield	"bs_h",		x7, _yCurr - 4,	_infWidth,	_infHeight ui_tooltip `Высота Фартука`

	_yCurr = _yCurr + _dy

	ui_outfield `Тип Торца`,	x0, _yCurr,		x2 - x0 - 1, _outfHeight

	ui_infield{3}	"gs_count_type_m",	x2,	_yCurr - 4,	_infWidth,	25,
		2,6,2,1,
		42,21,42,21,
		1, "", 1,
		2, "", 2

	_yCurr = _yCurr + _dy + 2

	ui_separator x0,_yCurr, xEnd, _yCurr

	ui_infield	"A",		x4, _picy - 22,	_infWidth,	_infHeight ui_tooltip `Ширина Столешницы`

	if not(dob_face) and bBacksplash then
		ui_pict 3, x3, _picy, 132, 119, 1
	else
		ui_pict 4, x3, _picy, 132, 119, 1
	endif
	
	ui_infield	"BB",	x3 - 93, 	_picy + 50,		_infWidth,	_infHeight ui_tooltip `Глубина Варочной Панели`
	ui_infield	"B",	x3 + 136, 	_picy + 50,		_infWidth,	_infHeight ui_tooltip `Глубина Столешницы`
	ui_infield	"AA",	x4,			_picy + 121,	_infWidth,	_infHeight ui_tooltip `Ширина Варочной Панели`

	_yCurr = _yCurr + 175

	ui_outfield `Смещение по X`,	x0, 	_yCurr,		x2 - x0  + 5, _outfHeight
	ui_infield	"CooktopDistX",				x2 + 6, _yCurr - 4,	_infWidth,	_infHeight

	ui_outfield `Смещение по Y`,	x5 - 6, _yCurr,		x7 - x5 + 5, _outfHeight
	ui_infield	"CooktopDistY",				x7, _yCurr - 4,	_infWidth,	_infHeight

return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------
	pageStartY	= 5
	_yCurr 		= pageStartY

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 5, !SYMBOLCLAS_COOKTOP
													gs_cont_pen 		= gs_cont_pen,
													gs_fill_type 		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen 		= gs_back_pen,
													b2DSymbol			= b2DSymbol,
													iCooktopSymbolType	= iCooktopSymbolType,
													stSymbolTypes		= stCooktopSymbolTypes,
													iFillListID			= TABID_2DRepresentation,
													pageStartY			= _yCurr

return

! ------------------------------------------------------------------------------
"3DRepresentationSurfacesPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "count_mat"
		SurfItemID = SurfItemID + 1
	
		if bBacksplash then
			surfacesArray[SurfItemID]	= "bs_mat"
			SurfItemID = SurfItemID + 1
		endif
	
		surfacesArray[SurfItemID]	= "bmat"
		SurfItemID = SurfItemID + 1
	
		if	CooktopType_m = 2 |\
			CooktopType_m = 6 |\
			CooktopType_m = 7 |\
			CooktopType_m = 11 |\
			CooktopType_m = 12 then

			surfacesArray[SurfItemID]	= "matarc"
			SurfItemID = SurfItemID + 1
		endif
	

		if	CooktopType_m = 3 |\
			CooktopType_m = 4 |\
			CooktopType_m = 8 |\
			CooktopType_m = 9 |\
			CooktopType_m = 13 then

			surfacesArray[SurfItemID]	= "gbmat"
			SurfItemID = SurfItemID + 1
		
			surfacesArray[SurfItemID]	= "ghmat"
			SurfItemID = SurfItemID + 1
		endif
	

		if	CooktopType_m = 3 |\
			CooktopType_m = 8 |\
			CooktopType_m = 13 then

			surfacesArray[SurfItemID]	= "SwitchMat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k

	call "ui_object_3DRepresentation" parameters 	surfacesArray 		= surfacesArray,
													childFlagArray 		= childFlagArray,
													bShowResol			= 1,
													bEdgeVisibility		= 1,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													iSurfaceListID		= TABID_3DRepresentationSurfaces

return

! ------------------------------------------------------------------------------
"MinimalPage":
! ------------------------------------------------------------------------------

	xPict1 = 130
	yPict1 = pageStartY + 40
	xPict2 = 115
	yPict2 = pageStartY + 16
	dist = 10
	wGap = 50
	hGap = 18
	xField = 90
	yField = 18
	fieldToPic = 3
	fieldToField = 5
	
	if dob_face = 0 then
		ui_pict 1, xPict1, yPict1, 159, 163, 1 

		ui_infield "A", 		xPict1 + 36, 							yPict1 - yField - fieldToPic,	xField, yField ui_tooltip `Ширина`
		ui_infield "B", 		xPict1 + 159 + fieldToPic, 				yPict1 + 42, 					xField, yField ui_tooltip `Длина`
		ui_infield "MSFront", 	xPict1 + 159 + fieldToPic, 				yPict1 + 119, 					xField, yField ui_tooltip `Минимальное Пространство Спереди`
		ui_infield "MSSide", 	xPict1 + 36 - xField - fieldToField, yPict1 - yField - fieldToPic, 	xField, yField ui_tooltip `Минимальное Пространство Сбоку 1`
		ui_infield "MSSide2", 	xPict1 + 36 + xField + fieldToField, yPict1 - yField - fieldToPic, 	xField, yField ui_tooltip `Минимальное Пространство Сбоку 2`

	else
		ui_pict 2, xPict2, yPict2, 167, 222, 1

		ui_infield "RMSSide", 	xPict2 + 42 - xField - fieldToField, 	yPict2 - yField - fieldToPic, xField, yField ui_tooltip `Минимальное Пространство Сзади со Стороны 1`
		ui_infield "A", 		xPict2 + 42,							yPict2 - yField - fieldToPic, xField, yField ui_tooltip `Ширина`
		ui_infield "RMSSide2", 	xPict2 + 42 + xField + fieldToField, 	yPict2 - yField - fieldToPic, xField, yField ui_tooltip `Минимальное Пространство Сзади со Стороны 2`
		
		ui_infield "MSRear", 	xPict2 + 167 + fieldToPic, yPict1, 		xField, yField ui_tooltip `Минимальное Пространство Сзади`
		ui_infield "B",			xPict2 + 167 + fieldToPic, yPict1 + 64, 	xField, yField ui_tooltip `Длина`
		ui_infield "MSFront", 	xPict2 + 167 + fieldToPic, yPict1 + 142, 	xField, yField ui_tooltip `Минимальное Пространство Спереди`
		
		ui_infield "MSSide", 	xPict2 - 35, 		yPict2 + 222 + fieldToPic, xField, yField ui_tooltip `Минимальное Пространство Сбоку 1`
		ui_infield "MSSide2", 	xPict2 + 167 - 55, 	yPict2 + 222 + fieldToPic, xField, yField ui_tooltip `Минимальное Пространство Сбоку 2`
	endif

return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "gs_list_volts"
		stParamNames[7] = "FM_Type"
		stParamNames[8] = "FM_InventoryNumber"
		stParamNames[9] = "FM_SerialNumber"
		stParamNames[10] = "FM_ProductionYear"
		stParamNames[11] = "FM_ObjectWeight"
		stParamNames[12] = "FM_ObjectWeightUnit"
		stParamNames[13] = "FM_PowerConsumption"
		stParamNames[14] = "FM_Egress"
		stParamNames[15] = "gs_list_custom1"
		stParamNames[16] = "gs_list_custom2"
		stParamNames[17] = "gs_list_custom3"
		stParamNames[18] = "gs_list_custom4"
		stParamNames[19] = "gs_list_custom5"
	
	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif
	
	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames

return

