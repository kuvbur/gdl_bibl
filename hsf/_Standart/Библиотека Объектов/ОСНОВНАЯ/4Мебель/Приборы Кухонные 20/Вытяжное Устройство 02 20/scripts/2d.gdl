
! Hood and Vent

if bStorySensitive then
	if GLOB_CSTORY_ELEV <= GLOB_HSTORY_ELEV then	! home story, or lower
		hood_line_type = overhead_line_type
	else											! upper home story
		hood_line_type = gs_line_Type
	endif
else
	hood_line_type = gs_line_type
endif

! --- Hotspots ---------------------------------------------------------
unID = 1
hotspot2 A/2, B: unID=unID+1

if (GLOB_HSTORY_ELEV + GLOB_HSTORY_HEIGHT) >= GLOB_CSTORY_ELEV then
	hotspot2 0, B/2: unID=unID+1
	hotspot2 A, B/2: unID=unID+1

	call "Resize_A_B_ZZYZX" parameters ALL
endif


! --- 2D ----------------------------------------------------------------
pen gs_cont_pen
fill gs_fill_type
line_type hood_line_type

if bStorySensitive then
	if (GLOB_HSTORY_ELEV + GLOB_HSTORY_HEIGHT) >= GLOB_CSTORY_ELEV then				! home story, or home story +1
		gosub "hood_2d"
	endif
else
	gosub "hood_2d"
endif

! --- start of modifying to Shaft

DrawingNum = 1

IF bShowMEP THEN

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_line_type = hood_line_type,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1
	DEL 1
ENDIF

! --- end of modifying to Shaft

end

! =============================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
! =============================================================================

! -----------------------------------------------------------------------------
"rectsymbol":
! -----------------------------------------------------------------------------
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,     b,      1,
			a,     b,      1,
			a,     0,      1,
			0,     0,      1,
			0,     b,     -1
return

! -----------------------------------------------------------------------------
"hood_1":
! -----------------------------------------------------------------------------
	gosub "rectsymbol"
	hotspot2 a/2, b-0.18: unID=unID+1
	circle2 a/2, b-0.18, 0.11

	ConEdit_1 = 1
return

! -----------------------------------------------------------------------------
"hood_2":
! -----------------------------------------------------------------------------
	gosub "rectsymbol"
	mul2 (10/6)*a, (10/5)*b
	POLY2_B       6, 1, gs_fill_pen,gs_back_pen,
				   0.425,          0.5,      1,
				   0.175,          0.5,      1,
				   0.175,         0.27,      1,
					 0.3,         0.21,      1,
				   0.425,         0.27,      1,
				   0.425,          0.5,      1

	LINE2               0,            0,          0.3,         0.21
	LINE2             0.3,         0.21,          0.6,            0
	del 1
return

! -----------------------------------------------------------------------------
"hood_3":
! -----------------------------------------------------------------------------
	mul2 (10/9)*a, (10/5)*b

	poly2_b 17, 1+2+4+64,gs_fill_pen,gs_back_pen,
			   0.03,            0,      1,
				0.87,            0,      1,
				0.87,         0.03,    900,
				   0,           90,   4001,
				 0.9, 0.3557475324498,      1,
				0.87, 0.3557475324498,    900,
				   0, 48.48450364693,   4001,
		0.5671425505895, 0.01361595941019,    900,
				   0, 38.82488062727,   4001,
				0.31,          0.5,      1,
		0.3328574494105, 0.01361595941019,    900,
				   0, 38.82488062727,   4001,
				0.03, 0.3557475324498,    900,
				   0, 48.48450364693,   4001,
				   0,         0.03,      1,
				0.03,         0.03,    900,
				   0,           90,   4001

	poly2_b 7, 1,gs_fill_pen,gs_back_pen,
				0.31,          0.5,      1,
				0.31,        0.285,      1,
				0.45, 0.3897434547245,    900,
				   0, 106.3947134638,   4001,
				0.59,        0.285,      1,
				0.59,          0.5,      1,
				0.31,          0.5,      1
	del 1

	ConEdit_1 = 0

return

! -----------------------------------------------------------------------------
"hood_4":
! -----------------------------------------------------------------------------
	mul2 (100/89.5)*a, (100/56.4)*0.93*b

	if bWallMounted then
		mul2 1, 1.127235816
		poly2_b 7, 1+2+64, gs_fill_pen, gs_back_pen,
				0, 0, 1,
				0.4477300978155, -1.9,    900,
				0, -26.5056,   4001,
				0.895, 0.0001173274014294,      1,
				0.895, 0.538,      1,
				0, 0.538,      1,
				0, 0, 1
	else
		poly2_b 9, 1+2+64, gs_fill_pen, gs_back_pen,
				0,            0,      1,
				0.4477300978155, -1.755179604082,    900,
				0, -28.60584669403,   4001,
				0.895, 0.0001173274014294,      1,
				0.895, 0.5314070168942,      1,
				0.4475, -0.8181012759901,    900,
				0, 36.69127528966,   4001,
				0, 0.5314070168942,      1,
				0,            0,      1
	endif

	add2 0.288, 0.288

	line2 0.32, 0.25, 0, 0.25
	line2 0, 0.25, 0, -0.083
	line2 0, 0, 0.32, 0
	line2 0.32, -0.083, 0.32, 0.25
	line2 0, -0.083, 0.32, -0.083

	if bWallMounted then del 1
	del 2

	ConEdit_1 = 0

return

! -----------------------------------------------------------------------------
"hood_5":
! -----------------------------------------------------------------------------
	add2 a/2, b/2
	POLY2_B 8, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-a/2, b/2, 1+32,
		a/2, b/2, 1+32,
		a/2, -b/2+ar, 1+32,
		a/2-ar, -b/2+ar, 900,
		ar, -90, 4001,
		-a/2+ar, -b/2, 1+32,
		-a/2+ar, -b/2+ar, 900,
		ar, -90, 4001

	if bWallMounted then
		add2 0, b/2-sks/2
		RECT2 -skl/2, -sks/2, skl/2, sks/2
		del 1
		add2 0, b/2-ss/2
		RECT2 -sl/2, -ss/2, sl/2, ss/2
		del 1
	else
		RECT2 -skl/2, -sks/2, skl/2, sks/2
		RECT2 -sl/2, -ss/2, sl/2, ss/2
	endif
	del 1

	ConEdit_1 = 0

return

! -----------------------------------------------------------------------------
"hood_6":
! -----------------------------------------------------------------------------
	gosub "rectsymbol"

	add2 (315/900)*a, 0.5*b

	if not(bWallMounted) then add2 0, -0.125

	Line2 0,0, (275/900)*a, 0
	Line2 (275/900)*a, 0, (275/900)*a, 0.25
	Line2 (275/900)*a, 0.25, 0, 0.25
	Line2 0, 0.25, 0, 0

	if not(bWallMounted) then del 1
	del 1

	ConEdit_1 = 0

return

! -----------------------------------------------------------------------------
"hood_7":
! -----------------------------------------------------------------------------
	add2 a/2, 0
	poly2_b 8, 1+2+4+64,gs_fill_pen,gs_back_pen,
		a/2-ds, 0, 1,
		a/2, ds, 1,
		a/2, b-ds, 1,
		a/2-ds, b, 1,
		-a/2+ds, b, 1,
		-a/2, b-ds, 1,
		-a/2, ds, 1,
		-a/2+ds, 0, 1

	hotspot2 a/2-ds, 0: unID=unID+1
	hotspot2 a/2, ds: unID=unID+1
	hotspot2 a/2, b-ds: unID=unID+1
	hotspot2 a/2-ds, b: unID=unID+1

	hotspot2 -a/2+ds, 0: unID=unID+1
	hotspot2 -a/2, ds: unID=unID+1
	hotspot2 -a/2, b-ds: unID=unID+1
	hotspot2 -a/2+ds, b: unID=unID+1
	del 1

	if not(bWallMounted) then
		dy=(B-0.3)/2
		Line2 A/2+0.15,B-dy,A/2-0.15,B-dy
		Line2 A/2-0.15,B-dy,ds,B
		Line2 A/2+0.15,B-dy,A-ds,B
	else
		dy=0
	endif

	Line2 A/2+3",B-0.15-dy,A/2-3",B-0.15-dy
	Line2 A/2,B-0.15+3"-dy,A/2,B-0.15-3"-dy

	Line2 A/2+0.15,B-0.3-dy,A/2+0.15,B-dy
	Line2 A/2+0.15,B-0.3-dy,A/2-0.15,B-0.3-dy
	Line2 A/2-0.15,B-0.3-dy,A/2-0.15,B-dy

	Line2 0, B-ds,A/2-0.15,B-dy
	Line2 a, B-ds,A/2+0.15,B-dy


	Line2 A/2-0.15,B-0.3-dy,0,0+ds
	Line2 A/2-0.15,B-0.3-dy,0+ds,0

	Line2 A/2+0.15,B-0.3-dy,A-ds,0
	Line2 A/2+0.15,B-0.3-dy,A,0+ds

	hotspot2 A/2,b-0.15-dy: unID=unID+1

	ConEdit_1 = 0

return

! -----------------------------------------------------------------------------
"hood_8":
! -----------------------------------------------------------------------------
	gosub "rectsymbol"

	mul2 (10/6)*a, (10/5)*b
	if not(bWallMounted) then
		mul2 1, 1/1.6

		parts=2
		mask=0
	else
		parts=1
		mask=1
	endif

	for i=1 to parts
		POLY2_       11, 1,
			   0.215,         0.26,      1,
			   0.385,         0.26,      1,
			   0.385,          0.3,    900,
				   0,           90,   4001,
			   0.425,          0.3,      1,
			   0.425,          0.5,      mask,
			   0.175,          0.5,      1,
			   0.175,          0.3,      1,
			   0.215,          0.3,    900,
				   0,           90,   4001,
			   0.215,         0.26,      1

		POLY2_       5, 1,
			   -0.2+0.3,        0.134,      1,
			   0.2+0.3,        0.134,      1,
			   0.2+0.3,          0.5,      mask,
			   -0.2+0.3,          0.5,      1,
			   -0.2+0.3,        0.134,      1

		hotspot2 0.3, 0.4: unID=unID+1

		mul2 1, -1
		add2 0, -2*0.4
	next i
	del 2*parts

	if not(bWallMounted) then del 1
	del 1

	ConEdit_1 = 0

return

! -----------------------------------------------------------------------------
"hood_9":
! -----------------------------------------------------------------------------
	gosub "rectsymbol"

	line2 a/2+3",		b-0.075,		a/2-3",		b-0.075
	line2 a/2,			b-4"-1/4"+3",	a/2,		b-4"-1/4"-3"

	line2 a/2+0.135,	b-0.15,			a/2+0.135,	b
	line2 a/2+0.135,	b-0.15,			a/2-0.135,	b-0.15
	line2 a/2-0.135,	b-0.15,			a/2-0.135,	b

	line2 a/2-0.135,	b-0.15,			0,			0
	line2 a/2+0.135,	b-0.15,			a,			0

	hotspot2 a/2, b-0.075: unID=unID+1

	ConEdit_1 = 0
return

! -----------------------------------------------------------------------------
"hood_10":
! -----------------------------------------------------------------------------
	gosub "rectsymbol"

	mul2 (10/9)*a, (10/4)*b
	POLY2_B 11, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0.385,         0.16,      1,
		0.515,         0.16,      1,
		0.515,         0.22,    900,
		   0,           90,   4001,
		0.575,         0.22,      1,
		0.575,          0.4,      1,
		0.325,          0.4,      1,
		0.325,         0.22,      1,
		0.385,         0.22,    900,
		   0,           90,   4001,
		0.385,         0.16,      1
	del 1

	ConEdit_1 = 0
return

! -----------------------------------------------------------------------------
"hood_2d":
! -----------------------------------------------------------------------------

	! --- Hood 1 ---
	if type_m = 1 then
		gosub "hood_1"
	endif


	! --- Hood 2 ---
	if type_m = 2 then
		gosub "hood_2"
	endif


	! --- Hood 3 ---
	if type_m = 3 then
		gosub "hood_3"
	endif


	! --- Hood 4 ---
	if type_m = 4 then
		gosub "hood_4"
	endif


	! --- Hood 5 ---
	if type_m = 5 then
		gosub "hood_5"
	endif


	! --- Hood 6 ---
	if type_m = 6 then
		gosub "hood_6"
	endif


	! --- Hood 7 ---
	if type_m = 7 then
		gosub "hood_7"
	endif


	! --- Hood 8 ---
	if type_m = 8 then
		gosub "hood_8"
	endif


	! --- Hood 9 ---
	if type_m = 9 then
		gosub "hood_9"
	endif


	! --- Hood 10 ---
	if type_m = 10 then
		gosub "hood_10"
	endif
return
