
if not(bStorySensitive) then hideparameter "overhead_line_type"

call "FM_types" parameters all

minZZYZX = 0.2
if type_m = 3 | type_m = 4 then
	minZZYZX = 0.42
endif
if type_m = 5 then
	minZZYZX = 0.18
endif

values "a" range (0.4, )
values "b" range [0.3, )
values "zzyzx" range [minZZYZX, )

if type_m = 2 then
	values "depthCabinet" range (0.24, )
else
	values "depthCabinet" range (0, )
endif

values{2} "gs_detlevel_3D_m" 	2, `Детальный`,
								1, `Простой`,
								0, `Откл.`

if gs_detlevel_3D_m = 1 then
	hideparameter "gs_resol"
	lock "gs_resol"
endif

if gs_detlevel_3D_m = 0 then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif

values "gs_resol" range [6,)

values "type" stTypes
values "type_m" 1, 2, 3, 4, 5

if GLOB_MODPAR_NAME = "type" | GLOB_MODPAR_NAME = "type_m" then
	if type_m = 1 then
		a = 0.90
		b = 0.50
		zzyzx = 0.2
	endif
	if type_m = 2 then
		a = 0.90
		b = 0.50
		zzyzx = 0.20
	endif
	if type_m = 3 then
		a = 0.60
		b = 0.31
		zzyzx = 0.45
	endif
	if type_m = 4 then
		a = 0.60
		b = 0.31
		zzyzx = 0.45
	endif
	if type_m = 5 then
		a = 0.56
		b = 0.30
		zzyzx = 0.20
	endif
	parameters a = a, b = b, zzyzx = zzyzx
endif

if type_m = 2 then values "depthCabinet" range (0.249,]

if type_m = 3 or type_m = 4 or type_m = 5 then 
	hideparameter "depthCabinet"
	lock "depthCabinet"
endif

if gs_cont_pen = 0 then parameters gs_cont_pen = 4
if gs_fill_pen = 0 then parameters gs_fill_pen = 19

if type_m = 2 then isavailable = 0


ac_bottomlevel = 0
if type_m = 1 or type_m = 2 then
	ac_toplevel = zzyzx
else
	ac_toplevel = zzyzx + ConStatus_1 * ConLength_1
endif
parameters ac_bottomlevel = ac_bottomlevel
parameters ac_toplevel = ac_toplevel


if isMEPEnabled THEN

	dim shape_typ_to_validation[3]


					! --- CONNECTION --- !

	ConID = 1
	ConName = `Вытяжное Соединение`

	if type_m = 1 then
		shape_typ_to_validation[1] = 1		! Rectangle
		shape_typ_to_validation[2] = 0		! Circle
		shape_typ_to_validation[3] = 0		! Oval

		ConDVecX = 0
		ConDVecY = 1
		ConDVecZ = 0

		ConWVecX = -1
		ConWVecY = 0
		ConWVecZ = 0

		ConWidth_1 = a-16"
		parameters ConWidth_1 = ConWidth_1
		ConDepth_1 = 1.8"
		parameters ConDepth_1 = ConDepth_1
		LOCK "ConWidth_1", "ConDepth_1", "ConWallThickness_1"

		ConPosX_1 = A/2
		ConPosY_1 = B
		ConPosZ_1 = 5.9"
	endif

	if type_m = 3 or type_m = 4 then
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

		ConDVecX = 0
		ConDVecY = 0
		ConDVecZ = 1

		ConWVecX = 1
		ConWVecY = 0
		ConWVecZ = 0

		if GLOB_MODPAR_NAME = "type" | GLOB_MODPAR_NAME = "type_m" then
			ConWidth_1 = aa3
			parameters ConWidth_1 = ConWidth_1
			ConDepth_1 = aa3
			parameters ConDepth_1 = ConDepth_1
		endif

		ConPosX_1 = A/2
		ConPosY_1 = B-0.02-aa3/2
		ConPosZ_1 = zzyzx
	endif

	if type_m = 5 then
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

		ConDVecX = 0
		ConDVecY = 0
		ConDVecZ = 1

		ConWVecX = 1
		ConWVecY = 0
		ConWVecZ = 0

		if GLOB_MODPAR_NAME = "type" | GLOB_MODPAR_NAME = "type_m" then
			ConWidth_1 = 0.12
			parameters ConWidth_1 = ConWidth_1
			ConDepth_1 = 0.12
			parameters ConDepth_1 = ConDepth_1
		endif

		ConPosX_1 = A/2
		ConPosY_1 = B-0.08
		ConPosZ_1 = zzyzx
	endif

	if ConWidth_1 < 0.001 then
		ConWidth_1 = 0.001
		parameters ConWidth_1 = ConWidth_1
	endif
	values "ConWidth_1" RANGE (0, ]
	if ConDepth_1 < 0.001 then
		ConDepth_1 = 0.001
		parameters ConDepth_1 = ConDepth_1
	endif
	values "ConDepth_1" RANGE (0, ]
	values "ConWallThickness_1" RANGE (0, ]
	values "ConLength_1" RANGE [0, ]

	parameters ConPosX_1 = ConPosX_1
	parameters ConPosY_1 = ConPosY_1
	parameters ConPosZ_1 = ConPosZ_1


	call "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	ac_mep_connectionpage_active = 0

	if gs_ui_current_page = TABID_MEP then
		ac_mep_connectionpage_active = 1
	endif

	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
else

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	hideparameter "gs_connection"
endif

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif

hideparameter all "A", "B", "ZZYZX"

