

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

	TABID_ROOT								= -1
	_idxTab = 1

	TABID_Style								= _idxTab  : _idxTab = _idxTab + 1
	TABID_RepresentationAndSurfaces			= _idxTab  : _idxTab = _idxTab + 1
	TABID_Min								= _idxTab  : _idxTab = _idxTab + 1
	TABID_Descriptions						= _idxTab  : _idxTab = _idxTab + 1

ui_current_page gs_ui_current_page
ui_dialog `Параметры Кухонного Прибора`


! =============================================================================
! Display actual tabpages
! =============================================================================

ui_page TABID_Style, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "Style"

ui_page TABID_RepresentationAndSurfaces, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "RepresentationAndSurfacesPage"

ui_page TABID_Min, TABID_ROOT, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalPage"

ui_page TABID_Descriptions, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"Style":
! ------------------------------------------------------------------------------

	pageStartY	= 0
	x0			= 0  			! start Position X Coordinate
	x1			= x0 + 100
	x2			= x1 + 11
	x3			= x2 + 100
	x4			= x3 + 11
	x5			= x4 + 9
	x6			= x5 + 19
	x7			= x6 + 105
	xEnd		= 444
	_yCurr		= pageStartY   	! start Position Y Coordinate
	yEnd		= 266
	_infHeight	= 19
	_outfHeight	= 15
	_infWidth 	= 200
    _dy			= 24

	ui_outfield	`Тип Прибора`,	x0,	_yCurr,	x2,	13
	_yCurr = _yCurr + _dy

	ui_infield{3} "ApplType_m", x0+1, _yCurr - 4,	x3,	20,
			8, "",
			2,
			1, 1, 20, 1, 20,
			"", stApplType[1], 1,
			"", stApplType[2], 2,
			"", stApplType[3], 3,
			"", stApplType[4], 4

	_yCurr = _yCurr + _dy

	if ApplType_m > 0 then

		ui_outfield	`Стиль Дверцы`,	x0,	_yCurr,	x3,	13
		_yCurr = _yCurr + _dy

		if ApplType_m = APPLTYPE_MICROWAVE_OVEN then
			iImage1 = 1
			iImage2 = 2
		endif
		if ApplType_m = APPLTYPE_SINGLE_OVEN then
			iImage1 = 3
			iImage2 = 4
		endif
		if ApplType_m = APPLTYPE_COMB_MICOVEN then
			iImage1 = 5
			iImage2 = 6
		endif
		if ApplType_m = APPLTYPE_DOUBLE_OVEN then
			iImage1 = 7
			iImage2 = 8
		endif
		ui_infield{3}	"ApplStyle_m",	x0+1,	_yCurr,	x3,	95,
			4,1,8,2,
			90,90,90,90,
			iImage1, "", 1,
			iImage2, "", 2

	endif

	ui_separator x4, pageStartY, x4, pageStartY + 170

	_yCurr = pageStartY

	ui_outfield	`Глубина Духового Шкафа`,	x5,	_yCurr,	xEnd - x5,	13
	_yCurr = _yCurr + 50

	ui_pict 2, x6, _yCurr , 101, 92, 1
	
	_yCurr = _yCurr + 92/2
	ui_infield	"ApplDepth",	x7,    _yCurr - _infHeight/2, xEnd - x7,	_infHeight ui_tooltip `Глубина Прибора`


return


! ------------------------------------------------------------------------------
"RepresentationAndSurfacesPage":
! ------------------------------------------------------------------------------

	_idx = 1
	dim paramNameReprArray[]
	dim childFlagReprArray[]
	dim iconNameReprArray[]

		paramNameReprArray 	[_idx] = "gs_2D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1

		paramNameReprArray 	[_idx] = "Appl2D"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "ApplLineType"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contLineType_flat(1)"
		_idx = _idx + 1

		paramNameReprArray 	[_idx] = "gs_cont_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_contourPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_type"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillType_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_fill_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_back_pen"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_fillBgPen_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_3D_representation"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "gs_detlevel_3D_m"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_detailLevel3D_flat(1)"
		_idx = _idx + 1

		if gs_detlevel_3D_m <> DET3D_OFF then
			paramNameReprArray 	[_idx] = "gs_shadow"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_shadow_flat(1)"
			_idx = _idx + 1
		endif

		paramNameReprArray 	[_idx] = "Appl3D"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1

		paramNameReprArray 	[_idx] = "gs_material"
		childFlagReprArray 	[_idx] = 0
		iconNameReprArray 	[_idx] = ""
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "PanelMat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "BaseMat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		_idx = _idx + 1
		paramNameReprArray 	[_idx] = "GlassMat"
		childFlagReprArray 	[_idx] = 1
		iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
	
		if 	ApplStyle_m = OVENDOOR_STYLE_2 then
			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "SwitchMat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

		if 	(ApplStyle_m <> OVENDOOR_STYLE_1) |\
			(ApplType_m <> APPLTYPE_MICROWAVE_OVEN) then

			_idx = _idx + 1
			paramNameReprArray 	[_idx] = "HandleMat"
			childFlagReprArray 	[_idx] = 1
			iconNameReprArray 	[_idx] = "ui_list_surface_flat(1)"
		endif

	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
	
	call "ui_object_Representation" parameters all	listFieldID  	= TABID_RepresentationAndSurfaces,
													paramNameArray 	= paramNameReprArray,
													childFlagArray	= childFlagReprArray,
													iconNameArray	= iconNameReprArray
return

! ------------------------------------------------------------------------------
"MinimalPage":
! ------------------------------------------------------------------------------

	x0		= 0
	x1		= x0 + 64
	x2		= x1 + 76
	x3		= x2 + 19
	x4		= x3 + 95
	x5		= x4 + 17
	xEnd	= 444

	pageStartY = 0
	y1		= pageStartY + 18
	y2		= y1 + 22
	y3		= y2 + 46
	y4		= y3 + 102
	yEnd	= 266

	_infHeight = 19
	_infWidth  = 90

	ui_pict 3, x2, y2, 130, 210, 1
	
	ui_infield	"MSSide2",		x1,    y1, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Сбоку 2`
	ui_infield	"A",			x3,    y1, _infWidth,	_infHeight ui_tooltip `Размер 1`
	ui_infield	"MSSide",		x4,    y1, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Сбоку 1`
	ui_infield	"ApplDepth",	x5,    y3, _infWidth,	_infHeight ui_tooltip `Глубина Прибора`
	ui_infield	"MSFront",		x5,    y4, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Спереди`



return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "gs_list_volts"
		stParamNames[7] = "FM_Type"
		stParamNames[8] = "FM_InventoryNumber"
		stParamNames[9] = "FM_SerialNumber"
		stParamNames[10] = "FM_ProductionYear"
		stParamNames[11] = "FM_ObjectWeight"
		stParamNames[12] = "FM_ObjectWeightUnit"
		stParamNames[13] = "FM_PowerConsumption"
		stParamNames[14] = "FM_Egress"
		stParamNames[15] = "gs_list_custom1"
		stParamNames[16] = "gs_list_custom2"
		stParamNames[17] = "gs_list_custom3"
		stParamNames[18] = "gs_list_custom4"
		stParamNames[19] = "gs_list_custom5"
	
	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif
	
	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames

return
