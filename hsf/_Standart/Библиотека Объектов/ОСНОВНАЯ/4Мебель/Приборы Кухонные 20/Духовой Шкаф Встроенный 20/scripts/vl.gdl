
call "FM_types" parameters all


values "A" range [0.05,]
values "B" range [0.01, ]
values "ZZYZX" range [0.05, ]
values "ApplDepth" range [0.02, ]

values "ApplType" stApplType
values "ApplType_m"	APPLTYPE_MICROWAVE_OVEN,
					APPLTYPE_SINGLE_OVEN,
					APPLTYPE_COMB_MICOVEN,
					APPLTYPE_DOUBLE_OVEN

if GLOB_MODPAR_NAME = "ApplType" then
	ApplType_m = APPLTYPE_MICROWAVE_OVEN
	if ApplType = stApplType[2] then ApplType_m = APPLTYPE_SINGLE_OVEN
	if ApplType = stApplType[3] then ApplType_m = APPLTYPE_COMB_MICOVEN
	if ApplType = stApplType[4] then ApplType_m = APPLTYPE_DOUBLE_OVEN
	parameters ApplType_m = ApplType_m
else
	ApplType_m = max(min(ApplType_m,4),1)
	parameters ApplType = stApplType[ApplType_m]
endif


values "OvenDoorStyle" stOvenDoorStyle
values "ApplStyle_m" OVENDOOR_STYLE_1, OVENDOOR_STYLE_2

if GLOB_MODPAR_NAME = "OvenDoorStyle" then
	ApplStyle_m = OVENDOOR_STYLE_1
	if OvenDoorStyle = stOvenDoorStyle[2] then ApplStyle_m = OVENDOOR_STYLE_2
	parameters ApplStyle_m = ApplStyle_m
else
	if ApplStyle_m = OVENDOOR_STYLE_1 then
		OvenDoorStyle = stOvenDoorStyle[1]
	else
		OvenDoorStyle = stOvenDoorStyle[2]
	endif
	parameters OvenDoorStyle = OvenDoorStyle
endif

values{2} "gs_detlevel_3D_m" 	DET3D_DETAILED,	stDetlevel3D[1],
								DET3D_SIMPLE, 	stDetlevel3D[2],
								DET3D_OFF, 		stDetlevel3D[3]

if GLOB_MODPAR_NAME = "ApplType" | GLOB_MODPAR_NAME = "ApplType_m" then

	if ApplType_m = APPLTYPE_MICROWAVE_OVEN then
		ZZYZX=0.4
		parameters A=0.6 , ZZYZX=0.4
		A=0.6
	endif

	if ApplType_m = APPLTYPE_SINGLE_OVEN then
		ZZYZX=0.4
		parameters ApplHeight=0.5, ZZYZX=0.4
		ApplHeight=0.5
	endif

	if ApplType_m = APPLTYPE_COMB_MICOVEN then
		parameters A=0.6 , ZZYZX=0.8
		A=0.6
		ZZYZX=0.8
	endif

	if ApplType_m = APPLTYPE_DOUBLE_OVEN then
		parameters A=0.6 , ZZYZX=0.9
		A=0.6
		ZZYZX=0.9
	endif

endif


if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19

if ApplStyle_m = OVENDOOR_STYLE_1 then
	hideparameter "SwitchMat"
	lock "SwitchMat"
endif

if not(Appl2D) then
	hideparameter "ApplLineType", "ApplDepth"
	lock "ApplLineType", "ApplDepth"
endif

if gs_detlevel_3D_m = DET3D_OFF then hideparameter "gs_shadow"
if ApplStyle_m = OVENDOOR_STYLE_1 and ApplType_m = APPLTYPE_MICROWAVE_OVEN then
	hideparameter "gs_detlevel_3D_m"
	lock "gs_detlevel_3D_m"
endif


! --- Minimal Space ---
values "MSFront" range [0, )
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif

hideparameter all "A", "B", "ZZYZX"

