
call "FM_types" parameters all


values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif


if not(dt) then
	lock	"txt", "tpen", "fontType", "fsz", "fsz_point",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation"
	hideparameter	"txt", "tpen", "fontType", "fsz", "fsz_point",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation"
endif

if gs_cont_pen = 0 then parameters gs_cont_pen = 10
if gs_fill_pen = 0 then parameters gs_fill_pen = 91
if tpen = 0 then parameters tpen = 86


values "toe_d" range [0, fr_th1-0.01)

parameters fsz = abs(fsz)
values "fsz" range (0,]

VALUES "MSFront" RANGE [0,)
Dot_in_mm = 25.4 / 72


!!! Synhronized mm and point in marker width
if GLOB_MODPAR_NAME = "fsz" then
	parameters fsz_point = fsz / Dot_in_mm
	fsz_point = fsz / Dot_in_mm
endif
if GLOB_MODPAR_NAME = "fsz_point" then
	parameters fsz = fsz_point * Dot_in_mm
	fsz = fsz_point * Dot_in_mm
endif

if gs_door_type_m = 0 then
	hideparameter 	"doorframe_w",
					"vert_gnum",
					"hor_gnum",
					"doorglas_mat",
					"gs_knob_mat",
					"gs_bevel"

	lock 			"doorframe_w",
					"vert_gnum",
					"hor_gnum",
					"doorglas_mat",
					"gs_knob_mat",
					"gs_bevel",
					"gs_knob_type"
else
	hideparameter 	"HandleMat"
	lock 			"HandleMat"
endif

! --- Minimal Space ---
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

CALL "Kitchen Cabinets UI macro" parameters all \
									pageIds = pageIds,
									CornCabType = 0,
									bEdgeBevel = 1,
									CabinetType = 0,
									bCounterOverhang = 1,
									bMirror = 1,

									CornerEdgeSizes = 0,
									bToe = 1,
									bToeType = 0,
									bShelf = 0,
									bShowSurfaces = 1,

									bDoubleFacadeEdit = 0,
									bOneRearPanelEdit = 1,
									bHorKnobPosition = 0,
									bVerKnobPosition =0,
									bPositionType = 0,
									bSidePanels = 1,
									bDrainSymb = 0,

									nDoors = 1,

									bCabSymbol = 0,
									bPossibleSinknTap = 0,
									bApplianceEdit = 0,
									bSymbLineEdit = 0,
									bMinSpaceEdit = 1,
									bShadowEdit = 1,
									bHotspotEdit = 0,

									bKnobParams = 1,

									bEnableBackSplash	= 1,
									bEnableDoorpanel	= 1,
									bEnableEndpanel		= 1,
									bDoubleDoor			= 0,
									bEnableKnobPlacing	= 0,
									bEnableShelf		= 0,
									bEnableSink			= 0,
									isTallCabinet		= 0,

									bManufacturerDoor = _bManufacturerDoor

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif
