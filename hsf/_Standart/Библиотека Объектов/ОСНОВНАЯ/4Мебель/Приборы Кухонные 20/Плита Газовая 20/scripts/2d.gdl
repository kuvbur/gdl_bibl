
! --- Oriented View ------------------------------ [

angleViewRot = 0
if dt then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]

pen	gs_cont_pen
fill gs_fill_type

DRAWINDEX 20
poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, 0, 1,
		A, 0, 1,
		A, B, 1,
		0, B, 1

unID = 1

hotspot2 A/2, B, unID : unID = unID+1

CALL "Resize_A_B_ZZYZX" Parameters ALL

ADD2 a/2,b
IF CooktopType_m<>0 THEN CALL "fa_CooktopType" PARAMETERS ALL AA=AA, BB=BB, gs_symbol_type_m = 1, bSwitch = 0
DEL 1

if Backsplash then
	line2 0, b-th/2, a, b-th/2
endif

! --- Minimal Space ---
DRAWINDEX 10
call "fa_minimalspace" parameters all MSType = 20, unID = 3000


! ======================================================================
! 2D TEXT
! ======================================================================

unID = 200
if dt then

	DRAWINDEX 20
	nTrans = 0

	HOTSPOT2 a/2,		b/2+txty,	unID,  txtx,  1+128	: unID = unID+1
	HOTSPOT2 a/2+txtx,	b/2+txty,	unID,  txtx,  2		: unID = unID+1
	HOTSPOT2 -1,		b/2+txty,	unID,  txtx,  3		: unID = unID+1

	HOTSPOT2 a/2+txtx,	b/2,		unID,  txty,  1+128 : unID = unID+1
	HOTSPOT2 a/2+txtx,	b/2+txty,	unID,  txty,  2		: unID = unID+1
	HOTSPOT2 a/2+txtx,	-1,			unID,  txty,  3		: unID = unID+1

	pen tpen
	define style textstyle	fontType, fontSize * 1000 / GLOB_SCALE, 5,
							gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
	style textstyle
	add2 a/2+txtx,b/2+txty
	nTrans = nTrans + 1

	! Text Rotation - Readable / Always Horizontal / Align with Symbol
	! ----------------------------------------------------------------------
	if typeTextRotation_m = 2 then								! Readable
		rot2 180 * (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps))	! 1/0
		nTrans = nTrans + 1
	else
		if typeTextRotation_m = 1 then							! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED))	! 1/-1
			nTrans = nTrans + 1
		endif
	endif
	! ----------------------------------------------------------------------

	text2 0,0,txt

	del nTrans
else
	HOTSPOT2 a/2,b/2, unID
endif

END
