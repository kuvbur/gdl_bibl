
pen gs_cont_pen
resol gs_resol
if gs_shadow=0 then shadow off


if gs_detlevel_3D_m = 0 then end


if gs_detlevel_3D_m = 1 then
	addy th
	addz zzyzx-th
	rotx -90
	material b_mat
	prism_	4, b-th,
			0, 0, 15,
			a, 0, 115,
			0, zzyzx-th, 115,
			-a, 0, 115

	material d_mat
	prism_	4, -th,
			th/4, 3.5*th, 15,
			a-th/2, 0, 115,
			0, zzyzx-8*th, 115,
			-a+th/2, 0, 115

	material p_mat
	prism_	4, -th/2,
			th/4, zzyzx-th-th/4, 15,
			a-th/2, 0, 115,
			0, -2.75*th,  115,
			-a+th/2, 0, 115

	prism_	4, -th/2,
			0, 0, 15,
			a, 0, 115,
			0, 3*th,  115,
			-a, 0, 115

	addz -th/2
	prism_	4, b-th/2,
			0, 0, 15,
			a, 0, 115,
			0, -th, 115,
			-a, 0, 115

	del 4

	IF Backsplash=1 THEN
		addz zzyzx
		addy b
		roty 90
		prism_	6, a,
				0, 0, 15,
				0, -th/2, 115,
				-zzyzx/3/2, -th/2, 115,
				-th/2, -th/2, 115,
				-zzyzx/3/2, th/2, 115,
				0, th, 115
		del 3
	ENDIF

endif


if gs_detlevel_3D_m = 2 then
	addy th
	addz zzyzx-th
	rotx -90
	material b_mat
	! --- Cabinet ---
	prism_	15, b-th,
			0, 0, 15,
			a, 0, 115,
			0, zzyzx-th, 115,
			-a, 0, 115,
			0, 0, -1,

			2.5*th, 4*th, 75,
			a-5*th, 0, 175,
			2*th, 90, 2075,
			0, zzyzx-13*th, 175,
			2*th, 90, 2075,
			-a+5*th, 0, 175,
			2*th, 90, 2075,
			0, -zzyzx+13*th, 175,
			2*th, 90, 2075,
			2.5*th, 4*th, -1

	if f_3D=1 then
		add 3*th, zzyzx-6*th, th/2
		material f_mat
		t=(a-6*th)/12
		for i=1 to 13
			cylind b-2*th, r
			addx t
		next i
		del i
	endif

	material b_mat
	addz b-th
	prism_	9, -th/4,
			2.5*th, 4*th, 75,
			a-5*th, 0, 175,
			2*th, 90, 2075,
			0, zzyzx-13*th, 175,
			2*th, 90, 2075,
			-a+5*th, 0, 175,
			2*th, 90, 2075,
			0, -zzyzx+13*th, 175,
			2*th, 90, 2075
	del 1

	material d_mat
	! --- Oven Door ---
	prism_	10, -th,
			th/4, 3.5*th, 15,
			a-th/2, 0, 115,
			0, zzyzx-8*th, 115,
			-a+th/2, 0, 115,
			0, 0, -1,

			A/4, 6*th, 15,
			A/2, 0, 115,
			0, zzyzx-13*th, 115,
			-A/2, 0, 115,
			A/4, 6*th, -1

	material g_mat
	! --- Oven Door Window ---
	prism_	5, -th/2,
			A/4, 6*th, 15,
			A/2, 0, 115,
			0, zzyzx-13*th, 115,
			-A/2, 0, 115,
			A/4, 6*th, -1

	material p_mat
	prism_	4, -th/2,
			th/4, zzyzx-th-th/4, 15,
			a-th/2, 0, 115,
			0, -2.75*th,  115,
			-a+th/2, 0, 115

	prism_	4, -th/2,
			0, th/8, 15,
			a, 0, 115,
			0, 3*th-th/8,  115,
			-a, 0, 115

	addz -th/2
	prism_	4, b-th/2,
			0, 0, 15,
			a, 0, 115,
			0, -th/2, 108,
			-a, 0, 113

	del 3

	add a/2, -th, zzyzx*2.75/4
	prism_	12, th/2,
			a/2*0.9, 0, 15,
			0, -th/10, 179,
			th-th/10, -90, 2079,
			-(a*0.9-2*(th-th/10)), 0, 179,
			th-th/10, -90, 2079,
			0, th/10, 179,
			th/2, 0, 179,
			0, -th/10, 179,
			th-th/10-th/2, 90, 2079,
			(a*0.9-2*(th-th/10)), 0, 179,
			th-th/10-th/2, 90, 2079,
			0, th/10, 179
	del 1

	addz zzyzx-th/2
	addy -th/2
	prism_	10, th/2,
			0, 0, 14,
			a, 0, 114,
			0, b-th/2, 114,
			-a, 0, 114,
			0, 0, -1,

			th, th, 15,
			a-2*th, 0, 115,
			0, b-th/2-2*th, 115,
			-a+2*th, 0, 115,
			th, th, -1

	! --- BackSplash ---
	IF Backsplash=1 THEN
		addz th/2
		addy b-th/2
			roty 90

			prism_	6, a,
					0, 0, 15,
					0, -th/2, 115,
					-zzyzx/3/2, -th/2, 115,
					-th/2, -th/2, 115,
					-zzyzx/3/2, th/2, 115,
					0, th, 115

			del 1

			addz zzyzx/3/2+th/2
			addy -1.5*th
			rotx 90-atn((th/2)/(zzyzx/3/2))
			material gb_mat
			prism_	4, zzyzx/500,
					a/3, zzyzx/3/2/4, 15,
					a/3, 0, 115,
					0, zzyzx/3/2/2, 115,
					-a/3, 0, 115
			del 3
		del 2
	ENDIF

	del 3
endif

! --- Cooktop ---
ADD A/2,b,ZZYZX
call "fa_CooktopType" parameters all 	AA 			= AA,
										BB 			= BB,
										bmat 		= matTop,
										gbmat 		= gb_mat,
										ghmat 		= gh_mat,
										SwitchMat 	= matButton,
										bSwitch 	= 0 \
										returned_parameters nSwitch
DEL 1

! --- Switches ---
if gs_detlevel_3D_m = 2 then
	addz zzyzx-2*th
	rotx 90
	for i=1 to nSwitch+1
		addx a/(nSwitch+2)
		gosub 40
	next i
	del i-1
	del 2
endif

end


! --- Knobs ---
40:
	if a<1.1 then
		knobsize = (a-2.5*th)/2
	else
		knobsize = (1.1-2.5*th)/2
	endif
	material matButton
	addz - th/2
	fprism_ matButton, matButton, matButton, matButton,
			2, th/2/2, 45, th/2/2/2,
			0, 0, 979,
			knobsize/10, 360, 4079
	addz th/2/2
	fprism_ matButton, matButton, matButton, matButton,
			3, th/2, 60, th/3,
			knobsize/30, -knobsize/15, 15,
			0, y/15, 15,
			-knobsize/30, -knobsize/15, 15
	del 2
	return
