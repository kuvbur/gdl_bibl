
call "FM_types" parameters all

values{2} "gs_detlevel_3D_m" 	2, `Детальный`,
								1, `Простой`,
								0, `Откл.`
								
values "gs_resol" range [3, )

if gs_detlevel_3D_m = 1 then
	f_3D = 0
	parameters f_3D = f_3D
	lock "f_3D"
	lock "f_mat"
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if not(f_3D) then lock "f_mat"

if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

if gs_cont_pen<=0 then parameters gs_cont_pen=7

! --- Cooktops ---
dim stCooktopType[5]
	stCooktopType[1] = `Тип 1`
	stCooktopType[2] = `Тип 2`
	stCooktopType[3] = `Тип 3`
	stCooktopType[4] = `Тип 4`
	stCooktopType[5] = `Тип 5`

dim idxCooktopType[5]
	idxCooktopType[1] = 3
	idxCooktopType[2] = 4
	idxCooktopType[3] = 8
	idxCooktopType[4] = 9
	idxCooktopType[5] = 13

VALUES "CooktopType" 	stCooktopType
VALUES "CooktopType_m" 	idxCooktopType

if GLOB_MODPAR_NAME = "CooktopType" then
	cooktopType_m = 1
	for iCooktopType = 1 to vardim1(idxCooktopType)
		if cooktopType = stCooktopType[iCooktopType] then
			cooktopType_m = idxCooktopType[iCooktopType]
		endif
	next iCooktopType
	parameters cooktopType_m = cooktopType_m
else
	idx = 1
	for i=1 to vardim1(idxCooktopType)
		if idxCooktopType[i] = cooktopType_m then
			idx = i
		endif
	next i
	parameters cooktopType = stCooktopType[idx]
endif

if GLOB_MODPAR_NAME="CooktopType" or GLOB_MODPAR_NAME="CooktopType_m" then
	IF CooktopType_m<>0 THEN CALL "fa_CooktopType" PARAMETERS ALL\
							returned_parameters defaultAA, defaultBB

	PARAMETERS AA=defaultAA, BB=defaultBB
endif
values "A" range [AA, )
values "B" range [BB+2*0.055, )

! --- Text ---
values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif


if not(dt) then
	lock	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m"
	hideparameter	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m"
endif

values "fontSize" range (0,]

hideparameter all "A", "B", "ZZYZX"

! --- Minimal Space ---
VALUES "MSFront" RANGE [0,)
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif
