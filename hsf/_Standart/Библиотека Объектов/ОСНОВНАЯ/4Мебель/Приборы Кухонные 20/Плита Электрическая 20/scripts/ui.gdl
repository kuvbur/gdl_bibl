

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

	TABID_ROOT								= -1
	_idxTab = 1

	TABID_Style								= _idxTab  : _idxTab = _idxTab + 1
	TABID_Dimension							= _idxTab  : _idxTab = _idxTab + 1
	TABID_2DRepresentation					= _idxTab  : _idxTab = _idxTab + 1
	TABID_Text								= _idxTab  : _idxTab = _idxTab + 1
	TABID_3DRepresentationSurfaces			= _idxTab  : _idxTab = _idxTab + 1
	TABID_Min								= _idxTab  : _idxTab = _idxTab + 1
	TABID_Descriptions						= _idxTab  : _idxTab = _idxTab + 1

ui_current_page gs_ui_current_page
ui_dialog `Параметры Электрической Плиты`


! =============================================================================
! Display actual tabpages
! =============================================================================

ui_page TABID_Style, TABID_ROOT, `Стиль`, "uiTab_Style_flat(1)"
	gosub "Style"

ui_page TABID_Dimension, TABID_ROOT, `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "Dimension"

ui_page TABID_2DRepresentation, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

	ui_page TABID_Text, TABID_2DRepresentation, `Текст Символа`, "uiTab_Text_flat(1)"
		gosub "SymText"

ui_page TABID_3DRepresentationSurfaces, TABID_ROOT, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationSurfacesPage"

ui_page TABID_Min, TABID_ROOT, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalPage"

ui_page TABID_Descriptions, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Text_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"Style":
! ------------------------------------------------------------------------------

	pageStartY 	= 5
	x0			= 0  				! start Position X Coordinate
	x2			= x0 + 222
	xEnd		= 444
	yEnd		= 266
	_yCurr		= pageStartY   		! start Position Y Coordinate
	_dy			= 20
	_outfHeight	= 15
	_infHeight	= 19

	ui_infield{3} "Backsplash", x0, _yCurr, x2 - 2, _infHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `Фартук`, 0,
				"", `Фартук`, 1

	ui_infield{3} "f_3D", x2, _yCurr, x2 - 2, _infHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `Решетка Духового Шкафа`, 0,
				"", `Решетка Духового Шкафа`, 1

	_yCurr = _yCurr + _dy

	ui_infield{3} "iCooktopType", x0, _yCurr, xEnd - x0, yEnd - _yCurr,
		1, 1,												! Range_Electric_CooktopType_Matrix.png
		13, 4,
		105, 105, 90, 90,
		1, "", 1,
		2, "", 2,
		3, "", 3,
		4, "", 4,
		5, "", 5,
		6, "", 6,
		7, "", 7,
		8, "", 8,
		9, "", 9,
		10, "", 10,
		11, "", 11,
		12, "", 12,
		13, "", 13

return

! ------------------------------------------------------------------------------
"Dimension":
! ------------------------------------------------------------------------------

	x0			= 0  			! start Position X Coordinate
	x3			= x0 + 155
	x4			= x3 + 31
	_picy		= 68
	_infHeight	= 19
	_infWidth 	= 88

	ui_pict 3, x3, _picy, 132, 119, 1				! Range_Electric_Dimensions.png

	ui_infield	"A",	x4, 		_picy - 22,		_infWidth,	_infHeight ui_tooltip `Ширина`
	ui_infield	"BB",	x3 - 93, 	_picy + 50,		_infWidth,	_infHeight ui_tooltip `Глубина Варочной Панели`
	ui_infield	"B",	x3 + 136, 	_picy + 50,		_infWidth,	_infHeight ui_tooltip `Длина`
	ui_infield	"AA",	x4,			_picy + 121,	_infWidth,	_infHeight ui_tooltip `Ширина Варочной Панели`

return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------

	pageStartY	= 5
	_yCurr 		= pageStartY

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 16, !SYMBOLCLAS_COOKTOP_ELECTRIC
													gs_cont_pen 		= gs_cont_pen,
													gs_fill_type 		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen 		= gs_back_pen,
													b2DSymbol			= b2DSymbol,
													iCooktopSymbolType	= iCooktopSymbolType,
													stSymbolTypes		= stCooktopSymbolTypes,
													pageStartY			= _yCurr

return


! ------------------------------------------------------------------------------
"SymText":
! ------------------------------------------------------------------------------

	call "ui_object_SymbolText" parameters	dt						= dt,
											bParName_stDisplayText	= 0,
											fontType				= fontType,
											fontSize				= fontSize,
											bParName_FontSize		= 1,
											gs_text_style_bold		= gs_text_style_bold,
											gs_text_style_italic	= gs_text_style_italic,
											gs_text_style_underline	= gs_text_style_underline,
											bParName_PenText		= 0,
											tpen					= tpen,
											typeTextRotation_m		= typeTextRotation_m,
											txtx					= txtx,
											txty					= txty

return

! ------------------------------------------------------------------------------
"3DRepresentationSurfacesPage":
! ------------------------------------------------------------------------------

	dim surfacesArray[]
	SurfItemID = 1  

		surfacesArray[SurfItemID]	= "b_mat"
		SurfItemID = SurfItemID + 1
	
		surfacesArray[SurfItemID]	= "p_mat"
		SurfItemID = SurfItemID + 1
	
		surfacesArray[SurfItemID]	= "gb_mat"
		SurfItemID = SurfItemID + 1
	
		surfacesArray[SurfItemID]	= "d_mat"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "g_mat"
		SurfItemID = SurfItemID + 1

		if f_3D then
			surfacesArray[SurfItemID]	= "f_mat"
			SurfItemID = SurfItemID + 1
		endif

		surfacesArray[SurfItemID]	= "bmat"
		SurfItemID = SurfItemID + 1

		if	iCooktopType <> 1 &\
			iCooktopType <> 5 &\
			iCooktopType <> 10 then
	
			surfacesArray[SurfItemID]	= "matarc"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k

	call "ui_object_3DRepresentation" parameters 	surfacesArray 		= surfacesArray,
													childFlagArray 		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													iSurfaceListID		= TABID_3DRepresentationSurfaces,
													bShowResol			= 1


return

! ------------------------------------------------------------------------------
"MinimalPage":
! ------------------------------------------------------------------------------

	pageStartY = 0
	xPict1 = 130
	yPict1 = pageStartY + 40
	xField = 90
	yField = 18
	fieldToPic = 3
	fieldToField = 5

	ui_pict 2, xPict1, yPict1, 159, 163, 1  			! Range_Electric_MinSpace.png

	ui_infield "A", 		xPict1 + 36, 							yPict1 - yField - fieldToPic,	xField, yField ui_tooltip `Ширина`
	ui_infield "B", 		xPict1 + 159 + fieldToPic, 				yPict1 + 42, 					xField, yField ui_tooltip `Длина`
	ui_infield "MSFront", 	xPict1 + 159 + fieldToPic, 				yPict1 + 119, 					xField, yField ui_tooltip `Минимальное Пространство Спереди`
	ui_infield "MSSide", 	xPict1 + 36 - xField - fieldToField, yPict1 - yField - fieldToPic, 	xField, yField ui_tooltip `Минимальное Пространство Сбоку 1`
	ui_infield "MSSide2", 	xPict1 + 36 + xField + fieldToField, yPict1 - yField - fieldToPic, 	xField, yField ui_tooltip `Минимальное Пространство Сбоку 2`


return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "gs_list_volts"
		stParamNames[7] = "FM_Type"
		stParamNames[8] = "FM_InventoryNumber"
		stParamNames[9] = "FM_SerialNumber"
		stParamNames[10] = "FM_ProductionYear"
		stParamNames[11] = "FM_ObjectWeight"
		stParamNames[12] = "FM_ObjectWeightUnit"
		stParamNames[13] = "FM_PowerConsumption"
		stParamNames[14] = "FM_Egress"
		stParamNames[15] = "gs_list_custom1"
		stParamNames[16] = "gs_list_custom2"
		stParamNames[17] = "gs_list_custom3"
		stParamNames[18] = "gs_list_custom4"
		stParamNames[19] = "gs_list_custom5"
	
	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif
	
	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames

return
