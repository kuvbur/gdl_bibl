
call "FM_types" parameters all

values{2} "gs_detlevel_3D_m" 	2, `Детальный`,
								1, `Простой`,
								0, `Откл.`
								
values "gs_resol" range [3, )

if gs_detlevel_3D_m = 1 then
	f_3D = 0
	parameters f_3D = f_3D
	lock "f_3D"
	lock "f_mat"
	lock "gs_resol"
	hideparameter "gs_resol"
endif


if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

if gs_cont_pen<=0 then parameters gs_cont_pen=7

! --- Cooktops ---
dim stCooktopType[8]
	stCooktopType[1] = `2 Конфорки (1)`
	stCooktopType[2] = `2 Конфорки (2)`
	stCooktopType[3] = `4 Конфорки (1)`
	stCooktopType[4] = `4 Конфорки (2)`
	stCooktopType[5] = `4 Конфорки (3)`
	stCooktopType[6] = `5 Конфорок (1)`
	stCooktopType[7] = `5 Конфорок (2)`
	stCooktopType[8] = `4 Конфорки (4)`

COOKTOP_TYPE_1 = 1
COOKTOP_TYPE_2 = 2
COOKTOP_TYPE_3 = 5
COOKTOP_TYPE_4 = 6
COOKTOP_TYPE_5 = 7
COOKTOP_TYPE_6 = 10
COOKTOP_TYPE_7 = 11
COOKTOP_TYPE_8 = 12

values{2} "iCooktopType" 	COOKTOP_TYPE_1, 	stCooktopType[1],
							COOKTOP_TYPE_2,		stCooktopType[2],
							COOKTOP_TYPE_3,		stCooktopType[3],
							COOKTOP_TYPE_4,		stCooktopType[4],
							COOKTOP_TYPE_5,		stCooktopType[5],
							COOKTOP_TYPE_6,		stCooktopType[6],
							COOKTOP_TYPE_7,		stCooktopType[7],
							COOKTOP_TYPE_8,		stCooktopType[8]

if iCooktopType = 1 or iCooktopType = 5 or iCooktopType = 10 then
	hideparameter "matarc"
	lock"matarc"
endif
if not(f_3D) then		!oven rack
	lock "f_mat"
endif


if GLOB_MODPAR_NAME="CooktopType" or GLOB_MODPAR_NAME="iCooktopType" then
	IF iCooktopType<>0 THEN CALL "fa_CooktopType" PARAMETERS ALL CooktopType_m = iCooktopType\
							returned_parameters defaultAA, defaultBB

	PARAMETERS AA=defaultAA, BB=defaultBB
endif
values "A" range [AA, )
values "B" range [BB+2*0.055, )


values "gs_cooktop_symbol_type" stCooktopSymbolTypes
values "iCooktopSymbolType" 1, 2, 3, 4

if GLOB_MODPAR_NAME = "gs_cooktop_symbol_type" then
	if gs_cooktop_symbol_type = stCooktopSymbolTypes[1] then iCooktopSymbolType = 1
	if gs_cooktop_symbol_type = stCooktopSymbolTypes[2] then iCooktopSymbolType = 2
	if gs_cooktop_symbol_type = stCooktopSymbolTypes[3] then iCooktopSymbolType = 3
	if gs_cooktop_symbol_type = stCooktopSymbolTypes[4] then iCooktopSymbolType = 4
	parameters iCooktopSymbolType = iCooktopSymbolType
else
	if iCooktopSymbolType = 1 then gs_cooktop_symbol_type = stCooktopSymbolTypes[1]
	if iCooktopSymbolType = 2 then gs_cooktop_symbol_type = stCooktopSymbolTypes[2]
	if iCooktopSymbolType = 3 then gs_cooktop_symbol_type = stCooktopSymbolTypes[3]
	if iCooktopSymbolType = 4 then gs_cooktop_symbol_type = stCooktopSymbolTypes[4]
	parameters gs_cooktop_symbol_type = gs_cooktop_symbol_type
endif

if not(b2DSymbol) then
	lock "gs_cooktop_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type", "iCooktopSymbolType"
	hideparameter "gs_cooktop_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type", "iCooktopSymbolType"
endif
if b2DSymbol and iCooktopSymbolType < 3 then
	lock "gs_symbol_fill_type"
	hideparameter "gs_symbol_fill_type"
endif

! --- Text ---
values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif


if not(dt) then
	lock	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation"
	hideparameter	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation"
endif

values "fontSize" range (0,]

hideparameter all "A", "B", "ZZYZX"

! --- Minimal Space ---
values "MSFront" range [0,)
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif
