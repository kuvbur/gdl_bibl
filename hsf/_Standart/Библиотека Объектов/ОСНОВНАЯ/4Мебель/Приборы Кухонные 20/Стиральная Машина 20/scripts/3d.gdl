
pen gs_cont_pen

c = zzyzx - storage_hgh
if gs_detlevel_3D_m = DET3D_OFF then end

bSolidModel = GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50
if bSolidModel then gs_detlevel_3D_m = DET3D_SIMPLE
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m = DET3D_SIMPLE
if not(gs_shadow) then SHADOW OFF

! -----------------------------------------------------------------------------
! Modifying to Shaft
! -----------------------------------------------------------------------------

if gs_detlevel_3D_m = DET3D_SIMPLE	then resol 8
if gs_detlevel_3D_m = DET3D_DETAILED then resol 24

if bShowMEP then
	add -a / 2, -b, 0
	if useSysMat then
		matAppliance	= sMat
		matControlHandle	= sMat
		matGlass	= sMat
		matControlKnob	= sMat
		matDoorFrame	= sMat
		matControlPanel	= sMat
		gs_con_mat	= sMat
	endif

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16
	del 1
endif


! -----------------------------------------------------------------------------
! 3D HOTSPOTS
! -----------------------------------------------------------------------------

unID=1

hotspot	-a/2,	0,	0,	unID : unID = unID + 1
hotspot	-a/2,  -b,	0,	unID : unID = unID + 1
hotspot	 a/2,  -b,	0,	unID : unID = unID + 1
hotspot	 a/2,   0,	0,	unID : unID = unID + 1
hotspot	   0,-b/2,	0,	unID : unID = unID + 1

hotspot	   0,	0,	    0,	unID, zzyzx, 	1 + 128	: unID = unID + 1
hotspot	   0,	0,	   -1,	unID, zzyzx,	3		: unID = unID + 1
hotspot	   0,	0,	zzyzx,	unID, zzyzx,    2		: unID = unID + 1

hotspot	-a/2,	0,	storage_hgh,					unID, machineHeight, 	 1  : unID = unID + 1
hotspot	-a/2,	0,	-1,								unID, machineHeight, 3+128  : unID = unID + 1
hotspot	-a/2,	0,	storage_hgh + machineHeight,	unID, machineHeight, 	 2  : unID = unID + 1

hotspot	-a/2,	-b,	storage_hgh,					unID, machineHeight, 	 1  : unID = unID + 1
hotspot	-a/2,	-b,	-1,								unID, machineHeight, 3+128  : unID = unID + 1
hotspot	-a/2,	-b,	storage_hgh+ machineHeight,	unID, machineHeight, 	 2  : unID = unID + 1

hotspot	 a/2,	-b,	storage_hgh,					unID, machineHeight, 	 1  : unID = unID + 1
hotspot	 a/2,	-b,	-1,								unID, machineHeight, 3+128  : unID = unID + 1
hotspot	 a/2,	-b,	storage_hgh + machineHeight,	unID, machineHeight, 	 2  : unID = unID + 1

hotspot	 a/2,	0,	storage_hgh,					unID, machineHeight, 	 1  : unID = unID + 1
hotspot	 a/2,	0,	-1,								unID, machineHeight, 3+128  : unID = unID + 1
hotspot	 a/2,	0,	storage_hgh + machineHeight,	unID, machineHeight, 	 2  : unID = unID + 1

! -----------------------------------------------------------------------------
! Laundry Storage
! -----------------------------------------------------------------------------

add -a/2, -b, 0
gosub iStyle
if gs_bLaundryStorage then
	CALL "fa_laundrystorage" parameters all  w_mat = matAppliance
endif
del 1

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================


! -----------------------------------------------------------------------------
! Top Loading - simple and detailed
! -----------------------------------------------------------------------------

1:
	addz storage_hgh
	material matAppliance

	if gs_detlevel_3D_m = DET3D_SIMPLE then gs_resol = 3

	add a/2, b, 0
	prism_    9,     0.1,
				a/2,     0.0,     11,
				a/2,    -b,     11,
				a/2-0.02,    -b,     11,
				a/2-0.02,    -b+0.01,     11,
			   -a/2+0.02,    -b+0.01,     11,
			   -a/2+0.02,    -b,     11,
			   -a/2,    -b,     11,
			   -a/2,     0.0,     11,
				a/2,     0.0,     -1

	addz 0.1
	prism_    5,     machineHeight-0.13,
				a/2,     0.0,     14,
				a/2,    -b,     14,
			   -a/2,    -b,     14,
			   -a/2,     0.0,     14,
				a/2,     0.0,     -1

	LIN_ -a/2 + 0.02, -b, 0, a/2 - 0.02, -b, 0

	addz machineHeight - 0.13
	resol 1.5*gs_resol
	prism_    22,     0.03,
				a/2,     0.0,     14,
				a/2,    -b,     14,
			   -a/2,    -b,     14,
			   -a/2,     0.0,     14,
				a/2,     0.0,     -1,

				a/2-0.05,    -0.18,      15+64,
				a/2-0.09,    -0.18,    901,
				0.0,      90.0,   4001,
				a/2-0.09,    -0.14,      15+64,
			   -a/2+0.09,    -0.14,      15+64,
			   -a/2+0.09,    -0.18,    901,
				0.0,      90.0,   4001,
			   -a/2+0.05,    -0.18,      15+64,
			   -a/2+0.05,    -b+0.08,      15+64,
			   -a/2+0.09,    -b+0.08,    915+64,
				0.0,      90.0,   4001,
			   -a/2+0.09,    -b+0.04,      15+64,
				a/2-0.09,    -b+0.04,      15+64,
				a/2-0.09,    -b+0.08,    900,
				0.0,      90.0,   4001,
				a/2-0.05,    -b+0.08,      15+64,
				a/2-0.05,    -0.18,     -1

	prism_    17,     0.03,
				a/2-0.055,    -0.18,      15+64,
				a/2-0.09,    -0.18,    900,
				0.0,      90.0,   4001,
				a/2-0.09,    -0.145,      15+64,
			   -a/2+0.09,    -0.145,      15+64,
			   -a/2+0.09,    -0.18,    900,
				0.0,      90.0,   4001,
			   -a/2+0.055,    -0.18,      15+64,
			   -a/2+0.055,    -b+0.08,      15+64,
			   -a/2+0.09,    -b+0.08,    900,
				0.0,      90.0,   4001,
			   -a/2+0.09,    -b+0.045,      15+64,
				a/2-0.09,    -b+0.045,      15+64,
				a/2-0.09,    -b+0.08,    900,
				0.0,      90.0,   4001,
				a/2-0.055,    -b+0.08,      15+64,
				a/2-0.055,    -0.18,     -1

	del 2

	r = (a/2 - 0.01)**2 / 0.04 + 0.01

	if ControlPanel then
		add 0, 0, zzyzx + 0.05 - storage_hgh
		rotx 65
		cutplane
		del 2

		add -a/2 + 0.35, 0, zzyzx - 1.1 - storage_hgh
		roty 75
		cutplane
		del 2

		rotx 90
		resol 8*gs_resol
		prism_    6, 0.1,
				   -a/2+0.01,     machineHeight,      15,
				   -a/2+0.01,     zzyzx-0.02-storage_hgh,      15+64,
					0.0,    zzyzx-r-storage_hgh,    915,
					a/2-0.01,     zzyzx-0.02-storage_hgh,      3015,
					a/2-0.01,     machineHeight,      15,
				   -a/2+0.01,     machineHeight,     -1
		del 1

		cutend

		add a/2-0.35, 0, zzyzx - 1.1 - storage_hgh
		roty -75
		cutplane
		del 2

		rotx 90
		prism_    6, 0.1,
				   -a/2+0.01,     machineHeight,      15,
				   -a/2+0.01,     zzyzx-0.02-storage_hgh,      15+64,
					0.0,    zzyzx-r-storage_hgh,    915,
					a/2-0.01,     zzyzx-0.02-storage_hgh,      3015,
					a/2-0.01,     machineHeight,      15,
				   -a/2+0.01,     machineHeight,     -1
		del 1

		cutend
		cutend

		material matControlHandle

		add 0, 0, zzyzx + 0.05 - storage_hgh
		rotx 65
		cutplane
		del 2

		add -a/2 + 0.35, 0, zzyzx - 1.1 - storage_hgh
		roty -105
		cutplane
		del 2

		add a/2 - 0.35, 0, zzyzx - 1.1 - storage_hgh
		roty 105
		cutplane
		del 2

		rotx 90
		prism_    6, 0.1,
				   -a/2+0.01,     machineHeight,      15,
				   -a/2+0.01,     zzyzx-0.02-storage_hgh,      15+64,
					0.0,    zzyzx-r-storage_hgh,    915,
					a/2-0.01,     zzyzx-0.02-storage_hgh,      3015,
					a/2-0.01,     machineHeight,      15,
				   -a/2+0.01,     machineHeight,     -1
		del 1

		cutend
		cutend
		cutend
	endif

	!--- KNOB ---

	mul a/0.7,1,1
	rotx 90
	add 0,machineHeight-0.03,b
	resol gs_resol
	material matControlHandle
	prism_      8, 0.005,
				0, 0.02, 15+64,
				1, 0, 815+64,
				0.3185, 0.0118, 1015+64,
				0.3185, -0.0118, 1015+64,
				0, -0.02, 1015+64,
				-0.3185, -0.0118, 1015+64,
				-0.3185, 0.0118, 1015+64,
				0, 0.02, 1015+64
	del 3

	add 0, -b + 0.65, machineHeight
	mul a/0.7, 1, 1
	prism_      15,  0.003,
			   -0.275477,    -0.584182,      79,
			   -0.275,    -0.589581,    900,
				0.0,     174.9508,   4001,
			   -0.275,    -0.595,      79,
			   -0.1,    -0.595,      15,
			   -0.1,    -0.605,      15,
				0.1,    -0.605,      15,
				0.1,    -0.595,      15,
				0.275,    -0.595,      79,
				0.275,    -0.589581,    900,
				0.0,     174.9508,   4001,
				0.275477,    -0.584182,      79,
				0.0,    -2.444386,    900,
				0.0,      16.8474,   4001,
			   -0.275477,    -0.584182,     -1
	del 4

return


! -----------------------------------------------------------------------------
! Front Loading - simple and detailed
! -----------------------------------------------------------------------------

2:
	addz storage_hgh

	if gs_detlevel_3D_m = DET3D_SIMPLE then
		gs_resol = 6
	endif
	resol gs_resol

	toe_h = c/9

	e = c - c/30 - c*12/90 - c/90 - toe_h
	r = min(a/3.5, e/3.5, c/5)

	addz toe_h
	addy b*2/60
	material matAppliance
	rotx 90
	prism_  8, b*2/60,
		0, 0, 15,
		a, 0, 15,
		a, e, 15,
		0, e, 15,
		0, 0, 13,
		0, 0, -1,
		a/2, e/2, 913,
		r, 360, 4013


	if gs_detlevel_3D_m = DET3D_SIMPLE then
		prism_  2, 0.005,
			a/2, e/2, 913,
			r, 360, 4013
	endif

	addz -b*2/60
	material matDoorFrame
	if gs_detlevel_3D_m = DET3D_DETAILED then
		prism_  3, b*2/60,
			a/2, e/2, 979,
			r, 360, 4079,
			!r-4*a/60, 360, 4079
			r-(r/5), 360, 4079
	endif
	del 2

	material matGlass
	add a/2, b*12/60, e/2
	roty -90
	rotz -90
	if gs_detlevel_3D_m = DET3D_DETAILED then
		revolve 2,360, 1,
			0, r-(r/2.5), 0,
			b*10/60, r-(r/5), 1
	endif
	del 5

	!--- box ---

	if bSolidModel then
		add 0, b*2/60, toe_h
		prism_  5, e+c*13/90,
			0, 0, 15,
			a, 0, 15,
			a, b-0.02, 15,
			0, b-0.02, 15,
			0, 0, -1
		del 1
	else
		material matAppliance
		addz toe_h
		addy b*2/60
		rotx 90
		roty 90
		for m=1 to 2
			poly_ 5,
				0, 0, 1,
				b-b*2/60, 0, 1,
				b-b*2/60, e+c*13/90, 1,
				0, e+c*13/90, 1,
				0, 0, -1
			addz a
		next m
		del 5

		addy b
		rotx 90
		poly_ 5,
			0, 0, 1,
			a, 0, 15,
			a, e+c*13/90, 1,
			0, e+c*13/90, 1,
			0, 0, -1
		del 3
	endif

	!--- toe ---
	material matAppliance
	addy b*2/60
	prism_  5, toe_h,
		0, 0, 15,
		a, 0, 15,
		a, b-0.02, 15,
		0, b-0.02, 15,
		0, 0, -1
	del 1

	!--- control panel ---
	material matControlPanel
	add 0, b*2/60, e + toe_h + c/90
	rotx 90
	prism_  5, b*2/60,
		0, 0, 15,
		a, 0, 15,
		a, c*11/90, 15,
		0, c*11/90, 15,
		0, 0, -1

	addy -c/90
	poly_  5,
		0, 0, 1,
		a, 0, 1,
		a, c*13/90, 1,
		0, c*13/90, 1,
		0, 0, -1
	del 3

	!--- counter ---

	material matAppliance
	addz c - (c/30)
	prism_  5, c/30,
		0, 0, 15,
		a, 0, 15,
		a, b, 15,
		0, b, 15,
		0, 0, -1
	del 1

	!--- knobs ---
	if gs_detlevel_3D_m = DET3D_DETAILED then
		material matControlKnob
		add a/10, 0, c - c/30 - c*6/90
		rotx 90
		resol gs_resol
		for n=1 to 4
			cylind b/60, a/60
			addx a/15
		next n
		del 6
	endif

	del 1

return




