
! -----------------------------------------------------------------------------
! Macros
! -----------------------------------------------------------------------------
call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle"  1, 2

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = STYLE_TOP_LOADING
	if stStyle = stStyles[2] then iStyle = STYLE_FRONT_LOADING
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = STYLE_FRONT_LOADING then stStyle = stStyles[2]
	parameters stStyle = stStyle
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------
values{2} "gs_detlevel_3D_m" 	DET3D_DETAILED,	`Детальный`,
								DET3D_SIMPLE, 	`Простой`,
								DET3D_OFF, 		`Откл.`

! -----------------------------------------------------------------------------
! Default sizes and default Connectons Positons
! -----------------------------------------------------------------------------
values "gs_resol" range [3,)
values "A" range [0.2,]
values "B" range [0.3, ]
values "ZZYZX" range [0.2, ]
values "gs_LStorage_height" range [0.15, ]

if	GLOB_MODPAR_NAME = "iStyle" |\
	GLOB_MODPAR_NAME = "machineHeight" |\
	GLOB_MODPAR_NAME = "gs_LStorage_height" |\
	GLOB_MODPAR_NAME = "gs_bLaundryStorage" |\
	GLOB_MODPAR_NAME = "ControlPanel"  then

	zzyzx = machineHeight + (0.2 * ControlPanel * (iStyle = STYLE_TOP_LOADING)) + (gs_bLaundryStorage * gs_LStorage_height)
	parameters zzyzx = zzyzx
else
	machineHeight = zzyzx - (0.2 * ControlPanel * (iStyle = STYLE_TOP_LOADING)) - (gs_bLaundryStorage * gs_LStorage_height)
	parameters machineHeight = machineHeight
endif

if GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then

	ConPosX_1 = defaultConnectionPos[iStyle][1]
	ConPosY_1 = defaultConnectionPos[iStyle][2]
	ConPosZ_1 = defaultConnectionPos[iStyle][3]
	ConPosX_2 = defaultConnectionPos[iStyle][4]
	ConPosY_2 = defaultConnectionPos[iStyle][5]
	ConPosZ_2 = defaultConnectionPos[iStyle][6]
	parameters ConPosX_1 = ConPosX_1, ConPosY_1 = ConPosY_1 , ConPosZ_1 = ConPosZ_1
	parameters ConPosX_2 = ConPosX_2, ConPosY_2 = ConPosY_2 , ConPosZ_2 = ConPosZ_2
endif

if GLOB_MODPAR_NAME = "A" then
	a2 = a / 2
	parameters a2 = a2
else
	A = 2 * a2
	parameters A = A
endif

! -----------------------------------------------------------------------------
! Styles Type's Lock and hideparameters
! -----------------------------------------------------------------------------

if iStyle = STYLE_TOP_LOADING then
	lock "matGlass", "matControlKnob", "matDoorFrame", "matControlPanel"
	hideparameter "matGlass", "matControlKnob", "matDoorFrame", "matControlPanel"
else
	lock "matControlHandle", "ControlPanel"
	hideparameter "matControlHandle", "ControlPanel"
endif

if not(gs_bLaundryStorage) then
	lock "gs_LStorage_height"
	hideparameter  "gs_LStorage_height"
endif

! -----------------------------------------------------------------------------
! 2D symbols
! -----------------------------------------------------------------------------

values "gs_washer_symbol_type" stWasherSymbolTypes
values "iWasherSymbolType" 1, 2, 3, 4

if GLOB_MODPAR_NAME = "gs_washer_symbol_type" then
	for iWSymbol = 1 to vardim1(stWasherSymbolTypes)
		if gs_washer_symbol_type = stWasherSymbolTypes[iWSymbol] then iWasherSymbolType = iWSymbol
	next iWSymbol
	parameters iWasherSymbolType= iWasherSymbolType
else
	gs_washer_symbol_type = stWasherSymbolTypes[max(1, min(iWasherSymbolType, vardim1(stWasherSymbolTypes)))]
	parameters gs_washer_symbol_type = gs_washer_symbol_type
endif

if not(b2DSymbol) then
	lock "gs_washer_symbol_type", "iWasherSymbolType", "gs_symbol_pen", "gs_symbol_fill_type"
	hideparameter "gs_washer_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type"
endif
if b2DSymbol and (iWasherSymbolType <> 2) then
	lock "gs_symbol_fill_type"
	hideparameter "gs_symbol_fill_type"
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if gs_cont_pen <= 0 then parameters gs_cont_pen = 10
if gs_fill_pen <= 0 then parameters gs_fill_pen = 91
if penText <= 0 then parameters penText = 86

if gs_detlevel_3D_m = DET3D_SIMPLE then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = DET3D_OFF then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

! -----------------------------------------------------------------------------
! Texts
! -----------------------------------------------------------------------------

values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

DIM stSymbolType[2]
	stSymbolType[1]=`Детальный`
	stSymbolType[2]=`Простой`

values "symbolType" stSymbolType
values "iSymbolType" 1, 2

if GLOB_MODPAR_NAME = "symbolType" then
	iSymbolType = 2
	if symbolType=stSymbolType[1] then iSymbolType=1
	parameters iSymbolType = iSymbolType
else
	symbolType = stSymbolType[2]
	if iSymbolType=1 then symbolType=stSymbolType[1]
	parameters symbolType = symbolType
endif

if not(symboltypes_enable) | (symboltypes_enable & b2DSymbol) then
	iSymbolType = 2
	parameters iSymbolType = iSymbolType

	lock "symbolType"
	hideparameter "symbolType"
endif

if not(dt) then
	lock	"fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m", "stDisplayText", "penText"
	hideparameter	"fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m", "stDisplayText", "penText"
endif

values "fontSize" range (0,]

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------
values "MSFront" range [0,)
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

! -----------------------------------------------------------------------------
! Modifying to Shaft
! -----------------------------------------------------------------------------
if isMEPEnabled then
	if iStyle = STYLE_TOP_LOADING then
		conCorrZ = 0.23
	else
		conCorrZ = zzyzx / 30
	endif

	if NOT(ConStatus_1) AND NOT(ConStatus_2) then
		LOCK "gs_con_mat"
		hideparameter "gs_con_mat"
	endif

	DIM shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

	! --- CONNECTIONS max height limitations ---

	if iStyle = STYLE_TOP_LOADING then
		conCorrZ = 0.23
	else
		conCorrZ = zzyzx / 30
	endif

	! --- CONNECTIONS ---

	parameters MEP_NumberConnections = 2

	! --- 1st CONNECTION ---

	ConID = 1
	ConName = `Подключение Холодной Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if ConWidth_1 < 0.001 then
		ConWidth_1 = 0.001
		parameters ConWidth_1 = ConWidth_1
	endif
	if ConWidth_1 > MIN(A/2, (zzyzx-conCorrZ)/2) then
		ConWidth_1 = MIN(A/2, (zzyzx-conCorrZ)/2)
		parameters ConWidth_1 = ConWidth_1
	endif
	values "ConWidth_1" range (0, MIN(A/2, (zzyzx-conCorrZ)/2)]
	parameters ConDepth_1 = ConWidth_1
	values "ConLength_1" range [0, ]


	values "ConPosX_1" range [ConWidth_1/2, A-ConWidth_1/2]
	parameters ConPosY_1 = B
	values "ConPosZ_1" range [ConWidth_1/2, zzyzx-conCorrZ-ConWidth_1/2]


	CALL "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1-a/2,
		ConPosY = ConPosY_1-b,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


	! --- 2nd CONNECTION ---

	ConID = 2
	ConName = `Подключение Канализации`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if ConWidth_2 < 0.001 then
		ConWidth_2 = 0.001
		parameters ConWidth_2 = ConWidth_2
	endif
	if ConWidth_2 > MIN(A/2, (zzyzx-conCorrZ)/2) then
		ConWidth_2 = MIN(A/2, (zzyzx-conCorrZ)/2)
		parameters ConWidth_2 = ConWidth_2
	endif
	values "ConWidth_2" range (0, MIN(A/2, (zzyzx-conCorrZ)/2)]
	parameters ConDepth_2 = ConWidth_2
	values "ConLength_2" range [0, ]


	values "ConPosX_2" range [ConWidth_2/2, A-ConWidth_2/2]
	parameters ConPosY_2 = B
	values "ConPosZ_2" range [ConWidth_2/2, zzyzx-conCorrZ-ConWidth_2/2]


	CALL "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2-a/2,
		ConPosY = ConPosY_2-b,
		ConPosZ = ConPosZ_2,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	ac_mep_connectionpage_active = 0

	if gs_ui_current_page = TABID_MEP then
		ac_mep_connectionpage_active = 1
	endif

	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
else
	hideparameter "gs_connection", "gs_con_mat", "useSysMat"

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active
endif

! -----------------------------------------------------------------------------
! Onorm list Settings
! -----------------------------------------------------------------------------

if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

hideparameter all "A", "B", "ZZYZX"

