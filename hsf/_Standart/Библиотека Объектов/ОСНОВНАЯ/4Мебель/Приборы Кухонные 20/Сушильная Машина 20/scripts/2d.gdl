


! --- Oriented View ------------------------------ [

angleViewRot = 0
if dt then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]

DrawingNum = 1
if DrawingNum = 1 then	! Top

	unID = 1
	pen gs_cont_pen

	add2 a/2,b
	hotspot2 0, 0, unID: unID = unID + 1

	fill gs_fill_type
	drawindex 20
	poly2_b 5, 1+2+64, gs_fill_pen,gs_back_pen,
			-a/2,	0,	1,
			-a/2,	-b,	1,
			a/2,	-b,	1,
			a/2,	0,	1,
			-a/2,	0,	-1
	del 1

	! ==========================================================================
	! Symbols
	! ==========================================================================

	if b2DSymbol and not(compatibilityForSymbolTypes = COMP_STATE_PLACEDorDEFAULT) then
		pen gs_symbol_pen
		aorb = a
		if a > b then aorb = b
		add2 a/2,b/2

		if iDrierSymbolType = 1 then
			circle2 0,0,aorb/3.5
			line2 -aorb/6,aorb/7,aorb/6,aorb/7
			line2 0,aorb/7,0,-aorb/5
		endif

		if iDrierSymbolType = 2 then
			add2 0,b/5

			rc=aorb/6
			circle2 0, 0, rc
			line2 -0.25*rc, 0.96824*rc, -0.75*rc, -0.66143*rc
			line2 0.25*rc, 0.96824*rc, 0.75*rc, -0.66143*rc
			del 1
			add2 0, -b/3
			circle2 0, 0, aorb/15
			fill gs_symbol_fill_type
			poly2_b 7, 1+2+64, gs_symbol_pen, gs_back_pen,
					aorb/15,	0,		1,
					0,			0,		900,
					0,			180,	4001,
					-aorb/15,	0,		1,
					0,			0,		900,
					0,			180,	4001,
					aorb/15,	0,		1
			del 1
		endif

		if iDrierSymbolType = 3 then
			circle2 0,0,aorb/3.5
			circle2 0,0,aorb/4.5
		endif
		del 1
	endif

	drawindex 10
	call "fa_minimalspace" parameters all MSType = 20, unID = 3000

	call "Resize_A_B_ZZYZX" Parameters ALL

! ======================================================================
! 2D TEXT
! ======================================================================

	unID = 200
	if dt then
		drawindex 30
		nTrans = 0

		hotspot2 a/2+txtx,	b/2+txty,	unID,  txtx,  2    	: unID = unID + 1	! moving
		hotspot2 a/2,		b/2+txty,	unID,  txtx,  1+128 : unID = unID + 1	! base
		hotspot2 -1,		b/2+txty,	unID,  txtx,  3    	: unID = unID + 1	! reference

		hotspot2 a/2+txtx,	b/2,		unID,  txty,  1+128 : unID = unID + 1
		hotspot2 a/2+txtx,	b/2+txty,	unID,  txty,  2    	: unID = unID + 1
		hotspot2 a/2+txtx, 	-1,			unID,  txty,  3    	: unID = unID + 1

		pen tpen
		define style textstyle	fontType, fontSize * 1000 / GLOB_SCALE, 5,
								gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
		style textstyle
		add2 a/2 + txtx, b/2 + txty
		nTrans = nTrans + 1

		! Text Rotation - Readable / Always Horizontal / Align with Symbol
		! ----------------------------------------------------------------------
		if typeTextRotation_m = 2 then								! Readable
			rot2 180 * (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps))	! 1/0
			nTrans = nTrans + 1
		else
			if typeTextRotation_m = 1 then							! Horizontal
				rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED)) ! 1/-1
				nTrans = nTrans + 1
			endif
		endif
		! ----------------------------------------------------------------------

		text2 0,0,txt

		del nTrans
	else
		hotspot2 a/2, b/2, unID
	endif
endif
del TOP

! --- start of modifying to Shaft

if bShowMEP then

	pen gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	if DrawingNum = 2 then PROJECT2 -3, 90, 2	! Bottom
	if DrawingNum = 3 then PROJECT2 4, 270, 2	! Front
	if DrawingNum = 4 then PROJECT2 4, 90, 2	! Back
	if DrawingNum = 5 then PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	if DrawingNum = 6 then PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12

	del 1
endif

! --- end of modifying to Shaft
