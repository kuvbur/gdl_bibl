
!! Dryer

pen gs_cont_pen

if gs_detlevel_3D_m = 0 then end

addz storage_hgh

! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

if gs_detlevel_3D_m=1 then resol 8
if gs_detlevel_3D_m=2 then resol 24


if bShowMEP then

	if useSysMat then
		w_mat			= sMat
		k_mat			= sMat
		gs_con_mat		= sMat
		matGlass		= sMat
		matDoorFrame	= sMat
		matControlPanel	= sMat
		matControl		= sMat
	endif


	call "MEP_m_ConnectionsACL_4" parameters 	SetProgram 				= SetProgram,
												ui_current_con 			= ui_current_con,
												MEP_NumberConnections 	= MEP_NumberConnections,
												MEP_NumConnectionData 	= gs_Connections,
												gs_AddConnections 		= gs_AddConnections,
												cShow3D 				= 1,
												gs_ConMat 				= gs_con_mat,
												MEP_InsShow 			= 0,
												MEP_cline_show_3D 		= 0,
												gs_cont_pen 			= gs_cont_pen,
												ConPosX_1				= ConPosX_1,
												ConPosY_1				= ConPosY_1,
												ConPosZ_1				= ConPosZ_1,
												ConLength_1				= ConLength_1,
												ConEdit_1 				= 16
endif

! --- end of modifying to Shaft

material w_mat

if gs_detlevel_3D_m = 1 then gs_resol=3

if gs_shadow=0 then SHADOW OFF

add a/2,b,0
hotspot -a/2,0,0-storage_hgh
hotspot 0,0,ZZYZX-storage_hgh
hotspot a/2,0,0-storage_hgh
hotspot 0,-b/2,0-storage_hgh
hotspot a/2,-b,0-storage_hgh
hotspot -a/2,-b,0-storage_hgh

hotspot -a/2,0,appl_height
hotspot a/2,0,appl_height
hotspot a/2,-b,appl_height
hotspot -a/2,-b,appl_height
del 1

if iStyle = STYLE_PANEL then

	add a/2,b,0

	prism_    9,     	0.1,
			a/2,     	0.0,     	11,
			a/2,    	-b,     	11,
			a/2-0.02,	-b,     	11,
			a/2-0.02,	-b+0.01,    11,
		   -a/2+0.02,	-b+0.01,    11,
		   -a/2+0.02,	-b,     	11,
		   -a/2,   		-b,     	11,
		   -a/2,     	0.0,     	11,
			a/2,     	0.0,     	-1

	addz 0.1
	prism_    5,     0.25,
				a/2,     0.0,   10,
				a/2,    -b,     10,
			   -a/2,    -b,     10,
			   -a/2,     0.0,   10,
				a/2,     0.0,   -1
	lin_ -a/2 + 0.02,-b,0,a/2 - 0.02,-b,0
	del 1

	addz appl_height - 0.03
	prism_   5,     0.03,
				a/2,     0.0,   14,
				a/2,    -b,     14,
			   -a/2,    -b,     14,
			   -a/2,     0.0,   14,
				a/2,     0.0,   -1

	del 1

	rotx 90
	add 0,appl_height - 0.03,0

	prism_      5,       b - 0.03,
				a/2,     0.0,      				11,
				a/2,    -appl_height + 0.38,	11,
			   -a/2,    -appl_height + 0.38,	11,
			   -a/2,     0.0,      				11,
				a/2,     0.0,     				-1

	! --- door frame ---

	addz b-0.03
	resol 1.5 * gs_resol

	prism_      22,      	0.03,
				a/2,     	0.0,      				14,
				a/2,    	-appl_height+0.38,      14,
			   -a/2,   		-appl_height+0.38,      14,
			   -a/2,     	0.0,      				14,
				a/2,     	0.0,     				-1,

			   -a/2+0.09,	-0.1,					15+64,
			   -a/2+0.09,	-0.14,    				900,
				0.0,		90.0,   				4001,
			   -a/2+0.05,	-0.14,      			15+64,
			   -a/2+0.05,	-appl_height+0.47,      15+64,
			   -a/2+0.09,	-appl_height+0.47,    	900,
				0.0,		90.0,   				4001,
			   -a/2+0.09,	-appl_height+0.43,      15+64,
				a/2-0.09,	-appl_height+0.43,      15+64,
				a/2-0.09,	-appl_height+0.47,    	900,
				0.0,		90.0,   				4001,
				a/2-0.05,	-appl_height+0.47,      15+64,
				a/2-0.05,	-0.14,      			15+64,
				a/2-0.09,	-0.14,    				900,
				0.0,		90.0,   				4001,
				a/2-0.09,	-0.1,      				15+64,
			   -a/2+0.09,	-0.1,     				-1

	! ---door ---

	prism_      17,    		0.03,
				a/2-0.055,	-0.14,      		15+64,
				a/2-0.09,	-0.14,				900,
				0.0,      	90.0,   			4001,
				a/2-0.09,	-0.105,      		15+64,
			   -a/2+0.09,	-0.105,      		15+64,
			   -a/2+0.09, 	-0.14,    			900,
				0.0,      	90.0,   			4001,
			   -a/2+0.055, 	-0.14,      		15+64,
			   -a/2+0.055,	-appl_height+0.47,	15+64,
			   -a/2+0.09, 	-appl_height+0.47,	900,
				0.0,      	90.0,   			4001,
			   -a/2+0.09, 	-appl_height+0.435,	15+64,
				a/2-0.09, 	-appl_height+0.435,	15+64,
				a/2-0.09,  	-appl_height+0.47,	900,
				0.0,      	90.0,   			4001,
				a/2-0.055,	-appl_height+0.47,	15+64,
				a/2-0.055,	-0.14,     			-1
	del 1
	del 1
	del 1

	r = (a/2 - 0.01)**2 / 0.04 + 0.01

	if bControlPanel = 1 then

		add 0, 0, ZZYZX + 0.05 - storage_hgh
		rotx 65
		cutplane
		del 2

		add -a/2 + 0.35,0,ZZYZX - 1.1 - storage_hgh
		roty 75
		cutplane
		del 2

		rotx 90
		resol 8 * gs_resol
		prism_    6, 0.1,
			   -a/2+0.01,	appl_height,      		15,
			   -a/2+0.01,	ZZYZX-0.02-storage_hgh,	15+64,
				0.0,		ZZYZX-r-storage_hgh,    915,
				a/2-0.01,	ZZYZX-0.02-storage_hgh,	3015,
				a/2-0.01,	appl_height,      		15,
			   -a/2+0.01,	appl_height,     		-1
		del 1

		cutend

		add a/2 - 0.35, 0, ZZYZX-1.1 - storage_hgh
		roty -75
		cutplane
		del 2

		rotx 90
		prism_    6, 0.1,
			   -a/2+0.01,	appl_height,      		15,
			   -a/2+0.01,	ZZYZX-0.02-storage_hgh,	15+64,
				0.0,		ZZYZX-r-storage_hgh,    915,
				a/2-0.01,	ZZYZX-0.02-storage_hgh,	3015,
				a/2-0.01,	appl_height,			15,
			   -a/2+0.01,	appl_height,     		-1
		del 1
		
		cutend
		cutend

		material k_mat

		add 0, 0, ZZYZX + 0.05 - storage_hgh
		rotx 65
		cutplane
		del 2

		add -a/2 + 0.35, 0, ZZYZX - 1.1 - storage_hgh
		roty -105
		cutplane
		del 2

		add a/2 - 0.35, 0, ZZYZX - 1.1 - storage_hgh
		roty 105
		cutplane
		del 2

		rotx 90
		prism_    6,0.1,
			   -a/2+0.01,	appl_height,      		15,
			   -a/2+0.01,	ZZYZX-0.02-storage_hgh,	15+64,
				0.0,		ZZYZX-r-storage_hgh,    915,
				a/2-0.01,	ZZYZX-0.02-storage_hgh,	3015,
				a/2-0.01,	appl_height,      		15,
			   -a/2+0.01,	appl_height,     		-1
		del 1

		cutend
		cutend

		cutend

	endif
	!!!!! KNOB !!!!!
	
	mul a/0.7,1,1
	rotx 90
	add 0,appl_height-0.03,b
	resol gs_resol
	material k_mat
	prism_  8,	0.005,
			0,			0.02,		15+64,
			1,			0,			815+64,
			0.3185,		0.0118,		1015+64,
			0.3185,		-0.0118,	1015+64,
			0,			-0.02,		1015+64,
			-0.3185,	-0.0118,	1015+64,
			-0.3185,	0.0118,		1015+64,
			0,			0.02,		1015+64
	del 2
	del 1

	rotx 90
	add 0, appl_height - 0.145,b
	prism_      11,    0.005,
			   -a/2+0.09,	0.0,	15+64,
			   -a/2+0.09,	-0.025,	900,
				0.0, 		90.0,   4001,
			   -a/2+0.065,	-0.025,	15+64,
			   -a/2+0.065,	-0.035,	15+64,
				a/2-0.065, 	-0.035,	15+64,
				a/2-0.065, 	-0.025,	15+64,
				a/2-0.09, 	-0.025,	900,
				0.0,      	90.0,	4001,
				a/2-0.09,	0.0,	15,
			   -a/2+0.09,	0.0,	-1

	del 4

else	!------------------ Glazed front opening ---------------------

	if gs_detlevel_3D_m = DET3D_SIMPLE then
		gs_resol = 6
	endif
	resol gs_resol

	frontPanelHeight = appl_height - appl_height/30 - appl_height*12/90 - appl_height/90
	r = min(a/3.5, frontPanelHeight/3.5, appl_height/5)


	addy b*2/60
	material w_mat
	rotx 90
	prism_  8, b*2/60,
		0, 0,						15,
		a, 0, 						15,
		a, frontPanelHeight, 		15,
		0, frontPanelHeight, 		15,
		0, 0, 						13,
		0, 0, 						-1,
		a/2, frontPanelHeight/2, 	913,
		r, 360, 					4013


	if gs_detlevel_3D_m = DET3D_SIMPLE then
		prism_  2, 0.005,
			a/2, 	frontPanelHeight/2, 913,
			r, 		360, 				4013
	endif

	addz -b*2/60
	material matDoorFrame
	if gs_detlevel_3D_m = DET3D_DETAILED then
		prism_  3, b*2/60,
			a/2, 		frontPanelHeight/2, 979,
			r, 			360, 				4079,
			r-(r/5), 	360, 				4079
	endif
	del 2

	material matGlass
	add a/2, b*12/60, frontPanelHeight/2
	roty -90
	rotz -90
	if gs_detlevel_3D_m = DET3D_DETAILED then
		revolve 2,		360,		1,
				0,		r-(r/2.5),	0,
				b*10/60, r-(r/5),	1
	endif
	del 4

	!--- box ---

	if gs_detlevel_3D_m = DET3D_SIMPLE then
		add 0, b*2/60, 0
		prism_  5, frontPanelHeight+appl_height*13/90,
			0, 0,		15,
			a, 0,		15,
			a, b-0.02,	15,
			0, b-0.02,	15,
			0, 0,		-1
		del 1
	else
		material w_mat

		addy b*2/60
		rotx 90
		roty 90
		for m=1 to 2
			poly_ 5,
				0,				0,											1,
				b - b * 2 / 60,	0,											1,
				b - b * 2 / 60,	frontPanelHeight + appl_height * 13 / 90,	1,
				0,				frontPanelHeight + appl_height * 13 / 90,	1,
				0,				0,											-1
			addz a
		next m
		del 5

		addy b
		rotx 90
		poly_ 5,
			0, 0,											1,
			a, 0,											15,
			a, frontPanelHeight + appl_height * 13 / 90,	1,
			0, frontPanelHeight + appl_height  *13 / 90,	1,
			0, 0,											-1
		del 1

		rotx 180

		poly_ 5,
			0, 0,				1,
			a, 0,				15,
			a, b - b * 2 / 60,	1,
			0, b - b * 2 / 60,	1,
			0, 0,				-1

		del 2
	endif

	!--- control panel ---
	material matControlPanel
	add 0, b * 2 / 60, frontPanelHeight + appl_height / 90
	rotx 90
	prism_  5, b * 2 / 60,
		0, 0, 15,
		a, 0, 15,
		a, appl_height * 11 / 90, 15,
		0, appl_height * 11 / 90, 15,
		0, 0, -1

	addy -appl_height / 90
	poly_  5,
		0, 0,						1,
		a, 0,						1,
		a, appl_height * 13 / 90,	1,
		0, appl_height * 13 / 90,	1,
		0, 0,						-1
	del 3

	!--- counter ---

	material w_mat
	addz appl_height - (appl_height/30)
	prism_  5, appl_height/30,
		0, 0, 15,
		a, 0, 15,
		a, b, 15,
		0, b, 15,
		0, 0, -1
	del 1

	!--- knobs ---
	if gs_detlevel_3D_m = DET3D_DETAILED then
		material matControl
		add a/10, 0, appl_height - appl_height/30 - appl_height * 6/90
		rotx 90
		resol gs_resol
		for n=1 to 4
			cylind b/60, a/60
			addx a/15
		next n
		del 6
	endif

	del 1
endif
!!! ================ Laundry Storage ===========================

if gs_bLaundryStorage then
	call "fa_laundrystorage" parameters all
endif




