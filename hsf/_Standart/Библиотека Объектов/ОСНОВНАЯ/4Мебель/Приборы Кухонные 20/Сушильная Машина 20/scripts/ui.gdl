
ui_dialog `Параметры Сушильной Машины`
ui_current_page gs_ui_current_page

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_page TABID_Style, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "Style"

ui_page TABID_2DRepresentation, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

ui_page TABID_Text, TABID_2DRepresentation, `Текст Символа`, "uiTab_Text_flat(1)"
	gosub "SymText"

ui_page TABID_3DRepresentationSurfaces, TABID_ROOT, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationSurfacesPage"

ui_page TABID_MinimalSpace, TABID_ROOT, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalPage"

if isMEPEnabled then
	ui_page TABID_MEP, TABID_ROOT, `Соединения`, "uiTab_Connections_flat(1)"
		gosub "MepPage"
endif

ui_page TABID_Descriptions, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Text_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Connections_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"Style":
! ------------------------------------------------------------------------------

	pageStartY	= 0
	x0			= 0  			! start Position X Coordinate
	x1			= x0 + 1
	x2			= x1 + 210
	x3			= x2 + 11
	x4			= x3 + 9
	x5			= x4 + 91
	x6			= x5 + 32
	xEnd		= 444
	_yCurr		= pageStartY   	! start Position Y Coordinate
    _dy			= 24
	y1			= pageStartY + 51
	yEnd		= 265
	_infHeight	= 19
	_infWidth 	= 88
	_outfHeight	= 15

	ui_style 0,1
	ui_outfield `Стиль`,	x0, _yCurr,		x2 - x0, _outfHeight
	ui_style 0,0

	_yCurr = _yCurr +_dy

	ui_infield{3}	"iStyle",	x1,	_yCurr,	x2 - x1,	95,
		4,8,2,1,
		105,105,90,90,
		1, "", 1,
		2, "", 2

	_yCurr = _yCurr + _dy * 4.5

	ui_infield{3} "bControlPanel", x0, _yCurr, x2- x0, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Панель Управления`, 0,
					"", `Панель Управления`, 1

	_yCurr = _yCurr + _dy

	ui_infield{3} "gs_bLaundryStorage", x0, _yCurr, x2- x0, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Хранилище для Белья`, 0,
					"", `Хранилище для Белья`, 1

	ui_separator x3, pageStartY, x3, pageStartY + _dy * 9

	_yCurr = pageStartY

	ui_style 0,1
	ui_outfield `Размеры`,	x4, _yCurr,		xEnd - x4, _outfHeight
	ui_style 0,0

	_yCurr = _yCurr + _dy

	ui_infield	"A",		x6,    _yCurr, _infWidth,	_infHeight ui_tooltip `Ширина`
	_yCurr = _yCurr + _dy * 2

	ui_infield	"B",		x4,    _yCurr, _infWidth,	_infHeight ui_tooltip `Длина`
	_yCurr = _yCurr + _dy * 2.5

	ui_infield	"appl_height",	x4,    _yCurr, _infWidth,	_infHeight  ui_tooltip `Высота Корпуса`

	if iStyle = STYLE_PANEL then
		if bControlPanel then
			if gs_bLaundryStorage then
				ui_pict 1, x5, y1 , 118, 168, 1					! Dryer_Style_1.png
				ui_infield	"gs_LStorage_height",		x4,    _yCurr + _dy * 2 , _infWidth,	_infHeight ui_tooltip `Высота Хранилища для Белья`
			else
				ui_pict 3, x5, y1 , 118, 168, 1					! Dryer_Style_3.png
			endif
		else
			if gs_bLaundryStorage then
				ui_pict 2, x5, y1 , 118, 168, 1					! Dryer_Style_2.png
				ui_infield	"gs_LStorage_height",		x4,    _yCurr + _dy * 2, _infWidth,	_infHeight  ui_tooltip `Высота Хранилища для Белья`
			else
				ui_pict 4, x5, y1 , 118, 168, 1					! Dryer_Style_4.png
			endif
		endif
	else		!STYLE_GLAZED
		if gs_bLaundryStorage then
			ui_pict 6, x5, y1 , 118, 168, 1						! Dryer_Style_6.png
			ui_infield	"gs_LStorage_height",		x4,    _yCurr + _dy * 2, _infWidth,	_infHeight  ui_tooltip `Высота Хранилища для Белья`
		else
			ui_pict 5, x5, y1 , 118, 168, 1						! Dryer_Style_5.png
		endif
	endif
return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------

	x1 = 0
	x2 = 171
	x3 = 261
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => _fillWidth)
	pageStartY	= 5
	_yCurr 		= pageStartY
    _dy			= 24

	_pictWidth	= 84
	_pictHeight	= 43
	_infWidth	= 97
	_infHeight	= 50
	_cellWidth	= 84
	_cellHeight	= 43

	ui_infield{3} "iDrierSymbolType", x2, _yCurr, _infWidth, _infHeight,
				2, 9,						! Dryer_SymbTypes.png
				2, 1,						
				_cellWidth, _cellHeight,
				_pictWidth, _pictHeight,
				1, "", 1,
				2, "", 2


	_infHeight	= 19
	ui_style 0,1
	ui_infield{3} "b2DSymbol", x1,		_yCurr,		x2 - 2,	_infHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `2D-символ`, 0,
				"", `2D-символ`, 1
	ui_style 0,0
	_yCurr = _yCurr + _pictHeight + 10

	ui_infield{3} "dt", x1,		_yCurr,		x2 - 2,	_infHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `Показ Текста в Символе`, 0,
				"", `Показ Текста в Символе`, 1

	ui_pict_button ui_function, "", "uiFunctionText_flat(1)", x2, _yCurr - 1, 37, 22, -TABID_Text
	_yCurr = _yCurr + _dy

	_itemID = 1
	ui_listfield TABID_2DRepresentation, 0, _yCurr, 444, 265 - _yCurr, 1, "", ""

		ui_listitem _itemID, TABID_2DRepresentation, "gs_cont_pen", 			0, "ui_list_contourPen_flat(1)"		: _itemID = _itemID + 1
		ui_listitem _itemID, TABID_2DRepresentation, "gs_fill_type", 			0, "ui_list_fillType_flat(1)"		: _itemID = _itemID + 1
		ui_listitem _itemID, TABID_2DRepresentation, "gs_fill_pen", 			0, "ui_list_fillPen_flat(1)"			: _itemID = _itemID + 1
		ui_listitem _itemID, TABID_2DRepresentation, "gs_back_pen", 			0, "ui_list_fillBgPen_flat(1)"		: _itemID = _itemID + 1
		ui_listitem _itemID, TABID_2DRepresentation, "gs_symbol_pen", 			0, "ui_list_contourPen_flat(1)"		: _itemID = _itemID + 1
		ui_listitem _itemID, TABID_2DRepresentation, "gs_symbol_fill_type", 	0, "ui_list_fillType_flat(1)"		: _itemID = _itemID + 1
return

! ------------------------------------------------------------------------------
"SymText":
! ------------------------------------------------------------------------------

	call "ui_object_SymbolText" parameters	dt						= dt,
											txt						= txt,
											fontType				= fontType,
											fontSize				= fontSize,
											bParName_FontSize		= 1,
											gs_text_style_bold		= gs_text_style_bold,
											gs_text_style_italic	= gs_text_style_italic,
											gs_text_style_underline	= gs_text_style_underline,
											tpen					= tpen,
											typeTextRotation_m		= typeTextRotation_m,
											txtx					= txtx,
											txty					= txty

return

! ------------------------------------------------------------------------------
"3DRepresentationSurfacesPage":
! ------------------------------------------------------------------------------

	dim surfacesArray[]
	SurfItemID = 1   
	surfacesArray[SurfItemID]	= "w_mat"
	SurfItemID = SurfItemID + 1

	if iStyle = STYLE_GLAZED then
		surfacesArray[SurfItemID]	= "matGlass"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "matDoorFrame"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "matControlPanel"
		SurfItemID = SurfItemID + 1

		surfacesArray[SurfItemID]	= "matControl"
		SurfItemID = SurfItemID + 1
	else
		surfacesArray[SurfItemID]	= "k_mat"
		SurfItemID = SurfItemID + 1
	endif

	if isMEPEnabled then
		surfacesArray[SurfItemID]	= "gs_con_mat"
		SurfItemID = SurfItemID + 1
	endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k

	call "ui_object_3DRepresentation" parameters 	surfacesArray 		= surfacesArray,
													childFlagArray 		= childFlagArray,
													bShowResol			= 1,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													iSurfaceListID		= TABID_3DRepresentationSurfaces

return

! ------------------------------------------------------------------------------
"MinimalPage":
! ------------------------------------------------------------------------------

	x0		= 0
	x1		= x0 + 64
	x2		= x1 + 76
	x3		= x2 + 19
	x4		= x3 + 95
	x5		= x4 + 17
	xEnd	= 444

	pageStartY = 0
	y1		= pageStartY + 18
	y2		= y1 + 22
	y3		= y2 + 46
	y4		= y3 + 70
	yEnd	= 265

	_infHeight = 19
	_infWidth  = 90

	ui_pict 7, x2, y2, 121, 163, 1
	
	ui_infield	"MSSide2",		x1,    y1, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Сбоку 2`
	ui_infield	"A",			x3,    y1, _infWidth,	_infHeight ui_tooltip `Ширина`
	ui_infield	"MSSide",		x4,    y1, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Сбоку 1`
	ui_infield	"B",			x5,    y3, _infWidth,	_infHeight ui_tooltip `Длина`
	ui_infield	"MSFront",		x5,    y4, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Спереди`

return

! ------------------------------------------------------------------------------
"MepPage":
! ------------------------------------------------------------------------------

	DIM ui_con_type[1]
		ui_con_type[1] = 0

	call "MEP_m_ConnectionsACL_4" parameters ALL	ui_con_type			= ui_con_type,
													ui_system_var_pos	= 2

return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"
	
	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif
	
	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames


return
