
![#COMPATIBILITY] - START

if compatibilityForSymbolTypes = COMP_STATE_PLACEDorDEFAULT then
	bDefaultElem = (GLOB_INTGUID = "" | GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")

	if bDefaultElem then
		compatibilityForSymbolTypes = COMP_STATE_NEW_ELEMENT
	else
		compatibilityForSymbolTypes = COMP_STATE_OLD_UPDATED
		b2DSymbol = 0
		parameters b2DSymbol = b2DSymbol
	endif
	parameters compatibilityForSymbolTypes = compatibilityForSymbolTypes
endif

![#COMPATIBILITY] - end

call "FM_types" parameters all

values{2} "gs_detlevel_3D_m" 	DET3D_DETAILED,	`Детальный`,
								DET3D_SIMPLE, 	`Простой`,
								DET3D_OFF, 		`Откл.`

values "gs_resol" range [3, )
values{2} "iStyle"  STYLE_PANEL, `Панель`,
					STYLE_GLAZED, `Остекленная`

if iStyle = STYLE_GLAZED then
	lock "bControlPanel"
	hideparameter "bControlPanel"
endif

if gs_detlevel_3D_m = DET3D_SIMPLE then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = DET3D_OFF then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

If gs_cont_pen=0 then parameters gs_cont_pen=10
if gs_fill_pen=0 then parameters gs_fill_pen=91
if tpen=0 then parameters tpen=86

if not(dt) then
	lock	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m"
	hideparameter	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m"
endif

! ====== Setting the height parameters ====

if not(iStyle = STYLE_PANEL) then
	bControlPanel = 0
endif
parameters 	bControlPanel	= bControlPanel

if GLOB_MODPAR_NAME = "ZZYZX"  then
	appl_height = ZZYZX - gs_bLaundryStorage * gs_LStorage_height - 0.2 * bControlPanel
	parameters appl_height = appl_height
else
	ZZYZX = appl_height + gs_bLaundryStorage * gs_LStorage_height + 0.2 * bControlPanel
	parameters 	ZZYZX = ZZYZX
endif

values "gs_drier_symbol_type" stDrierSymbolTypes
values "iDrierSymbolType" 1, 2

if GLOB_MODPAR_NAME = "gs_drier_symbol_type" then
	if gs_drier_symbol_type = stDrierSymbolTypes[1] then iDrierSymbolType= 1
	if gs_drier_symbol_type = stDrierSymbolTypes[2] then iDrierSymbolType= 2
	parameters iDrierSymbolType= iDrierSymbolType
else
	if iDrierSymbolType= 1 then gs_drier_symbol_type = stDrierSymbolTypes[1]
	if iDrierSymbolType= 2 then gs_drier_symbol_type = stDrierSymbolTypes[2]
	parameters gs_drier_symbol_type = gs_drier_symbol_type
endif

if not(b2DSymbol) then
	lock "gs_drier_symbol_type", "iDrierSymbolType", "gs_symbol_pen", "gs_symbol_fill_type"
	hideparameter "gs_drier_symbol_type", "iDrierSymbolType", "gs_symbol_pen", "gs_symbol_fill_type"
endif
if b2DSymbol and iDrierSymbolType = 1 then
	lock "gs_symbol_fill_type"
	hideparameter "gs_symbol_fill_type"
endif

values "fontSize" range (0,]

! --- Minimal Space ---
values "MSFront" range [0,)
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

! --- start of modify to Shaft

	values "A" range(0.2, ]
	values "B" range(0.2, ]
	values "gs_LStorage_height" range(0.15, ]
	values "ZZYZX" range(0.8 + storage_hgh, ]
	values "appl_height" range(0.64, ]

if isMEPEnabled then

	if NOT(ConStatus_1) then
		LOCK "gs_con_mat"
		hideparameter "gs_con_mat"
	endif

	DIM shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	parameters MEP_NumberConnections = 1


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Подключение Канализации`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if ConWidth_1 < 0.001 then
		ConWidth_1 = 0.001
		parameters ConWidth_1 = ConWidth_1
	endif
	if ConWidth_1 > MIN(A/2, (ZZYZX-0.23)/2) then
		ConWidth_1 = MIN(A/2, (ZZYZX-0.23)/2)
		parameters ConWidth_1 = ConWidth_1
	endif
	values "ConWidth_1" RANGE (0, MIN(A/2, (ZZYZX-0.23)/2)]
	parameters ConDepth_1 = ConWidth_1
	values "ConLength_1" RANGE [0, ]


	values "ConPosX_1" RANGE [ConWidth_1/2, A-ConWidth_1/2]
	parameters ConPosY_1 = B
	values "ConPosZ_1" RANGE [ConWidth_1/2, ZZYZX-0.23-ConWidth_1/2]


	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif

	ac_mep_connectionpage_active = 0

	if gs_ui_current_page = TABID_MEP then
		ac_mep_connectionpage_active = 1
	endif

	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

else
	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	hideparameter "gs_connection", "gs_con_mat", "useSysMat"
endif

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

hideparameter all "A", "B", "ZZYZX"
