
PEN gs_cont_pen
if not(gs_shadow) then shadow off
if gs_detlevel_3D_m = DET3D_OFF then end
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m = DET3D_SIMPLE
resol gs_resol


! --- 3D Hotspot --------------------------------------------------------------
unID=1

hotspot	0, 0, 0, unID, zzyzx, 1 : unID=unID+1
hotspot	0, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	A, 0, 0, unID, zzyzx, 1 : unID=unID+1
hotspot	A, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	0, B, 0, unID, zzyzx, 1 : unID=unID+1
hotspot	0, B, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	A, B, 0, unID, zzyzx, 1 : unID=unID+1
hotspot	A, B, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, B, zzyzx, unID, zzyzx, 2 : unID=unID+1


! --- CALL COUNTERTOP ---------------------------------------------------------

call "fa_counter_sink" parameters all	gs_sink_type_m	= 0,
										side_vis_m		= side_vis_m + 1,
										dob_face		= 0,
										countsink_unID	= unID,
					returned_parameters unID

if rear_panel then rear_cab = rear_cab + 0.02
if left_panel then left_cab = left_cab + 0.02
if right_panel then right_cab = right_cab + 0.02

if gs_count_type_m > 0 then
	A 		= A - left_cab - right_cab
	B 		= B - front_cab - rear_cab
	ZZYZX 	= ZZYZX-count_th
	add left_cab, front_cab, 0
endif

CPH=ZZYZX/7

! --- Cabinet -----------------------------------------------------------------
MATERIAL gs_cabinet_mat
PRISM_ 5, ZZYZX,
	0,fr_th1+0.005,15,
	A,fr_th1+0.005,15,
	A,B,15,
	0,B,15,
	0,fr_th1+0.005,-1

ADDZ toe_h
PRISM_ 5, ZZYZX-toe_h-0.01,
	0.01,fr_th1,15,
	A-0.01,fr_th1,15,
	A-0.01,fr_th1+0.005,15,
	0.01,fr_th1+0.005,15,
	0.01,fr_th1,-1
DEL 1

! --- Toe ---------------------------------------------------------------------
MATERIAL toe_mat

PRISM_ 5, toe_h,
	0,toe_d,15,
	A,toe_d,15,
	A,fr_th1,15,
	0,fr_th1,15,
	0,toe_d,-1

if gs_door_type_m = 0 then
	! --- Front -------------------------------------------------------------------
	ADDZ toe_h
	MATERIAL doorframe_mat
	PRISM_ 5, ZZYZX-toe_h-CPH-0.005,
		0,0,15,
		A,0,15,
		A,fr_th1,15,
		0,fr_th1,15,
		0,0,-1
	DEL 1

	! --- Handle ------------------------------------------------------------------
	YY=0.02

	IF gs_detlevel_3D_m = DET3D_DETAILED THEN
		ADDZ ZZYZX-CPH-0.004
		MATERIAL HandleMat
		PLANE_ 4,
			0,0,0,1,
			A,0,0,1,
			A,fr_th1,0,1,
			0,fr_th1,0,1

		ROTx 45
		Addy -YY
		PLANE_ 4,
			YY*2,0,0,1,
			A-YY*2,0,0,1,
			A,YY,0,1,
			0,YY,0,1
		DEL 2
		DEL 1
	endif

else
	ADD 0,fr_th1,toe_h					!*** FRONT DOORS
	door_w = a
	door_h = ZZYZX - toe_h - CPH

	CALL "fa_cabdoortyp" PARAMETERS ALL gs_knob_type_m	= 0,
										cabdoortyp_unID	= unID,
						returned_parameters unID

	ADD a/2, -0.02, door_h-0.02
	ROTY 90
	IF gs_detlevel_3D_m = DET3D_DETAILED  then GOSUB "knobs"
	DEL 2
	DEL 1

endif

! --- Control Panel -----------------------------------------------------------
IF gs_detlevel_3D_m = DET3D_DETAILED THEN

	ADD 0,fr_th1,ZZYZX-CPH
	ROTx 90
	MATERIAL cp_mat

	PRISM_ 10, fr_th1,
		0,0,15,
		A,0,15,
		A,CPH,15,
		0,CPH,15,
		0,0,-1,
		A/45,A/45,15,
		A-A/45,A/45,15,
		A-A/45,CPH-A/45,15,
		A/45,CPH-A/45,15,
		A/45,A/45,-1

	PRISM_ 10, fr_th1,
		A/45*1.5,A/45*1.5,15,
		A-A/45*1.5,A/45*1.5,15,
		A-A/45*1.5,CPH-A/45*1.5,15,
		A/45*1.5,CPH-A/45*1.5,15,
		A/45*1.5,A/45*1.5,-1,
		A*2/10,CPH/3,15,
		A*4/10,CPH/3,15,
		A*4/10,CPH*2/3,15,
		A*2/10,CPH*2/3,15,
		A*2/10,CPH/3,-1

	MATERIAL gs_button_mat
	PRISM_ 5, fr_th1*2/3,
		A*2/10,CPH/3,15,
		A*4/10,CPH/3,15,
		A*4/10,CPH*2/3,15,
		A*2/10,CPH*2/3,15,
		A*2/10,CPH/3,-1

	add a*3/4,CPH/2,fr_th1

	fprism_ gs_button_mat, gs_button_mat, gs_button_mat, gs_button_mat,
			2, 0.015, 45, 0.005,
			0, 0, 979,
			CPH/6, 360, 4079
	del 1

	DEL 2
ELSE
	ADD 0,fr_th1,ZZYZX-CPH-TopPanel*0.005
	ROTx 90
	MATERIAL cp_mat
	PRISM_ 5, fr_th1,
		0,0,15,
		A,0,15,
		A,CPH,15,
		0,CPH,15,
		0,0,-1
	DEL 2
ENDIF

if gs_count_type_m > 0 then del 1

! --- CALL END PANELS ---------------------------------------------------------

if rear_panel then
	door_w=a: door_h=ZZYZX-toe_h
	add door_w+left_cab,b+front_cab,toe_h
	rotz 180
		GOSUB "endPanels"
	del 2
endif

if left_panel then
	door_w=b-fr_th1: door_h=ZZYZX-toe_h
	add left_cab,door_w+front_cab+fr_th1,toe_h
	rotz -90
		GOSUB "endPanels"
	del 2
endif

if right_panel then
	door_w=b-fr_th1: door_h=ZZYZX-toe_h
	add left_cab+a,front_cab+fr_th1,toe_h
	rotz 90
		GOSUB "endPanels"
	del 2
endif

END

! --- END PANELS --------------------------------------------------------------
"endPanels":
		!! on rear panel there is no glas and grilles and knobs!!
		gs_knob_type_m=0
		If iEndPanelType = 1 	then gs_door_type_m=1
		If iEndPanelType = 2 	then gs_door_type_m=2
		If iEndPanelType = 4 	then gs_door_type_m=11
		If iEndPanelType = 7 	then gs_door_type_m=4
		If iEndPanelType = 9 	then gs_door_type_m=6
		If iEndPanelType = 99 	then gs_door_type_m=10

		gs_full_edit=0
		door_ang=0

	CALL "fa_cabdoortyp" PARAMETERS ALL cabdoortyp_unID=unID,
		fr_th1=0.02,gs_cust_p=gs_cust_p_end,doorframe_w=doorframe_w_end,
		doorframe_mat=doorframe_mat_end,doorglas_mat=doorframe_mat_end,
		door_w = door_w, door_h = door_h, gs_knob_type_m = 0, gs_door_type_m = gs_door_type_m,
		returned_parameters unID

RETURN

"knobs":

IF gs_detlevel_3D_m = DET3D_DETAILED THEN

		if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
			ADDz 0
			addy -fr_th1+0.02
			!!! knob on top drawer
			CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
					gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
					gs_knob_mat=gs_knob_mat
			del 2
		else
			if gs_knob_type_m=8 then
				roty 180
				Addz knob_size/2
				addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
				del 3
			else
				if gs_knob_type_m=4 then
					Addz 0.05
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				else
					Addz knob_size/2
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				endif
			endif
		endif
	endif

return

