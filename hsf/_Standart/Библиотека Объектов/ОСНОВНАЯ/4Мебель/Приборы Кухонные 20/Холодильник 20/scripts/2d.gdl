

! --- Oriented View ------------------------------ [

angleViewRot = 0
if dt then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]


! ======================================================================
! SCALE
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

unID = 1
pen gs_cont_pen
LINE_TYPE gs_line_type


HOTSPOT2 0,b,	unID : unID = unID + 1
HOTSPOT2 a/2,b, unID : unID = unID + 1


DRAWINDEX 20
fill gs_fill_type

if symboltypes_enable and det2D = 3 and iSymbolType = 1 then
	mul2 a/0.6, b/0.6
	POLY2_B 15, 1+2+64,gs_fill_pen,gs_back_pen,
			0.580642080675,		0,					1,
			0.5806420806751,	0.01935791932484,   900,
			0,					90,					4001,
			0.6,				0.01935791932475,   1,
			0.6,				0.6,				1,
			0,					0.6,				1,
			0,					0.01935791932475,   1,
			0.01935791932498, 	0.01935791932477,	900,
			0,					90,					4001,
			0.019357919325,		0,					1,
			0.04585433388241,	0,					1,
			0.04585433388241,	-0.02581055909982,  1,
			0.1103807316317,	-0.02581055909982,  1,
			0.1103807316317,	0,					1,
			0.580642080675,		0,					1

	line2    0.6, 0.09033695684914, 0, 0.09033695684914
	line2    0.6, 0.1032422363988, 0, 0.1032422363988
	line2    0.04585433388241, 0, 0.1103807316317, 0

	del 1
else
	poly2_b 5, 1+2+4+64,gs_fill_pen,gs_back_pen,
			0,0,1,
			0,b,1,
			a,b,1,
			a,0,1,
			0,0,-1
	if (det2D > 1 and det2D < 4) then
		line2 0,door_thk,a,door_thk
	endif
endif


! ======================================================================
! Symbols
! ======================================================================

if det2D = 4 then
	pen gs_symbol_pen
	add2 a/2, b/2
	aorb = a
	if a > b then aorb = b

	if iFridgeSymbolType = 1 then
		line2 -a/2, -b/6, a/2, -b/6
		add2 0, b/6
		gosub "star"
		del 1
		add2 0, -b/3
		gosub "star"
		del 1
		add2 a/4, -b/3
		gosub "star"
		del 1
		add2 -a/4, -b/3
		gosub "star"
		del 1
	endif

	if iFridgeSymbolType = 2 or iFridgeSymbolType = 3 then
		line2 -a/2, b/4, a/2, b/4
		circle2 0, b/4*1.5, aorb/20
		add2 0, b/4*1.5
		fill gs_symbol_fill_type
		poly2_b  7, 1+2+64, gs_symbol_pen, gs_fill_back_pen,
				 aorb/20, 0, 0,
				 0, 0,    900,
				 0, 180,  4000,
				-aorb/20, 0,  0,
				 0, 0,  900,
				 0, 180,4000,
				 aorb/20, 0, 0

		del 1
	endif

	if iFridgeSymbolType = 2 then
		add2 0, -b/3.5
		gosub "star"
		del 1
		add2 a/4, b/20
		gosub "star"
		del 1
		add2 -a/4, b/20
		gosub "star"
		del 1
	endif

	if iFridgeSymbolType = 4 then
		LINE_TYPE 1
		line2 0, aorb/6, 0, -aorb/6
		rot2 45
		line2 0, aorb/6, 0, -aorb/6
		rot2 45
		line2 0, aorb/6, 0, -aorb/6
		rot2 45
		line2 0, aorb/6, 0, -aorb/6
		del 3
		LINE_TYPE gs_line_type
	endif
	del 1

	if iFridgeSymbolType = 5 then
		LINE2 a/2-(a/4),b/2,a/2+(a/4),b/2
		CIRCLE2 a/2,b/2+(b/6),0.01
		CIRCLE2 a/2,b/2-(b/6),0.01
	endif

	if iFridgeSymbolType = 6 then
		LINE2 a/2-(a/4),b/2+(b/50),a/2+(a/4),b/2+(b/50)
		LINE2 a/2-(a/4),b/2-(b/50),a/2+(a/4),b/2-(b/50)
		CIRCLE2 a/2,b/2+(b/6),0.01
		CIRCLE2 a/2,b/2-(b/6),0.01
	endif

	if iFridgeSymbolType = 7 then

		! Refrigerator
		LINE2 a/2-(4*(a/10)),b/2+(b/4),a/2-(a/10),b/2+(b/4)
		CIRCLE2 a/2-(a/4),b/2+(3*(b/8)),0.01
		CIRCLE2 a/2-(a/4),b/2+(b/8),0.01

		!Icebox
		LINE2 a/2+(4*(a/10)),b/2-(b/4)+(b/50),a/2+(a/10),b/2-(b/4)+(b/50)
		LINE2 a/2+(4*(a/10)),b/2-(b/4)-(b/50),a/2+(a/10),b/2-(b/4)-(b/50)
		CIRCLE2 a/2+(a/4),b/2-(3*(b/8)),0.01
		CIRCLE2 a/2+(a/4),b/2-(b/8),0.01
	endif

	if iFridgeSymbolType = 8 then
		LINE2 0, b*6/7, a, b*6/7
		for i = 1 to 12
			add2 a/12, 0
			LINE2 0, b*6/7, 0, b
		next i
		del i-1
	endif
endif

DRAWINDEX 10

call "Resize_A_B_ZZYZX" Parameters ALL

call "fa_minimalspace" parameters all MSType = 20, unID = 3000


! ======================================================================
! 2D TEXT
! ======================================================================
unID = 200

if dt then
	DRAWINDEX 30
	nTrans = 0

	HOTSPOT2 a/2,		b/2+txty,	unID,  txtx,  1+128 : unID=unID+1
	HOTSPOT2 a/2+txtx,	b/2+txty,	unID,  txtx,  2		: unID=unID+1
	HOTSPOT2 -1,		b/2+txty,	unID,  txtx,  3		: unID=unID+1

	HOTSPOT2 a/2+txtx,	b/2,		unID,  txty,  1+128 : unID=unID+1
	HOTSPOT2 a/2+txtx,	b/2+txty,	unID,  txty,  2		: unID=unID+1
	HOTSPOT2 a/2+txtx,	-1,			unID,  txty,  3		: unID=unID+1

	pen tpen
	define style textstyle	fontType, fontSize * 1000 / GLOB_SCALE, 5,
							gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
	style textstyle
	add2 a/2+txtx,b/2+txty
	nTrans = nTrans + 1

	! Text Rotation - Readable / Always Horizontal / Align with Symbol
	! ----------------------------------------------------------------------
	if typeTextRotation_m = 2 then								! Readable
		rot2 180 * (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps))
		nTrans = nTrans + 1
	else
		if typeTextRotation_m = 1 then							! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED))	! 1/-1
			nTrans = nTrans + 1
		endif
	endif
	! ----------------------------------------------------------------------

	text2 0,0,txt

	del nTrans
else
	HOTSPOT2 a/2,b/2, unID
endif
end

"star":
	LINE_TYPE 1
	line2 0, aorb/10, 0, -aorb/10
	rot2 60
	line2 0, aorb/10, 0, -aorb/10
	del 1
	rot2 120
	line2 0, aorb/10, 0, -aorb/10
	del 1
	LINE_TYPE gs_line_type
return
