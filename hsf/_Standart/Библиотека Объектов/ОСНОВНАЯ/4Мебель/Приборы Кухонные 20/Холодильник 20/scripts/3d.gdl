
pen gs_cont_pen
if gs_detlevel_3D_m=0 then end
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

if not(gs_shadow) then SHADOW OFF

!* 3D Hotspot ************************************
unID=1

hotspot	0, 0, 0, 		unID, zzyzx, 1 : unID=unID+1
hotspot	0, 0, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, 0, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

hotspot	A, 0, 0, 		unID, zzyzx, 1 : unID=unID+1
hotspot	A, 0, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, 0, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

hotspot	0, B, 0, 		unID, zzyzx, 1 : unID=unID+1
hotspot	0, B, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, B, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

hotspot	A, B, 0,		unID, zzyzx, 1 : unID=unID+1
hotspot	A, B, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, B, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

if not(bFreezer) and (gs_handle_style_m = 1 or gs_handle_style_m = 3 or gs_handle_style_m = 5) then
	diff = 0
	if gs_handle_style_m = 3 then diff = 0.03
	hotspot	A-diff, 0, 0, 			unID, HandlePos, 1+128 : unID=unID+1
	hotspot	A-diff, 0, -1, 			unID, HandlePos, 3+128 : unID=unID+1
	hotspot	A-diff, 0, HandlePos, 	unID, HandlePos, 2 : unID=unID+1
endif
!**************************************************

c=zzyzx

if topp then
	material toppmat
	addz c-0.025
	prism_ 5,	0.025,
			0,	0,	15,
			a,	0,	15,
			a,	b,	15,
			0,	b,	15,
			0,	0,	1
	del 1
endif

topv=0
if topp then topv=0.025
material gs_cabinet_mat
prism_ 5,c-topv,
		0,	door_thk+0.005,	15,
		a,	door_thk+0.005,	15,
		a,	b,				15,
		0,	b,				15,
		0,	door_thk+0.005,	-1

if not(bToe) then goto 5
material toe_mat
!!!!Toe
prism_ 5,toe_h,
		0,	toe_d,		15,
		a,	toe_d,		15,
		a,	door_thk,	15,
		0,	door_thk,	15,
		0,	toe_d,		-1
goto 5
end

5:
material gs_front_mat
if topp then topv=0.0255

if not(bFreezer)		 then goto 10			! None
if gs_freezer_type_m = 1 then goto 20			! Top
if gs_freezer_type_m = 2 then goto 30			! Bottom

! no freezer
10:
	add 0,door_thk,_actToeHeight
	rotx 90
	prism_ 5,door_thk,
			0,0,15,
			a,0,15,
			a,c-_actToeHeight-topv,15,
			0,c-_actToeHeight-topv,15,
			0,0,-1
	del 2

	gosub 130
	gosub 150
	del top
end


! top freezer
20:
	add 0,door_thk,_actToeHeight
	rotx 90
	prism_ 5,door_thk,
			0,0,15,
			a,0,15,
			a,c-freh-_actToeHeight-0.005-topv,15,
			0,c-freh-_actToeHeight-0.005-topv,15,
			0,0,-1
	del 2

	add 0,door_thk,c-freh-topv
	rotx 90
	prism_ 5,door_thk,
			0,0,15,
			a,0,15,
			a,freh,15,
			0,freh,15,
			0,0,-1
	del 2
	gosub 130
	gosub 140
end


! bottom freezer
30:
	add 0,door_thk,freh+_actToeHeight+0.005
	rotx 90
	prism_ 5,door_thk,
			0,0,15,
			a,0,15,
			a,c-freh-_actToeHeight-0.005-topv,15,
			0,c-freh-_actToeHeight-0.005-topv,15,
			0,0,-1
	del 2

	add 0,door_thk,_actToeHeight
	rotx 90
	prism_ 5,door_thk,
			0,0,15,
			a,0,15,
			a,freh,15,
			0,freh,15,
			0,0,-1
	del 2
	gosub 130
	gosub 140
end


! HANDLES

40:	!!! *** Handle 1A-with freezer

	if gs_freezer_type_m = 1 then					! Top
		ADD a, -0.03, c - topv - freh - 0.005 - HandleSize
	else
		ADD a, -0.03, _actToeHeight + freh + 0.005			! Bottom
	endif
		gosub "handle1"									! Top Handle
	del 1

	if gs_freezer_type_m = 1  then
		ADD a, -0.03, c - freh - topv					! Top
	else
		ADD a, -0.03, _actToeHeight + freh - HandleSize		! Bottom
	endif
	gosub "handle1"									! Bottom Handle
	del 1
END

45:	!!! *** Handle 1A-no freezer

	ADD a, -0.03, HandlePos-HandleSize/2
	gosub "handle1"
	DEL 1

END


50:	!!! *** Handle 1B-with freezer

	if gs_freezer_type_m = 1 then			! Top
		ADD a, -0.03, _actToeHeight+freh/2-topv
	else
		ADD a, -0.03, _actToeHeight+freh+0.005		! Bottom
	endif

	!* Bottom Handle
	HandleSize = c-_actToeHeight-freh-freh/2-0.005
	gosub "handle1"
	DEL 1

	if gs_freezer_type_m = 1 then
		ADD a, -0.03,c-freh-topv			! Top
	else
		ADD a, -0.03,_actToeHeight+freh/2			! Bottom
	endif
	!* Top Handle
	HandleSize = freh/2
	gosub "handle1"
	DEL 1
END


55:	!!! *** Handle 1B-No freezer

	ADD a, -0.03, (_actToeHeight+c-3*c/4-topv)/2
	HandleSize = c-c/4
	gosub "handle1"
	DEL 1
END


60: !!! *** Handle 2A-with freezer
	N = HandleSize
	if gs_freezer_type_m = 2 then
		placeMod = _actToeHeight + freh - N/2 - 0.03		! Bottom
	else
		placeMod = c - topv - freh + N/2 + 0.03
	endif

	!* Top Handle
	gosub "handle2"

	if gs_freezer_type_m = 2 then
		placeMod = _actToeHeight + freh + N/2 + 0.035		! Bottom
	else
		placeMod = c - topv - freh - N/2 - 0.035
	endif
	!* Bottom Handle
	gosub "handle2"
END


65: !!! *** Handle 2A-no freezer

	N = HandleSize
	placeMod = HandlePos
	gosub "handle2"
END


70:	!!! *** Handle 2B-with freezer
	N = freh*1/3
	if gs_freezer_type_m = 2 then
		placeMod = freh + _actToeHeight - N					! Bottom
	else
		placeMod = c - freh + N - topv
	endif
	gosub "handle2"

	N = (c - freh - _actToeHeight)* 2/3
	if gs_freezer_type_m = 2 then
		placeMod = _actToeHeight + freh + N/2 + 0.05		! Bottom
	else
		placeMod = c - N*3/5 - freh - topv
	endif
	gosub "handle2"
end


75:	!!! *** Handle 2B-no freezer
	N=c*2/3
	placeMod = _actToeHeight+((c-_actToeHeight)/2)-topv
	gosub "handle2"
end


80: !!! *** Handle 3A-with freezer

	handle = HandleSize

	if gs_freezer_type_m = 2 then
		placeMod2 = HandleSize + _actToeHeight + freh + 0.035	! Bottom
	else
		placeMod2 = c - freh - 0.03 - topv
	endif
	!* Bottom Handle
	gosub "handle3"

	if gs_freezer_type_m = 2 then
		placeMod2 = freh + _actToeHeight - 0.03					! Bottom
	else
		placeMod2 = c - freh - topv + HandleSize + 0.025
	endif
	!* Top Handle
	gosub "handle3"
END


85: !!! *** Handle 3A-no freezer

	handle = HandleSize
	placeMod2 = HandlePos + HandleSize/2
	gosub "handle3"
END


90: !!! *** Handle 3B-with freezer

	handle = (c-freh)*2/3
	if gs_freezer_type_m = 2 then
		 placeMod2 = freh + _actToeHeight + handle + 0.05		! Bottom
	else
		placeMod2 = c - freh - 0.03 - topv
	endif
	gosub "handle3"

	handle = 0.20
	if gs_freezer_type_m = 2 then
		placeMod2 = freh + _actToeHeight - 0.03					! Bottom
	else
		placeMod2 = c - freh - topv + handle + 0.025
	endif
	gosub "handle3"
END

95: !!! *** Handle 3B-no freezer

	handle = c*2/3
	placeMod2 = c-((c - handle - _actToeHeight)/2) - topv
	gosub "handle3"
END


130:
reqBKnobs = 0
success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
if success > 0 and reqBKnobs then
	end
endif
if not(bHandle) OR gs_detlevel_3D_m = 1  then  end	!* no handle
return

140:		!with freezer
	MATERIAL handle_mat
	if gs_handle_style_m = 1 then goto 40	!1A
	if gs_handle_style_m = 2 then goto 50	!1B
	if gs_handle_style_m = 3 then goto 60	!2A
	if gs_handle_style_m = 4 then goto 70	!2B
	if gs_handle_style_m = 5 then goto 80	!3A
	if gs_handle_style_m = 6 then goto 90	!3B
return


150:		!no freezer
	MATERIAL handle_mat
	if gs_handle_style_m = 1 then goto 45	!1A
	if gs_handle_style_m = 2 then goto 55	!1B
	if gs_handle_style_m = 3 then goto 65	!2A
	if gs_handle_style_m = 4 then goto 75	!2B
	if gs_handle_style_m = 5 then goto 85	!3A
	if gs_handle_style_m = 6 then goto 95	!3B
return

"handle1":
	ROTY -90
	RECT HandleSize, 0.03
	ROTX 90
	RECT HandleSize, 0.03
	del 2
return

"handle2":
	addz placeMod
	roty 90
	resol  72
	CUTPLANE -90
	X = SQR(4*N*N - 1/4*N*N)
	N2 = 2*N - X

	add  0, 2*N - N2, a-0.05
	PRISM_ 4,		0.03,
			0,			 0,		979,
			2*N,		 360,	4079,
			0,			 0,		979,
			1.99*N-0.005,360,	4079
	del  1
	CUTEND
	del 2
return

"handle3":
	add a - 0.005, 0, placeMod2
	rotx 90
	roty 270
	rotx 180

	bprism_ handle_mat,handle_mat,handle_mat,
			9,		0.005,	0.025,
			0,		0,		15,
			0.05,	0,		15,
			0.05,	handle,	15,
			0,		handle,	15,
			0,		handle-0.025,	15,
			0.03,	handle-0.025,	15,
			0.03,	0.025,	15,
			0,		0.025,	15,
			0,		0,		-1

	del 4
return
