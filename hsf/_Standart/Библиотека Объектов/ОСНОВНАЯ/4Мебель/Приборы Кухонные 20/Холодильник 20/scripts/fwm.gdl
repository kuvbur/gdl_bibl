
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "6F7DDA68-C2A6-4347-8D1B-41D82F94BA06"
	_startID	= "95647698-CBAD-4E6E-B9ED-C30E5EC043C4"
	_endID		= "D3E32F75-9CA3-4287-8C1F-1EACD74084F4"
gosub "newBooleansToeFreezer_FWM"

	_startID	= "D3E32F75-9CA3-4287-8C1F-1EACD74084F4"
	_endID		= "9E10A639-E249-42EF-9C3C-7B2A6DB73D0C"
gosub "FontSizeParamType_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! newBooleansToeFreezer
! ==============================================================================
"newBooleansToeFreezer_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
		! If toe_h was 0, that equals with bToe being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("toe_h", _toe_h)

		if abs(_toe_h) < EPS then
			bToe = 0
		else
			bToe = 1
		endif
		parameters bToe = bToe

		! If gs_freezer_type_m = 0 that equals with bFreezer being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_freezer_type_m", _gs_freezer_type_m)

		if _gs_freezer_type_m = 0 then
			bFreezer = 0
		else
			bFreezer = 1
		endif
		parameters	bFreezer = bFreezer

		!Throwing out old compatibility issues from script
		bHas = STORED_PAR_VALUE("gs_detlevel_2D_m", _gs_detlevel_2D_m)
		if bHas then
			if _gs_detlevel_2D_m = -1 then
				gs_detlevel_2D_m = 1	! 1:200
				parameters gs_detlevel_2D_m = gs_detlevel_2D_m
			endif
		endif

		actualGuid = _endID
	endif

return

! ==============================================================================
! Make the Font Size parameter a Length Type parameter
! ==============================================================================
"FontSizeParamType_FWM":
	if actualGuid = _startID then
		bHas = DELETED_PAR_VALUE("fsz", _fsz)
		
		fontSize = _fsz / 100
		parameters fontSize = fontSize
		
		actualGuid = _endID
	endif
return
