

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT									= -1
_idxTab = 1

	TABID_Style								= _idxTab  : _idxTab = _idxTab + 1
	TABID_Handle							= _idxTab  : _idxTab = _idxTab + 1
	TABID_2DRepresentation					= _idxTab  : _idxTab = _idxTab + 1
	TABID_Text								= _idxTab  : _idxTab = _idxTab + 1
	TABID_3DRepresentationSurfaces			= _idxTab  : _idxTab = _idxTab + 1
	TABID_MinimalSpace						= _idxTab  : _idxTab = _idxTab + 1
	TABID_Descriptions						= _idxTab  : _idxTab = _idxTab + 1

ui_current_page gs_ui_current_page
ui_dialog `Параметры Холодильника`

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_page TABID_Style, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "Style"

	ui_page TABID_Handle, TABID_Style, `Ручка`, "uiTab_Handle_flat(1)"
		gosub "Handle"

ui_page TABID_2DRepresentation, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationPage"

	ui_page TABID_Text, TABID_2DRepresentation, `Текст Символа`, "uiTab_Text_flat(1)"
		gosub "SymText"

ui_page TABID_3DRepresentationSurfaces, TABID_ROOT, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationSurfacesPage"

ui_page TABID_MinimalSpace, TABID_ROOT, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalPage"

ui_page TABID_Descriptions, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Handle_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Text_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"Style":
! ------------------------------------------------------------------------------

	pageStartY	= 0
	x0			= 0  			! start Position X Coordinate
	x1			= x0 + 33
	x2			= x1 + 88
	x3			= x2 + 2
	x4			= x3 + 3
	x5			= x4 + 51
	x6			= x5 + 34
	x7			= x6 + 21
	x8			= x7 + 79
	xEnd		= 444
	_yCurr		= pageStartY   	! start Position Y Coordinate
    _dy			= 24
	y1			= pageStartY + 95
	yEnd		= 266
	_infHeight	= 19
	_infWidth 	= 90

	ui_infield{3} "bFreezer", x0, _yCurr, x0 + x4 - 3, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Мороз. Камера`, 0,
					"", `Мороз. Камера`, 1

	ui_infield{3} "gs_freezer_type_m", x4, _yCurr,	_infWidth,	_infHeight,
			8, "",
			2,
			1, 1, 20, 1, 20,
			"", `Верх`, 1,
			"", `Низ`, 2

	ui_infield{3} "bToe", x7, _yCurr, 442 - x7, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Основание`, 0,
					"", `Основание`, 1
	
	_yCurr = _yCurr + _dy

	ui_infield{3} "bHandle", x0, _yCurr, x0 + x5 - 3, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Ручка`, 0,
					"", `Ручка`, 1

	ui_pict_button ui_function, "", "uiFunctionHandle_flat(1)", x5, _yCurr-1, 37, 22, -TABID_Handle

	ui_infield{3} "topp", x7, _yCurr, 442 - x7, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Верхняя Панель`, 0,
					"", `Верхняя Панель`, 1

	_yCurr = _yCurr + _dy

	ui_separator 0,_yCurr, xEnd, _yCurr

	if bFreezer then

		if bToe then
			ui_infield	"toe_h",		x1,    _yCurr + _dy * 7, _infWidth,	_infHeight ui_tooltip `Высота Основания`
			ui_infield	"toe_d",		x4,    _yCurr + _dy * 8, _infWidth,	_infHeight ui_tooltip `Отступ Основания`
 
			if gs_freezer_type_m = 1 then			!Top
				ui_pict 6, x4, y1 , 181, 143, 1		! Refrigerator_Toe_TopF.png
				_dyMod = 3
			else									!Bottom
				ui_pict 5, x4, y1 , 181, 143, 1		! Refrigerator_Toe_LowF.png
				_dyMod = 6
			endif
		else
			if gs_freezer_type_m = 1 then			!Top
				ui_pict 4, x4, y1 , 181, 143, 1		! Refrigerator_TopF.png
				_dyMod = 3
			else									!Bottom
				ui_pict 3, x4, y1 , 181, 143, 1		! Refrigerator_LowF.png
				_dyMod = 6
			endif
		endif
	ui_infield	"freh",			x1,    _yCurr + _dy * _dyMod, _infWidth,	_infHeight ui_tooltip `Высота Морозильной Камеры`
	else
		if bToe then
			ui_infield	"toe_h",		x1,    _yCurr + _dy * 7, _infWidth,	_infHeight ui_tooltip `Высота Основания`
			ui_infield	"toe_d",		x4,    _yCurr + _dy * 8, _infWidth,	_infHeight ui_tooltip `Отступ Основания`
			ui_pict 2, x4, y1 , 181, 143, 1				! Refrigerator_Toe.png
		else
			ui_pict 1, x4, y1 , 181, 143, 1				! Refrigerator.png
		endif
	endif

	ui_infield	"B",			x4,    _yCurr + _dy * 1, _infWidth,	_infHeight ui_tooltip `Размер 2`
	ui_infield	"A",			x7,    _yCurr + _dy * 1, _infWidth,	_infHeight ui_tooltip `Размер 1`
	ui_infield	"door_thk",		x1,    _yCurr + _dy * 2, _infWidth,	_infHeight ui_tooltip `Толщина Двери`
	ui_infield	"ZZYZX",		x8,    _yCurr + _dy * 4, _infWidth,	_infHeight ui_tooltip `Общая Высота`
return

! ------------------------------------------------------------------------------
"Handle":
! ------------------------------------------------------------------------------
	x0		= 0
	x1		= x0 + 78
	x2		= x1 + 135
	x3		= x2 + 18
	x4		= x3 + 123
	xEnd	= 444
	_yCurr	= pageStartY
    _dy			= 25
	yEnd		= 266
	_outfHeight	= 15
	_infHeight	= 25

	ui_style 0,1
	ui_infield{3} "bHandle", x0, _yCurr, x2 - x0, 19,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Ручка`, 0,
					"", `Ручка`, 1
	ui_style 0,0

	_yCurr = _yCurr + _dy

	ui_infield{3} "gs_handle_style_m", x1, _yCurr, 135, 96,
		2,7, 8, 2,
		135, 96, 120, 90,
		1, stHandleType[1], 1,
		2, stHandleType[2], 2,
		3, stHandleType[3], 3,
		4, stHandleType[4], 4,
		5, stHandleType[5], 5,
		6, stHandleType[6], 6

	ui_outfield `Высота`,			x3, _yCurr, 	x4 - x3-1, 	_outfHeight, 4 * (not(bHandle) or gs_handle_style_m = 2 or gs_handle_style_m = 4 or gs_handle_style_m = 6)
	ui_infield	"HandleSize",		x4, _yCurr-4, 	xEnd - x4,	_infHeight
	_yCurr = _yCurr + _dy

	ui_outfield `Привязка по Вертик.`, x3, _yCurr,	x4 - x3-1,	_outfHeight, 4 * (not(bHandle) or bFreezer or gs_handle_style_m = 2 or gs_handle_style_m = 4 or gs_handle_style_m = 6)
	ui_infield	"HandlePos",		 x4, _yCurr-4,	xEnd - x4,	_infHeight
	_yCurr = _yCurr + _dy * 3

	ui_infield	"handle_mat",		 x3, _yCurr,	xEnd - x3,	_infHeight
return

! ------------------------------------------------------------------------------
"2DRepresentationPage":
! ------------------------------------------------------------------------------

	pageStartY	= 5
	_yCurr 		= pageStartY

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 15, !SYMBOLCLAS_REFRIGERATOR
													TABID_Text			= TABID_Text,
													symboltypes_enable	= symboltypes_enable,
													gs_cont_pen 		= gs_cont_pen,
													gs_fill_type 		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen 		= gs_back_pen,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													iFridgeSymbolType	= iFridgeSymbolType,
													stSymbolTypes		= stFridgeSymbolTypes,
													gs_symbol_fill_type	= gs_symbol_fill_type,
													gs_line_type		= gs_line_type,
													iFillListID			= TABID_2DRepresentation,
													pageStartY			= _yCurr

return

! ------------------------------------------------------------------------------
"SymText":
! ------------------------------------------------------------------------------

	call "ui_object_SymbolText" parameters	dt						= dt,
											txt						= txt,
											fontType				= fontType,
											fontSize				= fontSize,
											bParName_FontSize		= 1,
											gs_text_style_bold		= gs_text_style_bold,
											gs_text_style_italic	= gs_text_style_italic,
											gs_text_style_underline	= gs_text_style_underline,
											tpen					= tpen,
											typeTextRotation_m		= typeTextRotation_m,
											txtx					= txtx,
											txty					= txty


return

! ------------------------------------------------------------------------------
"3DRepresentationSurfacesPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1   
	surfacesArray[SurfItemID]	= "gs_cabinet_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_front_mat"
	SurfItemID = SurfItemID + 1

	if bToe then
		surfacesArray[SurfItemID]	= "toe_mat"
		SurfItemID = SurfItemID + 1
	endif

	if topp then
		surfacesArray[SurfItemID]	= "toppmat"
		SurfItemID = SurfItemID + 1
	endif

	if bHandle then
		surfacesArray[SurfItemID]	= "handle_mat"
		SurfItemID = SurfItemID + 1
	endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k

	call "ui_object_3DRepresentation" parameters 	surfacesArray 		= surfacesArray,
													childFlagArray 		= childFlagArray,
													bShowResol			= 0,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													iSurfaceListID		= TABID_3DRepresentationSurfaces


return

! ------------------------------------------------------------------------------
"MinimalPage":
! ------------------------------------------------------------------------------

	x0		= 0
	x1		= x0 + 64
	x2		= x1 + 76
	x3		= x2 + 19
	x4		= x3 + 95
	x5		= x4 + 17
	xEnd	= 444

	pageStartY = 0
	y1		= pageStartY + 18
	y2		= y1 + 22
	y3		= y2 + 46
	y4		= y3 + 102
	yEnd	= 266

	_infHeight = 19
	_infWidth  = 90

	ui_pict 8, x2, y2, 130, 210, 1
	
	ui_infield	"MSSide2",		x1,    y1, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Сбоку 2`
	ui_infield	"A",			x3,    y1, _infWidth,	_infHeight ui_tooltip `Размер 1`
	ui_infield	"MSSide",		x4,    y1, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Сбоку 1`
	ui_infield	"B",			x5,    y3, _infWidth,	_infHeight ui_tooltip `Размер 2`
	ui_infield	"MSFront",		x5,    y4, _infWidth,	_infHeight ui_tooltip `Минимальное Пространство Спереди`


return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------
	dim stParamNames[]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "gs_list_volts"
		stParamNames[7] = "FM_Type"
		stParamNames[8] = "FM_InventoryNumber"
		stParamNames[9] = "FM_SerialNumber"
		stParamNames[10] = "FM_ProductionYear"
		stParamNames[11] = "FM_ObjectWeight"
		stParamNames[12] = "FM_ObjectWeightUnit"
		stParamNames[13] = "FM_PowerConsumption"
		stParamNames[14] = "FM_Egress"
		stParamNames[15] = "gs_list_custom1"
		stParamNames[16] = "gs_list_custom2"
		stParamNames[17] = "gs_list_custom3"
		stParamNames[18] = "gs_list_custom4"
		stParamNames[19] = "gs_list_custom5"
	
	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif
	
	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames

return
