

call "FM_types" parameters all

! --- Door Thickness ----------------
values "door_thk" range (0, B)

! --- Toe ----------------
if not(bToe) then
	lock			"toe_d", "toe_h"
	hideparameter	"toe_d", "toe_h"
endif

values "toe_d" range [0, door_thk)
values "toe_h" range (0, zzyzx - topp*0.0255)

! --- Freezer ----------------

values "freh" range (0, (zzyzx-_actToeHeight)/2]

values{2} "gs_freezer_type_m",	1, `Верх`,
								2, `Низ`
if not(bFreezer) then
	lock			"gs_freezer_type_m", "freh"
	hideparameter	"gs_freezer_type_m", "freh"
endif

values "gs_handle_style" stHandleType
values "gs_handle_style_m" 1, 2, 3, 4, 5, 6

if GLOB_MODPAR_NAME = "gs_handle_style" then
	for iHand = 1 to 6
		if gs_handle_style = stHandleType[iHand] then gs_handle_style_m = iHand
	next iHand
	parameters gs_handle_style_m = gs_handle_style_m
else
	for iHand = 1 to 6
		if gs_handle_style_m = iHand then gs_handle_style = stHandleType[iHand]
	next iHand
	parameters gs_handle_style = gs_handle_style
endif

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

values{2} "gs_detlevel_3D_m" 	2, `Детальный`,
								1, `Простой`,
								0, `Откл.`

DIM stSymbolType[2]
	stSymbolType[1]=`Детальный`
	stSymbolType[2]=`Простой`

values "symbolType" stSymbolType
values "iSymbolType" 1, 2

if GLOB_MODPAR_NAME = "symbolType" then
	iSymbolType = 2
	if symbolType=stSymbolType[1] then iSymbolType=1
	parameters iSymbolType = iSymbolType
else
	symbolType = stSymbolType[2]
	if iSymbolType=1 then symbolType=stSymbolType[1]
	parameters symbolType = symbolType
endif
	
if not(symboltypes_enable) then
	iSymbolType = 2
	parameters iSymbolType = iSymbolType
	lock "symbolType", "iSymbolType"
	hideparameter "symbolType", "iSymbolType"
else
	if not((gs_detlevel_2D_m = 3) | (gs_detlevel_2D_m = 0)) then
		lock "symbolType", "iSymbolType"
		hideparameter "symbolType", "iSymbolType"
	endif
endif


values "gs_fridge_symbol_type" stFridgeSymbolTypes
values "iFridgeSymbolType" 1, 2, 3, 4, 5, 6, 7, 8

if GLOB_MODPAR_NAME = "gs_fridge_symbol_type" then
	for i = 1 to vardim1(stFridgeSymbolTypes)
		if gs_fridge_symbol_type = stFridgeSymbolTypes[i] then iFridgeSymbolType = i
	next i
	parameters iFridgeSymbolType = iFridgeSymbolType
else
	gs_fridge_symbol_type = stFridgeSymbolTypes[max(1, min(iFridgeSymbolType,vardim1(stFridgeSymbolTypes)))]
	parameters gs_fridge_symbol_type = gs_fridge_symbol_type
endif

if gs_detlevel_2D_m <> 4 then
	lock "gs_fridge_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type", "iFridgeSymbolType"
	hideparameter "gs_fridge_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type", "iFridgeSymbolType"
endif

if gs_detlevel_2D_m = 4  and (iFridgeSymbolType < 2 or iFridgeSymbolType > 3) then
	lock "gs_symbol_fill_type"
	hideparameter "gs_symbol_fill_type"
endif

values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_shadow"
	hideparameter "gs_shadow"
endif

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19
if tpen=0 then parameters tpen=86

IF not(bHandle) THEN lock "gs_handle_style_m","HandleSize","HandlePos","handle_mat"

IF gs_handle_style_m = 2 or gs_handle_style_m = 4 or gs_handle_style_m = 6 THEN LOCK "HandleSize","HandlePos"

IF gs_handle_style_m = 1 or gs_handle_style_m = 3 or gs_handle_style_m = 5 THEN
	if bFreezer then
		if gs_handle_style_m = 1 then
			values "HandleSize" range [0, freh]
		else
			values "HandleSize" range [0, freh-0.06]
		endif
		LOCK "HandlePos"
	else
		values "HandleSize" range [0, ZZYZX - toe_h - topp*0.0255]
		values "HandlePos" range [toe_h + HandleSize/2, ZZYZX - HandleSize/2- topp*0.0255]
	endif
endif

if not (dt) then
	lock	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation","typeTextRotation_m"
	hideparameter	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m"
endif

values "fontSize" range (0,]


! --- Minimal Space ---
values "MSFront" RANGE [0,)
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif
! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

hideparameter all "A", "B", "ZZYZX"
