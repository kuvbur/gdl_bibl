
pen gs_cont_pen
if gs_detlevel_3D_m=0 then end
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

if not(gs_shadow) then SHADOW OFF

!* 3D Hotspot ************************************
unID=1

hotspot	0, 0, 0, 		unID, zzyzx, 1 : unID=unID+1
hotspot	0, 0, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, 0, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

hotspot	A, 0, 0, 		unID, zzyzx, 1 : unID=unID+1
hotspot	A, 0, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, 0, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

hotspot	0, B, 0, 		unID, zzyzx, 1 : unID=unID+1
hotspot	0, B, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, B, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

hotspot	A, B, 0,		unID, zzyzx, 1 : unID=unID+1
hotspot	A, B, -1, 		unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, B, zzyzx, 	unID, zzyzx, 2 : unID=unID+1

!**************************************************

c=zzyzx

!Cabinet
	material gs_cabinet_mat

rotx 90
CUTPOLYA 5, 2, -(b-door_thk),
		  0.05, 	door_thk+0.05, 	15,
		  a-0.05, 	door_thk+0.05, 	15,
		  a-0.05, 	c-0.05, 		15,
		  0.05, 	c-0.05, 		15,
		  0.05, 	door_thk+0.05, 	-1

del 1
	prism_ 5,c,
			0,	door_thk+0.005,	15,
			a,	door_thk+0.005,	15,
			a,	b,				15,
			0,	b,				15,
			0,	door_thk+0.005,	-1

cutend

!Doors
addz _actToeHeight
addy door_thk
rotx 90

material gs_front_mat

if gs_detlevel_3D_m = 2 and s_ice = 1 then

	prism_ 10,door_thk,
			0,0,15,
			0,c-_actToeHeight,15,
			freezer_w,c-_actToeHeight,15,
			freezer_w,0,15,
			0,0,-1,
			(freezer_w-s_ice_x-.03)/2,s_ice_z-_actToeHeight,15,
			(freezer_w-s_ice_x-.03)/2+s_ice_x,s_ice_z-_actToeHeight,15,
			(freezer_w-s_ice_x-.03)/2+s_ice_x,s_ice_z-_actToeHeight+s_ice_y,15,
			(freezer_w-s_ice_x-.03)/2,s_ice_z-_actToeHeight+s_ice_y,15,
			(freezer_w-s_ice_x-.03)/2,s_ice_z-_actToeHeight,-1
else
	prism_ 5,door_thk,
			0,			0,		15,
			0,			c-_actToeHeight,15,
			freezer_w,	c-_actToeHeight,15,
			freezer_w,	0,		15,
			0,			0,		-1
endif

del 2
prism_ 5,c-_actToeHeight,
		a,				0,			15,
		a,				door_thk,	15,
		freezer_w+0.01,	door_thk,	15,
		freezer_w+0.01,	0,			15,
		a,				0,			-1
del 1

!Toe
material toe_mat
prism_ 5,_actToeHeight,
		0,	toe_d,		15,
		a,	toe_d,		15,
		a,	door_thk,	15,
		0,	door_thk,	15,
		0,	toe_d,		-1


if gs_detlevel_3D_m = 2 then
	if s_ice = 1 then						!Ice
		add (freezer_w-s_ice_x-0.03)/2,0.01,s_ice_z
		addz s_ice_y
		rotx -90
		material ice_in
		size = 0.15
		gosub "ice"
		del 2
		prism_ 5,		 s_ice_y,
				0,		 0.15,	15,
				s_ice_x, 0.15,	15,
				s_ice_x, 0.151,	15,
				0,		 0.151,	15,
				0,		 0.15,	-1

		rotx 90
		material ice_fr
		size = 0.01
		gosub "ice"
		del 1
	ENDIF
	del top


	! --- Handle
	if bHandle then
		reqBKnobs = 0
		success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
		if success > 0 and reqBKnobs then
			end
		endif
	
		resol gs_resol
		material handle_mat
	
		r=0.03
		if iHandleType=0 then
			add freezer_w-r,-0.03,_actToeHeight+((c-handle_h-_actToeHeight)/2)
			for i=1 to 2
				Addz 0.05
				cylind handle_h-0.1,0.017
				DEL 1
				addx r*2+.01
			next i
			del 2
	
			for i=1 to 2
				cylind 0.05,0.022
				addz handle_h-0.05
				cylind 0.05,0.022
				del 1
				addx r*2+.01
			next i
			del 2
	
			for i=1 to 2
				gosub "handlest1"
				addz handle_h-0.05
				gosub "handlest1"
				del 1
				addx r*2+0.01
			next i
			DEL 2
			DEL 1
		endif
	
		if iHandleType then
			add freezer_w-r,0,0
			gosub "handlest2"
			del 1
	
			add freezer_w+r+0.01,0,0
			gosub "handlest2"
			del 1
		endif
	endif
	! --- End of handle

endif

end

"ice":
	prism_ 10,			size,
			0,			0,		 		15,
			s_ice_x,	0,		 		15,
			s_ice_x,	s_ice_y, 		15,
			0,			s_ice_y, 		15,
			0,			0,		 		-1,
			s_ice_x/5,	s_ice_y/5,		15,
			s_ice_x*4/5,s_ice_y/5,		15,
			s_ice_x*4/5,s_ice_y*4/5,	15,
			s_ice_x/5,	s_ice_y*4/5,	15,
			s_ice_x/5,	s_ice_y/5,		-1

return


"handlest1":
	prism_ 5,		0.05,
			-0.015,	0,		15,
			-0.015,	0.04,	15,
			0.015,	0.04,	15,
			0.015,	0,		15,
			-0.015,	0,		-1
return

"handlest2":
	N=handle_h
	roty 90
	resol  72
	CUTPLANE -90
	X = SQR(4*N*N - 1/4*N*N)
	N2 = 2*N - X

	add  -(_actToeHeight+((c-_actToeHeight)/2)), 2*N-N2,-r/2
	prism_ 4,0.03,
			0,			0,		979,
			2*N,		360,	4079,
			0,			0,		979,
			1.99*N-0.005,	360,	4079
	del 1
	CUTEND
	del 1
return

