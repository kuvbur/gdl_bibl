
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "5350F9A1-99D3-477F-8350-CB500AB5766B"
	_startID	= "AFAFD39F-FC1B-4432-8F87-3B437CDF20F5"
	_endID		= "82831E75-BBD7-453D-A92F-BFC19BFBE56D"
gosub "newBooleansToeHandle_FWM"

	_startID	= "82831E75-BBD7-453D-A92F-BFC19BFBE56D"
	_endID		= "92F31592-33BF-4933-8442-21E4DF9BEB4D"
gosub "FontSizeParamType_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! newBooleansToeHandle
! ==============================================================================
"newBooleansToeHandle_FWM":
	if actualGuid = _startID | actualGuid = _autoID then

		! If toe_h was 0, that equals with bToe being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("toe_h", _toe_h)

		if abs(_toe_h) < EPS then
			bToe = 0
		else
			bToe = 1
		endif
		parameters bToe = bToe
		
		! The integer setting the handle style was renamed from gs_handle_st to iHandleType
		bSuccessful = DELETED_PAR_VALUE ("gs_handle_st", _gs_handle_st)
		if bSuccessful then
			parameters iHandleType = _gs_handle_st
		endif

		! Handle can be switched off now with bHandle boolean, but in default, it is on, so nothing to do.

		actualGuid = _endID
	endif
return

! ==============================================================================
! FontSizeParamType
! ==============================================================================
"FontSizeParamType_FWM":
	if actualGuid = _startID then
		bHas = DELETED_PAR_VALUE("fsz", _fsz)
		
		fontSize = _fsz / 100
		parameters fontSize = fontSize
		
		actualGuid = _endID
	endif
return

