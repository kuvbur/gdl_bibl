
call "FM_types" parameters all

values "door_thk" range (0,B)
values "freezer_w" range (0.06,A/2]
values "s_ice_x" range (0,freezer_w-0.06]
values "s_ice_y" range (0,ZZYZX - _actToeHeight - 0.02)
values "s_ice_z" range [_actToeHeight, ZZYZX - s_ice_y-0.02]

! --- Toe ----------------
values "toe_d" range [0, door_thk)
values "toe_h" range (0, ZZYZX-0.1)
if not(bToe) then
	lock			"toe_d", "toe_h"
	hideparameter	"toe_d", "toe_h"
endif

! --- Handle ----------------
values{2} "iHandleType",	0, `Стиль 1`,
							1, `Стиль 2`

values "handle_h" range [0.1, ZZYZX - _actToeHeight)
if not(bHandle) then
	lock			"iHandleType", "handle_h", "handle_mat"
	hideparameter	"iHandleType", "handle_h"
endif

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

values{2} "gs_detlevel_3D_m" 	2, `Детальный`,
								1, `Простой`,
								0, `Откл.`
								
values "gs_resol" range [3, )
DIM stSymbolType[2]
	stSymbolType[1]=`Детальный`
	stSymbolType[2]=`Простой`

values "symbolType" stSymbolType
values "iSymbolType" 1, 2

if GLOB_MODPAR_NAME = "symbolType" then
	iSymbolType = 2
	if symbolType=stSymbolType[1] then iSymbolType=1
	parameters iSymbolType = iSymbolType
else
	symbolType = stSymbolType[2]
	if iSymbolType=1 then symbolType=stSymbolType[1]
	parameters symbolType = symbolType
endif
	
if not(symboltypes_enable) then
	iSymbolType = 2
	parameters iSymbolType = iSymbolType
	lock "symbolType", "iSymbolType"
	hideparameter "symbolType", "iSymbolType"
else
	if not((gs_detlevel_2D_m = 3) | (gs_detlevel_2D_m = 0)) then
		lock "symbolType", "iSymbolType"
		hideparameter "symbolType", "iSymbolType"
	endif
endif


values "gs_fridge_symbol_type" stFridgeSymbolTypes
values "iFridgeSymbolType" 1, 2, 3, 4, 5, 6, 7, 8

if GLOB_MODPAR_NAME = "gs_fridge_symbol_type" then
	for i = 1 to vardim1(stFridgeSymbolTypes)
		if gs_fridge_symbol_type = stFridgeSymbolTypes[i] then iFridgeSymbolType = i
	next i
	parameters iFridgeSymbolType = iFridgeSymbolType
else
	gs_fridge_symbol_type = stFridgeSymbolTypes[max(1, min(iFridgeSymbolType, vardim1(stFridgeSymbolTypes)))]
	parameters gs_fridge_symbol_type = gs_fridge_symbol_type
endif

if not(gs_detlevel_2D_m = 4) then
	lock "gs_fridge_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type", "iFridgeSymbolType"
	hideparameter "gs_fridge_symbol_type", "gs_symbol_pen", "gs_symbol_fill_type", "iFridgeSymbolType"
endif
if gs_detlevel_2D_m = 4 and (iFridgeSymbolType < 2 or iFridgeSymbolType > 3) then
	lock "gs_symbol_fill_type"
	hideparameter "gs_symbol_fill_type"
endif

values "typeTextRotation" `Читаемый`,`Всегда Горизонтально`,`Выровнять по Символу`
values "typeTextRotation_m" 2, 1, 0

if GLOB_MODPAR_NAME = "typeTextRotation" then
	if typeTextRotation = `Читаемый`			then typeTextRotation_m = 2
	if typeTextRotation = `Всегда Горизонтально`	then typeTextRotation_m = 1
	if typeTextRotation = `Выровнять по Символу`	then typeTextRotation_m = 0
	parameters typeTextRotation_m = typeTextRotation_m
else
	if typeTextRotation_m = 2					then typeTextRotation = `Читаемый`
	if typeTextRotation_m = 1					then typeTextRotation = `Всегда Горизонтально`
	if typeTextRotation_m = 0					then typeTextRotation = `Выровнять по Символу`
	parameters typeTextRotation = typeTextRotation
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19
if tpen=0 then parameters tpen=86

if not (dt) then
	lock	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m"
	hideparameter	"txt", "tpen", "fontType", "fontSize",
			"gs_text_style_bold", "gs_text_style_italic", "gs_text_style_underline",
			"txtx", "txty", "typeTextRotation", "typeTextRotation_m"
endif

if not(s_ice) then lock "s_ice_x","s_ice_y","s_ice_z","ice_fr", "ice_in"

values "fontSize" range (0,]

! --- Minimal Space ---
values "MSFront" RANGE [0,)
values "MSSide" range (-a/2,]
values "MSSide2" range (-a/2,]

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Storage" parameters all
else
	hideparameter "gs_onorm_Title"
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

hideparameter all "A", "B", "ZZYZX"
