
pen gs_cont_pen


! ==============================================================================
! 		Hotspots
! ==============================================================================

unID = 200

! --- Width Edit ---
hotspot2 0,		0,	unID, B, 1+256	: unID = unID + 1
hotspot2 0,		B,	unID, B, 2		: unID = unID + 1
hotspot2 0,		-1,	unID, B, 3		: unID = unID + 1

hotspot2 cribLength,		0,	unID, B, 1+256	: unID = unID + 1
hotspot2 cribLength,		B,	unID, B, 2		: unID = unID + 1
hotspot2 cribLength,		-1,	unID, B, 3		: unID = unID + 1

if bCabinet then
	hotspot2 A,		0,	unID, B, 1+256	: unID = unID + 1
	hotspot2 A,		B,	unID, B, 2		: unID = unID + 1
	hotspot2 A,		-1,	unID, B, 3		: unID = unID + 1
else
	unID = unID + 3
endif


! --- Crib Length Edit ---
hotspot2 0,				B, unID, cribLength, 1+256	: unID = unID + 1
hotspot2 cribLength,	B, unID, cribLength, 2		: unID = unID + 1
hotspot2 -1,			B, unID, cribLength, 3		: unID = unID + 1

hotspot2 0,				0, unID, cribLength, 1+256	: unID = unID + 1
hotspot2 cribLength,	0, unID, cribLength, 2		: unID = unID + 1
hotspot2 -1,			0, unID, cribLength, 3		: unID = unID + 1


! --- Cabinet X Length Edit ---
if bCabinet then
	if iCabOrient = 1 then
		hotspot2 cribLength,	0, unID, cabWidth, 1+128	: unID = unID + 1
		hotspot2 A,				0, unID, cabWidth, 2		: unID = unID + 1
		hotspot2 -1,			0, unID, cabWidth, 3		: unID = unID + 1

		hotspot2 cribLength,	B, unID, cabWidth, 1+128	: unID = unID + 1
		hotspot2 A,				B, unID, cabWidth, 2		: unID = unID + 1
		hotspot2 -1,			B, unID, cabWidth, 3		: unID = unID + 1
	else
		hotspot2 cribLength,	0, unID, cabDepth, 1+128	: unID = unID + 1
		hotspot2 A,				0, unID, cabDepth, 2		: unID = unID + 1
		hotspot2 -1,			0, unID, cabDepth, 3		: unID = unID + 1

		hotspot2 cribLength,	B, unID, cabDepth, 1+128	: unID = unID + 1
		hotspot2 A,				B, unID, cabDepth, 2		: unID = unID + 1
		hotspot2 -1,			B, unID, cabDepth, 3		: unID = unID + 1
	endif
else
	unID = unID +12
endif


! --- Middle Hotspot ---
hotspot2 cribLength/2, B/2, unID : unID = unID + 1


! ==============================================================================
! 		Bed and Table
! ==============================================================================

rot2 -90
add2 -B, 0

drawindex 20
call "fa_bed" parameters all	bCalledAsMacro	= 1,
								A				= B,
								B				= cribLength

del 2

if bCabinet then
	_fillTypeCab = gs_fill_type
	_fillPenCab = gs_fill_pen
	_backPenCab = gs_back_pen

	if bUniformFill then
		_fillTypeCab = fillTypeUniform
		_fillPenCab = fillPenUniform
		_backPenCab = backPenUniform
	endif

	fill _fillTypeCab

	add2 cribLength, 0

	drawindex 30
	poly2_b 4, 1+2+4+64,
		_fillPenCab, _backPenCab,
		0,				0,	1,
		_cabXLength,	0,	1,
		_cabXLength,	B,	1,
		0,				B,	1

	del 1
endif


! ==============================================================================
! 		Minimalspace
! ==============================================================================

drawindex 10
call "fa_minimalspace" parameters all 	MSType	= 15,		! MINSPACE_CRIB
										MSRear	= 0,
										unID	= 2000




