
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 		=-1
TABID_CribPresent	= 1
TABID_Headboard	= 2
TABID_LegMattress	= 3
TABID_CabSettings	= 4
TABID_DrawerKnob	= 5
TABID_Appearance	= 6
TABID_Surfaces		= 7
TABID_MinimalSpace	= 8
TABID_Descriptions	= 9

pageStartY = 5

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Кровати`, 444, 266
ui_current_page gs_ui_current_page

UI_PAGE TABID_CribPresent, TABID_Root, `Стиль`, "uiTab_Style_flat(1)"
	gosub "cribPage"

UI_PAGE TABID_Headboard, TABID_CribPresent, `Спинки`, "uiTab_CribHeadboard_flat(1)"
	gosub "headboardPage"

UI_PAGE TABID_LegMattress, TABID_CribPresent, `Матрас, Подушка и Основание`, "uiTab_Leg_flat(1)"
	gosub "mattressPage"

UI_PAGE TABID_CabSettings, TABID_CribPresent, `Шкаф`, "uiTab_Cabinet_flat(1)"
	gosub "cabinetPage"

UI_PAGE TABID_DrawerKnob, TABID_CribPresent, `Ящики и Ручки`, "uiTab_Knob_flat(1)"
	gosub "drawerknobPage"

UI_PAGE TABID_Appearance, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "appearancePage"

UI_PAGE TABID_Surfaces, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "surfacesPage"

UI_PAGE TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "minimalSpacePage"

UI_PAGE TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "descriptionPage"

FILE_DEPENDENCE "uiTab_Style_flat(1)"
FILE_DEPENDENCE "uiTab_CribHeadboard_flat(1)"
FILE_DEPENDENCE "uiTab_Leg_flat(1)"
FILE_DEPENDENCE "uiTab_Cabinet_flat(1)"
FILE_DEPENDENCE "uiTab_Knob_flat(1)"
FILE_DEPENDENCE "uiTab_2DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_3DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_MinSpace_flat(1)"
FILE_DEPENDENCE "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"cribPage":
! ------------------------------------------------------------------------------
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37
	x1 = 224
	x2 = 230
	x3 = 353
	x4 = 407
	dy1 = 21
	dy2 = 30

	ui_infield{3} "iCribStyle", 0, pageStartY + 20, 221, 240,
		1, 1, 8, 2,			! ui_CribStyles.png
		100, 100,
		90, 90,
		1, stCribStyleValues[1], 1,
		2, stCribStyleValues[2], 2,
		3, stCribStyleValues[3], 3,
		4, stCribStyleValues[4], 4,
		5, stCribStyleValues[5], 5

	_yCurr = pageStartY + 3
	ui_style 0, 1
	ui_outfield `Преднастроенные Параметры`, 0, _yCurr, 221, _outfHeight
	ui_outfield `Стиль`, x2, _yCurr, 210, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + dy1 + _yOffset
	ui_outfield `Спинки`, x2, _yCurr, 210 - widthInf2, _outfHeight

	_yCurr = _yCurr - _yOffset
	ui_pict_button ui_function, "", "uiFunctionHeadboard_flat(1)", x4, _yCurr-1, widthInf2, 22, -TABID_Headboard ! Set GLOB_UI_BUTTON_ID

	ui_separator x2,  _yCurr +_infHeight + 5, 444, _yCurr + _infHeight + 5
	
	_yCurr = _yCurr + dy2
	ui_outfield `Матрас`, x2, _yCurr, 210, _outfHeight

	_yCurr = _yCurr + dy1 + _yOffset
	ui_infield{3} "iSupType", x3, _yCurr - _yOffset, 90, _infHeight,
					8, "",
					0, 0,
					0, 0, 0, 0,
					"", stSupportTypeValues[1], 1,
					"", stSupportTypeValues[2], 2


	ui_outfield `Основание`, x2, _yCurr, 210 - 90, _outfHeight

	_yCurr = _yCurr + dy1
	ui_infield{3} "pw", x2, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Подушка`, 0,
					"", `Подушка`, 1
	ui_pict_button ui_function, "", "uiFunctionLeg_flat(1)", x4, _yCurr-1, widthInf2, 22, -TABID_LegMattress ! Set GLOB_UI_BUTTON_ID


	ui_separator x2,  _yCurr + _infHeight + 5, 444, _yCurr + _infHeight + 5

	_yCurr = _yCurr + dy2 +_yOffset
	ui_infield{3} "bCabinet", x2, _yCurr, 210-widthInf2, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Шкаф`, 0,
					"", `Шкаф`, 1
	ui_pict_button ui_function, "", "uiFunctionCabinet_flat(1)", x4, _yCurr-1, widthInf2, 22, -TABID_CabSettings ! Set GLOB_UI_BUTTON_ID

	ui_separator x2,  _yCurr + _infHeight + 5, 444, _yCurr + _infHeight + 5

	_yCurr = _yCurr + dy2 + _yOffset
	ui_outfield `Ящики`, x2, _yCurr, 210, _outfHeight

	_yCurr = _yCurr + dy1
	ui_infield{3} "bKnob", x2, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Ручка`, 0,
					"", `Ручка`, 1
	ui_pict_button ui_function, "", "uiFunctionDrawerKnob_flat(1)", x4, _yCurr-1, widthInf2, 22, -TABID_DrawerKnob ! Set GLOB_UI_BUTTON_ID

return

! ------------------------------------------------------------------------------
"headboardPage":
! ------------------------------------------------------------------------------

	x1 = 0
	y1 = pageStartY - 4
	x2 = 247
	x3 = 230
	x4 = 352
	dy = 24

	widthInf 	= 90
	widthOutf	= 120

	heightInf 	= 19
	heightOutf	= 15

	fillTypeInfLength 	= 195
	fillTypeInfHeight	= 23

	ui_style 0, 1
	ui_outfield `Стиль Спинки`, x1, y1 + 4, 150, heightOutf
	ui_style 0, 0

	y1 = y1 + dy

	ui_infield{3} "iHeadboardStyle", 76, y1, 136, 89,
		2, 2, 4, 1,									! ui_CribHeadboard.png
		135, 88, 135, 88,
		1, stHBoardStyleValues[1], 1,
		2, stHBoardStyleValues[2], 2,
		3, stHBoardStyleValues[3], 3,
		4, stHBoardStyleValues[4], 4


	ui_outfield `Высота`, 				x3, y1+4, 	widthOutf, 	heightOutf
	ui_infield "headboardHeight", 		x4, y1, 	widthInf, 	heightInf

	y1 = y1 + dy

	ui_outfield `Ограждение Спереди`, 	x3, y1+4, 	widthOutf,	heightOutf
	ui_infield "frontRailHeight", 		x4, y1, 	widthInf, 	heightInf

	y1 = y1 + dy

	ui_outfield `Ограждение Сзади`, 	x3, y1+4, 	widthOutf,	heightOutf
	ui_infield "rearRailHeight", 		x4, y1, 	widthInf, 	heightInf

	y1 = y1 + dy

	ui_infield{3} "bBoardEdge", x3, y1, 120, heightInf,
		7, "", 2, 1,
		20, 20, 20, 20,
		0, `Поручни`, 0,
		1, `Поручни`, 1

	y1 = y1 + dy * 2

	ui_pict 9, x2 - 21, y1 + 3, 18, 18, 1 ui_tooltip `Покрытие Каркаса Кровати`
	ui_infield "gs_frame_mat", x2, y1, fillTypeInfLength, fillTypeInfHeight

	y1 = y1 + fillTypeInfHeight + 5

	ui_pict 10, x2 - 21, y1 + 3, 18, 18, 1 ui_tooltip `Покрытие Спинки`
	ui_infield "matHeadboard", x2, y1, fillTypeInfLength, fillTypeInfHeight

return

! ------------------------------------------------------------------------------
"mattressPage":
! ------------------------------------------------------------------------------

	call "ui_furniture_parts" parameters	bIsMattresAndPillow = 1,
											bCrib = 1,
											pw = pw

	ui_separator 220, 0, 220, 265

	x1 = 229
	y1 = pageStartY + 1
	x2 = 353
	x3 = x1 + 17
	dy = 24

	widthInf 	= 88
	widthOutf	= 120

	heightInf 	= 19
	heightOutf	= 15

	fillTypeInfLength 	= 212
	fillTypeInfHeight	= 25

	ui_style 0,1
	ui_outfield `Основание`, x1, y1, widthOutf, heightOutf
	ui_style 0,0
	y1 = y1 + dy

	ui_outfield `Тип`,					x1, y1, 	widthOutf, 	heightOutf
	ui_infield{3} "iSupType", 			x2, y1-4, 	widthInf, 	heightInf,
				8, "", 2, 1,
				88, 19, 80, 20,
				1, stSupportTypeValues[1], 1,
				2, stSupportTypeValues[2], 2
	y1 = y1 + dy

	ui_outfield  `Высота`,			 	x1, y1, 	widthOutf, 	heightOutf
	ui_infield "supportHeight", 		x2, y1-4, 	widthInf,	heightInf

	y1 = y1 + dy

	if iSupType = 2 then			! Leg
		ui_outfield `Стиль Ножек`, 		x1, y1, 	widthOutf, 	heightOutf
		y1 = y1 + dy
	
		ui_infield{3} "iLegStyle", 			305, y1-4, 	136, 		89,
			2, 3, 6, 1,									! ui_CribLeg.png
			135, 88, 135, 88,
			1, stLegStyleValues[1], 1,
			2, stLegStyleValues[2], 2,
			3, stLegStyleValues[3], 3,
			4, stLegStyleValues[4], 4,
			5, stLegStyleValues[5], 5,
			6, stLegStyleValues[6], 6
	else
		widthOutf2	= 155
		widthInf2	= 52
		x4 			= 389

		ui_outfield `Количество Ящиков`, 		x1, y1, 	widthOutf2, 	heightOutf
		ui_infield{3} "nUnderDrawer", 		x4, y1-4, 	widthInf2, 	heightInf,
					8, "", 2, 1,
					88, 19, 80, 20,
					1, "1", 1,
					2, "2", 2
	endif

	y1 = pageStartY + 189

	if iSupType = 2 then
		ui_infield "matLeg", x1, y1, fillTypeInfLength, fillTypeInfHeight
	endif

return

! ------------------------------------------------------------------------------
"cabinetPage":
! ------------------------------------------------------------------------------

	x1 = 0
	y1 = pageStartY - 2
	x2 = 171
	dy = 24

	widthInf 	= 90
	widthInf2 	= 52
	widthOutf	= 165

	heightInf 	= 19
	heightOutf	= 15

	fillTypeInfLength 	= 195
	fillTypeInfHeight	= 23

	ui_style 0, 1
	ui_infield{3} "bCabinet", x1, y1, 165, 19,
		7, "", 2, 1,
		20, 20, 20, 20,
		0,  `Шкаф`, 0,
		1,  `Шкаф`, 1
	ui_style 0, 0

	_iFlag = not(bCabinet) * 4

	y1 = y1 + dy

	ui_infield{3} "bCabLeg", x1, y1, 165, heightInf,
		7, "", 2, 1,
		20, 20, 20, 20,
		0, `Ножки Шкафа`, 0,
		1, `Ножки Шкафа`, 1

	y1 = y1 + dy

	ui_outfield `Ориентация`, 			x1, y1, 	widthOutf, 	heightOutf, _iFlag
	ui_infield{3} "iCabOrient", 		x2, y1-4, 	widthInf, 	heightInf,
		8, "", 2, 1,
		widthInf, heightInf, 80, 20,
		1, stCabinetOrientValues[1], 1,
		2, stCabinetOrientValues[2], 2

	y1 = y1 + dy

	ui_outfield `Стиль Шкафа`, 		x1, y1, 	widthOutf, 	heightOutf, _iFlag
	ui_infield{3} "iCabStyle", 			x2, y1-4, 	widthInf, 	heightInf,
		8, "", 2, 1,
		widthInf, heightInf, 80, 20,
		1, stCabinetStyleValues[1], 1,
		2, stCabinetStyleValues[2], 2

	y1 = y1 + dy

	ui_outfield `Высота Шкафа`, 		x1, y1, 	widthOutf, 	heightOutf, _iFlag
	ui_infield "cabHeight", 			x2, y1-4, 	widthInf, 	heightInf

	y1 = y1 + dy

	if iCabOrient = 1 then
		ui_outfield  `Ширина Шкафа`, 		x1, y1, 	widthOutf, heightOutf, _iFlag
		ui_infield "cabWidth", 				x2, y1-4, 	widthInf, heightInf
	else
		ui_outfield  `Глубина Шкафа`, 		x1, y1, 	widthOutf, heightOutf, _iFlag
		ui_infield "cabDepth", 				x2, y1-4, 	widthInf, 	heightInf
	endif

	y1 = y1 + dy

	if iCabStyle = 1 then
		ui_outfield `Количество Ящиков`, x1, y1, widthOutf, heightOutf, _iFlag
	else
		ui_outfield `Количество Полок`, x1, y1, widthOutf, heightOutf, _iFlag
	endif

	ui_infield{3} "nDrawer", x2, y1-4, widthInf2, heightInf,
		8, "", 5, 1,
		107, 22, 80, 20,
		1, "1", 1,
		2, "2", 2,
		3, "3", 3,
		4, "4", 4,
		5, "5", 5

	y1 = y1 + dy * 2

	ui_infield "matCabinet", x1, y1, 	fillTypeInfLength, fillTypeInfHeight

return

! ------------------------------------------------------------------------------
"drawerknobPage":
! ------------------------------------------------------------------------------

	x1 = 0
	y1 = pageStartY
	x2 = 18
	x3 = 230
	x4 = 352
	dy = 24

	widthInf 	= 90
	widthOutf	= 214

	heightInf 	= 19
	heightOutf	= 15

	fillTypeInfLength 	= 212
	fillTypeInfHeight	= 23

	_outfFlag = 0

	if iSupType = 2 & not(bCabinet) then _outfFlag = 4
	ui_style 0, 1
	ui_outfield `Стиль Ящика`, x1, y1, widthOutf, heightOutf, _outfFlag
	ui_style 0, 0

	y1 = y1 + dy

	ui_infield{3} "iDrawerStyle", 77, y1-4, 136, 89,
		2, 4, 4, 1,										! ui_CribDrawer.png
		135, 88, 135, 88,
		1, stDrawerStyleValues[1], 1,
		2, stDrawerStyleValues[2], 2,
		3, stDrawerStyleValues[3], 3,
		4, stDrawerStyleValues[4], 8

	y1 = y1 + 85 + dy * 2

	ui_pict 7, x1, y1 + 3, 18, 18, 1 ui_tooltip `Покрытие Ящика под Кроватью`
	ui_infield "matDrawer", x1 + 23, y1, 	fillTypeInfLength - 23, fillTypeInfHeight

	y1 = y1 + dy + 4

	ui_pict 7, x1, y1 + 3, 18, 18, 1 ui_tooltip `Покрытие Ящика в Шкафу`
	ui_infield "matDoorFrame", x1 + 23, y1, 	fillTypeInfLength - 23, fillTypeInfHeight

	y1 = y1 + dy + 4
	ui_pict 8, x1, y1 + 3, 18, 18, 1 ui_tooltip `Покрытие Филенки Ящика`
	ui_infield "matDoorGlass", x1 + 23, y1, 	fillTypeInfLength - 23, fillTypeInfHeight

	ui_separator 220, 0, 220, 265

	y1 = pageStartY

	ui_style 0,1

	ui_infield{3} "bKnob", x3, y1-4, 140, heightInf,
		7, "", 2, 1,
		20, 20, 20, 20,
		0, `Ручка`, 0,
		1, `Ручка`, 1

	ui_style 0,0

	y1 = y1 + dy

	ui_infield{3} "iKnobStyle",	x3+76,	y1-4,	136, 89,
				2, 5, 9, 1,								! ui_CribKnob.png
				135, 88, 135, 88,
				1, stKnobStyleValues[1], 1,
				2, stKnobStyleValues[2], 2,
				3, stKnobStyleValues[3], 3,
				4, stKnobStyleValues[4], 4,
				5, stKnobStyleValues[5], 5,
				6, stKnobStyleValues[6], 6,
				7, stKnobStyleValues[7], 7,
				8, stKnobStyleValues[8], 8

	y1 = pageStartY + 115

	_iFlag = (not(bKnob) or iKnobStyle < 4) * 4

	ui_outfield `Размер`, x3, y1, 120, heightOutf, _iFlag

	ui_infield "knobSize", x4, y1-4, widthInf, heightInf

	y1 = y1 + dy + 18

	ui_infield "matKnob", x3, y1, fillTypeInfLength, fillTypeInfHeight

return

! ------------------------------------------------------------------------------
"surfacesPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[]
	SurfItemID = 1   
	surfacesArray[SurfItemID]	= "bUniformSurfaces"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "gs_frame_mat"
	SurfItemID = SurfItemID + 1

	surfacesArray[SurfItemID]	= "matHeadboard"
	SurfItemID = SurfItemID + 1

	if iSupType = 1 then
		surfacesArray[SurfItemID]	= "matDrawer"
	else
		surfacesArray[SurfItemID]	= "matLeg"
	endif
	SurfItemID = SurfItemID + 1

	if bCabinet then
		surfacesArray[SurfItemID]	= "matCabinet"
		SurfItemID = SurfItemID + 1
	endif

	if bCabinet then
		surfacesArray[SurfItemID]	= "matDoorFrame"
		SurfItemID = SurfItemID + 1
	endif

	if iDrawerStyle = 3 then
	surfacesArray[SurfItemID]	= "matDoorGlass"
	SurfItemID = SurfItemID + 1
	endif

	if bCabinet and bKnob then
	surfacesArray[SurfItemID]	= "matKnob"
	SurfItemID = SurfItemID + 1
	endif

	surfacesArray[SurfItemID]	= "mmat"
	
	if pw then	
		SurfItemID = SurfItemID + 1
		surfacesArray[SurfItemID]	= "pillow_mat"
	endif

	dim childFlagArray[]
	for k = 1 to SurfItemID
		childFlagArray[k]	= 0
	next k

	call "ui_object_3DRepresentation" parameters 	surfacesArray 		= surfacesArray,
													childFlagArray 		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													iSurfaceListID		= TABID_Surfaces

return

! ------------------------------------------------------------------------------
"appearancePage":
! ------------------------------------------------------------------------------

	call "ui_object_2DRepresentation" parameters 	iSymbolClass	= 2, ! SYMBOLCLAS_BED
													iSymbolType		= iSymbolType,
													stSymbolTypes	= stSymbolTypes,
													bUniformFill	= bUniformFill,
													iFillFor		= iFillFor,
													iFillListID		= TABID_Appearance

return

! ------------------------------------------------------------------------------
"minimalSpacePage":
! ------------------------------------------------------------------------------

if bCabinet then
	xxPict = 90

	y1 = pageStartY + 21
	y2 = pageStartY + 44
	y3 = pageStartY + 182

	ImageX = 217
	ImageY = 135

	ui_pict 6, xxPict, y2, ImageX, ImageY, 1 					! CribCabinetMinSpace.png

	ui_infield "cribLength", xxPict + 36, y1, 88, 19						ui_tooltip `Длина Кровати`

	if iCabOrient = 1 then
		ui_infield "cabWidth", xxPict + 128, y1, 88, 19				ui_tooltip `Ширина Шкафа`
	else
		ui_infield "cabDepth", xxPict + 128, y1, 88, 19				ui_tooltip `Глубина Шкафа`
	endif

	ui_infield "MSSide2", 	xxPict - 23,				y3, 		88, 19	ui_tooltip `Минимальное Пространство Сбоку 2`
	ui_infield "MSSide", 	xxPict + 150, 				y3, 		88, 19	ui_tooltip `Минимальное Пространство Сбоку 1`
	ui_infield "MSFront", 	xxPict + ImageX + 3, 		y2 + 85, 	88, 19	ui_tooltip `Минимальное Пространство Спереди`
	ui_infield "B", 		xxPict + ImageX + 3, 		y2 + 25, 	88, 19	ui_tooltip `Ширина Кровати`	
else
	call "fa_minimalspace" parameters 	MSType 			= 15		! MINSPACE_CRIB

endif
	
return


! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_Descriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return

