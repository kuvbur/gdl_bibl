
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

if gs_detlevel_3D_m = 0 then lock "gs_resol", "gs_shadow"
if gs_detlevel_3D_m = 1 then lock "gs_resol"

ac_bottomlevel = 0
parameters ac_bottomlevel = ac_bottomlevel

ac_toplevel = ZZYZX
parameters ac_toplevel = ac_toplevel


values "cribLength" range [1.2, )
values "A" range [1.2 + (bCabinet) * (_cabXLength), )
values "B" range [0.4, )

values "nUnderDrawer" 1, 2
values "legWidth" range [0.03, )

if iSupType = 1 then
	_minlegheight = 0.15
else
	_minlegheight = 0.05
	if iLegStyle = 5 then _minlegheight = legWidth + 0.05
	if iLegStyle = 6 then _minlegheight = 1.5 * legWidth + 0.05
endif
values "supportHeight" range (_minlegheight, )

values "headboardHeight" range [0.2, )
values "frontRailHeight" range [0.2, headboardHeight]
values "rearRailHeight" range [0.2, headboardHeight]
values "mrh" range [0.05, min(rearRailHeight, frontRailHeight)]
values "platformHeight" range [platformThk, min(rearRailHeight, frontRailHeight) - mrh]
values "platformThk" range [0.01, 0.15]
values "cabHeight" range [0.2 + supportHeight, ]
values "cabWidth" range [0.2, )
values "cabDepth" range [0.2, )
values "nDrawer" 1, 2, 3, 4, 5

values "MSFront" range [0, )
values "MSSide" range [0, )
values "MSSide2" range [0, )

values "gs_resol" range [3, )

if bCabinet then
	_maxKnobSize = min((iCabOrient = 1) * cabWidth + (iCabOrient = 2) * B, (A - 2 * _headboardThk) / nUnderDrawer) - 0.2
else
	_maxKnobSize = (A - 2 * _headboardThk) / nUnderDrawer - 0.2
endif

values "knobSize" range [0.05, _maxKnobSize]

if GLOB_MODPAR_NAME = "A" then
	cribLength = A - (bCabinet) * (_cabXLength)
	parameters cribLength = cribLength
else
	A = cribLength + (bCabinet) * (_cabXLength)
	parameters A = A
endif


ZZYZX = max(headboardHeight + supportHeight + _edgeHgt, bCabinet * (bCabLeg * supportHeight + cabHeight))
parameters ZZYZX = ZZYZX
lock "ZZYZX"

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

! ==============================================================================
! 		String - Integer connections
! ==============================================================================

values "gs_detlevel_3D" stDetlevel3D
values "gs_detlevel_3D_m" DET3D_DETAILED, DET3D_SIMPLE, DET3D_OFF

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_DETAILED
	if gs_detlevel_3D = stDetlevel3D[2]	then gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetlevel3D[3]	then gs_detlevel_3D_m = DET3D_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetlevel3D[1]
	if gs_detlevel_3D_m = DET3D_SIMPLE	then gs_detlevel_3D = stDetlevel3D[2]
	if gs_detlevel_3D_m = DET3D_OFF		then gs_detlevel_3D = stDetlevel3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif


values "stCribStyle" stCribStyleValues
values "iCribStyle" CRIB_SIMPLE, CRIB_DRAWER, CRIB_TABLE, CRIB_COMPLETE, CRIB_CUSTOM

if GLOB_MODPAR_NAME = "stCribStyle" then
	iCribStyle = 1
	for iCrib = 2 to vardim1(stCribStyleValues)
		if stCribStyle = stCribStyleValues[iCrib] then
			iCribStyle = iCrib
			iCrib = vardim1(stCribStyleValues)		! break
		endif
	next iCrib
	parameters iCribStyle = iCribStyle
else
	iCribStyle = max(1, min(iCribStyle, vardim1(stCribStyleValues)))
	stCribStyle = stCribStyleValues[iCribStyle]
	parameters stCribStyle = stCribStyle, iCribStyle = iCribStyle
endif


values "stSupType" stSupportTypeValues
values "iSupType" 1, 2

if GLOB_MODPAR_NAME = "stSupType" then
	iSupType = 1
	if stSupType = stSupportTypeValues[2] then iSupType = 2
	parameters iSupType = iSupType
else
	stSupType = stSupportTypeValues[1]
	if iSupType = 2 then stSupType = stSupportTypeValues[2]
	parameters stSupType = stSupType
endif


values "stLegStyle" stLegStyleValues
values "iLegStyle" 1, 2, 3, 4, 5, 6

if GLOB_MODPAR_NAME = "stLegStyle" then
	iLegStyle = 1
	for iLeg = 2 to vardim1(stLegStyleValues)
		if stLegStyle = stLegStyleValues[iLeg] then
			iLegStyle = iLeg
			iLeg = vardim1(stLegStyleValues)		! break
		endif
	next iLeg
	parameters iLegStyle = iLegStyle
else
	iLegStyle = max(1, min(iLegStyle, vardim1(stLegStyleValues)))
	stLegStyle = stLegStyleValues[iLegStyle]
	parameters stLegStyle = stLegStyle, iLegStyle = iLegStyle
endif


values "stHeadboardStyle" stHBoardStyleValues
values "iHeadboardStyle" 1, 2, 3, 4

if GLOB_MODPAR_NAME = "stHeadboardStyle" then
	iHeadboardStyle = 1
	for iHBoard = 2 to vardim1(stHBoardStyleValues)
		if stHeadboardStyle = stHBoardStyleValues[iHBoard] then
			iHeadboardStyle = iHBoard
			iHBoard = vardim1(stHBoardStyleValues)		! break
		endif
	next iHBoard
	parameters iHeadboardStyle = iHeadboardStyle
else
	iHeadboardStyle = max(1, min(iHeadboardStyle, vardim1(stHBoardStyleValues)))
	stHeadboardStyle = stHBoardStyleValues[iHeadboardStyle]
	parameters stHeadboardStyle = stHeadboardStyle, iHeadboardStyle = iHeadboardStyle
endif


values "stCabOrient" stCabinetOrientValues
values "iCabOrient" 1, 2

if GLOB_MODPAR_NAME = "stCabOrient" then
	iCabOrient = 1
	if stCabOrient = stCabinetOrientValues[2] then iCabOrient = 2
	parameters iCabOrient = iCabOrient
else
	stCabOrient = stCabinetOrientValues[1]
	if iCabOrient = 2 then stCabOrient = stCabinetOrientValues[2]
	parameters stCabOrient = stCabOrient
endif


values "stCabStyle" stCabinetStyleValues
values "iCabStyle" 1, 2

if GLOB_MODPAR_NAME = "stCabStyle" then
	iCabStyle = 1
	if stCabStyle = stCabinetStyleValues[2] then iCabStyle = 2
	parameters iCabStyle = iCabStyle
else
	stCabStyle = stCabinetStyleValues[1]
	if iCabStyle = 2 then stCabStyle = stCabinetStyleValues[2]
	parameters stCabStyle = stCabStyle
endif


values "stDrawerStyle" stDrawerStyleValues
values "iDrawerStyle" 1, 2, 3, 8

if GLOB_MODPAR_NAME = "stDrawerStyle" then
	iDrawerStyle = 8
	for iDraw = 1 to vardim1(stDrawerStyleValues) - 1
		if stDrawerStyle = stDrawerStyleValues[iDraw] then
			iDrawerStyle = iDraw
			iDraw = vardim1(stDrawerStyleValues)		! break
		endif
	next iDraw
	parameters iDrawerStyle = iDrawerStyle
else
	if iDrawerStyle = 8 then
		stDrawerStyle = stDrawerStyleValues[4]
	else
		stDrawerStyle = stDrawerStyleValues[iDrawerStyle]
	endif
	parameters stDrawerStyle = stDrawerStyle
endif


values "stKnobStyle" stKnobStyleValues
values "iKnobStyle" 1, 2, 3, 4, 5, 6, 7, 8

if GLOB_MODPAR_NAME = "stKnobStyle" then
	iKnobStyle = 1
	for iKnob = 2 to vardim1(stKnobStyleValues)
		if stKnobStyle = stKnobStyleValues[iKnob] then
			iKnobStyle = iKnob
			iKnob = vardim1(stKnobStyleValues)		! break
		endif
	next iKnob
	parameters iKnobStyle = iKnobStyle
else
	iKnobStyle = max(0, min(iKnobStyle, vardim1(stKnobStyleValues)))
	stKnobStyle = stKnobStyleValues[iKnobStyle]
	parameters stKnobStyle = stKnobStyle, iKnobStyle = iKnobStyle
endif


values "stSymbolType" stSymbolTypes
values "iSymbolType" 1, 2, 3, 4

if GLOB_MODPAR_NAME = "stSymbolType" then
	iSymbolType = 1
	for iSymb = 2 to vardim1(stSymbolTypes)
		if stSymbolType = stSymbolTypes[iSymb] then
			iSymbolType = iSymb
			iSymb = vardim1(stSymbolTypes)		! break
		endif
	next iSymb
	parameters iSymbolType = iSymbolType
else
	iSymbolType = max(1, min(iSymbolType, vardim1(stSymbolTypes)))
	stSymbolType = stSymbolTypes[iSymbolType]
	parameters stSymbolType = stSymbolType, iSymbolType = iSymbolType
endif


values "stFillFor" stFillForValues
values "iFillFor" 1, 2, 3, 4

if GLOB_MODPAR_NAME = "stFillFor" then
	iFillFor = 1
	for iFill = 2 to vardim1(stFillForValues)
		if stFillFor = stFillForValues[iFill] then
			iFillFor = iFill
			iFill = vardim1(stFillForValues)		! break
		endif
	next iFill
	parameters iFillFor = iFillFor
else
	iFillFor = max(1, min(iFillFor, vardim1(stFillForValues)))
	stFillFor = stFillForValues[iFillFor]
	parameters stFillFor = stFillFor, iFillFor = iFillFor
endif

! ==============================================================================
! 		Uniform surfaces
! ==============================================================================

if GLOB_MODPAR_NAME = "bUniformSurfaces" then
	if bUniformSurfaces then
		gs_frame_mat 	= matDoorFrame
		matHeadboard	= matDoorFrame
		matDrawer	 	= matDoorFrame
		matCabinet		= matDoorFrame

		parameters 	gs_frame_mat 	= gs_frame_mat,
					matHeadboard	= matHeadboard,
					matDrawer		= matDrawer,
					matCabinet		= matCabinet
	endif
else
	if bUniformSurfaces then
		if GLOB_MODPAR_NAME = "matDoorFrame" then
			gs_frame_mat 	= matDoorFrame
			matHeadboard	= matDoorFrame
			matDrawer	 	= matDoorFrame
			matCabinet		= matDoorFrame
	
			parameters 	gs_frame_mat 	= gs_frame_mat,
						matHeadboard	= matHeadboard,
						matDrawer		= matDrawer,
						matCabinet		= matCabinet

		endif	
		if GLOB_MODPAR_NAME = "gs_frame_mat" then
			matDoorFrame 	= gs_frame_mat
			matHeadboard	= gs_frame_mat
			matDrawer	 	= gs_frame_mat
			matCabinet		= gs_frame_mat
	
			parameters 	matDoorFrame 	= matDoorFrame,
						matHeadboard	= matHeadboard,
						matDrawer		= matDrawer,
						matCabinet		= matCabinet

		endif
		if GLOB_MODPAR_NAME = "matHeadboard" then
			matDoorFrame 	= matHeadboard
			gs_frame_mat	= matHeadboard
			matDrawer	 	= matHeadboard
			matCabinet		= matHeadboard
	
			parameters 	matDoorFrame 	= matDoorFrame,
						gs_frame_mat	= gs_frame_mat,
						matDrawer		= matDrawer,
						matCabinet		= matCabinet

		endif
		if GLOB_MODPAR_NAME = "matDrawer" then
			matDoorFrame 	= matDrawer
			gs_frame_mat	= matDrawer
			matHeadboard 	= matDrawer
			matCabinet		= matDrawer
	
			parameters 	matDoorFrame 	= matDoorFrame,
						gs_frame_mat	= gs_frame_mat,
						matHeadboard	= matHeadboard,
						matCabinet		= matCabinet

		endif
		if GLOB_MODPAR_NAME = "matCabinet" then
			matDoorFrame 	= matCabinet
			gs_frame_mat	= matCabinet
			matHeadboard 	= matCabinet
			matDrawer		= matCabinet
	
			parameters 	matDoorFrame 	= matDoorFrame,
						gs_frame_mat	= gs_frame_mat,
						matHeadboard	= matHeadboard,
						matDrawer		= matDrawer

		endif
	endif
endif


! ==============================================================================
! 		Hideparameters
! ==============================================================================

if iSupType = 1 then
	hideparameter "stLegStyle"
	lock "stLegStyle", "iLegStyle", "matLeg"

	if bCabinet then
		bCabLeg = 0
		parameters bCabLeg = bCabLeg
		lock "bCabLeg"
	endif
else
	hideparameter "nUnderDrawer"
	lock "nUnderDrawer", "matDrawer"
endif

if not(pw) then
	hideparameter "pillow_mat"
	lock "pillow_mat"
endif

if not(bCabinet) then
	hideparameter	"stCabOrient",
					"cabHeight",
					"cabWidth",
					"cabDepth",
					"bCabLeg",
					"stCabStyle",
					"nDrawer",
					"matCabinet",
					"matDoorFrame"

	lock	"stCabOrient",
			"iCabOrient",
			"cabHeight",
			"cabWidth",
			"cabDepth",
			"bCabLeg",
			"stCabStyle",
			"iCabStyle",
			"nDrawer",
			"matCabinet",
			"matDoorFrame"
endif


if iCabOrient = 1 then
	hideparameter "cabDepth"
	lock "cabDepth"
else
	hideparameter "cabWidth"
	lock "cabWidth"
endif

if iSupType = 2 and not(bCabinet) then
	hideparameter "stDrawerStyle", "matKnob", "matDrawer"
	lock "stDrawerStyle", "iDrawerStyle", "stKnobStyle", "iKnobStyle", "knobSize"
endif

if bCabinet and iCabStyle = 2 and iSupType = 2 then
	hideparameter "stDrawerStyle", "iDrawerStyle", "stKnobStyle", "iKnobStyle", "knobSize", "matDoorFrame", "matDrawer", "matDoorGlass", "matKnob"
	lock "stDrawerStyle", "iDrawerStyle", "stKnobStyle", "iKnobStyle", "knobSize", "matDoorFrame", "matDrawer", "matDoorGlass", "matKnob"
endif

if bCabinet and iCabStyle = 2 and iSupType = 1 then
	hideparameter "matDoorFrame", "matDoorGlass"
	lock "matDoorFrame", "matDoorGlass"
endif

if iDrawerStyle <> 3 or not(bCabinet) then
	hideparameter "matDoorGlass"
	lock "matDoorGlass"
endif

if iDrawerStyle = 8 then
	hideparameter "bKnob", "stKnobStyle", "knobSize"
	lock "bKnob", "stKnobStyle", "iKnobStyle", "knobSize"
endif

if iKnobStyle < 4 then
	hideparameter "knobSize"
	lock "knobSize"
endif


if bUniformFill then
	hideparameter	"gs_fill_type",
					"gs_fill_pen",
					"gs_back_pen",
					"fillTypePillow",
					"fillPenPillow",
					"backPenPillow",
					"fillTypeBlanket",
					"fillPenBlanket",
					"backPenBlanket",
					"iFillFor"

	lock	"gs_fill_type",
			"gs_fill_pen",
			"gs_back_pen",
			"fillTypePillow",
			"fillPenPillow",
			"backPenPillow",
			"fillTypeBlanket",
			"fillPenBlanket",
			"backPenBlanket",
			"iFillFor"
else
	hideparameter	"fillTypeUniform",
					"fillPenUniform",
					"backPenUniform"

	lock	"fillTypeUniform",
			"fillPenUniform",
			"backPenUniform"
endif


if	iSupType = 2 and not(bCabinet)				| \
	iDrawerStyle = 8							| \
	bCabinet and iCabStyle = 2 and iSupType = 2	then
		hideparameter "matKnob", "bKnob"
		lock "matKnob", "bKnob"
endif

if not(bKnob) then
	hideparameter "stKnobStyle", "iKnobStyle", "knobSize", "matKnob"
	lock "stKnobStyle", "iKnobStyle", "knobSize", "matKnob"
endif

if GLOB_MODPAR_NAME = "bCabinet" or GLOB_MODPAR_NAME = "iSymbolType" then
	iFillFor = 1
	parameters iFillFor = iFillFor
endif

if (iSymbolType = 3 or iSymbolType = 4) and not(bUniformFill) and not(bCabinet) then lock "iFillFor"


! ==============================================================================
! 		Lock Parameters
! ==============================================================================

lock "RadCushCorner", "RadCushEdge", "doorThk", "gs_bevel", "tabletopThk", "tabletopOverhang", "legWidth"


! ==============================================================================
! 		Presets
! ==============================================================================

if GLOB_MODPAR_NAME = "stCribStyle" or GLOB_MODPAR_NAME = "iCribStyle" then
	if iCribStyle = CRIB_SIMPLE then
		iSupType = 2
		supportHeight = 0.2
		iLegStyle = 4
		iHeadboardStyle = 3
		headboardHeight = 0.70
		frontRailHeight = 0.55
		rearRailHeight = 0.55
		bBoardEdge = 1
		mrh = 0.1
		platformHeight = 0.22
		platformThk = 0.12
		pw = 0
		bCabinet = 0
		MSFront = 1.0
		MSSide = 0.1
		MSSide2 = 0.1
	endif

	if iCribStyle = CRIB_DRAWER then
		iSupType = 1
 		supportHeight = 0.2
		nUnderDrawer = 1
		iHeadboardStyle = 4
		headboardHeight = 0.65
		frontRailHeight = 0.65
		rearRailHeight = 0.65
		bBoardEdge = 0
		mrh = 0.15
		platformHeight = 0.08
		platformThk = 0.08
		pw = 0
		bCabinet = 0
		iDrawerStyle = 8
		bKnob = 1
		MSFront = 1.0
		MSSide = 0.1
		MSSide2 = 0.1
	endif

	if iCribStyle = CRIB_TABLE then
		iSupType = 2
		supportHeight = 0.25
		iLegStyle = 1
		iHeadboardStyle = 2
		headboardHeight = 0.65
		frontRailHeight = 0.60
		rearRailHeight = 0.60
		bBoardEdge = 0
		mrh = 0.15
		platformHeight = 0.05
		platformThk = 0.05
		pw = 0
		bCabinet = 1
		bCabLeg = 0
		iCabOrient = 2
		iCabStyle = 1
		cabHeight = 0.90
		cabDepth = 0.25
		nDrawer = 3
		bKnob = 1
		iDrawerStyle = 8
		MSFront = 1.0
		MSSide = 0.9
		MSSide2 = 0.1
	endif

	if iCribStyle = CRIB_COMPLETE then
		iSupType = 1
		supportHeight = 0.20
		nUnderDrawer = 2
		iHeadboardStyle = 1
		headboardHeight = 0.65
		frontRailHeight = 0.40
		rearRailHeight = 0.60
		bBoardEdge = 1
		mrh = 0.15
		platformHeight = 0.05
		platformThk = 0.05
		pw = 0
		bCabinet = 1
		iCabOrient = 1
		iCabStyle = 1
		cabHeight = 0.80
		cabWidth = 0.4
		nDrawer = 4
		bKnob = 1
		iDrawerStyle = 2
		iKnobStyle = 1
		MSFront = 1.0
		MSSide = 0.1
		MSSide2 = 0.1
	endif

	parameters 	iSupType = iSupType,
				supportHeight = supportHeight,
				iLegStyle = iLegStyle,
				nUnderDrawer = nUnderDrawer,
				iHeadboardStyle = iHeadboardStyle,
				headboardHeight = headboardHeight,
				frontRailHeight = frontRailHeight,
				rearRailHeight = rearRailHeight,
				bBoardEdge = bBoardEdge,
				mrh = mrh,
				platformHeight = platformHeight,
				platformThk = platformThk,
				pw = pw,
				bCabinet = bCabinet,
				bCabLeg = bCabLeg,
				iCabOrient = iCabOrient,
				iCabStyle = iCabStyle,
				cabHeight = cabHeight,
				cabWidth = cabWidth,
				cabDepth = cabDepth,
				iDrawerStyle = iDrawerStyle,
				nDrawer = nDrawer,
				bKnob = bKnob,
				iKnobStyle = iKnobStyle,
				MSFront = MSFront,
				MSSide = MSSide,
				MSSide2 = MSSide2
endif

if	GLOB_MODPAR_NAME = "stSupType" or GLOB_MODPAR_NAME = "iSupType" |\
	GLOB_MODPAR_NAME = "supportHeight" |\
	GLOB_MODPAR_NAME = "nUnderDrawer" |\
	GLOB_MODPAR_NAME = "stLegStyle" or GLOB_MODPAR_NAME = "iLegStyle" |\
	GLOB_MODPAR_NAME = "stHeadboardStyle" or GLOB_MODPAR_NAME = "iHeadboardStyle" |\
	GLOB_MODPAR_NAME = "headboardHeight" |\
	GLOB_MODPAR_NAME = "frontRailHeight" |\
	GLOB_MODPAR_NAME = "rearRailHeight" |\
	GLOB_MODPAR_NAME = "bBoardEdge" |\
	GLOB_MODPAR_NAME = "mrh" |\
	GLOB_MODPAR_NAME = "platformHeight" |\
	GLOB_MODPAR_NAME = "platformThk" |\
	GLOB_MODPAR_NAME = "bCabinet" |\
	GLOB_MODPAR_NAME = "stCabOrient" or GLOB_MODPAR_NAME = "iCabOrient" |\
	GLOB_MODPAR_NAME = "bCabLeg" |\
	GLOB_MODPAR_NAME = "cabHeight" |\
	GLOB_MODPAR_NAME = "cabWidth" or GLOB_MODPAR_NAME = "cabDepth" |\
	GLOB_MODPAR_NAME = "stCabStyle" or GLOB_MODPAR_NAME = "iCabStyle" |\
	GLOB_MODPAR_NAME = "stDrawerStyle" or GLOB_MODPAR_NAME = "iDrawerStyle" |\
	GLOB_MODPAR_NAME = "nDrawer" |\
	GLOB_MODPAR_NAME = "bKnob" |\
	GLOB_MODPAR_NAME = "stKnobStyle" or GLOB_MODPAR_NAME = "iKnobStyle" |\
	GLOB_MODPAR_NAME = "knobSize" \
then
	iCribStyle = CRIB_CUSTOM
	parameters iCribStyle = iCribStyle
endif
