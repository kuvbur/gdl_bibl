
!!!!!!!!!!!!!!!!
!Resize_A_B_ZZYZX with different params

unID 	= 20000

!!!!!!!!!!!!!!!! Make Length stretchable

HOTSPOT2 0, 0, 				unID, resizeA, 1+256,	A:	unID=unID+1
HOTSPOT2 -0.001, 0, 		unID, resizeA, 3,		A:	unID=unID+1
HOTSPOT2 resizeA, 0, 		unID, resizeA, 2,		A:	unID=unID+1

HOTSPOT2 0, resizeB, 		unID, resizeA, 1+256,	A: 	unID=unID+1
HOTSPOT2 -0.001, resizeB, 	unID, resizeA, 3,		A:	unID=unID+1
HOTSPOT2 resizeA, resizeB, 	unID, resizeA, 2,		A:	unID=unID+1

!!!!!!!!!!!!!!!! Make Width stretchable

HOTSPOT2 0, 0, 				unID, resizeB, 1+256,	B:	unID=unID+1
HOTSPOT2 0, -0.001, 		unID, resizeB, 3,		B:	unID=unID+1
HOTSPOT2 0, resizeB, 		unID, resizeB, 2,		B:	unID=unID+1

HOTSPOT2 resizeA, 0, 		unID, resizeB, 1+256,	B:	unID=unID+1
HOTSPOT2 resizeA, -0.001, 	unID, resizeB, 3,		B:	unID=unID+1
HOTSPOT2 resizeA, resizeB, 	unID, resizeB, 2,		B:	unID=unID+1

!!!!!!!!!!!!!!!!

pen gs_cont_pen
add2 -marAxisX, -marAxisY

! Minimal space
drawindex 10
	call "fa_minimalspace" parameters all	MSType 	= 14,		! MINSPACE_DOUBLE_BED 
											unID 	= 3000

hotspot2 a/2, b,			unID: unID = unID + 1
hotspot2 a/2, b/2, 			unID: unID = unID + 1
hotspot2 a/2, 0,			unID: unID = unID + 1
hotspot2 0, b/2, 			unID: unID = unID + 1
hotspot2 a, b/2, 			unID: unID = unID + 1


!!! ******************************** Different fills
if bUniformFill then
	fillTypeBed			= fillTypeUniform
	fillPenBed			= fillPenUniform
	backPenBed			= backPenUniform
	fillTypePillow		= fillTypeUniform
	fillPenPillow		= fillPenUniform
	backPenPillow		= backPenUniform
	fillTypeBlanket		= fillTypeUniform
	fillPenBlanket		= fillPenUniform
	backPenBlanket		= backPenUniform
	iContour		= 1
else
	fillTypeBed			= gs_fill_type
	fillPenBed			= gs_fill_pen
	backPenBed			= gs_back_pen
	iContour		= 7+64
endif


! --- structure ---
rbeam	= bd / 2
rcol	= ldm / 2
lcone	= bd
lbeam	= B - 2 * marAxisY

add2 marAxisX, marAxisY
rot2 90
gosub "beam"
del 2
add2 A - marAxisX, B - marAxisY
rot2 270
gosub "beam"
del 2

rbeam	= ldm / 2
rcol	= ldm / 2
lcone	= ldm / 2 + 0.025
lbeam	= A - 2 * marAxisX

add2 A - marAxisX, marAxisY
rot2 180
gosub "beam"
del 2
add2 marAxisX, B - marAxisY
gosub "beam"
del 1

! --- matrasse ---
drawindex 30

arc2 marMatX + 4", b - marMatY - 4",		4",      90.0,     180.0
arc2 marMatX + 4", marMatY + 4",			4",     180.0,     270.0
arc2 a - marMatX - 4", marMatY + 4",		4",     270.0,       0.0
arc2 a - marMatX - 4", b - marMatY - 4",	4",   0.0,      90.0

line2 marMatX + 4",	marMatY, 		a - marMatX - 4",	marMatY
line2 marMatX + 4",	b - marMatY,	a - marMatX - 4",	b - marMatY
line2 marMatX,		marMatY + 4",	marMatX,			b - marMatY - 4"
line2 a - marMatX,	marMatY + 4",	a - marMatX,		b - marMatY - 4"

! --- fill inside beam axes ---
fill fillTypeBed
drawindex 10
poly2_b 13, 2 + 4 + 64, fillPenBed, backPenBed,
	marAxisX + ldm/2, marAxisY,			1,
	A - marAxisX - ldm/2, marAxisY,		1,
	A - marAxisX, marAxisY,				901,
	0, -90,								4001,
	A - marAxisX, B - marMatY,			1,
	A - marAxisX, B - marAxisY,			901,
	0, -90,								4001,
	marAxisX + ldm/2, B - marAxisY,	 	1,
	marAxisX, B - marAxisY,				901,
	0, -90,								4001,
	marAxisX, marMatY,					1,
	marAxisX, marAxisY,					901,
	0, -90,								4001


! --- linen ---
IF iSymbolType = 3 THEN		! diagonal lines
	LINE2 a - marMatX, marMatY,	a/2, b - marMatY
	LINE2 marMatX, marMatY,		a/2, b - marMatY
endif

IF iSymbolType = 2 THEN		! only pillow
	fill fillTypePillow
	drawindex 20
	for i = 1 to 2
		POLY2_B 5, iContour, fillPenPillow, backPenPillow,
			marPillowX, b - marPillowY - 0.28, 1,
			a/2 - dPillow, b - marPillowY - 0.28, 1,
			a/2 - dPillow, b - marPillowY, 1,
			marPillowX, b - marPillowY, 1,
			marPillowX, b - marPillowY - 0.28, -1

		add2 a, 0
		mul2 -1, 1
	next i
	del 4
ENDIF

IF iSymbolType = 1 THEN		! pillow and blanket
	ADD2 a/2, 0
	mm=1
	GOSUB 110
	mm=-1
	GOSUB 110
	DEl 1
ENDIF

del 1

END

!---------------->!!! RETURN !!!<----------------

110:
	fill fillTypePillow
	drawindex 20

	POLY2_B 6, iContour, fillPenPillow, backPenPillow,
		mm*(dPillow), b - marPillowY - 0.24, 1,
		mm*(dPillow), b - marPillowY, 1,
		mm*(a/2 - marPillowX), b - marPillowY, 1,
		mm*(a/2 - marPillowX), b - marPillowY - 0.28, 1,
		mm*(a/2 - marMatX - 0.17), b - marPillowY - 0.28, 1,
		mm*(a/2 - marMatX - 0.21), b - marPillowY - 0.24, 1

	drawindex 30
	fill fillTypeBlanket

	POLY2_B 6, iContour, fillPenBlanket, backPenBlanket,
		0, marMatY + 0.03, 1,
		mm*(a/2 - marMatX - 0.03), marMatY + 0.03, 1,
		mm*(a/2 - marMatX - 0.03), b - marPillowY - 0.42, 1,
		mm*(a/2 - marMatX - 0.21), b - marPillowY - 0.24, 1,
		0, b - marPillowY - 0.24, 0,
		0, marMatY + 0.03, -1
	LINE2 mm*(a/2 - marMatX - 0.03), b - marPillowY - 0.42,  mm*(a/2 - marMatX - 0.21), b - marPillowY - 0.42
	LINE2 mm*(a/2 - marMatX - 0.21), b - marPillowY - 0.24,  mm*(a/2 - marMatX - 0.21), b - marPillowY - 0.42
RETURN


!===================================================================================
"beam":
!
! input
! 	rbeam:		beam radius
! 	rcol:		column radius
! 	lcone:		beam endcone length
!	lbeam:		beam length
! output
!	drawing		O<======> from origo along x
!===================================================================================

drawindex 30
rect2	lcone, -rbeam, lbeam - lcone, rbeam

acone = atn(rbeam / lcone)
cx = rcol * cos(acone)
cy = rcol * sin(acone)

for i = 1 to 2
	line2	cx, cy, lcone, rbeam
	line2	cx, -cy, lcone, -rbeam
	add2 lbeam, 0
	rot2  180
next i
del 4


fill fillTypeBed

poly2_b 4, 2 + 4 + 64, fillPenBed,  backPenBed,
	0, 0,					1,
	lbeam, 0,				1,
	lbeam - lcone, rbeam,	1,
	lcone, rbeam,			1

drawindex 40

poly2_b 2, 1+2+4+64, fillPenBed, backPenBed,
	0, 0,	901,
	rcol, 360, 4001
	
return


