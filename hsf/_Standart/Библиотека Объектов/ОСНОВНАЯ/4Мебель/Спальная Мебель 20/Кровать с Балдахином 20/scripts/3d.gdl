
! Bed Canopy

IF gs_shadow=0 THEN SHADOW OFF

if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 5

pen gs_cont_pen

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!   3D HOTSPOTS   !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


Unid=1
!!! *** X Dimension
HOTSPOT 0,0,0,					unID, resizeA,1+256,	A :	unID=unID+1
HOTSPOT -1,0,0,					unID, resizeA,3,		A : unID=unID+1
HOTSPOT resizeA,0,0,			unID, resizeA,2,		A : unID=unID+1

HOTSPOT 0,resizeB,0,			unID, resizeA,1+256,	A : unID=unID+1
HOTSPOT -1,resizeB,0,			unID, resizeA,3,		A : unID=unID+1
HOTSPOT resizeA,resizeB,0,		unID, resizeA,2,		A : unID=unID+1

!!! *** Y Dimension
HOTSPOT 0,0,0,					unID, resizeB,1+256,	B : unID=unID+1
HOTSPOT 0,-1,0,					unID, resizeB,3,		B : unID=unID+1
HOTSPOT 0,resizeB,0,			unID, resizeB,2,		B : unID=unID+1

HOTSPOT resizeA,0,0,			unID, resizeB,1+256,	B : unID=unID+1
HOTSPOT resizeA,-1,0,			unID, resizeB,3,		B : unID=unID+1
HOTSPOT resizeA,resizeB,0,		unID, resizeB,2,		B : unID=unID+1

!!! *** Z Dimension
HOTSPOT 0,0,0,					unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT 0,0,-1,					unID, ZZYZX,3 : 	unID=unID+1
HOTSPOT 0,0,ZZYZX,				unID, ZZYZX,2 : 	unID=unID+1

HOTSPOT resizeA,0,0,			unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT resizeA,0,-1,			unID, ZZYZX,3 : 	unID=unID+1
HOTSPOT resizeA,0,ZZYZX,		unID, ZZYZX,2 : 	unID=unID+1

HOTSPOT resizeA,resizeB,0,		unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT resizeA,resizeB,-1,		unID, ZZYZX,3 : 	unID=unID+1
HOTSPOT resizeA,resizeB,ZZYZX,	unID, ZZYZX,2 : 	unID=unID+1

HOTSPOT 0,resizeB,0,			unID, ZZYZX,1+128 : unID=unID+1
HOTSPOT 0,resizeB,-1,			unID, ZZYZX,3 : 	unID=unID+1
HOTSPOT 0,resizeB,ZZYZX,		unID, ZZYZX,2 : 	unID=unID+1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   LEGS   !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

material lmat

for J=1 to 2
	for T=1 to 2
		call "fa_log_element" parameters 	frv	= 1",
											ldm	= ldm,
											lht	= zzyzx,
											lr	= gs_resol,
											ltp	= iBedLogType,
											lcb	= 1",
											lct	= 1"

	   addx a - 2 * marAxisX
	next T
	del 2
	addy b - 2 * marAxisY
next J
del 2
body -1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!   FRONT AND BACK SIDE   !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

material lmat

add 0,0,6"
roty 90

for J=1 to 2
	for I=1 to 3
		addz marAxisY
		call "fa_log_element" parameters 	frv	= 1",
                                          	ldm	= ldm,
                                            lht	= a - ldm - 2 * marAxisX,
                                            lr	= gs_resol,
                                            ltp	= iBedLogType,
                                            lcb	= 1",
                                            lct	= 1"
		del 1
		if J=1 then
			if I=1 then addx -15"
			if I=2 then addx -(zzyzx-6"-15"-3")
		else
			if I=1 then addx -35"
			if I=2 then addx -(zzyzx-6"-35"-3")
		endif
	next I
	del 2
	addy b - 2 * marAxisY
next J
del 4

body -1

material lmat

					m=nob
					n=nob+1


add (a - 2 * marAxisX) / n, 0, 6"


for J=1 to 2
	for I=1 to m
		if J=1 then
			hfb=15"
		else
			hfb=35"
		endif
		call "fa_log_element" parameters 	frv	= 1",
                                            ldm	= bdm,
                                            lht	= hfb,
                                            lr	= gs_resol,
                                            ltp	= iBedLogType,
                                            lcb	= bdm,
                                            lct	= bdm
		addx (a - 2 * marAxisX) / n
	next I
	del m
	addy b - 2 * marAxisY
next J
del 3
body -1


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!   LEFT AND RIGHT SIDE   !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

material lmat

add 0,0,10"
rotx -90

for J=1 to 2
	for I=1 to 3
		call "fa_log_element" parameters 	frv	= 1",
                                            ldm	= bd,
                                            lht	= b - 2 * marAxisY,
                                            lr	= gs_resol,
                                            ltp	= iBedLogType,
                                            lcb	= bd,
                                            lct	= bd
		if I=1 then
			addy -7"
		else
			addy -(zzyzx-10"-7"-3")
		endif
	next I
	del 3
	addx a - 2 * marAxisX
next J
del 4

body -1


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!   TOP CONSTRUCTION   !!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

material cmat
plane 4,	4",						4",						zzyzx-3",
			a - 2 * marAxisX - 4",	4",						zzyzx-3",
			a - 2 * marAxisX - 4",	b - 2 * marAxisY - 4",	zzyzx-3",
			4",						b - 2 * marAxisY - 4",	zzyzx-3"

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!    MATTRESS and PILLOWS   !!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

material mmat

add -marAxisX + marMatX, -marAxisY + marMatY, 8 1/2"
call "fa_cushion" parameters 	gs_detlevel_3D_m 	= gs_detlevel_3D_m,
								cush_a				= a - 2 * marMatX,
								cush_b				= b - marAxisY - bdm/2 - marMatY,
								cush_h				= 12 1/2"+3",
								r2					= 2",
								r1					= 3",
								gs_resol			= gs_resol,
								cush_mat			= mmat


del 1

add -marAxisX, -marAxisY, 8 1/2"
material pillow_mat
resol 1.5*gs_resol

add a/2, b - marPillowY - 0.14, 15 1/2"
wPillow = a/2 - marPillowX - dPillow
for K = 1 to iNumPillows
	if iNumPillows = 2 then
		addx (wPillow / 2 + dPillow) * (2 * K - 3)	!*-1 or *1
	else
		addx 0
	endif

	call "fa_pillow" parameters	a 			= wPillow,
								gs_resol	= 4 * gs_resol,
								pillow_mat	= pillow_mat

	del 1
next K
del 2


