
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

values{2} "iBedLogType" 	BEDLOG_SOFT, `Гладкие`,
							BEDLOG_RUSTIC, `Грубые`
values{2} "iNumPillows" 	NUMPILLOW_NONE, `Без Подушки`,
							NUMPILLOW_ONE, `Одна Подушка`,
							NUMPILLOW_TWO, `Две Подушки`
values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_SIMPLE, `Простой`,
								DLEV3D_OFF, `Откл.`
values "gs_resol" range [3,)
values "A" range [0.4 + 2 * marAxisX,)

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19

values "zzyzx" range [50",)
values "bd" range [3/4",)
values "ldm" range [1",)
values "bd" range [1/2",)

if iNumPillows = NUMPILLOW_NONE then lock "pillow_mat"

if gs_detlevel_3D_m = DLEV3D_SIMPLE then lock "gs_resol"
if gs_detlevel_3D_m = DLEV3D_OFF then lock "gs_resol", "gs_shadow"

! --- Minimal Space ---

if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif
if MSSide < 0 then
	MSSide = 0
	parameters MSSide = MSSide
endif
if MSLength < 0 then
	MSLength = 0
	parameters MSLength = MSLength
endif
if MSSide2 < 0 then
	MSSide2 = 0
	parameters MSSide2 = MSSide2
endif


!!! ******************************** Different fills
if GLOB_MODPAR_NAME = "fillFor" then
	iFillFor = 1
	for i = 1 to vardim1(stFillFor)
		if fillFor = stFillFor[i] then	iFillFor = i
	next i
	parameters iFillFor = iFillFor
else
	iFillFor = max(min(iFillFor,3),1)
	parameters fillFor = stFillFor[iFillFor]
endif

if fillPenUniform=0 then parameters fillPenUniform=19
if fillPenPillow=0 then parameters fillPenPillow=19
if fillPenBlanket=0 then parameters fillPenBlanket=19

if iSymbolType = 1 or iSymbolType = 2 then
	if iSymbolType = 1 then
		values "fillFor" stFillFor
		values "iFillFor" 1, 2, 3
	endif
	if iSymbolType = 2 then
		values "fillFor" stFillFor[1], stFillFor[2]
		values "iFillFor" 1, 2
		hideparameter "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
		lock 	      "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	endif
else
	values "fillFor" stFillFor[1]
	values "iFillFor" 1
	lock		  "fillFor", "iFillFor"
	hideparameter "fillFor", "iFillFor"
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket"
endif

if bUniformFill then
	lock "fillFor", "iFillFor"
	hideparameter "fillFor", "iFillFor"
	hideparameter "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", "gs_fill_type", "gs_fill_pen", "gs_back_pen"
	lock 	      "fillTypePillow", "fillPenPillow", "backPenPillow", "fillTypeBlanket", "fillPenBlanket", "backPenBlanket", "gs_fill_type", "gs_fill_pen", "gs_back_pen"
else
	hideparameter "fillTypeUniform", "fillPenUniform", "backPenUniform"
	lock 	      "fillTypeUniform", "fillPenUniform", "backPenUniform"
endif


! ----- 2D Symbol Type -----
values "styp" stSymbolTypes		! Type 1(1),Type 2(2),Type 3(3),Type 4(4)
values "iSymbolType" 1, 2, 3, 4
if GLOB_MODPAR_NAME = "styp" then
	iSymbolType = 1
	for i = 1 to vardim1(stSymbolTypes)
		if styp= stSymbolTypes[i] then	iSymbolType = i
	next i
	parameters iSymbolType = iSymbolType
else
	iSymbolType = max(min(iSymbolType,4),1)
	parameters styp= stSymbolTypes[iSymbolType]
endif


! ----- Resize -----
if GLOB_MODPAR_NAME = "resizeA" | GLOB_MODPAR_NAME = "resizeB" then
	A = resizeA + 2 * marAxisX
	B = resizeB + 2 * marAxisY
	parameters	A = A,
				B = B
else
	resizeA = A - 2 * marAxisX
	resizeB = B - 2 * marAxisY
	parameters	resizeA = resizeA,
				resizeB = resizeB
endif
