
call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle"  1, 2

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	parameters stStyle = stStyle
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------

values "gs_detlevel_3D" `Детальный`, `Откл.`
values "gs_detlevel_3D_m" 2, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D_m" then
	gs_detlevel_3D = `Детальный`
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	parameters gs_detlevel_3D = gs_detlevel_3D
else
	gs_detlevel_3D_m = 2
	if gs_detlevel_3D = `Откл.` then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
endif

if gs_detlevel_3D_m = 0 then
	hideparameter "gs_shadow"
	lock "gs_shadow"
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if gs_cont_pen = 0 then parameters gs_cont_pen = 4
if gs_fill_pen = 0 then parameters gs_fill_pen = 19

values "a" range (0,)
values "b" range (0,)
values "ZZYZX" range (0,)
values "tableDepth" range (0,)
values "shortSideLength" range (0,)

isFirstRun = 0
qq = APPLICATION_QUERY ("parameter_script", "firstoccasion_in_progress", isFirstRun)

if (GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle") and isFirstRun then
	a = defaultSizes[iStyle][1]
	zzyzx = defaultSizes[iStyle][2]
	shortSideLength = defaultSizes[iStyle][3]
	parameters a = a, zzyzx = zzyzx

	if iStyle = 1 then
		b = tableDepth
		parameters b = b
	else
		tableDepth = b
		parameters tableDepth = tableDepth
	endif
endif

if iStyle = 1 then
	hideparameter "tableDepth", "shortSideLength"
else
	b = tableDepth + shortSideLength * sin (45)
	a = tableDepth + shortSideLength * cos (45)
	parameters b = b, a = a
	lock "a", "b"
	hideparameter "MSLength"
endif

values "gs_ui_current_page" 1

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------

values "MSFront" range [0,)
values "MSLength" range [0,)


