
!! Computer Table.gsm

IF gs_shadow=0 THEN SHADOW OFF


IF gs_detlevel_3D_m = DLEV3D_OFF THEN END
IF gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 5


pen gs_cont_pen


rdwid=abs(rdwid)
mdln=abs(mdln)
mtwid=abs(mtwid)
frath=abs(frath)
numshel=abs(numshel)


ADD frath,0,zzyzx-frath
	MATERIAL gs_top_mat
	BLOCK a-frath*2,mtwid,frath
DEL 1
ADD frath/2,frath/2,0
GOSUB 10
ADDX a-frath
GOSUB 10
DEL 2
FOR xx=1 TO numshel
ADD frath,mtwid-frath,z0+frath*2+(zzyzx-z0-frath*2.5)/(numshel+1)*(xx-1)
	MATERIAL gs_top_mat
	BLOCK a-frath*2,b+frath-mtwid,frath
DEL 1
NEXT xx
IF ABS(rdwid)>EPS GOSUB 50
GOSUB 100
ADD frath,frath*0.6,zzyzx-frath*1.7
ROTY 90
	MATERIAL gs_leg_mat
	EXTRUDE 3,0,0,a-frath*2,48,-frath*0.7,0,1,0,0,901,frath*0.7,180,4001
DEL 2
ADD -mdln,frath,zzyzx-frath*2
	MATERIAL gs_top_mat
	IF ABS(mdln)>EPS THEN BLOCK a+mdln-frath,mtwid-frath*2,frath*0.7
DEL 1
END

10:GOSUB 12
ADDY b-frath
GOSUB 12
GOSUB 14
MATERIAL gs_leg_mat
z0=(zzyzx-frath*3)*0.25+frath*3
DEL 1
FOR xx=1 to gs_resol/4
PUT  0,frath*2-frath*2*cos(360/gs_resol*xx),z0-frath*2+frath*2*sin(360/gs_resol*xx),0
NEXT xx
FOR xx=1 to gs_resol/4
PUT  0,mtwid-frath-frath*2+frath*2*cos(90-360/gs_resol*xx),z0+frath*2-frath*2*sin(90-360/gs_resol*xx),0
NEXT xx

	TUBE 2,6+nsp/4,51,0,0,901,frath/2,360,4001,
	 0,0,frath*3,0,
	 0,0,frath*3.001,0,
	 0,0,z0-frath*2,0,
	 GET(nsp/2),
	 0,mtwid-frath-frath*2,z0,0,
	 GET(nsp),
	 0,mtwid-frath,zzyzx-frath/1.99,0,
	 0,mtwid-frath,zzyzx-frath/2,0
RETURN

12:ROTY -90
MATERIAL leg_base_mat
	RESOL gs_resol
	REVOLVE 6,360,63,
	 0,frath/4,1,frath/4,frath/4,1,frath/4,frath*1,1,frath*2.4,frath*0.9,1,
	 frath*2.4,frath*0.4,1,frath*3,frath*0.4,1
DEL 1
RETURN

14:MATERIAL gs_leg_mat
FOR xx=1 to gs_resol/4
PUT  0,frath*2*cos(360/gs_resol*xx)-frath*2,zzyzx-frath*2.5+frath*2*sin(360/gs_resol*xx),0
NEXT xx
	TUBE 2,5+nsp/4,51,0,0,901,frath/2,360,4001,
	 0,0,frath*3,0,
	 0,0,frath*3.001,0,
	 0,0,zzyzx-frath*2.5,0,
	 GET(nsp),
	 0,frath*0.75-b,zzyzx-frath/2,0,
	 0,frath*0.749-b,zzyzx-frath/2,0
ADD 0,frath*0.75-b,zzyzx-frath/2
ROTX 90
	MATERIAL leg_base_mat
	CYLIND frath/4,frath/2
DEL 2
RETURN

50:zc=zzyzx-frath*7
ADD a,mtwid-frath,frath*4
	MATERIAL gs_top_mat
	BLOCK rdwid,b+frath-mtwid,frath*0.7
ADDZ zc-frath*0.7
	BLOCK rdwid,b+frath-mtwid,frath*0.7
ADD rdwid,frath*0.35,frath*0.35
GOSUB 52
ADDY b+frath*0.35-mtwid
GOSUB 52
DEL 3
ADD -frath*0.35,frath*1.35,0
	MATERIAL gs_leg_mat
	CYLIND z0-frath,frath*0.35
DEL 2
RETURN

52:
ROTY 90



	iv=90/(gs_resol/4)

	FOR xx=-1 to (gs_resol/4)
	PUT frath*2-frath*2*cos(iv*xx),0,frath*2*sin(iv*xx),0
	NEXT xx
	FOR xx=0 to (gs_resol/4)+1
	PUT zc-frath*0.7-frath*2+frath*2*sin(iv*xx),0,frath*2*cos(iv*xx),0
	NEXT xx
		MATERIAL gs_leg_mat
		TUBE 2,nsp/4,51,0,0,901,frath*0.35,360,4001,
		 GET(nsp)

DEL 1
RETURN

100:

z2=zzyzx-z0-frath*2.5-(zzyzx-z0-frath*2.5)*0.67
fi=atn((z2-frath*4)/(b-mtwid+frath))
z3=(b-mtwid+frath)*tan(fi)
ADD frath,mtwid-frath,zzyzx-z2+frath+z3
ROTX -fi
	MATERIAL gs_top_mat
	BLOCK a-frath*2,(b-mtwid+frath)/cos(fi),frath*0.7
ADDZ frath*0.7
CUTPLANE 180
DEL 2
ADD (a-frath*2)/2,(b-mtwid+frath)/1.4,0
rad=(a-frath*3)/2
n1=INT(gs_resol/1.5)
FOR xx=1 to n1
PUT  -rad,frath-frath*cos(90/n1*xx),
 frath+frath*sin(90/n1*xx),0
NEXT xx
FOR xx=1 to n1*2-1
ang1=90/n1*xx
PUT  -rad*cos(ang1),
 rad*sin(ang1)+frath,
 frath*2+rad*(0.34-0.34*((ang1-90)/90)^2),
 0
NEXT xx
FOR xx=1 to n1
PUT  rad,frath-frath*cos(90-90/n1*xx),
 frath+frath*sin(90-90/n1*xx),0
NEXT xx
	MATERIAL gs_leg_mat
	TUBE 2,6+nsp/4,51,0,0,901,frath*0.35,360,4001,
	 -rad,0,-z3,0,
	 -rad,0,frath/1000-z3,0,
	 -rad,0,frath,0,
	 GET(n1),
	 GET(nsp-n1),
	 GET(n1),
	 rad,0,frath,0,
	 rad,0,frath/1000-z3,0,
	 rad,0,-z3,0
CUTEND
DEL 2
RETURN
