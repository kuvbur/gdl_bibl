
unID = 1
hotspot2 0, 0,	unID : unID = unID + 1
hotspot2 -a/2, 0,	unID : unID = unID + 1
hotspot2 a/2, 0,	unID : unID = unID + 1
hotspot2 0, b/2,	unID : unID = unID + 1
hotspot2 0, -b/2,	unID : unID = unID + 1

! Minimal Space
reqBMinSpace = 0
call "fa_minimalspace" parameters all MSType = 0, unID = 3000,
							returned_parameters reqBMinSpace,
												MSFillType,
												MSFillPen,
												MSFillBackPen,
												MSContLineType,
												MSContLinePen

if reqBMinSpace <> 0 then
	unID = 3000
	hotspot2 -a/2-MSFront,	0,	unID : unID = unID + 1
	hotspot2 a/2+MSFront,	0,	unID : unID = unID + 1
	hotspot2 0,				b/2+MSFront,	unID : unID = unID + 1
	hotspot2 0,				-b/2-MSFront,	unID : unID = unID + 1

	FILL MSFillType
	pen MSContLinePen
	line_type MSContLineType

	if ABdifference<EPS then		!! A=B
	POLY2_B 3, 1+2+4+64,MSFillPen,MSFillBackPen,
				0, 0,					900,
				a/2+MSFront,	360,	4001,
				a/2,			360,	4000
	endif

	if ABdifference>EPS and iTypeTableEnd = TABLEEND_ARC then	!!a<>b
		if a>b then
			hotspot2 -ABdifference/2,	-b/2-MSFront,	unID : unID = unID + 1
			hotspot2 ABdifference/2,	-b/2-MSFront,	unID : unID = unID + 1
			hotspot2 ABdifference/2,	b/2+MSFront,	unID : unID = unID + 1
			hotspot2 -ABdifference/2,	b/2+MSFront,	unID : unID = unID + 1

			POLY2_B 16, 1+2+4+64,MSFillPen,MSFillBackPen,
					-ABdifference/2,	-b/2-MSFront,	1,
					ABdifference/2,		-b/2-MSFront,	1,
					ABdifference/2,		0,				900,
					b/2+MSFront,		180,			4001,
					-ABdifference/2,	b/2+MSFront,	1,
					-ABdifference/2,	0,				900,
					b/2+MSFront,		180,			4001,
					-ABdifference/2,	-b/2-MSFront,	-1,
					-ABdifference/2,	-b/2,			0,
					ABdifference/2,		-b/2,			0,
					ABdifference/2,		0,				900,
					b/2,				180,			4000,
					-ABdifference/2,	b/2,			0,
					-ABdifference/2,	0,				900,
					b/2,				180,			4000,
					-ABdifference/2,	-b/2,			-1
		endif
		if a<b then
			hotspot2 -a/2-MSFront,	ABdifference/2,	unID : unID = unID + 1
			hotspot2 -a/2-MSFront,	-ABdifference/2,	unID : unID = unID + 1
			hotspot2 a/2+MSFront,	ABdifference/2,	unID : unID = unID + 1
			hotspot2 a/2+MSFront,	-ABdifference/2,	unID : unID = unID + 1

			POLY2_B 16, 1+2+4+64,MSFillPen,MSFillBackPen,
					-a/2-MSFront,	ABdifference/2,		1,
					-a/2-MSFront,	-ABdifference/2,	1,
					0,				-ABdifference/2,	900,
					a/2+MSFront,	180,				4001,
					a/2+MSFront,	ABdifference/2,		1,
					0,				ABdifference/2,		900,
					a/2+MSFront,	180,				4001,
					-a/2-MSFront,	ABdifference/2,		-1,
					-a/2,			ABdifference/2,		0,
					-a/2,			-ABdifference/2,	0,
					0,				-ABdifference/2,	900,
					a/2,			180,				4000,
					a/2,			ABdifference/2,		0,
					0,				ABdifference/2,		900,
					a/2,			180,				4000,
					-a/2,			ABdifference/2,		-1
		endif
	endif

	if ABdifference>EPS and iTypeTableEnd = TABLEEND_ELLIP then		!!a<>b
		v=-MSFront
		contourFlag = 1
		gosub "ellipse"
		v=0
		contourFlag = 0
		gosub "ellipse"
		poly2_b nn+nn, 1+2+4+64, MSFillPen, MSFillBackPen,
				get(nsp)
	endif

endif

! 2D Symbol

pen gs_cont_pen
line_type 1
fill gs_fill_type

! 2D Symbol * A=B
if ABdifference<EPS then		!!a=b
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, 0, 901,
			a/2, 360, 4001


	HOTARC2 0,0,A/2,0,180
	HOTARC2 0,0,A/2,180,360

!!		if widthTabletopFrame>0 then circle2 0, 0, a/2-widthTabletopFrame
endif


! 2D Symbol * A<>B * ARC
if ABdifference>EPS and iTypeTableEnd = TABLEEND_ARC then	!!a<>b
	if a>b then
		hotspot2 -ABdifference/2, -b/2,	unID : unID = unID + 1
		hotspot2 ABdifference/2, -b/2,	unID : unID = unID + 1
		hotspot2 ABdifference/2, b/2,	unID : unID = unID + 1
		hotspot2 -ABdifference/2, b/2,	unID : unID = unID + 1

		HOTLINE2 -ABdifference/2, -b/2,ABdifference/2, -b/2
		HOTLINE2 -ABdifference/2, b/2,ABdifference/2, b/2

		ADD2 ABdifference/2,0
		ROT2 -90
		HOTARC2 0,0,b/2,0,180
		DEL 2
		ADD2 -ABdifference/2,0
		ROT2 90
		HOTARC2 0,0,b/2,0,180
		DEL 2


		fill gs_fill_type
		poly2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-ABdifference/2, -b/2, 1,
				ABdifference/2, -b/2, 1,
				ABdifference/2, 0, 901,
				b/2, 180, 4001,
				-ABdifference/2, b/2, 1,
				-ABdifference/2, 0, 901,
				b/2, 180, 4001
		pen gs_cont_pen
		poly2_ 7, 1,                  !!contour
				-ABdifference/2, -b/2, 1,
				ABdifference/2, -b/2, 1,
				ABdifference/2, 0, 901,
				b/2, 180, 4001,
				-ABdifference/2, b/2, 1,
				-ABdifference/2, 0, 901,
				b/2, 180, 4001

!			if widthTabletopFrame>0 then
!				arc2 ABdifference/2, 0, b/2-widthTabletopFrame, -90, 90
!				arc2 -ABdifference/2, 0, b/2-widthTabletopFrame, 90, 270
!				line2 -ABdifference/2, -b/2+widthTabletopFrame, ABdifference/2, -b/2+widthTabletopFrame
!				line2 -ABdifference/2, b/2-widthTabletopFrame, ABdifference/2, b/2-widthTabletopFrame
!			endif
	endif

	if a<b then
		hotspot2 -a/2, ABdifference/2,	unID : unID = unID + 1
		hotspot2 -a/2, -ABdifference/2,	unID : unID = unID + 1
		hotspot2 a/2, ABdifference/2,	unID : unID = unID + 1
		hotspot2 a/2, -ABdifference/2,	unID : unID = unID + 1

		HOTLINE2 -a/2, ABdifference/2,-a/2, -ABdifference/2
		HOTLINE2 a/2, ABdifference/2,a/2, -ABdifference/2

		HOTARC2 0,ABdifference/2,a/2,0,180
		HOTARC2 0,-ABdifference/2,a/2,180,360


		fill gs_fill_type

		poly2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-a/2, ABdifference/2, 1,
				-a/2, -ABdifference/2, 1,
				0, -ABdifference/2, 901,
				a/2, 180, 4001,
				a/2, ABdifference/2, 1,
				0, ABdifference/2, 901,
				a/2, 180, 4001
		pen gs_cont_pen
		poly2_ 7, 1,   !!contour
				-a/2, ABdifference/2, 1,
				-a/2, -ABdifference/2, 1,
				0, -ABdifference/2, 901,
				a/2, 180, 4001,
				a/2, ABdifference/2, 1,
				0, ABdifference/2, 901,
				a/2, 180, 4001

!			if widthTabletopFrame>0 then
!				arc2  0, ABdifference/2, a/2-widthTabletopFrame, 0, 180
!				arc2 0, -ABdifference/2, a/2-widthTabletopFrame, 180, 360
!				line2 -a/2+widthTabletopFrame, -ABdifference/2, -a/2+widthTabletopFrame, ABdifference/2
!				line2 a/2-widthTabletopFrame, -ABdifference/2, a/2-widthTabletopFrame, ABdifference/2
!			endif
	endif

endif

! 2D Symbol * A<>B * Elliptic

if ABdifference>EPS and iTypeTableEnd = TABLEEND_ELLIP then		!!A<>b
	v=0
	gosub 10

	pen gs_cont_pen
	fill gs_fill_type

		poly2_b n, 1+2+4+64, gs_fill_pen, gs_back_pen,
				get(nsp)
!			if widthTabletopFrame>0 then
!				v=widthTabletopFrame
!				gosub 10
!
!				poly2_b n, 1+2+64, gs_fill_pen, 19,
!						get(nsp)
!			endif

	IF B>A THEN
		MUL2 1,B/A
			HOTARC2 0,0,a/2,0,180
			HOTARC2 0,0,a/2,180,360
		DEL 1
	ENDIF

	IF A>B THEN
		MUL2 A/B,1
			HOTARC2 0,0,b/2,0,180
			HOTARC2 0,0,b/2,180,360
		DEL 1
	ENDIF
endif
END

! GOSUB

10:
	n=0
	for x=0 to 360 step 10
		n=n+1
		put (a/2-v)*cos(x), (b/2-v)*sin(x), 1
	next x
	return

"ellipse":
	nn=0
	for x=0 to 360 step 10
		nn=nn+1
		put (a/2-v)*cos(x), (b/2-v)*sin(x), contourFlag
	next x
		put (a/2-v)*cos(0), (b/2-v)*sin(0), -1
		nn=nn+1
	return
