
if gs_detlevel_3D_m = DLEV3D_OFF then end

call "Resize_A_B_ZZYZX" PARAMETERS ALL

IF gs_shadow=0 THEN SHADOW OFF

pen gs_cont_pen

! --- middle hotspots --------------------------------------------------
unID = 1
hotspot a/2, b/2, 0,		unID: unID = unID + 1
hotspot a/2, b/2, zzyzx,	unID: unID = unID + 1

! --- legs -------------------------------------------------------------
material gs_leg_mat

add tbo, tbo, 0
for x=1 to 2
	for y=1 to 2
		roty -90
		del 1
		block ls, ls, zzyzx-tbtt
		addx a-2*(tbo+ls/2)
	next y
	del 2
	addy b-2*(tbo+ls/2)
next x
del 3

vert 0,0,0
vert zzyzx,0,0
vert 0,0.1,0
vert 0,0,0.1
coor 2,-1,-4,-3,-2
base


! --- table --------------------------------------------------------------
material gs_top_mat

add tbf, tbf, zzyzx-tbtt

if tbf>0 then
	block a-2*tbf, b-2*tbf, tbtt
else
	block a, b, tbtt
endif


if a>b then gosub 5000
if a<b then gosub 1000


if tbf>0 then
	add -tbf, -tbf, 0
	prism_  10, tbft,
			0, 0, 15,
			a, 0, 15,
			a, b, 15,
			0, b, 15,
			0, 0, -1,
			tbf, tbf, 15,
			a-tbf, tbf, 15,
			a-tbf, b-tbf, 15,
			tbf, b-tbf, 15,
			tbf, tbf, -1
	del 1
endif
del 1

vert 0,0,0
vert a,0,0
vert 0,b,0
vert 0,0,tbtt
coor 1,-1,-2,-3,-4
base
body -1


! --- frame -----------------------------------------
if frame then
	material fmat
	gosub 10
	add a, b, 0
	rotz 180
	gosub 10
	del 2
endif
end

! === end ===================================================


10:
	add tbo+2*ls/2, tbo+ls/2, zzyzx-ft-tbtt
	prism_  9, ft,
			0, 0, 15,
			a-2*(tbo+ls), 0, 15,
			a-2*(tbo+ls), ls/2, 15,
			a-2*(tbo+ls)-ls/2, ls/2, 15,
			a-2*(tbo+ls)-ls/2, ls, 15,
			ls/2, ls, 15,
			ls/2, ls/2, 15,
			0, ls/2, 15,
			0, 0, -1
	del 1
	add (tbo+ls/2), tbo+ls, zzyzx-ft-tbtt
	block ls, b-2*(tbo+ls), ft
	del 1
	return

1000:
	roty 90
	gosub 5000
	del 1
	return

5000:

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return
