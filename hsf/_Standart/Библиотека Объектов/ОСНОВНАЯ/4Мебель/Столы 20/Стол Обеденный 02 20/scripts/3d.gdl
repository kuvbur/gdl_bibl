
IF not(gs_shadow) THEN SHADOW OFF

pen gs_cont_pen


if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 4

RESOL gs_resol


! --- legs ---------------------------------------------------------------------

material gs_leg_mat

add tbo+ls*3/2, tbo+ls/2, 0
for x=1 to 2
	add -ls,ls,0
	rotz 90
	CYLIND zzyzx-tbtt-0.0375-ls/2, ls/2
	ADDZ zzyzx-tbtt-0.0375-ls/2
	ELBOW 0.0375, 90, ls/2
	ADD 0.0375, 0, 0.0375
	ROTY 90
	CYLIND b-2*(tbo+.0375+ls*3/2), ls/2
	add 0,0,b-2*(tbo+.0375+ls*3/2)
	ELBOW 0.0375, 90, ls/2
	del 2
	add b-2*(tbo+.0375+ls*3/2)+.0375,0,-(zzyzx-tbtt-ls/2)
	CYLIND zzyzx-tbtt-0.0375-ls/2, ls/2
	del 5

	addx a-2*(tbo+ls/2)
next x
del 2
for y=1 to 2
	roty -90
	del 1

	CYLIND zzyzx-tbtt-0.0375-ls/2, ls/2
	ADDZ zzyzx-tbtt-0.0375-ls/2
	ELBOW 0.0375, 90, ls/2
	ADD 0.0375, 0, 0.0375
	ROTY 90
	CYLIND a-2*(tbo+.0375+ls*3/2), ls/2
	add 0,0,a-2*(tbo+.0375+ls*3/2)
	ELBOW 0.0375, 90, ls/2
	del 2
	add a-2*(tbo+.0375+ls*3/2)+.0375,0,-(zzyzx-tbtt-ls/2)
	CYLIND zzyzx-tbtt-0.0375-ls/2, ls/2
	del 3

	addy b-2*(tbo+ls/2)
next y

del 3


vert 0,0,0
vert zzyzx,0,0
vert 0,0.1,0
vert 0,0,0.1
coor 2,-1,-4,-3,-2
base


! --- table --------------------------------------------------------------------

material gs_top_mat

add tbf, tbf, zzyzx-tbtt

if tbf>0 then
	block a-2*tbf, b-2*tbf, tbtt
else
	block a, b, tbtt
endif


if a>b then gosub 5000
if a<b then gosub 1000


if tbf>0 then
	add -tbf, -tbf, 0
	prism_  10, tbft,
			0, 0, 15,
			a, 0, 15,
			a, b, 15,
			0, b, 15,
			0, 0, -1,
			tbf, tbf, 15,
			a-tbf, tbf, 15,
			a-tbf, b-tbf, 15,
			tbf, b-tbf, 15,
			tbf, tbf, -1
	del 1
endif
del 1

vert 0,0,0
vert a,0,0
vert 0,b,0
vert 0,0,tbtt
coor 1,-1,-2,-3,-4
base
body -1


! ==============================================================================
end
! ==============================================================================


1000:
roty 90
gosub 5000
del 1
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return



