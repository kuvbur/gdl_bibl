
if not (gs_shadow) then shadow off
pen gs_cont_pen
if gs_detlevel_3D_m = 0 then end

if (iType = 1 or iType = 2) and not(bBentTop) then
	addx -wingLength
else
	addx 0
endif
call "Resize_A_B_ZZYZX" Parameters all
del 1

unID = 1
hotspot middleWidth/2, b/2, 0,		unID : unID = unID + 1
hotspot middleWidth/2, b/2, zzyzx,	unID : unID = unID + 1

! ==============================================================================
! --- legs ---
! ==============================================================================

material gs_leg_mat

add tableTopOverhang, tableTopOverhang, 0
for x = 1 to 2
	for y = 1 to 2
		block legWidth, legWidth, zzyzx - tableTopThk
		addx middleWidth - 2 * (tableTopOverhang) - legWidth
	next y
	del 2
	addy b - 2 * tableTopOverhang - legWidth
next x
del 3

gosub 5000 ! texture
! ==============================================================================
! --- Frame ---
! ==============================================================================

material matLegFrame
gosub 10
add middleWidth, b, 0
rotz 180
gosub 10
del 2

add tableTopOverhang + legWidth - tableTopThk, tableTopOverhang + legWidth, 0.1

material gs_leg_mat

for ii = 1 to 2
	block tableTopThk, b - 2 * (tableTopOverhang + legWidth), 0.06
	addx middleWidth - 2 * (tableTopOverhang + legWidth) + tableTopThk
next ii

gosub 1000 ! texture
del 2

add tableTopThk, b/2 - (tableTopOverhang + legWidth) - tableTopThk/2, 0
block middleWidth - 2 * (tableTopOverhang + legWidth), tableTopThk, 0.06
gosub 1000 ! texture
del 2

! ==============================================================================
! --- Tabletop ---
! ==============================================================================

material gs_top_mat

add 0, 0, zzyzx - tableTopThk

block middleWidth, b, tableTopThk

gosub 5000 ! texture

del 1


! ---  Type 1 ------------------------------------------------------------------

if iType = 1 then
	if bBentTop then
		add -legWidth, 0, zzyzx - 0.005
		roty 90
		for i=1 to 2
			block wingLength, b, tableTopThk
			addz middleWidth + 1.5*legWidth
		next i

		if middleWidth > b then gosub 5000 ! texture
		if middleWidth <= b then gosub 1000 ! texture

		del 4

		add -legWidth/2, tableTopOverhang + legWidth, 0
		material gs_leg_mat
		for jj = 1 to 2
			for j = 1 to 2
				block legWidth * 3/2, legWidth, zzyzx -tableTopThk
				addy b - 2 * (tableTopOverhang + legWidth) - legWidth
			next j
			del 2
			addx middleWidth - 2 * (tableTopOverhang + legWidth) + legWidth*7/2
		next jj
		del 3

		gosub 5000 ! texture

		add -legWidth/2, 0.05 + legWidth, 0.22
		prism_ 4, 0.06,
			0,			0,						15,
			legWidth/2,	0,						15,
			legWidth,	b - 0.1,	15,
			legWidth/2,	b - 0.1,	15
		del 1

		add 0, 0.05 + legWidth, 0.28
		prism_ 4, 0.06,
			0,				0,						15,
			legWidth/2,		0,						15,
			0,				b - 0.1 - legWidth * 2,	15,
			-legWidth/2,	b - 0.1 - legWidth * 2,	15
		del 1

		rotx 180
		add middleWidth- legWidth/2, -b + 0.05 + legWidth, -0.28

		prism_ 4,.06,
			0,			0,						15,
			legWidth/2,	0,						15,
			legWidth/2,	b - 0.1 - legWidth * 2,	15,
			legWidth/2,	b - 0.1 - legWidth * 2,	15
		del 1

		add middleWidth, -b + 0.05 + legWidth, -0.34
		prism_ 4,.06,
			0,				0,						15,
			legWidth/2,		0,						15,
			0,				b - 0.1 - legWidth * 2,	15,
			-legWidth/2,	b - 0.1 - legWidth * 2,	15
		del 2
	else
		! --- tabletop wings ---

		material gs_top_mat

		add -wingLength, 0, zzyzx - tableTopThk
		for i = 1 to 2
			block wingLength, b, tableTopThk
			addx middleWidth+ wingLength
		next i
		del 3

		! --- side legs ---

		material gs_leg_mat

		add -wingLength + tableTopOverhang, b/2 - legWidth/2, 0
		block legWidth, legWidth, zzyzx - tableTopThk		! side leg 1
		del 1

		add middleWidth + wingLength -tableTopOverhang - legWidth, b/2 - legWidth/2,0
		block legWidth, legWidth, zzyzx - tableTopThk		! side leg 1
		del 1

		! --- frames under wings ---

		for i = 1 to 2
			add 0,tableTopOverhang, 0.2

			material gs_leg_mat
			prism_ 4, 0.06,
				tableTopOverhang,					0,									15,
				tableTopOverhang,					legWidth/4,							15,
				-wingLength +tableTopOverhang+legWidth,	b/2 - legWidth/4 - tableTopOverhang,	15,
				-wingLength +tableTopOverhang+legWidth,	b/2 - legWidth/2 - tableTopOverhang,	15
			addz -0.2 + zzyzx - tableTopThk - 0.06

			material matLegFrame
			prism_ 4, 0.06,
				tableTopOverhang,					0,										15,
				tableTopOverhang,					legWidth/4,								15,
				-wingLength +tableTopOverhang+legWidth,	b/2 - legWidth/4 - tableTopOverhang,	15,
				-wingLength +tableTopOverhang+legWidth,	b/2 - legWidth/2 - tableTopOverhang,	15
			del 2

			add 0, b -tableTopOverhang- legWidth/4, 0.26

			material gs_leg_mat
			prism_ 4, 0.06,
				tableTopOverhang,		0,					15,
				tableTopOverhang,		legWidth/4,			15,
				-wingLength +tableTopOverhang+legWidth,	-b/2 + 3*legWidth/4 + tableTopOverhang,	15,
				-wingLength +tableTopOverhang+legWidth,	-b/2 + legWidth/2 + tableTopOverhang,	15
			addz -0.26 + zzyzx - tableTopThk - 0.06

			material matLegFrame
			prism_ 4, 0.06,
				tableTopOverhang,		0,					15,
				tableTopOverhang,		legWidth/4,			15,
				-wingLength +tableTopOverhang+legWidth, -b/2 + 3*legWidth/4 + tableTopOverhang,	15,
				-wingLength +tableTopOverhang+legWidth, -b/2 + legWidth/2 + tableTopOverhang,	15
			del 2

			add middleWidth, b, 0
			rotz 180
		next i
		del 2
	endif
endif


! ---  Type 2 ------------------------------------------------------------------

if iType = 2 then
	material gs_top_mat
	if bBentTop then
		addz zzyzx - 0.005
		roty 90
		for i = 1 to 2
			block wingLength, b, tableTopThk
			addz middleWidth - tableTopThk
		next i

		gosub 5000 ! texture

		del 4
	else
		add -wingLength, 0, zzyzx - tableTopThk
		for i = 1 to 2
			block wingLength - 0.002, b, tableTopThk
			del 1
			add middleWidth + 0.002, 0, zzyzx - tableTopThk
		next i
		del 1
	endif
endif


! ---  Type 3 ------------------------------------------------------------------

if iType = 3 then
	material gs_top_mat
	if bBentTop then
		addz zzyzx - 0.005
		roty 90
		addz middleWidth - tableTopThk
		block wingLength, b, tableTopThk

		gosub 5000 ! texture

		del 3
	else
		add middleWidth + 0.001, 0, zzyzx - tableTopThk
		block wingLength - 0.002, b, tableTopThk
		del 1
		add middleWidth + 0.002,0,zzyzx
		del 1
	endif
endif

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ==============================================================================


10:
	add tableTopOverhang + legWidth, tableTopOverhang , zzyzx - frameThk - tableTopThk
	block middleWidth - 2*(tableTopOverhang + legWidth), tableTopThk, frameThk
	del 1

	add tableTopOverhang, legWidth+tableTopOverhang , zzyzx - frameThk - tableTopThk
	block legWidth, b - 2*(tableTopOverhang + legWidth), frameThk
	del 1
	return


1000:
	roty 90
	gosub 5000
	del 1
	return


5000:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return
