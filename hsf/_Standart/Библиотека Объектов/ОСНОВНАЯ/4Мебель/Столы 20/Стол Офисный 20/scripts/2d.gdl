
!! Office Table.gsm

pen gs_cont_pen

if A < 0.2 then A = 0.2
if B < 0.2 then B = 0.2

! Minimal Space

call "fa_minimalspace" parameters all MSType = 0, unID = 3000,
									returned_parameters reqBMinSpace,
														MSFillType,
														MSFillPen,
														MSFillBackPen,
														MSContLineType,
														MSContLinePen

if reqBMinSpace <> 0 then
	unID = 3000
	fill MSFillType
	pen MSContLinePen
	line_type MSContLineType

	if gs_tabletop_style_m = 4 or gs_tabletop_style_m = 5 then
		AA = A/2+MSFront
		BB = B/2+MSFront

		add2 A/2, B/2
		hotspot2 -AA,	 0,	unID : unID = unID + 1
		hotspot2  AA,	 0,	unID : unID = unID + 1
		hotspot2  0,	 BB,	unID : unID = unID + 1
		hotspot2  0,	-BB,	unID : unID = unID + 1
		del 1

		if gs_tabletop_style_m = 4 then		! Ellipse
			add2 A/2, B/2
			mul2 1, BB/AA
			poly2_B 2, 1+2+4+64,MSFillPen,MSFillBackPen,
				0,	0,		901,
				AA,	360,	4001
			del 2
		endif

		if gs_tabletop_style_m = 5 then		! Oval
			add2 A/2, B/2
			if abs(A - B) < EPS then
				poly2_b 2, 1+2+4+64, MSFillPen,MSFillBackPen,
					0,0,901,
					AA,360,4001
			else
				if A < B then
					poly2_b 5, 1+2+4+64, MSFillPen,MSFillBackPen,
						-AA, -BB + AA, 1,
						-AA,  BB - AA, 1,
						 AA,  BB - AA, 1001,
						 AA, -BB + AA, 1,
						-AA, -BB + AA, 1001
					hotspot2 -AA,-BB + AA,	unID : unID = unID + 1
					hotspot2  AA,-BB + AA,	unID : unID = unID + 1
					hotspot2 -AA, BB - AA,	unID : unID = unID + 1
					hotspot2  AA, BB - AA,	unID : unID = unID + 1
				else
					poly2_b 5, 1+2+4+64, MSFillPen,MSFillBackPen,
						-AA + BB, -BB, 1,
						 AA - BB, -BB, 1,
						 AA - BB,  BB, 1001,
						-AA + BB,  BB, 1,
						-AA + BB, -BB, 1001
					hotspot2 -AA + BB,-BB,	unID : unID = unID + 1
					hotspot2  AA - BB,-BB,	unID : unID = unID + 1
					hotspot2 -AA + BB, BB,	unID : unID = unID + 1
					hotspot2  AA - BB, BB,	unID : unID = unID + 1
				endif
			endif
			del 1
		endif
	else
		tempA = A
		tempB = B

		if gs_tabletop_style_m = 1 or gs_tabletop_style_m = 2 or gs_tabletop_style_m = 3 then
			gosub "side1"
			gosub "front"
			gosub "rear"
		endif

		if gs_tabletop_style_m <> 7 then
						gosub "side2"
		endif

		if gs_tabletop_style_m = 2 then
			status1 = not(MSSide2 > EPS)
			status2 = not(MSFront > EPS)

			if MSSide2 > EPS or MSFront > EPS then
				gosub "cornerPoly1"
			endif

			if MSSide2 > EPS or MSRear > EPS then
				add2 0, B
				mul2 1, -1
				status2 = not(MSRear > EPS)
				gosub "cornerPoly1"
				del 2
			endif
		endif

		if gs_tabletop_style_m = 2 or gs_tabletop_style_m = 3 then
			status1 = not(MSSide > EPS)
			if MSSide > EPS or MSRear > EPS then
				add2 A, B
				mul2 -1, -1
				status2 = not(MSRear > EPS)
				gosub "cornerPoly1"

			endif
			del 2

			if MSSide > EPS or MSFront > EPS then
				add2 A, 0
				mul2 -1, 1
				status2 = not(MSFront > EPS)
				gosub "cornerPoly1"
				del 2
			endif
		endif

		if gs_tabletop_style_m = 6 then
			if MSFront > EPS then
				put A-B/2, -MSFront, 1,
					0, -MSFront, 1,
					0, 0, 0

				hotspot2  A-B/2, -MSFront,	unID : unID = unID + 1
				hotspot2  0, -MSFront,	unID : unID = unID + 1

				if MSSide < EPS then
					put A-B/2, 0, 1,
						A-B/2, -MSFront, -1

					poly2_b nsp/3, 1+2+4+64, MSFillPen,MSFillBackPen,
							get (nsp)

				endif
			endif
			if MSSide > EPS then
				put A-B/2, 0, 0,
					A-B/2, B/2, 900,
					A-B/2, B, 3001
			endif

			if MSRear > EPS then
				put A-B/2, B, 0,
					0, B, 1,
					0, B+MSRear, 1,
					A-B/2, B+MSrear, 1

				hotspot2  0, B+MSRear,	unID : unID = unID + 1
				hotspot2  A-B/2, B+MSrear,	unID : unID = unID + 1

				if MSSide < EPS  then
					put A-B/2, B, -1
					poly2_b nsp/3, 1+2+4+64, MSFillPen,MSFillBackPen,
							get (nsp)
				endif
			endif

			if MSSide > EPS then
				put A-B/2, B+MSSide, 1,
					A-B/2, B/2, 900,
					A-B/2, -MSSide, 3001

				hotspot2  A-B/2, B+MSSide,	unID : unID = unID + 1
				hotspot2  A-B/2, -MSSide,	unID : unID = unID + 1

				if MSFront < EPS then
					put A-B/2, 0, -1
				else
					put A-B/2, -MSFront, -1
				endif
				poly2_b nsp/3, 1+2+4+64, MSFillPen,MSFillBackPen,
						get (nsp)
			endif
		endif

		if gs_tabletop_style_m = 7 or gs_tabletop_style_m = 8 then
			beta = asn(B/2/gs_radius_2)

			cx = gs_radius_2 * cos(beta)
			cy = gs_radius_2 * sin(beta)

			gosub "curvedEndPoly"
			if 	gs_tabletop_style_m = 7 then
				mul2 -1, 1
				add2 -A, 0
				MSSide = MSSide2
				gosub "curvedEndPoly"
				del 2
				add2 gs_radius_2-cx, 0
				tempA = A - 2*(gs_radius_2-cx)
				tempB = B
				gosub "front"
				gosub "rear"
				del 1
			endif
		endif

		if gs_tabletop_style_m = 8 then
				tempA = A -(gs_radius_2-cx)
				tempB = B
				gosub "front"
				gosub "rear"
		endif
	endif
endif

call "Resize_A_B_ZZYZX" Parameters ALL

unID = 1
hotspot2 0,B/2,	unID : unID = unID + 1
hotspot2 A,B/2,	unID : unID = unID + 1
hotspot2 A/2,0,	unID : unID = unID + 1
hotspot2 A/2,B,	unID : unID = unID + 1
hotspot2 A/2,B/2,	unID : unID = unID + 1

line_type 1

call "fa_tabletop_macro" parameters A=A, B=B,
	gs_tabletop_style_m	= gs_tabletop_style_m,
	gs_radius_1			= gs_radius_1,
	gs_radius_2			= gs_radius_2,
	gs_cont_pen			= gs_cont_pen,
	gs_fill_type		= gs_fill_type,
	gs_fill_pen			= gs_fill_pen,
	gs_back_pen			= gs_back_pen,
	unID				= unID

end

"front":
	if MSFront > EPS then
		poly2_B 5, 1+2+4+64,MSFillPen,MSFillBackPen,
				0, 0, 1,
				0, -MSFront, 1,
				tempA, -MSFront, 1,
				tempA, 0, 0,
				0, 0, -1
	endif

	hotspot2 0, -MSFront, unID : unID = unID + 1
	hotspot2 tempA, -MSFront, unID : unID = unID + 1

return

"side1":
	if MSSide > EPS then
		poly2_B 5, 1+2+4+64,MSFillPen,MSFillBackPen,
				tempA, 0, 1,
				tempA+MSSide, 0, 1,
				tempA+MSSide, tempB, 1,
				tempA, tempB, 0,
				tempA, 0, -1
	endif

	hotspot2 tempA+MSSide, 0, unID : unID = unID + 1
	hotspot2 tempA+MSSide, tempB, unID : unID = unID + 1

return

"side2":
	if MSSide2 > EPS then
		poly2_B 5, 1+2+4+64,MSFillPen,MSFillBackPen,
				0, 0, 1,
				-MSSide2, 0, 1,
				-MSSide2, tempB, 1,
				0, tempB, 0,
				0, 0, -1
	endif

	hotspot2 -MSSide2, 0, unID : unID = unID + 1
	hotspot2 -MSSide2, tempB, unID : unID = unID + 1

return

"rear":
	if MSRear > EPS then
		poly2_B 5, 1+2+4+64,MSFillPen,MSFillBackPen,
				0, tempB, 1,
				0, tempB+MSRear, 1,
				tempA, B+MSRear, 1,
				tempA, tempB, 0,
				0, tempB, -1
	endif

	hotspot2 0, tempB+MSRear, unID : unID = unID + 1
	hotspot2 tempA, tempB+MSRear, unID : unID = unID + 1

return

"cornerPoly1":
	poly2_b 6, 1+2+4+64, MSFillPen,MSFillBackPen,
		0, 0, status1,
		0, gs_radius_1, 0,
		0, -1, 800,
		gs_radius_1, 0, 1000+status2,
		0, 0, 0,
		0, 0, -1
return


"curvedEndPoly":
	if MSSide > EPS then
		poly2_b 9, 1+2+4+64, MSFillPen,MSFillBackPen,
				A - gs_radius_2 + cx , 0, 0,
				A - gs_radius_2, B/2 , 900,
				A, B/2, 3000,
				A - gs_radius_2 + cx, B, 3001,
				A - gs_radius_2 + cx + MSSide*cos(beta), B+MSSide*sin(beta), 1,
				A - gs_radius_2, B/2 , 900,
				A+MSSide, B/2, 3001,
				A - gs_radius_2 + cx +MSSide*cos(beta), -MSSide*sin(beta),3001,
				A - gs_radius_2 + cx , 0, -1

		hotspot2  A - gs_radius_2 + cx + MSSide*cos(beta), B+MSSide*sin(beta),	unID : unID = unID + 1
		hotspot2  A - gs_radius_2 + cx +MSSide*cos(beta), -MSSide*sin(beta),	unID : unID = unID + 1
	endif
return
