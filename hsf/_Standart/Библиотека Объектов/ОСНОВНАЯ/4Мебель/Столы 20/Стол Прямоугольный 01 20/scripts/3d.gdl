
IF gs_shadow=0 THEN SHADOW OFF

pen gs_cont_pen

if gs_detlevel_3D_m = DETLEV_OFF then end
if gs_detlevel_3D_m = DETLEV_SIMPLE then gs_resol=4

if not(drawer) then
	heightDrawer = 0
endif

! ==============================================================================
! Legs
! ==============================================================================
RESOL gs_resol
material gs_leg_mat
lr=0
la=0
if iStyleLeg = LEG_ROUND or iStyleLeg = LEG_BALUSTER or iStyleLeg = LEG_DESIGN then lr=widthLeg/2
if iStyleLeg = LEG_BLOCK then la=widthLeg
if iStyleLeg = LEG_CONIC then lr=widthLeg/2
if iStyleLeg = LEG_RUSTIC then la=widthLeg

if shelf then
	add widthOverhang+widthLeg/2,widthOverhang+depthLeg/2,heightShelf

	cutform 5, 1, 1,
		0, 0, 1, thickShelf,
		0, 0, 15,
		a-2*(widthOverhang+widthLeg/2), 0, 15,
		a-2*(widthOverhang+widthLeg/2), b-2*(widthOverhang+widthLeg/2), 15,
		0,  b-2*(widthOverhang+widthLeg/2), 15,
		0, 0, 15
	del 1
endif

if iStyleLeg <> LEG_RUSTIC then
	add widthOverhang+lr, widthOverhang+lr, 0
	for x=1 to 2
		for y=1 to 2
			if iStyleLeg = LEG_ROUND then cylind zzyzx-thickTabletop, lr
			if iStyleLeg = LEG_CONIC then
				roty -90
				revolve 3,360,1+2+16+32,
						0,lr/2,1,
						(zzyzx-thickTabletop)/2,lr,1,
						zzyzx-thickTabletop,lr,1
				del 1
			endif
			if iStyleLeg = LEG_BLOCK then block la, depthLeg, zzyzx-thickTabletop
			if iStyleLeg = LEG_BALUSTER then
				roty -90
				eh=0.05*(zzyzx-thickTabletop-0.07)
				revolve 22, 360, 3,
						0, 0.02, 0,
						2*eh, 0.02, 0,
						3*eh, 0.023, 1,
						4*eh, 0.028, 1,
						5*eh, 0.031, 1,
						6*eh, 0.033, 1,
						7*eh, 0.033, 1,
						8*eh, 0.03, 1,
						9*eh, 0.026, 1,
						10*eh, 0.018, 0,
						10.5*eh, 0.0235, 1,
						11*eh, 0.026, 1,
						11.5*eh, 0.0235, 1,
						12*eh, 0.018, 0,
						13*eh, 0.026, 1,
						14*eh, 0.03, 1,
						15*eh, 0.033, 1,
						16*eh, 0.033, 1,
						17*eh, 0.031, 1,
						18*eh, 0.028, 1,
						19*eh, 0.023, 1,
						20*eh, 0.02, 0
				del 1
			endif
			if iStyleLeg = LEG_DESIGN then
				eh=0.05*(zzyzx-thickTabletop-0.07)
				if x=2 then rotz 180
				sweep   4, 20, 0, 1.025, 1+2+4+16+32,
						-lr/2, -lr/2, 0,
						lr/2, -lr/2, 0,
						lr/2, lr/2, 0,
						-lr/2, lr/2, 0,

						0, 0.00, -20*eh,
						0, 0.00, -19*eh,
						0, 0.00, -18*eh,
						0, 0.005, -17*eh,
						0, 0.009, -16*eh,
						0, 0.013, -15*eh,
						0, 0.015, -14*eh,
						0, 0.013, -13*eh,
						0, 0.009, -12*eh,
						0, 0.000, -11*eh,
						0, -0.009, -10*eh,
						0, -0.018, -9*eh,
						0, -0.023, -8*eh,
						0, -0.025, -7*eh,
						0, -0.023, -6*eh,
						0, -0.018, -5*eh,
						0, -0.009, -4*eh,
						0, -0.005, -3*eh,
						0, 0.000, -2*eh,
						0, 0, 0
				if x=2 then del 1
			endif
			if iStyleLeg = LEG_BALUSTER or iStyleLeg = LEG_DESIGN then
				add -lr, -lr, (zzyzx-thickTabletop-0.07)
				block 2*lr, 2*lr, 0.07
				del 1
			endif
			addx a-2*(widthOverhang+lr)-la
		next y
		del 2
		addy b-2*(widthOverhang+lr)-la
	next x
	del 3
endif

if iStyleLeg = LEG_RUSTIC then
	dy=zzyzx-thickTabletop-0.03
	eh=0.05
	eb=0.05
	if a>=b then
		dx=b-2*widthOverhang
		add widthOverhang, widthOverhang, 0.03
		rotz 90
		rotx 90
		mulx dx
		muly dy
		dt=(dx+2*0.03)/dx
		gosub 20
		addz a-2*widthOverhang-la
		gosub 20
		del 6
	else
		dx=a-2*widthOverhang
		add widthOverhang, widthOverhang+la, 0.03
		rotx 90
		mulx dx
		muly dy
		dt=(dx+2*0.03)/dx
		gosub 20
		addz -(b-2*widthOverhang-la)
		gosub 20
		del 5
	endif
endif

vert 0,0,0
vert zzyzx,0,0
vert 0,0.1,0
vert 0,0,0.1
coor 2,-1,-4,-3,-2
base

if iStyleLeg = LEG_SOLID then
	add widthOverhang,b/2-depthLeg/2,0
	block widthLeg, depthLeg, zzyzx-thickTabletop
	del 1
	add a-widthOverhang-widthLeg,b/2-depthLeg/2,0
	block widthLeg, depthLeg, zzyzx-thickTabletop
	del 1
endif

if iStyleLeg = LEG_FOLDING then
	rotx 180
	cutplane
	del 1

	legHeight = zzyzx-thickTabletop
	if drawer then
		legHeight = legHeight-heightDrawer
	endif
	addz legHeight
	alfa = 90-ATN (legHeight/(2/3*b-widthOverhang))
	cutplane
	del 1

	_legDepthOffs = depthLeg * TAN(alfa)
	addy b/2
	for i = 1 to 2
		addx widthOverhang
		for j = 1 to 2
			addy b/6
			rotx alfa
			addz -_legDepthOffs

			block widthLeg, depthLeg, zzyzx/COS(alfa) + _legDepthOffs

			del 3
			muly -1
		next j
		del 3
		addx a
		mulx -1
	next i
	del 5

	cutend
	cutend
endif

if shelf then cutend

! ======================================================================
! Shelf
! ======================================================================

if shelf then
	material smat
	if iStyleLeg = LEG_SOLID then
		add widthOverhang+widthLeg,b/2-depthLeg/2,heightShelf
		block a-2*(widthOverhang+widthLeg),depthLeg,thickShelf
		del 1
	else
		add widthOverhang+widthLeg/2,widthOverhang+widthLeg/2,heightShelf
		block a-2*(widthOverhang+widthLeg/2),b-2*(widthOverhang+widthLeg/2),thickShelf
		del 1
	endif
endif

! ======================================================================
! Drawer
! ======================================================================

if drawer then
	material dmat
	add widthOverhang,widthOverhang,zzyzx-thickTabletop-heightDrawer

	block a-2*(widthOverhang),b-2*(widthOverhang),.02
	del 1

	heightBlock=zzyzx-thickTabletop-heightDrawer+.02
	!la=.02
	thickBlock=heightDrawer
	gosub 10
	add a, b, 0
	rotz 180
	gosub 10
	del 2

	add widthOverhang+widthLeg+.005,widthOverhang,zzyzx-thickTabletop-heightDrawer+.02
	valt= (a-2*(widthOverhang+widthLeg)-((numDrawer+1)*.005))/numDrawer
	FOR i=1 TO numDrawer
		block valt,.02,heightDrawer
		ADDX valt+.005
	NEXT i
	DEL numDrawer

	del 1

	if gs_knob_type_m<>0 then
		material gs_knob_mat

		add widthOverhang+widthLeg+.005+valt/2,widthOverhang,zzyzx-thickTabletop-heightDrawer/2+.02
		if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 then
			FOR i=1 TO numDrawer
				CALL "fa_knobtyp" PARAMETERS all
				ADDX valt+.005
			NEXT i
			DEL numDrawer
		endif

		if gs_knob_type_m=4 then
			addx .05
			roty 90
			FOR i=1 TO numDrawer
				CALL "fa_knobtyp" PARAMETERS all
				ADDz valt+.005
			NEXT i
			DEL numDrawer
			del 2
		endif

		if gs_knob_type_m=5 or gs_knob_type_m=6 or gs_knob_type_m=7 or gs_knob_type_m=8 then
			addx -.0635
			roty -90
			FOR i=1 TO numDrawer
				CALL "fa_knobtyp" PARAMETERS all
				ADDz -valt-.005
			NEXT i
			DEL numDrawer
			del 2
		endif
		del 1
	endif
endif


! ==============================================================================
! Table
! ==============================================================================

material gs_top_mat
add widthTabletopFrame, widthTabletopFrame, zzyzx-thickTabletop

if widthTabletopFrame>0 then
	block a-2*widthTabletopFrame, b-2*widthTabletopFrame, thickTabletop
else
	block a, b, thickTabletop
endif


if a>b then gosub 5000
if a<b then gosub 1000


if widthTabletopFrame>0 then
	material gs_topedge_mat
	add -widthTabletopFrame, -widthTabletopFrame, 0
	prism_  10, thickTabletopFrame,
			0, 0, 15,
			a, 0, 15,
			a, b, 15,
			0, b, 15,
			0, 0, -1,
			widthTabletopFrame, widthTabletopFrame, 15,
			a-widthTabletopFrame, widthTabletopFrame, 15,
			a-widthTabletopFrame, b-widthTabletopFrame, 15,
			widthTabletopFrame, b-widthTabletopFrame, 15,
			widthTabletopFrame, widthTabletopFrame, -1
	del 1
endif
del 1

vert 0,0,0
vert a,0,0
vert 0,b,0
vert 0,0,thickTabletop
coor 1,-1,-2,-3,-4
base
body -1

! ==============================================================================
! Frame
! ==============================================================================

material fmat
if iStyleLeg = LEG_RUSTIC then frame=1
if frame=0 or ABS(thickFrame)<EPS then end
if iStyleLeg = LEG_DESIGN or iStyleLeg = LEG_BALUSTER then iPositionFrame = FRAMEPOS_UPPER

if iPositionFrame = FRAMEPOS_UPPER then
	heightBlock = zzyzx-thickTabletop-thickFrame		! Rectangular Frame
	heightFrame = zzyzx-thickTabletop-thickFrame/2		! Round Frame
else
	heightBlock = heightFrame							! Rectangular Frame
	heightFrame = heightFrame + thickFrame/2			! Round Frame
endif
thickBlock = thickFrame

if iStyleLeg <> LEG_RUSTIC then
	if iFrameSection = FRAMESECT_RECT then
		gosub 10
		add a, b, 0
		rotz 180
		gosub 10
		del 2
	else
		gosub 11
		add a, b, 0
		rotz 180
		gosub 11
		del 2
	endif
endif

if iStyleLeg = LEG_RUSTIC then
	add a/2, b/2, 0.03+dy/2-thickFrame/2
	if a>=b then heightFrameo=a-2*widthOverhang+2*0.07 else heightFrameo=b-2*widthOverhang+2*0.07
	if b>a then rotz 90
	prism_  15, thickFrame,
			-heightFrameo/2, -eb/2.5*dx, 15,
			heightFrameo/2, -eb/2.5*dx, 15,
			heightFrameo/2, eb/2.5*dx, 15,
			-heightFrameo/2, eb/2.5*dx, 15,
			-heightFrameo/2, -eb/2.5*dx, -1,
			-heightFrameo/2+0.04, -eb/5*dx, 15,
			-heightFrameo/2+0.07, -eb/5*dx, 15,
			-heightFrameo/2+0.07, eb/5*dx, 15,
			-heightFrameo/2+0.04, eb/5*dx, 15,
			-heightFrameo/2+0.04, -eb/5*dx, -1,
			heightFrameo/2-0.07, -eb/5*dx, 15,
			heightFrameo/2-0.04, -eb/5*dx, 15,
			heightFrameo/2-0.04, eb/5*dx, 15,
			heightFrameo/2-0.07, eb/5*dx, 15,
			heightFrameo/2-0.07, -eb/5*dx, -1
	addz -0.03
	prism_  4, 2*0.03+thickFrame,
			-heightFrameo/2+0.04, -eb/5*dx, 15,
			-heightFrameo/2+0.07, -eb/5*dx, 15,
			-heightFrameo/2+0.07, eb/5*dx, 15,
			-heightFrameo/2+0.04, eb/5*dx, 15
	prism_  4, 2*0.03+thickFrame,
			heightFrameo/2-0.07, -eb/5*dx, 15,
			heightFrameo/2-0.04, -eb/5*dx, 15,
			heightFrameo/2-0.04, eb/5*dx, 15,
			heightFrameo/2-0.07, eb/5*dx, 15
	del 1
	if b>a then del 1
	del 1
endif

end


! ======================================================================
! Subroutines
! ======================================================================


10:

	add widthOverhang+la+2*lr, widthOverhang+la/4+lr/2, heightBlock
	block a-2*(widthOverhang+la+2*lr), la/2+lr, thickBlock
	del 1
	add widthOverhang+la/4+lr/2, widthOverhang+la+2*lr, heightBlock
	block la/2+lr, b-2*(widthOverhang+la+2*lr), thickBlock
	del 1

	return


11:

	add widthOverhang+la/2+lr/2, widthOverhang+la/4+lr, heightFrame
	rotx 90
	roty 90
	cylind a-2*(widthOverhang+la/2 + lr), thickFrame/2
	del 3
	add widthOverhang+la/4+lr, widthOverhang+la/2+lr/2, heightFrame
	rotx 90
	roty 180
	cylind b-2*(widthOverhang+la/2 + lr), thickFrame/2
	del 3

	return


20:
	prism_  25, la,
			0, 0, 79,
			0, 4*eh, 79,
			2*eh, -90, 2079,
			2*eh, 90, 2079,
			4*eb, 12*eh, 79,
			2*eh, 90, 2079,
			2*eh, -90, 2079,
			0, 20*eh, 79,
			20*eb, 20*eh, 79,
			20*eb, 16*eh, 79,
			2*eh, -90, 2079,
			2*eh, 90, 2079,
			16*eb, 8*eh, 79,
			2*eh, 90, 2079,
			2*eh, -90, 2079,
			20*eb, 0, 79,
			12*eb, 0, 79,
			10*eb, 0, 979,
			2*eb, 180, 4079,
			0, 0, -1,
			10*eb-eb/2.5, 10*eh-thickFrame/2/dy, 79,
			10*eb+eb/2.5, 10*eh-thickFrame/2/dy, 79,
			10*eb+eb/2.5, 10*eh+thickFrame/2/dy, 79,
			10*eb-eb/2.5, 10*eh+thickFrame/2/dy, 79,
			10*eb-eb/2.5, 10*eh-thickFrame/2/dy, -1
	add -0.03/dx, -0.03/dy, -0.01
	prism_  7, 0.02+widthLeg,
			0, 0, 15,
			dt, 0, 15,
			dt, 0.02/dy, 15,
			dt-0.01/dx, 0.03/dy, 15,
			0.01/dx, 0.03/dy, 15,
			0, 0.02/dy, 15,
			0, 0, -1
	del 1

	return


1000:
	roty 90
	gosub 5000
	del 1

	return


5000:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base
	body -1

	return

