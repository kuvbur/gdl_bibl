

IF not(gs_shadow) THEN SHADOW OFF


if gs_detlevel_3D_m = 0 then end
if gs_detlevel_3D_m = 1 then gs_resol=6

pen gs_cont_pen
! -- BOTTOMS ---
resol gs_resol

! --- LEGS ---
resol 1.5*gs_resol
material gs_leg_mat
add .1,.1,0

for I=1 to 2
	cylind zzyzx-tbtt,ls
	addx a-.2
next I
del top

add .1,b-.1,0

for I=1 to 2
	cylind zzyzx-tbtt,ls
	addx a-.2
next I
del top

add a/2,b/2,0
for i=1 to 4
	add -a/2+0.1, -b/2+0.1, 0
	cutpolya 2, 2, 0,
		0,0,979,
		ls,360,4079
	del 1
	rotz 90
next i
del 4

vertLength = SQR((a/2-0.1)^2+(b/2-0.1)^2)
length = SQR((vertLength-ls/3)^2+(zzyzx-0.48-tbtt)^2)
alfa = acs((zzyzx-0.48-tbtt)/length)
beta = atn((a/2-0.1)/(b/2-0.1))
rotz beta

for i=1 to 4
	add 0,-vertLength,.48
	rotx -alfa
	cylind length,ls/3
	del 2
	if i%2 = 1 then
		rotz 2*(90-beta)
	else
		rotz 2*beta
	endif
next i
del 4

del 2

for i=1 to 4
	cutend
next i


! --- TABLE TOP ---

addz zzyzx-2"

material gs_top_mat
resol 2*gs_resol

if stt=0 then
	d = 0.1
	valt = int(B/d)
	d = B/valt
	addy d
	for i=1 to valt-1
		cutpolya 4, 2, 0,
			0,0,15,
			a,0,15,
			a,.01,15,
			0,.01,15
		addy d
	next i
	del i
endif

del 1
addz zzyzx-tbtt
block a,b,tbtt

if stt=0 then
	for i=1 to valt-1
		cutend
	next i
endif

del top

end
