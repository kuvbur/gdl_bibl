
call "Resize_A_B_ZZYZX" PARAMETERS ALL

IF gs_shadow=0 THEN SHADOW OFF

pen gs_cont_pen


if gs_detlevel_3D_m = DLEV3D_OFF then end
if gs_detlevel_3D_m = DLEV3D_SIMPLE then gs_resol = 4

unID = 1
hotspot a/2, b/2, 0, 		unID : unID = unID + 1
hotspot a/2, b/2, zzyzx,	unID : unID = unID + 1

! ==============================================================================
! legs
! ==============================================================================
RESOL gs_resol
material gs_leg_mat

add ohTabletop, ohTabletop, 0
for x=1 to 2
	for y=1 to 2
		add 0, wLeg, zzyzx-thTabletop
		rotx 180
		nCuts = 0
		if gs_detlevel_3D_m = DLEV3D_DETAILED then
			add wLeg/2, wLeg/2, 0
			for iTrans = 1 to 4
				rotz 90
				add -wLeg/2, -wLeg/2, 0
				cutplane 90-atn(wLeg*0.15 / ((zzyzx-thTabletop-thShelf/2)*0.6))
				del 1
			next iTrans
			nCuts = 4
			del 1+4
		endif
		block wLeg, wLeg, zzyzx-thTabletop
		for iTrans = 1 to nCuts
			cutend
		next iTrans
		del 2
		addx a-2*(ohTabletop+wLeg/2)
	next y
	del 2
	addy b-2*(ohTabletop+wLeg/2)
next x
del 3

vert 0,0,0
vert zzyzx,0,0
vert 0,0.1,0
vert 0,0,0.1
coor 2,-1,-4,-3,-2
base


! ==============================================================================
! table
! ==============================================================================

material gs_top_mat

add 0, 0, zzyzx-thTabletop

block a, b, thTabletop

if a>b then gosub 5000
if a<b then gosub 1000

del 1

vert 0,0,0
vert a,0,0
vert 0,b,0
vert 0,0,thTabletop
coor 1,-1,-2,-3,-4
base
body -1

! ==============================================================================
! frame+drawer
! ==============================================================================
material fmat

CornerSize = wLeg-wLeg*0.15*(zzyzx-thTabletop-hFrame-thTabletop)/((zzyzx-thTabletop)*0.6)+ 0.005

add ohTabletop+2*CornerSize/2+.001, ohTabletop+CornerSize/2, zzyzx-hFrame-thTabletop
block a-2*(ohTabletop+CornerSize)-.002, .01+.002, hFrame
del 1
add ohTabletop+CornerSize/2, ohTabletop+2*CornerSize/2, zzyzx-hFrame-thTabletop
block .01+.002, b-2*(ohTabletop+CornerSize), hFrame
del 1
add a, b, 0
rotz 180
add ohTabletop+2*CornerSize/2, ohTabletop+CornerSize/2, zzyzx-hFrame-thTabletop
block a-2*(ohTabletop+CornerSize), .01+.002, hFrame
del 1
add ohTabletop+CornerSize/2, ohTabletop+CornerSize/2, zzyzx-hFrame-thTabletop
block .01+.002, b-2*(ohTabletop+CornerSize/2), hFrame
del 1
del 2

if bDrawer then
	add ohTabletop+CornerSize,ohTabletop+CornerSize/2,zzyzx-thTabletop-hFrame-thTabletop
	prism_  13, thTabletop,
			0, 0, 15,
			a-2*(ohTabletop+CornerSize), 0, 15,
			a-2*(ohTabletop+CornerSize), CornerSize/2, 15,
			a-2*ohTabletop-CornerSize-CornerSize/2, CornerSize/2, 15,
			a-2*ohTabletop-CornerSize-CornerSize/2, b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			a-2*(ohTabletop+CornerSize), b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			a-2*(ohTabletop+CornerSize), b-2*ohTabletop-CornerSize, 15,
			0, b-2*ohTabletop-CornerSize, 15,
			0, b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			-CornerSize/2, b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			-CornerSize/2, CornerSize/2, 15,
			0, CornerSize/2, 15,
			0, 0, -1
	del 1

	material kmat

	add a/2-.005,ohTabletop+wLeg/2,zzyzx-hFrame/2
	rotx 90

	cylind .03,.01
	del 2
endif

! ==============================================================================
! shelf
! ==============================================================================
material smat
CornerSize = wLeg*0.85
add ohTabletop+CornerSize,ohTabletop+CornerSize/2,(zzyzx-thTabletop)*0.4
if bShelf and thShelf > EPS then
	prism_  13, thShelf,
			0, 0, 15,
			a-2*(ohTabletop+CornerSize), 0, 15,
			a-2*(ohTabletop+CornerSize), CornerSize/2, 15,
			a-2*ohTabletop-CornerSize-CornerSize/2, CornerSize/2, 15,
			a-2*ohTabletop-CornerSize-CornerSize/2, b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			a-2*(ohTabletop+CornerSize), b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			a-2*(ohTabletop+CornerSize), b-2*ohTabletop-CornerSize, 15,
			0, b-2*ohTabletop-CornerSize, 15,
			0, b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			-CornerSize/2, b-2*ohTabletop-CornerSize-CornerSize/2, 15,
			-CornerSize/2, CornerSize/2, 15,
			0, CornerSize/2, 15,
			0, 0, -1
endif
del 1

end

! ==============================================================================


1000:
	roty 90
	gosub 5000
	del 1
	return

5000:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

	return
