
IF gs_detlevel_3D_m=0 THEN END

RESOL gs_resol
PEN gs_cont_pen

IF gs_shadow=0 THEN SHADOW OFF

IF GLOB_CONTEXT>20 AND GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1
ADD a/2,b/2,0
MATERIAL gs_frame_mat

!!! *** LEGS

MULX a/(2*0.269875)
MULY b/(0.031694+0.489171)
ADDY -0.489171/2+0.0317/2

material gs_frame_mat

z=gs_seat_height-0.1

FOR n= 1 to 2
	ADD -0.27, 0, 0
	IF gs_detlevel_3D_m=1 THEN
		BLOCK 0.03, 0.03, z
	ELSE
		RULED 5, 63,
			0, 0, 0,
			0.02, 0, 0,
			0.02, 0.02, 0,
			0, 0.02, 0,
			0, 0, 0,

			0, 0, z,
			0.04, 0, z,
			0.04, 0.04, z,
			0,0.04, z,
			0, 0, z
	ENDIF
	ADDY 0.4
	MULY -1
	IF gs_detlevel_3D_m=1 THEN
		BLOCK 0.03, 0.03, z
	ELSE
		RULED 5, 63,
			0, 0, 0,
			0.02, 0, 0,
			0.02, 0.02, 0,
			0, 0.02, 0,
			0, 0, 0,

			0, 0, z,
			0.04, 0, z,
			0.04, 0.04, z,
			0,0.04, z,
			0, 0, z
	ENDIF
	DEL 3
	MULX -1
NEXT n
DEL 2
GOSUB "close_body_horizontal"

ADDZ gs_seat_height-0.1

ADD -0.54/2, 0, 0
BLOCK 0.54, 0.4, 0.03
DEL 1

GOSUB "close_body_vertical"

ADDZ 0.03
MATERIAL gs_seat_mat
ADDX 0.54/2-0.05
ROTZ 90
ROTX 90

if gs_detlevel_3d_m=1 then
	BLOCK 0.4, 0.22, 0.05
else
	CALL "fa_cushion_backrest" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
			cush_a=0.4,cush_b=0.22,cush_h=0.05,r2=0.006,r1=0.002,r3=0.05,
			FilletResol=gs_resol/2,gs_resol=gs_resol/2,cush_mat=gs_seat_mat
endif
DEL 2

ADDx -0.54+0.05
ROTz 90
ROTX 90

IF gs_detlevel_3D_m=1 THEN
	BLOCK 0.4, 0.22, 0.05
ELSE
	CALL "fa_cushion_backrest" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
	cush_a=0.4,cush_b=0.22,cush_h=0.05,r2=0.006,r1=0.002,r3=0.05,
	FilletResol=gs_resol/2,gs_resol=gs_resol/2,cush_mat=gs_seat_mat
ENDIF
DEL 4

! SEAT CUSHION

ADD -0.54/2+0.05, -0.0317, 0

IF gs_detlevel_3D_m=1 THEN
	BLOCK 0.44, 0.4, CushThk
ELSE
	CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
	cush_a=0.54-0.1,cush_b=0.4,cush_h=CushThk,r2=RadCushEdge,r1=RadCushCorner,
	FilletResol=gs_resol/2,gs_resol=gs_resol/2,cush_mat=gs_seat_mat
ENDIF

DEL 1

! BACK

alfa=atn((ZZYZX-gs_seat_height+0.07)/0.089)

ADDX -0.54/2+0.05
ADDY 0.4
ROTX -(90-alfa)
ADDY 0
ROTX 90

IF gs_detlevel_3D_m=1 THEN
	BLOCK 0.44, ((ZZYZX-gs_seat_height+0.07)/sin(alfa)), CushThk
ELSE
	CALL "fa_cushion_backrest" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
	cush_a=0.54-0.1,cush_b=((ZZYZX-gs_seat_height+0.07)/sin(alfa)),cush_h=CushThk,r2=RadCushEdge,r1=RadCushCorner,r3=gs_arc,
	FilletResol=gs_resol/2,gs_resol=gs_resol,cush_mat=gs_seat_mat,ArcResol=gs_resol
ENDIF

DEL TOP

! Detailed Model
	unID=1
ADD a/2,b/2,0

	HOTSPOT    -a/2, -b/2, 0, unID: unID=unID+1
	HOTSPOT    a/2, -b/2, 0, unID: unID=unID+1
	HOTSPOT    a/2, b/2, 0, unID: unID=unID+1
	HOTSPOT    -a/2, b/2, 0, unID: unID=unID+1

	!!Backrest
	hotspot 0, b/2, 0, unID, ZZYZX, 1+128: unID=unID+1
	hotspot 0, b/2, -1, unID, ZZYZX, 3: unID=unID+1
	hotspot 0, b/2, ZZYZX, unID, ZZYZX, 2: unID=unID+1


MULX a/(2*0.269875)
MULY b/(0.031694+0.489171)

ADDY -0.489171/2+0.0317/2


! Hotspot editing

	!Seating
	hotspot -0.22, 0, 0, unID, gs_seat_height, 1+128: unID=unID+1
	hotspot -0.22, 0, -1, unID, gs_seat_height, 3: unID=unID+1
	hotspot -0.22, 0, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

	!Seating
	hotspot 0.22, 0, 0, unID, gs_seat_height, 1+128: unID=unID+1
	hotspot 0.22, 0, -1, unID, gs_seat_height, 3: unID=unID+1
	hotspot 0.22, 0, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1


DEL TOP

END

"close_body_horizontal":
roty 90
gosub "close_body"
del 1
return

"close_body_vertical":
gosub "close_body"
return


"close_body":

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return
