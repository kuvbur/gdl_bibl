
UI_dialog `Параметры Стула`

ui_current_page gs_ui_current_page

dim tabTitles[5]
	tabTitles[1] = `Размеры`
	tabTitles[2] = `2D-отображение`
	tabTitles[3] = `3D-отображение и Покрытия`
	tabTitles[4] = `Минимальное Пространство`
	tabTitles[5] = `Характеристики`


dim tabIcons[5]
	tabIcons[1] = "uiTab_Dimension_flat(1)"
	tabIcons[2] = "uiTab_2DRepr_flat(1)"
	tabIcons[3] = "uiTab_3DRepr_flat(1)"
	tabIcons[4] = "uiTab_MinSpace_flat(1)"
	tabIcons[5] = "uiTab_Descriptions_flat(1)"

FILE_DEPENDENCE "uiTab_Dimension_flat(1)"
FILE_DEPENDENCE "uiTab_2DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_3DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_MinSpace_flat(1)"
FILE_DEPENDENCE "uiTab_Descriptions_flat(1)"

iPageIDRoot 			=-1
iPageIDStyle			= 1
iPageID2DRepresentation	= 2
iPageID3DRepresentation	= 3
iPageIDDimMinSpace		= 4
iPageIDDescriptions		= 5

pageStartY = 5


UI_PAGE iPageIDStyle, iPageIDRoot, tabTitles[1], tabIcons[1]
	gosub "dimensions"

UI_PAGE iPageID2DRepresentation, iPageIDRoot, tabTitles[2], tabIcons[2]
	gosub "2DPage"

UI_PAGE iPageID3DRepresentation, iPageIDRoot, tabTitles[3], tabIcons[3]
	gosub "3DPage"

UI_PAGE iPageIDDimMinSpace, iPageIDRoot, tabTitles[4], tabIcons[4]
	gosub "minSpacePage"

UI_PAGE iPageIDDescriptions, iPageIDRoot, tabTitles[5], tabIcons[5]
	gosub "descriptionPage"

! ------------------------------------------------------------------------------
end
! ------------------------------------------------------------------------------


! ------------------------------------------------------------------------------
!Subroutines
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"dimensions":
! ------------------------------------------------------------------------------
	x1=0    					! start Position X Coordinate
	y0=pageStartY+3   		! start Position Y Coordinate
	size_x=444-8				! width of 1.cell
	size_y=226					! height of 1.cell
	
	
	ui_outfield `Радиус Скругления Подушки`, 	x1, y0, 		169, 15 
	ui_infield "RadCushCorner", 170, y0 - 4, 	90, 19 
	y0 = y0 + 24

	ui_outfield `Скругление Ребер Подушки`, 	x1, y0, 		169, 15 
	ui_infield "RadCushEdge", 170, y0 - 4, 	90, 19 
	
	ui_separator 0, y0 + 21, 444, y0 + 21
	
	xxPict = 155
	yyPict = pageStartY + 77
	
	ui_pict	1, xxPict, yyPict, 131, 140, 1	!Dimensions.png

	
	ui_infield "gs_arc", xxPict - 100, yyPict + 4, 	90, 19 ui_tooltip `Радиус Дуги Спинки`
	ui_infield "CushThk", xxPict - 100, yyPict + 56, 	90, 19 ui_tooltip `Толщина Подушки`
	ui_infield "gs_seat_height", xxPict + 140, yyPict + 90, 	90, 19 ui_tooltip `Высота Сидения`

return

! ------------------------------------------------------------------------------
"2DPage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 14,
													gs_SymbolType_m		= gs_SymbolType_m,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													gs_2DSeat			= 0,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= iPageID2DRepresentation

return

! ------------------------------------------------------------------------------
"3DPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[2]    
		surfacesArray[1]	= "gs_frame_mat"
		surfacesArray[2]	= "gs_seat_mat"

	dim childFlagArray[2]
		childFlagArray[1]	= 0
		childFlagArray[2]	= 0

	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= iPageID3DRepresentation

return


! ------------------------------------------------------------------------------
"minSpacePage":
! ------------------------------------------------------------------------------

	x1 = 0
	x2 = 228
	y0 = pageStartY + 4

	pageStartY = pageStartY + 29

	call "ui_chairs_minimalspace" parameters 	MSType 	= 1, 					! MINSPACE_SIMPCHAIR
										MSSide	= MSSide,
										pageStartY = pageStartY

return


! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = iPageIDDescriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
