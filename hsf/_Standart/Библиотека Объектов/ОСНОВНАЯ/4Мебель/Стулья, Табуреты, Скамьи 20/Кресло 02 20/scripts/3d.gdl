
! Armchair 02


IF gs_detlevel_3D_m=0 THEN END

RESOL gs_resol
PEN gs_cont_pen


IF gs_shadow=0 THEN SHADOW OFF


IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

ADD a/2,0,0

unID=1
HOTSPOT -a/2, 0, 0, unID: unID=unID+1
HOTSPOT  a/2, 0, 0, unID: unID=unID+1
HOTSPOT -a/2, b, 0, unID: unID=unID+1
HOTSPOT  a/2, b, 0, unID: unID=unID+1

!!! *** HOTSPOTS in ZZYZX height

hotspot -a/2+0.03,  b-0.0394, 0, unID, zzyzx, 1+128: unID=unID+1
hotspot -a/2+0.03,  b-0.0394, -1, unID, zzyzx, 3: unID=unID+1
hotspot -a/2+0.03,  b-0.0394, zzyzx, unID, zzyzx, 2: unID=unID+1

hotspot  a/2-0.03,  b-0.0394, 0, unID, zzyzx, 1+128: unID=unID+1
hotspot  a/2-0.03,  b-0.0394, -1, unID, zzyzx, 3: unID=unID+1
hotspot  a/2-0.03,  b-0.0394, zzyzx, unID, zzyzx, 2: unID=unID+1


b=b+(gs_seat_height-0.4)*TAN(10)

!!! *** Hotspot editing

	hotspot -a/2+0.03, 0, 0, unID, gs_seat_height, 1+128: unID=unID+1
	hotspot -a/2+0.03, 0, -1, unID, gs_seat_height, 3: unID=unID+1
	hotspot -a/2+0.03, 0, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

	hotspot a/2-0.03, 0, 0, unID, gs_seat_height, 1+128: unID=unID+1
	hotspot a/2-0.03, 0, -1, unID, gs_seat_height, 3: unID=unID+1
	hotspot a/2-0.03, 0, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

ADDX -a/2
ROTZ 270

zz=(zzyzx-0.03)/(0.80-0.03)
bb=(b-0.045)/(0.60-0.045)

rr=0.015

az=zzyzx-0.25*1-0.04*SIN(10): ax=az*TAN(10)

ADD -(b-ax-0.03*TAN(10)), rr*2, 0.25*1+0.03+gs_seat_height-0.4
	CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
	cush_a=b-ax-0.03*TAN(10),
	cush_b=a-4*rr, cush_h=0.12, r1=0.02, r2=0.01, gs_resol=gs_resol, cush_mat=gs_seat_mat

DEL 1

CUTPLANE 180

ADD -(b-ax), a-rr*2, 0.25*1+gs_seat_height-0.4
ROTY -100
ROTX 180
   CALL "fa_cushion" parameters gs_detlevel_3D_m=gs_detlevel_3D_m,
	cush_a=az/COS(10)-(gs_seat_height-0.4)*COS(10),
	cush_b=a-4*rr, cush_h=0.04, r1=0.01, r2=0.01, gs_resol=6, cush_mat=gs_seat_mat
DEL 3

CUTEND


MATERIAL gs_arm_mat
RESOL 16
ADD -(b-ax), a-rr*2, 0.25*1+gs_seat_height-0.4
ROTZ 90
	CUTPLANE -100
DEL 2

ADDZ 0.25*1+gs_seat_height-0.4
if gs_detlevel_3D_m=2 then
	EXTRUDE 7, 0, 0, 0.03, 1+2+4+16+32,
		-0.03, a/2, 1,
		-0.03, rr, 1,
		-0.03-rr, 0, 1001,
		-b, 0, 1,
		-b, a, 1,
		-0.03-rr, a, 1,
		-0.03, a-rr, 1001
	gosub 1000
else
	EXTRUDE 5, 0, 0, 0.03, 1+2+4+16+32,
		-0.03, a, 0,
		-0.03, 0, 0,
		-b, 0, 0,
		-b, a, 0,
		-0.03, a, 0
	gosub 1000
endif
DEL 1

ADDZ 0.25*1+0.03+gs_seat_height-0.4
	CUTPLANE 180
DEL 1

	GOSUB 120

ADD 0, rr, gs_seat_height-0.4
	GOSUB 100
ADDY a-2*rr
	GOSUB 100
DEL 2
CUTEND
CUTEND

	GOSUB 110
ADDY a-2*rr
	GOSUB 110
DEL 1
CUTEND


!---------------- Legs ----------------
material gs_frame_mat
alpha=ATN(0.025/(1*0.25+gs_seat_height-0.4)): xx=rr/COS(alpha)-rr
IF gs_detlevel_3D_m=2 THEN
	ADD -0.02-xx, rr, 0
	ROTY -alpha
		CONE SQR((1*0.25+gs_seat_height-0.4)^2+0.025^2), rr, rr, 90+alpha, 90+alpha
	ADDY a-2*rr
		CONE SQR((1*0.25+gs_seat_height-0.4)^2+0.025^2), rr, rr, 90+alpha, 90+alpha
	DEL 3
ELSE
		GOSUB 140
	ADDY a-2*rr
		GOSUB 140
	DEL 1
ENDIF


xxx=(1*0.51+0.045-(1*0.25+gs_seat_height-0.4))*TAN(10)
alpha=ATN(xxx/(1*0.25+gs_seat_height-0.4)): xx=rr/COS(alpha)-rr
IF gs_detlevel_3D_m=2 THEN
	ADD -(b-ax)-xxx+rr+xx, rr, 0
	ROTY alpha
		CONE SQR((1*0.25+gs_seat_height-0.4)^2+xxx^2), rr, rr, 90-alpha, 90-alpha
	ADDY a-2*rr
		CONE SQR((1*0.25+gs_seat_height-0.4)^2+xxx^2), rr, rr, 90-alpha, 90-alpha
	DEL 3

ELSE
		GOSUB 150
	ADDY a-2*rr
		GOSUB 150
	DEL 1
ENDIF

IF gs_detlevel_3D_m=2 THEN
	gosub 2000
ELSE
	gosub 1000
ENDIF

DEL TOP

END


!---------------->!!! RETURN !!!<----------------

100:
ADDX -0.045
IF gs_detlevel_3D_m=2 THEN

TUBE 4, 15, 1+2+16+32,
	rr, rr, 1,
	rr, 0, 1,
	-rr, 0, 1001,
	-rr, rr, 1,

	0,  0,   -0.5,     0,
	0,  0,   0,     0,
	0,  0,   1*0.25+0.03,     0,
	-0.016703*bb,  0,   1*0.34473+0.03,     0,
	-0.019742*bb,  0,   1*0.361962+0.03,     0,
	-0.031711*bb,  0,   1*0.394847+0.03,     0,
	-0.049209*bb,  0,   1*0.425153+0.03,     0,
	-0.071703*bb,  0,   1*0.451962+0.03,     0,
	-0.098511*bb,  0,   1*0.474456+0.03,     0,
	-0.128818*bb,  0,   1*0.491954+0.03,     0,
	-0.161703*bb,  0,   1*0.503923+0.03,     0,
	-0.196167*bb,  0,   1*0.51+0.03,     0,
	-0.213665*bb,  0,   1*0.51+0.03,     0,
	-b,  0,   1*0.51+0.03,     0,
	-b*2,  0,   1*0.51+0.03,     0
ELSE

TUBE 4, 6, 1+2+16+32,
	rr, rr, 0,
	rr, -rr, 0,
	-rr, -rr, 0,
	-rr, rr, 0,

	0,  0,   -0.5,     0,
	0,  0,   0,     0,
	0,  0,   1*0.25+0.03,     0,
	-0.045845*bb,  0,   1*0.51+0.03,     0,
	-b,  0,   1*0.51+0.03,     0,
	-b*2,  0,   1*0.51+0.03,     0
ENDIF
DEL 1

RETURN

110:
GOSUB 130
ADD -0.045, 0, gs_seat_height-0.4
ROTX 90
PRISM_ 1+NSP/3, -rr*2,
	GET (NSP),
	0,     1*0.25+0.03,     -1
DEL 2
RETURN

120:
GOSUB 130
ADD -0.045, 0, gs_seat_height-0.4
ROTX 90
CUTPOLYA 1+NSP/3, 2, 0,
	GET (NSP),
	0,     1*0.25+0.03,     -1
DEL 2
RETURN

130:
IF gs_detlevel_3D_m=2 THEN
	PUT 0.0,     1*0.25+0.03,     15
	PUT -0.016703*bb,     1*0.34473+0.03,     15
	PUT -0.019742*bb,     1*0.361962+0.03,     15
	PUT -0.031711*bb,     1*0.394847+0.03,     15
	PUT -0.049209*bb,     1*0.425153+0.03,     15
	PUT -0.071703*bb,     1*0.451962+0.03,     15
	PUT -0.098511*bb,     1*0.474456+0.03,     15
	PUT -0.128818*bb,     1*0.491954+0.03,     15
	PUT -0.161703*bb,     1*0.503923+0.03,     15
	PUT -0.196167*bb,     1*0.51+0.03,     15
	PUT -0.213665*bb,     1*0.51+0.03,     15
	PUT -b,     1*0.51+0.03,     15
	PUT -b,     1*0.25+0.03,  15
ELSE

	PUT 0.0,     1*0.25+0.03,     15
	PUT -0.045845*bb,     1*0.51+0.03,     15
	PUT -b,     1*0.51+0.03,     15
	PUT -b,     1*0.25+0.03,  15
ENDIF
RETURN

140:
ROTX 90
PRISM_ 5, -2*rr,
	-0.02+rr+xx, 0, 15,
	-0.045+rr+xx, 1*0.25+gs_seat_height-0.4, 15,
	-0.045-rr-xx, 1*0.25+gs_seat_height-0.4, 15,
	-0.02-rr-xx, 0, 15,
	-0.02+rr+xx, 0, -1
DEl 1
RETURN

150:
ROTX 90
PRISM_ 5, -2*rr,
	-(b-ax)-xxx+(rr+xx)*2, 0, 15,
	-(b-ax)+(rr+xx)*2, 1*0.25+gs_seat_height-0.4, 15,
	-(b-ax), 1*0.25+gs_seat_height-0.4, 15,
	-(b-ax)-xxx, 0, 15,
	-(b-ax)-xxx+(rr+xx)*2, 0, -1
DEl 1
RETURN

END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return


5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

