
ResolLeg = ROUND_INT (gs_resol/2)
if ResolLeg < 3 then ResolLeg = 3

pen gs_cont_pen
if gs_shadow=0 then shadow off

resol gs_resol

if gs_detlevel_3D_m=0 then end

unID = 1
hotspot 0, 	 				chairWidth,	leg_h, unID,	halfChairDepth,	1 + 128, 	chairDepth	: unID = unID + 1
hotspot - chairDepth / 2, 	chairWidth,	leg_h, unID,	halfChairDepth,	2, 			chairDepth	: unID = unID + 1
hotspot chairDepth / 2, 	chairWidth,	leg_h, unID,	halfChairDepth,	3, 			chairDepth	: unID = unID + 1

hotspot - chairDepth / 2, 	0, 			leg_h, unID,	chairWidth,	1 + 128, 	chairWidth  : unID = unID + 1
hotspot - chairDepth / 2, 	chairWidth, leg_h, unID,	chairWidth,	2, 			chairWidth	: unID = unID + 1
hotspot - chairDepth / 2, 	-1, 		leg_h, unID,	chairWidth,	3, 			chairWidth	: unID = unID + 1

hotspot 0, 					chairWidth,	leg_h, unID,	halfChairDepth,	1 + 128, 	chairDepth 	 : unID = unID + 1
hotspot chairDepth / 2, 	chairWidth,	leg_h, unID,	halfChairDepth,	2, 			chairDepth 	 : unID = unID + 1
hotspot - chairDepth / 2, 	chairWidth,	leg_h, unID,	halfChairDepth,	3, 			chairDepth   : unID = unID + 1

hotspot chairDepth / 2,		0, 			leg_h, unID,	chairWidth,	1 + 128,	chairWidth  : unID = unID + 1
hotspot chairDepth / 2,		chairWidth, leg_h, unID,	chairWidth,	2, 			chairWidth 	: unID = unID + 1
hotspot chairDepth / 2,		-1, 		leg_h, unID,	chairWidth,	3, 			chairWidth	: unID = unID + 1

hotspot chairDepth / 2 - ta, 0, 	0, 				unID,	gs_seat_height,	1 + 128  : unID = unID + 1
hotspot chairDepth / 2 - ta, 0, 	gs_seat_height, unID,	gs_seat_height,	2 		 : unID = unID + 1
hotspot chairDepth / 2 - ta, 0, 	-1, 			unID,	gs_seat_height,	3		 : unID = unID + 1

hotspot - chairDepth / 2 + ta, 0, 	0, 				unID,	gs_seat_height,	1 + 128  : unID = unID + 1
hotspot - chairDepth / 2 + ta, 0, 	gs_seat_height, unID,	gs_seat_height,	2 		 : unID = unID + 1
hotspot - chairDepth / 2 + ta, 0, 	-1, 			unID,	gs_seat_height,	3		 : unID = unID + 1

hotspot 0,	3 * chairWidth / 4 - bt, 	0, 			unID,		zzyzx, 1 + 128		 : unID = unID + 1
hotspot 0,	3 * chairWidth / 4 - bt, 	zzyzx, 		unID,		zzyzx, 2 			 : unID = unID + 1
hotspot 0,	3 * chairWidth / 4 - bt, 	-1, 		unID,		zzyzx, 3			 : unID = unID + 1

if bOttoman then
	hotspot ottomanDepth / 2, - disOttomanChair, 	0, 				unID, ottomanHeight, 1 + 128,	ottomanHeight	 : unID = unID + 1
	hotspot ottomanDepth / 2, - disOttomanChair, 	ottomanHeight, 	unID, ottomanHeight, 2, 		ottomanHeight	 : unID = unID + 1
	hotspot ottomanDepth / 2, - disOttomanChair, 	-1, 			unID, ottomanHeight, 3,			ottomanHeight	 : unID = unID + 1
	
	hotspot -ottomanDepth / 2, - disOttomanChair, 	0, 				unID, ottomanHeight, 1 + 128,	ottomanHeight	 : unID = unID + 1
	hotspot -ottomanDepth / 2, - disOttomanChair, 	ottomanHeight, 	unID, ottomanHeight, 2, 		ottomanHeight	 : unID = unID + 1
	hotspot -ottomanDepth / 2, - disOttomanChair, 	-1, 			unID, ottomanHeight, 3,			ottomanHeight	 : unID = unID + 1

	hotspot 0, 	0, 					leg_h,	unID, disOttomanChair, 1 + 128	: unID = unID + 1
	hotspot 0, 	- disOttomanChair, 	leg_h,	unID, disOttomanChair, 2		: unID = unID + 1
	hotspot 0, 	chairWidth, 		leg_h,	unID, disOttomanChair, 3		: unID = unID + 1

	hotspot - ottomanDepth / 2,	- disOttomanChair, 					leg_h,	unID, ottomanWidth,	1 + 128, 	ottomanWidth	: unID = unID + 1
	hotspot - ottomanDepth / 2,	- ottomanWidth - disOttomanChair, 	leg_h,	unID, ottomanWidth,	2, 			ottomanWidth	: unID = unID + 1
	hotspot - ottomanDepth / 2,	1,									leg_h,	unID, ottomanWidth,	3, 			ottomanWidth	: unID = unID + 1

	hotspot	0,						- ottomanWidth - disOttomanChair, 	leg_h,	unID, halfOttomanDepth,	1 + 128, 	ottomanDepth 	: unID = unID + 1
	hotspot ottomanDepth / 2, 		- ottomanWidth - disOttomanChair, 	leg_h,	unID, halfOttomanDepth, 2, 			ottomanDepth 	: unID = unID + 1
	hotspot - ottomanDepth / 2, 	- ottomanWidth - disOttomanChair,	leg_h,	unID, halfOttomanDepth, 3, 			ottomanDepth	: unID = unID + 1


	hotspot ottomanDepth / 2, 	- disOttomanChair, 					leg_h,	unID, ottomanWidth, 1 + 128, 	ottomanWidth 	: unID = unID + 1
	hotspot ottomanDepth / 2, 	- ottomanWidth - disOttomanChair, 	leg_h,	unID, ottomanWidth, 2, 			ottomanWidth 	: unID = unID + 1
	hotspot ottomanDepth / 2, 	0,									leg_h,	unID, ottomanWidth, 3, 			ottomanWidth	: unID = unID + 1

	hotspot 0,					- ottomanWidth - disOttomanChair, 	leg_h,	unID, halfOttomanDepth, 	1 + 128, 	ottomanDepth	: unID = unID + 1
	hotspot -ottomanDepth / 2,	- ottomanWidth - disOttomanChair, 	leg_h,	unID, halfOttomanDepth, 	2, 			ottomanDepth	: unID = unID + 1
	hotspot ottomanDepth / 2,	- ottomanWidth - disOttomanChair,	leg_h,	unID, halfOttomanDepth, 	3, 			ottomanDepth	: unID = unID + 1
endif

if bLeg then
	material gs_leg_mat
	if iLegType = LEGTYPE_1 then gosub "legType1"
	if iLegType = LEGTYPE_2 then gosub "legType2"
	if iLegType = LEGTYPE_3 then gosub "legType3"
	if iLegType = LEGTYPE_4 then gosub "legType4"
	if iLegType = LEGTYPE_5 then gosub "legType5"
endif

if gs_detlevel_3D_m = 1 then
	material gs_seat_mat
	add 0, chairWidth / 2, leg_h
	prism_	4, th,
		chairDepth / 2 - ta,		- chairWidth / 2,		15,
		chairDepth / 2 - ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		- chairWidth / 2,		15
	addy chairWidth / 2
	rotx 90
	prism_	5, tBackPanel,
		- chairDepth / 2 + ta,		0,					15,
		- chairDepth / 2 + ta,		zzyzx-leg_h-rUpper,	15,
		chairDepth / 2 - ta, 		zzyzx-leg_h-rUpper,	15,
		chairDepth / 2 - ta,		0,					15,
		- chairDepth / 2 + ta,		0,					-1			
	del 2
	addz th
	prism_	4, th / 2,
		chairDepth / 2 - ta,		- chairWidth / 2,		15,
		chairDepth / 2 - ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		- chairWidth / 2,		15
	addz th / 2
	prism_	4, th / 2,
		chairDepth / 2 - ta,		- chairWidth / 2,		15,
		chairDepth / 2 - ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		- chairWidth / 2,		15
	addz th / 2
	addx chairDepth / 2 - ta - x / 2
	prism_	4, th,
		chairDepth / 2 - ta,		- chairWidth / 2,		15,
		chairDepth / 2 - ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		chairWidth / 2 - bt,	15,
		- chairDepth / 2 + ta,		- chairWidth / 2,		15
	del 1

	del 3

	rotx 90
	addz - chairWidth / 2
	for i=1 to 2
		prism_	4, chairWidth,
			chairDepth / 2 - ta,		0,				79,
			0,							hh - ta / 2,	179,
			ta / 2,						- 240,			2079,
			chairDepth / 2 - ta / 2,	0,				15
		mulx - 1
	next i
	del i - 1
	addz bt
	prism_	6, chairWidth / 4,
		chairDepth / 2 - ta,			3 * th,				79,
		chairDepth / 2 - ta,			zzyzx - ta - leg_h, 79,
		ta,								90,					2079,
		- chairDepth / 2 + 2 * ta,		zzyzx - leg_h,		79,
		ta,								90,					2079,
		- chairDepth / 2 + ta,			3 * th,				79
	del 3

	if bOttoman = 1 then
		addy - chairWidth / 2 - ottomanWidth / 2 - disOttomanChair
		prism_	4, 2 * thk,
			ottomanDepth / 2,	-ottomanWidth/2,	15,
			ottomanDepth / 2,	ottomanWidth/2,	15,
			-ottomanDepth / 2,	ottomanWidth/2,	15,
			-ottomanDepth / 2,	-ottomanWidth/2,	15
		addz 2 * thk
		prism_	4, thk,
			ottomanDepth / 2,		-ottomanWidth/2,	15,
			ottomanDepth / 2,		ottomanWidth/2,	15,
			-ottomanDepth / 2,		ottomanWidth/2,	15,
			-ottomanDepth / 2,		-ottomanWidth/2,	15
		addz thk
		prism_	4, 2 * thk,
			ottomanDepth / 2,		-ottomanWidth/2,	15,
			ottomanDepth / 2,		ottomanWidth/2,	15,
			-ottomanDepth / 2,		ottomanWidth/2,	15,
			-ottomanDepth / 2,		-ottomanWidth/2,	15
		del 4
	endif
endif

if gs_detlevel_3D_m = 2 then
	material gs_seat_mat
	add 0, chairWidth / 2, leg_h
	for j = 1 to 2
		rotx 90
		for i=1 to 2
			hprism_	s_mat, s_mat, s_mat, s_mat,
				4,	chairWidth / 2,	0,	r,	1,
				chairDepth / 2 - ta,		0,				78,
				0,							hh - ta / 2,	178,
				ta / 2,						-240,			2078,
				chairDepth / 2 - ta/2,		0,				78
			mulz - 1
		next i
		del i - 1
		del 1

		roty 90
		hprism_	s_mat, s_mat, s_mat, s_mat,
			9, chairDepth / 2 - ta, 0, r * 0.95, 1,
			-r,				- chairWidth / 2,			78,
			- th + 2 * r,	0,							178,
			r,				- 90,						2078,
			0,				chairWidth - 2 * r - bt,	178,
			r,				- 90,						2078,
			th - 2 * r,		0,							178,
			r,				- 90,						2078,
			0,				- chairWidth + 2 * r + bt,	178,
			r,				- 90,						2078
		hprism_	s_mat, s_mat, s_mat, s_mat,
			9,	chairDepth / 2 - ta, 0, r * 0.95, 1,
			-th-r / 2,		- chairWidth / 2,	78,
			-th / 2 + r,	0,					178,
			r / 2,			- 90,				2078,
			0,				chairWidth-r-bt,	178,
			r / 2,			- 90,				2078,
			th / 2-r,		0,					178,
			r / 2,			- 90,				2078,
			0,				- chairWidth+r+bt, 178,
			r / 2,			90,					2078
		hprism_	s_mat, s_mat, s_mat, s_mat,
			9, chairDepth / 2 - ta, 0, r * 0.95, 1,
			- th * 1.5 - r / 2,		- chairWidth / 2,			78,
			- th / 2 + r,			0,							178,
			r / 2,					- 90,						2078,
			0,						chairWidth - r - bt,		178,
			r / 2,					- 90,						2078,
			th / 2 - r,				0,							178,
			r / 2,					- 90,						2078,
			0,						- chairWidth + r + bt,		178,
			r / 2,					- 90,						2078
		del 1
		mulx - 1
	next j
	del j - 1

	rotx 90
	addz - (chairWidth / 2 - bt - chairWidth / 8)
	for i = 1 to 2
		hprism_	s_mat, s_mat, s_mat, s_mat,
			9, chairWidth / 8,	0,	rr * 0.9, 1,
			chairDepth / 2 - ta - 1.1 * rr,	3 * th, 					78,
			chairDepth / 2 - ta - rr,	  		3 * th,						78,
			rr,									90, 						2078,
			chairDepth / 2 - ta, 				zzyzx - rUpper - leg_h,		78,
			rUpper, 							90,	  						2078,
			- chairDepth / 2 + rUpper + ta,	zzyzx - leg_h, 				78,
			rUpper,								90, 						2078,
			- chairDepth / 2 + ta, 			3 * th + rr, 				78,
			rr, 								90, 						2078
		mulz -1
	next i
	del i - 1
	del 2

	roty 90
	for j = 1 to 2
		hprism_	s_mat, s_mat, s_mat, s_mat,
			9, x / 2, 0, r * 0.95, 1,
			- th * 2 - r,	- chairWidth / 2,			78,
			- th + 2 * r,	0,							178,
			r,				- 90,						2078,
			0,				chairWidth - 2 * r - bt,	178,
			r,				- 90,						2078,
			th				- 2 * r, 0,					178,
			r,				- 90,						2078,
			0,				- chairWidth + 2 * r + bt, 178,
			r,				- 90,						2078
		mulz - 1
	next j
	del j - 1
	del 1

	addy chairWidth / 2
	rotx 90
	prism_	5, tBackPanel,
			- chairDepth / 2 + ta,		0,						15,
			- chairDepth / 2 + ta,		zzyzx-leg_h-rUpper,		15,
			chairDepth / 2 - ta, 		zzyzx-leg_h-rUpper,		15,
			chairDepth / 2 - ta,		0,						15,
			- chairDepth / 2 + ta,		0,						-1
	del 2
	if bOttoman = 1 then
		addy - chairWidth / 2 - ottomanWidth/2 - disOttomanChair
		for j = 1 to 2
			roty 90
			hprism_	s_mat, s_mat, s_mat, s_mat,
				9,	ottomanDepth / 2, 0, r*0.95, 1,
				- r,				- ottomanWidth / 2,		78,
				- 2 * thk + 2 * r,	0,							178,
				r,					-90,						2078,
				0,					ottomanWidth-2*r,			178,
				r,					-90, 						2078,
				2 * thk - 2 * r,	0, 							178,
				r,					- 90, 						2078,
				0,					- ottomanWidth + 2 * r,	178,
				r,					- 90, 						2078
			hprism_	s_mat, s_mat, s_mat, s_mat,
				9, ottomanDepth / 2, 0, r / 2 * 0.95, 1,
				- 2 * thk - r / 2,	- ottomanWidth / 2,	78,
				- thk + r,			0,						178,
				r / 2,				- 90,					2078,
				0,					ottomanWidth - r,		178,
				r / 2,				- 90,					2078,
				thk - r,			0,						178,
				r / 2,				- 90,					2078,
				0,					- ottomanWidth + r,	178,
				r / 2,				- 90,					2078
			del 1
			mulx -1
		next j
		del j-1

		addz -thk
		roty 90
		for j = 1 to 2
			hprism_	s_mat, s_mat, s_mat, s_mat,
				9, ottomanDepth / 2, 0, r * 0.95, 1,
				- 2 * thk * 2 - r,	- ottomanWidth / 2, 78,
				- 2 * thk + 2 * r,	0,							178,
				r,					- 90,						2078,
				0,					ottomanWidth - 2 * r,		178,
				r,					- 90,						2078,
				2 * thk - 2 * r,	0,							178,
				r,					- 90,						2078,
				0,					- ottomanWidth + 2 * r,	178,
				r,					- 90,						2078
			mulz - 1
		next j
		del j - 1
		del 2
		del 1
	endif
	del 1
endif

end

"legType1":
	add - chairDepth / 2 + 0.7 * ta, 0.1 * chairWidth, 0
	call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 1,
									a				 = chairDepth - 1.4 * ta,
									b				 = 0.8 * chairWidth, 
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_resol		 = ResolLeg, 
									gs_cont_pen		 = gs_cont_pen,
									gs_leg_mat		 = gs_leg_mat
	if bOttoman = 1 then
		add chairDepth / 2 - 0.7 * ta - ottomanDepth / 2 + 0.7 * tb,- disOttomanChair - 0.1 * chairWidth - 0.875 * ottomanWidth, 0
		call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 1, 
										a				 = ottomanDepth - 1.4 * tb,
										b				 = 0.75 * ottomanWidth, 
										gs_detlevel_3D_m = gs_detlevel_3D_m,
										gs_resol		 = ResolLeg,
										gs_cont_pen		 = gs_cont_pen,
										gs_leg_mat		 = gs_leg_mat
		del 1
	endif
	del 1
return

"legType2":
	add - chairDepth / 2 + 0.7 * ta, 0.1 * chairWidth, 0
	call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 2,
									a				 = chairDepth - 1.4 * ta,
									b				 = 0.8 * chairWidth, 
									leg_h			 = leg_h, 
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_resol		 = ResolLeg,
									gs_cont_pen		 = gs_cont_pen, 
									gs_leg_mat		 = gs_leg_mat
	if bOttoman = 1 then
		add chairDepth / 2 - 0.7 * ta - ottomanDepth / 2 + 0.7 * tb,- disOttomanChair - 0.1 * chairWidth - 0.875 * ottomanWidth, 0
		call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 2,
										a				 = ottomanDepth - 1.4 * tb,
										b				 = 0.75 * ottomanWidth, 
										leg_h			 = leg_h, 
										gs_detlevel_3D_m = gs_detlevel_3D_m,
										gs_resol		 = ResolLeg,
										gs_cont_pen		 = gs_cont_pen,
										gs_leg_mat		 = gs_leg_mat
		del 1
	endif
	del 1
return

"legType3":
	add - chairDepth / 2 + 0.8 * ta, 0.1 * chairWidth, 0
	call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 3,
									a				 = chairDepth - 1.6 * ta,
									b				 = 0.8 * chairWidth, 
									leg_h			 = leg_h, 
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_resol		 = ResolLeg,
									gs_cont_pen		 = gs_cont_pen,
									gs_leg_mat		 = gs_leg_mat
	if bOttoman = 1 then
		add chairDepth / 2 - 0.8 * ta - ottomanDepth / 2 + 0.8 * tb,- disOttomanChair - 0.1 * chairWidth - 0.875 * ottomanWidth, 0
		call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 3,
										a				 = ottomanDepth - 1.6 * tb,
										b				 = 0.75 * ottomanWidth,
										leg_h			 = leg_h,
										gs_detlevel_3D_m = gs_detlevel_3D_m,
										gs_resol		 = ResolLeg,
										gs_cont_pen		 = gs_cont_pen,
										gs_leg_mat		 = gs_leg_mat
		del 1
	endif
	del 1
return

"legType4":
	add - chairDepth / 2 + 0.5 * ta, 0.1 * chairWidth, 0
	call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 4,
									a				 = chairDepth-1.0*ta,
									b				 = 0.8 * chairWidth, 
									leg_h			 = leg_h, 
									gs_detlevel_3D_m = gs_detlevel_3D_m,
									gs_resol		 = ResolLeg,
									gs_cont_pen		 = gs_cont_pen,
									gs_leg_mat		 = gs_leg_mat
	if bOttoman = 1 then
		add chairDepth / 2 - 0.5 * ta - ottomanDepth / 2 + 0.5 * tb, - disOttomanChair - 0.1 * chairWidth - 0.875 * ottomanWidth, 0
		call "fa_sofalegtyp" parameters sofaleg_typ_m	 = 4,
										a				 = ottomanDepth - tb,
										b				 = 0.75 * ottomanWidth,
										leg_h			 = leg_h,
										gs_detlevel_3D_m = gs_detlevel_3D_m,
										gs_resol		 = ResolLeg,
										gs_cont_pen		 = gs_cont_pen,
										gs_leg_mat		 = gs_leg_mat
		del 1
	endif
	del 1
return

"legType5":
	add - chairDepth / 2 + 0.8 * ta, 0.1 * chairWidth, 0
	call "fa_sofalegtyp" parameters sofaleg_typ_m	= 5,
									a				= chairDepth-1.6*ta,
									b				= 0.8*chairWidth,
									leg_h			= leg_h,
									ur				= ta/8, 
									lr				= ta/12,
									gs_cont_pen		= gs_cont_pen, 
									gs_leg_mat		= gs_leg_mat
	if bOttoman = 1 then
	add chairDepth / 2 - 0.8 * ta - ottomanDepth / 2 + 0.8 * tb,- disOttomanChair - 0.1 * chairWidth - 0.875 * ottomanWidth, 0
		call "fa_sofalegtyp" parameters sofaleg_typ_m	= 5,
										a				= ottomanDepth-1.6*tb,
										b				= 0.75*ottomanWidth,
										leg_h			= leg_h, 
										ur				= ta/8, 
										lr				= ta/12,
										gs_cont_pen		= gs_cont_pen,
										gs_leg_mat		= gs_leg_mat
		del 1
	endif
	del 1
return
