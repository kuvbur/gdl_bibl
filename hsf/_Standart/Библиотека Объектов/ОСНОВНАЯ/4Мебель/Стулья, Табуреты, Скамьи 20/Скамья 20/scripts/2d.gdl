
! Minimal Space

if gs_chair_style_m = CORNER then
	MsType = 30
	diff = gs_seat_thk
	diff2 = gs_seat_thk
	add2 A, 0
	mul2 -1, 1
endif

if gs_chair_style_m = STRAIGHT then
	MsType = 1
	diff = 0
	diff2 = 0
endif

if MSFront > EPS then
	call "fa_minimalspace" parameters all 	MSFront = MSFront,
											MSType = MSType, unID = 3000,
											diff = diff, diff2 = diff2
endif

if gs_chair_style_m= CORNER then
	del 2
endif

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

unID = 1
pen gs_cont_pen
fill gs_fill_type
! ======================================================================
! CORNER STYLE
! ======================================================================
if gs_chair_style_m= CORNER then

	add2 A, B

	! ------------------------------------------------------------------
	! hotspots
	! ------------------------------------------------------------------
	HOTSPOT2  0, 0, unID : unID = unID + 1
	HOTSPOT2 -a/2, -b/2, unID : unID = unID + 1
	HOTSPOT2 -a, 0, unID : unID = unID + 1
	HOTSPOT2 -a, -gs_seat_thk, unID : unID = unID + 1
	HOTSPOT2 -gs_seat_thk, -gs_seat_thk, unID : unID = unID + 1
	HOTSPOT2 -gs_seat_thk, -b, unID : unID = unID + 1
	HOTSPOT2  0, -b, unID : unID = unID + 1


	! ------------------------------------------------------------------
	! detailed 1:50 representation 
	! ------------------------------------------------------------------
	if det2D = 3 then												! detailed
		
		if gs_armrest then
			if gs_backrest then		
				POLY2_B  15, 1+2+4+64, gs_fill_pen, gs_back_pen,		! fill with armrests
					-A, 0, 1,
					-A, -0.04, 1,
					-A-0.01, -0.04,1,
					-A-0.01, -(0.01+gs_seat_thk),1,
					-A, -(0.01+gs_seat_thk),1,
					-A-0.01+0.04, -(0.01+gs_seat_thk),1,
					-A-0.01+0.04, -gs_seat_thk, 1,
					-gs_seat_thk, -gs_seat_thk,1,
					-gs_seat_thk, -B+0.03, 1,
					-gs_seat_thk-0.01, -B+0.03, 1,
					-gs_seat_thk-0.01, -B+0.03-0.04,1,
					-0.04, -B+0.03-0.04,1,
					-0.04, -B, 1,
					 0, -B, 1,
					 0, 0,1
				POLY2_  4, 1+4,					! armrest
					-A-0.01, -0.04,0,
					-A-0.01, -(0.01+gs_seat_thk),0,
					-A-0.01+0.04, -(0.01+gs_seat_thk),1,
					-A-0.01+0.04, -0.04, 1
				POLY2_  4, 1+4,					! armrest
					-gs_seat_thk-0.01, -B+0.03, 0,
					-gs_seat_thk-0.01, -B+0.03-0.04,0,
					-0.04, -B+0.03-0.04,1,
					-0.04, -B+0.03,1
			else
				POLY2_B  13, 1+2+4+64, gs_fill_pen, gs_back_pen,		! fill with armrests
					-A-0.01, 0, 1,
					-A-0.01, -0.04,1,
					-A-0.01, -(0.01+gs_seat_thk),1,
					-A, -(0.01+gs_seat_thk),1,
					-A-0.01+0.04, -(0.01+gs_seat_thk),1,
					-A-0.01+0.04, -gs_seat_thk, 1,
					-gs_seat_thk, -gs_seat_thk,1,
					-gs_seat_thk, -B+0.03, 1,
					-gs_seat_thk-0.01, -B+0.03, 1,
					-gs_seat_thk-0.01, -B+0.03-0.04,1,
					-0.04, -B +0.03-0.04,1,
					 0, -B-0.01, 1,
					 0, 0,1
				POLY2_  4, 1+4,									! armrest
					-A-0.01, 0,0,
					-A-0.01, -(0.01+gs_seat_thk),0,
					-A-0.01+0.04, -(0.01+gs_seat_thk),1,
					-A-0.01+0.04, 0, 1
				POLY2_  4, 1+4,									! armrest
					-gs_seat_thk-0.01, -B+0.03, 0,
					-gs_seat_thk-0.01, -B+0.03-0.04,0,
					0, -B+0.03-0.04,1,
					0, -B+0.03,1
			endif

		else		! no armrests
			POLY2_B  6, 1+2+4+64, gs_fill_pen, gs_back_pen,		! fill without armrests
				-a, 0, 1,
				-a, -gs_seat_thk, 1,
				-gs_seat_thk, -gs_seat_thk, 1,
				-gs_seat_thk, -b, 1,
				 0, -b, 1,
				 0, 0, 1
		endif
		if gs_backrest then	
			POLY2_  4, 1+4,					! leg
				-A,		0, 		0,
				-A,		-0.04,	1,
				-A+0.03,-0.04,	1,
				-A+0.03,0,0
		
			POLY2_  4, 1+4,					! leg
				-0.04,	0, 		1,
				-0.04,	-0.04,	1,
				0,		-0.04,	0,
		 		0, 		0, 		0

			POLY2_  4, 1+4,					! leg
				0,		-B, 		0,
				0,		-B+0.03,	1,
				-0.04,	-B+0.03,	1,
				-0.04, 	-B,			0

			POLY2_  4, 1+4,					! backrest
				-A+0.03,-0.03,	 	0,
				-A+0.03,-0.03+0.02,	1,
				-0.04,	-0.03+0.02,	0,
				-0.04, 	-0.03,		1

			POLY2_  4, 1+4,					! backrest
				-0.03,-B+0.03,	 	1,
				-0.03,-0.04,		0,
				-0.03+0.02,-0.04,	1,
				-0.03+0.02, -B+0.03,0
		endif

	! ------------------------------------------------------------------
	! middle 1:100, simple 1:200 representation
	! ------------------------------------------------------------------
	else												! middle armrest,backrest
		if det2D = 2 then
			if gs_armrest then
				line2 -A+0.03,	0,	-A+0.03,		-gs_seat_thk
				line2 0,	-B+0.03,	-gs_seat_thk,	-B+0.03						! armrest
			endif

			if gs_backrest then		
				line2 -0.03,	-0.03,	-A+0.03*gs_armrest,		-0.03				! backrest
				line2 -0.03,	-0.03,	-0.03,	-B+0.03*gs_armrest
			endif
		endif

		POLY2_B  6, 1+2+4+64, gs_fill_pen, gs_back_pen,		! middle base, simple
			-A,			0,				1,
			-A,			-gs_seat_thk,	1,
			-gs_seat_thk,-gs_seat_thk,	1,
			-gs_seat_thk, 	-B,			1,
			0,				-B,			1,
			0,				0,			1
	endif

! ======================================================================
! SRAIGHT STYLE
! ======================================================================

else
	ADD2 A, gs_seat_thk

	! ------------------------------------------------------------------
	! seat hotspots
	! ------------------------------------------------------------------
	HOTSPOT2 -a/2, -gs_seat_thk, unID : unID = unID + 1
	HOTSPOT2 0, -gs_seat_thk, unID : unID = unID + 1
	HOTSPOT2 0, 0, unID : unID = unID + 1
	HOTSPOT2 -A, -gs_seat_thk, unID : unID = unID + 1
	HOTSPOT2 -A, 0, unID : unID = unID + 1
	HOTSPOT2 -a/2, -gs_seat_thk/2, unID : unID = unID + 1

	! ------------------------------------------------------------------
	! detailed 1:50 representation
	! ------------------------------------------------------------------
	if det2D = 3 then
		if gs_armrest then
			if gs_backrest then												!with backrest
				POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,							!seat
					-A-0.005+0.03, -0.02, 1,
					-A-0.005+0.03, -gs_seat_thk, 1,
					0.005-0.03, -gs_seat_thk, 1,
					0.005-0.03, -0.02, 1
				POLY2_B   4, 1+2+4+64, gs_fill_pen, gs_back_pen,							! backrest
					-A, 0, 1,
					-A, -0.02, 1,
					0,-0.02,1,
					0,0,1
				POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,							! armrest
					-A-0.005, -0.02,1,
					-A-0.005, -(0.005+gs_seat_thk),1,
					-A-0.005+0.03, -(0.005+gs_seat_thk),1,
					-A-0.005+0.03, -0.02, 1

				POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,							! armrest
					0.005, -0.02,1,
					0.005, -(0.005+gs_seat_thk),1,
					0.005-0.03, -(0.005+gs_seat_thk),1,
					0.005-0.03, -0.02, 1
			else															! no backrest
				POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,							!seat
					-A-0.005+0.03, 0, 1,
					-A-0.005+0.03, -gs_seat_thk, 1,
					0.005-0.03, -gs_seat_thk, 1,
					0.005-0.03, 0, 1
				POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,							! armrest
					-A-0.005, 0.005,1,
					-A-0.005, -(0.005+gs_seat_thk),1,
					-A-0.005+0.03, -(0.005+gs_seat_thk),1,
					-A-0.005+0.03, 0.005, 1
				POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,							! armrest
					0.005, 0.005,1,
					0.005, -(0.005+gs_seat_thk),1,
					0.005-0.03, -(0.005+gs_seat_thk),1,
					0.005-0.03, 0.005, 1
			endif
		else
			if gs_backrest then												!with backrest
				POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,							!seat
					-A, -0.02, 1,
					-A, -gs_seat_thk, 1,
					0, -gs_seat_thk, 1,
					0, -0.02, 1
				POLY2_B   4, 1+2+4+64, gs_fill_pen, gs_back_pen,							! backrest
					-A, 0, 1,
					-A, -0.02, 1,
					0,-0.02,1,
					0,0,1
			else
			POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-a, 0, 1,
				-a, -gs_seat_thk, 1,
				0, -gs_seat_thk, 1,
				0, 0, 1
			endif
		endif

	! ------------------------------------------------------------------
	! middle 1:100, simple 1:200 representation
	! ------------------------------------------------------------------
	else													! middle armrest,backrest
		if det2D = 2 then
			if gs_armrest then
				line2 -A+0.03,	0,	-A+0.03,		-gs_seat_thk			! armrest
				line2 -0.03,	0,	-0.03,			-gs_seat_thk
			endif

			if gs_backrest then
				line2 -0.03*gs_armrest,	-0.03,	-A+0.03*gs_armrest,		-0.03			! backrest
			endif
		endif

		POLY2_B  4, 1+2+4+64, gs_fill_pen, gs_back_pen,			! middle base, simple
			-a, 0, 1,
			-a, -gs_seat_thk, 1,
			0, -gs_seat_thk, 1,
			0, 0, 1
	endif
endif
del 1
