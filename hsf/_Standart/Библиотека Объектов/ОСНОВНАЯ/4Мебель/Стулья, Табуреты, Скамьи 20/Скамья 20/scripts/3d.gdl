
if gs_detlevel_3d_m = DETLEV_OFF then end

unID = 1

pen gs_cont_pen


if not(gs_shadow) then SHADOW OFF

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m = DETLEV_SIMPLE

if gs_chair_style_m = CORNER	then add a/2,0,0
if gs_chair_style_m = STRAIGHT	then add a/2,0,0

! Hotspot editing
if gs_full_edit then
	if gs_chair_style_m = CORNER then
		unID=unID+1
		hotspot -a/2+gs_armrest*0.03, b-gs_seat_thk, 0, unID, gs_seat_height, 1+128
		unID=unID+1
		hotspot -a/2+gs_armrest*0.03, 0, -1, unID, gs_seat_height, 3
		unID=unID+1
		hotspot -a/2+gs_armrest*0.03, b-gs_seat_thk, gs_seat_height, unID, gs_seat_height, 2

		unID=unID+1
		hotspot a/2-gs_seat_thk, b-gs_seat_thk, 0, unID, gs_seat_height, 1+128
		unID=unID+1
		hotspot a/2-gs_seat_thk, 0, -1, unID, gs_seat_height, 3
		unID=unID+1
		hotspot a/2-gs_seat_thk, b-gs_seat_thk, gs_seat_height, unID, gs_seat_height, 2

		unID=unID+1
		hotspot a/2-gs_seat_thk, gs_armrest*0.03, 0, unID, gs_seat_height, 1+128
		unID=unID+1
		hotspot a/2-gs_seat_thk, 0, -1, unID, gs_seat_height, 3
		unID=unID+1
		hotspot a/2-gs_seat_thk, gs_armrest*0.03, gs_seat_height, unID, gs_seat_height, 2
	else
		if gs_chair_style_m = STRAIGHT then
			unID=unID+1
			hotspot -a/2+gs_armrest*0.03, 0, 0, unID, gs_seat_height, 1+128
			unID=unID+1
			hotspot -a/2+gs_armrest*0.03, 0, -1, unID, gs_seat_height, 3
			unID=unID+1
			hotspot -a/2+gs_armrest*0.03, 0, gs_seat_height, unID, gs_seat_height, 2

			unID=unID+1
			hotspot a/2-gs_armrest*0.03, 0, 0, unID, gs_seat_height, 1+128
			unID=unID+1
			hotspot a/2-gs_armrest*0.03, 0, -1, unID, gs_seat_height, 3
			unID=unID+1
			hotspot a/2-gs_armrest*0.03, 0, gs_seat_height, unID, gs_seat_height, 2
		endif
	endif
endif

unID = 20

if gs_chair_style_m = CORNER then
	unID=unID+1
	hotspot -a/2, b-gs_seat_thk, 0, unID
	unID=unID+1
	hotspot  a/2-gs_seat_thk, b-gs_seat_thk, 0, unID
	unID=unID+1
	hotspot  a/2-gs_seat_thk, 0, 0, unID
	unID=unID+1
	hotspot  a/2, 0, 0, unID
	unID=unID+1
	hotspot  a/2, b, 0, unID
	unID=unID+1
	hotspot -a/2, b, 0, unID

	if gs_armrest then
		unID=unID+1
		hotspot  a/2-gs_seat_thk-0.01, -0.01, gs_seat_height+0.2, unID
		unID=unID+1
		hotspot -a/2-0.01, b-gs_seat_thk-0.01, gs_seat_height+0.2, unID
	else
		unID=unID+2
	endif

	if gs_backrest then
		unID=unID+1
		hotspot a/2, 0, 0, unID, zzyzx, 1+128
		unID=unID+1
		hotspot a/2, 0, -1, unID, zzyzx, 3
		unID=unID+1
		hotspot a/2, 0, zzyzx, unID, zzyzx, 2

		unID=unID+1
		hotspot a/2, b, 0, unID, zzyzx, 1+128
		unID=unID+1
		hotspot a/2, b, -1, unID, zzyzx, 3
		unID=unID+1
		hotspot a/2, b, zzyzx, unID, zzyzx, 2

		unID=unID+1
		hotspot -a/2, b, 0, unID, zzyzx, 1+128
		unID=unID+1
		hotspot -a/2, b, -1, unID, zzyzx, 3
		unID=unID+1
		hotspot -a/2, b, zzyzx, unID, zzyzx, 2
	else
		unID=unID+9
	endif

	if gs_detlevel_3d_m = DETLEV_SIMPLE then gs_seat_style_m = PLANK
	GOSUB 1
endif

if gs_chair_style_m = STRAIGHT then
	unID=unID+1
	hotspot -a/2, 0, 0, unID
	unID=unID+1
	hotspot  a/2, 0, 0, unID
	unID=unID+1
	hotspot  a/2, gs_seat_thk, 0, unID
	unID=unID+1
	hotspot -a/2, gs_seat_thk, 0, unID

	if gs_armrest then
		unID=unID+1
		hotspot -a/2, 0, gs_seat_height+0.2, unID
		unID=unID+1
		hotspot  a/2, 0, gs_seat_height+0.2, unID
	else
		unID=unID+2
	endif

	if gs_backrest then
		unID=unID+1
		hotspot -a/2, gs_seat_thk, 0, unID, zzyzx, 1+128
		unID=unID+1
		hotspot -a/2, gs_seat_thk, -1, unID, zzyzx, 3
		unID=unID+1
		hotspot -a/2, gs_seat_thk, zzyzx, unID, zzyzx, 2

		unID=unID+1
		hotspot a/2, gs_seat_thk, 0, unID, zzyzx, 1+128
		unID=unID+1
		hotspot a/2, gs_seat_thk, -1, unID, zzyzx, 3
		unID=unID+1
		hotspot a/2, gs_seat_thk, zzyzx, unID, zzyzx, 2
	else
		unID=unID+6
	endif

	if gs_detlevel_3d_m = DETLEV_SIMPLE then gs_seat_style_m = PLANK

	GOSUB 2
endif

DEL 1
END

! ======================================================================
1:				!CORNER STYLE
! ======================================================================
	ADD A/2,B,0

! ----------------------------------------------------------------------
	!legs
! ----------------------------------------------------------------------

	if  gs_armrest then
		legMod = 0.015-0.2
	else
		legMod = 0.015
	endif

	MATERIAL gs_frame_mat

	ADD -A,-0.04,0
	if not(gs_backrest) then
		BLOCK 0.03,0.04,gs_seat_height-legMod
	else
		BLOCK 0.03,0.04, zzyzx
	endif

	ADDY -(gs_seat_thk-0.04)
	BLOCK 0.03,0.04,gs_seat_height-legMod
	ADDX A-gs_seat_thk
	BLOCK 0.04,0.04,gs_seat_height-0.015
	ADDY -B+gs_seat_thk
	BLOCK 0.04,0.03,gs_seat_height-legMod
	ADDX gs_seat_thk-0.04
	if not(gs_backrest) then
		BLOCK 0.04,0.03,gs_seat_height-legMod
	else
		BLOCK 0.04,0.03, zzyzx
	endif
	ADDY B-0.04
	IF not(gs_backrest) then
		BLOCK 0.04,0.04, gs_seat_height-0.015
	else
		BLOCK 0.04,0.04, zzyzx
	ENDIF
	DEL 6

	gosub 1000
! ----------------------------------------------------------------------
! bars under the seat (connect the legs)
! ----------------------------------------------------------------------
	ADD  -A+0.01,-(gs_seat_thk-0.04),gs_seat_height-0.075
	BLOCK 0.02,(gs_seat_thk-0.08),0.06
	gosub 3000
	ADDZ -0.18-(gs_seat_height-0.4)/2
	BLOCK 0.02,(gs_seat_thk-0.08),0.06
	gosub 3000
	DEL 1
	ADD 0.02,-0.03,0
	BLOCK A-(gs_seat_thk+0.03),0.02,0.06
	gosub 4000
	ADD 0,gs_seat_thk-0.04,0
	BLOCK A-0.07,0.02,0.06
	gosub 4000
	DEL 3
	ADD -(gs_seat_thk-0.01),-B+0.03,gs_seat_height-0.075
	BLOCK 0.02,B-(gs_seat_thk+0.03),0.06
	gosub 3000
	ADD (gs_seat_thk-0.04),0,0
	BLOCK 0.02,B-0.07,0.06
	gosub 3000
	ADD -(gs_seat_thk-0.07),-0.02,0
	BLOCK (gs_seat_thk-0.08),0.02,0.06
	gosub 4000
	ADDZ -0.18-(gs_seat_height-0.4)/2
	BLOCK (gs_seat_thk-0.08),0.02,0.06
	gosub 4000
	DEL 4
! ----------------------------------------------------------------------
	!seat
! ----------------------------------------------------------------------
	ADDZ gs_seat_height-0.015

	IF gs_seat_style_m = PLANK THEN

		MATERIAL gs_seat_mat

		IF gs_armrest=0 AND gs_backrest=1 THEN
			PRISM_    9,    0.015,
					-0.04,		0,				15,
					-A+0.03,	0,				15,
					-A+0.03,	-0.04,			15,
					-A,			-0.04,			15,
					-A,			-gs_seat_thk,	15,
					-gs_seat_thk,-gs_seat_thk,	11,
					 0,			-gs_seat_thk,	15,
					 0,			-0.04,			15,
					-0.04,		-0.04,			15

			gosub 5000

			PRISM_    6,    0.015,
					-gs_seat_thk,	-B,				15,
					-0.04,			-B,				15,
					-0.04,			-B+0.03,		15,
					0,				-B+0.03,		15,
					0,				-gs_seat_thk,	11,
					-gs_seat_thk,	-gs_seat_thk,	15

			gosub 3000

		ENDIF

		IF gs_armrest=1 AND gs_backrest=0 THEN

			PRISM_    9,    0.015,
					0,			0,			15,
					-A+0.03,	0.00,		15,
					-A+0.03,	-0.04,		15,
					-A,			-0.04,		15,
					-A,			-gs_seat_thk+0.04,	15,
					-A+0.03,	-gs_seat_thk+0.04,	15,
					-A+0.03,	-gs_seat_thk,		15,
					-gs_seat_thk,-gs_seat_thk,		11,
					 0,			-gs_seat_thk,		15

			gosub 5000

			PRISM_    8,    0.015,
					-gs_seat_thk,		-B+0.03,	15,
					-gs_seat_thk+0.04,	-B+0.03,	15,
					-gs_seat_thk+0.04,	-B,			15,
					-0.04,				-B,			15,
					-0.04,				-B+0.03,	15,
					 0,					-B+0.03,	15,
					 0,					-gs_seat_thk,11,
					-gs_seat_thk,		-gs_seat_thk,15

			gosub 3000

		ENDIF

		IF gs_armrest=1 AND gs_backrest=1 THEN

			PRISM_    11,    0.015,
					 0,			-0.04,		15,
					-0.04,		-0.04,		15,
					-0.04,		0,			15,
					-A+0.03,	0.00,		15,
					-A+0.03,	-0.04,		15,
					-A,			-0.04,		15,
					-A,			-gs_seat_thk+0.04,	15,
					-A+0.03,	-gs_seat_thk+0.04,	15,
					-A+0.03,	-gs_seat_thk,		15,
					-gs_seat_thk,-gs_seat_thk,		11,
					 0,			-gs_seat_thk,		15

			gosub 5000

			PRISM_    8,    0.015,
					-gs_seat_thk,		-B+0.03,	15,
					-gs_seat_thk+0.04,	-B+0.03,	15,
					-gs_seat_thk+0.04,	-B,			15,
					-0.04,				-B,			15,
					-0.04,				-B+0.03,	15,
					0,					-B+0.03,	15,
					 0,					-gs_seat_thk,11,
					-gs_seat_thk,		-gs_seat_thk,15

			gosub 3000

		ENDIF

		IF not(gs_armrest) AND not(gs_backrest) THEN

			PRISM_    5,    0.015,
					0,			 0,				15,
					-A,			 0.00,			15,
					-A,			 -gs_seat_thk,	15,
					-gs_seat_thk,-gs_seat_thk,	11,
					 0,			 -gs_seat_thk,	15

			gosub 5000

			PRISM_    4,    0.015,
					0,				-gs_seat_thk,	11,
					-gs_seat_thk,	-gs_seat_thk,	15,
					-gs_seat_thk,	-B,				15,
					0,				-B,				15

			gosub 3000

		ENDIF

		DEL 1

	else													!Seat Type=LATH

		MATERIAL gs_seat_mat

		rotz 45										
		if bMiteredLath then cutplane 90
		del 1

		if gs_backrest then
			PRISM_    8,    0.015,
					-0.04,		0.00,	15,
					-A+0.03,	0.00,	15,
					-A+0.03,	-0.04,	15,
					-A,			-0.04,	15,
					-A,			-0.07,	15,
					0,			-0.07,	15,
					0,			-0.04,	15,
					-0.04,		-0.04,	15
		else
			if gs_armrest then
				PRISM_    6,    0.015,
						0,       0,   15,
						-A+0.03, 0,   15,
						-A+0.03,-0.04,15,
						-A,     -0.04,15,
						-A,     -0.07,15,
						0,      -0.07,15
			else
				PRISM_    4,    0.015,
						0,       0,   15,
						-A,      0,   15,
						-A,     -0.07,15,
						0,      -0.07,15
			endif
		endif
		ADD -A,-gs_seat_thk,0
		IF not(gs_armrest) THEN
			BLOCK A,0.07,0.015
		ELSE
			PRISM_  6,    0.015,
					0.03,	0,		15,
					A,		0,		15,
					A,		0.07,	15,
					0,		0.07,	15,
					0,		0.04,	15,
					0.03,	0.04,	15
		ENDIF

		gosub 5000

		N=INT((gs_seat_thk+0.025)/0.095)
		DX=gs_seat_thk-(N*0.07+(N-1)*0.025)
		DK=0.025+DX/(N-1)

		ADDY 0.07+DK

		FOR I=1 TO N-2
			BLOCK A,0.07,0.015
			gosub 5000
			ADDY 0.07+DK
		NEXT I

		DEL N

		if bMiteredLath then cutend

		rotz 45										
		if bMiteredLath then cutplane -90
		del 1
	
		if bMiteredLath then										!Seat length
			_seatingLength = B-0.04
		else
			_seatingLength = B-gs_seat_thk
		endif

		ADD -gs_seat_thk,-B,0

		if not(gs_armrest) then							!Seat Type=Lath, right side
			block 0.07, _seatingLength, 0.015
		else
			prism_    6,    0.015,
					0.04,	0,				15,
					0.07,	0,				15,
					0.07,	_seatingLength,	15,
					0,		_seatingLength,	15,
					0,		0.03,			15,
					0.04,	0.03,			15
		endif

		gosub 3000

		N=INT((gs_seat_thk+0.025)/0.095)
		DX=gs_seat_thk-(N*0.07+(N-1)*0.025)
		DK=0.025+DX/(N-1)

		ADDX 0.07+DK
		FOR I=1 TO N-2
			 block 0.07, _seatingLength, 0.015
			 gosub 3000
			 ADDX 0.07+DK
		NEXT I

		IF gs_armrest=0 AND gs_backrest=0 THEN
			BLOCK 0.07,B-gs_seat_thk,0.015
		ELSE
			PRISM_    6,    0.015,
					0,		0,			15,
					0.03,	0,			15,
					0.03,	0.03,		15,
					0.07,	0.03,		15,
					0.07,	_seatingLength, 15,
					0,		_seatingLength, 15
		ENDIF

		gosub 3000
		DEL n+1

		if bMiteredLath then cutend
	ENDIF

	IF gs_backrest THEN
		MATERIAL gs_back_mat
		ADD -A+0.03,-0.03, zzyzx-0.19
		BLOCK A-0.07,0.02,0.06
		gosub 5000
		ADDZ 0.12
		BLOCK A-0.07,0.02,0.06
		gosub 5000
		DEL 2

		ADD -0.03,-B+0.03, zzyzx-0.19
		BLOCK 0.02,B-0.07,0.06
		gosub 3000
		ADDZ 0.12
		BLOCK 0.02,B-0.07,0.06
		gosub 3000
		DEL 2
	ENDIF
	DEL 1

! ----------------------------------------------------------------------
! armrests
! ----------------------------------------------------------------------

	if gs_armrest then
		MATERIAL gs_frame_mat
		ADD -A/2+0.005,B-(gs_seat_thk-0.04),gs_seat_height-0.015+0.17
		BLOCK 0.02,(gs_seat_thk-0.08),0.03
		gosub 3000
		DEL 1
		ADD  A/2-(gs_seat_thk-0.04),0.005,gs_seat_height-0.015+0.17
		BLOCK (gs_seat_thk-0.08),0.02,0.03
		gosub 5000
		DEL 1

		MATERIAL gs_seat_mat
		if gs_backrest then
			ADD -A/2-0.01,B-(gs_seat_thk+0.01),gs_seat_height-0.015+0.2
			BLOCK 0.04,(gs_seat_thk-0.03),0.015
			gosub 3000
			DEL 1

			ADD A/2-(gs_seat_thk+0.01),-0.01,gs_seat_height-0.015+0.2
			BLOCK (gs_seat_thk-0.03),0.04,0.015
			gosub 5000
			DEL 1
		else
			ADD -A/2-0.01,B-(gs_seat_thk+0.01),gs_seat_height-0.015+0.2
			BLOCK 0.04,(gs_seat_thk+0.02),0.015
			gosub 3000
			DEL 1
			ADD A/2-(gs_seat_thk+0.01),-0.01,gs_seat_height-0.015+0.2
			BLOCK (gs_seat_thk+0.02),0.04,0.015
			gosub 5000
			DEL 1
		endif
	endif

	gosub 5000

RETURN

! ======================================================================
2:				!STRAIGHT STYLE
! ======================================================================

	ADDX -A/2
! ----------------------------------------------------------------------
! legs
! ----------------------------------------------------------------------
	if  gs_armrest then
		legMod = 0.015-0.2
	else
		legMod = 0.015
	endif

	MATERIAL gs_frame_mat
		BLOCK 0.02,0.02,gs_seat_height-legMod
	ADDY gs_seat_thk-0.02
	if gs_backrest then
		BLOCK 0.02,0.02, zzyzx
	else
		BLOCK 0.02,0.02,gs_seat_height-legMod
	endif
	DEL 1

	ADDX A-0.02
		BLOCK 0.02,0.02,gs_seat_height-legMod
	ADDY gs_seat_thk-0.02
	if gs_backrest then
		BLOCK 0.02,0.02, zzyzx
	else
		BLOCK 0.02,0.02,gs_seat_height-legMod
	endif
	DEL 1

	ADDX -A/2+0.01
	BLOCK 0.02,0.02,gs_seat_height-0.015
	ADDY gs_seat_thk-0.02
	IF gs_backrest=0 THEN
		BLOCK 0.02,0.02,gs_seat_height-0.015
	ENDIF
	IF gs_backrest=1 THEN
		BLOCK 0.02,0.02, zzyzx
	ENDIF
	DEL 3

	gosub 1000

	! ===

	ADD 0.02,0,gs_seat_height-0.035
	BLOCK A/2-0.03,0.02,0.02
	gosub 4000
	ADDX A/2-0.01
	BLOCK A/2-0.03,0.02,0.02
	gosub 4000
	DEL 2
	ADDY gs_seat_thk-0.02
	ADD 0.02,0,gs_seat_height-0.035
	BLOCK A/2-0.03,0.02,0.02
	gosub 4000
	ADDX A/2-0.01
	BLOCK A/2-0.03,0.02,0.02
	gosub 4000

	DEL 3

	ADD 0,0.02,gs_seat_height-0.035
	BLOCK 0.02,gs_seat_thk-0.04,0.02
	ADDX A/2-0.01
	BLOCK 0.02,gs_seat_thk-0.04,0.02
	ADDX A/2-0.01
	BLOCK 0.02,gs_seat_thk-0.04,0.02
	DEL 3
	gosub 3000

! ----------------------------------------------------------------------
	! seat
! ----------------------------------------------------------------------
	MATERIAL gs_seat_mat
	ADDZ gs_seat_height-0.015
	IF gs_seat_style_m = PLANK THEN
		IF gs_armrest=0 AND gs_backrest=1 THEN
			PRISM_    12,    0.015,
				0,0,15,
				A,0,15,
				A,gs_seat_thk-0.02,15,
				A-0.02,gs_seat_thk-0.02,15,
				A-0.02,gs_seat_thk,15,
				A/2+0.01,gs_seat_thk,15,
				A/2+0.01,gs_seat_thk-0.02,15,
				A/2-0.01,gs_seat_thk-0.02,15,
				A/2-0.01,gs_seat_thk,15,
				0.02,gs_seat_thk,15,
				0.02,gs_seat_thk-0.02,15,
				0,gs_seat_thk-0.02,15
		ENDIF
		IF gs_armrest=1 AND gs_backrest=0 THEN
			PRISM_    12,    0.015,
				0.02,0,15,
				A-0.02,0,15,
				A-0.02,0.02,15,
				A,0.02,15,
				A,gs_seat_thk-0.02,15,
				A-0.02,gs_seat_thk-0.02,15,
				A-0.02,gs_seat_thk,15,
				0.02,gs_seat_thk,15,
				0.02,gs_seat_thk-0.02,15,
				0,gs_seat_thk-0.02,15,
				0,0.02,15,
				0.02,0.02,15
		ENDIF
		IF gs_armrest=1 AND gs_backrest=1 THEN
			PRISM_    16,    0.015,
				0.02,0,15,
				A-0.02,0,15,
				A-0.02,0.02,15,
				A,0.02,15,
				A,gs_seat_thk-0.02,15,
				A-0.02,gs_seat_thk-0.02,15,
				A-0.02,gs_seat_thk,15,
				A/2+0.01,gs_seat_thk,15,
				A/2+0.01,gs_seat_thk-0.02,15,
				A/2-0.01,gs_seat_thk-0.02,15,
				A/2-0.01,gs_seat_thk,15,
				0.02,gs_seat_thk,15,
				0.02,gs_seat_thk-0.02,15,
				0,gs_seat_thk-0.02,15,
				0,0.02,15,
				0.02,0.02,15
		ENDIF
		IF gs_armrest=0 AND gs_backrest=0 THEN
			PRISM_    4,    0.015,
				0,0,15,
				A,0,15,
				A,gs_seat_thk,15,
				0,gs_seat_thk,15
		ENDIF
		DEL 1
		gosub 5000
	ELSE
		IF  gs_backrest=1 THEN
			PRISM_    12,    0.015,
					0,			gs_seat_thk-0.07,	15,
					A,			gs_seat_thk-0.07,	15,
					A,			gs_seat_thk-0.02,	15,
					A-0.02,		gs_seat_thk-0.02,	15,
					A-0.02,		gs_seat_thk,		15,
					A/2+0.01,	gs_seat_thk,		15,
					A/2+0.01,	gs_seat_thk-0.02,	15,
					A/2-0.01,	gs_seat_thk-0.02,	15,
					A/2-0.01,	gs_seat_thk,		15,
					0.02,		gs_seat_thk,		15,
					0.02,		gs_seat_thk-0.02,	15,
					0,			gs_seat_thk-0.02,	15
		ENDIF
		IF  gs_backrest=0 AND gs_armrest=1 THEN
			PRISM_    8,    0.015,
					0,		gs_seat_thk-0.07,	15,
					A,		gs_seat_thk-0.07,	15,
					A,		gs_seat_thk-0.02,	15,
					A-0.02,	gs_seat_thk-0.02,	15,
					A-0.02,	gs_seat_thk,		15,
					0.02,	gs_seat_thk,		15,
					0.02,	gs_seat_thk-0.02,	15,
					0,		gs_seat_thk-0.02,	15
		ENDIF
		IF  gs_backrest=0 AND gs_armrest=0 THEN
			PRISM_    4,    0.015,
					0,	gs_seat_thk-0.07,	15,
					A,	gs_seat_thk-0.07,	15,
					A,	gs_seat_thk,		15,
					0,	gs_seat_thk,		15
		ENDIF
		IF gs_armrest=0 THEN
			BLOCK A,0.07,0.015
		ELSE
			PRISM_    8,    0.015,
					0.02,	0,		15,
					A-0.02,	0,		15,
					A-0.02,	0.02,	15,
					A,		0.02,	15,
					A,		0.07,	15,
					0,		0.07,	15,
					0,		0.02,	15,
					0.02,	0.02,	15
		ENDIF

		gosub 5000

		N=INT((gs_seat_thk+0.025)/0.095)
		DX=gs_seat_thk-(N*0.07+(N-1)*0.025)
		DK=0.025+DX/(N-1)
		ADDY 0.07+DK
		FOR I=1 TO N-2
			 BLOCK A,0.07,0.015
			 ADDY 0.07+DK
		NEXT I
		DEL N
	ENDIF

	IF gs_backrest THEN
		MATERIAL gs_back_mat
		ADD 0.02,gs_seat_thk-0.02, zzyzx-0.07
		BLOCK A/2-0.03,0.02,0.06
		ADDZ -0.12
		BLOCK A/2-0.03,0.02,0.06
		DEL 1
		ADDX A/2-0.01
		BLOCK A/2-0.03,0.02,0.06
		ADDZ -0.12
		BLOCK A/2-0.03,0.02,0.06
		DEL 3
	ENDIF

	gosub 5000

	IF gs_armrest THEN
		MATERIAL gs_frame_mat
		ADD 0,0.02,gs_seat_height-0.035+0.2
		BLOCK 0.02,gs_seat_thk-0.04,0.02
		gosub 5000
		ADD -0.005,-0.025,0.02
		MATERIAL gs_seat_mat

		IF gs_backrest=1 THEN
			BLOCK 0.03,(gs_seat_thk-0.015),0.015
		ELSE
			BLOCK 0.03,(gs_seat_thk+0.01),0.015
		ENDIF
		gosub 3000

		DEL 1
		ADDX A-0.02
		MATERIAL gs_frame_mat
		BLOCK 0.02,gs_seat_thk-0.04,0.02
		gosub 5000
		ADD -0.005,-0.025,0.02
		MATERIAL gs_seat_mat

		IF gs_backrest=1 THEN
			BLOCK 0.03, gs_seat_thk-0.015, 0.015
		ELSE
			BLOCK 0.03, gs_seat_thk+0.01, 0.015
		ENDIF
		gosub 3000

		DEL 3
	ENDIF
	DEL 1
RETURN


END

1000:
	roty 90
	gosub 5000
	del 1
return

3000:
	rotz 90
	gosub 5000
	del 1
return

4000:
	rotx 90
	gosub 5000
	del 1
return


5000:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4
	base
	body -1
return
