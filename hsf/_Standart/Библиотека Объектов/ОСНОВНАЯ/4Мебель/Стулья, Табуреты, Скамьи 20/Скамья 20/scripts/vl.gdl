
HIDEPARAMETER ALL "A", "B", "zzyzx"

call "FM_types" parameters all

values "gs_detlevel_3D" stDetlevel3D
values "gs_detlevel_3D_m" DETLEV_OFF, DETLEV_SIMPLE, DETLEV_DETAILED

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DETLEV_SIMPLE
	if gs_detlevel_3D = stDetlevel3D[1] 	then gs_detlevel_3D_m = DETLEV_OFF
	if gs_detlevel_3D = stDetlevel3D[3]		then gs_detlevel_3D_m = DETLEV_DETAILED
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetlevel3D[2]
	if gs_detlevel_3D_m = DETLEV_OFF		then gs_detlevel_3D = stDetlevel3D[1]
	if gs_detlevel_3D_m = DETLEV_DETAILED	then gs_detlevel_3D = stDetlevel3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! ======================================================================
! 2D Detail Level
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D = gs_detlevel_2D,
								gs_detlevel_2D_m = gs_detlevel_2D_m,
								enable2dDetLevels = 2 + 4 + 8 + 16

! enable2dDetLevels - possible values
!	2	- enable scale sansitive
!	4	- enable 1:50, detailed
!	8	- enable 1:100, middle
!	16	- enable 1:200, simple
!	32	- enable symbolic

! ======================================================================

values "gs_chair_style" stChairStyle
values "gs_chair_style_m" CORNER, STRAIGHT

if GLOB_MODPAR_NAME = "gs_chair_style" then
	gs_chair_style_m = CORNER
	if gs_chair_style = stChairStyle[2] 	then gs_chair_style_m = STRAIGHT
	parameters gs_chair_style_m = gs_chair_style_m
else
	gs_chair_style = stChairStyle[1]
	if gs_chair_style_m = STRAIGHT		then gs_chair_style = stChairStyle[2]
	parameters gs_chair_style = gs_chair_style
endif

values "gs_seat_style" stSeatStyle
values "gs_seat_style_m" LATH, PLANK

if GLOB_MODPAR_NAME = "gs_seat_style" then
	gs_seat_style_m = LATH
	if gs_seat_style = stSeatStyle[2] 	then gs_seat_style_m = PLANK
	parameters gs_seat_style_m = gs_seat_style_m
else
	gs_seat_style = stSeatStyle[1]
	if gs_seat_style_m = PLANK		then gs_seat_style = stSeatStyle[2]
	parameters gs_seat_style = gs_seat_style
endif

if gs_detlevel_3D_m = DETLEV_OFF then
	hideparameter "gs_shadow", "gs_full_edit"
	lock "gs_shadow", "gs_full_edit"
endif

values "gs_seat_thk" range [0.19, MIN(A,B)-0.05]
values "gs_seat_height" range [0.2,zzyzx-0.2]

if gs_chair_style_m = STRAIGHT then values "a" range [0.07,)

if gs_backrest then values "gs_seat_height" range (0, zzyzx - 0.2]

if gs_armrest=0 or gs_backrest=0 then lock "zzyzx"

if not (gs_backrest) then
	hideparameter "gs_back_mat"
	lock "gs_back_mat"
endif

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19

if (gs_chair_style_m = STRAIGHT | (gs_chair_style_m = CORNER & gs_seat_style_m = PLANK)) then
	hideparameter "bMiteredLath"
	lock "bMiteredLath"
endif

!!! ******************************** Minimal Space
values "MSFront" range [0, )
