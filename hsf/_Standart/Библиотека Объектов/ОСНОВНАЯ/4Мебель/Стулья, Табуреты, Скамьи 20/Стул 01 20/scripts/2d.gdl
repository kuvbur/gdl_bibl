
! ======================================================================
! SCALE
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

if det2D = 1 then 	CALL "fa_ChairSymbol" PARAMETERS ALL gs_SymbolType_m=1		!! 1/200
if det2D = 2 then 	CALL "fa_ChairSymbol" PARAMETERS ALL gs_SymbolType_m=2		!! 1/100
if det2D = 4 then 	CALL "fa_ChairSymbol" PARAMETERS ALL 						!! Symbolic

if det2D = 3 then 	! <= 1/75

	! --- Minimal Space and Hotspots ---

	call "fa_ChairSymbol" PARAMETERS ALL gs_SymbolType_m=0


	! --- Symbol ---

	H = ZZYZX/40
	postWidthPer2 = A * 0.03
	PEN gs_cont_pen
	fill gs_fill_type

	! seat
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,0,901,
			a/2,360,4001

	add2 0, A/2*sin(45) - A * 0.06
	! right backrest post
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			A/2*cos(45)+postWidthPer2, 3.8*H, 1,
			A/2*cos(45)-postWidthPer2, 3.8*H, 1,
			A/2*cos(45)-postWidthPer2, 0, 1,
			A/2*cos(45)+postWidthPer2, 0, 1
	line2  A/2*cos(45)-postWidthPer2, 3*H, A/2*cos(45)+postWidthPer2, 3*H

	! left backrest post
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-A/2*cos(45)-postWidthPer2, 3.8*H, 1,
			-A/2*cos(45)+postWidthPer2, 3.8*H, 1,
			-A/2*cos(45)+postWidthPer2, 0, 1,
			-A/2*cos(45)-postWidthPer2, 0, 1
	line2  -A/2*cos(45)+postWidthPer2,3*H, -A/2*cos(45)-postWidthPer2, 3*H
	del 1

	! backrest
	Alfa1 = atn((A/2*cos(45)-postWidthPer2)/(A/2*sin(45)+3.8*H - A * 0.06))
	arc2 0,0,(A/2*sin(45)+3.8*H - A * 0.06)/cos(Alfa1),90-Alfa1,90+Alfa1

endif
