
if gs_detlevel_3D_m = 0 then END

PEN gs_cont_pen


if not(gs_shadow) then SHADOW OFF


if gs_detlevel_3D_m=1 then gs_resol=12

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

	C=gs_seat_height

	unID=1

	HOTSPOT -a/2, -b/2, 0, unID: unID=unID+1
	HOTSPOT  a/2, -b/2, 0, unID: unID=unID+1
	HOTSPOT -a/2,  b/2, 0, unID: unID=unID+1
	HOTSPOT  a/2,  b/2, 0, unID: unID=unID+1

!Hotspot Editing - zzyzx

	hotspot A/2*COS(45)+0.45/40, A/2*COS(45)+2*0.45/40, 0, unID, zzyzx, 1+128: unID=unID+1
	hotspot A/2*COS(45)+0.45/40, A/2*COS(45)+2*0.45/40, -1, unID, zzyzx, 3: unID=unID+1
	hotspot A/2*COS(45)+0.45/40, A/2*COS(45)+2*0.45/40, zzyzx, unID, zzyzx, 2: unID=unID+1

	hotspot -A/2*COS(45)-0.45/40, A/2*COS(45)+2*0.45/40, 0, unID, zzyzx, 1+128: unID=unID+1
	hotspot -A/2*COS(45)-0.45/40, A/2*COS(45)+2*0.45/40, -1, unID, zzyzx, 3: unID=unID+1
	hotspot -A/2*COS(45)-0.45/40, A/2*COS(45)+2*0.45/40, zzyzx, unID, zzyzx, 2: unID=unID+1

!Hotspot editing - seat height
if gs_full_edit=1 then

	hotspot -a/2, 0, 0, unID, gs_seat_height, 1+128: unID=unID+1
	hotspot -a/2, 0, -1, unID, gs_seat_height, 3: unID=unID+1
	hotspot -a/2, 0, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

	hotspot a/2, 0, 0, unID, gs_seat_height, 1+128: unID=unID+1
	hotspot a/2, 0, -1, unID, gs_seat_height, 3: unID=unID+1
	hotspot a/2, 0, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1
endif
!End of hotspot editing

RESOL gs_resol

material gs_seat_mat
addZ C-0.02
	CYLIND 0.02,A/2
del 1

H=0.45/40

!-----------------------------------------------------------------------------------------------
!legs
!-----------------------------------------------------------------------------------------------


material gs_frame_mat
add A/2*COS(45),A/2*COS(45),0
for K=1 to 2
	for I=1 to 2
		RULED 4,119,
				-H,-H,0,
				-H,H,0,
				H,H,0,
				H,-H,0,
				-H, -3*H-0.15/40, C-0.02,
				-H, -H-0.15/40, C-0.02,
				H,-H-0.15/40, C-0.02,
				H,-3*H-0.15/40, C-0.02
		gosub 1000
		addX -A*COS(45)
	next I
	del 3
	muly -1
	add A/2*COS(45),A/2*COS(45),0
next K
del 3


!-----------------------------------------------------------------------------------------------
!back modes according to height
!-----------------------------------------------------------------------------------------------

if (toth - 10 * H) >= eps then
	material gs_back_mat

	add A/2*COS(45),A/2*COS(45)-2*H,C
	! fix part of backrest
	for I=1 to 2
		PRISM_ 4,3*H,
				-H,-H,11,
				-H,H,11,
				H,H,11,
				H,-H,11
		addX -A*COS(45)
	next I
	del 3

	add A/2 * COS(45), A/2 * COS(45) - 2 * H, C + 3 * H
	! moving part of backrest
	for I=1 to 2
		RULED 4, 111,
				-H, -H, 0,
				-H, H, 0,
				H, H, 0,
				H, -H, 0,
				-H, 3 * H, toth - 3 * H,
				-H, 4 * H, toth - 3 * H,
				H, 4 * H, toth - 3 * H,
				H, 3 * H, toth - 3 * H
		addX -A * COS(45)
	next I
	gosub 1000
	del 3

	beta = atn((toth - 3 * H) / (3 * H))
	add - 0, A / 2 * COS(45) - H, C + 3 * H
	rotx Beta
	CUTPLANE 0
	del 2

	! 1-2 rows in backrest
	ADD 0, A/2 * cos(45) - H, C + 3 * H
	ROTX Beta
	ADD 0, H, A/2 * cos(45) - 1.8 * H
	ROTZ 90
	ADDX  toth - 5 * H
	radBackrest =sqr((A/2 * cos(45) - 2 * H) ^ 2 + (A/2 * cos(45)) ^ 2)
	revolve 5, 360, 63,
			0, radBackrest, 0,
			- 5 * H, radBackrest, 0,
			- 5 * H, radBackrest + 0.5 * H, 0,
			0, radBackrest + 0.5 * H, 0,
			0, radBackrest, 0

	if (toth - 17 * H) >= eps then
		ADDX - 6 * H
		revolve 5, 360, 63,
				0, radBackrest, 0,
				- 5 * H, radBackrest, 0,
				- 5 * H, radBackrest + 0.5 * H, 0,
				0, radBackrest + 0.5 * H, 0,
				0, radBackrest, 0

		DEL 1
	endif
	roty 90
	gosub 2000
	del 1
	DEL 5
	CUTEND
endif
!End of detailed 3D


END

1000:
	roty 90
	base
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body -1
	del 1
return



2000:
	base
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 5, -1, -2, -3, -4
	body -1
return

