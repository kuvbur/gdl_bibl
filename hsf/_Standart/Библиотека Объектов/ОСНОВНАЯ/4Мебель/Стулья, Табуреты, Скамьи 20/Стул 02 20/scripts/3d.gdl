
!!! Chair 01.gsm !!!


IF gs_detlevel_3D_m=0 THEN END

PEN gs_cont_pen

IF gs_shadow=0 THEN SHADOW OFF


IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

C=gs_seat_height
ADD a/2,b/2,0

	unID=1

	HOTSPOT -a/2, -b/2, 0, unID: unID=unID+1
	HOTSPOT  a/2, -b/2, 0, unID: unID=unID+1
	HOTSPOT -a/2,  b/2, 0, unID: unID=unID+1
	HOTSPOT  a/2,  b/2, 0, unID: unID=unID+1

!!! *** Hotspot Editing - zzyzx *** !!!

	hotspot -a/2, b/2, 0, unID, zzyzx, 1+128: unID=unID+1
	hotspot -a/2, b/2, -1, unID, zzyzx, 3: unID=unID+1
	hotspot -a/2, b/2, zzyzx, unID, zzyzx, 2: unID=unID+1

	hotspot a/2, b/2, 0, unID, zzyzx, 1+128: unID=unID+1
	hotspot a/2, b/2, -1, unID, zzyzx, 3: unID=unID+1
	hotspot a/2, b/2, zzyzx, unID, zzyzx, 2: unID=unID+1

!!! *** Hotspot editing - seat height *** !!!
IF gs_full_edit=1 THEN

		hotspot -a/2, -b/2, 0, unID, gs_seat_height, 1+128: unID=unID+1
		hotspot -a/2, -b/2, -1, unID, gs_seat_height, 3: unID=unID+1
		hotspot -a/2, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

		hotspot a/2, -b/2, 0, unID, gs_seat_height, 1+128: unID=unID+1
		hotspot a/2, -b/2, -1, unID, gs_seat_height, 3: unID=unID+1
		hotspot a/2, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

ENDIF
!! *** End of hotspot editing *** !!

!! Simple 3D !!
IF gs_detlevel_3D_m=1 THEN
MATERIAL gs_seat_mat
	 ADDZ gs_seat_height-gs_seat_thk
		PRISM_ 5, gs_seat_thk,		! Seat prism
			-A/2,-B/2,15,
			-A/2,B/2,15,
			A/2,B/2,15,
			A/2,-B/2,15,
			-A/2,-B/2,-1
	   DEL 1

		GOSUB 1000

MATERIAL gs_frame_mat


ADD -A/2, -B/2,0
	BLOCK H,H, gs_seat_height-gs_seat_thk
DEL 1

ADD  A/2-h, -b/2, 0
	BLOCK H,H, gs_seat_height-gs_seat_thk
DEL 1

ADD A/2-h, B/2-h, 0
	BLOCK H, H, gs_seat_height-gs_seat_thk
DEL 1

ADD -A/2,  B/2-h, 0
	BLOCK H, H, gs_seat_height-gs_seat_thk
DEL 1

MATERIAL gs_back_mat
ADD -A/2,  B/2-h, gs_seat_height
	BLOCK a, H, toth
DEL 1

gosub 1000

ENDIF
!! End of simple 3D !!

!! Detailed 3D !!
IF gs_detlevel_3D_m=2 THEN
MULY -1

MATERIAL gs_seat_mat
	 ADDZ C-gs_seat_thk


		PRISM_ 8, gs_seat_thk,		! Seat prism
			-A/2+H,-B/2,15,
			-A/2+H,-B/2+H,15,
			-A/2,-B/2+H,15,
			-A/2,B/2,15,
			A/2,B/2,15,
			A/2,-B/2+H,15,
			A/2-H,-B/2+H,15,
			A/2-H,-B/2,15

		GOSUB 2000


	DEL 1

	MATERIAL gs_frame_mat

	FOR I= 1 TO 2
		ADD -A/2,-B/2,0
			BLOCK H,H,C+toth
		DEL 1

		ADD -A/2,B/2-H,0
			BLOCK H,H,C-gs_seat_thk
		DEL 1
		MULX -1
	NEXT I

	gosub 1000

	FOR I= 1 TO 2

		ADD -A/2+H/2,-B/2+H,C-C/8-gs_seat_thk
			BLOCK H/2,B-2*H,C/8
		DEL 1

		ADD -A/2+H/2,-B/2+H,0.5*C
			BLOCK H/2,B-2*H,C/8
		DEL 1
		MULX -1

	NEXT I

	gosub 3000

	MATERIAL gs_back_mat


	 ADD -A/2+H,-B/2,c+toth-toth/6-toth/15
		 BLOCK A-2*H,H/2,toth/6
	 DEL 1


	 ADD -A/2+H,-B/2,c+toth-toth/6-toth/15-toth/4
		 BLOCK A-2*H,H/2,toth/6
	 DEL 1

	gosub 2000

del 5
ENDIF
del 1
!! End of detailed 3D !!


END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

3000:
rotz 90
gosub 5000
del 1
return

5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return




