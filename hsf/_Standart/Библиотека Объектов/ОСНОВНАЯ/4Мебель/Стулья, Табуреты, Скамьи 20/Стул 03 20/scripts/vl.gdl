
HIDEPARAMETER ALL "A", "B", "zzyzx"

call "FM_types" parameters all

values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_SIMPLE, `Простой`,
								DLEV3D_OFF, `Откл.`

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

if gs_detlevel_2D_m <> 4 then
	HIDEPARAMETER "gs_SymbolType"
	lock "gs_SymbolType", "gs_SymbolType_m"
endif

call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

VALUES "gs_seat_height" RANGE [0.1, zzyzx]

if gs_cont_pen<=0 then parameters gs_cont_pen=4
if gs_fill_pen<=0 then parameters gs_fill_pen=19

If gs_detlevel_3D_m = DLEV3D_OFF then HIDEPARAMETER "gs_shadow"

!!! ******************************** Minimal Space

VALUES "MSFront" RANGE [0,)
VALUES "MSSide" RANGE [0,)
VALUES "MSRear" RANGE [0,)

VALUES "gs_seat_form" stSeatForm
VALUES "gs_seat_form_m" 1, 2

IF GLOB_MODPAR_NAME = "gs_seat_form" THEN
	gs_seat_form_m = 1
	IF gs_seat_form = stSeatForm[2] THEN gs_seat_form_m = 2
	PARAMETERS gs_seat_form_m = gs_seat_form_m
ELSE
	gs_seat_form = stSeatForm[1]
	IF gs_seat_form_m = 2 THEN gs_seat_form = stSeatForm[2]
	PARAMETERS gs_seat_form = gs_seat_form
ENDIF

VALUES "gs_seat_cover" stSeatCover
VALUES "gs_seat_cover_m" 1, 2, 3

IF GLOB_MODPAR_NAME = "gs_seat_cover" THEN
	IF gs_seat_cover = stSeatCover[1] THEN gs_seat_cover_m = 1
	IF gs_seat_cover = stSeatCover[2] THEN gs_seat_cover_m = 2
	IF gs_seat_cover = stSeatCover[3] THEN gs_seat_cover_m = 3
	PARAMETERS gs_seat_cover_m = gs_seat_cover_m
ELSE
	IF gs_seat_cover_m = 1 THEN gs_seat_cover = stSeatCover[1]
	IF gs_seat_cover_m = 2 THEN gs_seat_cover = stSeatCover[2]
	IF gs_seat_cover_m = 3 THEN gs_seat_cover = stSeatCover[3]
	PARAMETERS gs_seat_cover = gs_seat_cover
ENDIF

IF gs_seat_cover_m = 1 | gs_seat_cover_m = 3 THEN
	HIDEPARAMETER "gs_seat_mat"
	LOCK "gs_seat_mat"
ENDIF
