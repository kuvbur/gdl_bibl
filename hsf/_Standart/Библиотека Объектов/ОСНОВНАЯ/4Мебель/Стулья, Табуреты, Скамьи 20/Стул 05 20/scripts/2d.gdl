
! ======================================================================
! SCALE
! ======================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

! 2D SYMBOL


IF det2D = 1 THEN 	CALL "fa_ChairSymbol" PARAMETERS ALL gs_SymbolType_m=3		! 1/200
IF det2D = 2 THEN 	CALL "fa_ChairSymbol" PARAMETERS ALL gs_SymbolType_m=4		! 1/100
IF det2D = 4 THEN 	CALL "fa_ChairSymbol" PARAMETERS ALL 						! Symbolic

IF gs_SymbolType_m=1 OR gs_SymbolType_m=2 THEN
	IF ABS(A-B) > EPS THEN
		HOTSPOT2 0,0
		HOTSPOT2 A,0
		HOTSPOT2 A,B
		HOTSPOT2 0,B
	ENDIF
ENDIF

! 1/50
IF det2D = 3 THEN

	PEN gs_cont_pen
	! Minimal Space and Hotspots

	DRAWINDEX 10
	CALL "fa_ChairSymbol" PARAMETERS ALL gs_SymbolType_m=0

	! Background Fill and Symbol
	FILL gs_fill_type

	! ARM
	IF gs_armrest=1 then
		DRAWINDEX 30

		ARC1_X=(RR3-RR)*COS(2*(RR3_angle+RR_angle))
		ARC2_X=(RR3+1.5*RR)*COS(2*(RR3_angle+RR_angle))
		ARC1_Y=(RR3-RR)*SIN(2*(RR3_angle+RR_angle))
		ARC2_Y=(RR3+1.5*RR)*SIN(2*(RR3_angle+RR_angle))

		for hh=1 to 2
		ADD2 -RR3+arm_xy/2+(a-arm_xy-RR),b/2
		rot2 -(RR3_angle+RR_angle)

			ARC2 0,0,RR3-RR,0,2*(RR3_angle+RR_angle)
			ARC2 0,0,RR3+1.5*RR,0,2*(RR3_angle+RR_angle)

			LINE2 RR3-RR,0,RR3+1.5*RR,0
			rot2 2*(RR3_angle+RR_angle)
				LINE2 RR3-RR,0,RR3+1.5*RR,0
			DEL 1

			POLY2_B 7, 2+4+64, gs_fill_pen, gs_back_pen,
				RR3-RR,0,1,
				RR3+1.5*RR,0,1,
				0,0,901,
				RR3+1.5*RR,2*(RR3_angle+RR_angle),4001,
				ARC1_X, ARC1_Y,1,
				0,0,901,
				RR3-RR,-2*(RR3_angle+RR_angle),4001

		DEL 2
		mul2 -1,1
		ADD2 -a,0
		next hh
		del 4
	ENDIF

	! SEAT
	DRAWINDEX 20
	ADD2 leg_xy,leg_xy		!arc of cushion=0.05 see at call of CALL "fa_cushion_backrest"
		POLY2_B 12, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,0.05,1,
			0.05,0.05,901,
			0.05,90,4001,
			a-2*leg_xy-0.05,0,1,
			a-2*leg_xy-0.05,0.05,901,
			0.05,90,4001,
			a-2*leg_xy,b-2*leg_xy-0.05,1,
			a-2*leg_xy-0.05,b-2*leg_xy-0.05,901,
			0.05,90,4001,
			0.05,b-2*leg_xy,1,
			0.05,b-2*leg_xy-0.05,901,
			0.05,90,4001
	DEl 1

	! LINE of Backrest

	ADD2 arm_xy+RR,b-arm_xy-backrest_thk/2-RR
		LINE2 0,0,a-2*arm_xy-2*RR,0
	DEL 1

	! LEGS
	alfa2D=ASN(RR/0.05)
	D1=0.05*cos(alfa2D)
	D2=(0.05+leg_xy)/sin(45)
	D3=RR/sin(45)
	D4=D2-D1-D3

	for jj=1 to 2
		for gg=1 to 2
			rot2 45
			Add2 D3,0
			POLY2_B 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,RR,1,
				0,0,901,
				RR,180,4001,
				D4,-RR,1,
				D4+D1,0,901,
				0.05,-2*alfa2D,4001
			DEL 2
		rot2 90
		add2 0,-a
		Next gg
		del 4
	mul2 1,-1
	add2 0,-b
	Next jj
	del 4

ENDIF
