
HIDEPARAMETER ALL "A", "B", "zzyzx"

call "FM_types" parameters all

IF iChairModelStyle = 1 THEN
	values "zzyzx" range[(A/2-2*(A/42))/TAN(57)+0.5, )
ENDIF

IF iChairModelStyle = 2 THEN
	values "zzyzx" range[(A-G-A/8.6)*13/12, )
ENDIF

if iChairModelStyle = 4 THEN
	values "zzyzx" range[gs_seat_thk+A/5+0.001, )
ENDIF
values "gs_seat_thk" range[0, zzyzx*0.75]

values{2} "gs_detlevel_3D_m" 	DLEV3D_DETAILED, `Детальный`,
								DLEV3D_SIMPLE, `Простой`,
								DLEV3D_OFF, `Откл.`
values{2} "iChairModelStyle" 	CHAIRMODEL_STYLE1, `Стиль 1`,
								CHAIRMODEL_STYLE2, `Стиль 2`,
								CHAIRMODEL_STYLE3, `Стиль 3`,
								CHAIRMODEL_STYLE4, `Стиль 4`,
								CHAIRMODEL_STYLE5, `Стиль 5`

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

if gs_detlevel_2D_m <> 4 then
	HIDEPARAMETER "gs_SymbolType"
	lock "gs_SymbolType", "gs_SymbolType_m"
endif

IF gs_cont_pen<=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen<=0 THEN PARAMETERS gs_fill_pen=19
call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

values "gs_resol" range [3, )

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif


if GLOB_MODPAR_NAME = "temp_AA" then
	parameters A = temp_AA * 2
	parameters B = temp_AA * 2
	parameters temp_BB = temp_AA
else
	if GLOB_MODPAR_NAME = "temp_BB" then
		parameters A = temp_BB * 2
		parameters B = temp_BB * 2
		parameters temp_AA = temp_BB
	else
		if GLOB_MODPAR_NAME = "B" then
			parameters A = B
			parameters temp_AA = B / 2
			parameters temp_BB = B / 2
		else
			parameters B = A
			parameters temp_AA = A / 2
			parameters temp_BB = A / 2
		endif
	endif
endif

! --- Default Symbolic Value ---

IF GLOB_MODPAR_NAME = "iChairModelStyle"  THEN
	IF iChairModelStyle = CHAIRMODEL_STYLE5 AND gs_detlevel_2D_m = 4 THEN PARAMETERS gs_SymbolType = `Прямоугольный`
	IF iChairModelStyle <> CHAIRMODEL_STYLE5 AND gs_detlevel_2D_m = 4 THEN PARAMETERS gs_SymbolType = `Круглый`
ENDIF


! --- Minimal Space ---

VALUES "MSFront" RANGE [0,)
VALUES "MSSide" RANGE [0,)
VALUES "MSRear" RANGE [0,)
