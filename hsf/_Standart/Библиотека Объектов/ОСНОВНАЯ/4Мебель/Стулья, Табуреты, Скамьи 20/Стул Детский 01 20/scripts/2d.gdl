
! HighChair 01 2D Script

! ==============================================================================
! SCALE
! ==============================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D



! ------------------------------------------------------------------------------
!  Hotspots 
! ------------------------------------------------------------------------------
unID=1

hotspot2 a/2,	0,		unID: unID=unID+1
hotspot2 a/2,	b/2,	unID: unID=unID+1

! ==============================================================================
! 2D Symbol and Minimal Space
! ==============================================================================

fill gs_fill_type
pen gs_cont_pen

fill gs_fill_type

if det2D = 4 then				! Symbolic
	call "fa_ChairSymbol" parameters all 	gs_OrigoCentered = 0
else
	call "Resize_A_B_ZZYZX" Parameters all 

	if det2D = 3 then			! 1:50
		gosub "DetailedSymbol"
		gosub "DetailedMinSpace"
	else
		if det2D = 1 then		! 1:200
			gosub "DraftSymbol"
			gosub "SimpleMinSpace"
		else					! 1:100
			gosub "SimpleSymbol"
			gosub "DetailedMinSpace"
		endif
	endif
endif

! ==============================================================================
  end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================

! ==============================================================================
! Minimal space subroutines
! ==============================================================================
"DetailedMinSpace":
	drawindex 10
	call "fa_minimalspace" parameters all MSType = 3, unID = 3000
return

"SimpleMinSpace":
	drawindex 10
	call "fa_minimalspace" parameters all MSType = 4, unID = 3000
return


! ==============================================================================
"DetailedSymbol":		! 1:50
! ==============================================================================
	add2 A/2,B/2
	Drawindex 30
	add2 0, -b/2 + _length1 + _armWidthTop+ _armWidthDelta
	! ---seating
	
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		_seatWidth/2,     _seatDepth/2,			0,
		-_seatWidth/2,    _seatDepth/2,			0,
		-_seatWidth/2,    -_seatDepth/2+_Rad,   15,
		_seatWidth/2,     -_seatDepth/2+_Rad,   0
	
	! --- Armrests
	
	add2 _seatWidth/2 + _armThickBot/2,0
	poly2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
		_armThickBot/2,			_armWidthDelta + _armWidthBot-_Rad,		1,
		_armThickBot/2-_Rad,	_armWidthDelta + _armWidthBot-_Rad,		900,
		0,						90,										4001,
	
		-_armThickBot/2,		_armWidthDelta + _armWidthBot,			1,
	
		-_armThickBot/2,		-_armWidthDelta - _armWidthBot+_Rad,	1,
		-_armThickBot/2+_Rad,	-_armWidthDelta - _armWidthBot+_Rad,	900,
		0,						180,									4001
	
	del 1
	
	mul2 -1,1
	add2 _seatWidth/2+ _armThickBot/2,0
	
	poly2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
		_armThickBot/2,			_armWidthDelta + _armWidthBot-_Rad,		1,
		_armThickBot/2-_Rad,	_armWidthDelta + _armWidthBot-_Rad,		900,
		0,						90,										4001,
	
		-_armThickBot/2,		_armWidthDelta + _armWidthBot,			1,
	
		-_armThickBot/2,		-_armWidthDelta - _armWidthBot+_Rad,	1,
		-_armThickBot/2+_Rad,	-_armWidthDelta - _armWidthBot+_Rad,	900,
		0,						180,									4001
	
	del 2
	! ----Backrest
	add2 0,_armWidthDelta + _armWidthBot+_backThickBot/2
	rot2 90
	poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
		_backThickBot/2,		_backWidthBot/2-_Rad,		1,
		_backThickBot/2-_Rad,	_backWidthBot/2-_Rad,		900,
		0,						180,						4001,
	
		-_backThickBot/2,		-_backWidthBot/2+_Rad,		1,
		-_backThickBot/2+_Rad,	-_backWidthBot/2+_Rad,		900,
		0,						180,						4001
	del 2
del 1
	! ------------- BackLegs: Footpad
	Drawindex 20
	!-- Footpad
	add2 a/2 - legThick,  b/2-legThick
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,          0,      900+15,
			legThick,   360,    4000+15
	del 1
	
	add2 -a/2+ legThick, b/2-legThick
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,          0,      900+15,
			legThick,   360,    4000+15
	del 1
	
	! ------------- BackLegs: Legs
	
	_XBackStart		= -a/2+legThick
	_YBackStart		=  b/2-legThick
	_XBack 			= -a/2+legThick +_seatWidth/2
	_YBack 			= b/2-legThick +  (_length2-_length1)/2-_seatDepth/2
	_AngBack 		= atn (_YBack/_XBack)
	_LenghtBack 	= SQR (_XBack*_XBack + _YBack*_YBack)
	
	
	add2 _XBackstart, _YBackStart
	rot2 -(90-_AngBack)
	add2 	-legThick/2,  0
	
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,0,1,
		legThick,0,1,
		legThick,_LenghtBack,1,
		0,_LenghtBack,1
	
	del 3
	
	mul2 -1,1
	add2 _XBackstart, _YBackStart
	rot2 -(90-_AngBack)
	add2 -legThick/2,  0
	
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,0,1,
		legThick,0,1,
		legThick,_LenghtBack,1,
		0,_LenghtBack,1
	
	del 4
	
	! ----------FrontLegs : Footpads

	add2 -a/2+legThick, -b/2 + legThick
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,          0,      900+15,
			legThick,   360,    4000+15
	del 1

	add2 a/2-legThick, -b/2 + legThick
	poly2_b 2, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,          0,      900+15,
			legThick,   360,    4000+15
	del 1

	! ----------FrontLegs : Legs

	_XFrontStart	= _seatWidth/2+_armThickBot/2
	_YFrontStart	= -b/2 + _length1
	_XFront 		= a/2 - _seatWidth/2-_armThickBot/2 - legThick
	_YFront 		= _length1 - legThick
	_AngFront		= atn (_YFront /_XFront )
	_LenghtFront 	= SQR (_XFront *_XFront  + _YFront *_YFront )


	add2 _XFrontstart, _YFrontStart
	rot2 180+(90-_AngFront)
	add2  -legThick/2,0

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,0,1,
		legThick,0,1,
		legThick,_LenghtFront,1,
		0,_LenghtFront,1

	del 3

	mul2 -1,1

	add2 _XFrontStart, _YFrontStart
	rot2 180+(90-_AngFront)
	add2 -legThick/2,0

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,0,1,
		legThick,0,1,
		legThick,_LenghtFront,1,
		0,_LenghtFront,1

	del 4
	del 1
return
! ==============================================================================
"SimpleSymbol":		! 1: 100
! ==============================================================================
	add2 A/2,B/2
	add2 0, -b/2 + _length1 + _armWidthTop+ _armWidthDelta	
	Drawindex 30
	
	! ---seating
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			_seatWidth/2,     _seatDepth/2,			0,
			-_seatWidth/2,    _seatDepth/2,			0,
			-_seatWidth/2,    -_seatDepth/2+_Rad,   15,
			_seatWidth/2,     -_seatDepth/2+_Rad,   0
	
	! --- Armrests
	
	add2 _seatWidth/2 + _armThickBot/2,0
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			_armThickBot/2,     _armWidthDelta + _armWidthBot,      15,
			-_armThickBot/2,    _armWidthDelta + _armWidthBot,      15,
			-_armThickBot/2,    -_armWidthDelta - _armWidthBot,     15,
			_armThickBot/2,     -_armWidthDelta - _armWidthBot,     15
	del 1
	add2 -_seatWidth/2- _armThickBot/2,0
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			_armThickBot/2,     _armWidthDelta + _armWidthBot,      15,
			-_armThickBot/2,    _armWidthDelta + _armWidthBot,      15,
			-_armThickBot/2,    -_armWidthDelta - _armWidthBot,     15,
			_armThickBot/2,     -_armWidthDelta - _armWidthBot,     15
	del 1
	! ----Backrest
	add2 0,_armWidthDelta + _armWidthBot+_backThickBot/2
	rot2 90
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			_backThickBot/2,    _backWidthBot/2,                  15,
			-_backThickBot/2,   _backWidthBot/2,                  15,
			-_backThickBot/2,   -_backWidthBot/2,                 15,
			_backThickBot/2,    -_backWidthBot/2,                 15
	del 2
	del 1
	! ------------- BackLegs
	Drawindex 20
	
	line2	-a/2,	b/2,
			-_seatWidth/2,	-b/2 + _length1 + _armWidthTop + _armWidthDelta*2+_armWidthBot+_backThickBot/2
	
	line2	a/2,	b/2,
			_seatWidth/2,	-b/2 + _length1 + _armWidthTop + _armWidthDelta*2+_armWidthBot+_backThickBot/2
	
	
	
	! ----------FrontLegs
	
	line2	a/2,	-b/2 ,
			_seatWidth/2+_armThickBot/2,	-b/2 + _length1
	
	line2	-a/2,	-b/2 ,
			-_seatWidth/2-_armThickBot/2,	-b/2 + _length1
	
	del 1
return

! ==============================================================================
"DraftSymbol":		! 1: 200
! ==============================================================================

	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,0,1,
		0,B,1,
		A,B,1,
		A,0,1
return
