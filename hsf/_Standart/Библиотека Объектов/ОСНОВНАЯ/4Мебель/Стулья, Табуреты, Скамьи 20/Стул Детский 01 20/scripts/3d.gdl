
! High Chair Plastic 3D Script

! ==============================================================================
! 3D Representation (detail level, resol, shadow)
! ==============================================================================

if gs_detlevel_3D_m=0 then END
resol gs_resol
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1
if gs_shadow=0 then shadow off

! ==============================================================================
! Hotspot Editing
! ==============================================================================
unID=1

! ----- A,B dimensions ---------------------------------------------------------
hotspot   0, 0, 0, unID: unID=unID+1
hotspot   A, 0, 0, unID: unID=unID+1
hotspot   A, B, 0, unID: unID=unID+1
hotspot   0, B, 0, unID: unID=unID+1

! ----- ZZYZX (Height) ---------------------------------------------------------
hotspot A/2,B- tan(90-_Ang3)*ZZYZX,	0, 		unID, 	zzyzx, 1+128: unID=unID+1
hotspot A/2,B- tan(90-_Ang3)*ZZYZX,	-1, 	unID,	zzyzx, 3:     unID=unID+1
hotspot A/2,B- tan(90-_Ang3)*ZZYZX,	zzyzx, 	unID, 	zzyzx, 2:     unID=unID+1

! ----- seat height ------------------------------------------------------------
hotspot A/2,B/2,	0, 					unID, gs_seat_height, 1+128: unID=unID+1
hotspot A/2,B/2,	-1, 						unID, gs_seat_height, 3: unID=unID+1
hotspot A/2,B/2,	gs_seat_height, 	unID, gs_seat_height, 2: unID=unID+1


! ==============================================================================
! 3D GEOMETRY
! ==============================================================================
pen gs_cont_pen	
add A/2,_length1 + _armWidthDelta + _armWidthBot,0

! ------------------------------------------------------------------------------
! Armrests
! ------------------------------------------------------------------------------
group "GroupArmrest"
 
	add _seatWidth/2+ _armThickBot/2,0,_seatHeight
	rotz 180
	gosub "Armrest"
	del 2
	
	add -_seatWidth/2- _armThickBot/2,0,_seatHeight
	gosub "Armrest"
	del 1
  
endgroup

! ------------------------------------------------------------------------------
! Backrest
! ------------------------------------------------------------------------------
group "GroupBackrest"
	add 0, _seatDepth/2 + _backThickBot/2,_seatHeight
	rotz -90
	gosub "Backrest"
	del 2
endgroup


group "GroupLink"
	add -_seatWidth/2-_armThickBot/2,_armWidthDelta,_seatHeight
	gosub "Link"
	del 1
	
	Mul -1,1,1
	add -_seatWidth/2-_armThickBot/2,_armWidthDelta,_seatHeight
	gosub "Link"
	del 2
endgroup

! ------------------------------------------------------------------------------
! Seating
! ------------------------------------------------------------------------------
group "GroupSeating"
    addz _seatHeight
    rotx 90
    addz -_seatDepth/2 
    
    if gs_detlevel_3D_m = 2 then
	    prism_ 8,_seatDepth-_Rad -2*_lidHeight,
		    _seatWidth/2,			  	0,						15+64,
		    _seatWidth/2,			  	_seatThick+_seatRad,	15+64,
		    _seatWidth/2-_seatRad,	  	_seatThick+_seatRad,	900,
		    _seatWidth/2-_seatRad,	  	_seatThick,				3000+15+64,
		    -(_seatWidth/2-_seatRad), 	_seatThick,				15+64,
		    -(_seatWidth/2-_seatRad), 	_seatThick+_seatRad,	900,
		    -(_seatWidth/2),			_seatThick+_seatRad,	3000+15+64,
		    -_seatWidth/2,			    0,						15+64
    else
	    prism_ 6,           			_seatDepth-_Rad -2*_lidHeight,
		    _seatWidth/2,				0,					        15,
		    _seatWidth/2,				_seatThick+_seatRad,	  	15,
		    _seatWidth/2-_seatRad,		_seatThick,			        15,
		    -(_seatWidth/2-_seatRad),	_seatThick,			      	15,
		    -(_seatWidth/2),			_seatThick+_seatRad,	  	15,
		    -_seatWidth/2,			    0,					        15
    endif
    
    addz _seatDepth-_Rad-2*_lidHeight
    
    if gs_detlevel_3D_m = 2 then
	    hprism_ gs_seat_mat,gs_seat_mat,gs_seat_mat,gs_seat_mat,
		    8,  _lidHeight, 			0,  _lidHeight, 1,
		    _seatWidth/2,			    0,                      15+64,
		    _seatWidth/2,			    _seatThick+_seatRad,	15+64,
		    _seatWidth/2-_seatRad,		_seatThick+_seatRad,	900,
		    _seatWidth/2-_seatRad,		_seatThick,			    3000+15+64,
		    -(_seatWidth/2-_seatRad),	_seatThick,				15+64,
		    -(_seatWidth/2-_seatRad),	_seatThick+_seatRad,	900,
		    -(_seatWidth/2),			_seatThick+_seatRad,	3000+15+64,
		    -_seatWidth/2,			    0,						15+64
    else
	    hprism_ gs_seat_mat,gs_seat_mat,gs_seat_mat,gs_seat_mat,
		    6,				_lidHeight,45,_lidHeight,1,
		    _seatWidth/2,				0,							15,
		    _seatWidth/2,				_seatThick+_seatRad,		15,
		    _seatWidth/2-_seatRad,		_seatThick,					15,
		    -(_seatWidth/2-_seatRad), 	_seatThick,					15,
		    -(_seatWidth/2),			_seatThick+_seatRad,		15,
		    -_seatWidth/2,				 0,							15
    endif
    del 4
endgroup

! ------------------------------------------------------------------------------
! Grouping operations
! ------------------------------------------------------------------------------

Union_backrest_link           			= addgroup ("GroupLink","GroupBackrest")
Union_backrest_armrest_link   			= addgroup(Union_backrest_link, "GroupArmrest")
Union_backrest_armrest_link_seating		= addgroup(Union_backrest_armrest_link, "GroupSeating")

placegroup Union_backrest_armrest_link_seating

killgroup "GroupArmrest"
killgroup "GroupBackrest"
killgroup "GroupLink"
killgroup "GroupSeating"

! ------------------------------------------------------------------------------
! Legs
! ------------------------------------------------------------------------------

add -A/2+_armThickBot/2,-_armWidthDelta -_armWidthBot-_length1+legThick,0
gosub  "FrontLegs"
del 1

add A/2-_armThickBot/2,-_armWidthDelta -_armWidthBot-_length1+legThick,0
mul -1,1,1
gosub  "FrontLegs"
del 2

add -a/2+_armThickBot/2+legThick/2+0.02,_armWidthDelta+_armWidthBot+_length2-legThick-0.0002/legThick,0
gosub  "BackLegs"
del 1

add a/2-_armThickBot/2-legThick/2-0.02,_armWidthDelta+_armWidthBot+_length2-legThick-0.0002/legThick,0
mul -1,1,1
gosub  "BackLegs"
del 2

! ------------------------------------------------------------------------------
! Tray
! ------------------------------------------------------------------------------

if bTray then

	group "BaseTray"
		material gs_tray_mat
		addz _seatHeight + _armHeight - 1.7*_trayEdge 

		if gs_detlevel_3D_m = 2 then 

			prism_ 39,0.02,
			0,					                    0,		                                                15+64,
			
			_seatWidth/2-_trayEdge-_Rad,			0,		                                                15+64,
			_seatWidth/2-_trayEdge-_Rad,			_Rad,		                                            900,
			_seatWidth/2-_trayEdge,				    _Rad,		                                            3000+15+64,
			
			_seatWidth/2-_trayEdge,				    _armWidthDelta+_armWidthTop+_trayEdge-_Rad,		        15+64,
			_seatWidth/2-_trayEdge+_Rad,			_armWidthDelta+_armWidthTop+_trayEdge-_Rad,		        900,
			_seatWidth/2-_trayEdge+_Rad,			_armWidthDelta+_armWidthTop+_trayEdge,			        3000+15+64,
			
			_seatWidth/2 + _armThickTop +_trayEdge-_Rad,   _armWidthDelta+_armWidthTop+_trayEdge,	    	15+64,
			_seatWidth/2 + _armThickTop +_trayEdge-_Rad,   _armWidthDelta+_armWidthTop+_trayEdge-_Rad,		900,
			_seatWidth/2 + _armThickTop +_trayEdge,        _armWidthDelta+_armWidthTop+_trayEdge-_Rad,		3000+15+64,
			
			_seatWidth/2 + _armThickTop +_trayEdge,								_trayRad, 			        15+64,
			_seatWidth/2 + _armThickTop +_trayEdge+_trayRad,					_trayRad, 			        900,
			_seatWidth/2 + _armThickTop +_trayEdge+_trayRad,					0,					        3000+15+64,
			
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad,				0,					        15+64,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad,				-_trayRad,			        900,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2,				-_trayRad,			        3000+15+64,
			
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2,  			-_trayDepth+2*_Rad,   	    15+64,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad,	    -_trayDepth+2*_Rad,   	    900,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad,		-_trayDepth,       	        3000+15+64,
			
			0,                                									-_trayDepth,       	        15+64,
			
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad),	-_trayDepth,		        15+64,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad),	-_trayDepth+2*_Rad,	        900,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2),			-_trayDepth+2*_Rad,	        3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2),			-_trayRad,		            15+64,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad),			-_trayRad,		            900,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad), 			0,		                    3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +_trayEdge+_trayRad),					0,    				        15+64,
			-(_seatWidth/2 + _armThickTop +_trayEdge+_trayRad),					_trayRad,  			        900,
			-(_seatWidth/2 + _armThickTop +_trayEdge	),						_trayRad,			        3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +_trayEdge), 		    _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	15+64,
			-(_seatWidth/2 + _armThickTop +_trayEdge-_Rad), 	_armWidthDelta+_armWidthTop+_trayEdge-_Rad,	900,
			-(_seatWidth/2 + _armThickTop +_trayEdge-_Rad), 	_armWidthDelta+_armWidthTop+_trayEdge,	  	3000+15+64,
			
			-(_seatWidth/2-_trayEdge+_Rad),			            _armWidthDelta+_armWidthTop+_trayEdge,	    15+64,
			-(_seatWidth/2-_trayEdge+_Rad),			            _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	900,
			-(_seatWidth/2-_trayEdge),				            _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	3000+15+64,
			
			
			-(_seatWidth/2-_trayEdge),		  				    _Rad,										15+64,
			-(_seatWidth/2-_trayEdge-_Rad),					    _Rad,										900,
			-(_seatWidth/2-_trayEdge-_Rad),					    0,										    3000+15+64,
			
			0,					                    		    0,										    15+64
      
		else
			prism_ 15,0.01,
			0,															0,		                                    15,
			_seatWidth/2-_trayEdge,		                				0,		                                    15,
			_seatWidth/2-_trayEdge,		                				_armWidthDelta+_armWidthTop+_trayEdge,	    15,
			_seatWidth/2 + _armThickTop +_trayEdge,   					_armWidthDelta+_armWidthTop+_trayEdge,	    15,
			_seatWidth/2 + _armThickTop +_trayEdge,   					0,											15,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2, 	0,											15,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2, 	-_trayDepth,								15,
			0,                                        					-_trayDepth,    							15,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2),  -_trayDepth,        						15,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2),  0,                         					15,
			-(_seatWidth/2 + _armThickTop +_trayEdge), 					0,                      					15,
			-(_seatWidth/2 + _armThickTop +_trayEdge), 					_armWidthDelta+_armWidthTop+_trayEdge,	    15,
			-(_seatWidth/2-_trayEdge),		            				_armWidthDelta+_armWidthTop+_trayEdge,	    15,
			-(_seatWidth/2-_trayEdge),		            				0,		                                    15,
			0,				  											0,		                                    15

		endif
		del 1
	endgroup
	
	group "TopTray"
		
		addz  _seatHeight + _armHeight -1.2*_trayEdge
		if gs_detlevel_3D_m = 2 then 			

			hprism_ gs_tray_mat,gs_tray_mat,gs_tray_mat,gs_tray_mat,
			40+26,_trayEdge,0,_trayEdge/2,1,
			0,					0,		15+64,
			
			_seatWidth/2-_trayEdge-_Rad,		0,														15+64,
			_seatWidth/2-_trayEdge-_Rad,		_Rad,													900,
			_seatWidth/2-_trayEdge,		 	    _Rad,													3000+15+64,
			
			_seatWidth/2-_trayEdge,		  	    _armWidthDelta+_armWidthTop+_trayEdge-_Rad,				15+64,
			_seatWidth/2-_trayEdge+_Rad,		_armWidthDelta+_armWidthTop+_trayEdge-_Rad,				900,
			_seatWidth/2-_trayEdge+_Rad,		_armWidthDelta+_armWidthTop+_trayEdge,	  				3000+15+64,
			
			_seatWidth/2 + _armThickTop +_trayEdge-_Rad,    _armWidthDelta+_armWidthTop+_trayEdge,		15+64,
			_seatWidth/2 + _armThickTop +_trayEdge-_Rad,    _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	900,
			_seatWidth/2 + _armThickTop +_trayEdge,         _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	3000+15+64,
			
			_seatWidth/2 + _armThickTop +_trayEdge,           					_trayRad,  				15+64,
			_seatWidth/2 + _armThickTop +_trayEdge+_trayRad,  					_trayRad,    			900,
			_seatWidth/2 + _armThickTop +_trayEdge+_trayRad,  					0,         				3000+15+64,
			
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad,				0,		              	15+64,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad,				-_trayRad,		      	900,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2,				-_trayRad,		      	3000+15+64,
				
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2,  			-_trayDepth+2*_Rad,    	15+64,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad,		-_trayDepth+2*_Rad,		900,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad,		-_trayDepth,        	3000+15+64,
			
			0,                         	 										-_trayDepth,        	15+64,
			
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad),	-_trayDepth,			15+64,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_Rad),	-_trayDepth+2*_Rad,		900,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2),			-_trayDepth+2*_Rad,		3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2),			-_trayRad,				15+64,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad),			-_trayRad,				900,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad),			0,				  		3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +_trayEdge+_trayRad),					0,              		15+64,
			-(_seatWidth/2 + _armThickTop +_trayEdge+_trayRad),					_trayRad,				900,
			-(_seatWidth/2 + _armThickTop +_trayEdge	),						_trayRad,				3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +_trayEdge), 		_armWidthDelta+_armWidthTop+_trayEdge-_Rad,	15+64,
			-(_seatWidth/2 + _armThickTop +_trayEdge-_Rad), _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	900,
			-(_seatWidth/2 + _armThickTop +_trayEdge-_Rad), _armWidthDelta+_armWidthTop+_trayEdge,	  	3000+15+64,
			
			-(_seatWidth/2-_trayEdge+_Rad),			        _armWidthDelta+_armWidthTop+_trayEdge,	  	15+64,
			-(_seatWidth/2-_trayEdge+_Rad),			        _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	900,
			-(_seatWidth/2-_trayEdge),				        _armWidthDelta+_armWidthTop+_trayEdge-_Rad,	3000+15+64,
			
			
			-(_seatWidth/2-_trayEdge),		                _Rad,										15+64,
			-(_seatWidth/2-_trayEdge-_Rad),		            _Rad,										900,
			-(_seatWidth/2-_trayEdge-_Rad),		            0,											3000+15+64,
			
			0,					                            0,											15+64,
			0,					                            0,											-1,
			
			0,                                            	-(3*_trayEdge)+0.01,   						15+64,
			
			-(_seatWidth/2-2*_trayEdge)+_Rad,               -(3*_trayEdge)+0.01,   						15+64,
			-(_seatWidth/2-2*_trayEdge)+_Rad,               -(3*_trayEdge)-_Rad+0.01, 					900,
			-(_seatWidth/2-2*_trayEdge),                  	-(3*_trayEdge)-_Rad+0.01, 					3000+15+64,
			
			-(_seatWidth/2-2*_trayEdge),                -(_armWidthDelta+_armWidthTop+_trayEdge)+_Rad,  15+64,
			-(_seatWidth/2-2*_trayEdge)-_Rad,           -(_armWidthDelta+_armWidthTop+_trayEdge)+_Rad,  900,
			-(_seatWidth/2-2*_trayEdge)-_Rad,           -(_armWidthDelta+_armWidthTop+_trayEdge),		3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+2*_trayEdge+_Rad, 		-(_armWidthDelta+_armWidthTop+_trayEdge),		15+64,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+2*_trayEdge+_Rad, 		-(_armWidthDelta+_armWidthTop+_trayEdge)-_Rad,  900,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+2*_trayEdge,				-(_armWidthDelta+_armWidthTop+_trayEdge)-_Rad,  3000+15+64,
			
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+2*_trayEdge,  			-(_trayDepth-2*_trayEdge)+2*_Rad,               15+64,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+2*_trayEdge+2*_Rad,		-(_trayDepth-2*_trayEdge)+2*_Rad,               900,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+2*_trayEdge+2*_Rad,		-(_trayDepth-2*_trayEdge),						3000+15+64,
			
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_trayEdge-2*_Rad,			-(_trayDepth-2*_trayEdge),						15+64,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_trayEdge-2*_Rad,			-(_trayDepth-2*_trayEdge)+2*_Rad,               900,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_trayEdge,					-(_trayDepth-2*_trayEdge)+2*_Rad,               3000+15+64,
			
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_trayEdge,					-(_armWidthDelta+_armWidthTop+_trayEdge)-_Rad,  15+64,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_trayEdge-_Rad,			-(_armWidthDelta+_armWidthTop+_trayEdge)-_Rad,  900,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-2*_trayEdge-_Rad,			-(_armWidthDelta+_armWidthTop+_trayEdge),		3000+15+64,
			
			_seatWidth/2-2*_trayEdge+_Rad,                   									-(_armWidthDelta+_armWidthTop+_trayEdge),		15+64,
			_seatWidth/2-2*_trayEdge+_Rad,                   									-(_armWidthDelta+_armWidthTop+_trayEdge)+_Rad,  900,
			_seatWidth/2-2*_trayEdge,                     										-(_armWidthDelta+_armWidthTop+_trayEdge)+_Rad,  3000+15+64,
			        
			_seatWidth/2-2*_trayEdge,									-(3*_trayEdge)-_Rad+0.01,												15+64,
			_seatWidth/2-2*_trayEdge-_Rad,								-(3*_trayEdge)-_Rad+0.01,												900,
			_seatWidth/2-2*_trayEdge-_Rad,								-(3*_trayEdge)+0.01,													3000+15+64,
			
			0,															-(_armWidthDelta+_trayEdge),											-1

		else
			hprism_ gs_tray_mat,gs_tray_mat,gs_tray_mat,gs_tray_mat,
			15+10,_trayEdge,45,_trayEdge/2,1,
			0,															0,											15,
			_seatWidth/2-_trayEdge,										0,											15,
			_seatWidth/2-_trayEdge,										_armWidthDelta+_armWidthTop+_trayEdge,		15,
			_seatWidth/2 + _armThickTop +_trayEdge,						_armWidthDelta+_armWidthTop+_trayEdge,		15,
			_seatWidth/2 + _armThickTop +_trayEdge,						0,                                        	15,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2,		0,											15,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2,  	-_trayDepth,         						15,
			0,                                         					-_trayDepth,               					15,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2), 	-_trayDepth,           						15,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2),	0,											15,
			-(_seatWidth/2 + _armThickTop +_trayEdge),					0,											15,
			-(_seatWidth/2 + _armThickTop +_trayEdge),					_armWidthDelta+_armWidthTop+_trayEdge,		15,
			-(_seatWidth/2-_trayEdge),									_armWidthDelta+_armWidthTop+_trayEdge,		15,
			-(_seatWidth/2-_trayEdge),									0,											15,
			0,															0,											-1,

			0,															-(_armWidthDelta+_trayEdge),              	15,
			
			-(_seatWidth/2-2*_trayEdge),								-(_armWidthDelta+_trayEdge),              	15,
			-(_seatWidth/2-2*_trayEdge),								-(_armWidthDelta+_armWidthTop+_trayEdge), 	15,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+_trayEdge,	-(_armWidthDelta+_armWidthTop+_trayEdge), 15,
			-(_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2)+_trayEdge,	-(_trayDepth-_trayEdge),                  15,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-_trayEdge,		-(_trayDepth-_trayEdge),                  15,
			_seatWidth/2 + _armThickTop +0.01+_trayEdge+_trayRad*2-_trayEdge,		-(_armWidthDelta+_armWidthTop+_trayEdge), 15,
			
			_seatWidth/2-2*_trayEdge,									-(_armWidthDelta+_armWidthTop+_trayEdge),	15,
			_seatWidth/2-2*_trayEdge,									-(_armWidthDelta+_trayEdge),				15,
			0,															-(_armWidthDelta+_trayEdge),				-1
		endif
		del 1

	endgroup
! ------------------------------------------------------------------------------
! Rod in the middle
! ------------------------------------------------------------------------------
	add 0,-_armWidthDelta-_armWidthBot/2+_RodWidth/2,_seatHeight+_seatThick
  
	addz _armHeight-1.5 * _lidHeight-_seatThick
	cutplane
	del 1
  
	if gs_detlevel_3D_m = 2 then
		sweep 12,2,0,1.6,4,
	
			_RodWidth/2,			_RodWidth/2-_RodRad,    1,
			_RodWidth/2-_RodRad,	_RodWidth/2-_RodRad,    901,
			_RodWidth/2-_RodRad,	_RodWidth/2,            3001,
	
			-_RodWidth/2+_RodRad,	_RodWidth/2,            1,
			-_RodWidth/2+_RodRad,	_RodWidth/2-_RodRad,    901,
			-_RodWidth/2,			_RodWidth/2-_RodRad,    3001,
	
			-_RodWidth/2,			-_RodWidth/2+_RodRad,   1,
			-_RodWidth/2+_RodRad,	-_RodWidth/2+_RodRad,   901,
			-_RodWidth/2+_RodRad,	-_RodWidth/2,           3001,
	
			_RodWidth/2-_RodRad,	-_RodWidth/2,           1,
			_RodWidth/2-_RodRad,	-_RodWidth/2+_RodRad,   901,
			_RodWidth/2,			-_RodWidth/2+_RodRad,   3001,
	
			0,                      0,						0,
			0,                      -0.05,					_seatHeight+_armHeight+0.1
      
	else
		sweep 4,2,0,1.6,4,
			_RodWidth/2,     _RodWidth/2,		1,
			-_RodWidth/2,    _RodWidth/2,		1,
			-_RodWidth/2,    -_RodWidth/2,		1,
			_RodWidth/2,     -_RodWidth/2,		1,
	
			0,                0,                0,
			0,                -0.05,            _seatHeight+_armHeight+0.1
	endif
  
  cutend
  del 1

  Tray = addgroup("BaseTray", "TopTray")
  placegroup Tray 

endif       ! end of "if bTray = 1"

del 1	


! ==============================================================================
  end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
 "Armrest":				!Subroutine
! ==============================================================================

	material gs_seat_mat
	addy _armWidthDelta

	if gs_detlevel_3D_m = 2 then
	sweep 11,2,0,_Mul1,1+2+4+16,
		0,						0,				        1,
		_armThickBot/2,			0,				        1,
		_armThickBot/2,			_armWidthBot-_Rad,		1,
		_armThickBot/2-_Rad,	_armWidthBot-_Rad,		901,
		_armThickBot/2-_Rad,	_armWidthBot,			3001,
		0,			            _armWidthBot,			1,
		-_armThickBot/2+_Rad,	_armWidthBot,			1,
		-_armThickBot/2+_Rad,	_armWidthBot-_Rad,		901,
		-_armThickBot/2,		_armWidthBot-_Rad,		3001,
		-_armThickBot/2,		0,				        1,
		0,						0,				        1,
		
		_armThickBot/2,			0,						0,
		_armThickBot/2,			0,						_armHeight
		
	else
	sweep 4,2,0,_Mul1,1+2+4+16,
		_armThickBot/2,			0,						0,
		_armThickBot/2,			_armWidthBot,			0,
		-_armThickBot/2,		_armWidthBot,			0,
		-_armThickBot/2,		0,						0,
		
		_armThickBot/2,			0,						0,
		_armThickBot/2,			0, 						_armHeight

	endif
	del 1

	! ---------Lid -----------------------------------------------------------------


	add  _armThickBot/2-_armThickTop/2,0,_armHeight

	if gs_detlevel_3D_m = 2 then
	hprism_ gs_seat_mat2,gs_seat_mat2,gs_seat_mat2,gs_seat_mat2,
		18,_lidHeight,0,_lidHeight,1,
    
		_armThickTop/2,					_armWidthDelta,								15+64,
		_armThickTop/2,					_armWidthDelta+_armWidthTop-_Rad*_Mul1,		15+64,
		_armThickTop/2-_Rad*_Mul1,		_armWidthDelta+_armWidthTop-_Rad*_Mul1,		900,
		_armThickTop/2-_Rad*_Mul1,		_armWidthDelta+_armWidthTop,				3000+15+64,
		0,								_armWidthDelta+_armWidthTop,				15+64,
		-_armThickTop/2+_Rad*_Mul1,		_armWidthDelta+_armWidthTop,				15+64,
		-_armThickTop/2+_Rad*_Mul1,		_armWidthDelta+_armWidthTop-_Rad*_Mul1,		900,
		-_armThickTop/2,				_armWidthDelta+_armWidthTop-_Rad*_Mul1,		3000+15+64,
		
		-_armThickTop/2,				_armWidthDelta,								15+64,
		-_armThickTop/2,				-_armWidthDelta,							15+64,
		  
		-_armThickTop/2,				-(_armWidthDelta+_armWidthTop-_Rad*_Mul1),	15+64,
		-_armThickTop/2+_Rad*_Mul1,		-(_armWidthDelta+_armWidthTop-_Rad*_Mul1),	900,
		-_armThickTop/2+_Rad*_Mul1,		-(_armWidthDelta+_armWidthTop),				3000+15+64,
		0,								-(_armWidthDelta+_armWidthTop),				15+64,
		_armThickTop/2-_Rad*_Mul1,		-(_armWidthDelta+_armWidthTop),				15+64,
		_armThickTop/2-_Rad*_Mul1,		-(_armWidthDelta+_armWidthTop-_Rad*_Mul1),	900,
		_armThickTop/2,					-(_armWidthDelta+_armWidthTop-_Rad*_Mul1),	3000+15+64,
		
		_armThickTop/2,					-_armWidthDelta,							12+64
	else
	hprism_ gs_seat_mat2,   gs_seat_mat2,   gs_seat_mat2,   gs_seat_mat2,
		4,					_lidHeight,45,     _lidHeight,  1,
		_armThickTop/2,		_armWidthDelta+_armWidthTop,	15,
		-_armThickTop/2,	_armWidthDelta+_armWidthTop,	15,
			
		-_armThickTop/2,	-(_armWidthDelta+_armWidthTop),	15,
		_armThickTop/2,		-(_armWidthDelta+_armWidthTop),	15
	endif


	del 1

	mul 1,-1,1
	addy _armWidthDelta

	if gs_detlevel_3D_m = 2 then
	sweep 11,2,0,_Mul1,1+2+4+16,
		0,0,1,
		_armThickBot/2,			0,					1,
		_armThickBot/2,			_armWidthBot-_Rad,	1,
		_armThickBot/2-_Rad,	_armWidthBot-_Rad,	901,
		_armThickBot/2-_Rad,	_armWidthBot,		3001,
		0,						_armWidthBot,		1,
		-_armThickBot/2+_Rad,	_armWidthBot,		1,
		-_armThickBot/2+_Rad,	_armWidthBot-_Rad,	901,
		-_armThickBot/2,		_armWidthBot-_Rad,	3001,
		-_armThickBot/2,		0,					1,
		0,						0,					1,
		
		_armThickBot/2,         0,                  0,
		_armThickBot/2,         0,                  _armHeight

	else
	Sweep 4,2,0,_Mul1,1+2+4+16,
		_armThickBot/2,			0,					0,
		_armThickBot/2,			_armWidthBot,		0,
		-_armThickBot/2,		_armWidthBot,		0,
		-_armThickBot/2,		0,					0,
		
		_armThickBot/2,         0,                  0,
		_armThickBot/2,         0,                  _armHeight
	endif

	del 2

	rotx 90
	prism_ 4, _armWidthDelta,
	_armThickBot/2,		            0,		        15,
	_armThickBot/2,		            _armHeight,		15,
	_armThickBot/2-_armThickTop,	_armHeight,		15,
	-_armThickBot/2,				0,		        15

	mul 1,1,-1
	prism_ 4, _armWidthDelta,
	_armThickBot/2,		            0,		        15,
	_armThickBot/2,		            _armHeight,		15,
	_armThickBot/2-_armThickTop,	_armHeight,		15,
	-_armThickBot/2,				0,		        15

	del 2


return

! ==============================================================================
 "Backrest" :				!Subroutine
! ==============================================================================
	material gs_seat_mat

	! ---------Lower part of backrest ----------------------------------------------
	addz _armHeight
	cutplane 
	del 1
	for z = 1 to 2 
		addy _backWidthDelta/2
			if gs_detlevel_3D_m = 2 then
		
				sweep 10,2,0,_Mul2,1+2+4+16,
					0,						0,                      1,
					_backThickBot/2,		0,                      1,
					_backThickBot/2,		_backWidthBotFix-_Rad,	1,
					_backThickBot/2-_Rad,	_backWidthBotFix-_Rad,	901,
					_backThickBot/2-_Rad,	_backWidthBotFix,		3001,
					0,						_backWidthBotFix,		1,
					-_backThickBot/2+_Rad,	_backWidthBotFix,		1,
					-_backThickBot/2+_Rad,	_backWidthBotFix-_Rad,	901,
					-_backThickBot/2,		_backWidthBotFix-_Rad,	3001,
					-_backThickBot/2,		0,						1,
	
					_backThickBot/2,		0,  0,
					_backThickBot/2,		0,  _backHeight
			else
				sweep 4,2,0,_Mul2,1+2+4+16,
					_backThickBot/2,		0,					0,
					_backThickBot/2,		_backWidthBotFix,	0,
					-_backThickBot/2,		_backWidthBotFix,	0,
					-_backThickBot/2,		0,					0,
	
					_backThickBot/2,    	0,  0,
					_backThickBot/2,    	0,  _backHeight
			endif
	
		mul 1,-1,1
		addy _backWidthDelta/2
	next z 
	del 3*(z-1)

	rotx 90
	addz -_backWidthDelta/2

	prism_ 4, _backWidthDelta,
		_backThickBot/2,				0,		        15,
		_backThickBot/2,				_backHeight,	15,
		_backThickBot/2-_backThickTop,	_backHeight,	15,
		-_backThickBot/2,				0,		        15
	del 2


	cutend

	! ---------Upper part of backrest ----------------------------------------------
	material gs_seat_mat2
	addz _armHeight
	mul 1,1,-1
	cutplane 
	del 2

	for z = 1 to 2 
		addy _backWidthDelta/2
			if gs_detlevel_3D_m = 2 then
				Sweep 10,2,0,_Mul2,1+2+4+16,
					0,						0,                      1,
					_backThickBot/2,		0,                      1,
					_backThickBot/2,		_backWidthBotFix-_Rad,	1,
					_backThickBot/2-_Rad,	_backWidthBotFix-_Rad,	901,
					_backThickBot/2-_Rad,	_backWidthBotFix,		3001,
					0,						_backWidthBotFix,		1,
					-_backThickBot/2+_Rad,	_backWidthBotFix,		1,
					-_backThickBot/2+_Rad,	_backWidthBotFix-_Rad,	901,
					-_backThickBot/2,		_backWidthBotFix-_Rad,	3001,
					-_backThickBot/2,		0,						1,
	
					_backThickBot/2,		0,  0,
					_backThickBot/2,		0,  _backHeight
			else
				Sweep 4,2,0,_Mul2,1+2+4+16,
					_backThickBot/2,		0,                      0,
					_backThickBot/2,		_backWidthBotFix,		0,
					-_backThickBot/2,		_backWidthBotFix,		0,
					-_backThickBot/2,		0,						0,
	
					_backThickBot/2,		0,  0,
					_backThickBot/2,		0,  _backHeight
			endif
	
		mul 1,-1,1
		addy _backWidthDelta/2
	next z 
	del 3*(z-1)

	rotx 90
	addz -_backWidthDelta/2

	prism_ 4, _backWidthDelta,
		_backThickBot/2,				0,		        15,
		_backThickBot/2,				_backHeight,	15,
		_backThickBot/2-_backThickTop,	_backHeight,	15,
		-_backThickBot/2,				0,		        15
	del 2


	cutend


	! ---------Lid -----------------------------------------------------------------

	material gs_seat_mat

	add _backThickBot/2-_backThickTop/2, 0,_backHeight

	if gs_detlevel_3D_m = 2 then
		hprism_ gs_seat_mat2,gs_seat_mat2,gs_seat_mat2,gs_seat_mat2,
			16,_lidHeight,0,_lidHeight,1,
			_backThickTop/2,				0,15+64,
			_backThickTop/2,				_backWidthTop/2-_Rad*_Mul2,		15+64,
			_backThickTop/2-_Rad*_Mul2,		_backWidthTop/2-_Rad*_Mul2,		900,
			_backThickTop/2-_Rad*_Mul2,		_backWidthTop/2,				3000+15+64,
			0,								_backWidthTop/2,				15+64,
			-_backThickTop/2+_Rad*_Mul2,	_backWidthTop/2,				15+64,
			-_backThickTop/2+_Rad*_Mul2,	_backWidthTop/2-_Rad*_Mul2,		900,
			-_backThickTop/2,				_backWidthTop/2-_Rad*_Mul2,		3000+15+64,
			-_backThickTop/2,				0,								15+64,
		
			-_backThickTop/2,				-(_backWidthTop/2-_Rad*_Mul2),	15+64,
			-_backThickTop/2+_Rad*_Mul2,	-(_backWidthTop/2-_Rad*_Mul2),	900,
			-_backThickTop/2+_Rad*_Mul2,	-_backWidthTop/2,				3000+15+64,
			0,								-_backWidthTop/2,				15+64,
			_backThickTop/2-_Rad*_Mul2,		-_backWidthTop/2,				15+64,
			_backThickTop/2-_Rad*_Mul2,		-(_backWidthTop/2-_Rad*_Mul2),	900,
			_backThickTop/2,				-(_backWidthTop/2-_Rad*_Mul2),	3000+15+64
	else
		hprism_ gs_seat_mat2,gs_seat_mat2,gs_seat_mat2,gs_seat_mat2,
			4,_lidHeight,45,_lidHeight,1,
			_backThickTop/2,				_backWidthTop/2,				15,
			-_backThickTop/2,				_backWidthTop/2,				15,
		
			-_backThickTop/2,				-_backWidthTop/2,				15,
			_backThickTop/2,				-_backWidthTop/2,				15
	endif


	del 1

return

! ==============================================================================
  "Link":				!Subroutine
! ==============================================================================

	rotz 90
	cutplane -_Ang1
	del 1
	if gs_detlevel_3D_m = 2 then
		prism_ 7,_armHeight-_lidHeight-bTray * _lidHeight,
			0,0,15+64,
			_armThickBot/2,			0,								15+64,
			_armThickBot/2,			_armWidthBot-_Rad,				15+64,
			_armThickBot/2+_Rad,	_armWidthBot-_Rad,				900,
			_armThickBot/2+_Rad,	_armWidthBot,					3000+15+64,
			_armThickBot/2+_Rad,	_armWidthBot+_backThickBot/2,	15+64,
			0,			            _armWidthBot+_backThickBot/2,	15+64
	else
		prism_ 6,_armHeight-_lidHeight-bTray * _lidHeight,
			0,			0,			15,
			_armThickBot/2,			0,								15,
			_armThickBot/2,			_armWidthBot-R,		            15,
			_armThickBot/2+R,		_armWidthBot,					15,
			_armThickBot/2+R,		_armWidthBot+_backThickBot/2,	15,
			0,			            _armWidthBot+_backThickBot/2, 	15
	endif
	addz _armHeight-_lidHeight-bTray * _lidHeight

	if gs_detlevel_3D_m = 2 then
		hprism_ gs_seat_mat,gs_seat_mat,gs_seat_mat,gs_seat_mat,
			7,						_lidHeight,0,_lidHeight,		1,
			0,						0,								15+64,
			_armThickBot/2,			0,								15+64,
			_armThickBot/2,			_armWidthBot-_Rad,		        15+64,
			_armThickBot/2+_Rad,	_armWidthBot-_Rad,		        900,
			_armThickBot/2+_Rad,	_armWidthBot,		            3000+15+64,
			_armThickBot/2+_Rad,	_armWidthBot+_backThickBot/2,	15+64,
			0,						_armWidthBot+_backThickBot/2,	15+64
	else
		hprism_ gs_seat_mat,gs_seat_mat,gs_seat_mat,gs_seat_mat,
			6,_lidHeight,45,_lidHeight,1,
			0,						0,								15,
			_armThickBot/2,		    0,								15,
			_armThickBot/2,			_armWidthBot-_Rad,				15,
			_armThickBot/2+_Rad,	_armWidthBot,					15,
			_armThickBot/2+_Rad,	_armWidthBot+_backThickBot/2,	15,
			0,						_armWidthBot+_backThickBot/2,	15
	endif

	del 1
	cutend
return


! ==============================================================================
 "FrontLegs":		!Subroutine
! ==============================================================================

	material gs_frame_mat

	addz _seatHeight
	cutplane
	del 1

	! ---------- footpad -----------------------------------------------------------


		if gs_detlevel_3D_m = 2 then				! --- detailed geometry				
			hprism_ gs_frame_mat, gs_frame_mat, gs_frame_mat, gs_frame_mat,		
				2,			0.01,   0,0.006,1,
				0,			0,      900+15+64,
				legThick,   360,    4000+15+64

		else										! --- simple geometry
			hprism_ gs_frame_mat, gs_frame_mat, gs_frame_mat, gs_frame_mat,
				4,0.01,45,0.006,1,
				legThick,		legThick,		15,
				-legThick,		legThick,		15,
				-legThick,		-legThick,		15,
				legThick,		-legThick,		15
		endif


	! ---------- leg ---------------------------------------------------------------

		mul 1,1,-1
		addz -0.01
		cutplane 
		del 2

			rotx -(90-_Ang2)
			roty 90-_Ang1

			if gs_detlevel_3D_m = 2 then				! --- detailed geometry	
				sweep 2,2,0,1.6,4,
					0,          0,        901,
					legThick/2, 360,      4001,
					0,          0,        0,
					0,          0,        _seatHeight+_armHeight+0.1
			else										! --- simple geometry
				prism_ 4, _seatHeight+_armHeight+0.1,
					legThick/2,		legThick/2,			15,
					-legThick/2,	legThick/2,			15,
					-legThick/2,	-legThick/2,		15,
					legThick/2,		-legThick/2,		15
			endif
			del 2

		cutend

	cutend 
return

! ==============================================================================
 "BackLegs":		!Subroutine
! ==============================================================================

	material gs_frame_mat
	addz _seatHeight
	cutplane
	del 1

	! ---------- footpad -----------------------------------------------------------


		if gs_detlevel_3D_m = 2 then				! --- detailed geometry	
			hprism_ gs_frame_mat, gs_frame_mat, gs_frame_mat, gs_frame_mat,
				2,	0.01,	0,	0.002,1,
				0,				0,				900+15+64,
				legThick,		360,			4000+15+64
		else										! --- simple geometry
			hprism_ gs_frame_mat, gs_frame_mat, gs_frame_mat, gs_frame_mat,
				4,	0.01,	45,	0.006,	1,
				legThick,		legThick,		15,
				-legThick,		legThick,		15,
				-legThick,		-legThick,	    15,
				legThick,		-legThick,	    15
		endif

	! ---------- leg ---------------------------------------------------------------
		mul 1,1,-1
		addz -0.01
		cutplane 
		del 2

			rotx 90-_Ang3
			roty 90-_Ang1

			if gs_detlevel_3D_m = 2 then				! --- detailed geometry	
				sweep 2,2,0,1.6,4,
					0,          0,    901,
					legThick/2, 360,  4001,
					0,          0,    0,
					0,          0,    _seatHeight+_backHeight+0.1
			else										! --- simple geometry
				prism_ 4, _seatHeight+_armHeight+0.1,
					legThick/2,			legThick/2,		15,
					-legThick/2,		legThick/2,		15,
					-legThick/2,		-legThick/2,	15,
					legThick/2,			-legThick/2,	15
			endif
			del 2
		cutend
	cutend

return

! ==============================================================================
! Texture Subroutines
! ==============================================================================
"Horizontal Texture":
	roty 90
	gosub "Texture Coordinates"
	del 1
return


"Vertical Texture":
	gosub "Texture Coordinates"
return

"Texture Coordinates":
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1

return

