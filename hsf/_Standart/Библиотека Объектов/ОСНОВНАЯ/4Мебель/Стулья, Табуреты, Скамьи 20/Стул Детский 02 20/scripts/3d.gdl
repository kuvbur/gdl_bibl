
! High Chair Plastic 3D Script

! ==============================================================================
! 3D Representation (detail level, resol, shadow)
! ==============================================================================

if gs_detlevel_3D_m=0 then END
resol gs_resol
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1
if gs_shadow=0 then SHADOW OFF

! ==============================================================================
! Hotspot Editing
! ==============================================================================

! ----- A,B dimensions ---------------------------------------------------------

unID=1
hotspot   0, 0, 0, unID: unID=unID+1
hotspot   A, 0, 0, unID: unID=unID+1
hotspot   A, B, 0, unID: unID=unID+1
hotspot   0, B, 0, unID: unID=unID+1


! ----- ZZYZX (Height) ---------------------------------------------------------
hotspot a/2,_xLegs10,	0, 		unID, 	zzyzx, 1+128: unID=unID+1
hotspot a/2,_xLegs10,	-1, 				unID,	zzyzx, 3: unID=unID+1
hotspot a/2,_xLegs10,	zzyzx, unID, 	zzyzx, 2: unID=unID+1

! ----- seat height ------------------------------------------------------------
hotspot a/2,B/2,		0, 				unID, 	gs_seat_height, 1+128: unID=unID+1
hotspot a/2,B/2,		-1, 						unID, gs_seat_height, 3: unID=unID+1
hotspot a/2,B/2,		gs_seat_height, 	unID, 	gs_seat_height, 2: unID=unID+1


! ==============================================================================
! 3D GEOMETRY
! ==============================================================================
material gs_frame_mat
pen gs_cont_pen	

rotz 180	
if gs_detlevel_3D_m = 2 then	
add -a/2,0,_legThick/2
else
	add -a/2,0,0
endif
! ==============================================================================
! Legs
! ==============================================================================

! ------------------------------------------------------------------------------
! Cover on Leg 2
! ------------------------------------------------------------------------------
group "GroupLegCover"
if gs_detlevel_3D_m = 2 then

	material gs_seat_mat
	addx _leg2WidthBot/2
	rotx (90-_leg2AngXY)
	addz _leg2Height-_sideHeight
	mul 1,1,-1
	cylind 0.25 /sin(_leg2AngXY)/2-0.05 , _legThick/2+0.0025
	del 4

	material gs_seat_mat
	addx -_leg2WidthBot/2
	rotx (90-_leg2AngXY)
	addz _leg2Height-_sideHeight
	mul 1,1,-1
	cylind 0.25 /sin(_leg2AngXY)/2-0.05 , _legThick/2+0.0025
	del 4

endif
endgroup



if gs_detlevel_3D_m = 1 then	
	mul 1,1,-1
	cutplane
	del 1
	addz ZZYZX
	cutplane
	del 1
endif


! ------------------------------------------------------------------------------
! Leg 2
! ------------------------------------------------------------------------------

material gs_frame_mat

!! --------------------------------------- detailed geometry --------------------

	if gs_detlevel_3D_m = 2 then					
	rotx 90 + (90-_leg2AngXY)
	for k=1 to 2					
		legAng = 90
		_TubeRad = _legThick/2
		_arcAng = 90
		_arcRad = _legRad
		_origoY = _arcRad
		_origoX = A/2-_legThick-_arcRad+0.03 
		_startRad = 270
		gosub "ArcTube"
		_iSegment1 = _iArcResol+1
		
		
		tube 2,  5+_iSegment1, 0,
			0, 0, 901,
			_TubeRad, 360, 4001,
			-2,					0,			0,	0,
			0,					0,			0,	0,
			get (_iSegment1*4),
			_leg2WidthBot/2,_leg2Height/3,	0,	0,
			_leg2WidthBot/2,_leg2Height/2,	0,	0,
			_leg2WidthBot/2,_leg2Height,	0,	0



		! ------ edge protection -----------------
		if bEdgeProtect then 
				material gs_edge_mat
				_TubeRad = _legThick/2+ _edgeThick
				gosub "ArcTube"
				_iSegment1 = _iArcResol+1
				
				
				tube 	2,  4+_iSegment1, 		0,
						0, 			0, 			901,
						_TubeRad, 	360, 		4001,
						-2,						0,				0,	0,
						_leg2WidthBot/2-0.03,	0,				0,	0,
						_leg2WidthBot-_arcRad,	0,				0,	0,

						get (_iSegment1*4),
						_leg2WidthBot/2,		_leg1Height/3,	0,	0

				material gs_frame_mat
		endif

		addy _leg2Height
		mul 1,-1,1	

		_arcRad = _seatWidth/2-0.03
		_origoY = _arcRad
		_origoX = _leg2WidthBot/2-_arcRad
		_startRad =270
    _TubeRad = _legThick/2

		addy _sideHeight
		rotx 90
		cutplane
		del 2

			gosub "ArcTube"
			_iSegment1 = _iArcResol+1
		
			tube 2,  4+_iSegment1, 0,
				0, 0, 901,
				_TubeRad, 360, 4001,
				-2,					0,			0,		0,
				0,					0,			0,		0,
				get (_iSegment1*4),
				_leg2WidthBot/2,	_leg2Height/2,	0,	0,
				_leg2WidthBot/2,	_leg2Height,	0,	0
		cutend
		
		material gs_frame_mat

		del 2
	mul -1,1,1
	next k
	del k-1
	del 1

	else											! simple geometry	

	rotx 90 + (90-_leg2AngXY)
	addy _legThick/2

	tube 4,  8, 1+2+16+32,
			_legThick/2, 	0, 			0,
			_legThick/2, 	_legThick,	0,
			-_legThick/2,	_legThick,	0,
			-_legThick/2,	0,			0,

			-2,					0,				0,	0,
			0,					0,				0,	0,
			_leg2WidthBot/2,	0,				0,	0,
		_leg2WidthBot/2,	(ZZYZX) / sin(_leg2AngXY)-sin(_leg2AngXY) * _legThick/2,	0,	0,
		-_leg2WidthBot/2,	(ZZYZX) / sin(_leg2AngXY)-sin(_leg2AngXY) * _legThick/2,	0,	0,
		-_leg2WidthBot/2,	0,				0,	0,
		0,					0,	0,	0,
		2,					0,	0,	0	
	del 2	
	endif


! ------------------------------------------------------------------------------
! Leg 1
! ------------------------------------------------------------------------------
if gs_detlevel_3D_m = 1 then
	addz _yLegs1 + _legThick/2
cutplane
	del 1
endif


! --------------------------------------- detailed geometry --------------------
if gs_detlevel_3D_m = 2 then	
	rotx 90
	addz distLegs
	rotx -(90-_leg1AngXY)	
	for z= 1 to 2
		legAng = 90
		_TubeRad = _legThick/2
		_arcAng = 90
		_arcRad = _legRad
		_origoY = _arcRad
		_origoX = A/2-_arcRad+0.02 
		_startRad = 270
		gosub "ArcTube"
		_iSegment1 = _iArcResol+1
		
		
		tube 2,  5+_iSegment1, 0,
			0, 0, 901,
			_TubeRad, 360, 4001,
			-2,					0,			0,	0,
			0,					0,			0,	0,
			get (_iSegment1*4),
			_leg1WidthBot/2,_leg1Height/3,	0,	0,
			_leg1WidthBot/2,_leg1Height/2,	0,	0,
			_leg1WidthBot/2,_leg1Height,	0,	0

		! ------ edge protection -----------------
		if bEdgeProtect then
		
				material gs_edge_mat
				_TubeRad = _legThick/2+ _edgeThick
				gosub "ArcTube"
				_iSegment1 = _iArcResol+1
				
			
				tube 2,  4+_iSegment1, 	0,
					0, 	0, 	901,
					_TubeRad, 	360, 4001,
					-2,						0,				0,	0,
					_leg1WidthBot/2-0.03,	0,				0,	0,
					_leg1WidthBot-_arcRad,	0,				0,	0,
					get (_iSegment1*4),
					_leg1WidthBot/2,		_leg1Height/3,	0,	0

				material gs_frame_mat
		endif
	
	mul -1,1,1
	next z
	del z-1
	
	mul 1,-1,1
	addy -_leg1Height
		for k=1 to 2
			legAng = 90
			_TubeRad = _legThick/2
			_arcAng = 90
			_arcRad = _legRad
			_origoY = _legRad
			_origoX = _leg1WidthBot/2-_legRad
			_startRad = 270
			gosub "ArcTube"
			_iSegment1 = _iArcResol+1
			
			
			tube 2,  4+_iSegment1, 0,
				0, 0, 901,
				_TubeRad, 360, 4001,
				-2,					0,			0,	0,
				0,					0,			0,	0,
				get (_iSegment1*4),
				_leg1WidthBot/2,_leg1Height/2,	0,	0,
				_leg1WidthBot/2,_leg1Height,	0,	0
		mul -1,1,1
		next k
		del k-1
	del 2

	del 3





! --------------------------------------- simple geometry ---------------------
else 
	addy _legThick/ cos(90-_leg1AngXY)
	rotx 90
	addz distLegs
	rotx -(90-_leg1AngXY)
	addy _legThick/2 - tan(90-_leg1AngXY)*_legThick

	for j = 1 to 1
	

	
			tube 4,  8, 1+2+16+32,
				_legThick/2, 0, 0,
				_legThick/2, _legThick,0,
				-_legThick/2,_legThick,0,
				-_legThick/2,0,0,
	
				-2,					0,				0,	0,
				0,					0,				0,	0,
				_leg1WidthBot/2,	0,				0,	0,
				_leg1WidthBot/2,	_yLegs1 / sin(_leg1AngXY)+ sin(_leg2AngXY) * _legThick/2,	0,	0,
				-_leg1WidthBot/2,	_yLegs1 / sin(_leg1AngXY)+ sin(_leg2AngXY) * _legThick/2,	0,	0,
				-_leg1WidthBot/2,					0,	0,	0,
				0,					0,	0,	0,
				2,					0,	0,	0		
	
	
	
	mul 1,-1,1
	addy -_leg1Height
	next j
	del 2*(j-1)
	del 5
	
endif

if gs_detlevel_3D_m = 1 then
	cutend
	cutend
	cutend
endif

! ==============================================================================
! Footholder
! ==============================================================================
add _seatWidth/2,-_xLegs4 , _seatHeight
rotz 90
rotx -90

__Width = _seatWidth
__Depth= seatDepth
__Rad = _legThick/2
gosub "FootHolder"
del 3
	

! ==============================================================================
! Seat
! ==============================================================================
material gs_seat_mat

! --- Seating ------------------------------------------------------------------
! ------------------------------------------------------------------------------
add -_seatWidth/2+_legThick/2, -_xLegs4 - seatDepth/2,_seatHeight-_seatThick
gosub "Cushion"
del 1

! --- Sides --------------------------------------------------------------------
! ------------------------------------------------------------------------------
Group "GroupSides"
rotx 90
roty 90


for i=1 to 2
if gs_detlevel_3D_m = 2 then								! detailed geometry
	add -distLegs/2,_seatHeight,_seatWidth/2-_legThick/2-0.005
	rotx 8
	prism_ 4, 0.005,
		max(_xLegs7,0),																_legThick/2,							15,
		_xLegs6/2,																_legThick/2+_yLegs1 - _seatHeight,15,
		-(_xLegs1-distLegs/2)- tan(_backAng)* (_yLegs1 - _seatHeight)-0.01,	_legThick/2+_yLegs1 - _seatHeight,15,
		-seatDepth/2+_legRad,													_legThick/2,							15
	
	del 2

else														! simple geometry
	add -distLegs/2,_seatHeight,_seatWidth/2-_legThick/2
	rotx 0
	prism_ 4, 0.01,
		max(_xLegs7,0),																_legThick/2,							15,
		_xLegs6/2,																_legThick/2+_yLegs1 - _seatHeight,15,
		-(_xLegs1-distLegs/2)- tan(_backAng)* (_yLegs1 - _seatHeight)-0.02,	_legThick/2+_yLegs1 - _seatHeight,15,
		-seatDepth/2+_legRad/2,													_legThick/2,							15
	
	del 2

endif


mul 1,1,-1
next i
del i-1
del 2

endgroup
! --- Armrests -----------------------------------------------------------------
! ------------------------------------------------------------------------------
if gs_detlevel_3D_m = 2 then								! detailed geometry
for k=1 to 2
	rotx 90+(90-_leg2AngXY)
	addz -_legThick/2
	cutplane
	del 2
	
	
	rotx 90
	addz distLegs+_legThick/2
	rotx -(90-_leg1ANgXY)
	mul 1,1,-1
	cutplane
	del 4
	
	add A/2-_legThick-0.01,-distLegs/2+_xLegs6/2,_yLegs1
	rotx 90	
	cylind _xLegs6, _legThick/2+0.005
	del 2
	cutend
	cutend
	
	mul -1,1,1
next k
del k-1
endif

! --- Cushion ------------------------------------------------------------------
! ------------------------------------------------------------------------------
if gs_detlevel_3D_m = 2 then	
	material gs_seat_mat2
	add 0, -distLegs/2, _seatHeight + _seatThick
	
	prism_ 8,0.0002, 	
	
	_seatWidth/2-_legThick-_seatOffset-_legRad, 	seatDepth/2-_seatOffset, 			15,
	_seatWidth/2-_legThick-_seatOffset-_legRad,	seatDepth/2-_seatOffset-_legRad,	900,
	_seatWidth/2-_legThick-_seatOffset, 			seatDepth/2-_seatOffset-_legRad, 	3000+15,
	_seatWidth/2-_legThick-_seatOffset, 			-seatDepth/2,						15,
	-_seatWidth/2+_legThick+_seatOffset, 		-seatDepth/2, 						15,
	
	-_seatWidth/2+_legThick+_seatOffset, 		seatDepth/2-_seatOffset-_legRad, 	15,
	-_seatWidth/2+_legThick+_seatOffset+_legRad,	seatDepth/2-_seatOffset-_legRad, 	900,
	-_seatWidth/2+_legThick+_seatOffset+_legRad, seatDepth/2-_seatOffset, 			3000+15
	
	del 1
endif

! ==============================================================================
! Backrest
! ==============================================================================

__XX = sin (45) *_legRad

! --- Cushion ------------------------------------------------------------------
! ------------------------------------------------------------------------------
group "GroupBackCushion"
if gs_detlevel_3D_m = 2 then

rotz 180
	add 0, distLegs/2 +seatDepth/2 -2* _sideThick, gs_seat_height-0.01
	rotz 180
	rotx 90+_backAng-2 

cutform 8,1,32,
0,0,1,_sideThick+0.01,
_seatWidth/2-_legThick-_seatOffset,		 			0,							15,
	_seatWidth/2-_legThick-_seatOffset+0.02, 			_backLength*0.7*0.9,			15,
	_seatWidth/2-_legThick-_seatOffset-_backRad+0.02,	_backLength*0.7*0.9, 			900,
	_seatWidth/2-_legThick-_seatOffset-_backRad+0.02, 	_backLength*0.7*0.9+_backRad,	3000+15,

	-(_seatWidth/2-_legThick-_seatOffset-_backRad+0.02), _backLength*0.7*0.9+_backRad,	15,
	-(_seatWidth/2-_legThick-_seatOffset-_backRad+0.02),	_backLength*0.7*0.9, 			900,
	-(_seatWidth/2-_legThick-_seatOffset+0.02),			_backLength*0.7*0.9,			3000+15,
-(_seatWidth/2-_legThick-_seatOffset), 				0,							15
del 3

	materialBackrest = gs_seat_mat2
	material materialBackrest

	gosub "Backrest"
	
cutend	
	del 1
endif
endgroup

! --- Backrest -----------------------------------------------------------------
! ------------------------------------------------------------------------------
group "GroupBackrest"
	
rotz 180
if gs_detlevel_3D_m = 2 then

	materialBackrest = gs_seat_mat
	material materialBackrest
	gosub "Backrest"

else

addz _seatHeight+_legThick
mul 1,1,-1
cutplane 
del 2
		addz ZZYZX-_legThick/2-sin(_leg2AngXY) * _legThick/2
	cutplane 
	del 1
		
	add 0, distLegs/2+seatDepth/2 - _legThick/2- (_sideThick/sin(90-_backAng)), _seatHeight 
		rotx - _backAng
	
	prism 4,1,
			_seatWidth/2,	0,
			_seatWidth/2,	_sideThick,		
			-_seatWidth/2,	_sideThick,
			-_seatWidth/2,	0
	del 2
	cutend
cutend

endif
del 1	
endgroup		! end of "GroupBackrest"


! ==============================================================================
! Tray
! ==============================================================================

if bTray then					

rotx 90 + (90-_leg2AngXY)
cutplane
del 1
			
	rotx 180
	add 0,distLegs -_xLegs1-_TrayDepth/2,-_yLegs1	
	material gs_frame_mat
! ------------------------------------------------------------------------------
! Tray: supporting rod 
! ------------------------------------------------------------------------------

for k=1 to 2

	legAng = 90
	_TubeRad = _legThick/2
	_arcAng = 90
	_arcRad = _legRad
	_origoY = _legRad
	_origoX = _leg2WidthBot/2-_legRad
	_startRad = 270

! ------ Detailed geometry -----------------------------------------------------
	if gs_detlevel_3D_m = 2 then		
		gosub "ArcTube"
		_iSegment1 = _iArcResol+1
		
		
		material gs_frame_mat
		tube 2,  4+_iSegment1, 0,						
			0, 0, 901,
			_TubeRad, 360, 4001,
			-2,			0,		0,		0,
			0,			0,		0,		0,
			get (_iSegment1*4),
			_leg2WidthBot/2,_xLegs6+_TrayDepth/2,0,0,
			_leg2WidthBot/2,_xLegs6+_TrayDepth*2,0,0

! ------ Simple geometry -----------------------------------------------------		
	else								

		tube 	4,  		6, 0,
			_legThick/2, 0, 0,
			_legThick/2, _legThick,0,
			-_legThick/2,_legThick,0,
			-_legThick/2,0,0,
			-2,					0,						0,	0,
			0,					0,						0,	0,
			_leg2WidthBot/2,	0,						0,	0,
			_leg2WidthBot/2,	seatDepth,				0,	0,
			_leg2WidthBot/2,	_xLegs6+_TrayDepth/2,	0,	0,
			_leg2WidthBot/2,	_xLegs6+_TrayDepth*2,	0,	0


	endif

	mul -1,1,1
	next k

del k-1	
	del 2
cutend

! ------------------------------------------------------------------------------	
! Tray: base 
! ------------------------------------------------------------------------------
	material gs_tray_mat

! ------ Detailed geometry -----------------------------------------------------
	if gs_detlevel_3D_m = 2 then				

		add 0,-distLegs +_xLegs1 + _legThick+0.03,_yLegs1	+0.01
		if iTrayStyle = STYLE_RECTTRAY then
			for k = 1 to 2
				prism_ 	15,						_legThick/3,							!base of tray
						_trayWidth/2, 			_trayDepth/2-_legRad, 			15+64,
						_trayWidth/2,			10, 							800,
						_trayWidth/2-_legRad,	_trayDepth/2,					1000+15+64,
						0,						_trayDepth/2,					0,
			
						0,						-_trayDepth/2+_TrayY,			0,
						_TrayX-_TrayRad,		-_trayDepth/2+_TrayY,			15+64,
						_TrayX-_TrayRad,		-_trayDepth/2+_TrayY-_TrayRad,	900,
						_TrayX,					-_trayDepth/2+_TrayY-_TrayRad,	3000+15+64,
						_TrayX,					-_trayDepth/2+_TrayRad,			15+64,
						_TrayX+_TrayRad,		-_trayDepth/2+_TrayRad,			900,
						_TrayX+_TrayRad,		-_trayDepth/2,					3000+15+64,
			
						_trayWidth/2-_legRad, 	-_trayDepth/2,					15+64,
						_trayWidth/2-_legRad,	-(_trayDepth/2-_legRad),		900,
						_trayWidth/2, 			-(_trayDepth/2-_legRad),		3000+15+64,
						_trayWidth/2,			0,								0
				mul -1,1,1
			next k
			del k-1

		else		!  iTrayStyle = STYLE_OVALTRAY
			prism_ 13, _legThick/3,
!				0,											_trayDepth/2,									15,			!1
				-(_trayWidth/2-_trayRad3),					_trayDepth/2,									15+64,			!2
				-(_trayWidth/2-_trayRad3),					_trayDepth/2-_trayRad3,							900,		!3
				-(_trayWidth/2), 							_trayDepth/2-_trayRad3,							3000+15+64,	!4
				-(_trayWidth/2),							-(_trayDepth/2-_trayRad3),						15+64,			!5
				-(_trayWidth/2-_trayRad3),					-(_trayDepth/2-_trayRad3),						900,		!6	
			
				-(_trayWidth/2-_trayRad3-cos(60)*_trayRad3), -(_trayDepth/2-_trayRad3+ sin(60)*_trayRad3),	3000+15+64,	!7
				0,											-tan(60) * (_trayWidth/2- _trayRad3/2), 		900,		!8
				
				_trayWidth/2-_trayRad3-cos(60)*_trayRad3, 	-(_trayDepth/2-_trayRad3+ sin(60)*_trayRad3),	3000+15+64,	!9
				_trayWidth/2-_trayRad3,						-(_trayDepth/2-_trayRad3),						900,		!10	
				_trayWidth/2,								-(_trayDepth/2-_trayRad3),						3000+15+64,	!11
				_trayWidth/2, 								_trayDepth/2-_trayRad3,							15+64,			!12
				_trayWidth/2-_trayRad3,						_trayDepth/2-_trayRad3,							900,		!13
				_trayWidth/2-_trayRad3,						_trayDepth/2,									3000+15+64		!14


		endif
		del 1

! ------ Simple geometry -----------------------------------------------------
	else											
		add 0,-distLegs +_xLegs1+ _legThick+0.03,_yLegs1+_legThick
		for k = 1 to 2
			prism_ 	11,							_legThick/3,							!base of tray
					_trayWidth/2, 				_trayDepth/2-_legRad, 			15+64,
					_trayWidth/2-_legRad*0.7,	_trayDepth/2,					15+64,
					0,							_trayDepth/2,					0,	
					0,							-_trayDepth/2+_TrayY,			15,
					_TrayX-_TrayRad,			-_trayDepth/2+_TrayY,			15+64,
					_TrayX,						-_trayDepth/2+_TrayY-_TrayRad,	15+64,
					_TrayX,						-_trayDepth/2+_TrayRad,			15+64,
					_TrayX+_TrayRad,			-_trayDepth/2,					15+64,		
					_trayWidth/2-_legRad, 		-_trayDepth/2,					15+64,
					_trayWidth/2, 				-(_trayDepth/2-_legRad),		15+64,
					_trayWidth/2,				0,								15
			mul -1,1,1
		next k
		del k-1
		del 1
	endif
! ------------------------------------------------------------------------------
! Tray: Edge 
! ------------------------------------------------------------------------------

		add 0,	-distLegs +_xLegs1 +_legThick+0.03,	_yLegs1 + _legThick/2
		
		_trayWidth2 = _trayWidth+_TrayEdge*2
		_trayDepth2 = _trayDepth+_TrayEdge*2
		_TrayX2 	= _TrayX-_TrayEdge
		_TrayY2 	= _TrayY-_TrayEdge
		_TrayRad2 	= _TrayRad-_TrayEdge/10


! ------ Detailed geometry -----------------------------------------------------

	if gs_detlevel_3D_m = 2 then

		if iTrayStyle = STYLE_RECTTRAY then
			hprism_ 	gs_tray_mat,		gs_tray_mat,			gs_tray_mat, gs_tray_mat,	
					58,				_legThick/2,			0, 		0.004,					1,
					0,							-_trayDepth2/2+_TrayY2,					14+64,
					_TrayX2-_TrayRad2,			-_trayDepth2/2+_TrayY2,					14+64,
					_TrayX2-_TrayRad2,			-_trayDepth2/2+_TrayY2-_TrayRad2,		900,
					_TrayX2,					-_trayDepth2/2+_TrayY2-_TrayRad2,		14+3000+64,
					_TrayX2,					-_trayDepth2/2+_TrayRad2,				14+64,
					_TrayX2+_TrayRad2,			-_trayDepth2/2+_TrayRad2,				900,
					_TrayX2+_TrayRad2,			-_trayDepth2/2,							14+3000+64,
			
					_trayWidth2/2-_legRad, 		-_trayDepth2/2,							14+64,
					_trayWidth2/2-_legRad,		-(_trayDepth2/2-_legRad),				900,
					_trayWidth2/2, 				-(_trayDepth2/2-_legRad), 				14+3000+64,
					_trayWidth2/2,				0,										14+64,
					_trayWidth2/2, 				_trayDepth2/2-_legRad, 					14+64,
					_trayWidth2/2,				10, 									800,
					_trayWidth2/2-_legRad,		_trayDepth2/2,							1000+14+64,
					0,							_trayDepth2/2,							14+64,
		
					-(_trayWidth2/2-_legRad),	_trayDepth2/2,							14+64,
					-(_trayWidth2/2-_legRad),	_trayDepth2/2-_legRad, 					900,
					-_trayWidth2/2, 			_trayDepth2/2-_legRad, 					3000+14+64,
					-_trayWidth2/2,				0,										14+64,
					-_trayWidth2/2, 			-(_trayDepth2/2-_legRad), 				14+64,
					-_trayWidth2/2+_legRad,		-(_trayDepth2/2-_legRad),				900,
					-_trayWidth2/2+_legRad, 	-_trayDepth2/2,							14+3000+64,
		
					-(_TrayX2+_TrayRad2),		-_trayDepth2/2,							14+64,
					-(_TrayX2+_TrayRad2),		-_trayDepth2/2+_TrayRad2,				900,
					-(_TrayX2),					-_trayDepth2/2+_TrayRad2,				14+3000+64,
		
					-_TrayX2,					-_trayDepth2/2+_TrayY2-_TrayRad2,		14+64,
					-(_TrayX2-_TrayRad2),		-_trayDepth2/2+_TrayY2-_TrayRad2,		900,
					-(_TrayX2-_TrayRad2),		-_trayDepth2/2+_TrayY2,					14+3000+64,
					0,							-_trayDepth2/2+_TrayY2,					-1,
		
					0,							-_trayDepth/2+_TrayY,					14+64,
					_TrayX-_TrayRad,			-_trayDepth/2+_TrayY,					14+64,
					_TrayX-_TrayRad,			-_trayDepth/2+_TrayY-_TrayRad,			900,
					_TrayX,						-_trayDepth/2+_TrayY-_TrayRad,			14+3000+64,
					_TrayX,						-_trayDepth/2+_TrayRad,					14+64,
					_TrayX+_TrayRad,			-_trayDepth/2+_TrayRad,					900,
					_TrayX+_TrayRad,			-_trayDepth/2,							14+3000+64,
		
					_trayWidth/2-_legRad, 		-_trayDepth/2,							14+64,
					_trayWidth/2-_legRad,		-(_trayDepth/2-_legRad),				900,
					_trayWidth/2, 				-(_trayDepth/2-_legRad), 				14+3000+64,
					_trayWidth/2,				0,										14+64,
					_trayWidth/2, 				_trayDepth/2-_legRad, 					14+64,
					_trayWidth/2,				10, 									800,
					_trayWidth/2-_legRad,		_trayDepth/2,							1000+12+64,
					0,							_trayDepth/2,							14+64,
		
		
					-(_trayWidth/2-_legRad),	_trayDepth/2,							14+64,
					-(_trayWidth/2-_legRad),	_trayDepth/2-_legRad, 					900,
					-_trayWidth/2, 				_trayDepth/2-_legRad, 					3000+14+64,
					-_trayWidth/2,				0,										14+64,
					-_trayWidth/2, 				-(_trayDepth/2-_legRad),				14+64,
					-_trayWidth/2+_legRad,		-(_trayDepth/2-_legRad),				900,
					-_trayWidth/2+_legRad, 		-_trayDepth/2,							14+3000+64,
		
					-(_TrayX+_TrayRad),			-_trayDepth/2,							14+64,
					-(_TrayX+_TrayRad),			-_trayDepth/2+_TrayRad,					900,
					-(_TrayX),					-_trayDepth/2+_TrayRad,					14+3000+64,
		
					-_TrayX,					-_trayDepth/2+_TrayY-_TrayRad,			14+64,
					-(_TrayX-_TrayRad),			-_trayDepth/2+_TrayY-_TrayRad,			900,
					-(_TrayX-_TrayRad),			-_trayDepth/2+_TrayY,					14+3000+64,
					0,							-_trayDepth/2+_TrayY,					-1

		else		!  iTrayStyle = STYLE_OVALTRAY
			hprism_ 	gs_tray_mat,		gs_tray_mat,	gs_tray_mat, 			gs_tray_mat,	
				30,		_legThick/2,			0, 				0.004,					1,
				0,											_trayDepth/2 +_trayEdge,						15+64,		!1
				-(_trayWidth/2-_trayRad3),					_trayDepth/2 +_trayEdge,						15+64,		!2
				-(_trayWidth/2-_trayRad3),					_trayDepth/2-_trayRad3,							900,		!3
				-(_trayWidth/2+_trayEdge), 					_trayDepth/2-_trayRad3,							3000+15+64,	!4
				-(_trayWidth/2+_trayEdge),					-(_trayDepth/2-_trayRad3),						15+64,		!5
				-(_trayWidth/2-_trayRad3),					-(_trayDepth/2-_trayRad3),						900,		!6	
			
				-(_trayWidth/2-_trayRad3-cos(60)*(_trayRad3+_trayEdge)), -(_trayDepth/2-_trayRad3+ sin(60)*(_trayRad3+_trayEdge)),	3000+15+64, !7
				0,											-tan(60) * (_trayWidth/2- _trayRad3/2), 		900,		!8
				
				_trayWidth/2-_trayRad3-cos(60)*(_trayRad3+_trayEdge), -(_trayDepth/2-_trayRad3+ sin(60)*(_trayRad3+_trayEdge)),	3000+15+64, !9
				_trayWidth/2-_trayRad3,						-(_trayDepth/2-_trayRad3),						900,		!10	
				_trayWidth/2+_trayEdge,						-(_trayDepth/2-_trayRad3),						3000+15+64,	!11
				_trayWidth/2+_trayEdge, 					_trayDepth/2-_trayRad3,							15+64,		!12
				_trayWidth/2-_trayRad3,						_trayDepth/2-_trayRad3,							900,		!13
				_trayWidth/2-_trayRad3,						_trayDepth/2+_trayEdge,							3000+15+64,	!14
				0,											_trayDepth/2+_trayEdge,							-1,			!15


				0,											_trayDepth/2,									12,			!1
				-(_trayWidth/2-_trayRad3),					_trayDepth/2,									14+64,		!2
				-(_trayWidth/2-_trayRad3),					_trayDepth/2-_trayRad3,							900,		!3
				-(_trayWidth/2), 							_trayDepth/2-_trayRad3,							3000+14+64,	!4
				-(_trayWidth/2),							-(_trayDepth/2-_trayRad3),						14+64,		!5
				-(_trayWidth/2-_trayRad3),					-(_trayDepth/2-_trayRad3),						900,		!6	
			
				-(_trayWidth/2-_trayRad3-cos(60)*_trayRad3),-(_trayDepth/2-_trayRad3+ sin(60)*_trayRad3),	3000+14+64,	!7
				0,											-tan(60) * (_trayWidth/2- _trayRad3/2), 		900,		!8
				
				_trayWidth/2-_trayRad3-cos(60)*_trayRad3, 	-(_trayDepth/2-_trayRad3+ sin(60)*_trayRad3),	3000+14+64,	!9
				_trayWidth/2-_trayRad3,						-(_trayDepth/2-_trayRad3),						900,		!10	
				_trayWidth/2,								-(_trayDepth/2-_trayRad3),						3000+14+64,	!11
				_trayWidth/2, 								_trayDepth/2-_trayRad3,							14+64,		!12
				_trayWidth/2-_trayRad3,						_trayDepth/2-_trayRad3,							900,		!13
				_trayWidth/2-_trayRad3,						_trayDepth/2,									3000+14+64,	!14
				0,											_trayDepth/2,									-1			!1

		endif
	else

! ------ Simple geometry -------------------------------------------------------

			hprism_ 	gs_tray_mat,			gs_tray_mat,			gs_tray_mat, gs_tray_mat,	
					42,					_legThick*1.5,		0, 			0.004,	1,
					0,								-_trayDepth2/2+_TrayY2,			15+64,
					_TrayX2-_TrayRad2,				-_trayDepth2/2+_TrayY2,			15+64,
					_TrayX2,						-_trayDepth2/2+_TrayY2-_TrayRad2,15+64,
					_TrayX2,						-_trayDepth2/2+_TrayRad2,		15+64,
					_TrayX2+_TrayRad2,				-_trayDepth2/2,					15+64,
			
					_trayWidth2/2-_legRad, 			-_trayDepth2/2,					15+64,
					_trayWidth2/2, 					-(_trayDepth2/2-_legRad), 		15+64,
					_trayWidth2/2,					0,								15+64,
					_trayWidth2/2, 					_trayDepth2/2-_legRad, 			15+64,
					_trayWidth2/2-_legRad*0.7,		_trayDepth2/2,					15+64,
					0,								_trayDepth2/2,					15+64,
		
					-(_trayWidth2/2-_legRad*0.7),	_trayDepth2/2,					15+64,
					-_trayWidth2/2, 				_trayDepth2/2-_legRad, 			15+64,
					-_trayWidth2/2,					0,								15+64,
					-_trayWidth2/2, 				-(_trayDepth2/2-_legRad), 		15+64,
					-_trayWidth2/2+_legRad, 		-_trayDepth2/2,					15+64,
		
					-(_TrayX2+_TrayRad2),			-_trayDepth2/2,					15+64,
					-(_TrayX2),						-_trayDepth2/2+_TrayRad2,		15+64,
		
					-_TrayX2,						-_trayDepth2/2+_TrayY2-_TrayRad2,15+64,
					-(_TrayX2-_TrayRad2),			-_trayDepth2/2+_TrayY2,			15+64,
					0,								-_trayDepth2/2+_TrayY2,			-1,
		
					0,								-_trayDepth/2+_TrayY,			15+64,
					_TrayX-_TrayRad,				-_trayDepth/2+_TrayY,			15+64,
					_TrayX,							-_trayDepth/2+_TrayY-_TrayRad,	15+64,
					_TrayX,							-_trayDepth/2+_TrayRad,			15+64,
					_TrayX+_TrayRad,				-_trayDepth/2,					15+64,
		
					_trayWidth/2-_legRad, 			-_trayDepth/2,					15+64,
					_trayWidth/2, 					-(_trayDepth/2-_legRad), 		15+64,
					_trayWidth/2,					0,								15+64,
					_trayWidth/2, 					_trayDepth/2-_legRad, 			15+64,
					_trayWidth/2-_legRad*0.7,		_trayDepth/2,					15+64,
					0,								_trayDepth/2,					15+64,
		
		
					-(_trayWidth/2-_legRad*0.7),	_trayDepth/2,					15+64,
					-_trayWidth/2, 					_trayDepth/2-_legRad, 			15+64,
					-_trayWidth/2,					0,								15+64,
					-_trayWidth/2, 					-(_trayDepth/2-_legRad),		15+64,
					-_trayWidth/2+_legRad, 			-_trayDepth/2,					15+64,
		
					-(_TrayX+_TrayRad),				-_trayDepth/2,					15+64,
					-(_TrayX),						-_trayDepth/2+_TrayRad,			15+64,
		
					-_TrayX,						-_trayDepth/2+_TrayY-_TrayRad,	15+64,
					-(_TrayX-_TrayRad),				-_trayDepth/2+_TrayY,			15+64,
					0,								-_trayDepth/2+_TrayY,			-1
	endif
del 1
endif


! ==============================================================================
! Grouping Operations
! ==============================================================================


Subtract_sides_backrest = subgroup ("GroupSides", "GroupBackrest")
Subtract_cushion_backrest = subgroup ("GroupBackrest", "GroupBackCushion")
Union_backrest_legcover =  addgroup("GroupLegCover", Subtract_cushion_backrest)

placegroup Subtract_sides_backrest
placegroup "GroupBackCushion"
placegroup Union_backrest_legcover

killgroup "GroupBackCushion"
killgroup "GroupSides"
killgroup  "GroupBackrest"

del 2

! ==============================================================================
  end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================
! ------------------------------------------------------------------------------
"FootHolder":			!Subroutine for creating fottholder frame
! ------------------------------------------------------------------------------

! local variables:
	!__Width 		!received input from the calling script
	!__Depth 		!received input from the calling script
	!__Rad			!received input from the calling script
	!__axisX		
	!__iSegment

! input parameters from subroutine "ArcTube"
	!_TubeRad 
	!_arcAng
	!_arcRad 
	!_origoY 
	!_origoX 
	!_axisX
	!_startRad 
	!_iArcResol
! ------------------------------------------------------------------------------

roty 90
rotx 90
add -__Width/2,-__Depth/2,0

for k=1 to 2

		_TubeRad = __Rad
		_arcAng = 90
		_arcRad = _legRad
		_origoY = _legRad
		_origoX = __Width/2-_legRad
		__axisX = __Width/2
		_startRad = 270

	if gs_detlevel_3D_m = 2 then	

		gosub "ArcTube"
		__iSegment = _iArcResol+1

		tube 	2,  		4+__iSegment, 	0,
				0, 			0, 				901,
				_TubeRad, 	360, 			4001,
				-1,			0,				0,		0,
				0,			0,				0,		0,
				get (__iSegment*4),
				__axisX,	__Depth/2,		0,		0,
				__axisX, 	__Depth/2+1,	0,		0
	else

			tube 	4,  		6, 			0,
				_legThick/2, 	0, 			0,
				_legThick/2, 	_legThick,	0,
				-_legThick/2,	_legThick,	0,
				-_legThick/2,	0,			0,
				-2,				0,				0,			0,
				0,				0,				0,			0,
				__Width/2,		0,				0,			0,
				__Width/2,		__Depth/2,		0,			0,
				__axisX, 		__Depth/2,		0,			0,
				__axisX, 		__Depth/2+0.1,	0,			0

	endif
		
mul -1,1,1
next k
del k-1
del 3

_TubeRad = _legThick/2
_arcAng = 60
_arcRad = _legRad
_origoY = _legRad
_origoX = __Depth/2-_legRad
_startRad = 270

for k=1 to 2
	gosub "ArcTube"
	__iSegment = _iArcResol+1
	if gs_detlevel_3D_m = 2 then
		tube 	2,  4+__iSegment, 0,
				0, 				0, 		901,
				_TubeRad, 		360, 	4001,
				-0.2,			0,		0,		0,
				0,				0,		0,		0,
				get (__iSegment*4),
				_origoX+_legRad/sin(60)+0.02,	_origoY+tan(60)*0.02,	0,0,
				_origoX+_legRad/sin(60)+0.5,	_origoY+tan(60)*0.5,	0,0
	else
		add 0,-_legThick/2,-_legThick/2
		tube 	4,  4+__iSegment, 0,
			_legThick/2, 0, 0,
			_legThick/2, _legThick,0,
			-_legThick/2,_legThick,0,
			-_legThick/2,0,0,
			-0.2,			0,		0,		0,
			0,				0,		0,		0,
			get (__iSegment*4),
			_origoX+_legRad/sin(60)+0.02,	_origoY+tan(60)*0.02,	0,0,
			_origoX+_legRad/sin(60)+0.5,	_origoY+tan(60)*0.5,	0,0
		del 1
	endif
	
	addz __Width
next k
del k-1

add _origoX+_legRad/sin(60)+0.05,	_origoY+tan(60)*0.05,	0
addz __Width/2	
roty 90
rotx 180+30


for k=1 to 2
		_TubeRad = _legThick/2
		_arcAng = 90
		_arcRad = _legRad
		_origoY = _legRad
		_origoX = __Width/2-_legRad
		__axisX = __Width/2
		_startRad = 270
		gosub "ArcTube"
		__iSegment = _iArcResol+1

		if gs_detlevel_3D_m = 2 then
			tube 2,  		4+__iSegment, 	0,
				0, 			0, 				901,
				_TubeRad, 	360, 			4001,
				-1,			0,		0,		0,
				0,			0,		0,		0,
				get (__iSegment*4),
				__axisX, 	0.06,	0,		0,
				__axisX, 	0.1+0.1,0,		0
				
! ----------edge protection ------------------------------------------------------------------

			if bEdgeProtect then 
				material gs_edge_mat
				_TubeRad = _legThick/2 + _edgeThick
				gosub "ArcTube"
				__iSegment = _iArcResol+1

				tube 2,  		4+__iSegment, 	0,
					0, 			0, 				901,
					_TubeRad, 	360, 			4001,
					-1,			0,		0,		0,
					0,			0,		0,		0,
					get (__iSegment*4),
					__axisX, 	0.06,	0,		0,
					__axisX, 	0.1+0.1,0,		0	
				material gs_frame_mat

			endif
		else

			add 0,_legThick/2,-0.75* _legThick
			tube 	4,  	6, 			0,
				_legThick/2, 0, 			0,
				_legThick/2, _legThick,	0,
				-_legThick/2,_legThick,	0,
				-_legThick/2,0,			0,
				-2,			0,			0,	0,
				0,			0,			0,	0,
				__Width/2,	0,			0,	0,
				__Width/2,	__Depth,	0,	0,
				__axisX, 	0.06,		0,	0,
				__axisX, 	0.1+0.1,	0,	0
		del 1
		endif
		
	mul -1,1,1
next k
del k-1	
del 4
return


! ------------------------------------------------------------------------------
"ArcTube":			!Subroutine for creating tubes along curved path
! ------------------------------------------------------------------------------
! the path of the tube lies in X-Y plane
! _origoX		! center point of arc		
! _origoY		! center point of arc	
! _TubeRad		! radius of tube cross-section
! _arcRad		! radius of arc (path)
! _arcAng		! angle of arc (path)
! _startRad		! starting radius 

_iArcResol		= _arcAng/(360/gs_resol)

if 	_iArcResol - int(_iArcResol)<0.5 then
	_iArcResol 	= int(_iArcResol)
	beta 		= _arcAng / _iArcResol
else
	_iArcResol 	= int (_iArcResol) + 1
	beta 		= _arcAng / _iArcResol
endif

__alpha= _startRad
for i = 1 to _iArcResol+1
	put  _origoX +_arcRad*cos (__alpha),_origoY+ _arcRad * sin (__alpha), 0, 0
	__alpha = __alpha + beta
next i

return


! ------------------------------------------------------------------------------
"Cushion" :		! Cushion Subroutine
! ------------------------------------------------------------------------------

cush_a 				= _seatWidth-_legThick
cush_b 				= seatDepth
cush_h 				= _seatThick*2
r2					= 0.01
r1					= _legRad
gs_resol 			= gs_resol
cush_mat 			= gs_seat_mat

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 then

	BASE

	cprism_ gs_seat_mat,gs_seat_mat,gs_seat_mat,
		5,cush_h,
		0,0,15,
		A-_legRad*4,0,15,
		A-_legRad*4,cush_b,15,
		0,cush_b,15,
		0,0,-1


	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4


	body -1

else

	resol gs_resol

	addz cush_h-(r2+0.01)

	for i=1 to 2
		base

		! Fillet Resolution
		S = 90/gs_resol
		for ii=0 to gs_resol
			PUT r1+r1*sin(270 - ii*S),r1+r1*cos(270 - ii*S),78
		next ii
		for ii=0 to gs_resol
			PUT cush_a-r1+r1*sin(180 - ii*S),r1+r1*cos(180 - ii*S),78
		next ii
		for ii=0 to gs_resol
			PUT cush_a-r1+r1*sin(90 - ii*S),cush_b-r1+r1*cos(90 - ii*S),78
		next ii
		for ii=0 to gs_resol
			PUT r1+r1*sin(360 - ii*S),cush_b-r1+r1*cos(360 - ii*S),78
		next ii

		hprism_ gs_seat_mat,gs_seat_mat,gs_seat_mat,gs_seat_mat,
					12+(NSP/3),r2+0.01,0,r2, 1,
			0,r1+cush_b/1000,14,
			0,r1,78,
					GET (NSP/4),
			r1,0,14,
			cush_a-r1-cush_a/1000,0,14,
			cush_a-r1,0,78,
					GET (NSP/3),
			cush_a,r1,14,
			cush_a,cush_b-r1-cush_b/1000,14,
			cush_a,cush_b-r1,78,
					GET (NSP/2),
			cush_a-r1,cush_b,14,
			r1+cush_a/1000,cush_b,14,
			r1,cush_b,78,
					GET (NSP),
			0,cush_b-r1,14

		rotx 90
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1
		del 1

		coor 2, -1, -2, -3, -4
		body -1

		mulz -1
		addz cush_h-2*(r2+0.01)
	next i
	del 4

	addz -(cush_h-2*(r2+0.01))

	! Fillet Resolution
	S = 90/gs_resol
	for ii=0 to gs_resol
		PUT r1+r1*sin(270 - ii*S),r1+r1*cos(270 - ii*S),74
	next ii
	for ii=0 to gs_resol
		PUT cush_a-r1+r1*sin(180 - ii*S),r1+r1*cos(180 - ii*S),74
	next ii
	for ii=0 to gs_resol
		PUT cush_a-r1+r1*sin(90 - ii*S),cush_b-r1+r1*cos(90 - ii*S),74
	next ii
	for ii=0 to gs_resol
		PUT r1+r1*sin(360 - ii*S),cush_b-r1+r1*cos(360 - ii*S),74
	next ii

	cprism_ gs_seat_mat,gs_seat_mat,gs_seat_mat,
				12+(NSP/3),cush_h-2*(r2+0.01),
		0,r1+cush_b/1000,10,
		0,r1,74,
				GET (NSP/4),
		r1,0,10,
		cush_a-r1-cush_a/1000,0,10,
		cush_a-r1,0,74,
				GET (NSP/3),
		cush_a,r1,10,
		cush_a,cush_b-r1-cush_b/1000,10,
		cush_a,cush_b-r1,74,
				GET (NSP/2),
		cush_a-r1,cush_b,10,
		r1+cush_a/1000,cush_b,10,
		r1,cush_b,74,
				GET (NSP),
		0,cush_b-r1,10

	del 1
	del 1

	base

	rotz 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1

	coor 2, -1, -2, -3, -4
	body -1

endif
return

! ------------------------------------------------------------------------------
"Backrest" :
! ------------------------------------------------------------------------------

material materialBackrest

add 0, distLegs/2 +seatDepth/2+0.01, gs_seat_height
rotx -_backAng 

mul 1,1.46,1
sweep 13, 2, 0, 1.22 ,4+2+1,
	0, 										0,							1,			!7
	-(_backWidth-_legRad),					0,							1,			!8
	-(_backWidth-_legRad),					-_legRad,					901,		!9
	-(_backWidth-_legRad+__XX),				-_legRad+__XX,				3001,		!10
	-(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	1001,		!14
	-(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		901,
	-(_backWidth-_sideThick-_legRad),		-_sideThick,				3001,		!16
	
	(_backWidth-_sideThick-_legRad),		-_sideThick,				1,			!16
	(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		901,
	(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	3001,		!14
	(_backWidth-_legRad+__XX),				-_legRad+__XX,				1001,		!10
	(_backWidth-_legRad),					-_legRad,					901,		!9
	(_backWidth-_legRad),					0,							3001,		!8
	
	0, 0, 0,
	0, 0, _backLength*0.7-0.014

	mulz -1
	
	hprism_ materialBackrest, materialBackrest  ,materialBackrest  , materialBackrest ,  
		15,0.046,0,0.046,1,
	0, 										0,							10+64,			!7
	-(_backWidth-_legRad),					0,							10+64,			!8
	-(_backWidth-_legRad),					-_legRad,					900,			!9
	-(_backWidth-_legRad+__XX),				-_legRad+__XX,				3000+10+64,		!10
	-(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	10+1000+64,		!14
	-(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		900,
	-(_backWidth-_sideThick-_legRad),		-_sideThick,				3000+10+64,		!16
	0,										- _sideThick,				10+64,			!17
	
	(_backWidth-_sideThick-_legRad),		-_sideThick,				10+64,			!16
	(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		900,
	(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	10+3000+64,		!14
	(_backWidth-_legRad+__XX),				-_legRad+__XX,				1000+10+64,		!10
	(_backWidth-_legRad),					-_legRad,					900,			!9
	(_backWidth-_legRad),					0,							3000+10+64,		!8
	0, 										0,							10+64			!7
del 1


	addz _backLength*0.7-0.014
	mul 1.22,1.22,1

sweep 13, 5, 0,	0.92,	1+2+4,
	0, 										0,							1,			!7
	-(_backWidth-_legRad),					0,							1,			!8
	-(_backWidth-_legRad),					-_legRad,					901,		!9
	-(_backWidth-_legRad+__XX),				-_legRad+__XX,				3001,	!10
	-(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	1001,		!14
	-(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		901,
	-(_backWidth-_sideThick-_legRad),		-_sideThick,				3001,		!16
	
	(_backWidth-_sideThick-_legRad),		-_sideThick,				1,			!16
	(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		901,
	(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	3001,		!14
	(_backWidth-_legRad+__XX),				-_legRad+__XX,				1001,		!10
	(_backWidth-_legRad),					-_legRad,					901,		!9
	(_backWidth-_legRad),					0,							3001,		!8
	
	0, 0, 0,
	0,0, _backLength*0.13,
	0,0, _backLength*0.17,
	0,0, _backLength*0.20,
	0,0,_backLength*0.22

	addz _backLength*0.22
	mul 0.717,0.717,1

sweep 13, 4, 0,	0.7,	1+2+4+32,
	0, 										0,							1,			!7
	-(_backWidth-_legRad),					0,							1,			!8
	-(_backWidth-_legRad),					-_legRad,					901,		!9
	-(_backWidth-_legRad+__XX),				-_legRad+__XX,				3001,		!10
	-(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	1001,		!14
	-(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		901,
	-(_backWidth-_sideThick-_legRad),		-_sideThick,				3001,		!16
	
	(_backWidth-_sideThick-_legRad),		-_sideThick,				1,			!16
	(_backWidth-_sideThick-_legRad),		-_sideThick-_legRad,		901,
	(_backWidth-_sideThick-_legRad+__XX),	-_sideThick-_legRad+__XX,	3001,		!14
	(_backWidth-_legRad+__XX),				-_legRad+__XX,				1001,		!10
	(_backWidth-_legRad),					-_legRad,					901,		!9
	(_backWidth-_legRad),					0,							3001,		!8
	
	0, 0, 0,
	0,0, _backLength*0.05,
	0,0, _backLength*0.07,
	0,0,_backLength*0.08


del 7
return

! ==============================================================================
! Texture Subroutines
! ==============================================================================
"Horizontal Texture":
roty 90
gosub "Texture Coordinates"
del 1
return


"Vertical Texture":
gosub "Texture Coordinates"
return

"Slanting Texture":
rotz 90-angle
gosub "Texture Coordinates"
del 1
return

"Texture Coordinates":
vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

