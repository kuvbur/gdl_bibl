


! ==============================================================================
! SCALE
! ==============================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters det2D

pen gs_cont_pen

! ------------------------------------------------------------------------------
!  Hotspots 
! ------------------------------------------------------------------------------
unID=1

hotspot2 a/2,	0,		unID	: unID=unID+1
hotspot2 a/2,	b/2,	unID	: unID=unID+1

hotspot2 0,		0,		unID	: unID=unID+1
hotspot2 0,		B,		unID	: unID=unID+1
hotspot2 A,		B,		unID	: unID=unID+1
hotspot2 A,		0,		unID	: unID=unID+1

! ==============================================================================
! 2D Symbol
! ==============================================================================

fill gs_fill_type

if det2D = 4 then		! Symbolic
	call "fa_ChairSymbol" parameters all 	gs_OrigoCentered = 0
else
	call "Resize_A_B_ZZYZX" Parameters ALL

	if det2D = 3 then		! 1:50
		gosub "DetailedSymbol"
		gosub "DetailedMinSpace"
	else
		if det2D = 1 then		! 1:200
			gosub "DraftSymbol"
			gosub "SimpleMinSpace"
		else	! 1:100
			gosub "SimpleSymbol"
			gosub "DetailedMinSpace"
		endif
	endif
endif


! ==============================================================================
 end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
"DetailedMinSpace":
! ==============================================================================
  drawindex 10
  call "fa_minimalspace" parameters all MSType = 3, unID = 3000
return

! ==============================================================================
"SimpleMinSpace":
! ==============================================================================
	drawindex 10
  call "fa_minimalspace" parameters all MSType = 4, unID = 3000
return

! ==============================================================================
"DetailedSymbol":
! ==============================================================================
	drawindex 20
	! ----- Legs ---------------------------------------------------------------

	for i = 1 to 2
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				_x2D3,	_y2D2, 		0,
				_x2D3,	B-_y2D2, 	0,
				0,B,1,
				0,0,0
			
		poly2_b 8, 1+2+4+64, gs_fill_pen, gs_back_pen,
				_x2D3,		_y2D2, 		1,
				_x2D3,		B-_y2D2, 	1,
				0,			B,			1,
				_x2D1,		B,			1,
				_x2D1+_x2D3,B-_y2D2, 	0,
				_x2D1+_x2D3,_y2D2, 		1,
				_x2D1,		0,			1,
				0,			0,			1
		add2 A, 0
		mul2 -1,1
	next i
	del (i-1)*2
	
	! ----- Seating ---------------------------------
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			_x2D1+_x2D3,	_y2D2, 		1,
			_x2D1+_x2D3,	B-_y2D2, 	1,
			A-_x2D1-_x2D3,	B-_y2D2, 	1,
			A-_x2D1-_x2D3, _y2D2, 		1
	
	
	! ----- Bottom Rods -----------------------------
	_RodWidth = 0.02
	for i = 1 to 2 
		poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				_x2D1 + _x2D2,								_y2D1, 1, 
				A - (_x2D1 + _x2D2), 						_y2D1, 0,
				A - (_x2D1 + _x2D2+_RodWidth/tan(_frameAngle2D)),_y2D1 + _RodWidth, 1,
				_x2D1 + _x2D2+_RodWidth/tan(_frameAngle2D),		_y2D1 + _RodWidth, 0	
		add2 0, B
		mul2 1,-1
	next i
	del (i-1)*2
	
	! ----- Top Rod ---------------------------------
	_RodDepth	= 0.03
	poly2_ 4,  1+2,
			_x2D1+_x2D3,	_y2D2+_RodDepth, 		1,
			A-_x2D1-_x2D3,	_y2D2+_RodDepth, 		0,
			A-_x2D1-_x2D3,	_y2D2+2*_RodDepth, 		1,
			_x2D1+_x2D3,	_y2D2+2*_RodDepth, 		0
	
	
	! ----- Backrest --------------------------------
	poly2_ 4,  1+2,
			_x2D1+_x2D3,	B-_y2D2-_backThick*1.5, 		1,
			A-_x2D1-_x2D3,	B-_y2D2-_backThick*1.5, 		0,
			A-_x2D1-_x2D3,	B-_y2D2-_backThick*0.5, 		1,
			_x2D1+_x2D3,	B-_y2D2-_backThick*0.5, 		0

return

! ==============================================================================
"SimpleSymbol":
! ==============================================================================
	drawindex 20
	! ----- Background Fill ----------------------------------------------------
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,	0, 	1,
			A,	0, 	0,
			A,	B, 	1,
			0, B, 	0

	! ----- Legs ---------------------------------------------------------------
	for i = 1 to 2
		poly2 	5,  1,
				_x2D3,	_y2D2, 	
				_x2D3,	B-_y2D2,
				0,B,
				0,0,
				_x2D3,	_y2D2

		add2 A, 0
		mul2 -1,1
	next i
	del (i-1)*2

	! ----- Seating ------------------------------------------------------------
	poly2 5,  1,
			_x2D3,	_y2D2, 	
			_x2D3,	B-_y2D2, 	
			A-_x2D3,	B-_y2D2, 	
			A-_x2D3, _y2D2, 		
			_x2D3,	_y2D2

	! ----- Backrest -----------------------------------------------------------
	line2 	A-_x2D3,	B-_y2D2-_backThick, 
			_x2D3,	B-_y2D2-_backThick 	

return

! ==============================================================================
"DraftSymbol":
! ==============================================================================
	drawindex 20
	poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,0,1,
		0,B,1,
		A,B,1,
		A,0,1
return


