

! ==============================================================================
! 3D Detail Level
! ==============================================================================

if gs_detlevel_3D_m = 0 then END
if gs_shadow=0 then SHADOW OFF
resol gs_resol
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

! ==============================================================================
! Hotspot Editing
! ==============================================================================

! ----- A,B dimensions ---------------------------------------------------------
unID = 1
hotspot 0, 0, 0, unID	: unID=unID+1
hotspot a, 0, 0, unID	: unID=unID+1
hotspot 0, b, 0, unID	: unID=unID+1
hotspot a, b, 0, unID	: unID=unID+1

! ----- ZZYZX (Height) ---------------------------------------------------------
hotspot a/2,b- _legHeight/ tan(legAngle)-_backThick/2,	0, 		unID, 	zzyzx, 1+128	: unID=unID+1
hotspot a/2,b- _legHeight/ tan(legAngle)-_backThick/2,	-1, 	unID,	zzyzx, 3		: unID=unID+1
hotspot a/2,b- _legHeight/ tan(legAngle)-_backThick/2,	zzyzx, 	unID, 	zzyzx, 2		: unID=unID+1

! ----- seat height ------------------------------------------------------------
hotspot a/2,b/2,0, unID, gs_seat_height, 1+128			: unID=unID+1
hotspot a/2,b/2,-1, unID, gs_seat_height, 3				: unID=unID+1
hotspot a/2,b/2,gs_seat_height, unID, gs_seat_height, 2	: unID=unID+1

! ==============================================================================
! 3D GEOMETRY
! ==============================================================================
add a, b/2, 0
rotz -90

material gs_frame_mat
pen gs_cont_pen

! ------------------------------------------------------------------------------
! Legs
! ------------------------------------------------------------------------------

cutplane 1,1,0,1
rotx 90
	
rotx 90-frameAngle
gosub "Legs"
del 1
	
mul 1,1,-1			
addz -A
rotx 90-frameAngle
gosub "Legs"
del 3
	
del 1
cutend


! ------------------------------------------------------------------------------
! Rods,seating and backrest
! ------------------------------------------------------------------------------

rotx 90

rotx 90-frameAngle					
addz  _legThick
cutplane 1,1,0,1				! cuts the superfluous ends of rods on the right side
del 2
	
mul 1,1,-1			
addz -A+_legThick/ sin( frameAngle)
rotx 90-frameAngle
cutplane 1,1,0,1			! cuts the superfluous ends of rods on the left side
del 3


_RodWidth	=_legWidthTop
_RodDepth	= _seatThick
_RodLength	= A-2*gs_seat_height/tan(frameAngle)+_legThick
_bStrap	= 0

add -_legWidthTop/2,gs_seat_height-_seatThick, gs_seat_height/tan(frameAngle)
gosub  "Rod"			! ----- seating
del 1

_RodWidth	= 0.02
_RodDepth	= 0.03
_RodLength	= A-2*sin(90-frameAngle)*(_legHeight- _legThick/tan(frameAngle)-_legFrameDepth*0.75)
_bStrap	= 1
add _legWidthTop/2-_RodDepth/2,sin(frameAngle)*(_legHeight- _legThick/tan(frameAngle)-_legFrameDepth*0.75),sin(90-frameAngle)*(_legHeight- _legThick/tan(frameAngle)-_legFrameDepth*0.75) 
rotz  90 
gosub  "Rod"			! ----- top rod
del 2
		
_RodLength	= A
_bStrap	= 0
add  B/2-gs_seat_height/3/tan(legAngle)-_legFrameDepth/2-_RodWidth,gs_seat_height/3, 0
rotz - legAngle
gosub  "Rod"			! ----- bottom rod
del 2
		
_RodLength	= A
add  -(B/2-gs_seat_height/3/tan(legAngle)-_legFrameDepth/2-_RodWidth),gs_seat_height/3, 0
rotz  legAngle
gosub  "Rod"			! ----- bottom rod
del 2
		
_RodLength	= A
add  B/2-gs_seat_height*2/3/tan(legAngle)-_legFrameDepth/2-_RodWidth,	gs_seat_height*2/3,	 0
rotz - legAngle		! ----- middle rod
gosub  "Rod"
del 2

roty 90
add - A/2,_backHeight,-_legWidthTop/2+_backThick/2
gosub "Backrest"		! ----- backrest
del 2

cutend
cutend
del 3

! ==============================================================================
  end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
"Legs":
! ==============================================================================
!input parameters: (user - defined)
!	B
!input parameters (defined in master script):
!	legAngle
!	_seatFrameDepthX
!	_seatFrameX0, _seatFrameX1, _seatFrameX2, _seatFrameX3
!	_centerYBot, _centerYTop 
!	_centerXBot, _centerXTop
!	_legThick
!	_legFrameRad 
!	_legWidthTop	
!	_seatFrameDepth	
!	_seatFrameHeight
!	_legHeight
!	_legFrameDepth
! ==============================================================================

	! ------------------------------------------------------------------------------
	! Detailed 3D representation
	! ------------------------------------------------------------------------------

	if gs_detlevel_3D_m = 2 then				

		! ----- slanting parts of leg-frame ----------------------------------------
		for i = 1 to 2		
			prism_ 8, _legThick,
					_centerXBot,								0,											15+64,
					_centerXBot+1,								0,											800,
					_centerXBot+sin(legAngle)*_legFrameRad,		_centerYBot+sin(90-legAngle)*_legFrameRad,	1000+15+64,
					_centerXTop+_legFrameRad*sin(legAngle),		_centerYTop+_legFrameRad*sin(90-legAngle),	15+64,
					_centerXTop,								_centerYTop, 								900,
					_centerXTop,								_legHeight,									3000+15+64,
					_legWidthTop/2-_legFrameDepthX,				_legHeight,									15,
					B/2-_legFrameDepthX, 						0, 											15
			gosub "Slanting Texture"
			mul -1,1,1
		next i
		del i-1
	
		! ----- horizontal parts of leg-frame --------------------------------------
		prism_ 4, _legThick,													! top horizontal part
				_legWidthTop/2-_legFrameDepthX,		_legHeight,					15,	
				-(_legWidthTop/2-_legFrameDepthX),	_legHeight,					15,	
				-_seatFrameX3,						_legHeight-_legFrameDepth,	15,
				_seatFrameX3,						_legHeight-_legFrameDepth,	15
		gosub "Horizontal Texture"
	
		prism_ 4,_legThick,													!middle horizontal part
				_seatFrameX1,			_seatFrameHeight,						15,	
				_seatFrameX2,			_seatFrameHeight+_seatFrameDepth,		15,	
				-_seatFrameX2,			_seatFrameHeight+_seatFrameDepth,		15,
				-_seatFrameX1,			_seatFrameHeight,						15	
		gosub "Horizontal Texture"
				
		prism_ 4, _legThick,													!bottom horizontal part
				B/2-_legFrameDepthX, 	0, 						15,
				_seatFrameX0,			_legFrameDepth,			15,
				-_seatFrameX0,			_legFrameDepth,			15,
				-(B/2-_legFrameDepthX), 0, 						15

		gosub "Horizontal Texture"

	else					
	! ------------------------------------------------------------------------------
	! Simple 3D representation
	! ------------------------------------------------------------------------------
		prism_ 15, _legThick,
			B/2, 				0,					15,
			_legWidthTop/2, 	_legHeight,			15,
			-_legWidthTop/2, 	_legHeight,			15,
			-B/2, 				0,					15,
			B/2, 				0,					-1,

			_seatFrameX0, 		_legFrameDepth,		15,
			_seatFrameX1,		_seatFrameHeight,	15,
			-_seatFrameX1,		_seatFrameHeight,	15,
			-_seatFrameX0, 		_legFrameDepth,		15,
			_seatFrameX0, 		_legFrameDepth,		-1,

			_seatFrameX2,		_seatFrameHeight+_seatFrameDepth,	15,
			_seatFrameX3,		_legHeight- _legFrameDepth,			15,
			-_seatFrameX3,		_legHeight- _legFrameDepth,			15,	
			-_seatFrameX2,		_seatFrameHeight+_seatFrameDepth,	15,
			_seatFrameX2,		_seatFrameHeight+_seatFrameDepth,	-1	
	
	endif

return

! ==============================================================================
"Backrest":
! ==============================================================================
!input parameters (defined in master script):
!	 iBackrest
!	_legHeightY
!	_backHeight
!	_backThick 
!	_backWidthTop	
!	_backWidthBot 
!	_backRad	
!	_backDepth	
!	_backDistX, _backDistY
!	_backDistX2
!	_backCenterY, _backCenterX
!	_backCenterY2, _backCenterX2
!	_backCenterY3, _backCenterX3
! ==============================================================================

	! ------------------------------------------------------------------------------
	! Detailed 3D representation
	! ------------------------------------------------------------------------------

	if gs_detlevel_3D_m = 2 then
		if iBackrest = BACKREST_TRAPEZOID then
			prism_ 8,_seatThick,
					_backWidthBot/2, 						0,											15,
					_backCenterX+_backRad*sin(frameAngle),	_backCenterY+_backRad*sin(90-frameAngle),	15+64,
					_backCenterX,							_backCenterY,								900,
					_backCenterX,							_backDepth,									3000+64+15,
					-_backCenterX,							_backDepth, 								15+64,
					-_backCenterX,							-_backCenterY,								900,
					-(_backCenterX+_backRad*sin(frameAngle)),_backCenterY+_backRad*sin(90-frameAngle),	1000+64+15,
					-_backWidthBot/2, 						0,											15

		else
			prism_ 14, _seatThick, 
					_backWidthBot/2, 					0,								15,
					_backCenterX2,	 				_backDepth-ZZYZX+_legHeightY,		15+64,
					_backCenterX2-100,	 			_backDepth-ZZYZX+_legHeightY,		800,
					_backCenterX2-_backDistX, 		_backCenterY2 - _backDistY,			1000+15+64,
				
					_backCenterX3 + _backDistX,		_backCenterY3+_backDistY,			15+64,
					_backCenterX3,					_backCenterY3,						900,
					_backCenterX3, 					_backDepth,							3000+64+15,
					! ----- symmetry on axis Y => : X coordinates turn negative, Y coordinates remain the same
	
					-_backCenterX3, 				_backDepth,							15+64,
					-_backCenterX3,					-_backCenterY3,						900,
					-(_backCenterX3 + _backDistX), 	_backCenterY3+_backDistY,			1000+64+15,
	
					-(_backCenterX2-_backDistX), 	_backCenterY2 - _backDistY,			15+64,		
					-_backCenterX2,	 				_backCenterY2,						900,
					-_backCenterX2,	 				_backDepth-ZZYZX+_legHeightY,		3000+64+15,
	
					-_backWidthBot/2,				0,									15

		endif
	else
	! ------------------------------------------------------------------------------
	! Simple 3D representation
	! ------------------------------------------------------------------------------
		prism_ 4, _backThick,
				_backWidthBot/2, 		0,				15,
				_backWidthTop/2,		_backDepth,		15,
				-_backWidthTop/2,		_backDepth,		15,
				-_backWidthBot/2, 		0,				15	
	endif

return

! ==============================================================================
"Rod":
! ==============================================================================
!input parameters:  (defined when calling subroutine)
!	_RodWidth					
!	_RodDepth					
!	_RodLength					
!	_bStrap					!bool for safety strap
!	gs_detlevel_3D
! ==============================================================================

	!local variables:						
	__StripeThick = 0.01
	__StripeWidth = 0.03

	! ------------------------------------------------------------------------------
	! Detailed 3D representation
	! ------------------------------------------------------------------------------

	if gs_detlevel_3D_m = 2 then
		prism_ 6,_RodLength,
				_RodWidth,		0, 			15+64,
				_RodWidth+1,	0, 			800,
				_RodWidth,		_RodDepth,	1000+15+64,
				0,				_RodDepth, 	15+64,
				-1,				_RodDepth,	800,
				0,				0,			15+1000+64
	
		if _bStrap then					! Safety strap
		
			add -__StripeThick/2, -__StripeThick/2, _RodLength/2-__StripeWidth/2
			material gs_strap_mat
			prism_ 12,__StripeWidth,
				_RodWidth+__StripeThick,				0, 								15+64,
				_RodWidth+__StripeThick+1,				0, 								800,
				_RodWidth+__StripeThick,				_RodDepth+__StripeThick,		1000+15+64,
				__StripeThick,							_RodDepth+__StripeThick, 		15,
				-sin(frameAngle)*_legHeight+gs_seat_height+_RodDepth+_seatThick,0, 			15,
				0,										0,								-1,
				_RodWidth+__StripeThick/2,				__StripeThick/2, 				15+64,
				_RodWidth+__StripeThick/2+1,			__StripeThick/2, 				800,
				_RodWidth+__StripeThick/2,				_RodDepth+__StripeThick/2,		1000+15+64,
				__StripeThick+__StripeThick/2,			_RodDepth+__StripeThick/2, 		15,
				-sin(frameAngle)*_legHeight+gs_seat_height+_RodDepth+_seatThick+__StripeThick*3,+__StripeThick/4, 15,
				+__StripeThick/2,						+__StripeThick/2,				-1

			del 1
	
			material gs_frame_mat
		endif
	else
	! ------------------------------------------------------------------------------
	! Simple 3D representation
	! ------------------------------------------------------------------------------
		prism_ 4,_RodLength, 
				_RodWidth,		0, 			15,
				_RodWidth,		_RodDepth,	15,
				0,				_RodDepth,	15,
				0,				0,			15
	endif

return


! ==============================================================================
! Texture Subroutines
! ==============================================================================
"Horizontal Texture":
	roty 90
	gosub "Texture Coordinates"
	del 1
return


"Vertical Texture":
	gosub "Texture Coordinates"
return

"Slanting Texture":
	rotz 90-frameAngle
	gosub "Texture Coordinates"
	del 1
return

"Texture Coordinates":
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4

	base

	body -1
return

