
HIDEPARAMETER ALL "A", "B", "zzyzx"

call "FM_types" parameters all

parameters ac_bottomlevel = 0
parameters ac_toplevel = ZZYZX

! ------------------------------------------------------------------------------
! Backrest style
! ------------------------------------------------------------------------------
values "stBackrest" stBackrestValues
values "iBackrest" 1, 2

if GLOB_MODPAR_NAME = "stBackrest" then
	iBackrest = BACKREST_TRAPEZOID
	if stBackrest = stBackrestValues[2] then iBackrest = BACKREST_CURVY
	parameters iBackrest = iBackrest
else
	stBackrest = stBackrestValues[1]
	if iBackrest = BACKREST_CURVY then stBackrest = stBackrestValues[2]
	parameters stBackrest = stBackrest
endif

if GLOB_MODPAR_NAME = "stBackrest" or GLOB_MODPAR_NAME = "iBackrest" then
	if not(A > 2*(ZZYZX/tan(frameAngle)+2*_backRad)) then
		A = 2*(ZZYZX/tan(frameAngle)+2*_backRad)
		parameters A = A
	endif
endif

! ------------------------------------------------------------------------------
! restrictions for 3D geometry
! ------------------------------------------------------------------------------
values 	"frameAngle" 		range ( max( 50,atn(ZZYZX/((A- (3+2)*_backRad)/2)) ),max(90, atn(ZZYZX/A/2)+ atn(_backWidthTop/2/A/2) ))
values 	"gs_seat_height" 	range [0.2,_legHeightY-_seatFrameDepth/2-_legFrameDepth)
values 	"ZZYZX"				range (2*_legFrameDepth+_seatFrameDepth+_backRad*2+0.1, 2)
values 	"A"					range (0.36,)
values 	"B"					range (0.2,)
values 	"legAngle"			range ( max ( 20,ATN(_legHeight/(B/2-_legFrameDepthX)) ), 89)
values  "MSFront"			range [0,)
values  "MSSide"			range [0,)
values  "MSRear"			range [0,)


if GLOB_MODPAR_NAME = "A" or GLOB_MODPAR_NAME = "B" then
	if _legWidthTop < 2*_legFrameDepthX then _legWidthTop = 2*_legFrameDepthX
	if not(A > 2*(ZZYZX/tan(frameAngle)+_backRad)) then 
		A = 2*(ZZYZX/tan(frameAngle)+_backRad)
		parameters A = A
	endif
endif
	

! ------------------------------------------------------------------------------
! 2D Symbol
! ------------------------------------------------------------------------------
call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

if gs_detlevel_2D_m <> 4 then	! not symbolic
	lock			"gs_SymbolType", "gs_SymbolType_m"
	hideparameter 	"gs_SymbolType", "gs_SymbolType_m"
endif

call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

IF gs_cont_pen=0 then parameters gs_cont_pen=4
IF gs_fill_pen=0 then parameters gs_fill_pen=19


! ------------------------------------------------------------------------------
! 3D Detail level
! ------------------------------------------------------------------------------

values "gs_resol" range [3,)
if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

values "gs_detlevel_3D" stDetlevel3D           ! Off(0), Simple(1), Detailed(2)
values "gs_detlevel_3D_m" 2, 1, 0
if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	if gs_detlevel_3D = stDetlevel3D[2] then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = stDetlevel3D[3] then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetlevel3D[1]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = stDetlevel3D[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetlevel3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif



! ------------------------------------------------------------------------------
! Minimal Space
! ------------------------------------------------------------------------------

values "MSFront"	range [0,)
values "MSSide"		range [0,)
values "MSRear"		range [0,)

