
HIDEPARAMETER ALL "A", "B", "zzyzx"

call "FM_types" parameters all

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0
VALUES "gs_resol" RANGE [3,)

call "2dDetailLevel" parameters gs_detlevel_2D = gs_detlevel_2D,
								gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters gs_detlevel_2D_m

if gs_detlevel_2D_m <> 4 then
	HIDEPARAMETER "gs_SymbolType"
	lock "gs_SymbolType", "gs_SymbolType_m"
endif

call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

if GLOB_MODPAR_NAME = "temp_AA" then
	parameters A = temp_AA * 2
	parameters B = temp_AA * 2
	parameters temp_BB = temp_AA
else
	if GLOB_MODPAR_NAME = "temp_BB" then
		parameters A = temp_BB * 2
		parameters B = temp_BB * 2
		parameters temp_AA = temp_BB
	else
		if GLOB_MODPAR_NAME = "B" then
			parameters A = B
			parameters temp_AA = B / 2
			parameters temp_BB = B / 2
		else
			parameters B = A
			parameters temp_AA = A / 2
			parameters temp_BB = A / 2
		endif
	endif
endif

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19

IF gs_seat_height<0.06 THEN PARAMETERS gs_seat_height=0.06
IF gs_seat_height>ZZYZX-0.225 THEN PARAMETERS gs_seat_height=ZZYZX-0.225

IF zzyzx<gs_seat_height+0.225 THEN PARAMETERS zzyzx=gs_seat_height+0.225

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" THEN
	IF gs_detlevel_3D = `Откл.` THEN PARAMETERS gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN PARAMETERS gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Детальный` THEN PARAMETERS gs_detlevel_3D_m = 2
ELSE
	IF gs_detlevel_3D_m = 0 THEN PARAMETERS gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN PARAMETERS gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN PARAMETERS gs_detlevel_3D = `Детальный`
ENDIF

if gs_detlevel_3D_m = 1 then
	lock "gs_resol", "hole"
	hideparameter "gs_resol", "hole"
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow", "hole"
	hideparameter "gs_resol", "gs_shadow", "hole"
endif

! --- Minimal Space ---

VALUES "MSFront" RANGE [0,)
VALUES "MSRear" RANGE [0,)
VALUES "MSSide" RANGE [0,)
