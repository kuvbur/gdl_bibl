
! Design Chair 08.gsm

if gs_detlevel_3D_m = 0 then end
if not (gs_shadow) then shadow off
hotspotoffset = (gs_detlevel_3D_m = 1) * 0.02   ! hotspot áthelyezése simple és detailed mód között
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m = 1

pen gs_cont_pen
resol gs_resol

add a/2,b/2,0


!--- Hotspots ------------------------------------------------------------------

unID=1
for i = 1 to 2
	hotspot   a/2, -b/2, 0,		unID	: unID=unID+1
	hotspot   a/2,  b/2, 0,		unID	: unID=unID+1

	hotspot  a/2,  b/2, 0,		unID, zzyzx, 1+128	: unID=unID+1
	hotspot  a/2,  b/2, -1,		unID, zzyzx, 3		: unID=unID+1
	hotspot  a/2,  b/2, zzyzx,	unID, zzyzx, 2		: unID=unID+1

	if gs_full_edit then
		hotspot a/2-0.075, -b/2 + hotspotoffset, 0,		unID, gs_seat_height, 1+128			: unID=unID+1
		hotspot a/2-0.075, -b/2 + hotspotoffset, -1,	unID, gs_seat_height, 3				: unID=unID+1
		hotspot a/2-0.075, -b/2 + hotspotoffset, gs_seat_height, unID, gs_seat_height, 2	: unID=unID+1
	endif
	mulx -1
next i
del 2

material gs_frame_mat


!--- Sides ---------------------------------------------------------------------

for i = 1 to 2
	addz gs_seat_height + 0.145
	prism_ 4, 0.02,
			-a/2,			-b/2,			15,
			-a/2,			 b/2 - 0.02,	15,
			-a/2 + 0.07,	 b/2 - 0.02,	15,
			-a/2 + 0.07,	-b/2,			15
	del 1

	if gs_detlevel_3D_m = 2 then
		addx -a/2 + 0.07
		roty -90
		prism_ 8, 0.02,
				0,							-b/2,			15,
				0,							-0.04,			15,
				0.06,						-0.04,			15,
				0.06,						 0.02,			15,
				0,							 0.02,			15,
				0,							 b/2 - 0.02,	15,
				gs_seat_height + 0.145,		 b/2 - 0.02,	15,
				gs_seat_height + 0.145,		-b/2,			15

		del 2
	else
		prism_ 4, gs_seat_height + 0.145,
				-a/2 + 0.07,	-b/2,			15,
				-a/2 + 0.07,	 b/2 - 0.02,	15,
				-a/2 + 0.05,	 b/2 - 0.02,	15,
				-a/2 + 0.05,	-b/2,			15
	endif

	mulx -1
next i
del 2

if gs_detlevel_3D_m = 2 then
	prism_ 4, 0.02,
		-a/2 + 0.05,	-0.04,		15,
		-a/2 + 0.05,	 0.02,		15,
		 a/2 - 0.05,	 0.02,		15,
		 a/2 - 0.05,	-0.04,		15
endif


!--- Seating -------------------------------------------------------------------

addz gs_seat_height - 0.05
prism_ 4, 0.02 + (gs_detlevel_3D_m = 1) * 0.03,
		-a/2 + 0.07,	-b/2 + 0.02,	15,
		 a/2 - 0.07,	-b/2 + 0.02,	15,
		 a/2 - 0.07,	 b/2 - 0.02,	15,
		-a/2 + 0.07,	 b/2 - 0.02,	15
del 1


!--- Backrest ------------------------------------------------------------------

if gs_detlevel_3D_m = 2 then
	addy b/2
	rotx 90
	prism_ 8, 0.02,
		-a/2,	gs_seat_height/2,	15,
		-0.015, gs_seat_height/2,	15,
		-0.015, gs_seat_height*7/6,	15,
		 0.015, gs_seat_height*7/6,	15,
		 0.015, gs_seat_height/2,	15,
		 a/2,	gs_seat_height/2,	15,
		 a/2,	zzyzx,				15,
		-a/2,	zzyzx,				15
	del 2
else
	addz gs_seat_height/2
	prism_ 4, zzyzx - gs_seat_height/2,
		-a/2,	b/2 - 0.02,		15,
		 a/2,	b/2 - 0.02,		15,
		 a/2,	b/2,			15,
		-a/2,	b/2,			15
endif


!--- Cushion -------------------------------------------------------------------

if gs_detlevel_3D_m = 2 then
	material gs_seat_mat
	addz gs_seat_height - 0.03
	prism_ 4, 0.03,
		-a/2 + 0.07,	-b/2,			15,
		 a/2 - 0.07,	-b/2,			15,
		 a/2 - 0.07,	 b/2 - 0.02,	15,
		-a/2 + 0.07,	 b/2 - 0.02,	15
	del 1
	add -a/2 + 0.075, b/2 - 0.06, zzyzx - 0.1
	roty 90
	cylind a - 0.15, 0.04
	del 2
endif

del 1
end
