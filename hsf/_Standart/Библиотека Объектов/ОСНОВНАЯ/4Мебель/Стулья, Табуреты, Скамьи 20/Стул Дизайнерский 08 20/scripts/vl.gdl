
HIDEPARAMETER ALL "A", "B", "zzyzx"

call "FM_types" parameters all

values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	if gs_detlevel_3D = `Детальный` then gs_detlevel_3D_m = 2
	if gs_detlevel_3D = `Простой` then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Откл.` then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = `Детальный`
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

if gs_detlevel_2D_m <> 4 then
	HIDEPARAMETER "gs_SymbolType"
	lock "gs_SymbolType", "gs_SymbolType_m"
endif

call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

if gs_cont_pen=0 then parameters gs_cont_pen=4
if gs_fill_pen=0 then parameters gs_fill_pen=19

values "gs_resol" range [3, )
values "gs_seat_height" range [a/15+0.03, zzyzx-0.165]
values "zzyzx" range [gs_seat_height+0.165, )

if gs_detlevel_3D_m = 1 then
	hideparameter "gs_resol"
	lock "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	hideparameter "gs_resol", "gs_shadow", "gs_full_edit"
	lock "gs_resol", "gs_shadow", "gs_full_edit"
endif

!--- Minimal Space -------------------------------------------------------------

values "MSFront" range [0,)
values "MSSide" range [0,)
values "MSRear" range [0,)

