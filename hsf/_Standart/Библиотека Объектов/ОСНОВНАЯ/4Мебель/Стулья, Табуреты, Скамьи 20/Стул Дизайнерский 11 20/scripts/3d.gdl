

IF gs_detlevel_3D_m=0 THEN END
IF gs_shadow=0 THEN SHADOW OFF

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1

pen gs_cont_pen

add a/2,b/2,0
unID=1

hotspot -a/2, -b/2, 0, unID: unID=unID+1
hotspot -a/2,  b/2, 0, unID: unID=unID+1
hotspot a/2, -b/2, 0, unID: unID=unID+1
hotspot a/2,  b/2, 0, unID: unID=unID+1

hotspot 0, b/2, 0, unID, zzyzx, 1+128: unID=unID+1
hotspot 0, b/2, -1, unID, zzyzx, 3: unID=unID+1
hotspot 0, b/2, zzyzx, unID, zzyzx, 2: unID=unID+1

hotspot 0, -b/2, 0, unID, gs_seat_height, 1+128: unID=unID+1
hotspot 0, -b/2, -1, unID, gs_seat_height, 3: unID=unID+1
hotspot 0, -b/2, gs_seat_height, unID, gs_seat_height, 2: unID=unID+1

del 1


addx a/2
addz gs_seat_height
call "DesignerChair11_m" parameters A 					= A,
									B 					= B,
									ZZYZX 				= 0.41,
									gs_detlevel_3D_m	= gs_detlevel_3D_m,
									mat 				= gs_seat_mat
del 1


material gs_leg_mat
addy 0.015
xx = 0.45*(A/0.52)
yy = 0.50*(B/0.60)
if gs_detlevel_3D_m = 2 then
	add -xx/2, 0, 0
	cylind 0.005, 0.008
	del 1
	add xx/2, 0, 0
	cylind 0.005, 0.008
	del 1
	add -xx/2, yy, 0
	cylind 0.005, 0.008
	del 1
	add xx/2, yy, 0
	cylind 0.005, 0.008
	del 1
	height = gs_seat_height-0.012-0.006-0.005
	addz 0.005
else
	height = gs_seat_height-0.012-0.006
endif


angV = atn(0.177/(gs_seat_height-0.012-0.006))
dist = SQR((xx)^2+(yy)^2)
rr = 0.04
for j = -1 to 1 step 2
	mulx j
	addx -xx/2
	rotz -90+atn(yy/xx)
	gosub 100
	del 3
next j
if gs_detlevel_3D_m = 2 then del 1

dist = SQR((xx)^2+(yy)^2)
for j = -1 to 1 step 2
	mulx j
	addx -xx/2
	rotz -90+atn(yy/xx)
	cutplane 90-angV
	addy dist
	cutplane -90+angV
	del 1

	addz 0.09
	rotx -90
	cylind dist, 0.005
	del 2

	del 3
next j

cutend
cutend
cutend
cutend

del 1
del 1

end


100:
	angH = 0
	angV2 = angV
	rr2 = rr
	hy = height*tan(angV)
	hz = height
	dd = (-hz+((dist-hy)/tan(angV2)))/(cos(angH)/tan(angV2)-sin(angH))
	hy2 = hy + dd*cos(angH)
	hz2 = hz - dd*sin(angH)

	put 0, -0.1*sin(angV), -0.1*cos(angV), 0
	put 0, -0.05*sin(angV), -0.05*cos(angV), 0
	put 0, 0, 0, 0
	if gs_detlevel_3D_m = 1 then
		put 0, hy, hz, 0
		put 0, hy2, hz2, 0
	else
		ang = 90+angH-angV
		num = int(ang/12)
		alpha_i = ang/num
		dist2 = rr*tan(ang/2)
		ky = -dist2*sin(angV)+rr*cos(angV)
		kz = -dist2*cos(angV)-rr*sin(angV)
		alpha = angV
		for i = 1 to num+1
			put 0, hy+ky-rr*cos(alpha), hz+kz+rr*sin(alpha), 0
			alpha = alpha + alpha_i
		next i

		ang = 90-angH-angV2
		num = int(ang/12)
		alpha_i = ang/num
		dist2 = rr2*tan(ang/2)
		ky = dist2*sin(angV2)-rr2*cos(angV2)
		kz = -dist2*cos(angV2)-rr2*sin(angV2)
		alpha = 90-angH
		for i = 1 to num+1
			put 0, hy2+ky+rr2*cos(alpha), hz2+kz+rr2*sin(alpha), 0
			alpha = alpha - alpha_i
		next i
	endif
	put 0, dist, 0, 0
	put 0, dist+0.05*sin(angV2), -0.05*cos(angV2), 0
	put 0, dist+0.10*sin(angV2), -0.10*cos(angV2), 0

	cutplane 180

	mm = 16+32
	tube 2, NSP/4, 1+2+mm,
		0, 0, 901,
		0.006, 360, 4001,

		get(NSP)

	cutend
return
