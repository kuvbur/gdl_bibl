
hideparameter all "A", "B", "zzyzx"

call "FM_types" parameters all

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0
VALUES "gs_resol" RANGE [3,)

call "2dDetailLevel" parameters gs_detlevel_2D = gs_detlevel_2D,
								gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters gs_detlevel_2D_m

if gs_detlevel_2D_m <> 4 then
	HIDEPARAMETER "gs_SymbolType"
	lock "gs_SymbolType", "gs_SymbolType_m"
endif

call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" THEN
	IF gs_detlevel_3D = `Откл.` THEN PARAMETERS gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN PARAMETERS gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Детальный` THEN PARAMETERS gs_detlevel_3D_m = 2
ELSE
	IF gs_detlevel_3D_m = 0 THEN PARAMETERS gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN PARAMETERS gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN PARAMETERS gs_detlevel_3D = `Детальный`
ENDIF

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

IF gs_leg_type_m = 3 OR gs_leg_type_m = 4 THEN
	VALUES "gs_seat_height" RANGE[0.42,]
	VALUES "zzyzx" RANGE[0.77,]
ELSE
	VALUES "gs_seat_height" RANGE[0.20,]
	VALUES "zzyzx" RANGE[0.55,]
ENDIF

IF GLOB_MODPAR_NAME = "gs_seat_height" THEN
	IF gs_leg_type_m = 3 OR gs_leg_type_m = 4 THEN
		IF gs_seat_height < 0.42 THEN
			gs_seat_height = 0.42
			PARAMETERS gs_seat_height = gs_seat_height
		ENDIF
	ELSE
		IF gs_seat_height < 0.20 THEN
			gs_seat_height = 0.20
			PARAMETERS gs_seat_height = gs_seat_height
		ENDIF
	ENDIF
	zzyzx = gs_seat_height+0.35
	PARAMETERS zzyzx = zzyzx
ELSE
	gs_seat_height = zzyzx - 0.35
	PARAMETERS gs_seat_height = gs_seat_height
ENDIF

VALUES "gs_leg_type" stLegType
VALUES "gs_leg_type_m" 1, 2, 3, 4, 5
IF GLOB_MODPAR_NAME = "gs_leg_type" THEN
	FOR i = 1 TO 5
		IF gs_leg_type = stLegType[i] THEN
			gs_leg_type_m = i
			PARAMETERS gs_leg_type_m = gs_leg_type_m
			i = 5
		ENDIF
	NEXT i
ELSE
	FOR i = 1 TO 5
		IF gs_leg_type_m = i THEN
			gs_leg_type = stLegType[i]
			PARAMETERS gs_leg_type = gs_leg_type
			i = 5
		ENDIF
	NEXT i
ENDIF

IF NOT(bUpholstered) THEN LOCK "gs_upholstery_mat"

IF gs_leg_type_m <> 4 THEN LOCK "gs_roller_mat"

!!! ******************************** Minimal Space

VALUES "MSFront" RANGE [0,)
VALUES "MSSide" RANGE [0,)
VALUES "MSRear" RANGE [0,)
