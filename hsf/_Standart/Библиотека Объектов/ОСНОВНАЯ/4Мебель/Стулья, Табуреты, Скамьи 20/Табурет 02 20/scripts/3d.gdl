
if gs_detlevel_3d_m = 0 then end
if not (gs_shadow) then shadow off

if gs_detlevel_3D_m = 1 then
	resol 3
else
	resol gs_resol
endif


pen gs_cont_pen
material gs_seat_mat
boardThk = 0.02


unID=1

hotspot a/2,0, 0, unID: unID=unID+1
hotspot a/2,b/2, 0, unID: unID=unID+1

hotspot a/2,0, ZZYZX, unID: unID=unID+1
hotspot a/2,b/2, ZZYZX, unID: unID=unID+1


call "Resize_A_B_ZZYZX" parameters all

if gs_detlevel_3D_m  = 1 then
	block boardThk, b, zzyzx - boardThk

	roty 90
	gosub 1000		! textures
	del 1

	addx a - boardThk
	block boardThk, b, zzyzx - boardThk

	roty 90
	gosub 1000		! textures
	del 1

	del 1

	addz zzyzx - boardThk
	block a, b, boardThk

	gosub 1000		! textures

	del 1
endif


if gs_detlevel_3D_m = 2 then
	addz zzyzx - boardThk
	prism_ 5, boardThk,
		0, 0, 15,
		a, 0, 15,
		a, b, 15,
		0, b, 15,
		0, 0, -1

	gosub 1000		! textures

	del 1


	rotz 90
	rotx 90

	for i = 1 to 2
		prism_ 11, boardThk,
			0, 0, 15,
			b/4, 0, 15,
			b/4 + b/20, zzyzx/45, 15,
			3 * b/4 - b/20, zzyzx/45, 15,
			3 * b/4, 0, 15,
			b, 0, 15,
			b, zzyzx - boardThk, 15,
			0, zzyzx - boardThk, 15,
			0, 0, -1,
			b/2, zzyzx/5, 915,
			0.01, 360, 4015

			rotz 90
			gosub 1000		! textures
			del 1

		addz a - boardThk
	next i
	del 2

	add b/2, zzyzx/5, boardThk/4
	cylind a - boardThk/2, 0.01

	roty 90
	gosub 1000		! textures
	del 2

	del 2
endif


end

! --- Texture directions ---

1000:


vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return





