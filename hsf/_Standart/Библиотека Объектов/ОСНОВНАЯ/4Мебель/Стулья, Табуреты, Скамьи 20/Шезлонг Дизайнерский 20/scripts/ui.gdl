

UI_dialog `Параметры Стула`

ui_current_page gs_ui_current_page

dim tabTitles[5]
	tabTitles[1] = `Стиль и Размеры`
	tabTitles[2] = `2D-отображение`
	tabTitles[3] = `3D-отображение и Покрытия`
	tabTitles[4] = `Минимальное Пространство`
	tabTitles[5] = `Характеристики`


dim tabIcons[5]
	tabIcons[1] = "uiTab_Dimension_flat(1)"
	tabIcons[2] = "uiTab_2DRepr_flat(1)"
	tabIcons[3] = "uiTab_3DRepr_flat(1)"
	tabIcons[4] = "uiTab_MinSpace_flat(1)"
	tabIcons[5] = "uiTab_Descriptions_flat(1)"

FILE_DEPENDENCE "uiTab_Dimension_flat(1)"
FILE_DEPENDENCE "uiTab_2DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_3DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_MinSpace_flat(1)"
FILE_DEPENDENCE "uiTab_Descriptions_flat(1)"

iPageIDRoot 			=-1
iPageIDStyle			= 1
iPageID2DRepresentation	= 2
iPageID3DRepresentation	= 3
iPageIDDimMinSpace		= 4
iPageIDDescriptions		= 5

pageStartY = 5

UI_PAGE iPageIDStyle, iPageIDRoot, tabTitles[1], tabIcons[1]
	gosub "Styles"

UI_PAGE iPageID2DRepresentation, iPageIDRoot, tabTitles[2], tabIcons[2]
	gosub "2DPage"

UI_PAGE iPageID3DRepresentation, iPageIDRoot, tabTitles[3], tabIcons[3]
	gosub "3DPage"

UI_PAGE iPageIDDimMinSpace, iPageIDRoot, tabTitles[4], tabIcons[4]
	gosub "minSpacePage"

UI_PAGE iPageIDDescriptions, iPageIDRoot, tabTitles[5], tabIcons[5]
	gosub "descriptionPage"

! ------------------------------------------------------------------------------
end
! ------------------------------------------------------------------------------


! ------------------------------------------------------------------------------
!Subroutines
! ------------------------------------------------------------------------------

! ------------------------------------------------------------------------------
"Styles":
! ------------------------------------------------------------------------------
	x1=0    					! start Position X Coordinate
	x2= 228
	y0=pageStartY+3   		! start Position Y Coordinate
	cell1_x=444					! width of 1.cell
	cell1_y=204					! height of 1.cell

	ui_infield{3} "bArmrest", x1, y0 - 4, 124, 19,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Подлокотники`, 0,
					"", `Подлокотники`, 1

	if gs_chairStyle_m = 1 | gs_chairStyle_m = 2 | gs_chairStyle_m = 5 then
		ui_infield{3} "gs_armrestType_m", 125, y0 - 4, 	90, 19,
					8, "", 0, 0,
					0, 0, 0, 0,
					"", starmresttypes[1], 1,
					"", starmresttypes[2], 2
	else
		ui_infield{3} "gs_armrestType_m", 125, y0 - 4, 	90, 19,
					8, "", 0, 0,
					0, 0, 0, 0,
					"", starmresttypes[1], 1,
					"", starmresttypes[2], 2,
					"", starmresttypes[3], 3
	endif

	ui_outfield `Высота Сидения`, x2, y0, 124, 15
	ui_infield "gs_seat_height", x2+125, y0 - 4, 	90, 19 
	y0 = y0 + 24

	ui_infield{3} "gs_backrest", x1, y0 - 4, 124, 19,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Спинка`, 0,
					"", `Спинка`, 1


	
	y0 = y0 + 20
	
	ui_infield{3} "gs_chairStyle_m", x1, y0, cell1_x, cell1_y+10,
		1, 1, 8, 2,				! ui_loungeChairs.png
		105, 105, 90, 90,
		1, stChairStyles[1], 1,
		2, stChairStyles[2], 2,
		3, stChairStyles[3], 3,
		4, stChairStyles[4], 4,
		5, stChairStyles[5], 5
return


! ------------------------------------------------------------------------------
"2DPage":
! ------------------------------------------------------------------------------
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 14,
													gs_SymbolType_m		= gs_SymbolType_m,
													stSymbolTypes		= stSymbolTypes,
													gs_detlevel_2D_m	= gs_detlevel_2D_m,
													gs_2DSeat			= 0,
													gs_cont_pen			= gs_cont_pen,
													gs_fill_type		= gs_fill_type,
													gs_fill_pen			= gs_fill_pen,
													gs_back_pen			= gs_back_pen,
													iFillListID			= iPageID2DRepresentation

return

! ------------------------------------------------------------------------------
"3DPage":
! ------------------------------------------------------------------------------
	_idx = 1
	dim surfacesArray[]    
		surfacesArray[_idx]	= "gs_frame_mat" : _idx = _idx +1
		surfacesArray[_idx]	= "gs_seat_mat" : _idx = _idx +1
	if gs_chairStyle_m = 5 then
		surfacesArray[_idx]	= "gs_seat_mat2" : _idx = _idx +1
	endif
	if gs_backrest and gs_chairStyle_m <> 4 then
		surfacesArray[_idx]	= "gs_back_mat" : _idx = _idx + 1
	endif
	if gs_backrest and gs_chairStyle_m = 5 then
		surfacesArray[_idx]	= "gs_back_mat2" : _idx = _idx + 1
	endif
	if bArmrest and (gs_chairStyle_m = 1 or gs_chairStyle_m = 5) then
		surfacesArray[_idx]	= "gs_arm_mat"
	endif

	_idx = 1
	dim childFlagArray[]
		childFlagArray[_idx]	= 0 : _idx = _idx +1
		childFlagArray[_idx]	= 0 : _idx = _idx +1
	if gs_chairStyle_m = 5 then
		childFlagArray[_idx]	= 0 : _idx = _idx +1
	endif
	if gs_chairStyle_m <> 4 then
		childFlagArray[_idx]	= 0 : _idx = _idx +1
	endif
	if gs_chairStyle_m = 5 then
		childFlagArray[_idx]	= 0 : _idx = _idx +1
	endif
	if bArmrest | not(gs_chairStyle_m <> 1 & gs_chairStyle_m <> 5) then
		childFlagArray[_idx]	= 0
	endif


	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= iPageID3DRepresentation

return


! ------------------------------------------------------------------------------
"minSpacePage":
! ------------------------------------------------------------------------------

	x1 = 0
	x2 = 228
	y0 = pageStartY + 4

	pageStartY = pageStartY + 29

	call "ui_chairs_minimalspace" parameters 	MSType 	= 1, 					! MINSPACE_SIMPCHAIR
										MSSide	= MSSide,
										pageStartY = pageStartY

return


! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = iPageIDDescriptions,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
