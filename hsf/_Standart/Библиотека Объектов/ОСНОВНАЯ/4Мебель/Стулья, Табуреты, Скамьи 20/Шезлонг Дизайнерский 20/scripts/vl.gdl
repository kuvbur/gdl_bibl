
HIDEPARAMETER ALL "A", "B", "zzyzx"

call "FM_types" parameters all

values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
values "gs_detlevel_3D_m" 0, 1, 2
values "gs_chairStyle" stChairStyles
values "gs_chairStyle_m" 1, 2, 3, 4, 5

if GLOB_MODPAR_NAME = "gs_chairStyle" then
	gs_chairStyle_m = 1
	for i = 2 to vardim1(stChairStyles)
		if gs_chairStyle = stChairStyles[i] then gs_chairStyle_m = i
	next i
	parameters gs_chairStyle_m = gs_chairStyle_m
else
	gs_chairStyle = stChairStyles[max(1, min(gs_chairStyle_m, vardim1(stChairStyles)))]
	parameters gs_chairStyle = gs_chairStyle
endif

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16+32

if gs_detlevel_2D_m <> 4 then
	HIDEPARAMETER "gs_SymbolType"
	lock "gs_SymbolType", "gs_SymbolType_m"
endif

VALUES "gs_resol" RANGE [3,)

if gs_chairStyle_m = 1 OR gs_chairStyle_m = 2 OR gs_chairStyle_m = 5 then
	values{2} "gs_armrestType_m"	ARMREST_TYPE_1,	starmresttypes[1],
									ARMREST_TYPE_2,	starmresttypes[2]

else
	values{2} "gs_armrestType_m"	ARMREST_TYPE_1,	starmresttypes[1],
									ARMREST_TYPE_2,	starmresttypes[2],
									ARMREST_TYPE_3,	starmresttypes[3]
endif

if not(bArmrest) then
	HIDEPARAMETER	"gs_armrestType_m"
	LOCK			"gs_armrestType_m"
endif

if not(bArmrest) OR (gs_chairStyle_m <> 1 AND gs_chairStyle_m <> 5) then
	HIDEPARAMETER	"gs_arm_mat"
	LOCK			"gs_arm_mat"
endif

IF gs_chairStyle_m <> 5 THEN
	hideparameter "gs_seat_mat2", "gs_back_mat2"
	lock "gs_seat_mat2", "gs_back_mat2"
ENDIF
IF gs_chairStyle_m = 4 THEN
	hideparameter "gs_back_mat"
	lock "gs_back_mat"
ENDIF

call "fa_ChairSymbol" parameters all stSymbolTypes = stSymbolTypes

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19

IF a<0.1 THEN PARAMETERS a=0.1
IF b<0.1 THEN PARAMETERS b=0.1

IF gs_seat_height<a/22 THEN PARAMETERS gs_seat_height=a/22
IF gs_seat_height>ZZYZX-0.2 THEN PARAMETERS gs_seat_height=ZZYZX-0.2

IF zzyzx<gs_seat_height+0.2 THEN PARAMETERS zzyzx=gs_seat_height+0.2

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" THEN
	IF gs_detlevel_3D = `Откл.` THEN PARAMETERS gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN PARAMETERS gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Детальный` THEN PARAMETERS gs_detlevel_3D_m = 2
ELSE
	IF gs_detlevel_3D_m = 0 THEN PARAMETERS gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN PARAMETERS gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN PARAMETERS gs_detlevel_3D = `Детальный`
ENDIF

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif


!!! ******************************** Minimal Space

IF gs_backrest = 0 THEN HIDEPARAMETER "gs_back_mat", "gs_back_mat2"
VALUES "MSFront" RANGE [0,)
VALUES "MSSide" RANGE [0,)
VALUES "MSRear" RANGE [0,)

if gs_chairStyle_m = 3 or gs_chairStyle_m = 4 then VALUES "zzyzx" RANGE (gs_seat_height + 0.22, gs_seat_height + 3 * b)
if gs_chairStyle_m = 5 then VALUES "zzyzx" RANGE (gs_seat_height + 0.22, gs_seat_height + 2 * b)


