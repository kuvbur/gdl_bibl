
! Cupboard

![#COMPATIBILITY] - START

if iFunctions = -1 then
	gosub "CupboardCompatibility"
endif

![#COMPATIBILITY] - END

IF gs_detlevel_3D_m=0 THEN END
IF not(gs_shadow) THEN SHADOW OFF

!* 3D Hotspot
unID=1

!* Total Height

hotspot	0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	0, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	A, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	A, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	0, B, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	0, B, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	A, B, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	A, B, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

!* Base Height

hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), cooh, 0, unID, leg_h, 1 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), cooh, -1, unID, leg_h, 3+128 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), cooh, leg_h, unID, leg_h, 2 : unID=unID+1

hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), cooh, 0, unID, leg_h, 1 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), cooh, -1, unID, leg_h, 3+128 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), cooh, leg_h, unID, leg_h, 2 : unID=unID+1

hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), B, 0, unID, leg_h, 1 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), B, -1, unID, leg_h, 3+128 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), B, leg_h, unID, leg_h, 2 : unID=unID+1

hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), B, 0, unID, leg_h, 1 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), B, -1, unID, leg_h, 3+128 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), B, leg_h, unID, leg_h, 2 : unID=unID+1

!!**************************************************

ADD a/2,b,0

PEN gs_cont_pen

MULX -1

thickness = 0.02
lffw=a-2*side_overhang-2*thickness/2      	!!! Leafs full widht
lffh=zzyzx-cot-leg_h-2*thickness/2     	!!! Leafs full height


!* Detailed 3D

CALL "fa_cabinet_credenza" PARAMETERS ALL b=b-fr_th1

! ----- Cornices -----

if bCornice then

	MULX -1
	ADDZ zzyzx-cot

	if cooh <= fr_th1 then
		corniceDepth = b-fr_th1 + cooh
	else
		corniceDepth = b
	endif

	CALL "fa_cornicetyp" PARAMETERS ALL b = corniceDepth

	DEL 1
	DEL 1

endif
!* Legs

if bBase then
	CALL "fa_cabinet_legtyp" PARAMETERS A = A,
										B = B-fr_th1,
										gs_detlevel_3D_m = gs_detlevel_3D_m,
										gs_leg_type_m = gs_leg_type_m,
										leg_h = leg_h,
										leg_w = leg_w,
										cooh = cooh,
										side_overhang = side_overhang,
										gs_resol = gs_resol,
										gs_leg_mat = gs_leg_mat,
										gs_cont_pen = gs_cont_pen,
										gs_lining_m = gs_lining_m
endif

!* Shelves and Hangers

corniceRight = 0
corniceLeft = 0
if gs_lining_m = LINING_SIDE_LEFT or gs_lining_m = LINING_SIDE_BOTH then corniceRight = 1
if gs_lining_m = LINING_SIDE_RIGHT or gs_lining_m = LINING_SIDE_BOTH then corniceLeft = 1
insideA = A - side_overhang*corniceRight - side_overhang*corniceLeft - 2*thickness
if iFunctions = COMBINED then
	insideTemp = insideA
	if dn >= 3 then
		insideA = ((insideA-thickness*(dn-1))/dn)*(dn-2)+thickness*(dn-3)
	else
		insideA = (insideA-thickness)/2
	endif
endif
if iFunctions = SHELVES or iFunctions = COMBINED then
	if bShelves then
		sfh=zzyzx-cot-leg_h-2*thickness      !!! Shelves full height
		bb=(sfh+thickness)/(iNumShelves+1)
		add -a/2+thickness+side_overhang*corniceLeft, -thickness,leg_h + bb
		if iFunctions = SHELVES then
			gosub "shelves"
		else
			addx insideTemp - insideA
			gosub "shelves"
			del 1
		endif
		del 1
	endif
endif

if iFunctions = COMBINED and dn < 3 then
	add -a/2+thickness+side_overhang*corniceLeft, -thickness,leg_h+thickness
	addx insideA
	material shmat
	PRISM_ 4, sfh,
			thickness,0,15,
			thickness,-b+fr_th1+cooh+thickness,15,
			0,-b+fr_th1+cooh+thickness,15,
			0,0,15
	del 2
endif

if iFunctions = HANGERS or iFunctions = COMBINED then
	material gs_hanger_mat
	ADD -a/2+thickness+side_overhang*corniceLeft, -b/2, (zzyzx - cot)/10*8.5
	if iFunctions = HANGERS then
		roty 90
		cylind insideA, thickness
		del 1
	else
		addx insideTemp - insideA - thickness
		roty 270
		cylind insideTemp - insideA -thickness, thickness
		del 2
	endif
	del 1
endif

!* Vertical panels

IF gs_lining_m = LINING_SIDE_NONE OR not(bCornice) then valt2=-a/2+thickness/2
IF gs_lining_m = LINING_SIDE_BOTH then valt2=-a/2+side_overhang+thickness/2
IF gs_lining_m = LINING_SIDE_LEFT then valt2=-a/2+thickness/2
IF gs_lining_m = LINING_SIDE_RIGHT then valt2=-a/2+side_overhang+thickness/2

vpn=CEIL(dn/2)-1

ADD valt2+(lffw2/dn)*2,-thickness,leg_h+thickness

FOR i=1 TO vpn
	MATERIAL gs_cabinet_mat
	PRISM_ 4, zzyzx-cot-leg_h-2*thickness,
			thickness/2,0,15,
			-thickness/2,0,15,
			-thickness/2,-b+fr_th1+cooh+thickness,15,
			thickness/2,-b+fr_th1+cooh+thickness,15
	ADDX (lffw2/dn)*2
NEXT i
DEL vpn
DEL 1

!* Leafs

ADD valt2,-b+fr_th1+cooh,leg_h+thickness/2
FOR i=1 TO dn
	call "fa_warddoortyp" parameters all bNotMacro = 0,
										gs_full_edit 		= 0,
										bAngleEdit 			= 0,
										bMirrored 			= 0,
										bFormMirror 		= 0,
										bSliding 			= 0,
										iDoorStyle			= iDoorStyle,
										iTempDoorStyle		= iDoorStyle,
										bBevel				= gs_bevel,
										doorThickness 		= fr_th1,
										doorFrameWidth		= doorframe_w,
										mullionWidth		= 0.016,
										nVerticalMullion	= vert_gnum,
										nHorizontalMullion	= hor_gnum,
										matDoorFrame		= doorframe_mat,
										matDoorGlass		= doorglas_mat,
										matDoorMirror		= doorglas_mat,
										matDoorDecor1		= doorframe_mat,
										matDoorDecor2		= doorframe_mat,
										doorWidth			= lffw2/dn,
										doorHeight			= lffh,
										doorCustomPanelName	= gs_cust_p,
										bKnob				= 0,
										doorAngle3D			= 0

	ADDX lffw2/dn
NEXT i
DEL dn
DEL 1

!* Knobs on leafs

if bKnob & _bDrawKnob then

	valt=-a/2+side_overhang+thickness/2
	if gs_lining_m = LINING_SIDE_LEFT then valt=-a/2+thickness/2
	if gs_lining_m = LINING_SIDE_RIGHT then valt=-a/2+side_overhang+thickness/2
	if gs_lining_m = LINING_SIDE_NONE then valt=-a/2+thickness/2

	add valt,-b+fr_th1+cooh,leg_h+thickness/2

	if gs_knob_p_m = 0 or	gs_knob_type_m = KNOB_STYLE_1 or \
							gs_knob_type_m = KNOB_STYLE_2 or \
							gs_knob_type_m = KNOB_STYLE_3 then
		temp = 0
	else
		temp = knob_size
	endif

	if INT(dn/2)<>dn/2 then
		add lffw2-lffw2/dn+knob_pl_x+temp, -fr_th1, knob_pl_y

		call "fa_knobtyp" parameters all

		add -temp, 0, -knob_pl_y
		hotspot	0, 0, 0, unID, knob_pl_y, 1+128 : unID=unID+1
		hotspot	0, 0, -1, unID, knob_pl_y, 3+128 : unID=unID+1
		hotspot	0, 0, knob_pl_y, unID, knob_pl_y, 2 : unID=unID+1
		del 1
		addx -knob_pl_x - temp
		hotspot	0, 0, 0, unID, knob_pl_x, 1+128 : unID=unID+1
		hotspot	-1, 0, 0, unID, knob_pl_x, 3+128 : unID=unID+1
		hotspot	knob_pl_x, 0, 0, unID, knob_pl_x, 2 : unID=unID+1
		del 1
		del 1
	endif

	h_knob=1-leg_h-thickness/2

	add lffw2/dn-(doorframe_w-fr_th1)/2*0,0,knob_pl_y
	for k=1 to INT(dn/2)
		for s=1 to 2
			add knob_pl_x + temp,-fr_th1,0

			call "fa_knobtyp" parameters all

			add -temp, 0, -knob_pl_y
			hotspot	0, 0, 0, unID, knob_pl_y, 1+128 : unID=unID+1
			hotspot	0, 0, -1, unID, knob_pl_y, 3+128 : unID=unID+1
			hotspot	0, 0, knob_pl_y, unID, knob_pl_y, 2 : unID=unID+1
			del 1
			addx -knob_pl_x - temp
			hotspot	0, 0, 0, unID, knob_pl_x, 1+128 : unID=unID+1
			hotspot	-1, 0, 0, unID, knob_pl_x, 3+128 : unID=unID+1
			hotspot	knob_pl_x, 0, 0, unID, knob_pl_x, 2 : unID=unID+1
			del 1

			del 1
			mulx -1
!			add -(doorframe_w-fr_th1)-knob_pl_x+0.05,fr_th1,0
		next s
		del 2
		add lffw2/dn*2,0,0
	next k
	del INT(dn)/2
	del 1

	if dn/2-int(dn/2)<0.001 then del 1

	if dn<>1 then del 1

endif

end


"shelves":
	for i=1 to iNumShelves
		material shmat
		prism_ 4, thickness,
				insideA,0,15,
				insideA,-b+fr_th1+cooh+thickness,15,
				0,-b+fr_th1+cooh+thickness,15,
				0,0,15
		addz bb
	next i
	del iNumShelves
return
