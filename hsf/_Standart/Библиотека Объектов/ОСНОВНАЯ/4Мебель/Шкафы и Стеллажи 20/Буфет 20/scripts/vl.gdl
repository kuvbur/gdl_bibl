
![#COMPATIBILITY] - START

if iFunctions = -1 then
	bDefaultElem = (GLOB_INTGUID = "" | GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")

	if bDefaultElem then
		gosub "CupboardCompatibility"
	else
		iFunctions = SHELVES
	endif
	parameters iFunctions = iFunctions
endif

![#COMPATIBILITY] - END

hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

! Custom Elements -------------------------------------

dim gs_pt[]
if iDoorStyle = DOOR_STYLE_CUST then
	call "Object_Panel_Collection" parameters returned_parameters gs_pt
	values "gs_cust_p" gs_pt
endif

! ----------------------------------------------

VALUES "gs_functions" stFunctions
VALUES "iFunctions" 1,2,3

if GLOB_MODPAR_NAME = "gs_functions" then
	iFunctions = SHELVES
	if gs_functions = stFunctions[2] then iFunctions = HANGERS
	if gs_functions = stFunctions[3] then iFunctions = COMBINED
	parameters iFunctions = iFunctions
else
	gs_functions = stFunctions[1]
	if iFunctions = HANGERS then gs_functions = stFunctions[2]
	if iFunctions = COMBINED then gs_functions = stFunctions[3]
	parameters gs_functions = gs_functions
endif

if iFunctions = HANGERS then
	lock "iNumShelves", "shmat", "bShelves"
	hideparameter "iNumShelves", "shmat", "bShelves"
endif
if not(bShelves) then
	lock "iNumShelves", "shmat"
endif

! Enable all Symbol Types
iSymbolStyleEnable = 2**1 + 2**2 + 2**3 + 2**4 + 2**5 + 2**6 + 2**7 + 2**8 + 2**9
call "cabSymbolType" parameters 	iSymbolStyle = iSymbolStyle,
									iSymbolStyleEnable = iSymbolStyleEnable

if iFunctions = SHELVES then
	lock "gs_hanger_mat"
	hideparameter "gs_hanger_mat"
endif

if 	iSymbolStyle <> 8 &\		! Lock Door Opening Angle if the Symbol has no opening doors
	iSymbolStyle <> 9 then

	lock "opa2"
	hideparameter "opa2"
endif

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=4
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=19

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" THEN
	IF gs_detlevel_3D = `Откл.` THEN PARAMETERS gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN PARAMETERS gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Детальный` THEN PARAMETERS gs_detlevel_3D_m = 2
ELSE
	IF gs_detlevel_3D_m = 0 THEN PARAMETERS gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN PARAMETERS gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN PARAMETERS gs_detlevel_3D = `Детальный`
ENDIF

IF gs_detlevel_3D_m = 1 THEN
	LOCK "gs_resol"
	HIDEPARAMETER "gs_resol"
ENDIF

IF gs_detlevel_3D_m = 0 THEN
	LOCK "gs_resol", "gs_shadow"
	HIDEPARAMETER "gs_resol", "gs_shadow"
ENDIF

if not(bCornice) then
	HIDEPARAMETER "gs_cornice_type_m", "gs_lining_m","cot","cooh","side_overhang","gs_cornice_mat"
	LOCK "gs_cornice_type_m", "gs_lining_m","cot","cooh","side_overhang","gs_cornice_mat"
endif

if not(bBase) then
	HIDEPARAMETER	"gs_leg_type_m", "leg_h","leg_w","gs_leg_mat"
	LOCK			"gs_leg_type_m", "leg_h","leg_w","gs_leg_mat"
ENDIF

values{2} "gs_knob_type_m"	KNOB_STYLE_1,	`Стиль 1`,
							KNOB_STYLE_2,	`Стиль 2`,
							KNOB_STYLE_3,	`Стиль 3`,
							KNOB_STYLE_4,	`Стиль 4`,
							KNOB_STYLE_5,	`Стиль 5`,
							KNOB_STYLE_6,	`Стиль 6`,
							KNOB_STYLE_7,	`Стиль 7`,
							KNOB_STYLE_8,	`Стиль 8`


if not(bKnob) then
	hideparameter	"gs_knob_type_m", "knob_pl_x", "knob_pl_y", "gs_knob_pos", "knob_size", "gs_knob_mat"
	lock			"gs_knob_type_m", "knob_pl_x", "knob_pl_y", "gs_knob_pos", "knob_size", "gs_knob_mat"
endif

dim st_knobplacing[2]
	st_knobplacing[1]=`Вертикальный`
	st_knobplacing[2]=`Горизонтальный`
values "gs_knob_p" st_knobplacing
if GLOB_MODPAR_NAME = "gs_knob_p" then
	for iKnob = 1 to vardim1(st_knobplacing)
		if gs_knob_p = st_knobplacing[iKnob] then
			gs_knob_p_m = iKnob - 1
		endif
	next iKnob
	parameters gs_knob_p_m = gs_knob_p_m
else
	gs_knob_p = st_knobplacing[gs_knob_p_m+1]
	parameters gs_knob_p = gs_knob_p
endif

if iDoorStyle <> DOOR_STYLE_CUST then
	hideparameter "gs_cust_p"
	lock "gs_cust_p"
endif

values "leg_h" range [0,)
values "leg_w" range (0,)
values "dn" range [1,)
values "doorframe_w" range [0.01,)
values "iNumShelves" range [1,)
values "cot" range (0,)
values "vert_gnum" range [0,)
values "hor_gnum" range [0,)
values "gs_resol" range [3,)
values "fr_th1" range [0.01,)
xKnobPosMod = 0
if gs_knob_type_m = KNOB_STYLE_4 & gs_knob_p_m = 1 then xKnobPosMod = knob_size * 128/100
if gs_knob_type_m > 4 & gs_knob_p_m = 1 then xKnobPosMod = knob_size
values "knob_pl_x" range [0.02, lffw2/dn - 0.02 - xKnobPosMod]
yKnobPosMod = 0
if gs_knob_type_m = KNOB_STYLE_4 & gs_knob_p_m = 0 then yKnobPosMod = knob_size * 128/100
if gs_knob_type_m > 4 & gs_knob_p_m = 0 then yKnobPosMod = knob_size
values "knob_pl_y" range [0.02 + yKnobPosMod, zzyzx-cot-leg_h-2*0.01 - 0.02]
values "knob_size" range [0.045, )

if bCornice then

	if gs_cornice_type_m = CORN_STYLE_1 then values "cooh" range [0,)
	if gs_cornice_type_m = CORN_STYLE_2 or gs_cornice_type_m = CORN_STYLE_3 then values "cooh" range [1/2*cot,)
	if gs_cornice_type_m = CORN_STYLE_4 then values "cooh" range [3/4*cot,)


	if gs_cornice_type_m = CORN_STYLE_1 then values "side_overhang" range [0,A/2 - 0.02)
	if gs_cornice_type_m = CORN_STYLE_2 or gs_cornice_type_m = CORN_STYLE_3 then values "side_overhang" range [1/2*cot,A/2 - 0.02)
	if gs_cornice_type_m = CORN_STYLE_4 then values "side_overhang" range [3/4*cot,A/2 - 0.02)

endif

if not(bCornice)  then
	LOCK "cooh","side_overhang","gs_lining_m"
	HIDEPARAMETER "cooh","side_overhang","gs_lining_m"
endif

if gs_lining_m = LINING_SIDE_NONE then				
	HIDEPARAMETER "side_overhang"
	LOCK "side_overhang"
endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** Parameters Values for Macro

VALUES "gs_detlevel_3D_m" 2,1,0

VALUES{2} "gs_lining_m"	LINING_SIDE_BOTH,	stLiningSide[1],
						LINING_SIDE_LEFT,	stLiningSide[2],
						LINING_SIDE_RIGHT,	stLiningSide[3],
						LINING_SIDE_NONE,	stLiningSide[4]

values{2} "gs_leg_type_m" 	LEG_STYLE_1, `Стиль 1`,
							LEG_STYLE_2, `Стиль 2`,
							LEG_STYLE_3, `Стиль 3`,
							LEG_STYLE_4, `Стиль 4`,
							LEG_STYLE_5, `Стиль 5`,
							LEG_STYLE_6, `Стиль 6`

values{2} "gs_cornice_type_m"	CORN_STYLE_1,		`Стиль 1`,
								CORN_STYLE_2,		`Стиль 2`,
								CORN_STYLE_3,		`Стиль 3`,
								CORN_STYLE_4,		`Стиль 4`

VALUES{2} "iDoorStyle"	DOOR_STYLE_1,		stDoorStyle[1],
						DOOR_STYLE_2,		stDoorStyle[2],
						DOOR_STYLE_3,		stDoorStyle[3],
						DOOR_STYLE_4,		stDoorStyle[4],
						DOOR_STYLE_5,		stDoorStyle[5],
						DOOR_STYLE_6,		stDoorStyle[6],
						DOOR_STYLE_7,		stDoorStyle[7],
						DOOR_STYLE_8,		stDoorStyle[8],
						DOOR_STYLE_9,		stDoorStyle[9],
						DOOR_STYLE_10,		stDoorStyle[10],
						DOOR_STYLE_CUST,	stDoorStyle[11]

if iDoorStyle = DOOR_STYLE_1 then
	lock "doorframe_w","hor_gnum","vert_gnum","doorglas_mat"
	hideparameter "doorframe_w","hor_gnum","vert_gnum","doorglas_mat"
endif
if iDoorStyle = DOOR_STYLE_2 or iDoorStyle = DOOR_STYLE_4 or iDoorStyle = DOOR_STYLE_7 or iDoorStyle = DOOR_STYLE_9 or iDoorStyle = DOOR_STYLE_CUST then
	lock "hor_gnum","vert_gnum","doorglas_mat"
	hideparameter "hor_gnum","vert_gnum","doorglas_mat"
endif
if iDoorStyle = DOOR_STYLE_6 then
	lock "hor_gnum","vert_gnum"
	hideparameter "hor_gnum","vert_gnum"
endif

if gs_knob_type_m=KNOB_STYLE_1 or gs_knob_type_m=KNOB_STYLE_2 or gs_knob_type_m=KNOB_STYLE_3 or not(bKnob) then
	lock			"gs_knob_p", "gs_knob_p_m", "knob_size"
	hideparameter	"gs_knob_p", "gs_knob_p_m", "knob_size"
endif
!!! ******************************** Minimal Space
if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif


