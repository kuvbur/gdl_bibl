
! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_Root 				= -1
_idxTab					= 1

TABID_StyleAndDim		= _idxTab  : _idxTab = _idxTab + 1
TABID_CorniceAndBase	= _idxTab  : _idxTab = _idxTab + 1
TABID_Door				= _idxTab  : _idxTab = _idxTab + 1
TABID_Knob				= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepr			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepr			= _idxTab  : _idxTab = _idxTab + 1
TABID_MinSpace			= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions		= _idxTab  : _idxTab = _idxTab + 1

pageStartY = 5

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Гардеробного Шкафа`,444,266
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDim,	TABID_Root,			`Стиль и Размеры`,	"uiTab_Dimension_flat(1)"
	gosub "StyleAndDimPage"

ui_page TABID_CorniceAndBase,	TABID_StyleAndDim,		`Карниз и Основание`,		"uiTab_Leg_flat(1)"
	gosub "CorniceAndBasePage"

ui_page TABID_Door,				TABID_StyleAndDim,		`Двери`,					"uiTab_WardrobeDoor_flat(1)"
	gosub "DoorPage"

ui_page TABID_Knob,				TABID_StyleAndDim,		`Ручка`,					"uiTab_Knob_flat(1)"
	gosub "KnobPage"

ui_page TABID_2DRepr,			TABID_Root,		`2D-отображение`,	"uiTab_2DRepr_flat(1)"
	gosub "2DReprPage"

ui_page TABID_3DRepr,			TABID_Root,		`3D-отображение и Покрытия`,	"uiTab_3DRepr_flat(1)"
	gosub "3DReprPage"

ui_page TABID_MinSpace,			TABID_Root,		`Минимальное Пространство`,		"uiTab_MinSpace_flat(1)"
	gosub "MinSpacePage"

ui_page TABID_Descriptions,		TABID_Root,		`Характеристики`,			"uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"


! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimPage":
! ------------------------------------------------------------------------------

	pageStartY	= 5
	_yCurr		= pageStartY
	_dy			= 24
	_infHeight 	= 19
	_outfHeight = 15
	_offsetY	= 4

	_infWidth	= 90
	_infWidthShort = 54
	widthInf2	= 37

	x1	= 0
	x4	= 210
	x2	= x4 - _infWidth
	x3	= x4 - 35 !UI function button

	x5	= 230
	x8	= 442
	x6	= x8 - _infWidth
	x7	= x8 - _infWidthShort

	y_separator	= 122

	x_pict		= 183
	y_pict		= 156

	! --- Left Side

	ui_outfield `Форма`,				x1, _yCurr,				x2-x1-2,	_outfHeight
	ui_infield{3} "iCabinetForm",		x2, _yCurr - _offsetY,	x4-x2,		43,
		2, 1,	!Wardrobe_StyleandDim_Shapes.png
		4, 2,
		77, 39, 77, 39,
		1, `Прямой`,	SHAPE_SQUARE,
		2, `L-угол`,	SHAPE_CORNER_L,
		3, `С-угол`,	SHAPE_CORNER_C	ui_tooltip `Форма Шкафа`
	_yCurr = _yCurr + 2 * _dy

	ui_infield{3} "bDoor", x1,	_yCurr - _offsetY,	x3-x1-2,	_infHeight,
		7, "",
		0, 0, 0, 0, 0, 0,
		"",`Двери`, 0,
		"",`Двери`, 1
	ui_pict_button ui_function, ``, "uiFunctionWardrobeDoor_flat(1)", x3, _yCurr - _offsetY - 1, widthInf2, 22, -TABID_Door ! Set GLOB_UI_BUTTON_ID
	_yCurr = _yCurr + _dy

	ui_infield{3} "bKnob", x1,	_yCurr - _offsetY,	x3-x1-2,	_infHeight,
		7, "",
		0, 0, 0, 0, 0, 0,
		"",`Ручка`, 0,
		"",`Ручка`, 1
	ui_pict_button ui_function, ``, "uiFunctionDrawerKnob_flat(1)", x3, _yCurr - _offsetY - 1, widthInf2, 22, -TABID_Knob ! Set GLOB_UI_BUTTON_ID
	_yCurr = _yCurr + _dy

	ui_outfield `Карниз и Основание`,							x1, _yCurr,				x3-x1-2,	_outfHeight
	ui_pict_button ui_function, ``, "uiFunctionLeg_flat(1)",	x3, _yCurr - _offsetY,	widthInf2,		22, -TABID_CorniceAndBase ! Set GLOB_UI_BUTTON_ID
	_yCurr = _yCurr + _dy - _offsetY

	ui_separator 0, _yCurr, 444, _yCurr

	! --- Right Side
	_yCurr	= pageStartY

	ui_outfield `Высота Шкафа`,		x5, _yCurr,				x6-x5-2,	_outfHeight
	ui_infield "ZZYZX",					x6,	_yCurr - _offsetY,	x8-x6,		_infHeight	ui_tooltip `Высота Шкафа`
	_yCurr = _yCurr + _dy

	ui_outfield `Внутреннее Наполнение`,		x5, _yCurr,				x6-x5-2,	_outfHeight
	ui_infield{3} "iFunctions",				x6,	_yCurr - _offsetY,	x8-x6,		_infHeight,
			8, "",
			0, 0, 0, 0, 0, 0,
			"", stInteriorType[1], 3,	!Shelf
			"", stInteriorType[2], 2,	!Hanger
			"", stInteriorType[3], 1,	!Combined
			"", stInteriorType[4], 0   ui_tooltip `Внутреннее Наполнение` !None
	_yCurr = _yCurr + _dy

	if iFunctions = 2 or iFunctions = 1 then	!Hanger or Combined
		_outflag = 0
	else
		_outflag = 4
	endif
	ui_outfield `Количество Штанг`,		x5, _yCurr,				x6-x5-2 + 35,	_outfHeight,	_outflag
	ui_infield "nHangers",				x7,	_yCurr - _offsetY,	x8-x7,		_infHeight	ui_tooltip `Количество Штанг`
	_yCurr = _yCurr + _dy

	if iFunctions = 3 or iFunctions = 1 then	!Shelf or Combined
		_outflag = 0
	else
		_outflag = 4
	endif
	ui_outfield `Кол-во Полок`,		x5, _yCurr,				x6-x5-2 + 35,	_outfHeight,	_outflag
	ui_infield "nShelves",				x7,	_yCurr - _offsetY,	x8-x7,		_infHeight	ui_tooltip `Кол-во Полок`
	_yCurr = _yCurr + _dy

	! --- Bottom part

	if iCabinetForm = SHAPE_SQUARE then	!Square
		ui_pict 2,					x_pict, y_pict, 76, 74, 1 !Wardrobe_StyleandDim_ShapeSquare.png
		ui_infield "A",				x_pict - 29, y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Ширина`
		ui_infield "B",				x_pict + 66, y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Длина`
	else
		if iCabinetForm = SHAPE_CORNER_L then	!Corner-L
			ui_pict 3, x_pict, y_pict, 76, 74, 1 !Wardrobe_StyleandDim_ShapeCornerL.png
		else 	!Corner-C
		ui_pict 4, x_pict, y_pict, 76, 74, 1 !Wardrobe_StyleandDim_ShapeCornerC.png
		endif
		ui_infield "A",				x_pict - 29, y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Ширина`
		ui_infield "sideDepth2",	x_pict + 66, y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Глубина Сбоку 2`
		ui_infield "B",				x_pict - 60, y_pict +77,				_infWidth,	_infHeight  ui_tooltip `Длина`
		ui_infield "sideDepth1",	x_pict + 35, y_pict +77,				_infWidth,	_infHeight  ui_tooltip `Глубина Сбоку 1`
	endif

	return


! ------------------------------------------------------------------------------
"CorniceAndBasePage":
! ------------------------------------------------------------------------------

	call "ui_cabinetBase" parameters all	iBaseClass = 1,	! BASECLASS_BASECORNICE
											iItemClass = 4	! ITEMCLASS_NO_SIDEOVERHANG

return

! ------------------------------------------------------------------------------
"DoorPage":
! ------------------------------------------------------------------------------

	pageStartY	= 5
	_dy			= 24
	_infHeight 	= 19
	_outfHeight = 15
	_offsetY	= 4
	x1 = 0
	x2 = 121

	_yCurr		= pageStartY + _dy * 6
	ui_outfield `Тип Дверей`, x1, _yCurr, x2 - 2, _outfHeight, 4 * not(bDoor)

	if iCabinetForm = SHAPE_SQUARE then
		ui_infield{3} "iDoorType", 	x2, _yCurr - _offsetY, 	90, 	_infHeight,
				8, "",
				0, 0, 0, 0, 0, 0,
				"", stDoorType[1], 1,
				"", stDoorType[2], 2   ui_tooltip `Тип Дверей`
		_yCurr		= _yCurr + _dy
		ui_infield "nDoor", 	x2, _yCurr - _offsetY, 	90, 	_infHeight ui_tooltip `Кол-во Дверей`
	else
		ui_outfield stDoorType[1], x2, _yCurr, 90, _outfHeight, 1 + 4 * not(bDoor)
		_yCurr		= _yCurr + _dy
		 if iCabinetForm = SHAPE_CORNER_C then
			ui_infield "nDoor", 	x2, _yCurr - _offsetY, 	90, 	_infHeight ui_tooltip `Кол-во Дверей`
		else
			formatString = "%~"
			ui_outfield str(formatString , nDoor), x2, _yCurr, 90, _outfHeight, 1 + 4 * not(bDoor)
		endif
	endif

	ui_outfield `Кол-во Дверей`, x1, _yCurr, x2 - 2, _outfHeight, 4 * not(bDoor)

	call "ui_cabinetDoor" parameters all	iItemClass = 4	! ITEMCLASS_WARDROBE
	
return

! ------------------------------------------------------------------------------
"KnobPage":
! ------------------------------------------------------------------------------

	call "ui_cabinetKnob" parameters all	iKnobClass = 2,	! KNOBCLASS_DOOR
											iItemClass = 0	! ITEMCLASS_OTHER

return

! ------------------------------------------------------------------------------
"2DReprPage":
! ------------------------------------------------------------------------------
	dim stSymbolTypes[9]
		stSymbolTypes[1] = `Стиль 1`
		stSymbolTypes[2] = `Стиль 2`
		stSymbolTypes[3] = `Стиль 3`
		stSymbolTypes[4] = `Стиль 4`
		stSymbolTypes[5] = `Стиль 5`
		stSymbolTypes[6] = `Стиль 6`
		stSymbolTypes[7] = `Стиль 7`
		stSymbolTypes[8] = `Стиль 8`
		stSymbolTypes[9] = `Стиль 9`

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 13,
													stSymbolTypes 		= stSymbolTypes,
													iSymbolStyle 		= iSymbolStyle,
													bOpeningAngle		= 0,
													bShowHangersin2D	= 1,
													bHangers			= bHangers,
													iFillListID			= TABID_2DRepr
return

! ------------------------------------------------------------------------------
"3DReprPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_cabinet_mat"
		SurfItemID = SurfItemID + 1
		if bDoor and (iDoorStyle <> 1 or iDoorStyle <> 8 or iDoorStyle <> 10 or iDoorStyle <> 99) then
			surfacesArray[SurfItemID]	= "matDoorFrame"
			SurfItemID = SurfItemID + 1
		endif
		if bDoor and (iDoorStyle = 3 or iDoorStyle = 5 or iDoorStyle = 7 or iDoorStyle = 9 or iDoorStyle = 12) then
			surfacesArray[SurfItemID]	= "matDoorGlass"
			SurfItemID = SurfItemID + 1
		endif
		if bDoor and (iDoorStyle = 14 or iDoorStyle = 15) then
			surfacesArray[SurfItemID]	= "matDoorMirror"
			SurfItemID = SurfItemID + 1
		endif
		if bDoor and (iDoorStyle <> 3 and iDoorStyle <> 5 and iDoorStyle <> 7 and iDoorStyle <> 9 and iDoorStyle <> 12) then
			surfacesArray[SurfItemID]	= "matDoorDecor1"
			SurfItemID = SurfItemID + 1
		endif
		if bDoor and (iDoorStyle > 15) then
			surfacesArray[SurfItemID]	= "matDoorDecor2"
			SurfItemID = SurfItemID + 1
		endif
		if bDoor and bKnob then
			surfacesArray[SurfItemID]	= "gs_knob_mat"
			SurfItemID = SurfItemID + 1
		endif
		if iFunctions = INTERIOR_SHELVES or iFunctions = INTERIOR_COMBINED then
			surfacesArray[SurfItemID]	= "gs_shelf_mat"
			SurfItemID = SurfItemID + 1
		endif
		if iFunctions = INTERIOR_HANGER or iFunctions = INTERIOR_COMBINED then
			surfacesArray[SurfItemID]	= "gs_hanger_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bCornice then
			surfacesArray[SurfItemID]	= "gs_cornice_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bBase then
			surfacesArray[SurfItemID]	= "gs_leg_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													bShowResol			= 1,
													gs_shadow			= gs_shadow,
													gs_resol			= gs_resol,
													iSurfaceListID		= TABID_3DRepr
return

! ------------------------------------------------------------------------------
"MinSpacePage":
! ------------------------------------------------------------------------------

	_infHeight 	= 19
	_infWidth	= 90
	x_pict		= 164
	y_pict		= 37

	if iCabinetForm = SHAPE_SQUARE then	!Square
		ui_pict 5,					x_pict, y_pict, 125, 131, 1 !Wardrobe_MinSpace_ShapeSquare.png
		ui_infield "A",				x_pict + 18,	y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Ширина`
		ui_infield "B",				x_pict + 128,	y_pict + 28,				_infWidth,	_infHeight  ui_tooltip `Длина`
		ui_infield "MSFront",		x_pict + 128,	y_pict + 79,				_infWidth,	_infHeight  ui_tooltip `Минимальное Пространство Спереди`

	else

		if iCabinetForm = SHAPE_CORNER_L then	!Corner-L
			ui_pict 6, x_pict, y_pict, 125, 131, 1 !Wardrobe_MinSpace_ShapeCornerL.png
			ui_infield "A",				x_pict + 18,	y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Ширина`
			ui_infield "sideDepth2",	x_pict + 128,	y_pict + 28,				_infWidth,	_infHeight  ui_tooltip `Глубина Сбоку 2`
			ui_infield "B",				x_pict - 93,	y_pict + 56,				_infWidth,	_infHeight  ui_tooltip `Длина`
			ui_infield "sideDepth1",	x_pict - 35,	y_pict + 134,				_infWidth,	_infHeight  ui_tooltip `Глубина Сбоку 1`
			ui_infield "MSFront",		x_pict + 128,	y_pict + 79,				_infWidth,	_infHeight  ui_tooltip `Минимальное Пространство Спереди`

			dimensionForm = ""
			qq= REQUEST ("Working_length_unit", "", dimensionForm)
			ui_outfield str(dimensionForm, MSFront),	x_pict + 56,	y_pict + 134,	50,	_outfHeight, 2	ui_tooltip `Минимальное Пространство Спереди`


		else 	!Corner-C
		ui_pict 7, x_pict, y_pict, 166, 180, 1 !Wardrobe_MinSpace_ShapeCornerC.png
			ui_infield "A",				x_pict + 18,	y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Ширина`
			ui_infield "sideDepth2",	x_pict + 115,	y_pict - _infHeight - 3,	_infWidth,	_infHeight  ui_tooltip `Глубина Сбоку 2`
			ui_infield "B",				x_pict - 93,	y_pict + 56,				_infWidth,	_infHeight  ui_tooltip `Длина`
			ui_infield "sideDepth1",	x_pict - 93,	y_pict + 134,				_infWidth,	_infHeight  ui_tooltip `Глубина Сбоку 1`
			ui_infield "MSFront",		x_pict + 62,	y_pict + 183,				_infWidth,	_infHeight  ui_tooltip `Минимальное Пространство Спереди`
		endif
	endif

return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return
