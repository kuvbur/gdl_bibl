
if gs_detlevel_3D_m = DETLEVEL_3D_OFF then END

! Hotspots for "Associative" dimensioning

unID=1

if GLOB_CONTEXT = 4 then
	unID=350
	hotspot a/2, b, 0, unID		:unID=unID+1
	hotspot -a/2, b, 0, unID	:unID=unID+1

	hotspot a/2, 0, 0, unID		:unID=unID+1
	hotspot -a/2, 0, 0, unID	:unID=unID+1
endif

pen gs_pen_2D
wnch = -1

if not(WIDO_REVEAL_SIDE) then
	ADDZ WALL_THICKNESS-2*WIDO_SILL
else
	ADDZ WALL_THICKNESS-WIDO_SILL-melys
endif

WALLNICHE 4, 1, 1+16,	!semi-inf cut
		0,		0,	wnch, 	melys,
		-A/2, 	0, 	1+2+4+8+16,
		-A/2, 	B, 	1+2+4+8+16,
		A/2, 	B, 	1+2+4+8+16,
		A/2, 	0, 	1+2+4+8+16

vvtg=0.02

material  gs_frame_mat

if tok then
	addz -gs_frame_thk
	prism_   9, gs_frame_thk,
		   -a/2, 		0, 		 15,
		   -a/2+tok_b, 	0, 		 15,
		   -a/2+tok_b, 	b-tok_f, 15,
			a/2-tok_j, 	b-tok_f, 15,
			a/2-tok_j, 	0, 		 15,
			a/2, 		0, 		 15,
			a/2, 		b, 		 15,
		   -a/2, 		b, 		 15,
		   -a/2, 		0, 		 -1
	del 1
endif

rotx -90

prism_  4, labmag,
	   -a/2+tok_b+vvtg, 0, 		15,
	   -a/2+tok_b+vvtg, vvtg, 	15,
		a/2-tok_j-vvtg, vvtg, 	15,
		a/2-tok_j-vvtg, 0, 		15

prism_   9, b-tok_f,
	   -a/2+tok_b, 		0, 			15,
	   -a/2+tok_b+vvtg, 0, 			15,
	   -a/2+tok_b+vvtg, melys-vvtg, 15,
		a/2-tok_j-vvtg, melys-vvtg, 15,
		a/2-tok_j-vvtg, 0, 			15,
		a/2-tok_j, 		0, 			15,
		a/2-tok_j, 		melys, 		15,
	   -a/2+tok_b,		melys, 		15,
	   -a/2+tok_b, 		0, 			-1

addz labmag

prism_  4,vvtg,
	   -a/2+tok_b+vvtg, 0, 			15,
	   -a/2+tok_b+vvtg, melys-vvtg, 15,
		a/2-tok_j-vvtg, melys-vvtg, 15,
		a/2-tok_j-vvtg, 0, 			15

if polc then
	ADDy 2*vvtg*(iOpeningType = OPENINGTYPE_SLIDING)

	for i=1 to polc_num
		addz (b-tok_f-vvtg-labmag)/(polc_num+1)
		prism_  4,vvtg,
			   -a/2+tok_b+vvtg, 0, 							 15,
			   -a/2+tok_b+vvtg, melys-vvtg-(2*vvtg*(iOpeningType = OPENINGTYPE_SLIDING)), 15,
				a/2-tok_j-vvtg, melys-vvtg-(2*vvtg*(iOpeningType = OPENINGTYPE_SLIDING)), 15,
				a/2-tok_j-vvtg, 0, 							 15
	next i

	del polc_num
	DEL 1
endif

addz  b-tok_f-vvtg-labmag

prism_  4,vvtg,
	   -a/2+tok_b+vvtg, 0, 			15,
	   -a/2+tok_b+vvtg, melys-vvtg, 15,
		a/2-tok_j-vvtg, melys-vvtg, 15,
		a/2-tok_j-vvtg, 0, 			15

del 3


! --- door panel ---

addy labmag

if iOpeningType = OPENINGTYPE_SLIDING then
	a_p = (a-tok_j-tok_b-2*vvtg)/2
	b_p = b-labmag-tok_f-2*vvtg
else
	a_p = (a - tok_j - tok_b)/2
	b_p = b-labmag-tok_f
endif

if iOpeningType = OPENINGTYPE_SLIDING then
	add a_p/2 + (tok_b - tok_j)/2, vvtg, -p_d
	side = 1
	mirr_panel = 0
	gosub "panelmacro"
	del 1

	add -a_p/2 + (tok_b - tok_j)/2, vvtg, 0
	mulx -1
	addz -2*p_d
	side = 1
	mirr_panel = 1
	gosub "panelmacro"
	del 3
endif


if iOpeningType = OPENINGTYPE_SINGLE then
	a_p = (a-tok_j-tok_b)
	b_p = b-labmag-tok_f

	add  (tok_b-tok_j)/2, 0, 0
	side = -1
	mirr_panel = 0
	gosub "panelmacro"
	del 1
endif

if iOpeningType = OPENINGTYPE_DOUBLE then
	addx a_p/2 + (tok_b - tok_j)/2
	side = -1
	mirr_panel = 0
	gosub "panelmacro"
	del 1

	addx -a_p/2 + (tok_b - tok_j)/2
	mulx -1
	side = -1
	mirr_panel = 1
	gosub "panelmacro"
	del 2
endif

if iOpeningType <> OPENINGTYPE_SLIDING then
	editingLength = a_p
	if iOpeningType = OPENINGTYPE_DOUBLE then
		add editingLength, b_p/2, -p_d
	else
		add a_p/2, b_p/2, -p_d
	endif
	hotspot -editingLength,					0,	                            0,	unID,	gs_open_3D,	4+128	: unID = unID + 1
	hotspot -editingLength*cos(gs_open_3D),	0,	editingLength*sin(gs_open_3D),	unID,	gs_open_3D,	5		: unID = unID + 1
	hotspot 0,					            0,	                            0,	unID,	gs_open_3D,	6		: unID = unID + 1
	hotspot 0,					            1,	                            0,	unID,	gs_open_3D,	7		: unID = unID + 1
	del 1
endif
del 1

DEL TOP

END

"panelmacro":

	call "d_panel_built_in" parameters a=a_p, b=b_p, gs_open_3D=side*gs_open_3D,
						gs_ap_m=gs_ap_m, mirrored=mirr_panel,gs_cust_p=gs_cust_p,p_d=p_d,
						gs_hando_m=iHandleStyle, mat_p=gs_leaf_mat, mat1=gs_glass_mat,
						mat2=gs_metal_mat, d_z=p_d-0.02, optyp_m=iOpeningType, gs_resol=gs_resol,
						bTurnPanel = bTurnPanel
						
	return

