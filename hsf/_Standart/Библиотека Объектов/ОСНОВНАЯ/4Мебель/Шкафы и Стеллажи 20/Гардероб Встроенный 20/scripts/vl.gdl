
hideparameter all "A", "B", "ZZYZX"

! Pens Range
! ---------------------------------------------------------------------------
for i = 1 to 255
	put i
next i
values "gs_pen_2D" use(NSP)
values "gs_penFr_fg" use(NSP)
values "gs_frame_pen" use(NSP)
values "gs_leaf_pen" use(NSP)
for i = 1 to nsp
	dummy = get(1)
next i
! ---------------------------------------------------------------------------

call "FM_types" parameters all
values "gs_ui_current_page" 1, 2, 3, 4

values "gs_detlevel_3D" stDetLevel
values "gs_detlevel_3D_m" 0,2
values "optyp" stOpeningType
values "iOpeningType" 1,2,3
values "gs_cabStyle_2D" stCabStyle
values "iCabStyle" 1,2,3
values "gs_hando" stHandleStyle
values "iHandleStyle" 1,2,3,4,5
values "gs_ap" stDoorPanel
values "gs_ap_m" 0,1,2,3,4,5,6,7,8,9,10

values "gs_resol" range [3, )
values "MSFront" range [0, )
values "gs_open_3D" range [0, 180]
values "polc_num" range [0, ]

! IFC Parameters
call "IFC_optype_door" parameters all
parameters ifc_optype = 0	! DOOR_OP_NOT_DEFINED

lock "ifc_optypestr"


IF GLOB_MODPAR_NAME = "polc_num" Then
	IF polc_num = 0 Then
		polc = 0
	Else
		polc = 1
	Endif
	parameters polc = polc
Endif

if GLOB_MODPAR_NAME = "gs_ap" then
	gs_ap_m = 0		! Off
	iPanel= 1
	do
		if gs_ap = stDoorPanel[iPanel] then gs_ap_m = iPanel
		iPanel= iPanel+1
	while iPanel < 11 and gs_ap_m = 0
	parameters gs_ap_m = gs_ap_m
else
	gs_ap = stDoorPanel[11]
	for i = 1 to vardim1(stDoorPanel) - 1
		if gs_ap_m = i then gs_ap = stDoorPanel[i]
	next i
	parameters gs_ap = gs_ap
endif

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DETLEVEL_3D_DETAILED
	if gs_detlevel_3D = stDetLevel[2] then gs_detlevel_3D_m = DETLEVEL_3D_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetLevel[1]
	if gs_detlevel_3D_m = DETLEVEL_3D_OFF then gs_detlevel_3D = stDetLevel[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if GLOB_MODPAR_NAME = "optyp" then
	iOpeningType = OPENINGTYPE_SINGLE
	if optyp = stOpeningType[2] then iOpeningType = OPENINGTYPE_DOUBLE
	if optyp = stOpeningType[3] then iOpeningType = OPENINGTYPE_SLIDING
	parameters iOpeningType = iOpeningType
else
	optyp = stOpeningType[1]
	if iOpeningType = OPENINGTYPE_DOUBLE	then optyp = stOpeningType[2]
	if iOpeningType = OPENINGTYPE_SLIDING	then optyp = stOpeningType[3]
	parameters optyp = optyp
endif

if GLOB_MODPAR_NAME = "gs_cabStyle_2D" then
	iCabStyle = CABSTYLE_1
	if gs_cabStyle_2D = stCabStyle[2] then iCabStyle = CABSTYLE_2
	if gs_cabStyle_2D = stCabStyle[3] then iCabStyle = CABSTYLE_3
	parameters iCabStyle = iCabStyle
else
	gs_cabStyle_2D = stCabStyle[1]
	if iCabStyle = CABSTYLE_2 then gs_cabStyle_2D = stCabStyle[2]
	if iCabStyle = CABSTYLE_3 then gs_cabStyle_2D = stCabStyle[3]
	parameters gs_cabStyle_2D = gs_cabStyle_2D
endif

if GLOB_MODPAR_NAME = "gs_hando" then
	iHandleStyle = HANDLESTYLE_1
	if gs_hando = stHandleStyle[2] then iHandleStyle = HANDLESTYLE_2
	if gs_hando = stHandleStyle[3] then iHandleStyle = HANDLESTYLE_3
	if gs_hando = stHandleStyle[4] then iHandleStyle = HANDLESTYLE_4
	if gs_hando = stHandleStyle[5] then iHandleStyle = HANDLESTYLE_NONE
	parameters iHandleStyle = iHandleStyle
else
	gs_hando = stHandleStyle[1]
	if iHandleStyle = HANDLESTYLE_2		then gs_hando = stHandleStyle[2]
	if iHandleStyle = HANDLESTYLE_3		then gs_hando = stHandleStyle[3]
	if iHandleStyle = HANDLESTYLE_4		then gs_hando = stHandleStyle[4]
	if iHandleStyle = HANDLESTYLE_NONE	then gs_hando = stHandleStyle[5]
	parameters gs_hando = gs_hando
endif
! --- Custom Elements ----------------------------------------------------------

if gs_ap_m <> 10 then							! not Custom Panel
	lock "gs_cust_p", "bTurnPanel"
endif

if gs_ap_m = 10 then							! Custom Panel
	dim gs_pt[]
	call "Object_Panel_Collection" parameters returned_parameters gs_pt
	values "gs_cust_p" gs_pt
endif

! --- Hide Parameters ----------------------------------------------------------
if not(gs_bFills) then
	hideparameter	"gs_fillFr",
					"gs_penFr_fg",
					"gs_penFr_bg"
endif

if not(tok) then
	Hideparameter "gs_frame_width", "gs_frame_thk", "tok_diff", "tok_b", "tok_j", "tok_f"
	Lock  "gs_frame_width", "gs_frame_thk", "tok_diff", "tok_b", "tok_j", "tok_f"
endif

if not(tok_diff) then lock "tok_b", "tok_j", "tok_f"


if iOpeningType = OPENINGTYPE_SLIDING then lock "gs_oef", "gs_open_2D", "gs_open_3D"
IF gs_oef=0 THEN LOCK "gs_open_2D"

