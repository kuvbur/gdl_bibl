
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "C23710AC-4602-4DC4-8821-D83297BD4065"
	_startID	= "B0A9AB9D-0093-4D7A-A8A5-7ABE9B251138"
	_endID		= "B3684995-9E79-47C7-B48B-4F4956384C29"
gosub "AccessoryBooleans_FWM"

	_startID	= "B3684995-9E79-47C7-B48B-4F4956384C29"
	_endID		= "0A27413D-8D04-4A96-9136-C867CA52EBD3"
gosub "newparameters_FWM"

	_startID	= "0A27413D-8D04-4A96-9136-C867CA52EBD3"
	_endID		= "2BA6F527-D10C-48F3-8836-5D3916FA9F20"
gosub "2DSymbolStyles_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
	
		! If gs_cornice_type_m was 0, that equals with bCornice being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_cornice_type_m", _gs_cornice_type_m)
		
		if _gs_cornice_type_m = 0 then
			bCornice = 0
		else
			bCornice = 1
		endif
		parameters bCornice = bCornice

		! If gs_leg_type_m was 0, that equals with bBase being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_leg_type_m", _gs_leg_type_m)
		
		if _gs_leg_type_m = 0 then
			bBase = 0
		else
			bBase = 1
		endif
		parameters bBase = bBase

		! If gs_knob_type_m was 0, that equals with bKnob being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m)
		
		if _gs_knob_type_m = 0 then
			bKnob = 0
			bKnobAbove = 0
		else
			bKnob = 1
			bKnobAbove = 1
		endif
		parameters bKnob = bKnob

		actualGuid = _endID
	endif
return

! ==============================================================================
! Newparameters_EnablebBevel_iKnobTypeAbove_xKnobPozAbove_yKnobPozAbove
! _knobSizeAbove_xDrawerKnobPosAbove_zDrawerKnobPosAbove_nKnobDrawersAbove
! ==============================================================================

"newparameters_FWM":
	if actualGuid = _startID then
	
		! bBevel was a hidden parameter, so it is turned off.
		bBevel = 0
		parameters bBevel = bBevel

		! Duplicate Knob parameters: Knobs Above get the value of Knobs Under
		bHas = STORED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m)
		if bHas then
			iKnobTypeAbove = _gs_knob_type_m
			parameters iKnobTypeAbove = iKnobTypeAbove
		endif

		bHas = STORED_PAR_VALUE("knob_pl_x", _knob_pl_x)
		if bHas then
			xKnobPozAbove = _knob_pl_x
			parameters xKnobPozAbove = xKnobPozAbove
		endif

		bHas = STORED_PAR_VALUE("knob_pl_y", _knob_pl_y)
		if bHas then
			yKnobPozAbove = _knob_pl_y
			parameters yKnobPozAbove = yKnobPozAbove
		endif

		bHas = STORED_PAR_VALUE("knob_size", _knob_size)
		if bHas then
			knobSizeAbove = _knob_size
			parameters knobSizeAbove = knobSizeAbove
		endif

		bHas = STORED_PAR_VALUE("zDrawerKnobPos", _zDrawerKnobPos)
		if bHas then
			zDrawerKnobPosAbove = _zDrawerKnobPos
			parameters zDrawerKnobPosAbove = zDrawerKnobPosAbove
		endif

		bHas = STORED_PAR_VALUE("nKnobDrawers", _nKnobDrawers)
		if bHas then
			nKnobDrawersAbove = _nKnobDrawers
			parameters nKnobDrawersAbove = nKnobDrawersAbove
		endif

		bHas = STORED_PAR_VALUE("gs_knob_mat", _gs_knob_mat)
		if bHas then
			matKnobAbove = _gs_knob_mat
			parameters matKnobAbove = matKnobAbove
		endif

		! Horizontal Position of Drawers did not work, it was 0 (in case of 1 Knob) or 
		! 0.1 (in case of 2 Knob), indepenedently from parameter
		if nKnobDrawers = 1 then
			xDrawerKnobPosAbove = 0
			xDrawerKnobPos = 0
		else
			xDrawerKnobPosAbove = 0.1
			xDrawerKnobPos = 0.1
		endif
		parameters xDrawerKnobPosAbove = xDrawerKnobPosAbove	
		parameters xDrawerKnobPos = xDrawerKnobPos	

		actualGuid = _endID
	endif
return

! ==============================================================================
! New Symbol 2D Styles
! ==============================================================================
"2DSymbolStyles_FWM":
	if actualGuid = _startID then
	
		bHas = DELETED_PAR_VALUE("iSymbolType", _iSymbolType)
		
		if _iSymbolType = 1 then iSymbolStyle = 1		! Style 1
		if _iSymbolType = 2 then iSymbolStyle = 2		! Style 2
		if _iSymbolType = 3 then iSymbolStyle = 4		! Style 4
		if _iSymbolType = 4 then iSymbolStyle = 7		! Style 7
		if _iSymbolType = 5 then iSymbolStyle = 8		! Style 8
		if _iSymbolType = 6 then iSymbolStyle = 9		! Style 9
		
		parameters 	iSymbolStyle = iSymbolStyle

		actualGuid = _endID
	endif
return
