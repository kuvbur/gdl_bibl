
if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then shadow off

! -----------------------------------------------------------------------------
! 3D Hotspots
! -----------------------------------------------------------------------------

if not(bBase) then addz -leg_h

unID = 1
hotspot	0, 0, 0, unID, zzyzx, 1+128 : unID = unID + 1
hotspot	0, 0, -1, unID, zzyzx, 3+128 : unID = unID + 1
hotspot	0, 0, zzyzx, unID, zzyzx, 2 : unID = unID + 1

hotspot	A, 0, 0, unID, zzyzx, 1+128 : unID = unID + 1
hotspot	A, 0, -1, unID, zzyzx, 3+128 : unID = unID + 1
hotspot	A, 0, zzyzx, unID, zzyzx, 2 : unID = unID + 1

hotspot	0, B, 0, unID, zzyzx, 1+128 : unID = unID + 1
hotspot	0, B, -1, unID, zzyzx, 3+128 : unID = unID + 1
hotspot	0, B, zzyzx, unID, zzyzx, 2 : unID = unID + 1

hotspot	A, B, 0, unID, zzyzx, 1+128 : unID = unID + 1
hotspot	A, B, -1, unID, zzyzx, 3+128 : unID = unID + 1
hotspot	A, B, zzyzx, unID, zzyzx, 2 : unID = unID + 1


hotspot	side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_LEFT), cooh, 0, unID, leg_h, 1 : unID = unID + 1
hotspot	side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_LEFT), cooh, -1, unID, leg_h, 3+128 : unID = unID + 1
hotspot	side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_LEFT), cooh, leg_h, unID, leg_h, 2 : unID = unID + 1

hotspot	A-side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_RIGHT), cooh, 0, unID, leg_h, 1 : unID = unID + 1
hotspot	A-side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_RIGHT), cooh, -1, unID, leg_h, 3+128 : unID = unID + 1
hotspot	A-side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_RIGHT), cooh, leg_h, unID, leg_h, 2 : unID = unID + 1

hotspot	A-side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_RIGHT), B, 0, unID, leg_h, 1 : unID = unID + 1
hotspot	A-side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_RIGHT), B, -1, unID, leg_h, 3+128 : unID = unID + 1
hotspot	A-side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_RIGHT), B, leg_h, unID, leg_h, 2 : unID = unID + 1

hotspot	side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_LEFT), B, 0, unID, leg_h, 1 : unID = unID + 1
hotspot	side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_LEFT), B, -1, unID, leg_h, 3+128 : unID = unID + 1
hotspot	side_overhang*(gs_lining_m=LINING_FRONT_BOTH OR gs_lining_m=LINING_FRONT_LEFT), B, leg_h, unID, leg_h, 2 : unID = unID + 1

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

unID = 100
pen gs_cont_pen
gosub iStyle

if not(bBase) then del 1

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Credenza Multidrawer
! -----------------------------------------------------------------------------

2:

	lffw = a-2*side_overhang-2*0.01       !!! Leafs full widht
	lffh = zzyzx-cot-dwh-leg_h-2*0.01     !!! Leafs full height

	add a/2, b, 0
	mulx -1

	! --- Cabinet ---

	call "fa_cabinet_credenza" parameters ALL b=b-fr_th1

	! --- Legs ---
	if bBase then
		call "fa_cabinet_legtyp" parameters ALL b=b-fr_th1
	endif
	! --- Cornices ---

	if bCornice then

		mulx -1
		addz zzyzx-cot

		if cooh <= fr_th1 then
			corniceDepth = b-fr_th1 + cooh
		else
			corniceDepth = b
		endif
		call "fa_cornicetyp" parameters 	a=a,
											b=corniceDepth,
											gs_detlevel_3D_m=gs_detlevel_3D_m,
											gs_cornice_type_m=gs_cornice_type_m,
											cot=cot,
											cooh=cooh,
											side_overhang = side_overhang,
											gs_lining_m=gs_lining_m,
											gs_resol=gs_resol,
											gs_cornice_mat=gs_cornice_mat,
											gs_cont_pen=gs_cont_pen
		del 2

	endif

	! --- Vertical and horizontal drawers ---

	if gs_lining_m = LINING_FRONT then valt2 = -a/2 + 0.01
	if gs_lining_m = LINING_FRONT_BOTH then valt2 = -a/2 + side_overhang + 0.01
	if gs_lining_m = LINING_FRONT_LEFT then valt2 = -a/2 + 0.01
	if gs_lining_m = LINING_FRONT_RIGHT then valt2 = -a/2 + side_overhang + 0.01

	add valt2, -b+fr_th1+cooh, leg_h+0.01

	for t=1 to vdnum
		for i=1 to hdnum
			! --- Bottom Drawers ---
			xDrawerKnobPosMod = 0
			if gs_knob_type_m > 3 then xDrawerKnobPosMod = knob_size/2

			call "fa_cabdoortyp" parameters gs_detlevel_3D_m	= gs_detlevel_3D_m,
											gs_full_edit		= 0,
											gs_bevel 			= bDrawerBevel,
											door_ang			= 0,
											door_w				= lffw2/hdnum,
											door_h				= lffh/vdnum,
											doorOffset			= 0.002,
											gs_door_type_m		= drawer_m, !gs_door_type_m,
											doorframe_w			= drawerframe_w,
											vert_gnum			= vert_gnum,
											hor_gnum			= hor_gnum,
											gs_cont_pen			= gs_cont_pen,
											gs_shadow			= gs_shadow,
											gs_resol			= gs_resol,
											fr_th1				= fr_th1,
											bLowerDrawer		= 0,
											UpperKnob			= 1,
											bKnob				= bDoorKnob,
											bKnobNoneType		= bDoorKnob,
											iNumKnob			= iNumDoorKnob,
											gs_knob_type_m		= gs_knob_type_m,
											gs_knob_p_m			= 1,
											knob_size			= knob_size,
											knob_pl_x_up		= lffw2/hdnum/2 - knob_pl_x_up - 0.001 - xDrawerKnobPosMod,
											knob_pl_y_up		= lffh/vdnum/2 + knob_pl_y_up - 0.001,
											gs_knob_mat			= gs_knob_mat,
											doorglas_mat		= DrawerGlasMat,
											doorframe_mat		= DrawerFrameMat,
											cabdoortyp_unID		= unID,
						returned_parameters unID

			if drawer_m < 4 & bDoorKnob then
				if iNumDoorKnob = 1 then
					hotspot lffw2/hdnum/2 + knob_pl_x_up, -fr_th1, lffh/vdnum/2, 					unID, knob_pl_y_up,1+128 	: unID = unID + 1
					hotspot lffw2/hdnum/2 + knob_pl_x_up, -fr_th1, -1, 								unID, knob_pl_y_up,3 		: unID = unID + 1
					hotspot lffw2/hdnum/2 + knob_pl_x_up, -fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_y_up,2 		: unID = unID + 1

					hotspot lffw2/hdnum/2,					-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,1+128 	: unID = unID + 1
					hotspot lffw2/hdnum/2 - 1,				-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,3 		: unID = unID + 1
					hotspot lffw2/hdnum/2 + knob_pl_x_up,	-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,2 		: unID = unID + 1
					unID = unID + 6
				else
					hotspot lffw2/hdnum/2 + knob_pl_x_up-0.214, -fr_th1, lffh/vdnum/2, 					unID, knob_pl_y_up,1+128 	: unID = unID + 1
					hotspot lffw2/hdnum/2 + knob_pl_x_up-0.214, -fr_th1, -1, 							unID, knob_pl_y_up,3 		: unID = unID + 1
					hotspot lffw2/hdnum/2 + knob_pl_x_up-0.214, -fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_y_up,2 		: unID = unID + 1

					hotspot lffw2/hdnum/2 - 0.214,				-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,1+128 	: unID = unID + 1
					hotspot lffw2/hdnum/2 - 1 - 0.214,			-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,3 		: unID = unID + 1
					hotspot lffw2/hdnum/2 + knob_pl_x_up-0.214, -fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,2 		: unID = unID + 1

					hotspot lffw2/hdnum-(lffw2/hdnum/2 + knob_pl_x_up-0.214), -fr_th1, lffh/vdnum/2, 					unID, knob_pl_y_up,1+128 	: unID = unID + 1
					hotspot lffw2/hdnum-(lffw2/hdnum/2 + knob_pl_x_up-0.214), -fr_th1, -1, 								unID, knob_pl_y_up,3 		: unID = unID + 1
					hotspot lffw2/hdnum-(lffw2/hdnum/2 + knob_pl_x_up-0.214), -fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_y_up,2 		: unID = unID + 1

					hotspot lffw2/hdnum-(lffw2/hdnum/2 - 0.214),				-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,1+128 	: unID = unID + 1
					hotspot lffw2/hdnum-(lffw2/hdnum/2 -1 - 0.214),				-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,3 		: unID = unID + 1
					hotspot lffw2/hdnum-(lffw2/hdnum/2 + knob_pl_x_up - 0.214),	-fr_th1, lffh/vdnum/2 + knob_pl_y_up, 	unID, knob_pl_x_up,2 		: unID = unID + 1

				endif
			else
				unID = unID + 12
			endif
			addx lffw2/hdnum
		next i
		del hdnum
		addz lffh/vdnum
	next t
	del vdnum
	del 1

	! --- Top drawers ---

	if abs (dwh) > EPS then		!!dwh<>0

		if gs_lining_m = LINING_FRONT_BOTH then valt = -a/2 + side_overhang + 0.01
		if gs_lining_m = LINING_FRONT_LEFT then valt = -a/2 + 0.01
		if gs_lining_m = LINING_FRONT_RIGHT then valt = -a/2 + side_overhang + 0.01
		if gs_lining_m = LINING_FRONT then valt = -a/2 + 0.01

		xDrawerKnobPosMod = 0
		if iDrawerKnobType > 3 then xDrawerKnobPosMod = drawerKnobSize/2

		add valt, -b+fr_th1+cooh, leg_h+0.01+lffh

		for i=1 to dnum

			call "fa_cabdoortyp" parameters gs_detlevel_3D_m	= gs_detlevel_3D_m,
											gs_full_edit		= 0,
											door_ang			= 0,
											door_w				= lffw2/dnum,
											door_h				= dwh,
											doorOffset			= 0.002,
											gs_door_type_m		= drawer_m, ! gs_door_type_m,
											doorframe_w			= drawerframe_w,
											fr_th1				= fr_th1,
											UpperKnob			= 0,
											bKnob				= bDrawerKnob,
											bKnobNoneType		= bDrawerKnob,
											iNumKnob			= iNumDrawerKnob,
											gs_knob_type_m		= iDrawerKnobType,
											gs_knob_p_m			= 1,
											knob_size			= drawerKnobSize,
											knob_pl_x			= lffw2/dnum/2 - xDrawerKnobPos - 0.001 - xDrawerKnobPosMod,
											knob_pl_y			= dwh/2 + zDrawerKnobPos - 0.001,
											gs_knob_mat			= matDrawerKnob,
											vert_gnum			= 0,
											hor_gnum			= 0,
											gs_cont_pen			= gs_cont_pen,
											gs_resol			= gs_resol,
											gs_shadow			= gs_shadow,
											doorglas_mat		= DrawerGlasMat,
											doorframe_mat		= DrawerFrameMat,
											gs_bevel 			= bDrawerBevel,
											cabdoortyp_unID		= unID,
						returned_parameters unID

			if drawer_m < 4 & bDrawerKnob then
				if iNumDrawerKnob = 1 then
					hotspot lffw2/dnum/2 + xDrawerKnobPos, -fr_th1, dwh/2, 						unID, zDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum/2 + xDrawerKnobPos, -fr_th1, -1, 						unID, zDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum/2 + xDrawerKnobPos, -fr_th1, dwh/2 + zDrawerKnobPos, 	unID, zDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum/2,					-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum/2 -1,				-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum/2 + xDrawerKnobPos,	-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,2 		: unID = unID + 1
					unID = unID + 6
				else
					hotspot lffw2/dnum - xDrawerKnobPos-0.08, -fr_th1, dwh/2, 					unID, zDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum - xDrawerKnobPos-0.08, -fr_th1, -1, 						unID, zDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum - xDrawerKnobPos-0.08, -fr_th1, dwh/2 + zDrawerKnobPos, 	unID, zDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum - 0.08,						-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum + 1 - 0.08,					-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum - xDrawerKnobPos -  0.08,	-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum + xDrawerKnobPos+0.08-lffw2/dnum, -fr_th1, dwh/2, 					unID, zDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum + xDrawerKnobPos+0.08-lffw2/dnum, -fr_th1, -1, 						unID, zDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum + xDrawerKnobPos+0.08-lffw2/dnum, -fr_th1, dwh/2 + zDrawerKnobPos, 	unID, zDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum + 0.08 - lffw2/dnum,					-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum - 1 + 0.08-lffw2/dnum,				-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum + xDrawerKnobPos+0.08-lffw2/dnum,	-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,2 		: unID = unID + 1
				endif
			else
				unID = unID + 12
			endif

			addx lffw2/dnum
		next i
		del dnum
		del 1
	endif

	del 2
return

! -----------------------------------------------------------------------------
! Credenza Simple
! -----------------------------------------------------------------------------

1:

	lffw = a-2*side_overhang-2*0.01           	! Leafs full widht
	lffh = zzyzx-cot-dwh-leg_h-2*0.01      	! Leafs full height

	add a/2,b,0
	mulx -1

	! --- Detailed 3D ---
	call "fa_cabinet_credenza" parameters ALL b=b-fr_th1

	! --- Cornices ---

	if bCornice then

		mulx -1
		addz zzyzx-cot

		if cooh <= fr_th1 then
			corniceDepth = b-fr_th1 + cooh
		else
			corniceDepth = b
		endif

		call "fa_cornicetyp" parameters 	a=a,
											b=corniceDepth,
											gs_detlevel_3D_m=gs_detlevel_3D_m,
											gs_cornice_type_m=gs_cornice_type_m,
											gs_lining_m=gs_lining_m,
											cot=cot,
											cooh=cooh, 
											side_overhang = side_overhang,
											gs_resol=gs_resol,
											gs_cornice_mat=gs_cornice_mat,
											gs_cont_pen=gs_cont_pen
		del 2

	endif

	! --- Legs ---
	if bBase then
		call "fa_cabinet_legtyp" parameters ALL b=b-fr_th1
	endif

	! --- Shelves ---

	valt = -a/2+0.01
	if gs_lining_m = LINING_FRONT_BOTH or gs_lining_m = LINING_FRONT_RIGHT then
		valt = valt+side_overhang
	endif
	material gs_cabinet_mat

	if bShelves then

		! --- Shelf between drawer and doors ---
		add valt, -0.02, zzyzx-cot-0.02-dwh
		prism_ 4, 0.02,
				lffw2-0.01,0,15,
				lffw2-0.01,-b+fr_th1+cooh+0.02,15,
				0.01,-b+fr_th1+cooh+0.02,15,
				0.01,0,15
		gosub 1100
		del 1

		! --- Bottom Shelves ---

		vpn = ceil (dn/2)-1
		spfw = a-2*side_overhang-2*0.02   ! --- Side panels full widht
		sfh = zzyzx-cot-leg_h-2*0.02      ! --- Shelves full height
		bb = (sfh+0.02-dwh)/(iNumShelves+1)

		addx -(spfw+0.02)/2+(spfw+0.02)/2
		add valt,-0.02,leg_h+bb
		for i=1 to iNumShelves
			prism_ 4, 0.02,
					lffw2-0.01,0,15,
					lffw2-0.01,-b+fr_th1+cooh+0.02,15,
					0.01,-b+fr_th1+cooh+0.02,15,
					0.01,0,15
			gosub 1100
			addz bb
		next i
		del iNumShelves
		del 2

	endif

	! --- Vertical panels ---
	vpn = ceil (dn/2)-1

	add valt+(lffw2/dn)*2, -0.02, leg_h+0.02
	for i=1 to vpn
		prism_ 4, zzyzx-cot-dwh-leg_h-2*0.02,
				0.01,0,15,
				-0.01,0,15,
				-0.01,-b+fr_th1+cooh+0.02,15,
				0.01,-b+fr_th1+cooh+0.02,15
		gosub 1100
		addx (lffw2/dn)*2
	next i
	del vpn
	del 1

	! --- Leafs ---

	if knob_pl_x_up < 0 then knob_pl_x_up = 0.05
	if knob_pl_y_up < 0 then knob_pl_y_up = zzyzx - cot - dwh - leg_h - 2*0.01 - 0.04

	if int (dn/2) <> dn/2 then
		add valt + lffw2, -b+fr_th1+cooh, (leg_h+0.01)
		mulx -1
		call "fa_cabdoortyp" parameters gs_detlevel_3D_m	= gs_detlevel_3D_m,
										gs_full_edit		= 1,
										gs_bevel 			= gs_bevel,
										door_ang			= 0,
										door_w				= lffw2/dn,
										door_h				= lffh,
										doorOffset			= 0.002,
										gs_door_type_m		= gs_door_type_m,
										doorframe_w			= doorframe_w,
										vert_gnum			= vert_gnum,
										hor_gnum			= hor_gnum,
										gs_cont_pen			= gs_cont_pen,
										gs_resol			= gs_resol,
										gs_shadow			= gs_shadow,
										fr_th1				= fr_th1,
										UpperKnob			= 1,
										bKnob				= bDoorKnob,
										bKnobNoneType		= bDoorKnob,
										iNumKnob			= iNumDoorKnob,
										gs_knob_type_m		= gs_knob_type_m,
										gs_knob_p_m			= gs_knob_p_m,
										knob_size			= knob_size,
										knob_pl_x_up		= knob_pl_x_up,
										knob_pl_y_up		= knob_pl_y_up,
										gs_knob_mat			= gs_knob_mat,
										doorglas_mat		= doorglas_mat,
										doorframe_mat		= doorframe_mat,
										gs_cust_p			= gs_cust_p,
										cabdoortyp_unID		= unID,
					returned_parameters unID

		del 2
	endif

	add valt, -b+fr_th1+cooh, leg_h+0.01
	for k=1 to int (dn/2)
		for s=1 to 2
			call "fa_cabdoortyp" parameters gs_detlevel_3D_m	= gs_detlevel_3D_m,
										gs_full_edit		= 1,
										gs_bevel 			= gs_bevel,
										door_ang			= 0,
										doorOffset			= 0.002,
										door_w				= lffw2/dn,
										door_h				= lffh,
										gs_door_type_m		= gs_door_type_m,
										doorframe_w			= doorframe_w,
										vert_gnum			= vert_gnum,
										hor_gnum			= hor_gnum,
										gs_cont_pen			= gs_cont_pen,
										gs_resol			= gs_resol,
										gs_shadow			= gs_shadow,
										fr_th1				= fr_th1,
										UpperKnob			= 1,
										bKnob				= bDoorKnob,
										bKnobNoneType		= bDoorKnob,
										iNumKnob			= iNumDoorKnob,
										gs_knob_type_m		= gs_knob_type_m,
										gs_knob_p_m			= gs_knob_p_m,
										knob_size			= knob_size,
										knob_pl_x_up		= knob_pl_x_up,
										knob_pl_y_up		= knob_pl_y_up,
										gs_knob_mat			= gs_knob_mat,
										doorglas_mat		= doorglas_mat,
										doorframe_mat		= doorframe_mat,
										gs_cust_p			= gs_cust_p,
										cabdoortyp_unID		= unID,
					returned_parameters unID

			mulx -1
			add  - 2*lffw2/dn, 0, 0
		next s
		del 2+2
		addx lffw2/dn*2
	next k
	del int (dn/2) + 1


	! --- Top drawers ---
	if abs (dwh) > EPS then		!dwh<>0

		if gs_lining_m = LINING_FRONT_BOTH then valt2 = -a/2 + side_overhang + 0.01
		if gs_lining_m = LINING_FRONT_LEFT then valt2 = -a/2 + 0.01
		if gs_lining_m = LINING_FRONT_RIGHT then valt2 = -a/2 + side_overhang + 0.01
		if gs_lining_m = LINING_FRONT then valt2 = -a/2 + 0.01

		xDrawerKnobPosMod = 0
		if iDrawerKnobType > 3 then xDrawerKnobPosMod = drawerKnobSize/2

		add valt2, -b+fr_th1+cooh, leg_h+0.01+lffh
		for i=1 to dnum
			call "fa_cabdoortyp" parameters gs_detlevel_3D_m	= gs_detlevel_3D_m,
											gs_full_edit		= 0,
											door_ang			= 0,
											door_w				= lffw2/dnum,
											door_h				= dwh,
											doorOffset			= 0.002,
											gs_door_type_m		= drawer_m,
											doorframe_w			= drawerframe_w,
											fr_th1				= fr_th1,
											UpperKnob			= 0,
											bKnob				= bDrawerKnob,
											bKnobNoneType		= bDrawerKnob,
											iNumKnob			= iNumDrawerKnob,
											gs_knob_type_m		= iDrawerKnobType,
											gs_knob_p_m			= 1,
											knob_size			= drawerKnobSize,
											knob_pl_x			= lffw2/dnum/2 - xDrawerKnobPos - 0.001 - xDrawerKnobPosMod,
											knob_pl_y			= dwh/2 + zDrawerKnobPos - 0.001,
											gs_knob_mat			= matDrawerKnob,
											vert_gnum			= 0,
											hor_gnum			= 0,
											gs_cont_pen			= gs_cont_pen,
											gs_resol			= gs_resol,
											gs_shadow			= gs_shadow,
											doorglas_mat		= DrawerGlasMat,
											doorframe_mat		= DrawerFrameMat,
											gs_bevel 			= bDrawerBevel,
											cabdoortyp_unID		= unID,
						returned_parameters unID

			if drawer_m < 4 & bDrawerKnob then
				if iNumDrawerKnob = 1 then
					hotspot lffw2/dnum/2 + xDrawerKnobPos, -fr_th1, dwh/2, 						unID, zDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum/2 + xDrawerKnobPos, -fr_th1, -1, 						unID, zDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum/2 + xDrawerKnobPos, -fr_th1, dwh/2 + zDrawerKnobPos, 	unID, zDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum/2,					-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum/2 -1,				-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum/2 + xDrawerKnobPos,	-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,2 		: unID = unID + 1
					unID = unID + 6
				else
					hotspot lffw2/dnum - xDrawerKnobPos-0.08, -fr_th1, dwh/2, 					unID, zDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum - xDrawerKnobPos-0.08, -fr_th1, -1, 						unID, zDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum - xDrawerKnobPos-0.08, -fr_th1, dwh/2 + zDrawerKnobPos, 	unID, zDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum - 0.08,						-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum + 1 - 0.08,					-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum - xDrawerKnobPos -  0.08,	-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum + xDrawerKnobPos+0.08-lffw2/dnum, -fr_th1, dwh/2, 					unID, zDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum + xDrawerKnobPos+0.08-lffw2/dnum, -fr_th1, -1, 						unID, zDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum + xDrawerKnobPos+0.08-lffw2/dnum, -fr_th1, dwh/2 + zDrawerKnobPos, 	unID, zDrawerKnobPos,2 		: unID = unID + 1

					hotspot lffw2/dnum + 0.08 - lffw2/dnum,					-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,1+128 	: unID = unID + 1
					hotspot lffw2/dnum - 1 + 0.08-lffw2/dnum,				-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,3 		: unID = unID + 1
					hotspot lffw2/dnum + xDrawerKnobPos + 0.08-lffw2/dnum,	-fr_th1, dwh/2 + zDrawerKnobPos, 	unID, xDrawerKnobPos,2 		: unID = unID + 1
				endif
			else
				unID = unID + 12
			endif

			addx lffw2/dnum
		next i
		del dnum
		del 1
	endif
	del top

return


! -----------------------------------------------------------------------------
!  for Credenza Simple 1000, 1100
! -----------------------------------------------------------------------------

1000:
	roty 90
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	del 1
	coor 2, -1, -2, -3, -4
	base
	body -1
return

1100:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	base
	body -1
return




