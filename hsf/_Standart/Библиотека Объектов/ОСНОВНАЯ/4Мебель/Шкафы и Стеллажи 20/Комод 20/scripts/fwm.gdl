
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "C293F4EA-D398-408C-BABA-5BB1B8F444BA"
	_startID	= "D8484763-20D3-4156-8529-F0E4EDCD0944"
	_endID		= "3FF33F36-6CB8-42E2-9FD1-7F3FBDAC27CE"
gosub "AccessoryBooleans_FWM"

	_startID	= "3FF33F36-6CB8-42E2-9FD1-7F3FBDAC27CE"
	_endID		= "2EBFDFD1-87C8-4783-A7CE-4F34FFDE03E8"
gosub "2DSymbolStyles_FWM"

	_startID	= "2EBFDFD1-87C8-4783-A7CE-4F34FFDE03E8"
	_endID		= "DE2FA419-2D42-4BB0-A424-FC0CCFB03A49"
gosub "Knobs_Shelves_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
	
		! If gs_cornice_type_m was 0, that equals with bCornice being off, otherwise it is on.
		_bHas = STORED_PAR_VALUE("gs_cornice_type_m", _gs_cornice_type_m)

		if _gs_cornice_type_m = 0 then
			bCornice = 0
		else
			bCornice = 1
		endif
		parameters bCornice = bCornice

		! If gs_leg_type_m was 0, that equals with bBase being off, otherwise it is on.
		_bHas = STORED_PAR_VALUE("gs_leg_type_m", _gs_leg_type_m)

		if _gs_leg_type_m = 0 then
			bBase = 0
		else
			bBase = 1
		endif
		parameters bBase = bBase
	
		actualGuid = _endID
	endif
return

! ==============================================================================
! 2D New Symbol Styles
! ==============================================================================
"2DSymbolStyles_FWM":
	if actualGuid = _startID then
		_gs_detlevel_2D_m	= 0			!invalid value to define
		_iStyle				= -1		!invalid value to define
		_bHas = DELETED_PAR_VALUE ("gs_detlevel_2D_m", _gs_detlevel_2D_m) & \
				STORED_PAR_VALUE ("iStyle", _iStyle)
		
		if _gs_detlevel_2D_m = 1 then iSymbolStyle = 1		! Style 1
		
		if _gs_detlevel_2D_m = 2 then
			if _iStyle = 1 then		iSymbolStyle = 7			! Simple - Style 7
			if _iStyle = 2 then 	iSymbolStyle = 6			! Multidrawer - Style 6
		endif													
		
		if _gs_detlevel_2D_m = 3 then iSymbolStyle = 8		! Style 8	
		if _gs_detlevel_2D_m = 4 then iSymbolStyle = 9		! Style 9
		parameters iSymbolStyle = iSymbolStyle

		actualGuid = _endID
	endif
return

! ==============================================================================
! Knobs_Shelves
! ==============================================================================
"Knobs_Shelves_FWM":
	if actualGuid = _startID then

		_bHas =  STORED_PAR_VALUE ("iDrawerKnobType",_iDrawerKnobType) & \
				 STORED_PAR_VALUE ("gs_knob_type_m",_gs_knob_type_m) & \
				 DELETED_PAR_VALUE ("shn",_iNumShelves)

		bDrawerKnob = 1
		bDoorKnob = 1
		iNumDoorKnob = 1
		iNumDrawerKnob = 1
		if _iDrawerKnobType = 0 then
			bDrawerKnob = 0		!None type
		else
			bDrawerKnob = 1
		endif
		if _gs_knob_type_m = 0 then
			bDoorKnob = 0			!None type
		else
			bDoorKnob = 1
		endif

		parameters 	iNumDoorKnob = iNumDoorKnob,
					iNumDrawerKnob = iNumDrawerKnob,
					bDrawerKnob = bDrawerKnob,
					bDoorKnob = bDoorKnob

		if _iNumShelves = 0 then
			bShelves = 0									!None type
		else
			bShelves = 1
		endif
		iNumShelves = _iNumShelves
		parameters	bShelves = bShelves,
					iNumShelves = iNumShelves

		actualGuid = _endID
	endif
return

