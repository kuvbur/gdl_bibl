
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all


! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle"  1, 2

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	parameters stStyle = stStyle
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------
values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
values "gs_detlevel_3D_m" 2, 1, 0
values "gs_resol" range [3,)

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	if gs_detlevel_3D = `Простой` then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Откл.` then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = `Детальный`
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	lock "vert_gnum","hor_gnum"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif
! -----------------------------------------------------------------------------
! Overhang - Lining
! -----------------------------------------------------------------------------
values{2} "gs_lining_m" LINING_FRONT_BOTH, `Спереди и по Бокам`,
						LINING_FRONT_LEFT, `Спереди и Слева`,
						LINING_FRONT_RIGHT, `Спереди и Справа`,
						LINING_FRONT, `Спереди`

if gs_lining_m = LINING_FRONT then
	lock "side_overhang"
	hideparameter "side_overhang"
endif

! -----------------------------------------------------------------------------
! Door Types
! -----------------------------------------------------------------------------
values "gs_door_type" `Стиль 1`,`Стиль 2`,`Стиль 3`,`Стиль 4`,
					`Стиль 5`,`Стиль 6`,`Стиль 7`,`Стиль 8`,`Стиль 9`,`Стиль 10`,`Спец. панель`
values "gs_door_type_m" 1, 2, 3, 11, 12, 9, 4, 5, 6, 7, 10

dim gs_pt[]
if gs_door_type = `Спец. панель` then
	call "Object_Panel_Collection" parameters returned_parameters gs_pt
	values "gs_cust_p" gs_pt
endif

if GLOB_MODPAR_NAME = "gs_door_type" then
	if gs_door_type = `Стиль 1` 	then gs_door_type_m = 1
	if gs_door_type = `Стиль 2` 	then gs_door_type_m = 2
	if gs_door_type = `Стиль 3` 	then gs_door_type_m = 3
	if gs_door_type = `Стиль 4` 	then gs_door_type_m = 11
	if gs_door_type = `Стиль 5` 	then gs_door_type_m = 12
	if gs_door_type = `Стиль 6` 	then gs_door_type_m = 9
	if gs_door_type = `Стиль 7` 	then gs_door_type_m = 4
	if gs_door_type = `Стиль 8` 	then gs_door_type_m = 5
	if gs_door_type = `Стиль 9` 	then gs_door_type_m = 6
	if gs_door_type = `Стиль 10` 	then gs_door_type_m = 7
	if gs_door_type = `Спец. панель` 	then gs_door_type_m = 10
	parameters gs_door_type_m = gs_door_type_m
else
	if gs_door_type_m = 1	then gs_door_type = `Стиль 1`
	if gs_door_type_m = 2	then gs_door_type = `Стиль 2`
	if gs_door_type_m = 3	then gs_door_type = `Стиль 3`
	if gs_door_type_m = 11	then gs_door_type = `Стиль 4`
	if gs_door_type_m = 12	then gs_door_type = `Стиль 5`
	if gs_door_type_m = 9	then gs_door_type = `Стиль 6`
	if gs_door_type_m = 4	then gs_door_type = `Стиль 7`
	if gs_door_type_m = 5	then gs_door_type = `Стиль 8`
	if gs_door_type_m = 6	then gs_door_type = `Стиль 9`
	if gs_door_type_m = 7	then gs_door_type = `Стиль 10`
	if gs_door_type_m = 10	then gs_door_type = `Спец. панель`
	parameters gs_door_type = gs_door_type
endif

if gs_door_type_m <> 10 then
	hideparameter "gs_cust_p"
	lock "gs_cust_p"
endif
if iStyle = 1 then
	if gs_door_type = `Стиль 1` then
		hideparameter "doorframe_w","hor_gnum","vert_gnum","doorglas_mat"
		lock "doorframe_w","hor_gnum","vert_gnum","doorglas_mat"
	endif
	if gs_door_type = `Стиль 2` or gs_door_type = `Стиль 4` or gs_door_type = `Стиль 7` or gs_door_type = `Стиль 9` then
		hideparameter "hor_gnum","vert_gnum","doorglas_mat"
		lock "hor_gnum","vert_gnum","doorglas_mat"
	endif
	if gs_door_type = `Стиль 6` then
		hideparameter "hor_gnum","vert_gnum"
		lock "hor_gnum","vert_gnum"
	endif
	if  gs_door_type = `Спец. панель` then
		hideparameter "doorframe_w","vert_gnum","hor_gnum","doorframe_mat","doorglas_mat"
		lock "doorframe_w","vert_gnum","hor_gnum","doorframe_mat","doorglas_mat"
	endif
endif

! -----------------------------------------------------------------------------
! Drawer Types
! -----------------------------------------------------------------------------
values "gs_drawer_type" `Стиль 1`, `Стиль 2`, `Стиль 3`, `Стиль 4`
values "drawer_m" 1, 2, 3, 8

if GLOB_MODPAR_NAME = "gs_drawer_type" then
	if gs_drawer_type = `Стиль 1` 	then drawer_m = 1
	if gs_drawer_type = `Стиль 2` 	then drawer_m = 2
	if gs_drawer_type = `Стиль 3` 	then drawer_m = 3
	if gs_drawer_type = `Стиль 4` 	then drawer_m = 8
	parameters drawer_m = drawer_m
else
	if drawer_m = 1	then gs_drawer_type = `Стиль 1`
	if drawer_m = 2	then gs_drawer_type = `Стиль 2`
	if drawer_m = 3	then gs_drawer_type = `Стиль 3`
	if drawer_m = 8	then gs_drawer_type = `Стиль 4`
	parameters gs_drawer_type = gs_drawer_type
endif

if abs (dwh) < EPS then	!!dwh=0
	hideparameter "dnum","gs_drawer_type","drawerframe_w","DrawerGlasMat","DrawerFrameMat"
	lock "dnum","gs_drawer_type","drawerframe_w","DrawerGlasMat","DrawerFrameMat"
endif
if iStyle = 2 then
	hideparameter "gs_door_type_m", "dn", "doorframe_w"
	lock "gs_door_type_m", "dn", "doorframe_w"
	if drawer_m = 1 then
		hideparameter "drawerframe_w","DrawerGlasMat", "vert_gnum", "hor_gnum"
		lock "drawerframe_w","DrawerGlasMat", "vert_gnum", "hor_gnum"
	endif
	if drawer_m = 2 then
		hideparameter "DrawerGlasMat", "vert_gnum", "hor_gnum"
		lock "DrawerGlasMat", "vert_gnum", "hor_gnum"
	endif
	if drawer_m = 8 then
		if iStyle = 2 then
			lock	"gs_knob_p",
			"gs_knob_p_m",
			"knob_size",
			"knob_pl_x_up",
			"knob_pl_y_up",
			"gs_knob_mat",
			"drawerKnobType",
			"iDrawerKnobPlacing",
			"drawerKnobSize",
			"xDrawerKnobPos",
			"zDrawerKnobPos",
			"gs_knob_type_m",
			"matDrawerKnob",
			"vert_gnum",
			"hor_gnum"
			hideparameter	"gs_knob_p",
					"gs_knob_p_m",
					"knob_size",
					"knob_pl_x_up",
					"knob_pl_y_up",
					"gs_knob_mat",
					"iDrawerKnobPlacing",
					"drawerKnobSize",
					"xDrawerKnobPos",
					"zDrawerKnobPos",
					"gs_knob_type_m",
					"matDrawerKnob",
					"vert_gnum",
					"hor_gnum"

		else
			lock "drawerKnobType","matDrawerKnob",
				"DrawerGlasMat","drawerframe_w",
				"vert_gnum","hor_gnum",
				"xDrawerKnobPos","zDrawerKnobPos"
			hideparameter "drawerKnobType","matDrawerKnob",
				"DrawerGlasMat","drawerframe_w",
				"vert_gnum","hor_gnum",
				"xDrawerKnobPos","zDrawerKnobPos"
		endif
	endif
endif
! -----------------------------------------------------------------------------
! Cornice Types
! -----------------------------------------------------------------------------

values{2} "gs_cornice_type_m"	CORN_STYLE_1,		`Стиль 1`,
								CORN_STYLE_2,		`Стиль 2`,
								CORN_STYLE_3,		`Стиль 3`,
								CORN_STYLE_4,		`Стиль 4`

if not(bCornice) then
	hideparameter "gs_cornice_type_m", "gs_lining_m", "cot", "cooh", "side_overhang", "gs_cornice_mat"
	lock "gs_cornice_type_m", "gs_lining_m", "cot", "cooh", "side_overhang", "gs_cornice_mat"
endif

! -----------------------------------------------------------------------------
! Leg Types
! -----------------------------------------------------------------------------
values{2} "gs_leg_type_m" 	LEG_STYLE_1, `Стиль 1`,
							LEG_STYLE_2, `Стиль 2`,
							LEG_STYLE_3, `Стиль 3`,
							LEG_STYLE_4, `Стиль 4`,
							LEG_STYLE_5, `Стиль 5`,
							LEG_STYLE_6, `Стиль 6`

if not(bBase) then
	lock 			"gs_leg_type_m", "leg_h", "leg_w", "gs_leg_mat"
	hideparameter	"gs_leg_type_m", "leg_h", "leg_w", "gs_leg_mat"
endif

! -----------------------------------------------------------------------------
! Knob Types
! -----------------------------------------------------------------------------

values{2} "gs_knob_type_m"	1, `Стиль 1`, 
							2, `Стиль 2`, 
							3, `Стиль 3`, 
							4, `Стиль 4`,
							5, `Стиль 5`,
							6, `Стиль 6`,
							7, `Стиль 7`,
							8, `Стиль 8`

if GLOB_MODPAR_NAME =  "iStyle" | GLOB_MODPAR_NAME =  "stStyle" then
	if iStyle = 2 then
		knob_pl_x_up = 0
		knob_pl_y_up = 0
	else
		knob_pl_x_up = 0.07
		knob_pl_y_up = 0.52
	endif
	parameters	knob_pl_x_up = knob_pl_x_up, knob_pl_y_up = knob_pl_y_up
endif

if not(bDoorKnob) then
	lock "gs_knob_p","gs_knob_p_m","knob_size","knob_pl_x_up","knob_pl_y_up","gs_knob_mat", "iNumDoorKnob", "gs_knob_type_m"
	hideparameter "gs_knob_p","gs_knob_p_m","knob_size","knob_pl_x_up","knob_pl_y_up","gs_knob_mat", "iNumDoorKnob", "gs_knob_type_m"
endif

if not(bDrawerKnob) then
	lock "iNumDrawerKnob", "iDrawerKnobType", "iDrawerKnobPlacing", "drawerKnobSize", "xDrawerKnobPos", "zDrawerKnobPos", "matDrawerKnob"
	hideparameter "iNumDrawerKnob", "iDrawerKnobType", "iDrawerKnobPlacing", "drawerKnobSize", "xDrawerKnobPos", "zDrawerKnobPos", "matDrawerKnob"
endif

! -----------------------------------------------------------------------------
! Drawer Knob Types
! -----------------------------------------------------------------------------
values{2} "iDrawerKnobType"	1, `Стиль 1`, 
							2, `Стиль 2`, 
							3, `Стиль 3`, 
							4, `Стиль 4`,
							5, `Стиль 5`,
							6, `Стиль 6`,
							7, `Стиль 7`,
							8, `Стиль 8`

if iDrawerKnobType = 0 then
	lock	"iDrawerKnobPlacing",
		"drawerKnobSize",
		"xDrawerKnobPos",
		"zDrawerKnobPos",
		"matDrawerKnob"
	hideparameter	"iDrawerKnobPlacing",
				"drawerKnobSize",
				"xDrawerKnobPos",
				"zDrawerKnobPos",
				"matDrawerKnob"
endif

! -----------------------------------------------------------------------------
! 2D Symbol
! -----------------------------------------------------------------------------
if iStyle = 1 then																		! With doors: enable all Symbol Types
	iSymbolStyleEnable = 2**1 + 2**2 + 2**3 + 2**4 + 2**5 + 2**6 + 2**7 + 2**8 + 2**9
else																					! Multidrawer: enable only Symbol Types without door symbol
	iSymbolStyleEnable = 2**1 + 2**2 + 2**3 + 2**6
endif

call "cabSymbolType" parameters 	iSymbolStyle = iSymbolStyle,
									iSymbolStyleEnable = iSymbolStyleEnable

if 	iSymbolStyle <> 8 &\			! Lock Door Opening Angle if the Symbol has no opening doors
	iSymbolStyle <> 9 then

	lock "opa2"
	hideparameter "opa2"
endif

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------
if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if gs_cont_pen = 0 then parameters gs_cont_pen = 4
if gs_fill_pen = 0 then parameters gs_fill_pen = 19

if iStyle = 1 then hideparameter "vdnum", "hdnum"
if iStyle = 2 then hideparameter "gs_door_type", "gs_cust_p", "dn", "gs_bevel", "doorframe_w", "bShelves", "iNumShelves"
if not(bShelves) then lock "iNumShelves"

values "dwh" range [0,zzyzx-cot-leg_h-0.12)
values "dnum" range [1,)
values "vdnum" range [1,)
values "hdnum" range [1,)
values "drawerframe_w" range [0.01,)
values "dn" range [1,)
values "fr_th1" range [0.01,)
values "iNumShelves" range [1,)
values "leg_h" range [0,)
values "leg_w" range (0,)
values "cot" range (0,)
values "vert_gnum" range [0,)
values "hor_gnum" range [0,)
values "doorframe_w" range [0.01,)
values "iNumDrawerKnob" 1,2
values "iNumDoorKnob" 1,2



if bCornice then

	if gs_cornice_type_m = CORN_STYLE_1 then values "cooh" range [0,)
	if gs_cornice_type_m = CORN_STYLE_2 or gs_cornice_type_m = CORN_STYLE_3 then values "cooh" range [1/2*cot,)
	if gs_cornice_type_m = CORN_STYLE_4 then values "cooh" range [3/4*cot,)

	if gs_cornice_type_m = CORN_STYLE_1 then values "side_overhang" range [0,A/2 - 0.02)
	if gs_cornice_type_m = CORN_STYLE_2 or gs_cornice_type_m = CORN_STYLE_3 then values "side_overhang" range [1/2*cot,A/2 - 0.02)
	if gs_cornice_type_m = CORN_STYLE_4 then values "side_overhang" range [3/4*cot,A/2 - 0.02)

endif

dim st_knobplacing[2]
	st_knobplacing[1] = `Вертикальный`
	st_knobplacing[2] = `Горизонтальный`


values{2} "iDrawerKnobPlacing" 	KNOBPLACING_HORIZ, `Горизонтальный`
values "drawerKnobSize"	range [0.045,)
if iDrawerKnobType < 4 then lock "drawerKnobSize"
values "xDrawerKnobPos" range [-lffw2/dnum/2 + 0.02, lffw2/dnum/2 - 0.02]
values "zDrawerKnobPos" range [-dwh/2 + 0.02, dwh/2 - 0.02]


if iStyle = 1 then
	values "gs_knob_p" st_knobplacing
	if GLOB_MODPAR_NAME = "gs_knob_p" then
		for iKnob = 1 to vardim1(st_knobplacing)
			if gs_knob_p = st_knobplacing[iKnob] then
				gs_knob_p_m = iKnob - 1
			endif
		next iKnob
		parameters gs_knob_p_m = gs_knob_p_m
	else
		gs_knob_p = st_knobplacing[gs_knob_p_m+1]
		parameters gs_knob_p = gs_knob_p
	endif
else
	values "gs_knob_p" st_knobplacing[2]
	lock "gs_knob_p"
endif


values "knob_size"	range [0.045,)
if gs_knob_type_m < 4 then lock "gs_knob_p"
if gs_knob_type_m < 4 then lock "knob_size"

if iStyle = 1 then
	values "knob_pl_x_up" range [0.02, lffw2/dn - 0.02]
	values "knob_pl_y_up" range [0.02, zzyzx-cot-dwh-leg_h-2*0.01 - 0.02]
else
	values "knob_pl_y_up" range [-(zzyzx-cot-dwh-leg_h-2*0.01)/vdnum/2 + 0.02, (zzyzx-cot-dwh-leg_h-2*0.01)/vdnum/2 - 0.02]
	if iNumDoorKnob = 1 then
		values "knob_pl_x_up" range [-lffw2/hdnum/2 + 0.02 + knob_size/2 * (gs_knob_type_m > 3), lffw2/hdnum/2 - 0.02 - knob_size/2 * (gs_knob_type_m > 3)]
	else
		values "knob_pl_x_up" range [-lffw2/hdnum/2 + 0.234 + knob_size/2 * (gs_knob_type_m > 3), ]
	endif
endif

if iStyle = 2 then
	lock "gs_bevel", "doorframe_mat", "doorglas_mat"
	hideparameter "gs_bevel", "doorframe_mat", "doorglas_mat"
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif



