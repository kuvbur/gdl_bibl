
actualGUID = FROM_GUID
! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "80622B61-A050-4660-B343-54EA6636258A"
	_startID	= "7F7C135C-A01F-46F9-A957-05CF11154442"
	_endID		= "1F7EDE9D-7E9D-4695-B349-186DC3CE53FA"
gosub "AccessoryBooleans_FWM"

	_startID	= "1F7EDE9D-7E9D-4695-B349-186DC3CE53FA"
	_endID		= "9166588D-58DB-4F25-ABB2-202BC5CFE85B"
gosub "Newparameters_FWM"

	_startID	= "9166588D-58DB-4F25-ABB2-202BC5CFE85B"
	_endID		= "A2CDE4AA-F966-43B2-825F-D7A23455EF99"
gosub "2DSymbolStyles_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
	
		! If gs_cornice_type_m was 0, that equals with bCornice being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_cornice_type_m", _gs_cornice_type_m)
		if _gs_cornice_type_m = 0 then
			bCornice = 0
		else
			bCornice = 1
		endif
		parameters bCornice = bCornice

		! If gs_leg_type_m was 0, that equals with bBase being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_leg_type_m", _gs_leg_type_m)
		if _gs_leg_type_m = 0 then
			bBase = 0
			gs_leg_type_m = 4
			parameters	bBase			= bBase,
						gs_leg_type_m	= gs_leg_type_m
		else
			bBase = 1
			parameters bBase = bBase
		endif
	
		! If gs_knob_type_m was 0, that equals with bKnob being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m)

		if _gs_knob_type_m = 0 then
			bKnob = 0
		else
			bKnob = 1
		endif
		parameters bKnob = bKnob

		actualGuid = _endID
	endif
return

! ==============================================================================
! Newparameters_EnablebBevel_matDoorInset1_matDoorInset2
! ==============================================================================

"Newparameters_FWM":
	if actualGuid = _startID then
	
		! bBevel was a hidden parameter, so it is turned off.
		bBevel = 0
		parameters bBevel = bBevel

		bHas = DELETED_PAR_VALUE("matDoorGlass", _matDoorGlass)
		bHas = DELETED_PAR_VALUE("matDoorMirror", _matDoorMirror)
		bHas = DELETED_PAR_VALUE("matDoorDecor1", _matDoorDecor1)
		bHas = DELETED_PAR_VALUE("matDoorDecor2", _matDoorDecor2)

		if iDoorType = 1 or iDoorType = 2 then	! Double Sliding Door, Triple Sliding Door
			if iDoorStyle = 1 then matDoorInset1 = _matDoorDecor1
			if iDoorStyle = 3 then matDoorInset1 = _matDoorGlass
			if iDoorStyle = 4 then matDoorInset1 = _matDoorDecor1
			if iDoorStyle = 5 then matDoorInset1 = _matDoorGlass
			if iDoorStyle = 9 then matDoorInset1 = _matDoorDecor1
			if iDoorStyle = 10 then matDoorInset1 = _matDoorGlass
			if iDoorStyle = 13 then matDoorInset1 = _matDoorDecor1
			if iDoorStyle = 14 then matDoorInset1 = _matDoorMirror
			if iDoorStyle = 15 then matDoorInset1 = _matDoorMirror
			if iDoorStyle = 16 then matDoorInset1 = _matDoorDecor1
			if iDoorStyle = 17 then matDoorInset1 = _matDoorDecor1
			if iDoorStyle = 18 then matDoorInset1 = _matDoorDecor1
			if iDoorStyle = 14 or iDoorStyle = 15 then
				matDoorInset2 = _matDoorDecor1
			else
				matDoorInset2 = _matDoorDecor2
			endif
		else	! None (Drawer)
			matDrawerInset = _matDoorGlass
		endif

		
		parameters matDoorInset1 = matDoorInset1
		parameters matDoorInset2 = matDoorInset2
		parameters matDrawerInset = matDrawerInset

		actualGuid = _endID
	endif
return

! ==============================================================================
! New Symbol 2D Styles
! ==============================================================================
"2DSymbolStyles_FWM":
	if actualGuid = _startID then
	
		bHas = DELETED_PAR_VALUE("iSymbolType", _iSymbolType)
		bHas = STORED_PAR_VALUE("iDoorType", _iDoorType)
		
		if _iDoorType = 0 then								! No Door
			if 	_iSymbolType = 1 |\
				_iSymbolType = 4 then						! Style 1
				
				iSymbolStyle = 1
			endif
			
			if _iSymbolType = 2 then iSymbolStyle = 2		! Style 2
			if _iSymbolType = 3 then iSymbolStyle = 3		! Style 3
		else												! Double Door Sliding, Triple Door Sliding
			if _iSymbolType = 1 then iSymbolStyle = 1		! Style 1
			if _iSymbolType = 2 then iSymbolStyle = 2		! Style 2
			if _iSymbolType = 3 then iSymbolStyle = 4		! Style 4
			if _iSymbolType = 4 then iSymbolStyle = 7		! Style 7
		endif
		
		parameters 	iSymbolStyle = iSymbolStyle

		actualGuid = _endID
	endif
return
