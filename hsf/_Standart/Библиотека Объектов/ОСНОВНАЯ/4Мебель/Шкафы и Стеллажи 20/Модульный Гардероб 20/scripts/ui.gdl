

gosub "findElements"

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_Root 				= -1
_idxTab					= 1

TABID_StyleAndDim		= _idxTab  : _idxTab = _idxTab + 1
TABID_Module1			= _idxTab  : _idxTab = _idxTab + 1
TABID_Module2			= _idxTab  : _idxTab = _idxTab + 1
TABID_Module3			= _idxTab  : _idxTab = _idxTab + 1
TABID_Module4			= _idxTab  : _idxTab = _idxTab + 1
TABID_Module5			= _idxTab  : _idxTab = _idxTab + 1
TABID_Module6			= _idxTab  : _idxTab = _idxTab + 1
TABID_CorniceAndBase	= _idxTab  : _idxTab = _idxTab + 1
TABID_Door				= _idxTab  : _idxTab = _idxTab + 1
TABID_Drawer			= _idxTab  : _idxTab = _idxTab + 1
TABID_Knob				= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepr			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepr			= _idxTab  : _idxTab = _idxTab + 1
TABID_MinSpace			= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions		= _idxTab  : _idxTab = _idxTab + 1

pageStartY = 5

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Гардеробного Шкафа`,444,266
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDim,	TABID_Root,			`Стиль и Размеры`,	"uiTab_Dimension_flat(1)"
	gosub "StyleAndDimPage"

ui_page TABID_Module1,		TABID_StyleAndDim,	`Модуль 1`,				"uiTab_WardrobeModules_flat(1)"
	gosub "Module1Page"

ui_page TABID_Module2,		TABID_StyleAndDim,	`Модуль 2`,				"uiTab_WardrobeModules_flat(1)"
	gosub "Module2Page"

ui_page TABID_Module3,		TABID_StyleAndDim,	`Модуль 3`,				"uiTab_WardrobeModules_flat(1)"
	gosub "Module3Page"

ui_page TABID_Module4,		TABID_StyleAndDim,	`Модуль 4`,				"uiTab_WardrobeModules_flat(1)"
	gosub "Module4Page"

ui_page TABID_Module5,		TABID_StyleAndDim,	`Модуль 5`,				"uiTab_WardrobeModules_flat(1)"
	gosub "Module5Page"

ui_page TABID_Module6,		TABID_StyleAndDim,	`Модуль 6`,				"uiTab_WardrobeModules_flat(1)"
	gosub "Module6Page"

ui_page TABID_CorniceAndBase,	TABID_Root,		`Карниз и Основание`,		"uiTab_Leg_flat(1)"
	gosub "CorniceAndBasePage"

ui_page TABID_Door,				TABID_Root,		`Двери`,					"uiTab_WardrobeDoor_flat(1)"
	gosub "DoorPage"

ui_page TABID_Drawer,			TABID_Root,		`Ящик`,				"uiTab_Drawer_flat(1)"
	gosub "DrawerPage"

ui_page TABID_Knob,				TABID_Root,		`Ручка`,					"uiTab_Knob_flat(1)"
	gosub "KnobPage"

ui_page TABID_2DRepr,			TABID_Root,		`2D-отображение`,	"uiTab_2DRepr_flat(1)"
	gosub "2DReprPage"

ui_page TABID_3DRepr,			TABID_Root,		`3D-отображение и Покрытия`,	"uiTab_3DRepr_flat(1)"
	gosub "3DReprPage"

ui_page TABID_MinSpace,			TABID_Root,		`Минимальное Пространство`,		"uiTab_MinSpace_flat(1)"
	gosub "MinSpacePage"

ui_page TABID_Descriptions,		TABID_Root,		`Характеристики`,			"uiTab_Descriptions_flat(1)"
	gosub "DescriptionsPage"


! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimPage":
! ------------------------------------------------------------------------------

	pageStartY	= 5
	_yCurr		= pageStartY
	_dy			= 24
	_infHeight 	= 19
	_outfHeight = 15
	_offsetY	= 4

	_infWidth	= 90
	_infWidthShort = 54
	widthInf2 = 37
	x1	= 0
	x4	= 210
	x2	= x4 - _infWidth
	x3	= x4 - _infWidthShort

	x_separator = 220

	x5	= x_separator + 10
	x6	= 315
	x7	= x6 + _infWidth

	x8	= 407
	x9	= x8 + widthInf2

	y_separator	= 166

	x_pict		= 9
	y_pict		= 192


	ui_outfield `Количество Модулей`,	x1, _yCurr,				x3-x1-2,	_outfHeight
	ui_infield{3} "nModule",		x3, _yCurr - _offsetY,	x4-x3,		_infHeight,
		8, "",
		0, 0, 0, 0, 0, 0,
		0, `1`,	1,
		0, `2`,	2,
		0, `3`,	3,
		0, `4`,	4,
		0, `5`,	5,
		0, `6`,	6	ui_tooltip `Количество Модулей`
	_yCurr = _yCurr + _dy


	ui_outfield `Тип Дверей`,	x1, _yCurr,				x2-x1-2,	_outfHeight
	ui_infield{3} "iDoorType",	x2, _yCurr - _offsetY,	x4-x2,		_infHeight,
		8, "",
		0, 0, 0, 0, 0, 0,
		0, stDoorType[1],	2,
		0, stDoorType[2],	1,
		0, stDoorType[3],	0  ui_tooltip `Тип Дверей`
	_yCurr = _yCurr + _dy

	ui_outfield `Тип Корпуса`,	x1, _yCurr,				x2-x1-2,	_outfHeight
	ui_infield{3} "iFrameType",	x2, _yCurr - _offsetY,	x4-x2,		_infHeight,
		8, "",
		0, 0, 0, 0, 0, 0,
		0, stFrameTypes[1],	1,
		0, stFrameTypes[2],	2,
		0, stFrameTypes[3],	3  ui_tooltip `Тип Корпуса`
	_yCurr = _yCurr + _dy

	ui_outfield `Высота Шкафа`,	x1, _yCurr,			x2-x1-2,	_outfHeight
	ui_infield "ZZYZX",			x2, _yCurr - _offsetY,	x4-x2,		_infHeight ui_tooltip `Высота Шкафа`
	_yCurr = _yCurr + _dy

	ui_outfield `Толщина Стенок`,	x1, _yCurr,				x2-x1-2,	_outfHeight
	ui_infield "frameWidth",	x2, _yCurr - _offsetY,	x4-x2,		_infHeight ui_tooltip `Толщина Стенок`
	_yCurr = _yCurr + _dy

	ui_separator x_separator, 0, x_separator, y_separator-3


	_yCurr = pageStartY

	ui_infield{3} "bUniform", x5,	_yCurr - _offsetY,	x9-x5,	_infHeight,
		7, "",
		0, 0, 0, 0, 0, 0,
		"",`Одинаковая Ширина Модулей`, 0,
		"",`Одинаковая Ширина Модулей`, 1
	_yCurr = _yCurr + _dy

	dimensionForm = ""
	qq= REQUEST ("Working_length_unit", "", dimensionForm)

	ui_outfield `Модуль 1`,							x5, _yCurr, x6-x5-2,	_outfHeight
	ui_outfield str(dimensionForm, modulWidth01),	x6, _yCurr, x7-x6,		_outfHeight ui_tooltip `Ширина Модуля 1`

	ui_pict_button ui_function, ``, "uiFunctionWardrobeModules_flat(1)", x8, _yCurr - _offsetY - 1, x9-x8, 22, -TABID_Module1 ! Set GLOB_UI_BUTTON_ID
	_yCurr = _yCurr + _dy


	if nModule > 1 then
		ui_outfield `Модуль 2`,		x5, _yCurr,				x6-x5-2,	_outfHeight
		ui_infield "modulWidth02",	x6, _yCurr - _offsetY,	x7-x6,		_infHeight ui_tooltip `Ширина Модуля 2`

		ui_pict_button ui_function, ``, "uiFunctionWardrobeModules_flat(1)", x8, _yCurr - _offsetY - 1, x9-x8, 22, -TABID_Module2 ! Set GLOB_UI_BUTTON_ID
		_yCurr = _yCurr + _dy
	endif

	if nModule > 2 then
		ui_outfield `Модуль 3`,		x5, _yCurr,				x6-x5-2,	_outfHeight
		ui_infield "modulWidth03",	x6, _yCurr - _offsetY,	x7-x6,		_infHeight ui_tooltip `Ширина Модуля 3`

		ui_pict_button ui_function, ``, "uiFunctionWardrobeModules_flat(1)", x8, _yCurr - _offsetY - 1, x9-x8, 22, -TABID_Module3 ! Set GLOB_UI_BUTTON_ID
		_yCurr = _yCurr + _dy
	endif

	if nModule > 3 then
		ui_outfield `Модуль 4`,		x5, _yCurr,				x6-x5-2,	_outfHeight
		ui_infield "modulWidth04",	x6, _yCurr - _offsetY,	x7-x6,		_infHeight ui_tooltip `Ширина Модуля 4`

		ui_pict_button ui_function, ``, "uiFunctionWardrobeModules_flat(1)", x8, _yCurr - _offsetY - 1, x9-x8, 22, -TABID_Module4 ! Set GLOB_UI_BUTTON_ID
		_yCurr = _yCurr + _dy
	endif

	if nModule > 4 then
		ui_outfield `Модуль 5`,		x5, _yCurr,				x6-x5-2,	_outfHeight
		ui_infield "modulWidth05",	x6, _yCurr - _offsetY,	x7-x6,		_infHeight ui_tooltip `Ширина Модуля 5`

		ui_pict_button ui_function, ``, "uiFunctionWardrobeModules_flat(1)", x8, _yCurr - _offsetY - 1, x9-x8, 22, -TABID_Module5 ! Set GLOB_UI_BUTTON_ID
		_yCurr = _yCurr + _dy
	endif

	if nModule > 5 then
		ui_outfield `Модуль 6`,		x5, _yCurr,				x6-x5-2,	_outfHeight
		ui_infield "modulWidth06",	x6, _yCurr - _offsetY,	x7-x6,		_infHeight ui_tooltip `Ширина Модуля 6`

		ui_pict_button ui_function, ``, "uiFunctionWardrobeModules_flat(1)", x8, _yCurr - _offsetY - 1, x9-x8, 22, -TABID_Module6 ! Set GLOB_UI_BUTTON_ID
		_yCurr = _yCurr + _dy
	endif

	ui_separator 0, y_separator, 444, y_separator

	if nModule = 1 then ui_pict 1, x_pict, y_pict, 424, 56, 1 ! ModularWardrobe_StyleandDim_1Modul.png
	if nModule = 2 then ui_pict 2, x_pict, y_pict, 424, 56, 1 ! ModularWardrobe_StyleandDim_2Modul.png
	if nModule = 3 then ui_pict 3, x_pict, y_pict, 424, 56, 1 ! ModularWardrobe_StyleandDim_3Modul.png
	if nModule = 4 then ui_pict 4, x_pict, y_pict, 424, 56, 1 ! ModularWardrobe_StyleandDim_4Modul.png
	if nModule = 5 then ui_pict 5, x_pict, y_pict, 424, 56, 1 ! ModularWardrobe_StyleandDim_5Modul.png
	if nModule = 6 then ui_pict 6, x_pict, y_pict, 424, 56, 1 ! ModularWardrobe_StyleandDim_6Modul.png

	!Positions for numbers subroutine
	x_pict2 = x_pict + 10 
	y_pict2 = y_pict + 16
	gosub "ModuleNumbersForPictures"

	ui_infield "B", x_pict - 9, y_pict - 22,_infWidth, _infHeight ui_tooltip `Длина`
	_xOffsetForInf = 114
	if nModule = 5 then _xOffsetForInf = 145
	if nModule = 6 then _xOffsetForInf = 177
	ui_infield "A", x_pict + _xOffsetForInf, y_pict - 22,_infWidth, _infHeight ui_tooltip `Общая Ширина`

	return


! ------------------------------------------------------------------------------
"Module1Page":
! ------------------------------------------------------------------------------

	_nEditedModule	= 1
	_actBDivision	= bDivision01
	_strBDivision	= "bDivision01"

	_actInteriorType	= iInteriorType01
	_strInteriorType	= "iInteriorType01"
	_strHangers			= "nHangers01"
	_strWire			= "nWire01"
	_strDrawersFront	= "nDrawersFront01"
	_strDrawers			= "nDrawers01"
	_strShelves			= "nShelves01"

	_actInteriorTypeUnder	= iInteriorTypeUnder01
	_strInteriorTypeUnder	= "iInteriorTypeUnder01"
	_strHangersUnder		= "nHangersUnder01"
	_strWireUnder			= "nWireUnder01"
	_strDrawersFrontUnder	= "nDrawersFrontUnder01"
	_strDrawersUnder		= "nDrawersUnder01"
	_strShelvesUnder		= "nShelvesUnder01"

	_strDivisionHeight		= "divisionHeight01"

	gosub "ModulePages"

return

! ------------------------------------------------------------------------------
"Module2Page":
! ------------------------------------------------------------------------------

	_nEditedModule	= 2
	_actBDivision	= bDivision02
	_strBDivision	= "bDivision02"

	_actInteriorType	= iInteriorType02
	_strInteriorType	= "iInteriorType02"
	_strHangers			= "nHangers02"
	_strWire			= "nWire02"
	_strDrawersFront	= "nDrawersFront02"
	_strDrawers			= "nDrawers02"
	_strShelves			= "nShelves02"

	_actInteriorTypeUnder	= iInteriorTypeUnder02
	_strInteriorTypeUnder	= "iInteriorTypeUnder02"
	_strHangersUnder		= "nHangersUnder02"
	_strWireUnder			= "nWireUnder02"
	_strDrawersFrontUnder	= "nDrawersFrontUnder02"
	_strDrawersUnder		= "nDrawersUnder02"
	_strShelvesUnder		= "nShelvesUnder02"

	_strDivisionHeight		= "divisionHeight02"

	gosub "ModulePages"

return

! ------------------------------------------------------------------------------
"Module3Page":
! ------------------------------------------------------------------------------

	_nEditedModule	= 3
	_actBDivision	= bDivision03
	_strBDivision	= "bDivision03"

	_actInteriorType	= iInteriorType03
	_strInteriorType	= "iInteriorType03"
	_strHangers			= "nHangers03"
	_strWire			= "nWire03"
	_strDrawersFront	= "nDrawersFront03"
	_strDrawers			= "nDrawers03"
	_strShelves			= "nShelves03"

	_actInteriorTypeUnder	= iInteriorTypeUnder03
	_strInteriorTypeUnder	= "iInteriorTypeUnder03"
	_strHangersUnder		= "nHangersUnder03"
	_strWireUnder			= "nWireUnder03"
	_strDrawersFrontUnder	= "nDrawersFrontUnder03"
	_strDrawersUnder		= "nDrawersUnder03"
	_strShelvesUnder		= "nShelvesUnder03"

	_strDivisionHeight		= "divisionHeight03"

	gosub "ModulePages"

return

! ------------------------------------------------------------------------------
"Module4Page":
! ------------------------------------------------------------------------------


	_nEditedModule	= 4
	_actBDivision	= bDivision04
	_strBDivision	= "bDivision04"

	_actInteriorType	= iInteriorType04
	_strInteriorType	= "iInteriorType04"
	_strHangers			= "nHangers04"
	_strWire			= "nWire04"
	_strDrawersFront	= "nDrawersFront04"
	_strDrawers			= "nDrawers04"
	_strShelves			= "nShelves04"

	_actInteriorTypeUnder	= iInteriorTypeUnder04
	_strInteriorTypeUnder	= "iInteriorTypeUnder04"
	_strHangersUnder		= "nHangersUnder04"
	_strWireUnder			= "nWireUnder04"
	_strDrawersFrontUnder	= "nDrawersFrontUnder04"
	_strDrawersUnder		= "nDrawersUnder04"
	_strShelvesUnder		= "nShelvesUnder04"

	_strDivisionHeight		= "divisionHeight04"

	gosub "ModulePages"

return

! ------------------------------------------------------------------------------
"Module5Page":
! ------------------------------------------------------------------------------

	_nEditedModule	= 5
	_actBDivision	= bDivision05
	_strBDivision	= "bDivision05"

	_actInteriorType	= iInteriorType05
	_strInteriorType	= "iInteriorType05"
	_strHangers			= "nHangers05"
	_strWire			= "nWire05"
	_strDrawersFront	= "nDrawersFront05"
	_strDrawers			= "nDrawers05"
	_strShelves			= "nShelves05"

	_actInteriorTypeUnder	= iInteriorTypeUnder05
	_strInteriorTypeUnder	= "iInteriorTypeUnder05"
	_strHangersUnder		= "nHangersUnder05"
	_strWireUnder			= "nWireUnder05"
	_strDrawersFrontUnder	= "nDrawersFrontUnder05"
	_strDrawersUnder		= "nDrawersUnder05"
	_strShelvesUnder		= "nShelvesUnder05"

	_strDivisionHeight		= "divisionHeight05"

	gosub "ModulePages"

return

! ------------------------------------------------------------------------------
"Module6Page":
! ------------------------------------------------------------------------------

	_nEditedModule	= 6
	_actBDivision	= bDivision06
	_strBDivision	= "bDivision06"

	_actInteriorType	= iInteriorType06
	_strInteriorType	= "iInteriorType06"
	_strHangers			= "nHangers06"
	_strWire			= "nWire06"
	_strDrawersFront	= "nDrawersFront06"
	_strDrawers			= "nDrawers06"
	_strShelves			= "nShelves06"

	_actInteriorTypeUnder	= iInteriorTypeUnder06
	_strInteriorTypeUnder	= "iInteriorTypeUnder06"
	_strHangersUnder		= "nHangersUnder06"
	_strWireUnder			= "nWireUnder06"
	_strDrawersFrontUnder	= "nDrawersFrontUnder06"
	_strDrawersUnder		= "nDrawersUnder06"
	_strShelvesUnder		= "nShelvesUnder06"

	_strDivisionHeight		= "divisionHeight06"

	gosub "ModulePages"

return

! ------------------------------------------------------------------------------
"CorniceAndBasePage":
! ------------------------------------------------------------------------------

	call "ui_cabinetBase" parameters all	iBaseClass = 1,	! BASECLASS_BASECORNICE
											iItemClass = 4	! ITEMCLASS_NO_SIDEOVERHANG
return

! ------------------------------------------------------------------------------
"DoorPage":
! ------------------------------------------------------------------------------

	call "ui_cabinetDoor" parameters all	iItemClass = 5	! ITEMCLASS_WARDROBE_MODULAR

return

! ------------------------------------------------------------------------------
"DrawerPage":
! ------------------------------------------------------------------------------

	call "ui_cabinetDrawer" parameters all 	iItemClass = 3,	! ITEMCLASS_WARDROBE_MODULAR
											nDrawersWithFront = nDrawersWithFront

return

! ------------------------------------------------------------------------------
"KnobPage":
! ------------------------------------------------------------------------------

	call "ui_cabinetKnob" parameters all	iKnobClass = 2,	! KNOBCLASS_DOOR
											iItemClass = 0	! ITEMCLASS_OTHER
return

! ------------------------------------------------------------------------------
"2DReprPage":
! ------------------------------------------------------------------------------
	dim stSymbolTypes[7]
		stSymbolTypes[1] = `Стиль 1`
		stSymbolTypes[2] = `Стиль 2`
		stSymbolTypes[3] = `Стиль 3`
		stSymbolTypes[4] = `Стиль 4`
		stSymbolTypes[5] = `Стиль 5`
		stSymbolTypes[6] = `Стиль 6`
		stSymbolTypes[7] = `Стиль 7`

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 13,
													stSymbolTypes 		= stSymbolTypes,
													iSymbolStyle 		= iSymbolStyle,
													bOpeningAngle		= 0,
													bShowHangersin2D	= 1,
													bHangers			= bHangers,
													iFillListID			= TABID_2DRepr
return

! ------------------------------------------------------------------------------
"3DReprPage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_cabinet_mat"
		SurfItemID = SurfItemID + 1
		if  iDoorType <> 0 and (iDoorStyle <> 1 or iDoorStyle <> 8 or iDoorStyle <> 10 or iDoorStyle <> 99) then
			surfacesArray[SurfItemID]	= "matDoorFrame"
			SurfItemID = SurfItemID + 1
		endif
		if iDoorType <> 0 and (iDoorStyle = 3 or iDoorStyle = 5 or iDoorStyle = 7 or iDoorStyle = 9 or iDoorStyle = 12) then
			surfacesArray[SurfItemID]	= "matDoorGlass"
			SurfItemID = SurfItemID + 1
		endif
		if iDoorType <> 0 and (iDoorStyle = 14 or iDoorStyle = 15) then
			surfacesArray[SurfItemID]	= "matDoorInset1"
			SurfItemID = SurfItemID + 1
		endif
		if iDoorType <> 0 and (iDoorStyle <> 3 and iDoorStyle <> 5 and iDoorStyle <> 7 and iDoorStyle <> 9 and iDoorStyle <> 12) then
			if iDoorStyle = 14 or iDoorStyle = 15 then
				surfacesArray[SurfItemID]	= "matDoorInset2"
			else
				surfacesArray[SurfItemID]	= "matDoorInset1"
			endif
			SurfItemID = SurfItemID + 1
		endif
		if iDoorType <> 0 and (iDoorStyle > 15) then
			surfacesArray[SurfItemID]	= "matDoorDecor2"
			SurfItemID = SurfItemID + 1
		endif
		if iDoorType <> 0 and bKnob then
			surfacesArray[SurfItemID]	= "gs_knob_mat"
			SurfItemID = SurfItemID + 1
		endif
		if iFrameType <> FRAME_WIRE and nShelves > 0 then
			surfacesArray[SurfItemID]	= "gs_shelf_mat"
			SurfItemID = SurfItemID + 1
		endif
		if iFrameType = FRAME_WIRE then
			surfacesArray[SurfItemID]	= "matSteelShelf"
			SurfItemID = SurfItemID + 1
		endif
		if nDrawersWithFront > 0 and nDrawers > 0 then
			surfacesArray[SurfItemID]	= "gs_drawer_mat"
			SurfItemID = SurfItemID + 1
		endif
		if iFrameType = FRAME_WIRE then
			surfacesArray[SurfItemID]	= "matDrawerInset"
			SurfItemID = SurfItemID + 1
		endif
		if nHangers > 0 then
			surfacesArray[SurfItemID]	= "gs_hanger_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bCornice then
			surfacesArray[SurfItemID]	= "gs_cornice_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bBase then
			surfacesArray[SurfItemID]	= "gs_leg_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													bShowResol			= 1,
													gs_shadow			= gs_shadow,
													gs_resol			= gs_resol,
													iSurfaceListID		= TABID_3DRepr
return

! ------------------------------------------------------------------------------
"MinSpacePage":
! ------------------------------------------------------------------------------

	x_pict		= 135
	y_pict		= 65
	_infWidth	= 90
	_infHeight 	= 19


	ui_pict 15, x_pict, y_pict, 141, 144, 1 !modularWardrobe_MinSpace.png

	ui_infield "A",			x_pict + 20,	y_pict - 22, _infWidth, _infHeight ui_tooltip `Общая Ширина`
	ui_infield "B",			x_pict + 143,	y_pict + 23, _infWidth, _infHeight ui_tooltip `Длина`
	ui_infield "MSFront",	x_pict + 143,	y_pict + 87, _infWidth, _infHeight ui_tooltip `Минимальное Пространство Спереди`

return

! ------------------------------------------------------------------------------
"DescriptionsPage":
! ------------------------------------------------------------------------------

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return

! ------------------------------------------------------------------------------
"ModulePages":
! ------------------------------------------------------------------------------

	pageStartY	= 30
	_yCurr		= pageStartY
	_dy			= 24
	_infHeight 	= 19
	_outfHeight = 15
	_offsetY	= 4

	_infWidth	= 115
	_infWidthShort = 55
	x1	= 0
	x4	= 290
	x2	= x4 - _infWidth
	x3	= x4 - _infWidthShort

	x_separator = 297

	x_pict		= 397
	y_pict		= 19

	y_separator	= 190

	x_pict2		= 19
	y_pict2		= 202

	! --- Flag for outfields
	_outfFlag = 0
	if nModule < _nEditedModule then
		_outfFlag = 4
	endif 

	ui_infield{4} _strBDivision, x1, _yCurr - _offsetY, x4 - x1, _infHeight,
		7, "",
		0, 0, 0, 0, 0, 0,
		0, `Нижнее Отделение`, 0,
		0, `Нижнее Отделение`, 1
	_yCurr = _yCurr + _dy
	
	if _actBDivision then
		_stDivisionType = `Тип Верхнего Отделения`
	else
		_stDivisionType = `Тип`
	endif

	ui_outfield		_stDivisionType,	x1, _yCurr,				x2-x1-2,	_outfHeight, _outfFlag
	ui_infield{4}	_strInteriorType,	x2, _yCurr - _offsetY,	x4-x2,		_infHeight,
			8, "", 6, 1, 0, 0, 0, 0,
			1, stInteriorType[1], 5,
			2, stInteriorType[2], 4,
			3, stInteriorType[3], 3,
			4, stInteriorType[4], 2,
			5, stInteriorType[5], 1,
			6, stInteriorType[6], 0   ui_tooltip _stDivisionType
	_yCurr = _yCurr + _dy

		if _actInteriorType = INTERIOR_HANGER then
			ui_outfield `Количество Штанг`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strHangers,		x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Штанг`
		endif
		if _actInteriorType = INTERIOR_WIRE then
			ui_outfield `Количество Сетчатых Корзин`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strWire,				x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Сетчатых Корзин`
		endif
	
		if _actInteriorType = INTERIOR_DRAWERFRONT then
			ui_outfield `Количество Ящиков с Фасадами`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strDrawersFront,				x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Ящиков с Фасадами`
		endif
	
		if _actInteriorType = INTERIOR_DRAWER then
			ui_outfield `Количество Ящиков`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strDrawers,		x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Ящиков`
		endif
	
		if _actInteriorType = INTERIOR_SHELF then
			ui_outfield `Кол-во Полок`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strShelves,		x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Кол-во Полок`
		endif

	
	_yCurr = _yCurr + _dy

	if _actBDivision then
		ui_outfield `Тип Нижнего Отделения`, x1 , _yCurr, x2-x1-2, _outfHeight, _outfFlag
		ui_infield{4} _strInteriorTypeUnder, x2, _yCurr - _offsetY, x4-x2, _infHeight,
				8, "", 6, 1, 0, 0, 0, 0,
				1, stInteriorType[1], 5,
				2, stInteriorType[2], 4,
				3, stInteriorType[3], 3,
				4, stInteriorType[4], 2,
				5, stInteriorType[5], 1,
				6, stInteriorType[6], 0   ui_tooltip `Тип Нижнего Отделения`
		_yCurr = _yCurr + _dy

		if _actInteriorTypeUnder = INTERIOR_HANGER then
			ui_outfield `Количество Штанг`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strHangersUnder,	x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Штанг`
		endif
		if _actInteriorTypeUnder = INTERIOR_WIRE then
			ui_outfield `Количество Сетчатых Корзин`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strWireUnder,		x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Сетчатых Корзин`
		endif
	
		if _actInteriorTypeUnder = INTERIOR_DRAWERFRONT then
			ui_outfield `Количество Ящиков с Фасадами`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strDrawersFrontUnder,		x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Ящиков с Фасадами`
		endif
	
		if _actInteriorTypeUnder = INTERIOR_DRAWER then
			ui_outfield `Количество Ящиков`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strDrawersUnder,	x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Количество Ящиков`
		endif
	
		if _actInteriorTypeUnder = INTERIOR_SHELF then
			ui_outfield `Кол-во Полок`,	x1, _yCurr,				x3-x1-2,	_outfHeight, _outfFlag
			ui_infield{4} _strShelvesUnder,	x3, _yCurr - _offsetY,	x4-x3,		_infHeight ui_tooltip `Кол-во Полок`
		endif

	endif

	ui_separator x_separator, 0, x_separator, 187

	ui_separator 0, y_separator, 444, y_separator

	ui_pict 7, x_pict, y_pict, 46, 139, 1 !ModularWardrobe_DivHeight.png
	ui_infield{4} _strDivisionHeight, x_pict-93, y_pict + 103, 90, _infHeight ui_tooltip `Высота Нижнего Отделения`


	gosub "PictOnModulePage"

	gosub "ModuleNumbersForPictures"

return

! ------------------------------------------------------------------------------
"PictOnModulePage":
! ------------------------------------------------------------------------------

	if _nEditedModule = 1 then
		ui_pict 9, x_pict2, y_pict2, 6, 36, 1 !modularWardrobe_start_active.png
	else
		ui_pict 8, x_pict2, y_pict2, 6, 36, 1 !modularWardrobe_start.png
	endif
	
	for iModule = 1 to nModule
		if iModule = nModule then
			if iModule = _nEditedModule then
				ui_pict 14,		x_pict2 + 6 + (iModule-1)*68, y_pict2, 68, 36, 1 !modularWardrobe_end_active.png
			else
				ui_pict 13,		x_pict2 + 6 + (iModule-1)*68, y_pict2, 68, 36, 1 !modularWardrobe_end.png
			endif
		else
			if iModule = _nEditedModule then
				ui_pict 11,		x_pict2 + 6 + (iModule-1)*68, y_pict2, 68, 36, 1 !modularWardrobe_middle_active.png
			else
				if iModule + 1 = _nEditedModule then
					ui_pict 12,	x_pict2 + 6 + (iModule-1)*68, y_pict2, 68, 36, 1 !modularWardrobe_middle_nextactive.png
				else
					ui_pict 10,	x_pict2 + 6 + (iModule-1)*68, y_pict2, 68, 36, 1 !modularWardrobe_middle.png
				endif
			endif
	
		endif
	next iModule

return

! ------------------------------------------------------------------------------
"ModuleNumbersForPictures":
! ------------------------------------------------------------------------------

	for iModuleforNum = 1 to nModule
		ui_outfield str(iModuleforNum, 1, 0), x_pict2 + 34 + (iModuleforNum - 1) * 68, y_pict2 + 40, 20, _outfHeight
	next iModuleforNum

return

