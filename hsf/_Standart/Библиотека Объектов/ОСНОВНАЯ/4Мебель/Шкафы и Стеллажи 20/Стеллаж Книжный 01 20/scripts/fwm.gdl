
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "82B3146C-E907-40AD-9747-B0B27AE36411"
	_startID	= "FB65177B-07D5-4084-AB5C-73700EC2840B"
	_endID		= "AEA7D768-1C76-4CCB-AE2B-4D581EFAD4CC"
gosub "AccessoryBooleans_FWM"

	_startID	= "AEA7D768-1C76-4CCB-AE2B-4D581EFAD4CC"
	_endID		= "224544BB-A334-4F53-92AE-780D354FA5A5"

gosub "newparameters_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
	
		! If gs_cornice_type_m was 0, that equals with bCornice being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_cornice_type_m", _gs_cornice_type_m)

		if _gs_cornice_type_m = 0 then
			bCornice = 0
		else
			bCornice = 1
		endif
		parameters	bCornice = bCornice
	
		! If gs_leg_type_m was 0, that equals with bBase being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_leg_type_m", _gs_leg_type_m)

		if _gs_leg_type_m = 0 then
			bBase = 0
			gs_leg_type_m = 4
			parameters	bBase 			= bBase,
						gs_leg_type_m	= gs_leg_type_m
		else
			bBase = 1
			parameters bBase = bBase
		endif

		actualGuid = _endID
	endif
return

! ==============================================================================
! New Parameters_bShelves_bVerticalPanels_HorizontalFrameThickness
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID then
		! If No. of Shelves was 0, then bShelves is off, otherwise it is on.
		bHas = DELETED_PAR_VALUE ("shn", _shn)
		if bHas then
			if abs(_shn) < EPS then
				bShelves = 0
			else
				bShelves = 1
			endif
		parameters bShelves = bShelves

		iNumShelves = _shn
		parameters iNumShelves = iNumShelves

		endif

		! If No. of Vertical Panels was 0, then bVerticalPanels is off, otherwise it is on.
		bHas = DELETED_PAR_VALUE ("vpn", _vpn)
		if bHas then
			if abs(_vpn) < EPS then
				bVerticalPanels = 0
			else
				bVerticalPanels = 1
			endif
		parameters bVerticalPanels = bVerticalPanels

		iNumVertPanels = _vpn
		parameters iNumVertPanels = iNumVertPanels

		endif

		! the Thickness of Horizontal Frame Panel is a new parameter,
		! it gets the Thickness of the Shelf as default.
		bHas = DELETED_PAR_VALUE ("sht", _sht)
		if bHas then
			horFrameThk = _sht
			parameters horFrameThk = horFrameThk
		endif

		actualGUID = _endID
	endif
return
