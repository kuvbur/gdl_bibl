
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 						=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_CorniceAndBase			= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Книжного Стеллажа`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_CorniceAndBase, TABID_StyleAndDimensions, `Карниз и Основание`, "uiTab_Leg_flat(1)"
	gosub "CorniceAndBasepage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "MinimalSpacepage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Leg_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37
	xPict = 126
	yPict = 80
	PictWidth = 222 
	PictHeight = 164
	_outfFlag = 0
	x1 = 0
	x2 = 160
	x3 = 231
	x4 = xPict - 93
	x5 = xPict - 3
	x6 = xPict + 105
	x7 = xPict + PictWidth +3
	dy1 = 24
	dy2 = 40

	_yCurr = pageStartY
	ui_infield{3} "bShelves", x1, _yCurr, x2 - 3, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Полки`, 0,
					"", `Полки`, 1
	ui_infield "iNumShelves", x2, _yCurr, 54, _infHeight  ui_tooltip `Кол-во Полок`
	ui_infield{3} "bCornice", x3, _yCurr, 210, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Карниз`, 0,
					"", `Карниз`, 1

	_yCurr = _yCurr + dy1
	ui_infield{3} "bVerticalPanels", x1, _yCurr, x2 - 3, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Вертикальные Панели`, 0,
					"", `Вертикальные Панели`, 1
	ui_infield "iNumVertPanels", x2, _yCurr, 54, _infHeight ui_tooltip `Количество Вертикальных Панелей`
	ui_infield{3} "bBase", x3, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Основание`, 0,
					"", `Основание`, 1

	ui_pict_button ui_function, "", "uiFunctionLeg_flat(1)", 444 - widthInf2, _yCurr-1, widthInf2, 22, -TABID_CorniceAndBase ! Set GLOB_UI_BUTTON_ID

	_yCurr = _yCurr + dy1
	ui_separator x1, _yCurr, 444, _yCurr

	ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1

	_yCurr = yPict - _infHeight - 3
	ui_infield "B", x5, _yCurr, 90, _infHeight ui_tooltip `Размер 2`
	ui_infield "A", x6, _yCurr, 90, _infHeight ui_tooltip `Размер 1`

	_yCurr = yPict + 11
	ui_infield "cot", x4, _yCurr, 90, _infHeight ui_tooltip `Толщина Карниза`

	_yCurr = _yCurr + dy2
	ui_infield "shelfThk", x4, _yCurr, 90, _infHeight ui_tooltip `Толщина Полок`

	_yCurr = _yCurr + dy2

	dimensionForm = ""
	 qq= REQUEST ("Working_length_unit", "", dimensionForm)

	if not(bShelves) then _outfFlag = 4
	ui_outfield str(dimensionForm, shelfThk), x4, _yCurr, 90, _outfHeight, _outfFlag + 1 ui_tooltip `Толщина Полок`

	_yCurr = _yCurr + dy2
	ui_outfield str(dimensionForm, shelfThk), x4, _yCurr, 90, _outfHeight, _outfFlag + 1 ui_tooltip `Толщина Полок`

	_outfFlag = 4
	_yCurr = _yCurr + dy1 - 3
	ui_infield "bpt", x4, _yCurr, 90, _infHeight ui_tooltip `Толщина Задней Панели`

	_yCurr = yPict + 70
	ui_infield "zzyzx", x7, _yCurr, 90, _infHeight ui_tooltip `Общая Высота`

	_yCurr = _yCurr + dy2 * 2
	ui_infield "spt", x7, _yCurr, 90, _infHeight ui_tooltip `Толщина Боковых Стенок`

	_yCurr = yPict + PictHeight + 4
	ui_infield "vpt", x6, _yCurr - 4, 90, _infHeight ui_tooltip `Толщина Внутренних Перегородок`

	ui_outfield str(dimensionForm, spt), x6 - 100, _yCurr, 90, _outfHeight, 1 ui_tooltip `Толщина Боковых Стенок`


return

! ------------------------------------------------------------------------------
"CorniceAndBasepage":
! ------------------------------------------------------------------------------
	call "ui_cabinetBase" parameters all	iBaseClass = 1,	! BASECLASS_BASECORNICE
											iItemClass = 4	! ITEMCLASS_NO_SIDEOVERHANG
return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	dim stSymbolTypes[3]
		stSymbolTypes[1] = `Стиль 1`
		stSymbolTypes[2] = `Стиль 2`
		stSymbolTypes[3] = `Стиль 3`

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 13,
													stSymbolTypes 		= stSymbolTypes,
													iSymbolStyle 		= iSymbolStyle,
													bOpeningAngle		= 0,
													iFillListID			= TABID_2DRepresentation
return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
 	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "spmat"
		SurfItemID = SurfItemID + 1
		surfacesArray[SurfItemID]	= "bpmat"
		SurfItemID = SurfItemID + 1
		if bVerticalPanels then 
			surfacesArray[SurfItemID]	= "vpmat"
			SurfItemID = SurfItemID + 1
		endif
		if bShelves then
			surfacesArray[SurfItemID]	= "shmat"
			SurfItemID = SurfItemID + 1
		endif
		if bCornice then 
			surfacesArray[SurfItemID]	= "gs_cornice_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bBase then
			surfacesArray[SurfItemID]	= "gs_leg_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3DRepresentation
return

! ------------------------------------------------------------------------------
"MinimalSpacepage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	xPict = 142
	yPict = 61
	PictWidth = 141 
	PictHeight = 144
	x1 = xPict + 29
	x2 = xPict + PictWidth + 3

	ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1
	
	_yCurr = yPict - _infHeight - 3
	ui_infield "A", x1, _yCurr, 90, _infHeight ui_tooltip `Размер 1`

	_yCurr = yPict + 23
	ui_infield "B", x2, _yCurr, 90, _infHeight ui_tooltip `Размер 2`

	_yCurr = _yCurr + 65
	ui_infield "MSFront", x2, _yCurr, 90, _infHeight ui_tooltip `Минимальное Пространство Спереди`
return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return

