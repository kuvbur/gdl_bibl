
if gs_detlevel_3D_m = 0 then END

if not(gs_shadow) then SHADOW OFF

pen gs_cont_pen


! --- Vertical Frame -----------------------------------------------------------
dist_fr=(a - (num_fr * frw)) / (num_fr - 1)

MATERIAL frmat
for k2 = 1 to 2
	for k1 = 0 to num_fr-1
		block frw, frw, zzyzx-sht
		addx dist_fr + frw
	next k1
	del (num_fr)
	addy (b-frw)
next k2
del 2

! --- Frame Members ------------------------------------------------------------

MATERIAL fmmat
zdist_sh =(zzyzx - bshelf_h)/ (num_sh-1)			!distance between shelves
for k4 = 0 to num_sh-1
	addz bshelf_h-fmw-sht
	for k3 = 0 to num_fr-1
		addy frw
		block frw, b-(2*frw), fmw    				!y direction
		del 1
		addx dist_fr+frw
	next k3
	del (num_fr)
	del 1

	addz bshelf_h-fmw-sht
	for k6 = 1 to 2
		for k5 = 0 to num_fr-2
			addx frw
			block dist_fr, frw, fmw    				!x direction
			del 1
			addx dist_fr+frw
		next k5
		del (num_fr-1)
		addy (b-frw)
	next k6
	del 2
	del 1
	addz zdist_sh
next k4
del (num_sh)

! --- Shelves ------------------------------------------------------------

MATERIAL shmat

!Top shelf

addz zzyzx-sht
if benchtype_m = 0 then
	block a, b, sht
	gosub 2000
else
	ntop=(b)/frw
	num_btop=INT(ntop/2)
	dtop=((b)-(num_btop+1)*frw)/(num_btop)

	for k10=0 to (num_btop)
		block a,frw,sht    						!x direction
		gosub 2000
		addy dtop+frw
	next k10
	del (num_btop)
	del 1
endif
del 1

!Shelves

for k7 = 1 to (num_sh-1)
	addz bshelf_h-sht
	if benchtype_m = 0 then						!flat
		if num_fr>2 then
			gosub "flatshelf1"
		else
			gosub "flatshelf2"
		endif
		gosub 2000
	else										!slatted
		n = (b-2*frw)/frw
		num_b = INT(n/2)
		d = ((b-2*frw)-num_b*frw)/(num_b+1)

		gosub "slattedside"
		gosub "slattedmiddle"
	endif
	del 1
	addz zdist_sh
next k7
del (num_sh-1)

END

"flatshelf1":
	for n=1 to (num_fr-2)
		PUT n*(dist_fr+frw),	0,		15
		PUT n*(dist_fr+frw),	frw,	15
		PUT n*(dist_fr+frw)+frw,frw,	15
		PUT n*(dist_fr+frw)+frw,0,		15
	next n

	for n=1 to (num_fr-2)
		PUT a-n*(dist_fr+frw),		b,		15
		PUT a-n*(dist_fr+frw),		b-frw,	15
		PUT a-(n*(dist_fr+frw)+frw),b-frw,	15
		PUT a-(n*(dist_fr+frw)+frw),b,		15
	next n

	prism_ 13+2*(num_fr-2)*4,sht,
			0,		frw,	15,
			frw,	frw,	15,
			frw,	0,		15,
			GET (NSP/2),
			a-frw,	0,		15,
			a-frw,	frw,	15,
			a,		frw,	15,
			a,		b-frw,	15,
			a-frw,	b-frw,	15,
			a-frw,	b,		15,
			GET (NSP),
			frw,	b,		15,
			frw,	b-frw,	15,
			0,		b-frw,	15,
			0,		frw,	-1

return

"flatshelf2":
	prism_ 13,		sht,
			0,		frw,	15,
			frw,	frw,	15,
			frw,	0,		15,
			a-frw,	0,		15,
			a-frw,	frw,	15,
			a,		frw,	15,
			a,		b-frw,	15,
			a-frw,	b-frw,	15,
			a-frw,	b,		15,
			frw,	b,		15,
			frw,	b-frw,	15,
			0,		b-frw,	15,
			0,		frw,	-1
return

"slattedside":
	for side=1 to 2
		for polc=0 to num_fr-2
			addx frw
			block dist_fr, frw, sht    !!x direction
			gosub 2000
			del 1
			addx dist_fr+frw
		next polc
		del (num_fr-1)
		addy (b-frw)
	next side
	del 2
return

"slattedmiddle":
	for mid=1 to num_b
		addy frw+d
		block a,frw,sht    !!x direction
		gosub 2000
		del 1
		addy frw+d
	next mid
	del num_b
return


2000:
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	base
	body -1
return
