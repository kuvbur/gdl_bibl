
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

if bLeg then
	if iLegType = LEG_RECT OR iLegType = LEG_TOE then
		lock "gs_resol"
		hideparameter "gs_resol"
	endif

	if iLegType = LEG_TOE then
		lock "wBase"
		hideparameter "wBase"
	endif

	if GLOB_MODPAR_NAME = "iLegType" then
		if iLegType = LEG_RECT or iLegType = LEG_ROUND then
			wBase = 0.04
			parameters wBase = wBase
		else	!iLegType = LEG_LEG
			wBase = 0.08
			parameters wBase = wBase
		endif
	endif
else
	lock "gs_legHeight", "gs_resol", "gs_leg_mat","wBase", "iLegType"
	hideparameter "gs_resol", "gs_leg_mat"
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_shadow"
	hideparameter "gs_shadow"
endif

values "gs_resol" range [3, )

values "gs_legHeight" range [0.05, )

values "gs_nRow" range [1,)
values "gs_nCol" range [1,)

values "wBase" range (0,min(a/gs_nCol/2, b/3, 0.2))

values "gs_frameWidth" range [0.01,)
values "gs_rowHeight" range [gs_frameWidth + 0.01,)
values "gs_colWidth" range [gs_frameWidth + 0.01,)
values{2} "iLegType" LEG_TOE,	`Основание`,
						LEG_RECT,	`Прямоугольные`,
						LEG_ROUND,	`Круглые`,
						LEG_LEG,	`Ножки`

! -----------------------------------------------------------------------------
! 2D - 3D detail
! -----------------------------------------------------------------------------

values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	if gs_detlevel_3D = `Откл.`		then gs_detlevel_3D_m = 0
	if gs_detlevel_3D = `Простой`	then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Детальный`	then gs_detlevel_3D_m = 2
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = `Детальный`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! Use only Symbol Types without door or drawer symbol
iSymbolStyleEnable = 2**1 + 2**2 + 2**3
call "cabSymbolType" parameters 	iSymbolStyle = iSymbolStyle,
									iSymbolStyleEnable = iSymbolStyleEnable


! -----------------------------------------------------------------------------
! Linking dimensions
! -----------------------------------------------------------------------------

values "A" range ((gs_nCol + 1) * gs_frameWidth,)
values "B" range (0,)
values "ZZYZX" range ((gs_nRow + 1) * gs_frameWidth,)

bLockFat=0
for i = 1 to gs_nRow - 1
	if gs_firstShelf[i]<>0 then bLockFat=1
next i
if bLockFat then
	bFatFrame=0
	parameters bFatFrame=bFatFrame
	lock "bFatFrame"
endif

if GLOB_MODPAR_NAME = "A" then
	gs_colWidth = (A-gs_frameWidth-bFatFrame*2*gs_frameWidth) / gs_nCol
	parameters gs_colWidth = gs_colWidth
else
	A = gs_nCol*gs_colWidth+gs_frameWidth+bFatFrame*2*gs_frameWidth
	parameters A = A
endif

if GLOB_MODPAR_NAME = "ZZYZX" then
	gs_rowHeight = (ZZYZX-legHeight-gs_frameWidth-bFatFrame*2*gs_frameWidth) / gs_nRow
	parameters gs_rowHeight = gs_rowHeight
else
	ZZYZX = gs_nRow * gs_rowHeight+gs_frameWidth+bFatFrame*2*gs_frameWidth+legHeight
	parameters ZZYZX = ZZYZX
endif


! -----------------------------------------------------------------------------
! Parameters for hotspot editing
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "gs_nRow" OR GLOB_MODPAR_NAME = "gs_nCol" then
	dim gs_firstShelf[]
	dim gs_lastShelf[]

	for i = 1 to gs_nRow - 1
		gs_firstShelf[i] = 0
		gs_lastShelf[i] = 0
	next i

	parameters gs_firstShelf = gs_firstShelf
	parameters gs_lastShelf = gs_lastShelf
endif
if GLOB_MODPAR_NAME = "gs_firstShelf" then
	for i = 1 to vardim1(gs_firstShelf)
		parameters	gs_firstShelf[i] = gs_firstShelf[i]
	next i
endif
if GLOB_MODPAR_NAME = "gs_lastShelf" then
	for i = 1 to vardim1(gs_lastShelf)
		parameters	gs_lastShelf[i] = gs_lastShelf[i]
	next i
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif


