
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "A28434D7-215A-483F-8256-2C3B314F2F1A"
	_startID	= "87FFF46A-1F64-4106-91AB-291AADB8F16F"
	_endID		= "2D84C4B7-F125-4AE5-AF5B-7B3EFCF0307E"
gosub "2DSymbolStyles_FWM"

	_startID	= "2D84C4B7-F125-4AE5-AF5B-7B3EFCF0307E"
	_endID		= "9673DF75-ACD0-481B-9DAE-9BBD7C517D30"
gosub "newparameters_FWM"

	_startID	= "9673DF75-ACD0-481B-9DAE-9BBD7C517D30"
	_endID		= "30D6B646-8D8B-474E-A0A6-0BB1753EAB94"
gosub "AccessoryBooleans_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! New Symbol 2D Styles
! ==============================================================================
"2DSymbolStyles_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
	
		iSymbolStyle = 6 							! Style 6
		parameters iSymbolStyle = iSymbolStyle

		actualGuid = _endID
	endif
return

! ==============================================================================
! newparameters_gs_knob_p_m
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID | actualGuid = _autoID then

		! Knob Placing is not a new parameter,
		! it was only used as 'Horizontal' before.
		gs_knob_p_m = 1
		parameters gs_knob_p_m = gs_knob_p_m

		actualGuid = _endID
	endif
return

! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID then

		! If gs_knob_type_m was 0, that equals with bKnob being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m)

		if _gs_knob_type_m = 0 then
			bKnob = 0
		else
			bKnob = 1
		endif
		parameters bKnob = bKnob

		actualGuid = _endID
	endif
return
