
! Wall Cabinet

IF gs_shadow=0 THEN SHADOW OFF
PEN gs_cont_pen

!!* 3D Hotspot ************************************
unID=1

!* Total Height

hotspot	0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	0, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	A, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	A, 0, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	0, B, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	0, B, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	0, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

hotspot	A, B, 0, unID, zzyzx, 1+128 : unID=unID+1
hotspot	A, B, -1, unID, zzyzx, 3+128 : unID=unID+1
hotspot	A, B, zzyzx, unID, zzyzx, 2 : unID=unID+1

!!* Leg Height

hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), cooh, 0, unID, bs_h, 1 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), cooh, -1, unID, bs_h, 3+128 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), cooh, bs_h, unID, bs_h, 2 : unID=unID+1

hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), cooh, 0, unID, bs_h, 1 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), cooh, -1, unID, bs_h, 3+128 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), cooh, bs_h, unID, bs_h, 2 : unID=unID+1

hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), B, 0, unID, bs_h, 1 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), B, -1, unID, bs_h, 3+128 : unID=unID+1
hotspot	A-side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_RIGHT), B, bs_h, unID, bs_h, 2 : unID=unID+1

hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), B, 0, unID, bs_h, 1 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), B, -1, unID, bs_h, 3+128 : unID=unID+1
hotspot	side_overhang*(gs_lining_m = LINING_SIDE_BOTH | gs_lining_m = LINING_SIDE_LEFT), B, bs_h, unID, bs_h, 2 : unID=unID+1

!!!**************************************************

lffh=zzyzx-cot-bs_h-2*0.01      ! Leafs full height

IF gs_lining_m = LINING_SIDE_BOTH	then lffw2=a-2*side_overhang-2*0.01
IF gs_lining_m = LINING_SIDE_LEFT	then lffw2=a-side_overhang-2*0.01
IF gs_lining_m = LINING_SIDE_RIGHT	then lffw2=a-side_overhang-2*0.01
IF gs_lining_m = LINING_SIDE_NONE OR not(bCornice) then lffw2=a-2*0.01

IF gs_lining_m = LINING_SIDE_NONE OR not(bCornice) then valt2=-a/2+0.01
IF gs_lining_m = LINING_SIDE_BOTH	then valt2=-a/2+side_overhang+0.01
IF gs_lining_m = LINING_SIDE_LEFT	then valt2=-a/2+0.01
IF gs_lining_m = LINING_SIDE_RIGHT	then valt2=-a/2+side_overhang+0.01

!!! *** Back Panel
IF gs_lining_m = LINING_SIDE_BOTH then valt3=-a/2+side_overhang
IF gs_lining_m = LINING_SIDE_BOTH then valt4=a/2-side_overhang
IF gs_lining_m = LINING_SIDE_LEFT then valt3=-a/2
IF gs_lining_m = LINING_SIDE_LEFT then valt4=a/2-side_overhang
IF gs_lining_m = LINING_SIDE_RIGHT then valt3=-a/2+side_overhang
IF gs_lining_m = LINING_SIDE_RIGHT then valt4=a/2
IF gs_lining_m = LINING_SIDE_NONE then valt3=-a/2
IF gs_lining_m = LINING_SIDE_NONE then valt4=a/2


IF gs_detlevel_3D_m=0 THEN END

ADD a/2,b,0
MULX -1


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Detailed 3D !!!
	CALL "fa_cabinet_credenza" PARAMETERS 	a=a,
											b=b-fr_th1,
											zzyzx=zzyzx,
											cot=cot,
											cooh=cooh, 
											side_overhang = side_overhang,
											leg_h=bs_h,
											gs_cont_pen=gs_cont_pen,
											gs_cabinet_mat=gs_cabinet_mat,
											gs_lining_m=gs_lining_m


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Cornices !!!
if bCornice then
	del 1
		ADDZ zzyzx-cot
			if cooh <= fr_th1 then
			    corniceDepth = b-fr_th1 + cooh
	    	else
		    	corniceDepth = b
	    	endif
			CALL "fa_cornicetyp" PARAMETERS ALL b = corniceDepth
		DEL 1
	MULX -1
endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Back Panel !!!

	MATERIAL bs_mat
	PRISM_ 4,bs_h,
			valt3+bst,0,15,
			valt3+bst,-bst,15,
			valt4-bst,-bst,15,
			valt4-bst,0,15

	PRISM_ 4,bs_h,
			valt3,0,15,
			valt3,-b+fr_th1+cooh,15,
			valt3+bst,-b+fr_th1+cooh,15,
			valt3+bst,0,15

	PRISM_ 4,bs_h,
			valt4,0,15,
			valt4,-b+fr_th1+cooh,15,
			valt4-bst,-b+fr_th1+cooh,15,
			valt4-bst,0,15

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Shelves !!!

	if bShelves then

		MATERIAL gs_cabinet_mat
	
		sfh=zzyzx-cot-bs_h-2*0.02      !!! Shelves full height
		bb = (sfh + 0.02) / (iNumShelves + 1)
	
		ADD valt2,-0.02,bs_h+bb
	
			for i=1 to iNumShelves
	
				PRISM_ 4, 0.02,
						lffw2-0.01,0,15,
						lffw2-0.01,-b+fr_th1+cooh+0.02,15,
						0.01,-b+fr_th1+cooh+0.02,15,
						0.01,0,15
	
			ADDZ bb
			NEXT i
			del iNumShelves
		DEL 1
	endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! Leafs !!!

ADD valt2,-b+fr_th1+cooh,bs_h+0.01

	!* Knob temp. position

	IF dn=1 THEN
		TempKnob1X=knob_pl_x
		TempKnob2X=knob_size+knob_pl_x
		TempKnob3X=knob_pl_x
		TempKnobPos=-knob_pl_x
		ELSE
		TempKnob1X=lffw2/2-knob_pl_x
		TempKnob2X=lffw2/2+knob_size+knob_pl_x
		TempKnob3X=lffw2/2+knob_pl_x
		TempKnobPos=knob_pl_x
	ENDIF

	FOR i=1 TO dn

		CALL "fa_cabdoortyp" PARAMETERS	gs_detlevel_3D_m	= gs_detlevel_3D_m,
										gs_full_edit		= 0,
										door_ang			= 0,
										door_w				= lffw2/dn,
										door_h				= lffh,
										gs_door_type_m		= gs_door_type_m,
										doorframe_w			= doorframe_w,
										vert_gnum			= vert_gnum,
										gs_cust_p			= gs_cust_p,
										hor_gnum			= hor_gnum,
										gs_cont_pen			= gs_cont_pen,
										gs_shadow			= gs_shadow,
										doorglas_mat		= doorglas_mat,
										doorframe_mat		= doorframe_mat,
										fr_th1				= fr_th1,
										gs_knob_type_m		= 0,
										gs_bevel			= gs_bevel

		! ----- Knob
		if bKnob & _bDrawKnob then
			if gs_knob_type_m=KNOB_STYLE_1 or gs_knob_type_m=KNOB_STYLE_2 or gs_knob_type_m=KNOB_STYLE_3 then
				ADD TempKnob1X,-fr_th1,knob_pl_y
					CALL "fa_knobtyp" PARAMETERS ALL

					AddZ -knob_pl_y
						hotspot	0, 0, 0, unID, knob_pl_y, 1+128 : unID=unID+1
						hotspot	0, 0, -1, unID, knob_pl_y, 3+128 : unID=unID+1
						hotspot	0, 0, knob_pl_y, unID, knob_pl_y, 2 : unID=unID+1
					DEL 1
					Addx TempKnobPos
						hotspot	0, 0, 0, unID, knob_pl_x, 1+128 : unID=unID+1
						hotspot	-1, 0, 0, unID, knob_pl_x, 3+128 : unID=unID+1
						hotspot	knob_pl_x, 0, 0, unID, knob_pl_x, 2 : unID=unID+1
					DEL 1

				DEL 1
			ELSE
				IF gs_knob_p_m = 1 THEN					! Horizontal
					ADD TempKnob2X,-fr_th1,knob_pl_y
						CALL "fa_knobtyp" PARAMETERS ALL

						Addx -knob_size
						AddZ -knob_pl_y
							hotspot	0, 0, 0, unID, knob_pl_y, 1+128 : unID=unID+1
							hotspot	0, 0, -1, unID, knob_pl_y, 3+128 : unID=unID+1
							hotspot	0, 0, knob_pl_y, unID, knob_pl_y, 2 : unID=unID+1
						DEL 1
						Addx -knob_pl_x
							hotspot	0, 0, 0, unID, knob_pl_x, 1+128 : unID=unID+1
							hotspot	-1, 0, 0, unID, knob_pl_x, 3+128 : unID=unID+1
							hotspot	knob_pl_x, 0, 0, unID, knob_pl_x, 2 : unID=unID+1
						DEL 2

					DEL 1
				ELSE
					ADD TempKnob3X,-fr_th1,knob_pl_y
						ROTY 180
						CALL "fa_knobtyp" PARAMETERS ALL
						DEL 1
						AddZ -knob_pl_y
							hotspot	0, 0, 0, unID, knob_pl_y, 1+128 : unID=unID+1
							hotspot	0, 0, -1, unID, knob_pl_y, 3+128 : unID=unID+1
							hotspot	0, 0, knob_pl_y, unID, knob_pl_y, 2 : unID=unID+1
						DEL 1
						Addx -knob_pl_x
							hotspot	0, 0, 0, unID, knob_pl_x, 1+128 : unID=unID+1
							hotspot	-1, 0, 0, unID, knob_pl_x, 3+128 : unID=unID+1
							hotspot	knob_pl_x, 0, 0, unID, knob_pl_x, 2 : unID=unID+1
						DEL 1

					DEL 1
				ENDIF
			ENDIF
		endif
		! ----- End of Knob

	mulx -1
	ADDX -2*lffw2/dn
	NEXT i
	DEL dn
DEL 1


del top
END


