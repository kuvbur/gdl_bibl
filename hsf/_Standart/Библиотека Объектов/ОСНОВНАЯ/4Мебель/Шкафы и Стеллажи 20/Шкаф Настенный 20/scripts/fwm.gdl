
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "C78D50C6-2BD1-4188-8483-A6C7533D2D23"
	_startID	= "6B3CDB5E-F638-456C-B058-8DB9E8D31AE7"
	_endID		= "92651D39-CDDC-44A9-A1E7-9B8EA081F5F1"
gosub "AccessoryBooleans_FWM"

	_startID	= "92651D39-CDDC-44A9-A1E7-9B8EA081F5F1"
	_endID		= "A6AFD9C9-775C-4F07-9F2B-DDD750B85469"
gosub "2DSymbolStyles_FWM"

	_startID	= "A6AFD9C9-775C-4F07-9F2B-DDD750B85469"
	_endID		= "144AAFCF-DF0A-4E00-9691-D098FA4FD570"
gosub "newparameters_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
	
		! If gs_cornice_type_m was 0, that equals with bCornice being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_cornice_type_m", _gs_cornice_type_m)

		if _gs_cornice_type_m = 0 then
			bCornice = 0
		else
			bCornice = 1
		endif
		parameters bCornice = bCornice
	
		! If gs_knob_type_m was 0, that equals with bKnob being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m)

		if _gs_knob_type_m = 0 then
			bKnob = 0
		else
			bKnob = 1
		endif
		parameters bKnob = bKnob

		actualGuid = _endID
	endif
return

! ==============================================================================
! New Symbol 2D Styles
! ==============================================================================
"2DSymbolStyles_FWM":
	if actualGuid = _startID then
	
		bHas = STORED_PAR_VALUE("iSymbolStyle", _iSymbolStyle)
		
		if _iSymbolStyle = 1 then iSymbolStyle = 1		! Style 1
		if _iSymbolStyle = 2 then iSymbolStyle = 7		! Style 7
		if _iSymbolStyle = 3 then iSymbolStyle = 8		! Style 8
		if _iSymbolStyle = 4 then iSymbolStyle = 9		! Style 9
		
		gs_line_type = 1
		
		parameters 	iSymbolStyle = iSymbolStyle,
					gs_line_type = gs_line_type

		actualGuid = _endID
	endif
return

! ==============================================================================
! newparameters_bShelves
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID then

		bHas = DELETED_PAR_VALUE("shn", _shn)

		! If No. of Shelves was 0, then bShelves is off, otherwise it is on.
		if bHas then
			if _shn = 0 then
				bShelves = 0
			else
				bShelves = 1
			endif
			parameters bShelves = bShelves
		endif

		actualGuid = _endID
	endif
return
