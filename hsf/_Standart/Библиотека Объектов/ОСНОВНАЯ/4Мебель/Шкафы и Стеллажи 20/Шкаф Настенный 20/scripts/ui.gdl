
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 					=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_Cornice					= _idxTab  : _idxTab = _idxTab + 1
TABID_Door						= _idxTab  : _idxTab = _idxTab + 1
TABID_Knob						= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Настенного Шкафа`
ui_current_page gs_ui_current_page

ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleAndDimensionpage"

ui_page TABID_Cornice, TABID_StyleAndDimensions, `Карниз`, "uiTab_Cornice_flat(1)"
	gosub "Cornicepage"

ui_page TABID_Door, TABID_StyleAndDimensions, `Двери`, "uiTab_WardrobeDoor_flat(1)"
	gosub "Doorpage"

ui_page TABID_Knob, TABID_StyleAndDimensions, `Ручка`, "uiTab_Knob_flat(1)"
	gosub "Knobpage"

ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepresentationpage"

ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DRepresentationpage"

ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "Descriptionspage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Leg_flat(1)"
file_dependence "uiTab_Drawer_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------

	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	widthInf2	= 37
	xPict = 126
	yPict = 80
	PictWidth = 222 
	PictHeight = 164
	x1 = 0
	x2 = 123
	x3 = 177
	x4 = 231
	x5 = xPict - 90 - 3
	x6 = xPict - 3
	x7 = xPict + 70
	x8 = xPict + 105
	x9 = xPict + PictWidth + 3
	dy = 24

	_yCurr = pageStartY
	ui_infield{3} "dn", x2, _yCurr, 50, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `1`, 1,
			"", `2`, 2 ui_tooltip `Кол-во Дверей`
	ui_pict_button ui_function, "", "uiFunctionDoor_flat(1)", x3, _yCurr-1, widthInf2, 22, -TABID_Door ! Set GLOB_UI_BUTTON_ID

	ui_infield{3} "bKnob", x4, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Ручка`, 0,
					"", `Ручка`, 1
	ui_pict_button ui_function, "", "uiFunctionDrawerKnob_flat(1)", 444 - widthInf2, _yCurr-1, widthInf2, 22, -TABID_Knob ! Set GLOB_UI_BUTTON_ID

	_yCurr = _yCurr + _yOffset
	ui_outfield `Кол-во Дверей`, x1, _yCurr, 120, _outfHeight

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{3} "bShelves", x1, _yCurr, 120, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Полки`, 0,
					"", `Полки`, 1
	ui_infield "iNumShelves", x2, _yCurr, 54, _infHeight ui_tooltip `Кол-во Полок`
	ui_infield{3} "bCornice", x4, _yCurr, 210 - widthInf2, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Карниз`, 0,
					"", `Карниз`, 1

	ui_pict_button ui_function, "", "uiFunctionCornice_flat(1)", 444 - widthInf2, _yCurr-1, widthInf2, 22, -TABID_Cornice ! Set GLOB_UI_BUTTON_ID ! c-173

	_yCurr = _yCurr + dy
	ui_separator x1, _yCurr, 444, _yCurr

	ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1

	_yCurr = yPict - _infHeight - 3
	ui_infield "A", x8, _yCurr, 90, _infHeight ui_tooltip `Размер 1`
	ui_infield "B", x6, _yCurr, 90, _infHeight ui_tooltip `Размер 2`

	_yCurr = yPict + 70
	ui_infield "zzyzx", x9, _yCurr, 90, _infHeight ui_tooltip `Общая Высота`

	_yCurr = yPict + 123
	ui_infield "bs_h", x5, _yCurr, 90, _infHeight ui_tooltip `Высота Задней Панели (0-Нет)`

	_yCurr = yPict + PictHeight + 2
	ui_infield "bst", x7, _yCurr, 90, _infHeight ui_tooltip `Толщина Задней Панели`

	_yCurr = _yCurr + _yOffset
	qq= REQUEST ("Working_length_unit", "", dimensionForm)
	ui_outfield str(dimensionForm,bst), xPict + PictWidth - 27, _yCurr, 90, _outfHeight ui_tooltip `Толщина Задней Панели`
	ui_outfield str(dimensionForm,bst), xPict + 7, _yCurr, 60, _outfHeight, 1 ui_tooltip `Толщина Задней Панели`

return

! ------------------------------------------------------------------------------
"Cornicepage":
! ------------------------------------------------------------------------------

	call "ui_cabinetBase" parameters all	iBaseClass = 2,	! BASECLASS_CORNICE
											iItemClass = 0	! ITEMCLASS_OTHER

return

! ------------------------------------------------------------------------------
"Doorpage":
! ------------------------------------------------------------------------------

	call "ui_cabinetDoor" parameters all	iItemClass = 3	! ITEMCLASS_WALL_CABINET

return

! ------------------------------------------------------------------------------
"Knobpage":
! ------------------------------------------------------------------------------

	call "ui_cabinetKnob" parameters all	iKnobClass = 2,	! KNOBCLASS_DOOR
											iItemClass = 0	! ITEMCLASS_OTHER

return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------

	dim stSymbolTypes[9]
		stSymbolTypes[1] = `Стиль 1`
		stSymbolTypes[2] = `Стиль 2`
		stSymbolTypes[3] = `Стиль 3`
		stSymbolTypes[4] = `Стиль 4`
		stSymbolTypes[5] = `Стиль 5`
		stSymbolTypes[6] = `Стиль 6`
		stSymbolTypes[7] = `Стиль 7`
		stSymbolTypes[8] = `Стиль 8`
		stSymbolTypes[9] = `Стиль 9`

	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 13,
													stSymbolTypes 		= stSymbolTypes,
													iSymbolStyle 		= iSymbolStyle,
													bOpeningAngle		= 1,
													bCabLineType		= 1,
													iFillListID			= TABID_2DRepresentation

return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------

	dim surfacesArray[] 
 	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_cabinet_mat"
		SurfItemID = SurfItemID + 1
		if gs_door_type_m <> DOOR_STYLE_CUST then
			surfacesArray[SurfItemID]	= "doorframe_mat"
			SurfItemID = SurfItemID + 1
		endif
		if gs_door_type_m = DOOR_STYLE_3 or gs_door_type_m = DOOR_STYLE_5 or gs_door_type_m = DOOR_STYLE_6 or gs_door_type_m = DOOR_STYLE_8 then
			surfacesArray[SurfItemID]	= "doorglas_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bCornice then
			surfacesArray[SurfItemID]	= "gs_cornice_mat"
			SurfItemID = SurfItemID + 1
		endif
		if abs(bs_h) > EPS then
			surfacesArray[SurfItemID]	= "bs_mat"
			SurfItemID = SurfItemID + 1
		endif

		if bKnob then
			surfacesArray[SurfItemID]	= "gs_knob_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3DRepresentation

return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames

return
