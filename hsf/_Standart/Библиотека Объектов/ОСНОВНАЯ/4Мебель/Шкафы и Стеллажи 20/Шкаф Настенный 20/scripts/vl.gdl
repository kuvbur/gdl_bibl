
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

!!! Custom Elements !!!-------------------------------------

DIM gs_pt[]

IF gs_door_type_m = DOOR_STYLE_CUST THEN

CALL "Object_Panel_Collection" PARAMETERS RETURNED_PARAMETERS gs_pt

VALUES "gs_cust_p" gs_pt

ENDIF


!!!---------------------------------------------------------------

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=7
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=91

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" THEN
	IF gs_detlevel_3D = `Откл.` THEN PARAMETERS gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN PARAMETERS gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Детальный` THEN PARAMETERS gs_detlevel_3D_m = 2
ELSE
	IF gs_detlevel_3D_m = 0 THEN PARAMETERS gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN PARAMETERS gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN PARAMETERS gs_detlevel_3D = `Детальный`
ENDIF

IF gs_detlevel_3D_m = 1 THEN
	LOCK "gs_resol"
	LOCK "hor_gnum", "vert_gnum", "gs_knob_mat", "knob_pl_x", "knob_pl_y", "gs_knob_type_m", "gs_knob_p", "knob_size"
	HIDEPARAMETER "gs_resol"
ENDIF

IF gs_detlevel_3D_m = 0 THEN
	LOCK "gs_resol", "gs_shadow"
	HIDEPARAMETER  "gs_resol", "gs_shadow"
ENDIF

VALUES{2} "gs_door_type_m"	DOOR_STYLE_1,		stDoorStyle[1],
							DOOR_STYLE_2,		stDoorStyle[2],
							DOOR_STYLE_3,		stDoorStyle[3],
							DOOR_STYLE_4,		stDoorStyle[4],
							DOOR_STYLE_5,		stDoorStyle[5],
							DOOR_STYLE_6,		stDoorStyle[6],
							DOOR_STYLE_7,		stDoorStyle[7],
							DOOR_STYLE_8,		stDoorStyle[8],
							DOOR_STYLE_9,		stDoorStyle[9],
							DOOR_STYLE_10,		stDoorStyle[10],
							DOOR_STYLE_CUST,	stDoorStyle[11]

IF gs_door_type_m <> DOOR_STYLE_CUST THEN
	HIDEPARAMETER "gs_cust_p"
	LOCK "gs_cust_p"
ENDIF

IF gs_door_type_m = DOOR_STYLE_1 THEN
	HIDEPARAMETER "doorframe_w","hor_gnum","vert_gnum","doorglas_mat"
	LOCK "doorframe_w","hor_gnum","vert_gnum","doorglas_mat"
ENDIF

IF gs_door_type_m = DOOR_STYLE_2 OR gs_door_type_m = DOOR_STYLE_4 OR \
	gs_door_type_m = DOOR_STYLE_7 OR gs_door_type_m = DOOR_STYLE_9 THEN
	HIDEPARAMETER "hor_gnum","vert_gnum","doorglas_mat"
	LOCK "hor_gnum","vert_gnum","doorglas_mat"
ENDIF

IF gs_door_type_m = DOOR_STYLE_6 THEN
	HIDEPARAMETER "hor_gnum","vert_gnum"
	LOCK "hor_gnum","vert_gnum"
ENDIF

IF  gs_door_type_m = DOOR_STYLE_CUST THEN
	HIDEPARAMETER "doorframe_w","vert_gnum","hor_gnum","doorframe_mat","doorglas_mat"
	LOCK "doorframe_w","vert_gnum","hor_gnum","doorframe_mat","doorglas_mat"
ENDIF

values{2} "gs_cornice_type_m"	CORN_STYLE_1,		`Стиль 1`,
								CORN_STYLE_2,		`Стиль 2`,
								CORN_STYLE_3,		`Стиль 3`,
								CORN_STYLE_4,		`Стиль 4`

if not(bCornice) then
	HIDEPARAMETER "gs_lining_m","cot","cooh","side_overhang","gs_cornice_mat", "gs_cornice_type_m"
	LOCK "gs_lining_m","cot","cooh","side_overhang","gs_cornice_mat", "gs_cornice_type_m"
endif

if gs_lining_m = LINING_SIDE_NONE then
	HIDEPARAMETER "side_overhang"
	LOCK "side_overhang"
endif

values{2} "gs_knob_type_m"	KNOB_STYLE_1,	`Стиль 1`,
							KNOB_STYLE_2,	`Стиль 2`,
							KNOB_STYLE_3,	`Стиль 3`,
							KNOB_STYLE_4,	`Стиль 4`,
							KNOB_STYLE_5,	`Стиль 5`,
							KNOB_STYLE_6,	`Стиль 6`,
							KNOB_STYLE_7,	`Стиль 7`,
							KNOB_STYLE_8,	`Стиль 8`

if not(bKnob) then
	lock			"gs_knob_type_m", "gs_knob_mat", "knob_pl_x", "knob_pl_y", "gs_knob_p", "knob_size"
	hideparameter	"gs_knob_type_m", "gs_knob_mat", "knob_pl_x", "knob_pl_y", "gs_knob_p", "knob_size"
endif

if gs_knob_type_m < 4 then
	lock "gs_knob_p", "knob_size"
	hideparameter "gs_knob_p", "knob_size"
endif

dim st_knobplacing[2]
	st_knobplacing[1]=`Вертикальный`
	st_knobplacing[2]=`Горизонтальный`

values "gs_knob_p" st_knobplacing
values "gs_knob_p_m" 0, 1

if GLOB_MODPAR_NAME = "gs_knob_p" then
	for iKnob = 1 to vardim1(st_knobplacing)
		if gs_knob_p = st_knobplacing[iKnob] then
			gs_knob_p_m = iKnob - 1
		endif
	next iKnob
	parameters gs_knob_p_m = gs_knob_p_m
else
	gs_knob_p = st_knobplacing[gs_knob_p_m+1]
	parameters gs_knob_p = gs_knob_p
endif

! Enable all Symbol Types
iSymbolStyleEnable = 2**1 + 2**2 + 2**3 + 2**4 + 2**5 + 2**6 + 2**7 + 2**8 + 2**9
call "cabSymbolType" parameters 	iSymbolStyle = iSymbolStyle,
									iSymbolStyleEnable = iSymbolStyleEnable

if 	iSymbolStyle <> 8 &\		! Lock Door Opening Angle if the Symbol has no opening doors
	iSymbolStyle <> 9 then

	lock "opa2"
	hideparameter "opa2"
endif

VALUES "dn" 1, 2
VALUES "doorframe_w" RANGE [0.01,)
values "iNumShelves" RANGE [1,)
VALUES "cot" RANGE [0,)
VALUES "vert_gnum" RANGE [0,)
VALUES "hor_gnum" RANGE [0,)
VALUES "bs_h" RANGE [0,)
VALUES "bst" RANGE (0,)
VALUES "gs_resol" RANGE [3,)
VALUES "fr_th1" RANGE [0.01,)

VALUES{2} "gs_lining_m"	LINING_SIDE_BOTH,	stLiningSide[1],
						LINING_SIDE_LEFT,	stLiningSide[2],
						LINING_SIDE_RIGHT,	stLiningSide[3],
						LINING_SIDE_NONE,	stLiningSide[4]

IF gs_lining_m = LINING_SIDE_BOTH	then lffw2=a-2*side_overhang-2*0.01
IF gs_lining_m = LINING_SIDE_LEFT	then lffw2=a-side_overhang-2*0.01
IF gs_lining_m = LINING_SIDE_RIGHT	then lffw2=a-side_overhang-2*0.01
IF gs_lining_m = LINING_SIDE_NONE OR not(bCornice) then lffw2=a-2*0.01

VALUES "knob_pl_x" RANGE [0.02, lffw2/dn - 0.02]
VALUES "knob_pl_y" RANGE [0.02, zzyzx-cot-bs_h-0.04]
VALUES "knob_size" RANGE [0.045,)

if bCornice then

	IF gs_cornice_type_m = CORN_STYLE_1 THEN VALUES "cooh" RANGE [0,)
	IF gs_cornice_type_m = CORN_STYLE_2 OR gs_cornice_type_m = CORN_STYLE_3 THEN VALUES "cooh" RANGE [1/2*cot,)
	IF gs_cornice_type_m = CORN_STYLE_4 THEN VALUES "cooh" RANGE [3/4*cot,)

	IF gs_cornice_type_m = CORN_STYLE_1 THEN VALUES "side_overhang" RANGE [0,A/2 - 0.02)
	IF gs_cornice_type_m = CORN_STYLE_2 OR gs_cornice_type_m = CORN_STYLE_3 THEN VALUES "side_overhang" RANGE [1/2*cot,A/2 - 0.02)
	IF gs_cornice_type_m = CORN_STYLE_4 THEN VALUES "side_overhang" RANGE [3/4*cot,A/2 - 0.02)

endif

if not(bShelves) then
	lock "iNumShelves"
	hideparameter "iNumShelves"
endif

IF abs(bs_h) < EPS THEN
	LOCK "bs_mat"
	HIDEPARAMETER "bs_mat"
ENDIF

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

