
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "F753F0C1-C5E7-4507-8D34-B8CF4DE973CB"
	_startID	= "BCEA445B-1060-4821-9E88-65736B846F97"
	_endID		= "A2248BB0-A24D-4F95-8CC3-8DA618DED999"
gosub "2DSymbolStyles_FWM"

	_startID	= "A2248BB0-A24D-4F95-8CC3-8DA618DED999"
	_endID		= "19036C0F-56C0-448D-B926-5083F8A7FCA6"
gosub "newparameters_FWM"

	_startID	= "19036C0F-56C0-448D-B926-5083F8A7FCA6"
	_endID		= "9816C95A-F5B0-4308-9B86-682EF6B4B895"
gosub "AccessoryBooleans_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! New Symbol 2D Styles
! ==============================================================================
"2DSymbolStyles_FWM":
	if actualGuid = _startID | actualGuid = _autoID then
	
		bHas = DELETED_PAR_VALUE ("gs_detlevel_2D", _gs_detlevel_2D)
		bHas = DELETED_PAR_VALUE ("iCabinetScheme", _iCabinetScheme)
		
		if _gs_detlevel_2D = 1 then		! Detailed 2D Symbol - ON 
			iSymbolStyle = 2	
		else							! Detailed 2D Symbol - OFF
			if _iCabinetScheme = 5 then	! Shelves
				iSymbolStyle = 1
			else
				iSymbolStyle = 6
			endif
		endif
		parameters iSymbolStyle = iSymbolStyle

		actualGuid = _endID
	endif
return

! ==============================================================================
! newparameters_bShelves_nDrawerKnob
! ==============================================================================
"newparameters_FWM":
	if actualGuid = _startID then

		bHas = STORED_PAR_VALUE("shelfNum", _shelfNum)

		! If No. of Shelves was 0, then bShelves is off, otherwise it is on.
		if bHas then
			if _shelfNum = 0 then
				bShelves = 0
			else
				bShelves = 1
			endif
			parameters bShelves = bShelves
		endif

		! No. of Knobs by Drawer is a new parameter, 
		! it was fix 1 before.
		nDrawerKnob = 1
		parameters nDrawerKnob = nDrawerKnob

		actualGuid = _endID
	endif
return

! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID then

		! If gs_knob_type_m was 0, that equals with bKnob being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m)

		if _gs_knob_type_m = 0 then
			bKnob = 0
		else
			bKnob = 1
		endif
		parameters bKnob = bKnob

		! If gs_knob_type_m_up was 0, that equals with bKnob_up being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_knob_type_m_up", _gs_knob_type_m_up)

		if _gs_knob_type_m_up = 0 then
			bKnob_up = 0
		else
			bKnob_up = 1
		endif
		parameters bKnob_up = bKnob_up

		actualGuid = _endID
	endif
return

