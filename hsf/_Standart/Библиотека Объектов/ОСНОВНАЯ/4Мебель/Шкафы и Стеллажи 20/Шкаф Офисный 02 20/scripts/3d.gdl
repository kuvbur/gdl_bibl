
! Plan Drawers

unID = 1

IF not(gs_shadow) THEN SHADOW OFF

if gs_detlevel_3D_m = 0 then end
if gs_detlevel_3D_m = 1 then
	resol 5
endif

resol gs_resol
pen gs_cont_pen

add 0.02,0.02,0
	MATERIAL toe_mat
	BLOCK A-0.04,B-0.02,toe_h * bBase
	gosub 2000
del 1


rotx 90
addy toe_h * bBase
addz -B
	MATERIAL gs_cabinet_mat
	PRISM_ 10, B,
	0,0,15,
	0,ZZYZX-toe_h * bBase,15,
	A,ZZYZX-toe_h * bBase,15,
	A,0,15,
	0,0,-1,
	0.02,0.02,15,
	0.02,ZZYZX-toe_h * bBase-0.02,15,
	A-0.02,ZZYZX-toe_h * bBase-0.02,15,
	A-0.02,0.02,15,
	0.02,0.02,-1

gosub 3000

addx 0.02
addy 0.02
	BLOCK A-0.04,ZZYZX-toe_h * bBase-0.04,0.02
del 5


add 0.02,0,toe_h * bBase+0.02

For j=1 to drawer_num
	MATERIAL gs_drawer_mat
	if gs_detlevel_3D_m = 2 & gs_bevel then
		addy 0.02
		rotx 90
		fprism_ gs_drawer_mat, gs_drawer_mat, gs_drawer_mat, gs_drawer_mat,
			5, 0.02, 45, 0.01,
			0, 0, 15,
			A-0.04, 0, 15,
			A-0.04, (ZZYZX-0.04-toe_h * bBase)/drawer_num-0.005, 15,
			0, (ZZYZX-0.04-toe_h * bBase)/drawer_num-0.005, 15,
			0, 0, -1
		del 2
		if j = 1 then
			addy 0.02
			block A-0.04, B-0.04, ZZYZX-0.04-toe_h * bBase
			del 1
		endif
	else
		block A-0.04,B-0.02,(ZZYZX-0.04-toe_h * bBase)/drawer_num-0.005
	endif

	! ----- Knob
	if bKnob then
		if nDrawerKnob = 2 then
			for hi = 1 to 2
				add knob_pl_x, 0, (ZZYZX-0.04-toe_h * bBase)/drawer_num/2 + knob_pl_y
				gosub 6000
				del 1

				mulx -1
				addx -A + 0.04
			next hi
			del 4
		else
			add knob_pl_x + A/2 - 0.02, 0, (ZZYZX-0.04-toe_h * bBase)/drawer_num/2 + knob_pl_y
			gosub 6000
			del 1
		endif
	endif	
	! ----- End of knob

	addz (ZZYZX-0.04-toe_h * bBase)/drawer_num
next j

gosub 2000

del drawer_num
del 1

call "Resize_A_B_ZZYZX" parameters all

END

1000:
roty 90
gosub 5000
del 1
return

2000:
gosub 5000
return

3000:
rotz 90
gosub 5000
del 1
return


5000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return

6000:
	if gs_knob_type_m >= 4 then
		if gs_knob_type_m <> KNOB_STYLE_8 then
			roty 90
		else
			roty -90
		endif
		addz knob_size/2
	endif

	CALL "fa_knobtyp" parameters	gs_detlevel_3D_m	= gs_detlevel_3D_m,
									gs_knob_type_m		= gs_knob_type_m,
									knob_size			= knob_size,
									gs_resol			= gs_resol,
									gs_shadow			= gs_shadow,
									gs_cont_pen			= gs_cont_pen,
									gs_knob_mat			= gs_knob_mat

	if gs_knob_type_m >= 4 then del 2

	addz -knob_pl_y
	hotspot	0, 0, 0,			unID, knob_pl_y, 1+128	: unID = unID + 1
	hotspot	0, 0, -1,			unID, knob_pl_y, 3+128	: unID = unID + 1
	hotspot	0, 0, knob_pl_y,	unID, knob_pl_y, 2		: unID = unID + 1
	del 1
	addx -knob_pl_x
	hotspot	0,			0, 0, unID, knob_pl_x, 1+128	: unID=unID+1
	hotspot	-1,			0, 0, unID, knob_pl_x, 3+128	: unID=unID+1
	hotspot	knob_pl_x,	0, 0, unID, knob_pl_x, 2		: unID=unID+1
	del 1

return
