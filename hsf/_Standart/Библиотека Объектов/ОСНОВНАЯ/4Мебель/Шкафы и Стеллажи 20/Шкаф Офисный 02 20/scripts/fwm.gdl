
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "43F58C21-7578-4B2D-BF49-BE0CD55BBE9F"
	_startID	= "C6C6F018-7097-41C2-918C-2488D528E76F"
	_endID		= "D886C18A-225A-4FF8-88F7-E9068ABAF8F0"
gosub "AccessoryBooleans_FWM"

	_startID	= "D886C18A-225A-4FF8-88F7-E9068ABAF8F0"
	_endID		= "DDA9DEFF-EA3D-407E-AB73-CAB4DAFABD89"
gosub "2DSymbolStyles_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! AccessoryBooleans
! ==============================================================================
"AccessoryBooleans_FWM":
	if actualGuid = _startID | actualGuid = _autoID then

		! If toe_h was 0, that equals with bBase being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("toe_h", _toe_h)

		if abs(_toe_h) < EPS then
			bBase = 0
		else
			bBase = 1
		endif
		parameters bBase = bBase
	
		! If gs_knob_type_m was 0, that equals with bKnob being off, otherwise it is on.
		bHas = STORED_PAR_VALUE("gs_knob_type_m", _gs_knob_type_m)

		if _gs_knob_type_m = 0 then
			bKnob = 0
		else
			bKnob = 1
		endif
		parameters bKnob = bKnob

		actualGuid = _endID
	endif
return

! ==============================================================================
! New Symbol 2D Styles
! ==============================================================================
"2DSymbolStyles_FWM":
	if actualGuid = _startID then
	
		iSymbolStyle 	= 1 							! Style 1
		bDrawer2D		= 0 							! Do not show drawers in 2D
		
		parameters 	iSymbolStyle = iSymbolStyle,
					bDrawer2D = bDrawer2D

		actualGuid = _endID
	endif
return
