
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=7
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=91

! Enable only Symbol Types without door symbol
iSymbolStyleEnable = 2**1 + 2**2 + 2**3 + 2**6
call "cabSymbolType" parameters 	iSymbolStyle = iSymbolStyle,
									iSymbolStyleEnable = iSymbolStyleEnable

VALUES "a" RANGE [0.3,)
VALUES "b" RANGE [0.2,)
VALUES "zzyzx" RANGE [toe_h * bBase + 0.05,)

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 0, 1, 2

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	if gs_detlevel_3D = `Откл.`		then parameters gs_detlevel_3D_m = 0
	if gs_detlevel_3D = `Простой`	then parameters gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Детальный`	then parameters gs_detlevel_3D_m = 2
else
	if gs_detlevel_3D_m = 0 then parameters gs_detlevel_3D = `Откл.`
	if gs_detlevel_3D_m = 1 then parameters gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = 2 then parameters gs_detlevel_3D = `Детальный`
endif


VALUES "gs_resol" RANGE [3,)


VALUES "drawer_num" RANGE [0,)
VALUES "toe_h" RANGE (0,)

IF drawer_num = 0 THEN HIDEPARAMETER "gs_drawer_mat", "gs_knob_mat"

if  not(bBase) then 
	hideparameter "toe_h", "toe_mat"
	lock "toe_h", "toe_mat"
endif

if gs_detlevel_3D_m = 0 then
	lock "gs_knob_mat", "gs_shadow", "gs_resol"
	hideparameter "gs_shadow", "gs_resol"
ENDIF

if gs_detlevel_3D_m = 1 then
	lock "gs_knob_mat", "gs_resol"
	hideparameter "gs_resol"
endif

values{2} "gs_knob_type_m"	KNOB_STYLE_1,	`Стиль 1`,
							KNOB_STYLE_2,	`Стиль 2`,
							KNOB_STYLE_3,	`Стиль 3`,
							KNOB_STYLE_4,	`Стиль 4`,
							KNOB_STYLE_5,	`Стиль 5`,
							KNOB_STYLE_6,	`Стиль 6`,
							KNOB_STYLE_7,	`Стиль 7`,
							KNOB_STYLE_8,	`Стиль 8`

values{2} "gs_knob_p_m" 1,	`Горизонтальный`

lock "gs_knob_p_m"

values "knob_size"	range [0.045,)

if gs_knob_type_m < 4 then lock "knob_size"

xKnobPosMod = 0

if gs_knob_type_m >= 4 then xKnobPosMod = knob_size/2

values "nDrawerKnob" 1, 2
if nDrawerKnob = 2 then
	values "knob_pl_x" range [0.02 + xKnobPosMod, A/2 - 0.04 - xKnobPosMod]
else
	values "knob_pl_x" range [-A/2 + 0.02 + xKnobPosMod, A/2 - 0.02 - xKnobPosMod]
endif

values "knob_pl_y" range [-((ZZYZX-0.04-toe_h * bBase)/drawer_num/2) + 0.02, ((ZZYZX-0.04-toe_h * bBase)/drawer_num/2) - 0.02]

if GLOB_MODPAR_NAME = "nDrawerKnob" then
	if nDrawerKnob = 1 then
		knob_pl_x = 0
	else
		knob_pl_x = 0.1
	endif
	parameters knob_pl_x = knob_pl_x
endif

if not(bKnob) then
	lock			"gs_knob_type_m", "gs_knob_p_m", "knob_size", "knob_pl_x", "knob_pl_y", "gs_knob_mat", "nDrawerKnob"
	hideparameter	"gs_knob_type_m", "gs_knob_p_m", "knob_size", "knob_pl_x", "knob_pl_y", "gs_knob_mat", "nDrawerKnob"
endif

! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif



