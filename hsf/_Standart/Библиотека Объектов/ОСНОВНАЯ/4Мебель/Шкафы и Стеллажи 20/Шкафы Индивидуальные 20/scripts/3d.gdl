
IF gs_detlevel_3D_m = 0 THEN END

PEN gs_cont_pen

reqBKnobs = 0
success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)

IF not(gs_shadow) THEN SHADOW OFF

!!! *************************************************************
!!! *** 3D HOTSPOTs *********************************************

Unid=1

HOTSPOT n*width*0.5,depth*0.5,0,unID :	unID=unID+1
HOTSPOT 0,0,0,unID :	unID=unID+1
HOTSPOT n*width,0,0,unID :	unID=unID+1
HOTSPOT n*width,depth,0,unID :	unID=unID+1
HOTSPOT 0,depth,0,unID :	unID=unID+1

HOTSPOT 0,0,zzyzx,unID :	unID=unID+1
HOTSPOT n*width,0,zzyzx,unID :	unID=unID+1
HOTSPOT n*width,depth,zzyzx,unID :	unID=unID+1
HOTSPOT 0,depth,zzyzx,unID :	unID=unID+1

addz legHeight * bBase
HOTSPOT a*0.5,0,0,uniD,height,1+128:	unID=unID+1
HOTSPOT a*0.5,0,height,uniD,height,2:	unID=unID+1
HOTSPOT a*0.5,0,0,uniD,height,3+128:	unID=unID+1
del 1

!!! *************************************************************
!!! *** 3D Feedback *********************************************

!!! 3D Feedback to editing
IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN
	gs_detlevel_3D_m = 1
ENDIF

if gs_detlevel_3D_m = 2 then
	resol gs_resol
else
	resol 8
endif

!!! *************************************************************
!!! ***Leg **************************************************

if bBase then

	hotspot	0, legSetBack, 0, unID, legHeight, 1+128 : unID=unID+1
	hotspot	0, legSetBack, -1, unID, legHeight, 3+128 : unID=unID+1
	hotspot	0, legSetBack, legHeight, unID, legHeight, 2 : unID=unID+1

	hotspot	a, legSetBack, 0, unID, legHeight, 1+128 : unID=unID+1
	hotspot	a, legSetBack, -1, unID, legHeight, 3 : unID=unID+1
	hotspot	a, legSetBack, legHeight, unID, legHeight, 2 : unID=unID+1

	hotspot	0, b, 0, unID, legHeight, 1+128 : unID=unID+1
	hotspot	0, b, -1, unID, legHeight, 3 : unID=unID+1
	hotspot	0, b, legHeight, unID, legHeight, 2 : unID=unID+1

	hotspot	a, b, 0, unID, legHeight, 1+128 : unID=unID+1
	hotspot	a, b, -1, unID, legHeight, 3 : unID=unID+1
	hotspot	a, b, legHeight, unID, legHeight, 2 : unID=unID+1

	material matLeg

	if iLegType = LEG_STYLE_ROUND or iLegType = LEG_STYLE_RECT then
		for jj=1 to 2
			for ii=1 to 2
				add	baseWidth / 2 + legSetBack, baseWidth / 2 + legSetBack,0
					if iLegType = LEG_STYLE_ROUND then
							cylind legHeight, baseWidth/2

							teta=90
							tx=ii
							gosub 5000
					endif
					if iLegType = LEG_STYLE_RECT then
							prism_ 4, legHeight,
									-baseWidth / 2,	-baseWidth / 2,	15,
									baseWidth / 2,	-baseWidth / 2,	15,
									baseWidth / 2,	baseWidth / 2,	15,
									-baseWidth / 2,	baseWidth / 2,	15

							teta=90
							tx=ii
							gosub 5000
					endif
				del 1
				addx a - baseWidth - legSetBack * 2
			next ii
			del 2
			addy b - baseWidth - legSetBack * 2
		next jj
		del 2

	endif

	if iLegType = LEG_STYLE_TOE then
		prism_ 4, legHeight,
			0, legSetBack, 15,
			a, legSetBack, 15,
			a, b, 15,
			0, b, 15

			teta=0
			tx=0
			gosub 5000
	endif

endif



!!! *************************************************************
!!! NUMBER of ELEMENTs ******************************************

addz legHeight * bBase

FOR k=1 to m
	FOR j=1 to n
		gosub 100:
		addx width
	NEXT j
		del n
		addz height
NEXT k

DEL top
END


100:

!!! *************************************************************
!!! *** 3D Model ************************************************

h=zzyzx

!!! *************************************************************
!!! ***Cabinet **************************************************

MATERIAL CabinetMat

ROTx -90

	PRISM_ 10, depth-0.02,
		0, 0, 11,
		width, 0, 11,
		width, -height, 11,
		0, -height, 11,
		0, 0, -1,

		0.02, -0.02, 15,
		width-0.02, -0.02, 15,
		width-0.02, -height+0.02, 15,
		0.02, -height+0.02, 15,
		0.02, -0.02, -1

		teta=90
		tx=0
		gosub 5000

ADDz depth-0.02
	PRISM_ 4, 0.02,
		0, 0, 14,
		width, 0, 14,
		width, -height, 14,
		0, -height, 14

		teta=90
		tx=0
		gosub 5000

DEL 2


!!! *************************************************************
!!! *** Door ****************************************************

add 0.02, doorPanelThk-doorSetBack, 0.02
rotx 90

IF gs_detlevel_3D_m = 2 THEN


	!!! *************************************************************
	!!! *** Solid ***************************************************

	IF iDoorType = 1 THEN
		MATERIAL DoorMat

		PRISM_ 4, doorPanelThk,
				0,0,15,
				0,height-0.04,15,
				width-0.02*2,height-0.04,15,
				width-0.02*2,0,15

				teta=90
				tx=0
				gosub 5000

		del 2

		gosub 300
		gosub 200
	ENDIF


	!!! *************************************************************
	!!! *** Top Louvered ********************************************

	IF iDoorType = 2 THEN
		MATERIAL DoorMat
		FOR ijk= 1 to 5
			PUT width/2-0.075-0.02, height-0.2-ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  height-0.2-ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  height-0.2+0.01-ijk*0.02, 15,
				width/2-0.075-0.02,  height-0.2+0.01-ijk*0.02, 15,
				width/2-0.075-0.02, height-0.2-ijk*0.02, -1
		NEXT ijk

		PRISM_ 5+nsp/3, doorPanelThk,
				0,0,15,
				0,height-0.04,15,
				width-0.02*2,height-0.04,15,
				width-0.02*2,0,15,
				0,0,-1,

				GET(nsp)

				teta=90
				tx=0
				gosub 5000

		del 2
		gosub 300
		gosub 200
	ENDIF


	!!! *************************************************************
	!!! *** Bottom Louvered *****************************************

	IF iDoorType = 3 THEN
		MATERIAL DoorMat
		FOR ijk= 1 to 5
			PUT width/2-0.075-0.02, 0.1+ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  0.1+ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  0.1+0.01+ijk*0.02, 15,
				width/2-0.075-0.02,  0.1+0.01+ijk*0.02, 15,
				width/2-0.075-0.02, 0.1+ijk*0.02, -1
		NEXT ijk

		PRISM_ 5+nsp/3, doorPanelThk,
				0,0,15,
				0,height-0.04,15,
				width-0.02*2,height-0.04,15,
				width-0.02*2,0,15,
				0,0,-1,

				GET(nsp)

				teta=90
				tx=0
				gosub 5000

		del 2
		gosub 300
		gosub 200
	ENDIF


	!!! *************************************************************
	!!! *** Top & Bottom Louvered ***********************************

	IF iDoorType = 4 THEN
		MATERIAL DoorMat

		FOR ijk= 1 to 5
			PUT width/2-0.075-0.02, height-0.2-ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  height-0.2-ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  height-0.2+0.01-ijk*0.02, 15,
				width/2-0.075-0.02,  height-0.2+0.01-ijk*0.02, 15,
				width/2-0.075-0.02, height-0.2-ijk*0.02, -1
		NEXT ijk


		FOR ijk= 1 to 5
			PUT width/2-0.075-0.02, 0.1+ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  0.1+ijk*0.02, 15,
				width/2-0.075-0.02+0.15,  0.1+0.01+ijk*0.02, 15,
				width/2-0.075-0.02,  0.1+0.01+ijk*0.02, 15,
				width/2-0.075-0.02, 0.1+ijk*0.02, -1
		NEXT ijk

		PRISM_ 5+nsp/3, doorPanelThk,
				0,0,15,
				0,height-0.04,15,
				width-0.02*2,height-0.04,15,
				width-0.02*2,0,15,
				0,0,-1,

				GET(nsp)

				teta=90
				tx=0
				gosub 5000

		del 2
		gosub 300
		gosub 200
	ENDIF

	!!! *************************************************************
	!!! *** Raised Panel ********************************************

	IF iDoorType = 5 THEN
		material matDoorFrame
		for fr=1 to 2
		base
		prism_ 5, 1.7500",
			0, 0, 15,
			0, doorHeight, 10,
			frameBorderWidth, doorHeight-frameBorderWidth, 15,
			frameBorderWidth, frameBorderWidth, 10,
			0, 0, -1

			teta=90
			tx=fr
			gosub 5000
		addx doorWidth
		mulx -1
		next fr
		del 4

		for fr=1 to 2
		base
		prism_ 5, 1.7500",
			0, doorHeight, 15,
			doorWidth, doorHeight, 10,
			doorWidth-frameBorderWidth, doorHeight-frameBorderWidth, 15,
			frameBorderWidth, doorHeight-frameBorderWidth, 10,
			0, doorHeight, -1

			teta=0
			tx=fr
			gosub 5000
		addy doorHeight
		muly -1
		next fr
		del 4


		for fr2=1 to 2
		base
		tube 4, 4, 1+2+16+32,
			-frameWidth+7/16", 1.6250",      0,
			-frameWidth, 1.1875",      0,
			-frameWidth, 0.5625",      0,
			-frameWidth+7/16", 0.1250",      0,

			doorWidth, 0, 0, 0,
			0, 0, 0, 0,
			0, doorHeight, 0, 0,
			doorWidth, doorHeight, 0, 0

			teta=90
			tx=fr2
			gosub 5000
		addx doorWidth
		mulx -1
		next fr2
		del 4

		for fr2=1 to 2
		base
		tube 4, 4, 1+2+16+32,
			-frameWidth+7/16", 1.6250",      0,
			-frameWidth, 1.1875",      0,
			-frameWidth, 0.5625",      0,
			-frameWidth+7/16", 0.1250",      0,

			0, 0, 0, 0,
			0, doorHeight, 0, 0,
			doorWidth, doorHeight, 0, 0,
			doorWidth, 0, 0, 0

			teta=0
			tx=fr2
			gosub 5000
		addy doorHeight
		muly -1
		next fr2
		del 4


		material DoorMat
		for pa=1 to 2
		base
		tube 4, 4, 1+2+16+32,
			-frameWidth, 1.1875",      0,
			-frameWidth-1", 1.3125",      0,
			-frameWidth-1", 0.4375",      0,
			-frameWidth, 0.5625",      0,

			doorWidth, 0, 0, 0,
			0, 0, 0, 0,
			0, doorHeight, 0, 0,
			doorWidth, doorHeight, 0, 0

			teta=90
			tx=pa
			gosub 5000
		addx doorWidth
		mulx -1
		next pa
		del 4

		for pa=1 to 2
		base
		tube 4, 4, 1+2+16+32,
			-frameWidth, 1.1875",      0,
			-frameWidth-1", 1.3125",      0,
			-frameWidth-1", 0.4375",      0,
			-frameWidth, 0.5625",      0,

			0, 0, 0, 0,
			0, doorHeight, 0, 0,
			doorWidth, doorHeight, 0, 0,
			doorWidth, 0, 0, 0

			teta=0
			tx=pa
			gosub 5000
		addy doorHeight
		muly -1
		next pa
		del 4

		addz 1 3/4"/2-fillThk/2
		base
		prism_ 5, fillThk,
			borderWidth, borderWidth, 15,
			doorWidth-borderWidth, borderWidth, 15,
			doorWidth-borderWidth, doorHeight-borderWidth, 15,
			borderWidth, doorHeight-borderWidth, 15,
			borderWidth, borderWidth, -1

			teta=90
			tx=0
			gosub 5000
		del 1

		del 2
		gosub 300
		gosub 200
	ENDIF

ELSE
	MATERIAL DoorMat
	PRISM_ 5, 0.01,
			0,0,15,
			0,height-0.04,15,
			width-0.02*2,height-0.04,15,
			width-0.02*2,0,15,
			0,0,15

			teta=90
			tx=0
			gosub 5000

	del 2
	if success > 0 and reqBKnobs then
		goto 50
	endif

	add 0.02+0.05,-0.005,height*0.5
	rotx 90
	MATERIAL KnobMat
	RESOL 4
	CYLIND 0.02,0.02
	del 2
	50:

ENDIF


RETURN

200:
	if success > 0 and reqBKnobs then
		return
	endif
	if iDoorType <> 5 then
		add 0.02+0.05,-doorSetBack,height*0.5
	else
		add 0.02+borderWidth+0.02+0.01,-doorSetBack+5/16",height*0.5
	endif
	rotx 90
	MATERIAL KnobMat
	RESOL gs_resol
	CYLIND knobThk,0.02
	del 2

return

300:

	if iDoorType <> 5 then
		add width/2-0.075/2, -numPlateThk-doorSetBack, height-0.1
	else
		add width/2-0.075/2, -numPlateThk-doorSetBack, height-0.05-0.02-borderWidth-0.01
	endif
	MATERIAL NumPlateMat
	if NumPlate=1 THEN
		BLOCK 0.075,numPlateThk,0.05
	endif
	DEL 1

return


5000:
rotz teta
add tx, tx, tx
vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4
base
body -1
del 2

return


