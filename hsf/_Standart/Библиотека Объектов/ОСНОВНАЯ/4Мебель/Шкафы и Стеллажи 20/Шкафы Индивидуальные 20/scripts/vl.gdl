
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

! Enable all Symbol Types
iSymbolStyleEnable = 2**1 + 2**2 + 2**3 + 2**4 + 2**5 + 2**6 + 2**7 + 2**8 + 2**9
call "cabSymbolType" parameters 	iSymbolStyle = iSymbolStyle,
									iSymbolStyleEnable = iSymbolStyleEnable

values "gs_detlevel_3D" st3dDetLevelValues
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2
	if gs_detlevel_3D = st3dDetLevelValues[2] then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = st3dDetLevelValues[3] then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = st3dDetLevelValues[1]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = st3dDetLevelValues[2]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = st3dDetLevelValues[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif
VALUES "n" RANGE [1,)
VALUES "m" RANGE [1,)
VALUES "gs_resol" RANGE [3,)

values "doorType" stDoorType
values "iDoorType" 1, 2, 3, 4, 5
if GLOB_MODPAR_NAME = "doorType" then
	iDoorType = 1
	if doorType = stDoorType[2]	then iDoorType = 2
	if doorType = stDoorType[3]	then iDoorType = 3
	if doorType = stDoorType[4]	then iDoorType = 4
	if doorType = stDoorType[5]	then iDoorType = 5
	parameters iDoorType = iDoorType
else
	doorType = stDoorType[1]
	if iDoorType = 2 then	doorType = stDoorType[2]
	if iDoorType = 3 then	doorType = stDoorType[3]
	if iDoorType = 4 then	doorType = stDoorType[4]
	if iDoorType = 5 then	doorType = stDoorType[5]
	parameters doorType = doorType
endif


VALUES{2} "iLegType"	LEG_STYLE_ROUND,	`Круглые`,
						LEG_STYLE_RECT,		`Прямоугольные`,
						LEG_STYLE_TOE,		`Основание`


values "legHeight" range [0.01,)

if not(bBase) then
	lock			"iLegType", "legHeight", "matLeg", "baseWidth"
	hideparameter	"iLegType", "legHeight", "matLeg", "baseWidth"
endif

if iLegType = 3 then
	lock			"baseWidth"
	hideparameter	"baseWidth"
endif

IF iDoorType = 1 THEN VALUES "width" RANGE [3*0.02+0.075*NumPlate+0.01,)
IF iDoorType = 2 OR iDoorType = 3 OR iDoorType = 4 THEN VALUES "width" RANGE [3*0.02+0.15,)
IF iDoorType = 5 THEN VALUES "width" RANGE [3*0.02+0.075*NumPlate+borderWidth*2+0.01,)

if iDoorType <> 5 then
	values "depth" range [0.05,)
else
	values "depth" range [0.02+1 3/4",)
endif

IF iDoorType = 1 THEN VALUES "height" RANGE [3*0.02+0.05*NumPlate+0.01,)
IF iDoorType = 2 OR iDoorType = 3 THEN VALUES "height" RANGE [3*0.02+0.05*NumPlate+0.1+0.01*10+0.01,)
IF iDoorType = 4 THEN VALUES "height" RANGE [3*0.02+0.05*NumPlate+0.2+0.01*20+0.02,)
IF iDoorType = 5 THEN VALUES "height" RANGE [3*0.02+0.05*NumPlate+borderWidth*2+0.01,)

if iDoorType <> 5 then
	lock "frameWidth", "matDoorFrame"
	hideparameter "frameWidth", "matDoorFrame"
endif

IF gs_detlevel_3D_m = 1 THEN
	HIDEPARAMETER "gs_resol"
	LOCK "DoorType", "NumPlate","KnobMat", "NumPlateMat", "frameWidth", "matDoorFrame", "gs_resol"
ENDIF

if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
ENDIF

IF NumPlate = 0 THEN HIDEPARAMETER "NumPlateMat"

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=2
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=91

IF GLOB_MODPAR_NAME = "n" OR GLOB_MODPAR_NAME = "width" OR GLOB_MODPAR_NAME = "frameWidth" THEN
	A = width*n
	PARAMETERS A = A
ELSE
	width = A/n
	PARAMETERS width = width
ENDIF

IF GLOB_MODPAR_NAME = "m" OR GLOB_MODPAR_NAME = "height" OR GLOB_MODPAR_NAME = "ilegType" OR GLOB_MODPAR_NAME = "DoorType" OR GLOB_MODPAR_NAME = "legHeight" THEN
	ZZYZX = height * m + legHeight * bBase
	PARAMETERS ZZYZX = ZZYZX
ELSE
	height = (ZZYZX - legHeight * bBase) / m
	PARAMETERS height = height
ENDIF

IF GLOB_MODPAR_NAME = "B" THEN
	depth = B
	PARAMETERS depth = depth
ELSE
	B = depth
	PARAMETERS B = B
ENDIF


!!! ******************************** Minimal Space

VALUES "MSFront" RANGE [0,)


! =============================================================================
! UI Support
! =============================================================================

if GLOB_UI_BUTTON_ID < 0 then
	parameters gs_ui_current_page = -GLOB_UI_BUTTON_ID
endif

