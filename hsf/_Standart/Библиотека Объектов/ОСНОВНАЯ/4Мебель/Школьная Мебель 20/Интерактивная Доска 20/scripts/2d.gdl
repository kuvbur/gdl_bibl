
pen gs_cont_pen
fill gs_fill_type

! ==============================================================================
! 		Hotspot Editing
! ==============================================================================

unID = 1

! --- Hotspot editing Board Length ---------------------------------------------

hotspot2 0,				0,	unID,	boardWidth,	1+256 :	unID = unID + 1
hotspot2 boardWidth,	0,	unID,	boardWidth,	2 :		unID = unID + 1
hotspot2 -1,			0,	unID,	boardWidth,	3 :		unID = unID + 1

! --- Hotspot editing Board Width ----------------------------------------------

hotspot2 boardWidth/2, -B,	unID :	unID = unID + 1

! --- Non Editing Corner Hotspots ----------------------------------------------

if bSpeaker then
	hotspot2 -0.18,		0,		unID :	unID = unID + 1
	hotspot2 -0.18,		-0.1,	unID :	unID = unID + 1
	hotspot2 A - 0.18,	0,		unID :	unID = unID + 1
	hotspot2 A - 0.18,	-0.1,	unID :	unID = unID + 1
endif

! ==============================================================================
! 		Board rectangle
! ==============================================================================

poly2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
	0,			0,		1,
	boardWidth,	0,		1,
	boardWidth,	-B,		1,
	0,			-B,		1


! ==============================================================================
! 		Floor Mount
! ==============================================================================

if iSupType = 2 then
	for i = 1 to 2
		add2 0.1, 0

		drawindex 10
		poly2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,		-B/2 - 0.2 * ZZYZX,	1,
			0.05,	-B/2 - 0.2 * ZZYZX,	1,
			0.05,	B/2 + 0.2 * ZZYZX,	1,
			0,		B/2 + 0.2 * ZZYZX,	1

		del 1

		add2 boardWidth, 0
		mul2 -1, 1
	next i

	del 4
endif


! ==============================================================================
! 		Speakers
! ==============================================================================

_speakerBoxWidth = 0.18
_speakerBoxDepth = 0.10

if bSpeaker then
	for i = 1 to 2
		poly2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,					0,			1,
			-_speakerBoxWidth,	0,			1,
			-_speakerBoxWidth,	-_speakerBoxDepth,		1,
			0,					-_speakerBoxDepth,		1

		add2 boardWidth, 0
		mul2 -1, 1
	next i

	del 4
endif

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================

