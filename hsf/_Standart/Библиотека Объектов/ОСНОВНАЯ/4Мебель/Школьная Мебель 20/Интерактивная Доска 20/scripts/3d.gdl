
! ==============================================================================
! 		3D Detail Level
! ==============================================================================

if gs_detlevel_3D_m = DET3D_OFF then END
if not(gs_shadow) then SHADOW OFF

pen gs_cont_pen

if gs_detlevel_3D_m = DET3D_SIMPLE then
	resol 8
else
	resol gs_resol
endif


! ==============================================================================
! 		Hotspot Editing
! ==============================================================================

unID = 100

! --- Hotspot editing Board Length ---------------------------------------------

hotspot 0,			0,	(distFloor + boardHeight),	unID,	boardWidth,	1+256 :	unID = unID + 1
hotspot boardWidth,	0,	(distFloor + boardHeight),	unID,	boardWidth,	2 :		unID = unID + 1
hotspot -1,			0,	(distFloor + boardHeight),	unID,	boardWidth,	3 :		unID = unID + 1

! --- Hotspot editing Distance from Floor --------------------------------------

addx boardWidth/2

hotspot 0,	-B,	0,			unID, distFloor, 1+128 :	unID = unID + 1
hotspot 0,	-B,	distFloor,	unID, distFloor, 2 :		unID = unID + 1
hotspot 0,	-B,	-1,			unID, distFloor, 3 :		unID = unID + 1

! --- Hotspot editing Board Height ---------------------------------------------

hotspot 0,	0,	distFloor,					unID, boardHeight, 1+128 :	unID = unID + 1
hotspot 0,	0,	distFloor + boardHeight,	unID, boardHeight, 2 :		unID = unID + 1
hotspot 0,	0,	-1,							unID, boardHeight, 3 :		unID = unID + 1

del 1


! ==============================================================================
! 		Board
! ==============================================================================

if gs_detlevel_3D_m = 2 then
	put		0,					0,						15 + 64,
			boardWidth,			0,						15 + 64,
			boardWidth,			boardHeight - 0.015,	15 + 64,
			boardWidth - 0.015,	boardHeight,			1015 + 64,
			0.015,				boardHeight,			15 + 64,
			0,					boardHeight - 0.015,	1015 + 64,
			0,					0,						-1
else
	put		0,				0,				15,
			boardWidth,		0,				15,
			boardWidth,		boardHeight,	15,
			0,				boardHeight,	15,
			0,				0,				-1
endif

put		0.02,				0.025,				14,
		boardWidth - 0.02,	0.025,				14,
		boardWidth - 0.02,	boardHeight - 0.02,	14,
		0.02,				boardHeight - 0.02,	14,
		0.02,				0.025,				-1

rotx 90
addy distFloor

! --- Frame ---
cprism_ frameMat, frameMat, frameMat,
	NSP/3, B,
	get(NSP)

! --- Board ---
cprism_ boardMat, frameMat, boardMat,
	5, B - 0.001,
	0.02,				0.025,				14,
	boardWidth - 0.02,	0.025,				14,
	boardWidth - 0.02,	boardHeight - 0.02,	14,
	0.02,				boardHeight - 0.02,	14,
	0.02,				0.025,				-1

del 2


! ==============================================================================
! 		Floor Mount
! ==============================================================================

material frameMat

if iSupType = 2 then
	for i = 1 to 2
		addx 0.1

		addy -B/2 - 0.2 * (distFloor + boardHeight)
		block 0.05, 0.4 * (distFloor + boardHeight), 0.05
		del 1

		add 0, -B, 0.05
		block 0.05, B, distFloor - 0.05
		del 1

		for j = 1 to 2
			add 0.03, 0, 0.05
			roty -90
			prism 3, 0.01,
				0,		0,
				0.08,	0,
				0,		0.2

			del 2

			addy -B
			muly -1
		next j
		del 4

		del 1	!addx 0.1

		addx boardWidth
		mulx -1
	next i

	del 4

	add 0.15, -B/2 - 0.015, 0.15

	block boardWidth - 0.3, 0.03, 0.05

	del 1
endif


! ==============================================================================
! 		Pen Holder
! ==============================================================================

material holderMat

if bHolder then
	add 0, -B - 0.08, distFloor
	block boardWidth, 0.08, 0.005
	del 1
endif


! ==============================================================================
! 		Speakers
! ==============================================================================

material speakBoxMat

_speakerBoxWidth = 0.18
_speakerBoxDepth = 0.09 + (gs_detlevel_3D_m = 1) * 0.01

_speakerFabWidth = 0.14
_speakerFabHeight = 0.66 + (gs_detlevel_3D_m = 1) * 0.1

if bSpeaker then
	addz (distFloor + boardHeight) - 0.8
	for i = 1 to 2
		if gs_detlevel_3D_m = 2 then
			add -0.14, -0.01, 0.25
			block 0.14, 0.01, 0.30
			del 1

			addy -0.1
			rotx -90

			fprism_ speakBoxMat, speakBoxMat, speakBoxMat, speakBoxMat,
				4, _speakerBoxDepth, atn(2), _speakerBoxDepth - 0.01,
				0,					0,		15,
				-_speakerBoxWidth,	0,		15,
				-_speakerBoxWidth,	-0.8,	15,
				0,					-0.8,	15

			del 2
		else
			prism 4, 0.8,
				0,					0,
				-_speakerBoxWidth,	0,
				-_speakerBoxWidth,	-_speakerBoxDepth,
				0,					-_speakerBoxDepth
		endif

		add -_speakerBoxWidth + 0.02, -0.1, 0.12 - (gs_detlevel_3D_m = 1) * 0.1
		rotx 90

		hprism_ speakFabMat, speakFabMat, speakFabMat, speakFabMat,
			10, 0.01, (gs_detlevel_3D_m = 1) * 45, 0.01, 1,
			0.01,						0,							15+64,
			_speakerFabWidth - 0.01,	0,							15+64,
			_speakerFabWidth,			0.01,						15+64+1000,
			_speakerFabWidth,			_speakerFabHeight - 0.01,	15+64,
			_speakerFabWidth - 0.01,	_speakerFabHeight,			15+64+1000,
			0.01,						_speakerFabHeight,			15+64,
			0,							_speakerFabHeight - 0.01,	15+64+1000,
			0,							0.01,						15+64,
			0.01,						0,							15+64+1000,
			0.01,						0,							-1

		if gs_detlevel_3D_m = 2 then
			hprism_ speakFabMat, speakFabMat, speakFabMat, speakFabMat,
				5, 0.005, 0, 0.005, 1,
				0.02,						-0.12,	15,
				0.02,						-0.07,	15 + 64,
				_speakerFabWidth/2,			-0.07,	915 + 64,
				0,							-180,	4015 + 64,
				_speakerFabWidth - 0.02,	-0.12,	15
		endif
		del 2

		addx boardWidth
		mulx -1
	next i

	del 4 + 1
endif


! ==============================================================================
! 		Picture
! ==============================================================================

if bPicture then
	define material "pictmat" 2, 1, 1, 1
	material "pictmat"

	add 0.02, -B, distFloor + 0.025
	rotx 90

	if bCustPic and stCustPictName <> "" then
		picture stCustPictName, boardWidth - 0.04, boardHeight - 0.045, 1
	else
		picture 1, boardWidth - 0.04, boardHeight - 0.045, 1
	endif

	del 2
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================

