
call "FM_types" parameters all

ac_bottomlevel = (iSupType = WALL_MOUNTED) * distFloor
parameters ac_bottomlevel = ac_bottomlevel

ac_toplevel = topHeight
parameters ac_toplevel = ac_toplevel


values "A" range [0.2, )
values "B" range [0.01, )

values "boardWidth" range [0.2, )
values "boardHeight" range [0.1, )
values "distFloor" range [0.2 * (iSupType = FLOOR_MOUNTED), )

topHeight = distFloor + boardHeight
parameters topHeight = topHeight
lock "topHeight"

if GLOB_MODPAR_NAME = "A" then
	boardWidth = A - (bSpeaker) * 0.36
	parameters boardWidth = boardWidth
else
	A = boardWidth + (bSpeaker) * 0.36
	parameters A = A
endif


if GLOB_MODPAR_NAME = "ZZYZX" then
	if iSupType = FLOOR_MOUNTED then
		distFloor = max(ZZYZX - boardHeight, (iSupType = FLOOR_MOUNTED) * 0.2)
		if ABS(distFloor - (0.2 * (iSupType = FLOOR_MOUNTED))) < EPS then
			boardHeight = ZZYZX - distFloor
		endif
		parameters distFloor = distFloor, boardHeight = boardHeight
	else
		boardHeight = ZZYZX
		parameters boardHeight = boardHeight
	endif
else
	ZZYZX = boardHeight + (iSupType = FLOOR_MOUNTED) * distFloor
	parameters ZZYZX = ZZYZX
endif


values "stSupType" stSupportValues
values "iSupType" WALL_MOUNTED, FLOOR_MOUNTED

if GLOB_MODPAR_NAME = "stSupType" then
	iSupType = 1
	if stSupType = stSupportValues[2] then iSupType = FLOOR_MOUNTED
	parameters iSupType = iSupType
else
	iSupType = max(1, min(iSupType, vardim1(stSupportValues)))
	stSupType = stSupportValues[iSupType]
	parameters stSupType = stSupType, iSupType = iSupType
endif

if not(bSpeaker) then 
	hideparameter "speakBoxMat", "speakFabMat"
	lock "speakBoxMat", "speakFabMat"
endif

if not(bPicture) then
	hideparameter "bCustPic", "stCustPictName"
	lock "bCustPic", "stCustPictName"
endif

if not(bCustPic) then
	hideparameter "stCustPictName"
	lock "stCustPictName"
endif


values "gs_detlevel_3D" stDetlevel3D
values "gs_detlevel_3D_m" DET3D_DETAILED, DET3D_SIMPLE, DET3D_OFF

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_DETAILED
	if gs_detlevel_3D = stDetlevel3D[2]	then gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetlevel3D[3]	then gs_detlevel_3D_m = DET3D_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetlevel3D[1]
	if gs_detlevel_3D_m = DET3D_SIMPLE	then gs_detlevel_3D = stDetlevel3D[2]
	if gs_detlevel_3D_m = DET3D_OFF		then gs_detlevel_3D = stDetlevel3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

values "gs_resol" range [8, )

if gs_detlevel_3D_m <> DET3D_DETAILED then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif

if not(bHolder) then
	hideparameter "holderMat"
	lock "holderMat"
endif


values "gs_ui_current_page" 1, 2


