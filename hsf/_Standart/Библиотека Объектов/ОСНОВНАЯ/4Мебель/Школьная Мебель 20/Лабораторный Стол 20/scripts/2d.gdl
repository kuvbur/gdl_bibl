

unID = 1

! ======================================================================
! Hotspots
! ======================================================================

hotspot2	0,	0,	unID, A, 1 + 256,	tableLength:	unID = unID + 1
hotspot2	-1,	0,	unID, A, 3,			tableLength:	unID = unID + 1
hotspot2	A,	0,	unID, A, 2, 		tableLength:	unID = unID + 1

hotspot2	0,	B,	unID, A, 1 + 256,	tableLength:	unID = unID + 1
hotspot2	-1,	B,	unID, A, 3,			tableLength:	unID = unID + 1
hotspot2	A,	B,	unID, A, 2,			tableLength:	unID = unID + 1

hotspot2	0,	0,	unID, B, 1 + 256,	tableDepth:		unID = unID + 1
hotspot2	0,	-1,	unID, B, 3,			tableDepth:		unID = unID + 1
hotspot2	0,	B,	unID, B, 2,			tableDepth:		unID = unID + 1

hotspot2	A,	0,	unID, B, 1 + 256,	tableDepth:		unID = unID + 1
hotspot2	A,	-1,	unID, B, 3,			tableDepth:		unID = unID + 1
hotspot2	A,	B,	unID, B, 2,			tableDepth:		unID = unID + 1


! ======================================================================
! Minimal Space
! ======================================================================

drawindex 5
reqBMinSpace = 0
minSpaceUnID = 3000
call "fa_minimalspace" parameters all MSType = 0, unID = 3000,
							returned_parameters reqBMinSpace,
												MSFillType,
												MSFillPen,
												MSFillBackPen,
												MSContLineType,
												MSContLinePen

if reqBMinSpace <> 0 then
	drawindex 10
	FILL MSFillType
	pen MSContLinePen
	line_type MSContLineType

	if MSFront > EPS then
		poly2_b 6, 1+2+4+64, MSFillPen, MSFillBackPen,
			-MSSide, -MSFront, 1,
			a + MSSide2, -MSFront, 1,
			a + MSSide2, 0, (MSSide2 > EPS and NOT(bDoubleRow)),
			a, 0, 0,
			0, 0, (MSSide > EPS and NOT(bDoubleRow)),
			-MSSide, 0, 1
	endif

	if bDoubleRow then
		if MSFront > EPS then
			poly2_b 6, 1+2+4+64, MSFillPen, MSFillBackPen,
				-MSSide, b, 0,
				0, b, 0,
				a, b, 0,
				a + MSSide2, b, 1,
				a + MSSide2, b + MSFront, 1,
				-MSSide, b + MSFront, 1
		endif

		if MSSide > EPS then
			poly2_b 4, 1+2+4+64, MSFillPen, MSFillBackPen,
				0, 0, 0,
				0, b, (MSFront < EPS),
				-MSSide, b, 1,
				-MSSide, 0, (MSFront < EPS)
		endif
		if MSSide2 > EPS then
			poly2_b 4, 1+2+4+64, MSFillPen, MSFillBackPen,
				a, 0, 0,
				a, b, (MSFront < EPS),
				a + MSSide2, b, 1,
				a + MSSide2, 0, (MSFront < EPS)
		endif
	endif

	if MSFront > EPS then
		hotspot2 -MSSide,				-MSFront,						minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
		hotspot2 a + MSSide2,			-MSFront,						minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
	endif

	if	(MSFront > EPS and NOT(bDoubleRow)) |\
		(MSFront < EPS and MSSide > EPS and bDoubleRow) |\
		(MSFront > EPS and MSSide < EPS and bDoubleRow) then
		hotspot2 -MSSide,			0,								minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
	endif
	if	(MSFront > EPS and NOT(bDoubleRow)) |\
		(MSFront < EPS and MSSide2 > EPS and bDoubleRow) |\
		(MSFront > EPS and MSSide2 < EPS and bDoubleRow) then
		hotspot2 a + MSSide2,		0,								minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
	endif

	if bDoubleRow then
		if	(MSFront < EPS and MSSide > EPS) |\
			(MSFront > EPS and MSSide < EPS) then
			hotspot2 -MSSide,		b,								minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
		endif
		if	(MSFront < EPS and MSSide2 > EPS) |\
			(MSFront > EPS and MSSide2 < EPS) then
			hotspot2 a + MSSide2,	b,								minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
		endif

		if MSFront > EPS then
			hotspot2 -MSSide,			b + MSFront,		minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
			hotspot2 a + MSSide2,		b + MSFront,		minSpaceUnID : minSpaceUnID = minSpaceUnID + 1
		endif
	endif
endif


pen gs_cont_pen
line_type 1
fill gs_fill_type

DrawingNum = 1

! ======================================================================
! Counter
! ======================================================================

drawindex 10

poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
	0, 0, 1,
	tableLength, 0, bShow2DRightEdge,
	tableLength, fullTableDepth, 1,
	0, fullTableDepth, bShow2DLeftEdge

if bDoubleRow then
	drawindex 50

	numLine	= 6
	for i = 1 to numLine
		line2 0, tableDepth, tableLength / (numLine * 2 - 1), tableDepth

		add2 tableLength / (numLine * 2 - 1) * 2, 0
	next i
	del numLine

	drawindex 10
endif


! ======================================================================
! Sink
! ======================================================================

if bSinkExist then
	for si = 1 to iNumSink
		if si = 1 then
			sinkPosX	= sinkDefPosX + sink1DistX
			sinkPosY	= sinkDefPosY + sink1DistY

			add2 sinkDefPosX, sinkDefPosY
				hotspot2 0,				sink1DistY,	unID, sink1DistX, 1 + 128:		unID = unID + 1
				hotspot2 -1,			sink1DistY,	unID, sink1DistX, 3:			unID = unID + 1
				hotspot2 sink1DistX,	sink1DistY,	unID, sink1DistX, 2:			unID = unID + 1

				hotspot2 sink1DistX,	0,				unID, sink1DistY, 1 + 128:		unID = unID + 1
				hotspot2 sink1DistX,	-1,				unID, sink1DistY, 3:			unID = unID + 1
				hotspot2 sink1DistX,	sink1DistY,		unID, sink1DistY, 2:			unID = unID + 1
			del 1

			add2 sinkPosX, sinkPosY
				hotspot2	0,									-sink_b / 2,						unID, sink1RotAngle,	4 + 128	: unID = unID + 1
				hotspot2	sink_b / 2 * SIN(sink1RotAngle),	-sink_b / 2 * COS(sink1RotAngle),	unID, sink1RotAngle,	5			: unID = unID + 1
				hotspot2	0,									0,									unID, sink1RotAngle,	6			: unID = unID + 1
			del 1
		else
			sinkPosX	= sinkDefPosX + sink2DistX
			sinkPosY	= sinkDefPosY + sink2DistY

			add2 sinkDefPosX, sinkDefPosY
				hotspot2 0,				sink2DistY,	unID, sink2DistX, 1 + 128:		unID = unID + 1
				hotspot2 -1,			sink2DistY,	unID, sink2DistX, 3:			unID = unID + 1
				hotspot2 sink2DistX,	sink2DistY,	unID, sink2DistX, 2:			unID = unID + 1

				hotspot2 sink2DistX,	0,				unID, sink2DistY, 1 + 128:		unID = unID + 1
				hotspot2 sink2DistX,	-1,				unID, sink2DistY, 3:			unID = unID + 1
				hotspot2 sink2DistX,	sink2DistY,		unID, sink2DistY, 2:			unID = unID + 1
			del 1

			add2 sinkPosX, sinkPosY
				hotspot2	0,									-sink_b / 2,						unID, sink2RotAngle,	4 + 128	: unID = unID + 1
				hotspot2	sink_b / 2 * SIN(sink2RotAngle),	-sink_b / 2 * COS(sink2RotAngle),	unID, sink2RotAngle,	5			: unID = unID + 1
				hotspot2	0,									0,									unID, sink2RotAngle,	6			: unID = unID + 1
			del 1
		endif

		add2 sinkPosX, sinkPosY
		rot2 sink1RotAngle * (si = 1) + sink2RotAngle * (si = 2)
			add2 -sink_a / 2, -sink_b / 2
				call "fa_sinktyp" parameters all	a				= sink_a,
													b				= sink_b,
													zzyzx			= sink_z,
													sunder			= bSinkUnderMounted,
													ConPosX_3		= drainPosX,
													ConPosY_3		= drainPosY,
													ConPosX_4		= drainPosX,
													ConPosY_4		= drainPosY,
													count_th		= topThk,
													gs_tap_type_m	= 0,
													bEnableTapEdit	= 0
			del 1

			if gs_tap_type_m > 0 then
				add2 tapDefPosX, tapDefPosY
					call "laboratoryTap_m" parameters all	tapID		= si,
												start_unID				= si * 100
				del 1
			endif
		del 2

		rot2 180
		add2 -tableLength, -fullTableDepth
	next si
	del 2 * iNumSink
endif


! ======================================================================
! Shelves, Backsplash
! ======================================================================

if bShelves | bEnableBsplash then
	add2 0, tableDepth
		call "laboratoryAccessories_m" parameters all	start_unID		= 300
	del 1
endif


! ======================================================================
! Gas Connection
! ======================================================================

if bGasConnOnTable then
	drawindex 20

	add2 gasConnDefPosX, gasConnDefPosY
		for gcNum = 1 to 1 + bDoubleRow
			gosub "gas connections"
	
			if iArrangement = ARRANGE_MIRR then
				mul2 1, 1 - bDoubleRow * 2
			else
				rot2 180
			endif
		next gcNum
		del 1 + bDoubleRow
	del 1
endif


! ======================================================================
! MEP Connections
! ======================================================================

if bShowMEP then
	drawindex 10

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED)) + SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left

				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 12 + bDoubleRow,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 12 + bDoubleRow,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 12 + bDoubleRow,
		ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 12 + bDoubleRow,
		ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConPosZ_5=ConPosZ_5, ConLength_5=ConLength_5, ConEdit_5 = 12 + bDoubleRow,
		ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConPosZ_6=ConPosZ_6, ConLength_6=ConLength_6, ConEdit_6 = 12 + bDoubleRow,
		ConPosX_7=ConPosX_7, ConPosY_7=ConPosY_7, ConPosZ_7=ConPosZ_7, ConLength_7=ConLength_7, ConEdit_7 = 12 + bDoubleRow
endif

! ======================================================================
! middle hotspot
! ======================================================================

hotspot2 tableLength/2, fullTableDepth/2, 	unID : unID = unID + 1

!================================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
!================================================================================

! ======================================================================
"gas connections":
! ======================================================================

	hotspot2 0,				-gasConnDistY,	unID, gasConnDistX, 1 + 128:	unID = unID + 1
	hotspot2 -1,			-gasConnDistY,	unID, gasConnDistX, 3:			unID = unID + 1
	hotspot2 gasConnDistX,	-gasConnDistY,	unID, gasConnDistX, 2:			unID = unID + 1

	hotspot2 gasConnDistX,	0,				unID, gasConnDistY, 1 + 128:	unID = unID + 1
	hotspot2 gasConnDistX,	1,				unID, gasConnDistY, 3:			unID = unID + 1
	hotspot2 gasConnDistX,	-gasConnDistY,	unID, gasConnDistY, 2:			unID = unID + 1

	add2 gasConnDistX - gasConnDist * (gasConnNum - 1) / 2, -gasConnDistY
		for gc = 1 to 4
			poly2_b 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-gasConnWidth / 2, 0, 1,
				gasConnWidth / 2, 0, 1,
				gasConnWidth / 2, -gasConnThk - gasConnThk2, 1,
				-gasConnWidth / 2, -gasConnThk - gasConnThk2, 1
			
			add2 gasConnDist, 0
		next gc
		del 4
	del 1

return



