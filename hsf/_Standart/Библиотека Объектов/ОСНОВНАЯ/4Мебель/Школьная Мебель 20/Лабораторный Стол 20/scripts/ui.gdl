
! ======================================================================
! UI
! ======================================================================

ui_dialog `Параметры Лабораторного Стола`

dim	stUIOutfields[81]
	stUIOutfields[ 1] = `Два Ряда`
	stUIOutfields[ 2] = `Расположение`
	stUIOutfields[ 3] = `Толщина Столешницы`
	stUIOutfields[ 4] = `Полки`
	stUIOutfields[ 5] = `Стыковка`
	stUIOutfields[ 6] = `Крепление`
	stUIOutfields[ 7] = `Сторона 1`
	stUIOutfields[ 8] = `Сторона 2`

	stUIOutfields[ 9] = `Высота Полок`
	stUIOutfields[10] = `Глубина Полок`
	stUIOutfields[11] = `Количество Полок`
	stUIOutfields[12] = `Толщина Полок`
	stUIOutfields[13] = `Толщина Обвязки`
	stUIOutfields[14] = `Сушка Пробирок`
	stUIOutfields[15] = `Электророзетки`
	stUIOutfields[16] = `Фартук`
	stUIOutfields[17] = `Высота Фартука`
	stUIOutfields[18] = `Толщина Фартука`

	stUIOutfields[19] = `Стиль Ножек`
	stUIOutfields[20] = `Стиль 1`
	stUIOutfields[21] = `Стиль 2`
	stUIOutfields[22] = `Стиль 3`
	stUIOutfields[23] = `Стиль 4`
	stUIOutfields[24] = `Стиль 5`
	stUIOutfields[25] = `Стиль 6`
	stUIOutfields[26] = `Стиль 7`
	stUIOutfields[27] = `Стиль 8`
	stUIOutfields[28] = `Нет`

	stUIOutfields[29] = `Передняя Панель`
	stUIOutfields[30] = `Высота`
	stUIOutfields[31] = `Отметка Верха`
	stUIOutfields[32] = `Тумба`
	stUIOutfields[33] = `Крепление Тумб`
	stUIOutfields[34] = `Высота Основания`
	stUIOutfields[35] = `Расстояние от Пола`
	stUIOutfields[36] = `Фаска Торца`
	stUIOutfields[37] = `Стиль Ручек`
	stUIOutfields[38] = `По Гориз. - Сторона 1`
	stUIOutfields[39] = `По Гориз. - Сторона 2`
	stUIOutfields[40] = `Привязка по Вертик.`
	stUIOutfields[41] = `Верх`
	stUIOutfields[42] = `Середина`
	stUIOutfields[43] = `Вертикальный`

	stUIOutfields[44] = `Тип Торца Столешницы`
	stUIOutfields[45] = `Свесы Столешницы`
	stUIOutfields[46] = `Мойка`
	stUIOutfields[47] = `Кол-во Моек`
	stUIOutfields[48] = `Врезная`
	stUIOutfields[49] = `Смеситель`
	stUIOutfields[50] = `Подключение Газа на Столе`
	stUIOutfields[51] = `От Центра по X`
	stUIOutfields[52] = `От Края Стола по Y`

	stUIOutfields[53] = `Столешница`
	stUIOutfields[54] = `Ножки`
	stUIOutfields[55] = `Тумба`
	stUIOutfields[56] = `Основания Тумб`
	stUIOutfields[57] = `Ящик`
	stUIOutfields[58] = `Ручка`
	stUIOutfields[59] = `Передняя Панель`
	stUIOutfields[60] = `Электророзетки`
	stUIOutfields[61] = `Мойка`
	stUIOutfields[62] = `Смеситель`
	stUIOutfields[63] = `Полки`
	stUIOutfields[64] = `Обвязка Полок`
	stUIOutfields[65] = `Сушка`
	stUIOutfields[66] = `Соединение`
	stUIOutfields[67] = `Колбы 1`
	stUIOutfields[68] = `Колбы 2`
	stUIOutfields[69] = `Соединение`

	stUIOutfields[70] = `Показ Торцов`
	stUIOutfields[71] = `2D-отображение`
	stUIOutfields[72] = `Перо Контура`
	stUIOutfields[73] = `Тип Штриховки`
	stUIOutfields[74] = `Перо Штриховки`
	stUIOutfields[75] = `Перо Фона`
	stUIOutfields[76] = `3D-отображение`
	stUIOutfields[77] = `3D`
	stUIOutfields[78] = `Аппроксимация`
	stUIOutfields[79] = `Отбрасывание Теней`
	stUIOutfields[80] = `Колбы в 3D`
	stUIOutfields[81] = `Дополнительные Узловые Точки`


dim	matParName[16]
	matParName[ 1]	= "count_mat"
	matParName[ 2]	= "gs_leg_mat"
	matParName[ 3]	= "gs_cabinet_mat"
	matParName[ 4]	= "gs_base_mat"
	matParName[ 5]	= "gs_door_mat"
	matParName[ 6]	= "gs_knob_mat"
	matParName[ 7]	= "gs_frontPanel_mat"
	matParName[ 8]	= "powerOutletmat"
	matParName[ 9]	= "gs_sink_mat"
	matParName[10]	= "gs_tap_mat"
	matParName[11]	= "shelfMat"
	matParName[12]	= "shelfFrameMat"
	matParName[13]	= "dryingRackMat"
	matParName[14]	= "gs_con_mat"
	matParName[15]	= "flaskMat1"
	matParName[16]	= "flaskMat2"

dim	matFlag[16]
	matFlag[ 1]	= 0
	matFlag[ 2]	= (NOT(bEnableLeftLeg) and NOT(bEnableRightLeg) and NOT(bEnableLeftLegReinforce) and NOT(bEnableRightLegReinforce))
	matFlag[ 3]	= (NOT(bOfficeCab1) and NOT(bOfficeCab2))
	matFlag[ 4]	= ((NOT(bOfficeCab1) and NOT(bOfficeCab2)) | iCabinetSupportType = CAB_TABLE_MOUNTED)
	matFlag[ 5]	= (NOT(bOfficeCab1) and NOT(bOfficeCab2))
	matFlag[ 6]	= ((NOT(bOfficeCab1) and NOT(bOfficeCab2)) | gs_knob_type_m = 0)
	matFlag[ 7]	= (NOT(bFrontPanel) | NOT(bEnableFrontPanel))
	matFlag[ 8]	= ((NOT(bShelves) | NOT(bPowerOutletShelf)) and (NOT(bsplashRear) | NOT(bPowerOutletBsplash)))
	matFlag[ 9]	= (gs_sink_type_m = 0)
	matFlag[10]	= (gs_sink_type_m = 0 | gs_tap_type_m = 0)
	matFlag[11]	= (NOT(bShelves) | (iNumShelf = 0 and NOT(bPowerOutletShelf)))
	matFlag[12]	= NOT(bShelves)
	matFlag[13]	= (NOT(bShelves) | (iTestTubeRack = DRYINGRACK_NONE and NOT(bFlasks)))
	matFlag[14]	= (NOT(ConStatus_1) AND NOT(ConStatus_2) AND NOT(ConStatus_3) and\
					NOT(ConStatus_4) AND NOT(ConStatus_5) AND NOT(ConStatus_6) AND NOT(ConStatus_7))
	matFlag[15]	= (iNumShelf = 0 | NOT(bShelves) | NOT(bFlasks))
	matFlag[16]	= (iNumShelf = 0 | NOT(bShelves) | NOT(bFlasks))

dim	stUITooltip[17]
	stUITooltip[ 1] = `Длина Стола`
	stUITooltip[ 2] = `Глубина Стола`
	stUITooltip[ 3] = `Высота Стола`
	stUITooltip[ 4] = `Глубина Полок`
	stUITooltip[ 5] = `Высота Полок`
	stUITooltip[ 6] = `Соединение со Стороны 1`
	stUITooltip[ 7] = `Соединение со Стороны 2`
	stUITooltip[ 8] = `Фартук Сзади`
	stUITooltip[ 9] = `Фартук со Стороны 1`
	stUITooltip[10] = `Фартук со Стороны 2`
	stUITooltip[11] = `Выступ Сзади`
	stUITooltip[12] = `Выступ Спереди`
	stUITooltip[13] = `Выступ Слева`
	stUITooltip[14] = `Выступ Справа`
	stUITooltip[15] = `Минимальное Пространство Сбоку 1`
	stUITooltip[16] = `Минимальное Пространство Сбоку 2`
	stUITooltip[17] = `Минимальное Пространство Спереди`


! =============================================================================
! Tabpage Selector Control
! =============================================================================

UI_CURRENT_PAGE gs_ui_current_page


uiTotalWidth		= 444
uiTotalHeight		= 266
xCenter				= uiTotalWidth / 2
yCenter				= uiTotalHeight / 2 + 14

textElevInfield		= 3 + 1
textElevBoolean		= 3
textElevPen			= 4
textElevMaterial	= 6 + 1
infieldHeight		= 19
booleanHeight		= 19
penHeight			= 19
penWidth			= 33
matHeight			= 25
lineTypeHeight		= 25
outfieldHeight		= 15

pageStartY = 5


! ==============================================================================
! Display Active UI pages
! ==============================================================================

ui_page iPageIDPreset, iPageIDRoot, `Преднастроенные Стили`, "uiTab_Style_flat(1)"
	gosub "presetPage"


ui_page iPageIDGeneral, iPageIDRoot, `Общие Параметры`, "uiTab_Dimension_flat(1)"
	gosub "generalPage"


ui_page iPageIDExtensions, iPageIDRoot, `Дополнительные Элементы`, "uiTab_FurnitureExtension_flat(1)"
	gosub "extensionsPage"


ui_page iPageIDAccessories, iPageIDRoot, `Аксессуары`, "uiTab_Cabinet_flat(1)"
	gosub "accessoriesPage"

if bOfficeCab1 then
	ui_page iPageIDDrawer1, iPageIDRoot, `Ящики - Сторона 1`, "uiTab_Drawer_flat(1)"
		gosub "drawer1Page"
endif

if bOfficeCab2 then
	ui_page iPageIDDrawer2, iPageIDRoot, `Ящики - Сторона 2`, "uiTab_Drawer_flat(1)"
		gosub "drawer2Page"
endif

ui_page iPageIDCounter, iPageIDRoot, `Столешница`, "uiTab_FurnitureCounter_flat(1)"
	gosub "counterPage"

ui_page iPageIDSurfaces, iPageIDRoot, `Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "surfacesPage"

ui_page iPageIDRepresentation, iPageIDRoot, `Отображение`, "uiTab_2DRepr_flat(1)"
	gosub "representationPage"

ui_page iPageIDMinimalSpace, iPageIDRoot, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
	gosub "minimalspacePage"

if isMEPEnabled then
	ui_page iPageIDMEP, iPageIDRoot, `Соединения`, "uiTab_Connections_flat(1)"
		gosub "mepPage"
endif

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"presetPage":
! ------------------------------------------------------------------------------
	ui_infield{3} "iPresetStyle", 0, pageStartY-2, 418, 228,
		1, 37, 6, 1,		!labUI_presetPict.png
		131, 112, 118, 92,
		1, stPresetStyle[1], 1,
		2, stPresetStyle[2], 2,
		3, stPresetStyle[3], 3,
		4, stPresetStyle[4], 4,
		5, stPresetStyle[5], 5,
		6, stPresetStyle[6], 6


return

! ------------------------------------------------------------------------------
"generalPage":
! ------------------------------------------------------------------------------

sepXPosFromCenter	= 70

totalColWidth	= 215 + sepXPosFromCenter
xPos			= 0
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 17

ui_separator uiTotalWidth / 2 + sepXPosFromCenter, yCurrent + fieldVerDist + textElevInfield + textElevBoolean, uiTotalWidth / 2 + sepXPosFromCenter, uiTotalHeight - 5

yCurrent = yCurrent - textElevBoolean
infieldWidth	= totalColWidth - 100
ui_infield{3} "bDoubleRow", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[ 1], 0,
	0, stUIOutfields[ 1], 1
yCurrent = yCurrent + textElevBoolean

infieldWidth	= 90
iFlag			= NOT(bDoubleRow)
ui_outfield stUIOutfields[ 2], xPos + 100, yCurrent, totalColWidth - infieldWidth - 2 - 100, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "iArrangement", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stArrangement[1], 1,
	0, stArrangement[2], 2
yCurrent = yCurrent + fieldVerDist + textElevInfield + 10

ui_separator 0, yCurrent - 8, uiTotalWidth, yCurrent - 8

ui_outfield stUIOutfields[ 3], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
yCurrent = yCurrent - textElevInfield
ui_infield{3} "topThk", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

yCurrent = yCurrent - textElevBoolean
infieldWidth	= 90
ui_style 0, 1
ui_infield{3} "bShelves", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[ 4], 0,
	0, stUIOutfields[ 4], 1
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist + textElevBoolean

if bDoubleRow then
	if bShelves then
		xPict		= 100
		yPict		= 120
		xPictPos	= totalColWidth / 2 - 10
		yPictPos	= uiTotalHeight - yPict - 25-35+pageStartY

		ui_pict 28, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!ui_MSLabWS_simplerow.png
	else
		xPict		= 109
		yPict		= 89
		xPictPos	= totalColWidth / 2 - 10
		yPictPos	= uiTotalHeight - yPict - 25-35+pageStartY

		ui_pict 27, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!ui_MSLabWS_simplerow.png
	endif
else
	if bShelves then
		xPict		= 87
		yPict		= 117
		xPictPos	= totalColWidth / 2 - 10
		yPictPos	= uiTotalHeight - yPict - 25-35+pageStartY

		ui_pict 34, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!ui_MSLabWS_simplerow.png
	else
		xPict		= 89
		yPict		= 119
		xPictPos	= totalColWidth / 2 - 10
		yPictPos	= uiTotalHeight - yPict - 25-35+pageStartY

		ui_pict 33, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!ui_MSLabWS_simplerow.png
	endif
endif

ui_infield "tableLength", xPictPos - xPict / 2 - infieldWidth / 2, yPictPos + yPict + 2, infieldWidth, infieldHeight			ui_tooltip stUITooltip[ 1]
ui_infield "tableDepth", xPictPos + xPict / 2 + 2, yPictPos + yPict + 2, infieldWidth, infieldHeight							ui_tooltip stUITooltip[ 2]
ui_infield "tableHeight", xPictPos + xPict / 2 + 2, yPictPos + yPict - 45, infieldWidth, infieldHeight							ui_tooltip stUITooltip[ 3]

if bShelves then
	ui_infield "shelfDepth", xPictPos - xPict / 2 - infieldWidth / 2, yPictPos - infieldHeight - 2, infieldWidth, infieldHeight	ui_tooltip stUITooltip[ 4]
	ui_infield "shelfHeight", xPictPos + xPict / 2 + 2, yPictPos + yPict - 80, infieldWidth, infieldHeight						ui_tooltip stUITooltip[ 5]
endif



totalColWidth	= 215 - 1 - sepXPosFromCenter
xPos			= 230 - 1 + sepXPosFromCenter
yPos			= pageStartY
yCurrent		= yPos + fieldVerDist + textElevBoolean + 10
fieldVerDist	= 22

infieldWidth	= 0
ui_style 0, 1
ui_outfield stUIOutfields[ 5], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0

if bDoubleRow then
	xPict		= 78
	yPict		= 65
	xPictPos	= xCenter + sepXPosFromCenter + totalColWidth / 2 + 5
	yPictPos	= pageStartY+55

	if iArrangement = ARRANGE_MIRR then
		if bConnectionLeft and bConnectionRight				then ui_pict 20, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_mirrored gang both.png
		if bConnectionLeft and NOT(bConnectionRight)		then ui_pict 21, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_mirrored gang left.png
		if NOT(bConnectionLeft) and bConnectionRight		then ui_pict 22, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_mirrored gang right.png
		if NOT(bConnectionLeft) and NOT(bConnectionRight)	then ui_pict 19, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_mirrored.png
	else
		if bConnectionLeft and bConnectionRight				then ui_pict 24, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_rotated gang both.png
		if bConnectionLeft and NOT(bConnectionRight)		then ui_pict 25, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_rotated gang left.png
		if NOT(bConnectionLeft) and bConnectionRight		then ui_pict 26, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_rotated gang right.png
		if NOT(bConnectionLeft) and NOT(bConnectionRight)	then ui_pict 23, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_double_rotated.png
	endif
else
	xPict		= 78
	yPict		= 33
	xPictPos	= xCenter + sepXPosFromCenter + totalColWidth / 2 + 5
	yPictPos	= pageStartY+70

	if bConnectionLeft and bConnectionRight				then ui_pict 30, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!labUI_simple gang both.png
	if bConnectionLeft and NOT(bConnectionRight)		then ui_pict 31, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!labUI_simple gang left.png
	if NOT(bConnectionLeft) and bConnectionRight		then ui_pict 32, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!labUI_simple gang right.png
	if NOT(bConnectionLeft) and NOT(bConnectionRight)	then ui_pict 29, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!labUI_simple.png
endif

ui_infield{3} "bConnectionLeft", xPictPos - xPict / 2 - infieldHeight - 2, yPictPos + 10 + 15 * bDoubleRow, infieldHeight, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, "", 0,
	0, "", 1	ui_tooltip stUITooltip[ 6]

ui_infield{3} "bConnectionRight", xPictPos + xPict / 2 + 5, yPictPos + 10 + 15 * bDoubleRow, infieldHeight, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, "", 0,
	0, "", 1	ui_tooltip stUITooltip[ 7]

yCurrent		= pageStartY+125
infieldWidth	= 0
ui_style 0, 1
ui_outfield stUIOutfields[ 6], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist + textElevInfield - 5

infieldWidth	= 0
iFlag			= NOT(bConnectionLeft)
ui_outfield stUIOutfields[ 7], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield + fieldVerDist
infieldWidth	= 130
ui_infield{3} "iReinforceLeft", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stReinforcement[1], 0,
	0, stReinforcement[2], 1,
	0, stReinforcement[3], 2
yCurrent = yCurrent + fieldVerDist + textElevInfield

infieldWidth	= 0
iFlag			= NOT(bConnectionRight)
ui_outfield stUIOutfields[ 8], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield + fieldVerDist
infieldWidth	= 130
ui_infield{3} "iReinforceRight", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stReinforcement[1], 0,
	0, stReinforcement[2], 1,
	0, stReinforcement[3], 2
yCurrent = yCurrent + fieldVerDist + textElevInfield

return

! ------------------------------------------------------------------------------
"extensionsPage":
! ------------------------------------------------------------------------------

sepXPosFromCenter	= 0

totalColWidth	= 215 + sepXPosFromCenter
xPos			= 230 - 230
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 25

ui_separator uiTotalWidth / 2 + sepXPosFromCenter, yCurrent, uiTotalWidth / 2 + sepXPosFromCenter, uiTotalHeight - 5

yCurrent = yCurrent - textElevBoolean
infieldWidth	= 0
ui_style 0, 1
ui_infield{3} "bShelves", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[ 4], 0,
	0, stUIOutfields[ 4], 1
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist + textElevBoolean

iFlag			= NOT(bShelves)
infieldWidth	= 90
ui_outfield stUIOutfields[ 9], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "shelfHeight", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

infieldWidth	= 90
ui_outfield stUIOutfields[10], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "shelfDepth", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

infieldWidth	= 90
ui_outfield stUIOutfields[11], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "iNumShelf", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, "0", 0,
	0, "1", 1,
	0, "2", 2,
	0, "3", 3,
	0, "4", 4,
	0, "5", 5,
	0, "6", 6
yCurrent = yCurrent + fieldVerDist + textElevInfield

iFlag			= (NOT(bShelves) | iNumShelf = 0)
infieldWidth	= 90
ui_outfield stUIOutfields[12], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "shelfThk", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

iFlag			= NOT(bShelves)
infieldWidth	= 90
ui_outfield stUIOutfields[13], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "shelfFrameThk", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

ui_outfield stUIOutfields[14], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
ui_style 0, 0
yCurrent = yCurrent - textElevInfield
ui_infield{3} "iTestTubeRack", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stOfficeCab[1], 0,
	0, stOfficeCab[2], 1,
	0, stOfficeCab[3], 2,
	0, stOfficeCab[4], 3
yCurrent = yCurrent + fieldVerDist + textElevInfield

yCurrent = yCurrent - textElevBoolean
infieldWidth	= 90
ui_infield{3} "bPowerOutletShelf", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[15], 0,
	0, stUIOutfields[15], 1
yCurrent = yCurrent + fieldVerDist + textElevBoolean



totalColWidth	= 215 - 1 - sepXPosFromCenter
xPos			= 230 - 1 + sepXPosFromCenter
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 25

infieldWidth	= 0
ui_style 0, 1
ui_outfield stUIOutfields[16], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0

xPict		= 113
yPict		= 57
xPictPos	= xCenter + sepXPosFromCenter + totalColWidth / 2 + 5
yPictPos	= pageStartY+60 + 20 * bDoubleRow

if NOT(bsplashLeft) and NOT(bsplashRight) and NOT(bsplashRear)	then ui_pict 11, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1					!labUI_bs_simple.png
if (bsplashLeft) and NOT(bsplashRight) and NOT(bsplashRear)		then ui_pict 12, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_simple_left.png
if NOT(bsplashLeft) and (bsplashRight) and NOT(bsplashRear)		then ui_pict 17, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_simple_right.png
if NOT(bsplashLeft) and NOT(bsplashRight) and (bsplashRear)		then ui_pict 16, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_simple_rear.png
if (bsplashLeft) and (bsplashRight) and NOT(bsplashRear)		then ui_pict 14, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!labUI_bs_simple_leftright.png
if NOT(bsplashLeft) and (bsplashRight) and (bsplashRear)		then ui_pict 18, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!labUI_bs_simple_rightrear.png
if (bsplashLeft) and NOT(bsplashRight) and (bsplashRear)		then ui_pict 13, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1		!labUI_bs_simple_leftrear.png
if bsplashLeft and bsplashRight and bsplashRear					then ui_pict 15, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1	!labUI_bs_simple_leftrightrear.png

ui_infield{3} "bsplashRear", xPictPos - infieldHeight / 2, yPictPos - 22, infieldHeight, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, "", 0,
	0, "", 1	ui_tooltip stUITooltip[ 8]

ui_infield{3} "bsplashLeft", xPictPos - xPict / 2 - infieldHeight - 2, yPictPos + 10 + 15 * bDoubleRow, infieldHeight, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, "", 0,
	0, "", 1	ui_tooltip stUITooltip[ 9]

ui_infield{3} "bsplashRight", xPictPos + xPict / 2 + 5, yPictPos + 10 + 15 * bDoubleRow, infieldHeight, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, "", 0,
	0, "", 1	ui_tooltip stUITooltip[10]

if bDoubleRow then
	xPictPos	= xPictPos - xPict / 2 + 2
	yPictPos	= yPictPos - 52
	xPict		= 3
	yPict		= 52
	if bsplashLeft then
		ui_pict 10, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_double_side.png
	else
		ui_pict 7, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_double_noleft.png
	endif

	xPictPos	= xPictPos + 113 - 3
	yPictPos	= yPictPos
	if bsplashRight then
		ui_pict 10, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_double_side.png
	else
		ui_pict 8, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_double_noright.png
	endif

	xPict		= 107
	yPict		= 1
	xPictPos	= xCenter + sepXPosFromCenter + totalColWidth / 2 + 5
	yPictPos	= yPictPos
	ui_pict 9, xPictPos - xPict / 2, yPictPos, xPict, yPict, 1			!labUI_bs_double_rear.png
endif

yCurrent = pageStartY+150
infieldWidth	= 90
iFlag			= (NOT(bsplashRear) and NOT(bsplashLeft) and NOT(bsplashRight))
ui_outfield stUIOutfields[17], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "bsplashHeight", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

ui_outfield stUIOutfields[18], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "bsplashThk", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

yCurrent = yCurrent - textElevBoolean
ui_infield{3} "bPowerOutletBsplash", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[15], 0,
	0, stUIOutfields[15], 1
yCurrent = yCurrent + fieldVerDist + textElevBoolean

return

! ------------------------------------------------------------------------------
"accessoriesPage":
! ------------------------------------------------------------------------------
sepXPosFromCenter	= 0

totalColWidth	= 215 + sepXPosFromCenter
xPos			= 230 - 230
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 22

ui_separator uiTotalWidth / 2 + sepXPosFromCenter, yCurrent, uiTotalWidth / 2 + sepXPosFromCenter, uiTotalHeight - 5

infieldWidth	= 70
ui_style 0, 1
ui_outfield stUIOutfields[19], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0
ui_infield{3} "gs_leg_type_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, 110,
	2, 5, 8, 4,		!labUIlegtypes.png
	70, 90, 70, 90,
	1, stUIOutfields[20], 1,
	2, stUIOutfields[21], 2,
	4, stUIOutfields[22], 3,
	5, stUIOutfields[23], 4,
	6, stUIOutfields[24], 5,
	8, stUIOutfields[25], 6
yCurrent = yCurrent + textElevInfield + 110 - 2

infieldWidth	= 0
ui_style 0, 1
ui_infield{3} "bFrontPanel", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[29], 0,
	0, stUIOutfields[29], 1
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist + textElevBoolean

infieldWidth	= 90
iFlag			= NOT(bFrontPanel)
ui_outfield stUIOutfields[30], xPos + 16, yCurrent, totalColWidth - infieldWidth - 2 - 16, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "gs_frontPanelHeight", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

ui_outfield stUIOutfields[31], xPos + 16, yCurrent, totalColWidth - infieldWidth - 2 - 16, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "gs_frontPanelTop", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield



totalColWidth	= 215 - 1 - sepXPosFromCenter
xPos			= 230 - 1 + sepXPosFromCenter
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 22

infieldWidth	= 90
ui_style 0, 1
ui_outfield stUIOutfields[32], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0
yCurrent = yCurrent - textElevInfield
ui_infield{3} "gs_officeCab_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stOfficeCab[1], 0,
	0, stOfficeCab[2], 1,
	0, stOfficeCab[3], 2,
	0, stOfficeCab[4], 3
yCurrent = yCurrent + fieldVerDist + textElevInfield

iFlag			= (NOT(bOfficeCab1) and NOT(bOfficeCab2))
ui_outfield stUIOutfields[33], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "iCabinetSupportType", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stCabinetSupportType[1], 1,
	0, stCabinetSupportType[2], 2
yCurrent = yCurrent + fieldVerDist + textElevInfield

if iCabinetSupportType = CAB_TABLE_MOUNTED then
	ui_outfield stUIOutfields[35], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
	yCurrent = yCurrent - textElevInfield
	ui_infield "cabDistFromFloor", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
else
	ui_outfield stUIOutfields[34], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
	yCurrent = yCurrent - textElevInfield
	ui_infield "gs_baseHeight", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
endif
yCurrent = yCurrent + fieldVerDist + textElevInfield

yCurrent = yCurrent - textElevBoolean
infieldWidth	= 90
ui_infield{3} "gs_bevel", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[36], 0,
	0, stUIOutfields[36], 1
yCurrent = yCurrent + fieldVerDist + textElevBoolean

ui_style 0, 1
infieldWidth	= 60
ui_outfield stUIOutfields[37], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
ui_style 0, 0
ui_infield{3} "gs_knob_type_m", xPos + totalColWidth - infieldWidth, yCurrent - 10, infieldWidth, 85,
	2, 4, 12, 4,		!labUIknobtypes.png
	60, 85, 60, 80,
	1, stUIOutfields[20], 1,
	2, stUIOutfields[21], 2,
	3, stUIOutfields[22], 3,
	4, stUIOutfields[23], 4,
	5, stUIOutfields[24], 5,
	6, stUIOutfields[25], 6,
	7, stUIOutfields[26], 7,
	8, stUIOutfields[27], 8,
	9, stUIOutfields[28], 0
yCurrent = yCurrent + textElevInfield + 80 - 2

infieldWidth	= 90
iFlag			= (NOT(bOfficeCab1) | gs_knob_type_m = 0)
ui_outfield stUIOutfields[38], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "gs_knobHorPos", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

iFlag			= (NOT(bOfficeCab2) | gs_knob_type_m = 0)
ui_outfield stUIOutfields[39], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "gs_knobHorPos_2", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

iFlag			= ((NOT(bOfficeCab1) and NOT(bOfficeCab2)) | gs_knob_type_m = 0)
ui_outfield stUIOutfields[40], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
ui_style 0, 0
yCurrent = yCurrent - textElevInfield
ui_infield{3} "gs_knobVerPos_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[41], 1,
	0, stUIOutfields[42], 0,
	0, stUIOutfields[43], -1
yCurrent = yCurrent + fieldVerDist + textElevInfield

return

! ------------------------------------------------------------------------------
"drawer1Page":
! ------------------------------------------------------------------------------
if iPageIDDrawer1 > 0 then
		call "OfficeCabinet01_m" parameters all iCabIndex		= 1,
												bOnlyDrawerUI	= 1,
												iPageIDDrawer	= 5,
												stDrawerTabTitles = `Ящики - Сторона 1`
endif

return

! ------------------------------------------------------------------------------
"drawer2Page":
! ------------------------------------------------------------------------------
if iPageIDDrawer2 > 0 then
		call "OfficeCabinet01_m" parameters all iCabIndex		= 2,
												bOnlyDrawerUI	= 1,
												iPageIDDrawer	= 6,
												stDrawerTabTitles = `Ящики - Сторона 2`
endif

return

! ------------------------------------------------------------------------------
"counterPage":
! ------------------------------------------------------------------------------
sepXPosFromCenter	= -15

totalColWidth	= 215 + sepXPosFromCenter
xPos			= 230 - 230
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 22

ui_separator uiTotalWidth / 2 + sepXPosFromCenter, yCurrent, uiTotalWidth / 2 + sepXPosFromCenter, uiTotalHeight - 5

infieldWidth	= 50
ui_style 0, 1
ui_outfield stUIOutfields[44], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0
yCurrent = yCurrent - textElevInfield
ui_infield{3} "iCounterType", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, 25,
	3, 3, 3, 1,		!labUIcounttypes.png
	44, 24, 40, 20,
	1, "", 1,
	2, "", 2
yCurrent = yCurrent + fieldVerDist + textElevInfield

ui_style 0, 1
ui_outfield stUIOutfields[45], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0

xPict		= 105
yPict		= 105
xPictPos	= 75
yPictPos	= pageStartY + 56

ui_pict 2, xPictPos, yPictPos, xPict, yPict, 1	!labUIcountTopView.png

infieldWidth	= 70
ui_infield "counterOvRear", xPictPos - infieldWidth - 2, yPictPos, infieldWidth, infieldHeight								ui_tooltip stUITooltip[11]
ui_infield "counterOvFront", xPictPos - infieldWidth - 2, yPictPos + yPict - infieldHeight, infieldWidth, infieldHeight		ui_tooltip stUITooltip[12]
ui_infield "counterOvLeft", xPictPos - infieldWidth + 50, yPictPos + yPict + 2, infieldWidth, infieldHeight					ui_tooltip stUITooltip[13]
ui_infield "counterOvRight", xPictPos - infieldWidth + 130, yPictPos + yPict + 2, infieldWidth, infieldHeight				ui_tooltip stUITooltip[14]



totalColWidth	= 215 - 1 - sepXPosFromCenter
xPos			= 230 - 1 + sepXPosFromCenter
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 22

infieldWidth	= 120
ui_style 0, 1
ui_outfield stUIOutfields[46], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0
yCurrent = yCurrent - textElevInfield
yCurrent = yCurrent + fieldVerDist + textElevInfield
infieldWidth	= 100
ui_infield{3} "gs_sink_type_m", xPos + 29, yCurrent, infieldWidth, 60,
	2, 6, 15, 3,		!labUIsinktypes.png
	80, 50, 80, 50,
	1, "", 1,
	2, "", 2,
	4, "", 4,
	7, "", 7,
	15, "", 0

infieldWidth	= 90
yCurrent		= yPos
iFlag			= (gs_sink_type_m = 0)
ui_style 0, 1
ui_outfield stUIOutfields[49], xPos + 138, yCurrent, infieldWidth, outfieldHeight, 4 * iFlag
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist - 5
ui_infield{3} "gs_tap_type_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[28], 0,
	0, stUIOutfields[20], 1,
	0, stUIOutfields[21], 2
yCurrent = yCurrent + fieldVerDist + textElevInfield

yCurrent = pageStartY+95

infieldWidth	= 35
ui_outfield stUIOutfields[47], xPos, yCurrent, totalColWidth - infieldWidth - 2 - 100, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "iNumSink", xPos + totalColWidth - infieldWidth - 100, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, "1", 1,
	0, "2", 2
yCurrent = yCurrent + fieldVerDist + textElevBoolean

infieldWidth	= 100
ui_infield{3} "bSinkUnderMounted", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[48], 0,
	0, stUIOutfields[48], 1
yCurrent = yCurrent + fieldVerDist + textElevBoolean

yCurrent = pageStartY+157
ui_separator uiTotalWidth / 2 + sepXPosFromCenter + 140, yPos, uiTotalWidth / 2 + sepXPosFromCenter + 140, yCurrent - 5

ui_separator xPos, yCurrent, uiTotalWidth, yCurrent

yCurrent = yCurrent - textElevBoolean + 10
infieldWidth	= 0
ui_style 0, 1
ui_infield{3} "bGasConnOnTable", xPos, yCurrent, totalColWidth - infieldWidth - 2, infieldHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[50], 0,
	0, stUIOutfields[50], 1
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist + textElevBoolean

infieldWidth	= 70
iFlag			= NOT(bGasConnOnTable)
ui_outfield stUIOutfields[51], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "gasConnDistX", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

ui_outfield stUIOutfields[52], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "gasConnDistY", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

return

! ------------------------------------------------------------------------------
"surfacesPage":
! ------------------------------------------------------------------------------
sepXPosFromCenter	= 0

totalColWidth	= 215 + sepXPosFromCenter
xPos			= 230 - 230
yPos			= pageStartY + 5
yCurrent		= yPos
fieldVerDist	= 25

infieldWidth	= 120
for i = 1 to 8
	iFLag		= matFlag[i]
	ui_outfield stUIOutfields[52 + i], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
	yCurrent = yCurrent - textElevMaterial
	ui_infield matParName[i], xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, matHeight
	yCurrent = yCurrent + (fieldVerDist + textElevInfield + textElevMaterial)
next i



totalColWidth	= 215 - 1 - sepXPosFromCenter
xPos			= 230 - 1 + sepXPosFromCenter
yPos			= pageStartY + 5
yCurrent		= yPos
fieldVerDist	= 25

infieldWidth	= 120
for i = 9 to 16
	iFLag		= matFlag[i]
	ui_outfield stUIOutfields[52 + i], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
	yCurrent = yCurrent - textElevMaterial
	ui_infield matParName[i], xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, matHeight
	yCurrent = yCurrent + (fieldVerDist + textElevInfield + textElevMaterial)
next i

return

! ------------------------------------------------------------------------------
"representationPage":
! ------------------------------------------------------------------------------

sepXPosFromCenter	= 0

totalColWidth	= 215 + sepXPosFromCenter
xPos			= 230 - 230
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 25

ui_separator uiTotalWidth / 2 + sepXPosFromCenter, yCurrent, uiTotalWidth / 2 + sepXPosFromCenter, uiTotalHeight - 5

infieldWidth	= 60
ui_outfield stUIOutfields[70], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_infield{3} "side_vis2D_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, 35,
	3, 1, 4, 1,		!labUIcabSideVis.png
	34, 34, 30, 30,
	1, stSideVis[1], 0,
	2, stSideVis[2], 1,
	3, stSideVis[3], 2,
	4, stSideVis[4], 3
yCurrent = yCurrent + fieldVerDist + textElevInfield + 19

infieldWidth	= 90
iFlag			= (gs_sink_type_m = 0)
ui_outfield stUIOutfields[46], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "SinkSymbol_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stSinksymbol[1], 3,
	0, stSinksymbol[2], 2,
	0, stSinksymbol[3], 1
yCurrent = yCurrent + fieldVerDist + textElevInfield

iFlag			= (gs_sink_type_m = 0 | gs_tap_type_m = 0)
ui_outfield stUIOutfields[62], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield{3} "TapSymbol_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 100, 1,
	0, 0, 0, 0,
	0, stDetail3D[1], 2,
	0, stDetail3D[2], 1,
	0, stDetail3D[3], 0
yCurrent = yCurrent + fieldVerDist + textElevInfield

ui_separator xPos, yCurrent, totalColWidth, yCurrent

yCurrent = yCurrent + 10
ui_style 0, 1
infieldWidth	= 0
ui_outfield stUIOutfields[71], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist

ui_outfield stUIOutfields[72], xPos, yCurrent, totalColWidth - penWidth - 2, outfieldHeight
yCurrent = yCurrent - textElevPen
ui_infield "gs_cont_pen", xPos + totalColWidth - penWidth, yCurrent, penWidth, penHeight
yCurrent = yCurrent + fieldVerDist + textElevPen

infieldWidth	= 120
ui_outfield stUIOutfields[73], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
yCurrent = yCurrent - textElevMaterial
ui_infield "gs_fill_type", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, matHeight
yCurrent = yCurrent + fieldVerDist + textElevMaterial

ui_outfield stUIOutfields[74], xPos, yCurrent, totalColWidth - penWidth - 2, outfieldHeight
yCurrent = yCurrent - textElevPen
ui_infield "gs_fill_pen", xPos + totalColWidth - penWidth, yCurrent, penWidth, penHeight
yCurrent = yCurrent + fieldVerDist + textElevPen

ui_outfield stUIOutfields[75], xPos, yCurrent, totalColWidth - penWidth - 2, outfieldHeight
yCurrent = yCurrent - textElevPen
ui_infield "gs_back_pen", xPos + totalColWidth - penWidth, yCurrent, penWidth, penHeight
yCurrent = yCurrent + fieldVerDist + textElevPen + 5



totalColWidth	= 214
xPos			= 230
yPos			= pageStartY
yCurrent		= yPos
fieldVerDist	= 25

ui_style 0, 1
infieldWidth	= 0
ui_outfield stUIOutfields[76], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist

infieldWidth	= 90
ui_outfield stUIOutfields[77], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight
yCurrent = yCurrent - textElevInfield
ui_infield{3} "gs_detlevel_3D_m", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight,
	8, "", 0, 0,
	0, 0, 0, 0,
	0, stDetail3D[1], 2,
	0, stDetail3D[2], 1,
	0, stDetail3D[3], 0
yCurrent = yCurrent + fieldVerDist + textElevInfield

infieldWidth	= 90
iFlag			= (gs_detlevel_3D_m <> DET3D_DETAILED)
ui_outfield stUIOutfields[78], xPos, yCurrent, totalColWidth - infieldWidth - 2, outfieldHeight, 4 * iFlag
yCurrent = yCurrent - textElevInfield
ui_infield "gs_resol", xPos + totalColWidth - infieldWidth, yCurrent, infieldWidth, infieldHeight
yCurrent = yCurrent + fieldVerDist + textElevInfield

yCurrent = yCurrent - textElevBoolean
infieldWidth	= 0
ui_infield{3} "gs_shadow", xPos, yCurrent, totalColWidth - infieldWidth - 2, booleanHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[79], 0,
	0, stUIOutfields[79], 1
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist + textElevBoolean

yCurrent = yCurrent - textElevBoolean
infieldWidth	= 0
ui_infield{3} "bFlasks", xPos, yCurrent, totalColWidth - infieldWidth - 2, booleanHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[80], 0,
	0, stUIOutfields[80], 1
ui_style 0, 0
yCurrent = yCurrent + fieldVerDist + textElevBoolean

yCurrent = yCurrent - textElevBoolean
infieldWidth	= 0
ui_infield{3} "gs_editableKnobs", xPos, yCurrent, totalColWidth - infieldWidth - 2, booleanHeight,
	7, "", 0, 0,
	0, 0, 0, 0,
	0, stUIOutfields[81], 0,
	0, stUIOutfields[81], 1
ui_style 0, 0

return

! ------------------------------------------------------------------------------
"minimalspacePage":
! ------------------------------------------------------------------------------

n = REQUEST ("Working_length_unit", "", workingLengthUnit)

if bDoubleRow then
	xPict		= 150
	yPict		= 200
	xPictPos	= xCenter - xPict / 2
	yPictPos	= yCenter - yPict / 2 + 10

	ui_pict 35, xPictPos, yPictPos, xPict, yPict, 1		!labUI_MSLabWS_doublerow.png
else
	xPict		= 189
	yPict		= 177
	xPictPos	= xCenter - xPict / 2
	yPictPos	= yCenter - yPict / 2 + 10

	ui_pict 36, xPictPos, yPictPos, xPict, yPict, 1		!labUI_MSLabWS_simplerow.png
endif

infieldWidth	= 80
ui_infield "tableLength", xPictPos + xPict / 2 - infieldWidth / 2, yPictPos - infieldHeight - 2, infieldWidth, infieldHeight	ui_tooltip stUITooltip[ 1]

ui_infield "MSSide", xPictPos - infieldWidth + 20, yPictPos - infieldHeight - 2, infieldWidth, infieldHeight					ui_tooltip stUITooltip[15]
ui_infield "MSSide2", xPictPos + xPict - 20, yPictPos - infieldHeight - 2, infieldWidth, infieldHeight							ui_tooltip stUITooltip[16]

ui_infield "tableDepth", xPictPos + xPict + 2, yPictPos + 70 - 35 * NOT(bDoubleRow), infieldWidth, infieldHeight				ui_tooltip stUITooltip[ 2]

ui_infield "MSFront", xPictPos + xPict + 2, yPictPos + 20 + 100 * NOT(bDoubleRow), infieldWidth, infieldHeight					ui_tooltip stUITooltip[17]

if bDoubleRow then
	ui_outfield STR(workingLengthUnit, MSFront), xPictPos + xPict + 2, yPictPos + yPict - 30, infieldWidth, outfieldHeight
endif

return

! ------------------------------------------------------------------------------
"mepPage":
! ------------------------------------------------------------------------------
if iPageIDMEP then

	dim	ui_con_type[7]
		ui_con_type[1] = 0
		ui_con_type[2] = 0
		ui_con_type[3] = 0
		ui_con_type[4] = 0
		ui_con_type[5] = 0
		ui_con_type[6] = 0
		ui_con_type[7] = 0
	
	call "MEP_m_ConnectionsACL_4" parameters all	ui_con_type			= ui_con_type,
													ui_system_var_pos	= 2
endif

return
