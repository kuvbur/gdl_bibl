

! ==============================================================================
! 		3D Detail Level
! ==============================================================================

if gs_detlevel_3D_m = DET3D_OFF then END
if not(gs_shadow) then SHADOW OFF

pen gs_cont_pen

if gs_detlevel_3D_m = DET3D_SIMPLE then
	resol 8
	bBackHole = 0
else
	resol gs_resol
endif


! ==============================================================================
! 		Hotspot Editing
! ==============================================================================

add A/2,B/2,0

unID = 1

hotspot -A/2, -B/2, 0, unID: unID = unID + 1
hotspot  A/2, -B/2, 0, unID: unID = unID + 1
hotspot -A/2,  B/2, 0, unID: unID = unID + 1
hotspot  A/2,  B/2, 0, unID: unID = unID + 1

! ---- Hotspot Editing - ZZYZX ----

hotspot 0,  B/2, 0,		unID, ZZYZX, 1+128:	unID = unID + 1
hotspot 0,  B/2, ZZYZX,	unID, ZZYZX, 2:		unID = unID + 1
hotspot 0,  B/2, -1,	unID, ZZYZX, 3:		unID = unID + 1

! ---- Hotspot editing - seat height ---

hotspot 0, -B/2 + 0.02, 0,				unID, gs_seat_height, 1+128 :	unID = unID + 1
hotspot 0, -B/2 + 0.02, gs_seat_height,	unID, gs_seat_height, 2 :		unID = unID + 1
hotspot 0, -B/2 + 0.02, -1,				unID, gs_seat_height, 3 :		unID = unID + 1

del 1


! ==============================================================================
! 		Main Frame & Seat
! ==============================================================================

material gs_frame_mat

_legSideAng = atn(gs_seat_height / 0.04)
_legBackAng = acs(0.06 / (gs_seat_height - 0.06))
_legFrontAng = atn((gs_seat_height - 0.03) / 0.02)

put	0.06,	0.05,			12,
	0.06,	0.06,			912,
	0,		_legBackAng - 180,	4012,

	0.06 + (gs_seat_height - 0.145) / (tan(_legBackAng) * sin(_legSideAng)) - 0.01*sin(_legBackAng),	gs_seat_height - 0.08 + 0.01 * cos(_legBackAng),	12,
	0.06 + (gs_seat_height - 0.145) / (tan(_legBackAng) * sin(_legSideAng)),							gs_seat_height - 0.08,						912,
	0,		-_legBackAng,	4012,

	B - 0.02 - (0.06 / sin(_legFrontAng)) - (0.08 / tan(_legFrontAng)),	gs_seat_height - 0.07,	12,
	B - 0.02 - (0.06 / sin(_legFrontAng)) - (0.08 / tan(_legFrontAng)),	gs_seat_height - 0.08,	912,
	0,		-_legFrontAng,		4012,

	B - 0.02 - (0.06 / sin(_legFrontAng)) - (0.08 - (gs_seat_height - 0.145) / sin(_legSideAng)) / tan(_legFrontAng) + 0.01 * sin(_legFrontAng),	0.06 + 0.01 * cos(_legFrontAng),	12,
	B - 0.02 - (0.06 / sin(_legFrontAng)) - (0.08 - (gs_seat_height - 0.145) / sin(_legSideAng)) / tan(_legFrontAng),							0.06,					912,
	0,		_legFrontAng - 180,		4012

muly 1/sin(_legSideAng)
rotz -90

for i = 1 to 2
	rotx _legSideAng
	addx -B
	addz -0.015

	cutform NSP/3, 1, 1+2,
		0, 0, 0.015, 0.02,

		use (NSP)

	del 3

	muly -1
	addy -A
next i

del 4 + 2

gosub "seatFrame"

cutend
cutend

if not(bLegHole) then
	muly 1/sin(_legSideAng)
	rotz -90

	for j = 1 to 2
		rotx _legSideAng
		addx -B
		addz -0.015
		cprism_ gs_frame_mat, gs_frame_mat, gs_frame_mat,
			NSP/3, 0.010,

			use (NSP)
		del 3

		muly -1
		addy -A
	next j
	del 4 + 2

endif

for zz = 1 to NSP
	tempDummy = get (1)
next zz


! ==============================================================================
! 		Backrest
! ==============================================================================

material gs_back_mat

_backHGT = ZZYZX - gs_seat_height - 0.017
_backThalRad = 0.5 * (sqr((0.11 - 0.18 * cos(30))^2 + (_backHGT - 0.08)^2))
_backAng0 = atn((0.18 * cos(30) - 0.11) / (_backHGT - 0.08))
_backAng1 = asn(0.03 / _backThalRad) - _backAng0

! --- Backrest Style 1 ---
_yPosCurv1 = 0.01 + ((A/2 - 0.04 - 0.03 * tan(_legSideAng/2) + 0.02 * (1 - cos(30)))^2 - (_backHGT)^2 - 0.0001 + 0.02 * _backHGT) / (0.02 - 2 * _backHGT)


! --- Backrest Style 2 ---
_backCurvRad = ((A/2 - 0.05)^2 + 0.0004) / 0.04
_backTopAng = asn(((A/2 - 0.05)^2 - 0.0004) / ((A/2 - 0.05)^2 + 0.0004))
_xPosCornCent = 0.05 + 0.04 * cos(_backTopAng)
_yPosCornCent = _backHGT - 0.02 - 0.04 * sin(_backTopAng)
_tempAngC = atn((_yPosCornCent - 0.05) / (abs(0.04 + 0.03 * tan(_legSideAng/2) - 0.03 * (1 - cos(30)) - _xPosCornCent)))
_tempAngB = acs(0.04 / sqr((_yPosCornCent - 0.05)^2 + (0.04 + 0.03 * tan(_legSideAng/2) - 0.03 * (1 - cos(30)) - _xPosCornCent)^2))
_tempAngA = 180 - _tempAngC - _tempAngB
_xPosSide = _xPosCornCent - 0.04 * cos(_tempAngA)
_yPosSide = _yPosCornCent - 0.04 * sin(_tempAngA)

! --- Backrest Style 3 ---
_xPosCurv2 = 0.04 + 0.03 * tan(_legSideAng/2) + 0.06 + 2 * (_backHGT - 0.12) * cos(30)
_xPosSideCurv = 0.04 + 0.03 * tan(_legSideAng/2) - 2 * (_backHGT - 0.12) * (1 - cos(30))

add A, B - 0.05, gs_seat_height + 0.017
rotx 90
roty -90

gosub "backCutpol"

addz -A
prism_ 18, 5 * A,
	0,													-0.04,										15+64,
	0,													0,											915+64,
	0,													-90,										4015+64,
	0.06,												0,											915+64,
	0,													-30,										4015+64,
	0.06 - 0.18 * cos(30),								0.18 * sin(30),								915+64,
	0,													30 + _backAng1,								4015+64,
	-0.05,												_backHGT,									15,
	(0.008 / cos(_backAng1)) - 0.05,					_backHGT,									15,
	0.06 - 0.18 * cos(30) + 0.088 * cos(_backAng1),		0.18 * sin(30) + 0.088 * sin(_backAng1),	15+64,
	0.06 - 0.18 * cos(30),								0.18 * sin(30),								915+64,
	0,													-30 - _backAng1,							4015+64,
	0.06,												0,											915+64,
	0,													30,											4015+64,
	0,													0,											915+64,
	0,													90,											4015+64,
	B - 0.1,											-0.032,										15,
	B - 0.1,											-0.04,										15

del 1

for i = 1 to _numCuts
	cutend
next i

! ==============================================================================
  end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
"seatFrame":
! ==============================================================================

	addy B
	rotx 90
	roty -90

	cutpolya 13, 1, 0,
		-0.01,		-0.01,					15, 
		B + 0.01,	-0.01,					15, 
		B + 0.01,	gs_seat_height + 0.01,	15, 
		-0.01,		gs_seat_height + 0.01,	15, 
		-0.01,		-0.01,					-1,

		0.06,						0.00,	15+64, 
		0.06,						0.06,	915+64, 
		0,							_legBackAng-180,	4015+64, 
		0.06,						gs_seat_height,	15, 
		B - 0.02,					gs_seat_height,	15, 
		B,							0.03,	15, 
		B - 0.07,					0.00,	15+64, 
		0.06,						0.00,	-1

	del 3

	rotx 90
	addz -B

	cprism_ gs_seat_mat, gs_seat_mat, gs_seat_mat,
		12, B,

		0,																	0,															15,
		0.04 - 0.03 * (sin(_legSideAng) - tan(_legSideAng/2)),				gs_seat_height - 0.03 * (1 - cos(_legSideAng)),				15+64,
		0.04 + 0.03 * tan(_legSideAng/2),									gs_seat_height,												1015+64,
		A - 0.04 - 0.03 * tan(_legSideAng/2),								gs_seat_height,												15+64,
		A - 0.04 + 0.03 * (sin(_legSideAng) - tan(_legSideAng/2)),			gs_seat_height - 0.03 * (1 - cos(_legSideAng)),				1015+64,
		A,																	0,															15,

		A - 0.015 * sin(_legSideAng),										-0.015 * cos(_legSideAng),									15,
		A - 0.04 + 0.015 * sin(_legSideAng) - 0.03 * tan(_legSideAng/2),	gs_seat_height - 0.015 * (1 - cos(_legSideAng)) - 0.015,	15+64,
		A - 0.04 - 0.03 * tan(_legSideAng/2),								gs_seat_height - 0.015,										1015+64,
		0.04 + 0.03 * tan(_legSideAng/2),									gs_seat_height - 0.015,										15+64,
		0.04 - 0.015 * sin(_legSideAng) + 0.03 *  tan(_legSideAng/2),		gs_seat_height - 0.015 * (1 - cos(_legSideAng)) - 0.015,	1015+64,
		0.015 * sin(_legSideAng),											-0.015 * cos(_legSideAng),									15

	del 2

	cutend

return


! ==============================================================================
"backCutpol":
! ==============================================================================

	_numCuts = 0

	if iBackStyle = 1 then
		put		0.04 + 0.03 * tan(_legSideAng/2),						-0.05,				13,
				0.04 + 0.03 * tan(_legSideAng/2),						0,					13,
				0.02 + 0.03 * tan(_legSideAng/2),						0,					915+64,
				0,														30,					4015+64,

				A/2,													_yPosCurv1,			915+64,
				A/2,													_backHGT,			3015+64,
				A/2,													_yPosCurv1,			915+64,

				A - 0.02 - 0.03 * tan(_legSideAng/2) - 0.02 * cos(30),	0.01,				3015+64,
				A - 0.02 - 0.03 * tan(_legSideAng/2),					0,					915+64,
				0,														30,					4015+64,
				A - 0.04 - 0.03 * tan(_legSideAng/2),					-0.05,				15+64,

				0.04 + 0.03 * tan(_legSideAng/2),						-0.05,				-1
	endif

	if iBackStyle = 2 then
		__xPos0 = 0.04 + 0.03 * tan(_legSideAng/2)

		put		__xPos0,							-0.05,					15+64,
				__xPos0,							0,						15+64,
				__xPos0 + 0.03,						0,						915+64,
				__xPos0 + 0.03 * (1 - cos(30)),		0.015,					3015+64,
				__xPos0 + 0.03 - 0.08 * cos(30),	0.040,					915+64,
				__xPos0 + 0.03 * (1 - cos(30)),		0.065,					3015+64,

				_xPosSide,						_yPosSide,					15+64,
				_xPosCornCent,					_yPosCornCent,				915+64,
				0.05,							_backHGT - 0.02,			3015+64,
				A/2,							_backHGT - _backCurvRad,	915+64,
				A - 0.05,						_backHGT - 0.02,			3015+64,
				A - _xPosCornCent,				_yPosCornCent,				915+64,
				A - _xPosSide,					_yPosSide,					3015+64,

				A - __xPos0 - 0.03 * (1 - cos(30)),		0.065,				15+64,
				A - __xPos0 - 0.03 + 0.08 * cos(30),	0.040,				915+64,
				A - __xPos0 - 0.03 * (1 - cos(30)),	0.015,					3015+64,
				A - __xPos0 - 0.03,				0,							915+64,
				A - __xPos0,					0,							3015+64,
				A - __xPos0,					-0.05,						15+64,

				__xPos0,						-0.05,						-1
	endif

	if iBackStyle = 3 then
		__xPos0 = 0.04 + 0.03 * tan(_legSideAng/2)

		put		__xPos0,								-0.05,				15+64,
				__xPos0,								0,					15+64,
				__xPos0 + 0.06,							0,					915+64,
				0,										-30,				4015+64,
				__xPos0 + 0.06 - 0.09 * cos(30),		0.09 * sin(30),		915+64,
				0,										60,					4015+64,

				_xPosCurv2,								_backHGT - 0.03,	915+64,
				0,										-30,				4015+64,
				_xPosSideCurv + 0.03,					_backHGT - 0.03,	915+64,
				0,										-90,				4015+64,
				A - _xPosSideCurv - 0.03,				_backHGT,			15+64,
				A - _xPosSideCurv - 0.03,				_backHGT - 0.03,	915+64,
				0,										-90,				4015+64,
				A - _xPosCurv2,							_backHGT - 0.03,	915+64,
				0,										-30,				4015+64,

				A - __xPos0 - 0.06 * (1 - cos(30)),		0.06,				15+64,
				A - __xPos0 - 0.06 + 0.09 * cos(30),	0.09 * sin(30),		915+64,
				0,										60,					4015+64,
				A - __xPos0 - 0.06,						0,					915+64,
				0,										-30,				4015+64,
				A - __xPos0,							0,					15+64,
				A - __xPos0,							-0.05,				15+64,

				__xPos0,								-0.05,				-1
	endif


	roty -90

	cutform NSP/3, 1, 59,
		0, 0, 1, 1,
		get (NSP)

	_numCuts = _numCuts + 1

	if bBackHole and not(GLOB_CONTEXT > 20 and GLOB_CONTEXT < 30) and iBackStyle <> 3 then
		if iBackStyle = 1 then
			put		0,								_yPosCurv1,			913,
					0.75 * _backHGT - _yPosCurv1,	360,				4013
		else
			put		0,								_backHGT - 0.09,	913,
					0.05,							360,				4013
		endif

		addx A/2
		mulx 1 + (A/0.22 - 1) * (iBackStyle = 2)
		cutpolya NSP/3, 1, 0,
			get (NSP)

		del 2
			
		_numCuts = _numCuts + 1

	endif

	del 1

return

