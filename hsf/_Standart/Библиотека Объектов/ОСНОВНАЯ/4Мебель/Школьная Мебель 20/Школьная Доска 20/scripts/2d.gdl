
pen gs_cont_pen
fill gs_fill_type

! ==============================================================================
! 		Hotspot Editing
! ==============================================================================

unID = 1

! --- Hotspot editing Board Length ---------------------------------------------

hotspot2 0,		0,	unID,	boardWidth,	1+256 :	unID = unID + 1
hotspot2 A,		0,	unID,	boardWidth,	2 :		unID = unID + 1
hotspot2 -1,	0,	unID,	boardWidth,	3 :		unID = unID + 1

! --- Hotspot editing Board Width ----------------------------------------------

hotspot2 A/2, -B,	unID :	unID = unID + 1

! --- Hotspot editing Wings Opening Angle in 2D --------------------------------

if iBoardStyle = 2 then
		_xPos = A * (2 - cos(wingAng2D))/2
		_yPos = -A * sin(wingAng2D)/2 - B

		hotspot2 A/2,	-B,		unID,	wingAng2D,	4+128 :	unID = unID + 1
		hotspot2 _xPos,	_yPos,	unID,	wingAng2D,	5 :		unID = unID + 1
		hotspot2 A,		-B,		unID,	wingAng2D,	6 :		unID = unID + 1

		_xPos = A * cos(wingAng2D)/2
		_yPos = -A * sin(wingAng2D)/2 - B

		hotspot2 A/2,	-B,		unID,	wingAng2D,	4+128 :	unID = unID + 1
		hotspot2 _xPos,	_yPos,	unID,	wingAng2D,	5 :		unID = unID + 1
		hotspot2 0,		-B,		unID,	wingAng2D,	6+512 :	unID = unID + 1
endif

! --- Non Editing Corner Hotspots ----------------------------------------------

hotspot2 0, -B, unID :	unID = unID + 1
hotspot2 A, -B, unID :	unID = unID + 1


! ==============================================================================
! 		Board rectangle
! ==============================================================================

poly2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
	0,		0,		1,
	A,		0,		1,
	A,		-B,		1,
	0,		-B,		1


! ==============================================================================
! 		Board Wings
! ==============================================================================

if iBoardStyle = 2 then gosub "boardWings2D"


! ==============================================================================
! 		Sliding Board
! ==============================================================================

if iBoardStyle = 3 then
	add2 0, -B

	poly2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,		0,		1,
		A,		0,		1,
		A,		-B,		1,
		0,		-B,		1

	del 1
endif


! ==============================================================================
! 		Floor Mount
! ==============================================================================

if iSupType = 2 then
	for i = 1 to 2
		add2 -0.05 * (iBoardStyle = 1) + 0.1 * (iBoardStyle = 2), 0

		drawindex 10
		poly2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,		-B/2 - 0.2 * ZZYZX,	1,
			0.05,	-B/2 - 0.2 * ZZYZX,	1,
			0.05,	-B/2 + 0.2 * ZZYZX,	1,
			0,		-B/2 + 0.2 * ZZYZX,	1

		del 1

		add2 A, 0
		mul2 -1, 1
	next i

	del 4
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
"boardWings2D":
! ==============================================================================

	for i = 1 to 2
		add2 A, -B
		mul2 -1, 1
		rot2 -wingAng2D

		poly2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0,		0,		1,
			A/2,	0,		1,
			A/2,	-B,		1,
			0,		-B,		1

		del 3

		add2 A, 0
		mul2 -1, 1
	next i


return


