
! ==============================================================================
! 		3D Detail Level
! ==============================================================================

if gs_detlevel_3D_m = DET3D_OFF then END
if not(gs_shadow) then SHADOW OFF

pen gs_cont_pen


! ==============================================================================
! 		Material Handling
! ==============================================================================

if iBoardType = TYPE_WHITEBOARD then
	frameMat	= whiteBoardFrameMat
	boardMat	= whiteBoardMat
	holderMat	= whiteBoardHolderMat
endif


! ==============================================================================
! 		Hotspot Editing
! ==============================================================================

unID = 100

! --- Hotspot editing Board Length ---------------------------------------------

hotspot 0,	0,	distFloor + boardHeight,	unID,	boardWidth,	1+256 :	unID = unID + 1
hotspot A,	0,	distFloor + boardHeight,	unID,	boardWidth,	2 :		unID = unID + 1
hotspot -1,	0,	distFloor + boardHeight,	unID,	boardWidth,	3 :		unID = unID + 1

! --- Hotspot editing Distance from Floor --------------------------------------

addx A/2

hotspot 0,	0,	0,			unID, distFloor, 1+128 :	unID = unID + 1
hotspot 0,	0,	distFloor,	unID, distFloor, 2 :		unID = unID + 1
hotspot 0,	0,	-1,			unID, distFloor, 3 :		unID = unID + 1

! --- Hotspot editing Board Height ---------------------------------------------

if iBoardStyle = 3 then 
	hotspot 0,	-B,	distFloor,						unID, fullBoardHeight, 1+128,	boardHeight :	unID = unID + 1
	hotspot 0,	-B,	distFloor + fullBoardHeight,	unID, fullBoardHeight, 2,		boardHeight :	unID = unID + 1
	hotspot 0,	-B,	-1,								unID, fullBoardHeight, 3,		boardHeight :	unID = unID + 1
else
	hotspot 0,	-B,	distFloor,					unID, boardHeight, 1+128 :	unID = unID + 1
	hotspot 0,	-B,	distFloor + boardHeight,	unID, boardHeight, 2 :		unID = unID + 1
	hotspot 0,	-B,	-1,							unID, boardHeight, 3 :		unID = unID + 1
endif

del 1

! --- Hotspot editing Wings Opening Angle in 3D --------------------------------

if iBoardStyle = 2 then
		_xPos = A * (2 - cos(wingAng3D))/2
		_yPos = -A * sin(wingAng3D)/2 - B

		hotspot A/2,	0,		distFloor + boardHeight/2,	unID,	wingAng3D,	4+128 :	unID = unID + 1
		hotspot _xPos,	_yPos,	distFloor + boardHeight/2,	unID,	wingAng3D,	5 :		unID = unID + 1
		hotspot A,		0,		distFloor + boardHeight/2,	unID,	wingAng3D,	6 :		unID = unID + 1

		_xPos = A * cos(wingAng3D)/2
		_yPos = -A * sin(wingAng3D)/2 - B

		hotspot A/2,	0,		distFloor + boardHeight/2,	unID,	wingAng3D,	4+128 :	unID = unID + 1
		hotspot _xPos,	_yPos,	distFloor + boardHeight/2,	unID,	wingAng3D,	5 :		unID = unID + 1
		hotspot 0,		0,		distFloor + boardHeight/2,	unID,	wingAng3D,	6 :		unID = unID + 1
endif


! ==============================================================================
! 		Board
! ==============================================================================

rotx 90
addy distFloor

cprism_ frameMat, frameMat, frameMat,
	10, B,
	0,				0,				15,
	boardWidth,		0,				15,
	boardWidth,		boardHeight,	15,
	0,				boardHeight,	15,
	0,				0,				-1,

	0.02,				0.02,				14,
	boardWidth - 0.02,	0.02,				14,
	boardWidth - 0.02,	boardHeight - 0.02,	14,
	0.02,				boardHeight - 0.02,	14,
	0.02,				0.02,				-1

cprism_ boardMat, frameMat, boardMat,
	5, B,
	0.02,				0.02,				14,
	boardWidth - 0.02,	0.02,				14,
	boardWidth - 0.02,	boardHeight - 0.02,	14,
	0.02,				boardHeight - 0.02,	14,
	0.02,				0.02,				-1

del 2


! ==============================================================================
! 		Board wings
! ==============================================================================

if iBoardStyle = 2 then gosub "boardWings"


! ==============================================================================
! 		Floor Mount
! ==============================================================================

material frameMat

if iSupType = 2 then
	for i = 1 to 2
		addx -0.05 * (iBoardStyle = 1) + 0.1 * (iBoardStyle = 2)

		addy -B/2 - 0.2 * ZZYZX
		block 0.05, 0.4 * ZZYZX, 0.05
		del 1

		add 0, -B, 0.05
		block 0.05, B, distFloor - 0.05 - 0.03 * (bHolder) + (0.1 + boardHeight/2) * (iBoardStyle = 1)
		del 1

		for j = 1 to 2
			add 0.04, 0.03 - B, 0.05
			roty -90
			prism 4, 0.03,
				0,					0.08,
				0.08,				0,
				0.08 + 0.05*sqr(2),	0,
				0,					0.08 + 0.05*sqr(2)

			del 2

			addy -B
			muly -1
		next j
		del 4

		del 1

		addx A
		mulx -1
	next i

	del 4

	add 0.15 * (iBoardStyle = 2), -B/2 - 0.015, 0.15

	block A - 0.3 * (iBoardStyle = 2), 0.03, 0.05

	del 1
endif


! ==============================================================================
! 		Sliding Board
! ==============================================================================

if iBoardStyle = 3 then
	rotx 90
	add 0, distFloor + 0.8 * boardHeight, B

	cprism_ frameMat, frameMat, frameMat,
		10, B,
		0,				0,				15,
		boardWidth,		0,				15,
		boardWidth,		boardHeight,	15,
		0,				boardHeight,	15,
		0,				0,				-1,

		0.02,				0.02,				14,
		boardWidth - 0.02,	0.02,				14,
		boardWidth - 0.02,	boardHeight - 0.02,	14,
		0.02,				boardHeight - 0.02,	14,
		0.02,				0.02,				-1

	cprism_ boardMat, frameMat, boardMat,
		5, B,
		0.02,				0.02,				14,
		boardWidth - 0.02,	0.02,				14,
		boardWidth - 0.02,	boardHeight - 0.02,	14,
		0.02,				boardHeight - 0.02,	14,
		0.02,				0.02,				-1

	del 2
endif


! ==============================================================================
! 		Pen Holder
! ==============================================================================

material holderMat

if bHolder then
	add 0, -(1 + (iBoardStyle <> 1)) * B - 0.05, distFloor - 0.03
	block A, (1 + (iBoardStyle <> 1)) * B + 0.05, 0.03
	del 1
endif


! ==============================================================================
! 		Writing 
! ==============================================================================

if bWriting then
	define material "pictmat" 2, 1, 1, 1
	material "pictmat"

	if iBoardType = TYPE_BLACKBOARD then
		add 0.02, -B - 0.001 - B * (iBoardStyle = 3), distFloor + 0.02 + 0.8 * (iBoardStyle = 3) * boardHeight
		rotx 90

		picture 1, boardWidth - 0.04, boardHeight - 0.04, 1

		del 2
	else
		add 0.02, -B - 0.001 - B * (iBoardStyle = 3), distFloor + 0.02 + 0.8 * (iBoardStyle = 3) * boardHeight
		rotx 90

		picture 2, boardWidth - 0.04, boardHeight - 0.04, 1

		del 2
	endif
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
"boardWings":
! ==============================================================================

	for i = 1 to 2
		rotx 90
		add 0, distFloor, B
		roty -wingAng3D

		cprism_ frameMat, frameMat, frameMat,
			10, B,
			0,					0,				15,
			boardWidth/2,		0,				15,
			boardWidth/2,		boardHeight,	15,
			0,					boardHeight,	15,
			0,					0,				-1,

			0.02,					0.02,				15,
			boardWidth/2 - 0.02,	0.02,				15,
			boardWidth/2 - 0.02,	boardHeight - 0.02,	15,
			0.02,					boardHeight - 0.02,	15,
			0.02,					0.02,				-1

		cprism_ boardMat, boardMat, boardMat,
			5, B,
			0.02,					0.02,				15,
			boardWidth/2 - 0.02,	0.02,				15,
			boardWidth/2 - 0.02,	boardHeight - 0.02,	15,
			0.02,					boardHeight - 0.02,	15,
			0.02,					0.02,				-1

		del 3

		addx A
		mulx -1
	next i

return


