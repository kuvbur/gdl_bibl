
call "FM_types" parameters all

ac_bottomlevel = (iSupType = WALL_MOUNTED) * distFloor
parameters ac_bottomlevel = ac_bottomlevel

ac_toplevel = topHeight
parameters ac_toplevel = ac_toplevel


values "A" range [0.2, )
values "B" range [0.01, )

values "boardWidth" range [0.2, )
values "boardHeight" range [0.1, )
values "distFloor" range [0.2 * (iSupType = FLOOR_MOUNTED), )


if GLOB_MODPAR_NAME = "A" then
	parameters boardWidth = A
else
	parameters A = boardWidth
endif


if GLOB_MODPAR_NAME = "ZZYZX" then
	if iSupType = FLOOR_MOUNTED then
		distFloor = max(ZZYZX - boardHeight, (iSupType = FLOOR_MOUNTED) * 0.2)
		if ABS(distFloor - (0.2 * (iSupType = FLOOR_MOUNTED))) < EPS then
			boardHeight = ZZYZX - distFloor
		endif
		parameters distFloor = distFloor, boardHeight = boardHeight
	else
		boardHeight = ZZYZX
		parameters boardHeight = boardHeight
	endif
else
	parameters ZZYZX = boardHeight + (iSupType = FLOOR_MOUNTED) * distFloor
endif


if iBoardStyle = 3 then
	values "fullBoardHeight" range [0.18, )

	if GLOB_MODPAR_NAME = "fullBoardHeight" then
		boardHeight = fullBoardHeight / 1.8
		parameters boardHeight = boardHeight
	else
		fullBoardHeight = 1.8 * boardHeight
		parameters fullBoardHeight = fullBoardHeight
	endif
else
	values "fullBoardHeight" range [0.1, )

	fullBoardHeight = boardHeight
	parameters fullBoardHeight = fullBoardHeight
endif

topHeight = distFloor + fullBoardHeight
parameters topHeight = topHeight
lock "topHeight"


values "stBoardType" stTypeValues
values "iBoardType" TYPE_BLACKBOARD, TYPE_WHITEBOARD

if GLOB_MODPAR_NAME = "stBoardType" then
	iBoardType = TYPE_BLACKBOARD
	for iType = 2 to 2
		if stBoardType = stTypeValues[iType] then
			iBoardType = iType
			iType = TYPE_WHITEBOARD		! break, last
		endif
	next iType
	parameters iBoardType = iBoardType
else
	iBoardType = max(1, min(iBoardType, vardim1(stTypeValues)))
	stBoardType = stTypeValues[iBoardType]
	parameters stBoardType = stBoardType, iBoardType = iBoardType
endif


values "stBoardStyle" stStyleValues
values "iBoardStyle" 1, 2, 3

if GLOB_MODPAR_NAME = "stBoardStyle" then
	iBoardStyle = 1
	for iStyle = 2 to 3
		if stBoardStyle = stStyleValues[iStyle] then
			iBoardStyle = iStyle
			iStyle = 3		! break, last
		endif
	next iStyle
	parameters iBoardStyle = iBoardStyle
else
	iBoardStyle = max(1, min(iBoardStyle, vardim1(stStyleValues)))
	stBoardStyle = stStyleValues[iBoardStyle]
	parameters stBoardStyle = stBoardStyle, iBoardStyle = iBoardStyle
endif

if iBoardStyle <> 3 then hideparameter "fullBoardHeight"
lock "fullBoardHeight"

values "stSupType" stSupportValues
values "iSupType" WALL_MOUNTED, FLOOR_MOUNTED

if GLOB_MODPAR_NAME = "stSupType" then
	iSupType = WALL_MOUNTED
	for iSup = 2 to 2
		if stSupType = stSupportValues[iSup] then
			iSupType = iSup
			iSup = 2		! break, last
		endif
	next iSup
	parameters iSupType = iSupType
else
	iSupType = max(1, min(iSupType, vardim1(stSupportValues)))
	stSupType = stSupportValues[iSupType]
	parameters stSupType = stSupType, iSupType = iSupType
endif

if iBoardStyle = 3 then
	iSupType = 1
	parameters iSupType = iSupType
	lock "iSupType", "stSupType"
endif


if iBoardStyle <> 2 then
	hideparameter "gs_wings"
	lock "wingAng2D", "wingAng3D"
endif

values "wingAng2D" range [0, 180]
values "wingAng3D" range [0, 180]

values "gs_detlevel_3D" stDetlevel3D
values "gs_detlevel_3D_m" DET3D_DETAILED, DET3D_OFF

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_DETAILED
	if gs_detlevel_3D = stDetlevel3D[2]	then gs_detlevel_3D_m = DET3D_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetlevel3D[1]
	if gs_detlevel_3D_m = DET3D_OFF		then gs_detlevel_3D = stDetlevel3D[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3D_m = DET3D_OFF then
	hideparameter "gs_3D_representation"
	lock "gs_shadow"
endif

if iBoardType = TYPE_BLACKBOARD then
	hideparameter	"whiteBoardFrameMat",
					"whiteBoardMat",
					"whiteBoardHolderMat"
	lock	"whiteBoardFrameMat",
			"whiteBoardMat",
			"whiteBoardHolderMat"
else
	hideparameter	"frameMat",
					"boardMat",
					"holderMat"
	lock	"frameMat",
			"boardMat",
			"holderMat"
endif

if not(bHolder) then
	hideparameter "holderMat", "whiteBoardHolderMat"
	lock "holderMat", "whiteBoardHolderMat"
endif


values "gs_ui_current_page" 1, 2, 3


