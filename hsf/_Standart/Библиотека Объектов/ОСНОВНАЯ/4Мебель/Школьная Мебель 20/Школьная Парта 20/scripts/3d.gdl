
! ==============================================================================
! 		3D Detail Level, Shadow, Resol
! ==============================================================================

if gs_detlevel_3D_m = DET3D_OFF then END

if not(gs_shadow) then SHADOW OFF

if gs_detlevel_3D_m = DET3D_SIMPLE then
	resol 8
else
	resol gs_resol
endif


! ==============================================================================
! 		Hotspot Editing
! ==============================================================================

call "Resize_A_B_ZZYZX" parameters all

if iStorType = 3 then
	_frontThk = 0.015
else
	_frontThk = 0
endif

unID = 100

add tableTopOverhang - frameWidth, B - tableTopOverhang + _frontThk, ZZYZX

hotspot 0, 0, -tableTopThk,					unID, storageHeight, 1 + 128 :	unID = unID + 1
hotspot 0, 0, -tableTopThk - storageHeight, unID, storageHeight, 2 :		unID = unID + 1
hotspot 0, 0, 0,							unID, storageHeight, 3 :		unID = unID + 1

del 1

hotspot a/2, b/2, 0,		unID, zzyzx, 1	: unID = unID + 1
hotspot a/2, b/2, zzyzx,	unID, zzyzx, 2	: unID = unID + 1
hotspot a/2, b/2, -1,		unID, zzyzx, 3	: unID = unID + 1


! ==============================================================================
! 		Tabletop
! ==============================================================================

pen gs_cont_pen

addz ZZYZX - tableTopThk/2

for ii = 1 to 2
	if gs_detlevel_3D_m = DET3D_DETAILED then
		put	_tabletopCornRad,		0,						14+64,
			A - _tabletopCornRad,	0,						14+64,
			A,						_tabletopCornRad,		1014+64,
			A,						B - _tabletopCornRad,	14+64,
			A - _tabletopCornRad,	B,						1014+64,
			_tabletopCornRad,		B,						14+64,
			0,						B - _tabletopCornRad,	1014+64,
			0,						_tabletopCornRad,		14+64,
			_tabletopCornRad,		0,						1014+64,
			_tabletopCornRad,		0,						-1
	else
		put	0,		0,		14,
			A,		0,		14,
			A,		B,		14,
			0,		B,		14,
			0,		0,		-1
	endif

	hprism_ gs_top_mat, gs_top_mat, gs_top_mat, gs_top_mat,
		NSP/3, tableTopThk/2, 0, _tabletopEdgeRad, 1,
	
		get (NSP)

	mulz -1
next ii

del 3


! ==============================================================================
! 		Frame
! ==============================================================================

_nCuts = 0
_frameAng = atn((ZZYZX - tableTopThk - storageHeight - frameWidth/2 - 0.03) / (tableTopOverhang + frameWidth/2 - 0.010))

_fullLength = (ZZYZX - tableTopThk - frameWidth/2 - 0.03) / sin(_frameAng)
_length = _fullLength - 0.06 * tan(_frameAng/2) + 0.000
_tubeL = B/2 - _fullLength * cos(_frameAng) - 0.02


add tableTopOverhang + frameWidth/2, 0.02, 0

for jj = 1 to 2
	for kk = 1 to 2
		gosub "legBase"
		gosub "legFrame"

		addy B - 0.04
		muly -1
	next kk
	del 4

	addx A - 2*tableTopOverhang - frameWidth
	mulx -1
next jj
del 4

for nn = 1 to 2
	for oo = 1 to 2
		addz 0.03
		rotx _frameAng - 90

		cutpolya 2, 1, 0,
			0,		0,		913,
			frameWidth/2,	360,	4013
			
		_nCuts = _nCuts + 1

		del 2

		addy B - 0.04
		muly -1
	next oo
	del 4

	addx A - 2*tableTopOverhang - frameWidth
	mulx -1
next nn
del 4

for pp = 1 to 2
	gosub "frameRod"

	addx A - 2*tableTopOverhang - frameWidth
	mulx -1
next pp
del 4

del 1


add tableTopOverhang + frameWidth/2, 0.02 + 0.07/tan(_frameAng) , 0.1
roty 90

cylind A - 2*tableTopOverhang - frameWidth, frameWidth/2

del 2


for mm = 1 to 2
	add tableTopOverhang + frameWidth/2, tableTopOverhang + frameWidth/2 + 0.010, ZZYZX - tableTopThk - storageHeight - frameWidth/2
	roty 90

	cylind A - 2*tableTopOverhang - frameWidth, frameWidth/2

	del 2

	addy B - 2*tableTopOverhang - frameWidth - 0.02
next mm
del 2

for i = 1 to _nCuts
	cutend
next i


! ==============================================================================
! 		Hook
! ==============================================================================

if GLOB_CONTEXT < 20 or GLOB_CONTEXT > 30 then
	if bHooks and gs_detlevel_3D_m = DET3D_DETAILED then
		add tableTopOverhang + frameWidth/2, B/2 - 0.02, ZZYZX - tableTopThk - frameWidth + 0.005
		roty 180
		
		for ll = 1 to nDrawers
			gosub "hook"
		
			mulx -1
			addx A - 2 * tableTopOverhang - frameWidth
		next ll
		del 2 * nDrawers
		del 2
	endif
else
	gs_resol = 8
endif


! ==============================================================================
! 		Storage
! ==============================================================================

if bStorage then
	material gs_drawer_mat

	add tableTopOverhang + frameWidth, tableTopOverhang, ZZYZX - tableTopThk - storageHeight
	rotx -90

	_storWidth = A - 2 * (tableTopOverhang + frameWidth)
	_storDepth = B - 2*tableTopOverhang
	_storThk = 0.005

	prism_ 10, _storDepth,
		0,			0,				15,
		_storWidth,	0,				15,
		_storWidth,	-storageHeight,	15,
		0,			-storageHeight,	15,
		0,			0,				-1,

		_storThk,				-_storThk,					15,
		_storWidth - _storThk,	-_storThk,					15,
		_storWidth - _storThk,	_storThk - storageHeight,	15,
		_storThk,				_storThk - storageHeight,	15,
		_storThk,				-_storThk,					-1

	del 2

	if iStorType <> 1 then
		addx tableTopOverhang + frameWidth + _storThk
		addy tableTopOverhang + _storThk
		addz ZZYZX + _storThk - tableTopThk - storageHeight

		block _storWidth - 2*_storThk, _storThk, storageHeight - 2*_storThk

		del 3
	endif


	if nDrawers = 2 then
		add A/2 - 0.001, tableTopOverhang + 2*_storThk, ZZYZX - tableTopThk - storageHeight + 0.002
		block 0.002, B - 2*tableTopOverhang - 0.02 - 2*_storThk, storageHeight - 0.004
		del 1
	endif

	if iStorType = 3 then
		material gs_top_mat
	
		add tableTopOverhang + frameWidth, B - tableTopOverhang, ZZYZX - tableTopThk - storageHeight

		if nDrawers = 1 then
			block A - 2 * (tableTopOverhang + frameWidth), _frontThk, storageHeight
		else
			block A/2 - (tableTopOverhang + frameWidth) - 0.0005, _frontThk, storageHeight
			addx A/2 - (tableTopOverhang + frameWidth) + 0.0005
			block A/2 - (tableTopOverhang + frameWidth) - 0.0005, _frontThk, storageHeight
			del 1
		endif
		del 1
	endif
endif


! ==============================================================================
!		Knob
! ==============================================================================

if GLOB_CONTEXT < 20 or GLOB_CONTEXT > 30 then
	if bStorage and iStorType = 3 and bKnob then
		if nDrawers = 1 then
			addx A/2
			gosub "knob"
			del 1
		else
			addx A/4 + (tableTopOverhang + frameWidth) / 2
			gosub "knob"
			addx A/2 - (tableTopOverhang + frameWidth)
			gosub "knob"
			del 2
		endif
	endif
else
	gs_resol = 8
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
"legFrame":
! ==============================================================================

	__elbowResol = ceil(gs_resol *  (_frameAng / 360))

	if gs_detlevel_3D_m = DET3D_DETAILED then
		put	0,		0,		0,			0,
			0,		0,		0.015,		0,
			0,		0,		_length,	0

		for aa = 1 to __elbowResol
			put	0,		0.06*(1-cos(aa*_frameAng/__elbowResol)),	_length + 0.06*sin(aa*_frameAng/__elbowResol),		0
		next aa

		put	0,		_tubeL*sin(_frameAng),	_fullLength + _tubeL*cos(_frameAng),		0,
			0,		2*_tubeL*sin(_frameAng),		_fullLength + 2*_tubeL*cos(_frameAng),	0
	else
		put	0,		0,		0,			0,
			0,		0,		0.015,		0,
			0,		0,		_fullLength,	0,
			0,		_tubeL*sin(_frameAng),	_fullLength + _tubeL*cos(_frameAng),		0,
			0,		2*_tubeL*sin(_frameAng),		_fullLength + 2*_tubeL*cos(_frameAng),	0
	endif

	material gs_frame_mat

	addz 0.03
	rotx _frameAng - 90

	tube 2, NSP/4, 1+2+16+00+00+128,
		0,		0,		901,
		frameWidth/2,	360,	4001,

		get (NSP)

	del 2

return


! ==============================================================================
"legBase":
! ==============================================================================

	material gs_legbase_mat

	addz 0.03
	rotx _frameAng -90
	addz -0.03

	cylind 0.045, legWidth/2

	del 3

return


! ==============================================================================
"frameRod":
! ==============================================================================

	add 0, 0.07 / tan(_frameAng), 0.1
	rotx -90

	cylind B - 0.04 - 0.14 / tan(_frameAng), frameWidth/2

	del 2

return

! ==============================================================================
"hook":
! ==============================================================================

	cylind 0.1, 0.0025

	addz 0.1

	elbow 0.02, 180, 0.0025

	addx 0.04
	rotz 90
	mulz -1

	elbow 0.02, 180, 0.0025

	del 3

	addy 0.04

	elbow 0.02, 180, 0.0025

	del 2

	addy 0.04

	cylind 0.1, 0.0025

	del 1

return


! ==============================================================================
"knob":
! ==============================================================================

	add -gs_knobHorPos, B - tableTopOverhang + _frontThk, ZZYZX - tableTopThk - storageHeight/2 + gs_knobVerPos
	rotz 180

	if gs_knob_type_m > 3 then
		addx -knob_size/2
		roty -90
	endif

	call "fa_knobtyp" parameters	knob_size			= knob_size,
									gs_detlevel_3D_m	= gs_detlevel_3D_m,
									gs_knob_type_m		= gs_knob_type_m,
									gs_resol			= gs_resol,
									gs_shadow			= gs_shadow,
									gs_cont_pen			= gs_cont_pen,
									gs_knob_mat			= gs_knob_mat

	if gs_knob_type_m > 3 then
		del 4
	else
		del 2
	endif

return

