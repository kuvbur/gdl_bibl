
call "FM_types" parameters all

ac_bottomlevel = 0
parameters ac_bottomlevel = ac_bottomlevel

ac_toplevel = ZZYZX
parameters ac_toplevel = ac_toplevel


values "B" range [0.3, )
values "ZZYZX" range [0.4, )

values "gs_resol" range [3,)

values "frameWidth" range [0.015, )

legWidth = frameWidth + 0.002
parameters legWidth = legWidth
lock "legWidth"


values "tableTopOverhang" range [tableTopThk/2, )

values "nDrawers" 1, 2
values "storageHeight" range [0.08, 0.55 * ZZYZX]

values "gs_knobVerPos" range [-storageHeight/3, storageHeight/3]
values "gs_knobHorPos" range [-0.1, 0.1]


values "gs_detlevel_3D" stDetlevel3D
values "gs_detlevel_3D_m" DET3D_DETAILED, DET3D_SIMPLE, DET3D_OFF

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = DET3D_DETAILED
	if gs_detlevel_3D = stDetlevel3D[2]	then gs_detlevel_3D_m = DET3D_SIMPLE
	if gs_detlevel_3D = stDetlevel3D[3]	then gs_detlevel_3D_m = DET3D_OFF
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetlevel3D[1]
	if gs_detlevel_3D_m = DET3D_SIMPLE	then gs_detlevel_3D = stDetlevel3D[2]
	if gs_detlevel_3D_m = DET3D_OFF		then gs_detlevel_3D = stDetlevel3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif


if gs_detlevel_3D_m <> DET3D_DETAILED then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif


values "stDeskType" `Одиночная`, `Двойные`
values "iDeskType" DESKTYPE_SINGLE, DESKTYPE_DOUBLE

if GLOB_MODPAR_NAME = "stDeskType" then
	iDeskType = DESKTYPE_SINGLE
	if stDeskType = `Двойные` then iDeskType = DESKTYPE_DOUBLE
	parameters iDeskType = iDeskType
else
	stDeskType = `Одиночная`
	if iDeskType = DESKTYPE_DOUBLE then stDeskType = `Двойные`
	parameters stDeskType = stDeskType
endif


! --- Set Drawer Number with Desk Type -----------------------------------------

if iDeskType = DESKTYPE_DOUBLE then
	nDrawers = 2
else
	nDrawers = 1
endif
parameters nDrawers = nDrawers


values "A" range [0.4 + 2 * (tableTopOverhang + frameWidth), )


if not(bStorage) then
	hideparameter "stStorType", "storageHeight", "nDrawers"
	lock "stStorType", "storageHeight", "nDrawers"
endif

values "stStorType" `Открытый`, `Ящики`,`Полка`
values "iStorType" 2, 3, 1

if GLOB_MODPAR_NAME = "stStorType" then
	iStorType = 2
	if stStorType = `Полка`		then iStorType = 1
	if stStorType = `Ящики`	then iStorType = 3
	parameters iStorType = iStorType
else
	stStorType = `Открытый`
	if iStorType = 1 then stStorType = `Полка`
	if iStorType = 3 then stStorType = `Ящики`
	parameters stStorType = stStorType
endif


if iStorType <> 3 then
	hideparameter "bKnob", "gs_knob_type", "knob_size", "gs_knobHorPos", "gs_knobVerPos", "gs_knob_mat"
	lock "bKnob", "gs_knob_type", "knob_size", "gs_knobHorPos", "gs_knobVerPos", "gs_knob_mat"
else
	if not(bKnob) then
		hideparameter "gs_knob_type", "knob_size", "gs_knobHorPos", "gs_knobVerPos", "gs_knob_mat"
		lock "gs_knob_type", "knob_size", "gs_knobHorPos", "gs_knobVerPos", "gs_knob_mat"
	endif
endif

values "gs_knob_type" st_knobtypes
values "gs_knob_type_m" 0, 1, 2, 3, 4, 5, 6, 7, 8

if GLOB_MODPAR_NAME = "gs_knob_type" then
	for iKnob = 1 to 9
		if gs_knob_type = st_knobtypes[iKnob] then
			gs_knob_type_m = iKnob - 1
		endif
	next iKnob
	parameters gs_knob_type_m = gs_knob_type_m
else
	gs_knob_type = st_knobtypes[gs_knob_type_m+1]
	parameters gs_knob_type = gs_knob_type
endif

if gs_knob_type_m < 4 then
	hideparameter "knob_size"
	lock "knob_size"
endif

for i = 1 to 255
	put i
next i

values "gs_cont_pen" use(NSP)
values "gs_fill_pen" get(NSP)

values "MSFront" range [0, )
values "MSLength" range [0, )

