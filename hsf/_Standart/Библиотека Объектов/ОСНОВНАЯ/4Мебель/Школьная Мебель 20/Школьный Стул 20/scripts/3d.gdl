
! ==============================================================================
! 		3D Detail Level
! ==============================================================================

if gs_detlevel_3D_m = DET3D_OFF then END

if not(gs_shadow) then SHADOW OFF

if gs_detlevel_3D_m = DET3D_SIMPLE then
	resol 8
else
	resol gs_resol
endif


! ==============================================================================
! 		Hotspot Editing
! ==============================================================================

add A/2,B/2,0

unID = 1

hotspot -A/2, -B/2, 0, unID: unID = unID + 1
hotspot  A/2, -B/2, 0, unID: unID = unID + 1
hotspot -A/2,  B/2, 0, unID: unID = unID + 1
hotspot  A/2,  B/2, 0, unID: unID = unID + 1

! ---- Hotspot Editing - ZZYZX ----

hotspot -A/2 + 0.06,  B/2 - frameWidth/2, 0,		unID, ZZYZX, 1+128 :	unID = unID + 1
hotspot -A/2 + 0.06,  B/2 - frameWidth/2, ZZYZX,	unID, ZZYZX, 2 :		unID = unID + 1
hotspot -A/2 + 0.06,  B/2 - frameWidth/2, -1,		unID, ZZYZX, 3 :		unID = unID + 1

hotspot A/2 - 0.06,  B/2 - frameWidth/2, 0,		unID, ZZYZX, 1+128:	unID = unID + 1
hotspot A/2 - 0.06,  B/2 - frameWidth/2, ZZYZX,	unID, ZZYZX, 2:		unID = unID + 1
hotspot A/2 - 0.06,  B/2 - frameWidth/2, -1,	unID, ZZYZX, 3:		unID = unID + 1

! ---- Hotspot editing - seat height ---

if gs_full_edit then

	hotspot -A/2 + legWidth/2 + frameWidth/4, -B/2 + 0.06 + legWidth/2, 0, unID, gs_seat_height, 1+128 :			unID = unID + 1
	hotspot -A/2 + legWidth/2 + frameWidth/4, -B/2 + 0.06 + legWidth/2, gs_seat_height, unID, gs_seat_height, 2 :	unID = unID + 1
	hotspot -A/2 + legWidth/2 + frameWidth/4, -B/2 + 0.06 + legWidth/2, -1, unID, gs_seat_height, 3 :				unID = unID + 1

	hotspot A/2 - legWidth/2 - frameWidth/4, -B/2 + 0.06 + legWidth/2, 0, unID, gs_seat_height, 1+128 :				unID = unID + 1
	hotspot A/2 - legWidth/2 - frameWidth/4, -B/2 + 0.06 + legWidth/2, gs_seat_height, unID, gs_seat_height, 2 :	unID = unID + 1
	hotspot A/2 - legWidth/2 - frameWidth/4, -B/2 + 0.06 + legWidth/2, -1, unID, gs_seat_height, 3 :				unID = unID + 1
endif

del 1


! ==============================================================================
! 		Leg Bases
! ==============================================================================

pen gs_cont_pen
material gs_legbase_mat

add legWidth/2 + frameWidth, legWidth/2, 0
cylind 0.045, legWidth/2

addx A - legWidth - 2 * frameWidth
cylind 0.045, legWidth/2

add frameWidth, B - 0.04 - legWidth/2, 0
cylind 0.045, legWidth/2

addx legWidth - A
cylind 0.045, legWidth/2

del 4


! ==============================================================================
! 		Back Frame
! ==============================================================================

material gs_frame_mat

_backZ = gs_seat_height + 0.06 - seatThk - frameWidth/2
_backrestAng = atn((ZZYZX - frameWidth/2 - _backZ) / (0.04 - frameWidth/2))

! ----- Back Left -----

put		0,		0,		-10,		0,
		0,		0,		0.045,	0,
		0,		0,		_backZ,	0,
		0,		0.04 - frameWidth/2 - 0.06*cos(_backrestAng),	ZZYZX - frameWidth/2 - 0.06*sin(_backrestAng),	0,
		0,		0.04 - frameWidth/2,							ZZYZX - frameWidth/2,	0


add legWidth/2, B - 0.04, 0

tube 2, NSP/4, 1+2+16+00+00+128,
	0,		0,		901,
	frameWidth/2,	360,	4000,

	use (NSP)


! ----- Back Right -----

addx A - legWidth

tube 2, NSP/4, 1+2+16+00+00+128,
	0,		0,		901,
	frameWidth/2,	360,	4000,

	get (NSP)

del 1



! ----- Back Top -----

if gs_detlevel_3D_m = DET3D_DETAILED then
	_elbowResol = ceil(gs_resol/2)
else
	_elbowResol = 2
endif

for h = -1 to _elbowResol
	_xPos = 0.06 * (1 - cos(h*90/_elbowResol))
	_zPos = 0.06 * sin(h*90/_elbowResol)

	put		_xPos,	0,	_zPos,	0
next h 

for i = 0 to _elbowResol+1
	_xPos = A - legWidth - 0.06  * (1 - sin(i*90/_elbowResol))
	_zPos = 0.06 * cos(i*90/_elbowResol)

	put		_xPos,	0,	_zPos,	0
next i

add 0, 0.04 - frameWidth/2 - 0.06*cos(_backrestAng), ZZYZX - frameWidth/2 - 0.06*sin(_backrestAng)
rotx _backrestAng - 90

tube 2, NSP/4, 1+2+00+00+00+128,
	0,		0,		901,
	frameWidth/2,	360,	4000,

	get (NSP)

del 2

del 1


! ==============================================================================
! 		Main Frame
! ==============================================================================

addx legWidth/2 + frameWidth
addy legWidth/2


! ----- Main Left -----

put		0,		0,		-1,		0,
		0,		0,		0.045,	0,
		0,		0,		gs_seat_height - seatThk - frameWidth/2 - 0.06,	0

for j = 1 to _elbowResol
	_yPos = 0.06 * (1 - cos(j*90/_elbowResol))
	_zPos = gs_seat_height - seatThk - frameWidth/2 - 0.06 * (1 - sin(j*90/_elbowResol))

	put		0,	_yPos,	_zPos,	0
next j

for k = 0 to _elbowResol
	_yPos = B - 0.04 - legWidth/2 - 0.06 * (1 / (sin(_backrestAng)) - sin(k*_backrestAng/_elbowResol))
	_zPos = gs_seat_height - seatThk - frameWidth/2 + 0.06 * (1 - cos(k*_backrestAng/_elbowResol))

	put		0,	_yPos,	_zPos,	0
next k

put		0,		B - legWidth/2 - frameWidth/2 - (0.19 - frameWidth) * cos(_backrestAng),	ZZYZX - frameWidth/2 - (0.19 - frameWidth) * sin(_backrestAng),	0,
		0,		B - legWidth/2 - frameWidth/2,												ZZYZX - frameWidth/2,	0


tube 2, NSP/4, 1+2+16+00+00+128,
	0,		0,		901,
	frameWidth/2,	360,	4000,

	use (NSP)


! ----- Main Right -----

addx A - legWidth - 2*frameWidth

tube 2, NSP/4, 1+2+16+00+00+128,
	0,		0,		901,
	frameWidth/2,	360,	4000,

	get (NSP)

del 1



! ----- Main Top -----

for l = -1 to _elbowResol
	_xPos = 0.04 * (1 - cos(l*90/_elbowResol))
	_zPos = 0.04 * sin(l*90/_elbowResol)

	put		_xPos,	0,	_zPos,	0
next l 

for m = 0 to _elbowResol+1
	_xPos = A - legWidth - 2*frameWidth - 0.04  * (1 - sin(m*90/_elbowResol))
	_zPos = 0.04 * cos(m*90/_elbowResol)

	put		_xPos,	0,	_zPos,	0
next m

addy B - legWidth/2 - frameWidth/2 - (0.19 - frameWidth) * cos(_backrestAng)
addz ZZYZX - frameWidth/2 - (0.19 - frameWidth) * sin(_backrestAng)
rotx _backrestAng - 90

tube 2, NSP/4, 1+2+00+00+00+128,
	0,		0,		901,
	frameWidth/2,	360,	4000,

	get (NSP)

del 3

del 2


! ==============================================================================
! 		Frame Rods
! ==============================================================================

put		0,		-B,		0,		0,
		0,		-(B - 0.04 - legWidth/2),	0,		0
	
for n = 0 to _elbowResol
	_xPos = 1.00 * frameWidth * (1 - cos(n*90/_elbowResol)) - frameWidth/3
	_yPos = 1.00 * frameWidth * sin(n*90/_elbowResol) - frameWidth

	put		_xPos,	_yPos,	0,	0
next n 

for p = 0 to _elbowResol
	_xPos = A - legWidth - (5/3)*frameWidth - 1.00 * frameWidth  * (1 - sin(p*90/_elbowResol))
	_yPos = 1.00 * frameWidth * cos(p*90/_elbowResol) - frameWidth

	put		_xPos,	_yPos,	0,	0
next p

put		A - legWidth - 2*frameWidth,	-(B - 0.04 - legWidth/2),	0,	0,
		A - legWidth - 2*frameWidth,	-B,		0,	0

_nCuts = 0
add legWidth/2 + frameWidth, legWidth/2, 0

cutpolya 2, 1, 0,
	0,		0,		913,
	frameWidth/2,	360,	4013

_nCuts = _nCuts + 1

addx A - legWidth - 2 * frameWidth

cutpolya 2, 1, 0,
	0,		0,		913,
	frameWidth/2,	360,	4013

del 2

_nCuts = _nCuts + 1

add legWidth/2 + frameWidth, B - 0.04, 0.15

tube 2, NSP/4, 1+2+00+00+00+128,
	0,		0,		901,
	frameWidth/2,	360,	4000,

	get (NSP)

del 1


add legWidth/2 + frameWidth, legWidth/2, gs_seat_height - seatThk - frameWidth/2 - 0.06
roty 90

cylind A - legWidth - 2 * frameWidth, frameWidth/2

del 2

for i = 1 to _nCuts
	cutend
next i

! ==============================================================================
! 		Seat & Backrest
! ==============================================================================

material gs_seat_mat


! --- Seat ---

_seatWidth = (A - legWidth - frameWidth/2)

if gs_detlevel_3D_m = DET3D_DETAILED then
	_Rr = min(A /3, (B - 0.12) / 2)
	_Sstat = 1000
else
	_Rr = min(A /5, (B - 0.12) / 4)
	_Sstat = 0
endif

add A/2, 0.06 + legWidth/2, gs_seat_height - seatThk

prism_ 6, seatThk,
	-_seatWidth/2,			0,				8,
	 _seatWidth/2,			0,				13,
	 _seatWidth/2,			B - 0.12 - _Rr,	15 + 64,
	 _seatWidth/2 - _Rr,	B - 0.12,		15 + 64 + _Sstat,
	-_seatWidth/2 + _Rr,	B - 0.12,		15 + 64,
	-_seatWidth/2,			B - 0.12 - _Rr,	15 + 64 + _Sstat


! ----- Seat Front -----

_la = (0.06 + frameWidth/2) * 0.7854

put		0,				 (_seatWidth)/2,	8,
		0,				-(_seatWidth)/2,	13

if gs_detlevel_3D_m = DET3D_SIMPLE then
	put		_la,			-(_seatWidth)/2,			15,
			_la,			 (_seatWidth)/2,			15
else
	put		_la - 0.015,	-(_seatWidth)/2,			15 + 64,
			_la - 0.015,	-(_seatWidth)/2 + 0.015,	915 + 64,
			0,				 90,						4015 + 64,
			_la,			 (_seatWidth)/2 - 0.015,	15 + 64,
			_la - 0.015,	 (_seatWidth)/2 - 0.015,	915 + 64,
			0,				 90,						4015 + 64
endif

rotz -90

bprism_ gs_seat_mat, gs_seat_mat, gs_seat_mat,
	NSP/3,	seatThk,	 0.06 + frameWidth/2,

	get (NSP)

del 2


! ----- Backrest -----

addx A/2
addy B - frameWidth/2 - (0.15 - 0.5 * frameWidth) * cos(_backrestAng)
addz ZZYZX - frameWidth/2 - (0.15 - 0.5 * frameWidth) * sin(_backrestAng)
rotz 180
rotx 180 - _backrestAng
addz 0.002 - frameWidth/2 - seatThk

_r2 = 0.06 + frameWidth/2
_r1 = 0.015
bprism_ gs_seat_mat, gs_seat_mat, gs_seat_mat,
	9, seatThk, 5 * A,
	-A/2 + _r1,		0,			15 + 64,
	A/2 - _r1,		0,			15 + 64,
	A/2,			_r1,		1015 + 64,
	A/2,			0.15 - _r2,	15 + 64,
	A/2 - _r2,		0.15,		1015 + 64,
	_r2 - A/2,		0.15,		15 + 64,
	-A/2,			0.15 - _r2,	1015 + 64,
	-A/2,			_r1,		15 + 64,
	-A/2 + _r1,		0,			1015 + 64

del 6



! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================



