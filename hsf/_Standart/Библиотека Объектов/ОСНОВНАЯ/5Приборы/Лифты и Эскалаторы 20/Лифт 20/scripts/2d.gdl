
unID = 1

current_story_index = 0
n = REQUEST ("Story", "", current_story_index, current_story_name)

if isFrom14 and GLOB_CONTEXT <> 5 then
	if bSplitLevelStories then
		if current_story_index > connected_stories_id[1]/2 or current_story_index < connected_stories_id[num_connected_stories]/2 then end
	else
		if current_story_index > connected_stories_id[1] or current_story_index < connected_stories_id[num_connected_stories] then end
	endif
endif

if gs_detlevel_2D_m = 1 then	! Scale Sensitive
	if GLOB_SCALE <= 75 then
		gs_detlevel_2D_m = 2
	else
		if GLOB_SCALE <= 150 then
			gs_detlevel_2D_m = 3
		else
			gs_detlevel_2D_m = 4
		endif
	endif
endif

SOLID_WALL		= 1
POCKETCUT_WALL	= 2
SIDECUT_WALL	= 3

! ==============================================================================
! Rectangular
! ==============================================================================
if elevator_form_m = FORM_RECT then

	if bShowElevatorShaft then
		hotspot2 0, 0, unID                             : unID = unID + 1
		hotspot2 totalShaftWidth, 0, unID               : unID = unID + 1
		hotspot2 totalShaftWidth, totalShaftDepth, unID : unID = unID + 1
		hotspot2 0, totalShaftDepth, unID               : unID = unID + 1
	else
		unID = unID + 4
	endif

	hotspot2 elev_shaft_thick, elev_shaft_thick, unID                                 : unID = unID + 1
	hotspot2 totalShaftWidth-elev_shaft_thick, elev_shaft_thick, unID                 : unID = unID + 1
	hotspot2 totalShaftWidth-elev_shaft_thick, totalShaftDepth-elev_shaft_thick, unID : unID = unID + 1
	hotspot2 elev_shaft_thick, totalShaftDepth-elev_shaft_thick, unID                 : unID = unID + 1

	if gs_detlevel_2D_m = 6 then	! Symbolic 2 = Big X

		if bShowElevatorShaft then
			wBigX = totalShaftWidth
			hBigX = totalShaftDepth
		else
			add2 elev_shaft_thick+carSide1, elev_shaft_thick+carSpaceFront
			wBigX = car_width
			hBigX = car_depth
		endif
		pen gs_cont_pen
		fill gs_fill_type
		poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,     0, 1,
				wBigX, 0, 1,
				wBigX, hBigX, 1,
				0,     hBigX, 1,
				0,     0, -1
		line2 0, 0, wBigX, hBigX
		line2 0, hBigX, wBigX, 0
		if not(bShowElevatorShaft) then del 1
		end
	endif

	! --------------------------------------------------------------------------
	! Elevator Shaft
	! --------------------------------------------------------------------------

	if bShowElevatorShaft then
		if bSplitLevelStories then
			storyID = current_story_index*2
			if storyID < 0 then
				elevShaftDoorPosID = SL_below_story_door_pos[abs(storyID)]
			else
				elevShaftDoorPosID = SL_above_story_door_pos[storyID+1]
			endif
		else
			storyID = current_story_index
			if storyID < 0 then
				elevShaftDoorPosID = below_story_door_pos[abs(storyID)]
			else
				elevShaftDoorPosID = above_story_door_pos[storyID+1]
			endif
		endif


		! --- Walls and Doors

		elev_wall_thick = elev_shaft_thick
		pen gs_wall_cont_pen
		fill gs_gap_fill_type
		if gs_detlevel_2D_m <> 4 then
			add2 elev_shaft_thick, elev_shaft_thick
			poly2_b 5, 2+4+64, gs_gap_fill_pen, gs_gap_back_pen,
				0, 0, 1,
				totalShaftWidth-elev_shaft_thick*2, 0, 1,
				totalShaftWidth-elev_shaft_thick*2, totalShaftDepth-elev_shaft_thick*2, 1,
				0, totalShaftDepth-elev_shaft_thick*2, 1,
				0, 0, -1
			del 1
			fill gs_wall_fill_type

			if dir_second_opening_m = SECOND_OPENING_SIDE1 then
				pl=elev_shaft_thick+carSide1+car_width/2
			else
				if dir_second_opening_m = SECOND_OPENING_SIDE2 then
					pl=totalShaftWidth-car_width-elev_shaft_thick-carSide2+car_width/2
				else
					pl=carSide1+elev_shaft_thick+car_width/2
				endif
			endif
			if elevShaftDoorPosID = 1  or elevShaftDoorPosID = 3 then				! --- Front wall with door
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					pl-opening_width/2+door_pos, 0, 1,
					pl-opening_width/2+door_pos, elev_shaft_thick, 1,
					elev_shaft_thick, elev_shaft_thick, 0,
					0, 0, -1

				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					totalShaftWidth, 0, 1,
					pl+opening_width/2+door_pos, 0, 1,
					pl+opening_width/2+door_pos, elev_shaft_thick, 1,
					totalShaftWidth-elev_shaft_thick, elev_shaft_thick, 0,
					totalShaftWidth, 0, -1

				add2 pl+door_pos, 0
				frame_thk = shaft_inner_depth/2-car_depth/2 !-0.06
				gosub "shaft door"
				del 1
			else													! --- Front wall
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					totalShaftWidth, 0, 0,
					totalShaftWidth-elev_shaft_thick, elev_shaft_thick, 1,
					elev_shaft_thick, elev_shaft_thick, 0,
					0, 0, -1
				unID = unID + 4
			endif

			if (elevShaftDoorPosID = 2 or elevShaftDoorPosID = 3) and dir_second_opening_m = SECOND_OPENING_REAR then		! --- Back wall with second door
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					0, totalShaftDepth, 1,
					pl-opening_width/2+second_door_pos, totalShaftDepth, 1,
					pl-opening_width/2+second_door_pos, totalShaftDepth-elev_shaft_thick, 1,
					elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 0,
					0, totalShaftDepth, -1

				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					totalShaftWidth, totalShaftDepth, 1,
					pl+opening_width/2+second_door_pos, totalShaftDepth, 1,
					pl+opening_width/2+second_door_pos, totalShaftDepth-elev_shaft_thick, 1,
					totalShaftWidth-elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 0,
					totalShaftWidth, totalShaftDepth, -1

				add2 pl+second_door_pos, totalShaftDepth
				rot2 180
				frame_thk = shaft_inner_depth/2-car_depth/2 !-0.06
				gosub "shaft door"
				del 2
			else													! --- Back wall
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					0, totalShaftDepth, 1,
					totalShaftWidth, totalShaftDepth, 0,
					totalShaftWidth-elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 1,
					elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 0,
					0, totalShaftDepth, -1
				unID = unID + 4
			endif

			if (elevShaftDoorPosID = 2 or elevShaftDoorPosID = 3) and dir_second_opening_m = SECOND_OPENING_SIDE1 then		! --- Wall on Side 1 with second door
				pl=car_depth/2+elev_shaft_thick+openingFromSide
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					0, pl-opening_width/2+second_door_pos, 1,
					elev_shaft_thick, pl-opening_width/2+second_door_pos, 1,
					elev_shaft_thick, elev_shaft_thick, 0,
					0, 0, -1

				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					0, totalShaftDepth, 1,
					0, pl+opening_width/2+second_door_pos, 1,
					elev_shaft_thick, pl+opening_width/2+second_door_pos, 1,
					elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 0,
					0, totalShaftDepth, -1

				add2 0, pl+second_door_pos
				rot2 -90
				frame_thk = shaft_inner_width/2-car_width/2 !-0.06
				gosub "shaft door"
				del 2
			else													! --- Wall on Side 1
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					0, totalShaftDepth, 0,
					elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 1,
					elev_shaft_thick, elev_shaft_thick, 0,
					0, 0, -1
				unID = unID + 4
			endif

			if (elevShaftDoorPosID = 2 or elevShaftDoorPosID = 3) and dir_second_opening_m = SECOND_OPENING_SIDE2 then		! --- Wall on Side 2 with second door
				pl=openingFromSide+car_depth/2+elev_shaft_thick
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					totalShaftWidth, 0, 1,
					totalShaftWidth, pl-opening_width/2+second_door_pos, 1,
					totalShaftWidth-elev_shaft_thick, pl-opening_width/2+second_door_pos, 1,
					totalShaftWidth-elev_shaft_thick, elev_shaft_thick, 0,
					totalShaftWidth, 0, -1

				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					totalShaftWidth, totalShaftDepth, 1,
					totalShaftWidth, pl+opening_width/2+second_door_pos, 1,
					totalShaftWidth-elev_shaft_thick, pl+opening_width/2+second_door_pos, 1,
					totalShaftWidth-elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 0,
					totalShaftWidth, totalShaftDepth, -1

				add2 totalShaftWidth, pl+second_door_pos
				rot2 90
				frame_thk = shaft_inner_width/2-car_width/2 !-0.06
				gosub "shaft door"
				del 2
			else													! --- Wall on Side 2
				poly2_b 5, 1+2+4+32, gs_wall_fill_pen, gs_wall_back_pen,
					totalShaftWidth, 0, 1,
					totalShaftWidth, totalShaftDepth, 0,
					totalShaftWidth-elev_shaft_thick, totalShaftDepth-elev_shaft_thick, 1,
					totalShaftWidth-elev_shaft_thick, elev_shaft_thick, 0,
					totalShaftWidth, 0, -1
				unID = unID + 4
			endif
		else	! gs_detlevel_2D_m = 4
			poly2_b 5, 1+2+4+64, gs_gap_fill_pen, gs_gap_back_pen,
				0,               0, 1,
				totalShaftWidth, 0, 1,
				totalShaftWidth, totalShaftDepth, 1,
				0,               totalShaftDepth, 1,
				0,               0, -1
			unID = unID + 16
		endif
	else	! show elevator shaft
		unID = unID + 16
	endif


	! --------------------------------------------------------------------------
	! Elevator Car
	! --------------------------------------------------------------------------

	! --- where are doors in elevator car?
	! --- the doors are always available by design independently from shaft(wall)
	bFrontDoorPresent		= 1			! always displayed
	bSecondaryDoorPresent	= (dir_second_opening_m <> SECOND_OPENING_NONE)

	add2 elev_shaft_thick+carSide1, elev_shaft_thick+carSpaceFront

	hotspot2 0, 0, unID                     : unID = unID + 1
	hotspot2 car_width, 0, unID             : unID = unID + 1
	hotspot2 car_width, car_depth, unID     : unID = unID + 1
	hotspot2 0, car_depth, unID             : unID = unID + 1
	hotspot2 car_width/2, car_depth/2, unID : unID = unID + 1

	pen gs_cont_pen
	fill gs_fill_type
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, 0, 1,
		car_width, 0, 1,
		car_width, car_depth, 1,
		0, car_depth, 1,
		0, 0, -1

	if current_story_index*(1+bSplitLevelStories) = car_pos_story_m then
		if gs_detlevel_2D_m = 5 then
			! --- symbolic car -------------------------------------------------
			line2 0, 0, car_width, car_depth
			line2 car_width, 0, 0, car_depth

			if bFrontDoorPresent then
				add2 car_width/2+door_pos, frontGap
				gosub "elev car door"
				del 1
			endif

			if bSecondaryDoorPresent then
				if dir_second_opening_m = SECOND_OPENING_REAR then
					add2 car_width/2+second_door_pos, car_depth-frontGap
					rot2 180
					gosub "elev car door"
					del 2
				endif
				if dir_second_opening_m = SECOND_OPENING_SIDE1 then
					add2 frontGap/2, car_depth/2+second_door_pos
					rot2 -90
					gosub "elev car door"
					del 2
				endif
				if dir_second_opening_m = SECOND_OPENING_SIDE2 then
					add2 car_width-frontGap/2, car_depth/2+second_door_pos
					rot2 90
					gosub "elev car door"
					del 2
				endif
			endif
		else
			if gs_detlevel_2D_m < 4 then
				! --- detailed car ---------------------------------------------
				! --- front wall ---
				drawWall_detlevel		= gs_detlevel_2D_m
				drawWall_length			= car_width
				drawWall_bDoorPresent	= bFrontDoorPresent
				drawWall_doorStyle		= door_style_m
				drawWall_doorPos		= door_pos
				gosub "drawOneCarWall"


				! --- rear wall ---
				add2 0, car_depth
				mul2 1, -1
				drawWall_detlevel		= gs_detlevel_2D_m
				drawWall_length			= car_width
				drawWall_bDoorPresent	= (bSecondaryDoorPresent and dir_second_opening_m = SECOND_OPENING_REAR)
				drawWall_doorStyle		= door_style_m
				drawWall_doorPos		= second_door_pos
				gosub "drawOneCarWall"
				del 2


				! --- side 1 wall ---
				rot2 90
				mul2 1, -1
				drawWall_detlevel		= gs_detlevel_2D_m
				drawWall_length			= car_depth
				drawWall_bDoorPresent	= (bSecondaryDoorPresent and dir_second_opening_m = SECOND_OPENING_SIDE1)
				drawWall_doorStyle		= door_style_m
				drawWall_doorPos		= second_door_pos
				gosub "drawOneCarWall"
				del 2

				! --- side 2 wall ---
				add2 car_width, 0
				rot2 90
				drawWall_detlevel		= gs_detlevel_2D_m
				drawWall_length			= car_depth
				drawWall_bDoorPresent	= (bSecondaryDoorPresent and dir_second_opening_m = SECOND_OPENING_SIDE2)
				drawWall_doorStyle		= door_style_m
				drawWall_doorPos		= second_door_pos
				gosub "drawOneCarWall"
				del 2
			endif
		endif
	endif

	! --------------------------------------------------------------------------
	! Mechanics
	! --------------------------------------------------------------------------

	if gs_detlevel_2D_m = 2 and bShowMechin2D then
		if elevator_type_m = 1 then					! --- Mechanical
			del ntr()
			add2 elev_shaft_thick, elev_shaft_thick
			del_num = 0
			if cweight_pos_m = CW_POS_NORMAL then
				add2 shaft_inner_width/2, car_depth+carSpaceRear+carSpaceFront
				del_num = 1
			endif
			if cweight_pos_m = CW_POS_SIDE1 then
				add2 carSpaceSide1+cweight_depth, shaft_inner_depth/2
				rot2 90
				del_num = 2
			endif
			if cweight_pos_m = CW_POS_SIDE2 then
				add2 carSide1+car_width+carSpaceSide2, shaft_inner_depth/2
				rot2 -90
				del_num = 2
			endif

			add2 -cweight_width/2, 0
			hotspot2 0, cweight_depth/2, unID: unID = unID + 1
			hotspot2 cweight_width, cweight_depth/2, unID: unID = unID + 1

			line2 cweight_depth/6, 0, cweight_width-cweight_depth/6, 0
			line2 cweight_depth/6, cweight_depth, cweight_width-cweight_depth/6, cweight_depth
			line2 cweight_depth/2, 0, cweight_depth/2, cweight_depth
			line2 cweight_width-cweight_depth/2, 0, cweight_width-cweight_depth/2, cweight_depth
			line2 0, 0, 0, cweight_depth
			line2 cweight_width, 0, cweight_width, cweight_depth
			line2 0, cweight_depth/2, cweight_depth/3, cweight_depth/2
			line2 cweight_width, cweight_depth/2, cweight_width-cweight_depth/3, cweight_depth/2
			del del_num+2
		else										! --- Hydraulic
			add2 car_width/2, car_depth/2
			line_type 21
			rect2 -car_width/6, -car_depth/6, car_width/6, car_depth/6
			circle2 0, 0, shaft_inner_width/15
			del 2
			unID = unID + 2
		endif
	else
		unID = unID + 2
		del 1
	endif
endif


! ==============================================================================
! Segmented
! ==============================================================================
if elevator_form_m = FORM_SEGMENTED then

	if bShowElevatorWall then
		hotspot2 0, 0, unID: unID = unID + 1
		hotspot2 A, 0, unID: unID = unID + 1
	else
		add2 elev_wall_overhang, 0
		unID = unID + 2
	endif
	hotspot2 A, elev_wall_thick, unID: unID = unID + 1
	hotspot2 0, elev_wall_thick, unID: unID = unID + 1

	if gs_detlevel_2D_m = 2 or gs_detlevel_2D_m = 3 or gs_detlevel_2D_m = 5 then
		
		! --- Elevator Wall ----------------------------------------------------

		if bShowElevatorWall then
			storyID = current_story_index
			if storyID < 0 then
				elevShaftDoorPosID = S_below_story_door_pos[abs(storyID)]
			else
				elevShaftDoorPosID = S_above_story_door_pos[storyID+1]
			endif

			poly2_b 5, 2+4+64, 0, 0,
				0, 0, 1,
				A, 0, 1,
				A, elev_wall_thick, 1,
				0, elev_wall_thick, 1,
				0, 0, -1

			if elevShaftDoorPosID = 1 then				! --- Front wall with door
				pen gs_wall_cont_pen
				fill gs_wall_fill_type

				poly2_b 5, 1+2+4+64, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					A/2-opening_width/2+door_pos, 0, 1,
					A/2-opening_width/2+door_pos, elev_wall_thick, 1,
					0, elev_wall_thick, 1,
					0, 0, -1

				poly2_b 5, 1+2+4+64, gs_wall_fill_pen, gs_wall_back_pen,
					A, 0, 1,
					A/2+opening_width/2+door_pos, 0, 1,
					A/2+opening_width/2+door_pos, elev_wall_thick, 1,
					A, elev_wall_thick, 1,
					A, 0, -1

				add2 A/2+door_pos, 0
				gosub "shaft door"
				del 1
			else													! --- Front wall
				pen gs_wall_cont_pen
				fill gs_wall_fill_type

				poly2_b 5, 1+2+4+64, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					A, 0, 1,
					A, elev_wall_thick, 1,
					0, elev_wall_thick, 1,
					0, 0, -1
				unID = unID + 4
			endif
		else
			unID = unID + 4
		endif
	endif


	! --- Elevator Car ---------------------------------------------------------

	alpha = 180/segment_num
	if segment_num/2=int(segment_num/2) then
		dy = car_width/2
	else
		dy = car_width/2*sin(int(segment_num/2)*alpha)
	endif
	add2 0, segmentedFront
	pen gs_cont_pen
	fill gs_fill_type

	add2 A/2, elev_wall_thick

	hotspot2 -car_width/2, carWallThk, unID: unID = unID + 1
	hotspot2 car_width/2, carWallThk, unID: unID = unID + 1
	hotspot2 -car_width/2, 0, unID: unID = unID + 1
	hotspot2 car_width/2, 0, unID: unID = unID + 1

	alpha = 180/segment_num
	for i = 1 to segment_num
		_tX = cos(alpha*i)*car_width/2
		_tY	= carWallThk+(sin(alpha*i)*car_width/2)*((car_depth-carWallThk)/dy)
		put _tX, _tY, 1
		hotspot2 _tX, _tY, unID: unID = unID + 1
	next i

	if gs_detlevel_2D_m = 4 or gs_detlevel_2D_m = 5 or gs_detlevel_2D_m = 6 then					! --- 1:200, Symbolic 2
		pen gs_cont_pen
		fill gs_fill_type
		poly2_b 4+segment_num, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-car_width/2, carWallThk, 1,
			-car_width/2, 0, 1,
			car_width/2, 0, 1,
			car_width/2, carWallThk, 1,
			get(nsp)

		if gs_detlevel_2D_m = 5 or gs_detlevel_2D_m = 6 then			! --- Symbolic 2
			line2 -car_width/2, 0, cos(alpha*int(segment_num/3))*car_width/2, carWallThk+(sin(alpha*int(segment_num/3))*car_width/2)*((car_depth-carWallThk)/dy)
			line2 car_width/2, 0, -cos(alpha*int(segment_num/3))*car_width/2, carWallThk+(sin(alpha*int(segment_num/3))*car_width/2)*((car_depth-carWallThk)/dy)
		endif

		del 1
		pen gs_wall_cont_pen
		fill gs_wall_fill_type
		if (gs_detlevel_2D_m = 4 or gs_detlevel_2D_m = 6) & bShowElevatorWall then
			add2 0, -segmentedFront
			poly2_b 5, 1+2+4+64, gs_wall_fill_pen, gs_wall_back_pen,
				0, 0, 1,
				A, 0, 1,
				A, elev_wall_thick, 1,
				0, elev_wall_thick, 1,
				0, 0, -1
			del 1
		endif

	else											! --- 1:50, 1:100

		poly2_b 4+segment_num, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-car_width/2, carWallThk, 1,
			-car_width/2, 0, 1,
			car_width/2, 0, 1,
			car_width/2, carWallThk, 1,
			get (nsp)

		if current_story_index = car_pos_story_m then
			for i = 1 to segment_num
				put cos(alpha*(i-1))*(car_width/2-0.02), carWallThk+(sin(alpha*(i-1))*(car_width/2-0.02))*((car_depth-carWallThk)/dy), 1
			next i

			poly2_b 4+segment_num, 1+4, 0, 0,
				-car_width/2+0.02, carWallThk, 1,
				-door_width/2+door_pos, carWallThk, 0,
				door_width/2+door_pos, carWallThk, 1,
				car_width/2-0.02, carWallThk, 1,
				get(nsp)

			wallStatus_detlevel		= gs_detlevel_2D_m
			wallStatus_doorStyle	= door_style_m
			wallStatus_doorPos		= door_pos
			gosub "determineWallStatusAroundDoor"

			if leftWallStatus = SOLID_WALL then
				line2 -door_width/2+door_pos, carWallThk, -door_width/2+door_pos, 0

			else
				if leftWallStatus = SIDECUT_WALL then
					line2 -door_width+door_pos, 0, -door_width+door_pos, doorThk
					line2 -door_width+door_pos, doorThk, -door_width/2+door_pos, doorThk
					line2 -door_width/2+door_pos, doorThk, -door_width/2+door_pos, carWallThk
				else
					line2 -door_width/2+door_pos, 0, -door_width/2+door_pos, elevCarOffset
					line2 -door_width/2+door_pos, elevCarOffset, -door_width+door_pos, elevCarOffset
					line2 -door_width+door_pos, elevCarOffset, -door_width+door_pos, elevCarOffset+doorThk 
					line2 -door_width+door_pos, elevCarOffset+doorThk, -door_width/2+door_pos, elevCarOffset+doorThk
					line2 -door_width/2+door_pos, elevCarOffset+doorThk, -door_width/2+door_pos, carWallThk
				endif
			endif

			if rightWallStatus= SOLID_WALL then
				line2 door_width/2+door_pos, carWallThk, door_width/2+door_pos, 0
			else
				if rightWallStatus= SIDECUT_WALL then
					line2 door_width+door_pos, 0, door_width+door_pos, doorThk
					line2 door_width+door_pos, doorThk, door_width/2+door_pos, doorThk
					line2 door_width/2+door_pos, doorThk, door_width/2+door_pos, carWallThk
				else
					line2 door_width/2+door_pos, 0, door_width/2+door_pos, elevCarOffset
					line2 door_width/2+door_pos, elevCarOffset, door_width+door_pos, elevCarOffset
					line2 door_width+door_pos, elevCarOffset, door_width+door_pos, elevCarOffset+doorThk 
					line2 door_width+door_pos, elevCarOffset+doorThk, door_width/2+door_pos, elevCarOffset+doorThk
					line2 door_width/2+door_pos, elevCarOffset+doorThk, door_width/2+door_pos, carWallThk
				endif
			endif

			if gs_detlevel_2D_m = 2 then
				add2 door_pos, elevCarOffset
				gosub "elev car door"
				del 1
			endif
		endif
		del 1
	endif
	del 1 + not(bShowElevatorWall)
endif


end

! ==============================================================================
! Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
! determineWallStatusAroundDoor
! input:
!  wallStatus_detlevel
!  wallStatus_doorStyle
!  wallStatus_doorPos
! output:
!  leftWallStatus
!  rightWallStatus
!  doorThk
! ------------------------------------------------------------------------------
"determineWallStatusAroundDoor":
	doorThk	 = elevCarDoorThk
	if wallStatus_detlevel = 2 then
		if wallStatus_doorStyle = 1 then
			! sliding to one side
			doorThk	= elevCarDoorThk*2
			if wallStatus_doorPos < 0 then
				leftWallStatus	= SOLID_WALL
				rightWallStatus	= SIDECUT_WALL
			else
				leftWallStatus	= SIDECUT_WALL
				rightWallStatus	= SOLID_WALL
			endif
		else
			if wallStatus_doorStyle = 5 then
				! sliding to one side as pocket
				if wallStatus_doorPos < 0 then
					leftWallStatus	= SOLID_WALL
					rightWallStatus	= POCKETCUT_WALL
				else
					leftWallStatus	= POCKETCUT_WALL
					rightWallStatus	= SOLID_WALL
				endif
			else
				if wallStatus_doorStyle = 2 or wallStatus_doorStyle = 4 then
					! sliding to both sides as pocket
					leftWallStatus	= POCKETCUT_WALL
					rightWallStatus	= POCKETCUT_WALL
				else
					!Style 3
					leftWallStatus	= SOLID_WALL
					rightWallStatus	= SOLID_WALL
				endif
			endif
		endif
	else
		leftWallStatus	= SOLID_WALL
		rightWallStatus	= SOLID_WALL
	endif
return


! ------------------------------------------------------------------------------
! drawOneCarWall
! input:
!  drawWall_detlevel
!  drawWall_doorStyle
!  drawWall_bDoorPresent
!  drawWall_length
!  drawWall_doorPos
!  doorThk
!  door_width
!  gs_wall_fill_pen, gs_wall_back_pen
!  carWallThk
! ------------------------------------------------------------------------------
"drawOneCarWall":
	if drawWall_bDoorPresent then
		wallStatus_detlevel		= drawWall_detlevel
		wallStatus_doorStyle	= drawWall_doorStyle
		wallStatus_doorPos		= drawWall_doorPos
		gosub "determineWallStatusAroundDoor"

		if leftWallStatus = SOLID_WALL then
			poly2_b 5, 1+4, gs_wall_fill_pen, gs_wall_back_pen,
				0, 0, 1,
				drawWall_length/2-door_width/2+drawWall_doorPos, 0, 1,
				drawWall_length/2-door_width/2+drawWall_doorPos, carWallThk, 1,
				carWallThk,                                      carWallThk, 0,
				0, 0, -1
		else
			if leftWallStatus = SIDECUT_WALL then
				poly2_b 7, 1+4, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					drawWall_length/2-door_width+drawWall_doorPos,   0, 1,
					drawWall_length/2-door_width+drawWall_doorPos,   doorThk, 1,
					drawWall_length/2-door_width/2+drawWall_doorPos, doorThk, 1,
					drawWall_length/2-door_width/2+drawWall_doorPos, carWallThk, 1,
					carWallThk,                                      carWallThk, 0,
					0, 0, -1
			else
				poly2_b 9, 1+4, gs_wall_fill_pen, gs_wall_back_pen,
					0, 0, 1,
					drawWall_length/2-door_width/2+drawWall_doorPos, 0, 1,
					drawWall_length/2-door_width/2+drawWall_doorPos, elevCarOffset, 1,
					drawWall_length/2-door_width+drawWall_doorPos,   elevCarOffset, 1,
					drawWall_length/2-door_width+drawWall_doorPos,   elevCarOffset+doorThk, 1,
					drawWall_length/2-door_width/2+drawWall_doorPos, elevCarOffset+doorThk, 1,
					drawWall_length/2-door_width/2+drawWall_doorPos, carWallThk, 1,
					carWallThk,                                      carWallThk, 0,
					0, 0, -1
			endif
		endif

		if rightWallStatus = SOLID_WALL then
			poly2_b 5, 1+4, gs_wall_fill_pen, gs_wall_back_pen,
				drawWall_length,                                 0, 1,
				drawWall_length/2+door_width/2+drawWall_doorPos, 0, 1,
				drawWall_length/2+door_width/2+drawWall_doorPos, carWallThk, 1,
				drawWall_length-carWallThk,                      carWallThk, 0,
				drawWall_length,                                 0, -1
		else
			if rightWallStatus = SIDECUT_WALL then
				poly2_b 7, 1+4, gs_wall_fill_pen, gs_wall_back_pen,
					drawWall_length,                                 0, 1,
					drawWall_length/2+door_width+drawWall_doorPos,   0, 1,
					drawWall_length/2+door_width+drawWall_doorPos,   doorThk, 1,
					drawWall_length/2+door_width/2+drawWall_doorPos, doorThk, 1,
					drawWall_length/2+door_width/2+drawWall_doorPos, carWallThk, 1,
					drawWall_length-carWallThk,                      carWallThk, 0,
					drawWall_length,                                 0, -1
			else
				poly2_b 9, 1+4, gs_wall_fill_pen, gs_wall_back_pen,
					drawWall_length,                                 0, 1,
					drawWall_length/2+door_width/2+drawWall_doorPos, 0, 1,
					drawWall_length/2+door_width/2+drawWall_doorPos, elevCarOffset, 1,
					drawWall_length/2+door_width+drawWall_doorPos,   elevCarOffset, 1,
					drawWall_length/2+door_width+drawWall_doorPos,   elevCarOffset+doorThk, 1,
					drawWall_length/2+door_width/2+drawWall_doorPos, elevCarOffset+doorThk, 1,
					drawWall_length/2+door_width/2+drawWall_doorPos, carWallThk, 1,
					drawWall_length-carWallThk,                      carWallThk, 0,
					drawWall_length,                                 0, -1
			endif
		endif

		add2 drawWall_length/2+drawWall_doorPos, elevCarOffset
		if drawWall_doorPos < 0 then mul2 -1, 1
		gosub "elev car door"
		if drawWall_doorPos < 0 then del 1
		del 1
	else
		poly2_b 5, 1+4, gs_wall_fill_pen, gs_wall_back_pen,
			0,                          0, 1,
			drawWall_length,            0, 0,
			drawWall_length-carWallThk, carWallThk, 1,
			carWallThk, carWallThk,     0,
			0,                          0, -1
	endif
return

"shaft door":
	if gs_detlevel_2D_m = 2 then
		pen gs_wall_cont_pen
		fill gs_opening_fill_type

		add2 -opening_width/2, elev_wall_thick
		hotspot2 0, 0, unID: unID = unID + 1
		poly2_b 5, 1+2+4+32, gs_opening_fill_pen, gs_opening_back_pen,
			0, 0, 1,
			0.18, 0, 1,
			0.18, -0.06, 1,
			0, -0.06, 1,
			0, 0, -1

		hotspot2 opening_width, 0, unID: unID = unID + 1
		poly2_b 5, 1+2+4+32, gs_opening_fill_pen, gs_opening_back_pen,
			opening_width, 0, 1,
			opening_width-0.18, 0, 1,
			opening_width-0.18, -0.06, 1,
			opening_width, -0.06, 1,
			opening_width, 0, -1

		if bOpeningFrame then
			hotspot2 0.02, -0.06, unID: unID = unID + 1
			hotspot2 opening_width-0.02, -0.06, unID: unID = unID + 1

			for i = -1 to 1 step 2
				add2 opening_width*(i < 0), 0
				mul2 i, 1
				poly2_b 7, 1+2+4+32, gs_opening_fill_pen, gs_opening_back_pen,
					0, -0.06, 1,
					0.02, -0.06, 1,
					0.02, -elev_wall_thick-0.02, 1,
					-0.04, -elev_wall_thick-0.02, 1,
					-0.04, -elev_wall_thick, 1,
					0, -elev_wall_thick, 1,
					0, -0.06, -1
				del 2
			next i
		else
			hotspot2 0, -0.06, unID: unID = unID + 1
			hotspot2 opening_width, -0.06, unID: unID = unID + 1
		endif

		opening_in2D_in_car_pos = 0
		if elev_door_style_m = 1 then		! --- Sliding door
			if (gs_detlevel_2D_m <> 5 or gs_detlevel_2D_m <> 4) and (car_width/2-door_width/2+door_pos)<0 then mul2 1, -1
			add2 0.18-(door_width/2)*(opening_in2D_in_car_pos/100), 0
			rect2 0, 0, door_width/2, 0.02
			del 1

			add2 0.18+door_width/2-door_width*(opening_in2D_in_car_pos/100), 0.02
			rect2 0, 0, door_width/2, 0.02
			del 1
			if (gs_detlevel_2D_m <> 5 or gs_detlevel_2D_m <> 4) and (car_width/2-door_width/2+door_pos)<0 then del 1
		endif

		if elev_door_style_m = 2 then		! --- Opening door

			add2 0.18, 0
			rot2 -90*(opening_in2D_in_car_pos/100)
			poly2_b 5, 1+4, 0, 0,
				0, 0, 1,
				door_width/2, 0, 1,
				door_width/2, 0.02, 1,
				0, 0.02, 1,
				0, 0, -1
			del 2

			add2 door_width+0.18, 0
			rot2 90*(opening_in2D_in_car_pos/100)
			add2 -door_width/2, 0
			poly2_b 5, 1+4, 0, 0,
				0, 0, 1,
				door_width/2, 0, 1,
				door_width/2, 0.02, 1,
				0, 0.02, 1,
				0, 0, -1
			del 3
		endif
		del 1

		pen gs_wall_cont_pen
		fill gs_wall_fill_type
	else
		pen gs_wall_cont_pen
		fill gs_wall_fill_type
		line2 -opening_width/2, elev_wall_thick, opening_width/2, elev_wall_thick
		unID = unID + 4
	endif
return


"elev car door":
	if door_style_m and gs_detlevel_2D_m = 2 then
		if door_style_m = 1 then
			add2 -door_width/2, 0
			rect2 0, 0, door_width/2, elevCarDoorThk
			del 1
			add2 0, elevCarDoorThk
			rect2 0, 0, door_width/2, elevCarDoorThk
			del 1
		endif
		if door_style_m = 2 then
			add2 -door_width/2, 0
			rect2 0, 0, door_width/2, elevCarDoorThk
			del 1
			rect2 0, 0, door_width/2, elevCarDoorThk
		endif
		if door_style_m = 3 then
			add2 -door_width/2, 0
			rect2 0, 0, door_width/2, elevCarDoorThk
			line2 0.05, 0, 0.05, elevCarDoorThk
			line2 door_width/2-0.05, 0, door_width/2-0.05, elevCarDoorThk
			line2 0.05, elevCarDoorThk/2, door_width/2-0.05, elevCarDoorThk/2
			del 1
			rect2 0, 0, door_width/2, elevCarDoorThk
			line2 0.05, 0, 0.05, elevCarDoorThk
			line2 door_width/2-0.05, 0, door_width/2-0.05, elevCarDoorThk
			line2 0.05, elevCarDoorThk/2, door_width/2-0.05, elevCarDoorThk/2
		endif
		if door_style_m = 4 then
			add2 -door_width/2, 0
			rect2 0, 0, door_width/2, elevCarDoorThk
			line2 0.03, 0, 0.03, elevCarDoorThk
			line2 door_width/2-0.03, 0, door_width/2-0.03, elevCarDoorThk
			line2 0.03, elevCarDoorThk/2, door_width/2-0.03, elevCarDoorThk/2
			del 1
			rect2 0, 0, door_width/2, elevCarDoorThk
			line2 0.03, 0, 0.03, elevCarDoorThk
			line2 door_width/2-0.03, 0, door_width/2-0.03, elevCarDoorThk
			line2 0.03, elevCarDoorThk/2, door_width/2-0.03, elevCarDoorThk/2
		endif
		if door_style_m = 5 then
			add2 -door_width/2, 0
			rect2 0, 0, door_width/2, elevCarDoorThk
			del 1
			add2 0, elevCarDoorThk/4
			rect2 0, 0, door_width/2, elevCarDoorThk/2
			del 1
		endif
	endif

	if gs_detlevel_2D_m = 5 then
		line2 0, -0.20, 0, 0.20
	endif
return

