
if gs_detlevel_3D_m = 0 then end
if gs_shadow = 0 then shadow off
unID			= 1
num_cutend		= 0


penthouse_height	= penthouse_height * bPenthouse
pit_depth			= pit_depth * bPith
top_slab_thick		= top_slab_thick * bTopSlab
pit_slab_thick		= pit_slab_thick * bPitSlab

! ==============================================================================
! Rectangular
! ==============================================================================
if elevator_form_m = FORM_RECT then
	elev_wall_thick = elev_shaft_thick

	! --------------------------------------------------------------------------
	! Elevator Car
	! --------------------------------------------------------------------------

	! --- where are doors in elevator car?
	bFrontDoorPresent = 0
	bSecondaryDoorPresent = 0
	if bSplitLevelStories then
		for i = 1 to num_connected_stories
			storyID = connected_stories_id[i]
			if connected_stories_height[i] > gs_max_StrFl_height and available_connected_stories_boolean[i] then
				if storyID < 0 then
					if SL_below_story_door_pos[abs(storyID)] = 1 then bFrontDoorPresent = 1
					if SL_below_story_door_pos[abs(storyID)] = 2 then bSecondaryDoorPresent = 1
					if SL_below_story_door_pos[abs(storyID)] = 3 then
						bFrontDoorPresent = 1
						bSecondaryDoorPresent = 1
					endif
				else
					if SL_above_story_door_pos[storyID+1] = 1 then bFrontDoorPresent = 1
					if SL_above_story_door_pos[storyID+1] = 2 then bSecondaryDoorPresent = 1
					if SL_above_story_door_pos[storyID+1] = 3 then
						bFrontDoorPresent = 1
						bSecondaryDoorPresent = 1
					endif
				endif
			endif
		next i
	else
		for i = 1 to num_connected_stories
			storyID = connected_stories_id[i]
			if connected_stories_height[i] > gs_max_StrFl_height then
				if storyID < 0 then
					if below_story_door_pos[abs(storyID)] = 1 then bFrontDoorPresent = 1
					if below_story_door_pos[abs(storyID)] = 2 then bSecondaryDoorPresent = 1
					if below_story_door_pos[abs(storyID)] = 3 then
						bFrontDoorPresent = 1
						bSecondaryDoorPresent = 1
					endif
				else
					if above_story_door_pos[storyID+1] = 1 then bFrontDoorPresent = 1
					if above_story_door_pos[storyID+1] = 2 then bSecondaryDoorPresent = 1
					if above_story_door_pos[storyID+1] = 3 then
						bFrontDoorPresent = 1
						bSecondaryDoorPresent = 1
					endif
				endif
			endif
		next i
	endif

	if bSplitLevelStories then
		carPosStoryID = -car_pos_story_m+1+num_stories_above_HS*2+home_story_index*2
	else
		carPosStoryID = -car_pos_story_m+1+num_stories_above_HS+home_story_index
	endif

	pen gs_cont_pen

	add elev_shaft_thick+carSide1, elev_shaft_thick+carSpaceFront, connected_stories_elev[carPosStoryID]

	hotspot 0,			0,			-0.05, unID: unID = unID + 1
	hotspot car_width,	0,			-0.05, unID: unID = unID + 1
	hotspot car_width,	car_depth,	-0.05, unID: unID = unID + 1
	hotspot 0,			car_depth,	-0.05, unID: unID = unID + 1

	hotspot 0,			0,			car_height+0.05, unID: unID = unID + 1
	hotspot car_width,	0,			car_height+0.05, unID: unID = unID + 1
	hotspot car_width,	car_depth,	car_height+0.05, unID: unID = unID + 1
	hotspot 0,			car_depth,	car_height+0.05, unID: unID = unID + 1

	if gs_detlevel_3D_m = 2 then				! --- Detailed
																								! --- Front door
		addx car_width/2+door_pos+carSide1
		rotx 90
		addx -carSide1
		if door_pos < 0 then
			RightDoor = 1
		else
			RightDoor = 0
		endif

		_opening_in3D = opening_in3D
		gosub "elev car door"
		del 3

		if second_door_pos<0 then
			RightDoor = 1
		else
			RightDoor = 0
		endif

		if dir_second_opening_m = SECOND_OPENING_REAR then			! --- Second door on Back
			add car_width/2+second_door_pos, car_depth, 0
			rotz 180
			rotx 90
			_opening_in3D = 0
			gosub "elev car door"
			del 3
		endif
		if dir_second_opening_m = SECOND_OPENING_SIDE1 then		! --- Second door on Side 1
			add 0, car_depth/2+second_door_pos, 0
			rotz -90
			rotx 90
			_opening_in3D = 0
			gosub "elev car door"
			del 3
		endif
		if dir_second_opening_m = SECOND_OPENING_SIDE2 then		! --- Second door on Side 2
			add car_width, car_depth/2+second_door_pos, 0
			rotz 90
			rotx 90
			_opening_in3D = 0
			gosub "elev car door"
			del 3
		endif

		material car_int_mat
		cutpolya 5, 2, 0,
			carWallThk, carWallThk, 15,
			car_width-carWallThk, carWallThk, 15,
			car_width-carWallThk, car_depth-carWallThk, 15,
			carWallThk, car_depth-carWallThk, 15,
			carWallThk, carWallThk, -1

		material car_ext_mat
		prism_  5, car_height,
				0, 0, 15,
				car_width, 0, 15,
				car_width, car_depth, 15,
				0, car_depth, 15,
				0, 0, -1
		cutend

		addz -0.05
		cprism_ car_pave_mat, car_ext_mat, car_ext_mat,
				5, 0.05,
				0, 0, 15,
				car_width, 0, 15,
				car_width, car_depth, 15,
				0, car_depth, 15,
				0, 0, -1
		del 1

		addz car_height
		cprism_ car_ext_mat, car_ceiling_mat, car_ext_mat,
				5, 0.05,
				0, 0, 15,
				car_width, 0, 15,
				car_width, car_depth, 15,
				0, car_depth, 15,
				0, 0, -1
		del 1

		for j = 1 to num_cutend
			cutend
		next j
		num_cutend=0

		if bControlPanel then
			if dir_second_opening_m = SECOND_OPENING_SIDE2 then	! Side 2
				add carWallThk, carWallThk+0.10, 1
				rotz 90
			else
				add car_width-carWallThk, carWallThk+0.20, 1
				rotz -90
			endif
			gosub "inside control panel"
			del 2
		endif
	else
		material car_ext_mat
		addz -0.05
		prism_  5, car_height+0.10,
				0, 0, 15,
				car_width, 0, 15,
				car_width, car_depth, 15,
				0, car_depth, 15,
				0, 0, -1
		del 1
	endif
	del 1


	! --------------------------------------------------------------------------
	! Elevator Shaft
	! --------------------------------------------------------------------------

	if bShowElevatorShaft then
		if bSplitLevelStories then
			for i = 1 to num_connected_stories						! --- Openings/Doors
				addz connected_stories_elev[i]
				storyID = connected_stories_id[i]
				if connected_stories_height[i] > gs_max_StrFl_height and available_connected_stories_boolean[i] then
					if storyID < 0 then
						elevShaftDoorPosID = SL_below_story_door_pos[abs(storyID)]
					else
						elevShaftDoorPosID = SL_above_story_door_pos[storyID+1]
					endif
				else
					elevShaftDoorPosID = 4
				endif

				opening_in3D_in_car_pos = 0
				if car_pos_story_m = storyID then opening_in3D_in_car_pos = opening_in3D
				gosub "openings to shaft"
				del 1
			next i

			pen gs_wall_cont_pen
			material elev_shaft_int_mat
			add elev_shaft_thick, elev_shaft_thick, 0
			cutpolya 5, 2, 0,
				0, 0, 15,
				shaft_inner_width, 0, 15,
				shaft_inner_width, shaft_inner_depth, 15,
				0, shaft_inner_depth, 15,
				0, 0, -1
			del 1
			num_cutend = num_cutend+1

			for i = 1 to num_connected_stories						! --- Stories
				if abs(INT(connected_stories_id[i]/2)-(connected_stories_id[i]/2)) < eps then
					addz connected_stories_elev[i]
					cprism_ elev_shaft_edge_mat, elev_shaft_edge_mat, elev_shaft_ext_mat,
							5, connected_stories_height[i],
							0, 0, 15,
							totalShaftWidth, 0, 15,
							totalShaftWidth, totalShaftDepth, 15,
							0, totalShaftDepth, 15,
							0, 0, -1

					hotspot 0, 0, 0, unID: unID = unID + 1
					hotspot totalShaftWidth, 0, 0, unID: unID = unID + 1
					hotspot totalShaftWidth, totalShaftDepth, 0, unID: unID = unID + 1
					hotspot 0, totalShaftDepth, 0, unID: unID = unID + 1
					del 1
				endif
			next i

			for j = 1 to num_cutend
				cutend
			next j
			num_cutend = 0
		else
			for i = 1 to num_connected_stories						! --- Stories and doors
				addz connected_stories_elev[i]

				storyID = connected_stories_id[i]
				if connected_stories_height[i] > gs_max_StrFl_height then
					if storyID < 0 then
						elevShaftDoorPosID = below_story_door_pos[abs(storyID)]
					else
						elevShaftDoorPosID = above_story_door_pos[storyID+1]
					endif
				else
					elevShaftDoorPosID = 4
				endif

				opening_in3D_in_car_pos = 0
				if car_pos_story_m = storyID then opening_in3D_in_car_pos = opening_in3D
				gosub "openings to shaft"

				pen gs_wall_cont_pen
				material elev_shaft_int_mat
				add elev_shaft_thick, elev_shaft_thick, 0
				cutpolya 5, 2, 0,
					0, 0, 15,
					shaft_inner_width, 0, 15,
					shaft_inner_width, shaft_inner_depth, 15,
					0, shaft_inner_depth, 15,
					0, 0, -1
				del 1
				num_cutend = num_cutend+1

				cprism_ elev_shaft_edge_mat, elev_shaft_edge_mat, elev_shaft_ext_mat,
						5, connected_stories_height[i],
						0, 0, 15,
						totalShaftWidth, 0, 15,
						totalShaftWidth, totalShaftDepth, 15,
						0, totalShaftDepth, 15,
						0, 0, -1

				hotspot 0, 0, 0, unID: unID = unID + 1
				hotspot totalShaftWidth, 0, 0, unID: unID = unID + 1
				hotspot totalShaftWidth, totalShaftDepth, 0, unID: unID = unID + 1
				hotspot 0, totalShaftDepth, 0, unID: unID = unID + 1

				for j = 1 to num_cutend
					cutend
				next j
				num_cutend=0
				del 1
			next i
		endif


		pen gs_wall_cont_pen
		material elev_shaft_int_mat
		add elev_shaft_thick, elev_shaft_thick, 0
		cutpolya 5, 2, 0,
			0, 0, 15,
			shaft_inner_width, 0, 15,
			shaft_inner_width, shaft_inner_depth, 15,
			0, shaft_inner_depth, 15,
			0, 0, -1
		del 1

		if bPenthouse then				! --- penthouse
			addz connected_stories_elev[1]+connected_stories_height[1]
			cprism_ elev_shaft_edge_mat, elev_shaft_edge_mat, elev_shaft_ext_mat,
					5, penthouse_height,
					0, 0, 15,
					totalShaftWidth, 0, 15,
					totalShaftWidth, totalShaftDepth, 15,
					0, totalShaftDepth, 15,
					0, 0, -1

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, totalShaftDepth, 0, unID: unID = unID + 1
			hotspot 0, totalShaftDepth, 0, unID: unID = unID + 1
			del 1
		endif

		if bPith then						! --- pit
			addz connected_stories_elev[num_connected_stories]-pit_depth
			cprism_ elev_shaft_edge_mat, elev_shaft_edge_mat, elev_shaft_ext_mat,
					5, pit_depth,
					0, 0, 15,
					totalShaftWidth, 0, 15,
					totalShaftWidth, totalShaftDepth, 15,
					0, totalShaftDepth, 15,
					0, 0, -1

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, totalShaftDepth, 0, unID: unID = unID + 1
			hotspot 0, totalShaftDepth, 0, unID: unID = unID + 1
			del 1
		endif
		cutend

		if bTopSlab then						! --- top slab
			addz connected_stories_elev[1]+connected_stories_height[1]+penthouse_height
			cprism_ top_slab_ext_mat, top_slab_int_mat, top_slab_edge_mat,
					5, top_slab_thick,
					0, 0, 15,
					totalShaftWidth, 0, 15,
					totalShaftWidth, totalShaftDepth, 15,
					0, totalShaftDepth, 15,
					0, 0, -1

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, totalShaftDepth, 0, unID: unID = unID + 1
			hotspot 0, totalShaftDepth, 0, unID: unID = unID + 1
			del 1
		endif

		addz connected_stories_elev[1]+connected_stories_height[1]+penthouse_height+top_slab_thick
		hotspot 0, 0, 0, unID: unID = unID + 1
		hotspot totalShaftWidth, 0, 0, unID: unID = unID + 1
		hotspot totalShaftWidth, totalShaftDepth, 0, unID: unID = unID + 1
		hotspot 0, totalShaftDepth, 0, unID: unID = unID + 1
		del 1

		if bPitSlab then						! --- pit slab
			addz connected_stories_elev[num_connected_stories]-pit_depth-pit_slab_thick
			cprism_ pit_slab_ext_mat, pit_slab_int_mat, pit_slab_edge_mat,
					5, pit_slab_thick,
					0, 0, 15,
					totalShaftWidth, 0, 15,
					totalShaftWidth, totalShaftDepth, 15,
					0, totalShaftDepth, 15,
					0, 0, -1

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, 0, 0, unID: unID = unID + 1
			hotspot totalShaftWidth, totalShaftDepth, 0, unID: unID = unID + 1
			hotspot 0, totalShaftDepth, 0, unID: unID = unID + 1
			del 1
		endif
	endif
endif


! ==============================================================================
! Segmented
! ==============================================================================
if elevator_form_m = FORM_SEGMENTED then

	if door_pos < 0 then
		RightDoor = 1
	else
		RightDoor = 0
	endif
	! --------------------------------------------------------------------------
	! Elevator Wall
	! --------------------------------------------------------------------------
	if bShowElevatorWall then
		for i = 1 to num_connected_stories						! --- Stories and doors
			pen gs_wall_cont_pen

			addz connected_stories_elev[i]

			storyID = connected_stories_id[i]
			if connected_stories_height[i] > gs_max_StrFl_height then
				if storyID < 0 then
					elevShaftDoorPosID = S_below_story_door_pos[abs(storyID)]
				else
					elevShaftDoorPosID = S_above_story_door_pos[storyID+1]
				endif
			else
				elevShaftDoorPosID = 4
			endif

			if elevShaftDoorPosID = 1 then
				addx door_pos
				rotx 90
				addx -opening_width/2

				pen gs_cont_pen
				opening_in3D_in_car_pos = 0
				if car_pos_story_m = storyID then opening_in3D_in_car_pos = opening_in3D
				neg = -0.10-0.08
				pos = opening_width+0.10
				addx A/2

				gosub "wall door"

				pen gs_wall_cont_pen
				material elev_wall_edge_mat
				cutpolya 5, 2, 0,
					0, 0, 15,
					opening_width, 0, 15,
					opening_width, opening_height, 15,
					0, opening_height, 15,
					0, 0, -1
				del 4
				num_cutend = num_cutend+1
			endif

			addy elev_wall_thick
			rotx 90
			cprism_ elev_wall_int_mat, elev_wall_ext_mat, elev_wall_edge_mat,
					5, elev_wall_thick,
					0, 0, 15,
					A, 0, 15,
					A, connected_stories_height[i], 15,
					0, connected_stories_height[i], 15,
					0, 0, -1
			del 2

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot A, 0, 0, unID: unID = unID + 1
			hotspot A, elev_wall_thick, 0, unID: unID = unID + 1
			hotspot 0, elev_wall_thick, 0, unID: unID = unID + 1

			for j = 1 to num_cutend
				cutend
			next j
			num_cutend=0

			del 1
		next i

		if bPenthouse then				! --- penthouse
			addz connected_stories_elev[1]+connected_stories_height[1]
			addy elev_wall_thick
			rotx 90
			cprism_ elev_wall_int_mat, elev_wall_ext_mat, elev_wall_edge_mat,
					5, elev_wall_thick,
					0, 0, 15,
					A, 0, 15,
					A, penthouse_height, 15,
					0, penthouse_height, 15,
					0, 0, -1
			del 2

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot A, 0, 0, unID: unID = unID + 1
			hotspot A, elev_wall_thick, 0, unID: unID = unID + 1
			hotspot 0, elev_wall_thick, 0, unID: unID = unID + 1
			del 1
		endif

		if bPith then						! --- pit
			addz connected_stories_elev[num_connected_stories]-pit_depth
			addy elev_wall_thick
			rotx 90
			cprism_ elev_wall_int_mat, elev_wall_ext_mat, elev_wall_edge_mat,
					5, elev_wall_thick,
					0, 0, 15,
					A, 0, 15,
					A, pit_depth, 15,
					0, pit_depth, 15,
					0, 0, -1
			del 2

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot A, 0, 0, unID: unID = unID + 1
			hotspot A, elev_wall_thick, 0, unID: unID = unID + 1
			hotspot 0, elev_wall_thick, 0, unID: unID = unID + 1
			del 1
		endif

		if bTopSlab then						! --- top slab
			addz connected_stories_elev[1]+connected_stories_height[1]+penthouse_height
			addy elev_wall_thick
			rotx 90
			cprism_ top_slab_int_mat, top_slab_ext_mat, top_slab_edge_mat,
					5, elev_wall_thick,
					0, 0, 15,
					A, 0, 15,
					A, top_slab_thick, 15,
					0, top_slab_thick, 15,
					0, 0, -1
			del 2

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot A, 0, 0, unID: unID = unID + 1
			hotspot A, elev_wall_thick, 0, unID: unID = unID + 1
			hotspot 0, elev_wall_thick, 0, unID: unID = unID + 1
			del 1
		endif

		addz connected_stories_elev[1]+connected_stories_height[1]+penthouse_height+top_slab_thick
		hotspot 0, 0, 0, unID: unID = unID + 1
		hotspot A, 0, 0, unID: unID = unID + 1
		hotspot A, elev_wall_thick, 0, unID: unID = unID + 1
		hotspot 0, elev_wall_thick, 0, unID: unID = unID + 1
		del 1

		if bPitSlab then						! --- pit slab
			addz connected_stories_elev[num_connected_stories]-pit_depth-pit_slab_thick
			addy elev_wall_thick
			rotx 90
			cprism_ pit_slab_int_mat, pit_slab_ext_mat, pit_slab_edge_mat,
					5, elev_wall_thick,
					0, 0, 15,
					A, 0, 15,
					A, pit_slab_thick, 15,
					0, pit_slab_thick, 15,
					0, 0, -1
			del 2

			hotspot 0, 0, 0, unID: unID = unID + 1
			hotspot A, 0, 0, unID: unID = unID + 1
			hotspot A, elev_wall_thick, 0, unID: unID = unID + 1
			hotspot 0, elev_wall_thick, 0, unID: unID = unID + 1
			del 1
		endif
	else
		addx elev_wall_overhang
	endif


	! --------------------------------------------------------------------------
	! Elevator Car
	! --------------------------------------------------------------------------

	pen gs_cont_pen
	alpha = 180/segment_num
	if segment_num/2=int(segment_num/2) then
		dy = car_width/2
	else
		dy = car_width/2*sin(int(segment_num/2)*alpha)
	endif

	carPosStoryID = -car_pos_story_m+1+num_stories_above_HS+home_story_index
	add (A-car_width)/2, elev_wall_thick+segmentedFront,  connected_stories_elev[carPosStoryID]

	hotspot car_width/2,  0,			-0.2, unID, car_depth, 1+128:		unID=unID+1
	hotspot car_width/2, -1,			-0.2, unID, car_depth, 3:			unID=unID+1
	hotspot car_width/2,  car_depth,	-0.2, unID, car_depth, 2:			unID=unID+1

	hotspot car_width/2,  0,			-0.2+car_height+0.50, unID, car_depth, 1+128:		unID=unID+1
	hotspot car_width/2, -1,			-0.2+car_height+0.50, unID, car_depth, 3:			unID=unID+1
	hotspot car_width/2,  car_depth,	-0.2+car_height+0.50, unID, car_depth, 2:			unID=unID+1

	hotspot 0,			0, -0.2,					unID : unID = unID + 1
	hotspot car_width,	0, -0.2,					unID : unID = unID + 1
	hotspot car_width,	0, -0.2+car_height+0.50,	unID : unID = unID + 1
	hotspot 0,			0, -0.2+car_height+0.50,	unID : unID = unID + 1

	if GLOB_CONTEXT <> 4 & GLOB_CONTEXT <> 24 & GLOB_CONTEXT <> 44 then
		for i = 0 to segment_num
			hotspot car_width/2 + cos(alpha*i)*car_width/2, carWallThk+sin(alpha*i)*(car_width/2)*((car_depth-carWallThk)/dy), -0.2,			unID : unID = unID + 1
			hotspot car_width/2 + cos(alpha*i)*car_width/2, carWallThk+sin(alpha*i)*(car_width/2)*((car_depth-carWallThk)/dy), -0.2+car_height+0.50,	unID : unID = unID + 1
		next i
	else
		unID = unID + (segment_num+1)*2
	endif

	if gs_detlevel_3D_m = 2 then		! --- Detailed ---
		if door_pos < 0 then
			RightDoor = 1
		else
			RightDoor = 0
		endif

		add car_width/2+door_pos,0, 0
		rotx 90
		gosub "elev car door"
		del 2

		material car_back_mat				! --- Back Side ----
		rotx 90
		addz -carWallThk
		cprism_{2} car_back_mat, car_int_mat, car_back_mat,
				9, 0.08,
				0, 0, 0, 15, car_back_mat, 
				car_width/2-door_width/2+door_pos, 0, 0, 15, car_int_mat, 
				car_width/2-door_width/2+door_pos, door_height, 0, 15, car_int_mat, 
				car_width/2+door_width/2+door_pos, door_height, 0, 15, car_int_mat,
				car_width/2+door_width/2+door_pos, 0, 0, 15, car_back_mat, 
				car_width, 0, 0, 15, car_back_mat,
				car_width, car_height, 0, 15, car_back_mat,
				0, car_height, 0, 15, car_back_mat,
				0, 0, 0, -1, car_back_mat
		del 2
		for j = 1 to num_cutend
			cutend
		next j
		num_cutend=0
									! --- Bottom Side ----
		if bShowMullion then ss = 15 else ss = 79

		for i = 1 to segment_num
			put cos(alpha*i)*car_width/2, carWallThk+(sin(alpha*i)*car_width/2)*((car_depth-carWallThk)/dy), ss
		next i

		addx car_width/2
		addz -0.2
		cprism_ car_pave_mat, car_ext_mat, car_ext_mat,
			4+segment_num, 0.20,
			-car_width/2, carWallThk, ss,
			-car_width/2, 0, ss,
			car_width/2, 0, ss,
			car_width/2, carWallThk, ss,
			use(nsp)
		del 1

		addz car_height
		cprism_ car_ext_mat, car_ceiling_mat, car_ext_mat,
			4+segment_num, 0.30,
			-car_width/2, carWallThk, ss,
			-car_width/2, 0, ss,
			car_width/2, 0, ss,
			car_width/2, carWallThk, ss,
			get(nsp)
		del 1

		if bShowMullion then
			material car_mullion_mat
			resol 4

			for i = 1 to segment_num-1
				add cos(alpha*(i))*(car_width/2-0.02),carWallThk+sin(alpha*(i))*(car_width/2-0.02)*((car_depth-carWallThk)/dy), 0
				rotz i*alpha+45
				prism_ 2, car_height,
					0, 0, 979,
					0.02, 360, 4079
				del 2
			next i
		endif

		material car_front_mat
		for i = 1 to segment_num
			put cos(alpha*(i))*(car_width/2-0.01),carWallThk+sin(alpha*(i))*(car_width/2-0.01)*((car_depth-carWallThk)/dy),79
		next i

		for q = segment_num to 1 step -1
			put cos(alpha*(q-1))*(car_width/2-0.02),carWallThk+sin(alpha*(q-1))*(car_width/2-0.02)*((car_depth-carWallThk)/dy),79
		next q

		prism_ 2*segment_num+2, car_height,
			car_width/2-0.01,carWallThk,79,
			get(nsp/2),
			-car_width/2+0.02,carWallThk,79,
			get(nsp)

		if bControlPanel then
			add door_width/2+door_pos+0.20, carWallThk, 1
			rotz 180
			gosub "inside control panel"
			del 2
		endif
		del 1

		for j = 1 to num_cutend
			cutend
		next j
		num_cutend=0
	else
		! --- Simple -----------------------------------------------------------

		if bShowMullion then ss = 15 else ss = 79
		for i = 1 to segment_num
			put cos(alpha*i)*car_width/2, carWallThk+sin(alpha*i)*(car_width/2)*((car_depth-carWallThk)/dy), ss
		next i

		addx car_width/2
		addz -0.2
		cprism_ car_ext_mat, car_ext_mat, car_ext_mat,
			4+segment_num, car_height+0.50,
			-car_width/2, carWallThk, ss,
			-car_width/2, 0, ss,
			car_width/2, 0, ss,
			car_width/2, carWallThk, ss,
			get(nsp)
		del 2
	endif
	del 1 + not(bShowElevatorWall)
endif


end

! ==============================================================================
! Subroutines
! ==============================================================================

"openings to shaft":
	if dir_second_opening_m = SECOND_OPENING_SIDE1 then
		pl=elev_shaft_thick+carSide1+car_width/2
	else
		if dir_second_opening_m = SECOND_OPENING_SIDE2 then
			pl=totalShaftWidth-car_width-elev_shaft_thick-carSide2+car_width/2
		else
			pl=carSide1+elev_shaft_thick+car_width/2
		endif
	endif

	if elevShaftDoorPosID = 1 or elevShaftDoorPosID = 3 then				! --- Front door

		addx pl+door_pos
		if door_pos < 0 then
			mulx -1
		else
			mulx 1
		endif
		rotx 90
		addx -opening_width/2
		frame_thk = shaft_inner_depth/2-car_depth/2 !-0.06
		neg = -0.10-0.08
		pos = opening_width+0.10
		gosub "shaft door"
		del 3
		rotx 90
		addx -opening_width/2
		material elev_shaft_edge_mat
		cutform 5, 1, 2,
			0, 0, 1, -elev_shaft_thick*2,
			0, 0, 15,
			opening_width, 0, 15,
			opening_width, opening_height, 15,
			0, opening_height, 15,
			0, 0, -1
		num_cutend = num_cutend+1
		del 3
	endif
	if (elevShaftDoorPosID = 2 or elevShaftDoorPosID = 3) and dir_second_opening_m = SECOND_OPENING_REAR then			! --- Second door on Back

		add pl+second_door_pos, totalShaftDepth, 0
		rotz 180
		if second_door_pos < 0 then
			mulx -1
		else
			mulx 1
		endif

		rotx 90
		addx -opening_width/2
		frame_thk = shaft_inner_depth/2-car_depth/2 !-0.06
		neg = -0.10-0.08
		pos = opening_width+0.10
		gosub "shaft door"
		del 3

		rotx 90
		addx -opening_width/2
		material elev_shaft_edge_mat

		cutform 5, 1, 2,
			0, 0, 1, -elev_shaft_thick*2,
			0, 0, 15,
			opening_width, 0, 15,
			opening_width, opening_height, 15,
			0, opening_height, 15,
			0, 0, -1
		num_cutend = num_cutend+1
		del 4
	endif
	if (elevShaftDoorPosID = 2 or elevShaftDoorPosID = 3) and dir_second_opening_m = SECOND_OPENING_SIDE1 then		! --- Second door on Side 1
		add 0, elev_shaft_thick+0.04+car_depth/2+second_door_pos, 0
		rotz -90
		if second_door_pos < 0 then
			mulx -1
		else
			mulx 1
		endif
		rotx 90
		addx -opening_width/2
		frame_thk = shaft_inner_width/2-car_width/2 !-0.06
		neg = -0.10-0.08
		pos = opening_width+0.10
		gosub "shaft door"
		del 3

		rotx 90
		addx -opening_width/2
		material elev_shaft_edge_mat

		cutform 5, 1, 2,
			0, 0, 1, -elev_shaft_thick*2,
			0, 0, 15,
			opening_width, 0, 15,
			opening_width, opening_height, 15,
			0, opening_height, 15,
			0, 0, -1
		num_cutend = num_cutend+1
		del 4
	endif
	if (elevShaftDoorPosID = 2 or elevShaftDoorPosID = 3) and dir_second_opening_m = SECOND_OPENING_SIDE2 then		! --- Second door on Side 2
		add totalShaftWidth, 0.04+car_depth/2+elev_shaft_thick+second_door_pos, 0
		rotz 90
		if second_door_pos < 0 then
			mulx -1
		else
			mulx 1
		endif
		rotx 90
		addx -opening_width/2
		frame_thk = shaft_inner_width/2-car_width/2 !-0.06
		pos	= -0.10-0.08
		neg = opening_width+0.10
		gosub "shaft door"
		del 3

		rotx 90
		addx -opening_width/2
		material elev_shaft_edge_mat
		cutform 5, 1, 2,
			0, 0, 1, -elev_shaft_thick*2,
			0, 0, 15,
			opening_width, 0, 15,
			opening_width, opening_height, 15,
			0, opening_height, 15,
			0, 0, -1
		del 4
		num_cutend = num_cutend+1
	endif
return

"shaft door":
	pen gs_wall_cont_pen
	if gs_detlevel_3D_m = 2 then				! --- Detailed
		material opening_frame_mat
		addz -elev_wall_thick
		prism_ 9, 0.06,
			0, 0, 15,
			0.18, 0, 15,
			0.18, door_height, 15,
			0.18+door_width, door_height, 15,
			0.18+door_width, 0, 15,
			opening_width, 0, 15,
			opening_width, opening_height, 15,
			0, opening_height, 15,
			0, 0, -1
		del 1

		if bOpeningFrame then
			addz 0.06-elev_wall_thick
			prism_ 9, elev_wall_thick-0.06,
				0, 0, 15,
				0.02, 0, 15,
				0.02, opening_height-0.02, 15,
				opening_width-0.02, opening_height-0.02, 15,
				opening_width-0.02, 0, 15,
				opening_width, 0, 15,
				opening_width, opening_height, 15,
				0, opening_height, 15,
				0, 0, -1
			del 1

			prism_ 9, 0.02,
				-0.04, 0, 15,
				0.02, 0, 15,
				0.02, opening_height-0.02, 15,
				opening_width-0.02, opening_height-0.02, 15,
				opening_width-0.02, 0, 15,
				opening_width+0.04, 0, 15,
				opening_width+0.04, opening_height+0.04, 15,
				-0.04, opening_height+0.04, 15,
				-0.04, 0, -1
		endif

		if elev_door_style_m = 1 then		! --- Sliding door
			addz -elev_wall_thick
			material door_panel_mat
			add 0.18-(door_width/2)*(opening_in3D_in_car_pos/100), 0, -0.02
			block door_width/2, door_height, 0.02
			del 1
			add 0.18+door_width/2-door_width*(opening_in3D_in_car_pos/100), 0, -0.04
			block door_width/2, door_height, 0.02
			del 2
		endif

		if elev_door_style_m = 2 then		! --- Opening door
			addz -elev_wall_thick
			material door_panel_mat
			addx 0.18
			roty -90*(opening_in3D_in_car_pos/100)
			addz -0.02
			block door_width/2, door_height, 0.02
			del 3
			addx 0.18+door_width
			roty 90*(opening_in3D_in_car_pos/100)
			add -door_width/2, 0, -0.02
			block door_width/2, door_height, 0.02
			del 3
			del 1
		endif

		if bControlPanel then
			if carSide1+car_width/2-door_pos < shaft_inner_width/2 then
				add neg, 1, 0
			else
				add pos, 1, 0
			endif
			gosub "outside control panel"
			del 1
		endif
	else										! --- Simple
		material door_panel_mat
		addz -elev_wall_thick
		block opening_width, opening_height, elev_wall_thick
		del 1
	endif
return


"wall door":
	pen gs_wall_cont_pen
	if gs_detlevel_3D_m = 2 then				! --- Detailed
		material opening_frame_mat
		addz -elev_wall_thick
		prism_ 9, 0.06,
			0, 0, 15,
			0.18, 0, 15,
			0.18, door_height, 15,
			0.18+door_width, door_height, 15,
			0.18+door_width, 0, 15,
			opening_width, 0, 15,
			opening_width, opening_height, 15,
			0, opening_height, 15,
			0, 0, -1
		del 1

		if bOpeningFrame then
			addz 0.06-elev_wall_thick
			prism_ 9, elev_wall_thick-0.06,
				0, 0, 15,
				0.02, 0, 15,
				0.02, opening_height-0.02, 15,
				opening_width-0.02, opening_height-0.02, 15,
				opening_width-0.02, 0, 15,
				opening_width, 0, 15,
				opening_width, opening_height, 15,
				0, opening_height, 15,
				0, 0, -1
			del 1

			prism_ 9, 0.02,
				-0.04, 0, 15,
				0.02, 0, 15,
				0.02, opening_height-0.02, 15,
				opening_width-0.02, opening_height-0.02, 15,
				opening_width-0.02, 0, 15,
				opening_width+0.04, 0, 15,
				opening_width+0.04, opening_height+0.04, 15,
				-0.04, opening_height+0.04, 15,
				-0.04, 0, -1
		endif

		if elev_door_style_m = 1 then		! --- Sliding door
			if RightDoor then
				mulx -1
				addx -shaft_inner_width/2
			else
				mulx 1
				addx 0
			endif

			addx 0.17
			rotz -90
			cutplane 90
			del 2

			addz -elev_wall_thick
			material door_panel_mat
			add 0.17-(door_width/2)*(opening_in3D_in_car_pos/100), 0, 0.03
			block door_width/2, door_height, 0.02
			del 1
			add 0.17+door_width/2-door_width*(opening_in3D_in_car_pos/100), 0, 0.01
			block door_width/2, door_height, 0.02
			del 2+2

			cutend
		endif

		if elev_door_style_m = 2 then		! --- Opening door
			addz -elev_wall_thick+0.04
			material door_panel_mat
			addx 0.18
			roty -90*(opening_in3D_in_car_pos/100)
			addz -0.02
			block door_width/2, door_height, 0.02
			del 3
			addx 0.18+door_width
			roty 90*(opening_in3D_in_car_pos/100)
			add -door_width/2, 0, -0.02
			block door_width/2, door_height, 0.02
			del 3
			del 1
		endif

		if bControlPanel then
			if carSide1+car_width/2-door_pos < shaft_inner_width/2 then
				add neg, 1, 0
			else
				add pos, 1, 0
			endif
			gosub "outside control panel"
			del 1
		endif
	else										! --- Simple
		material door_panel_mat
		addz -elev_wall_thick
		block opening_width, opening_height, elev_wall_thick
		del 1
	endif
return

"elev car door":
	pen gs_cont_pen
	addz -carWallThk+elevCarOffset
	if door_style_m = 1 then
		material door_panel_mat

		if RightDoor then
			mulx -1
		else
			mulx 1
		endif
		addx -door_width/2
		add -(door_width/2)*(_opening_in3D/100), 0, carWallThk-elevCarDoorThk
		block door_width/2, door_height, elevCarDoorThk
		del 1
		add door_width/2-door_width*(_opening_in3D/100), 0, carWallThk-elevCarDoorThk*2
		block door_width/2, door_height, elevCarDoorThk
		del 3

		addz carWallThk

		if RightDoor then
			addx door_width
		else
			addx 0
		endif

		if SYMB_MIRRORED then MULX -1  ! Rotated or mirrored window
		cutpolya 5, 2, -elevCarDoorThk*2,
			0, 0, 15,
			0, door_height, 15,
			-door_width, door_height, 15,
			-door_width, 0, 15,
			0, 0, -1
		num_cutend = num_cutend+1
		if SYMB_MIRRORED then del 1  ! Rotated or mirrored window
		del 2
	endif

	if door_style_m = 2 then
		addx -door_width/2

		material door_panel_mat
		addx -(door_width/2)*(_opening_in3D/100)
		block door_width/2, door_height, elevCarDoorThk
		del 1
		addx door_width/2+(door_width/2)*(_opening_in3D/100)
		block door_width/2, door_height, elevCarDoorThk
		del 1
		! one transformation left for the cutform!
	endif

	if door_style_m = 3 then
		addx -door_width/2

		for i = -1 to 1 step 2
			addx door_width*(i < 0)
			mulx i
			material door_panel_mat
			roty 90*(_opening_in3D/100)
			prism_ 10, elevCarDoorThk,
				0, 0, 15,
				door_width/2, 0, 15,
				door_width/2, door_height, 15,
				0, door_height, 15,
				0, 0, -1,
				0.05, door_height/2, 15,
				door_width/2-0.05, door_height/2, 15,
				door_width/2-0.05, door_height-0.05, 15,
				0.05, door_height-0.05, 15,
				0.05, door_height/2, -1
			addz (elevCarDoorThk-elevCarGlassThk)/2
			material door_glass_mat
			prism_ 5, elevCarGlassThk,
				0.05, door_height/2, 15,
				door_width/2-0.05, door_height/2, 15,
				door_width/2-0.05, door_height-0.05, 15,
				0.05, door_height-0.05, 15,
				0.05, door_height/2, -1
			del 4
		next i
		del 1
	endif

	if door_style_m = 4 then
		addx -door_width/2

		for i = -1 to 1 step 2
			addx door_width*(i < 0)
			mulx i
			material door_panel_mat
			addx -(door_width/2)*(_opening_in3D/100)
			prism_ 10, elevCarDoorThk,
				0, 0, 15,
				door_width/2, 0, 15,
				door_width/2, door_height, 15,
				0, door_height, 15,
				0, 0, -1,
				0.03, 0.15, 15,
				door_width/2-0.03, 0.15, 15,
				door_width/2-0.03, door_height-0.05, 15,
				0.03, door_height-0.05, 15,
				0.03,  0.15, -1
			addz (elevCarDoorThk-elevCarGlassThk)/2
			material door_glass_mat
			prism_ 5, elevCarGlassThk,
				0.03, 0.15, 15,
				door_width/2-0.03, 0.15, 15,
				door_width/2-0.03, door_height-0.05, 15,
				0.03, door_height-0.05, 15,
				0.03, 0.15, -1
			del 4
		next i
		! one transformation left for the cutform!
	endif

	if  door_style_m = 5 then
		if RightDoor then
			mulx -1
		else
			mulx 1
		endif

		addx -door_width/2

		material door_panel_mat
		addx -(door_width/2)*(_opening_in3D/100)
		block (door_width/2), door_height, elevCarDoorThk
		add (door_width/2)*(_opening_in3D/100), 0, elevCarDoorThk/4
		block (door_width/2), door_height, elevCarDoorThk/2
		del 2

		cutform 5, 1, 2,
			0, 0, 1, elevCarDoorThk,
			0, 0, 15,
			-door_width/2, 0, 15,
			-door_width/2, door_height, 15,
			0, door_height, 15,
			0, 0, -1
		num_cutend = num_cutend+1
		del 2
	endif

	if door_style_m = 2 or door_style_m = 4 then
		addz carWallThk-elevCarOffset
		for i=1 to 2
			cutform 5, 1, 2,
				0, 0, 1, elevCarDoorThk,
				0, 0, 15,
				-door_width/2, 0, 15,
				-door_width/2, door_height, 15,
				0, door_height, 15,
				0, 0, -1
			num_cutend = num_cutend+1

			addx door_width+door_width/2
		next i
		del 3
		del 1	! for previous transformation!
	endif

	if SYMB_MIRRORED then MULX -1           ! Mirrored object
	material car_int_mat
	cutpolya 5, 2, -0.10,
		-door_width/2, 0, 15,
		door_width/2, 0, 15,
		door_width/2, door_height, 15,
		-door_width/2, door_height, 15,
		-door_width/2, 0, -1
	num_cutend = num_cutend+1
	if SYMB_MIRRORED then del 1             ! Mirrored object
	del 1
return

"inside control panel":
	numStory = 1+num_stories_above_HS+num_stories_below_HS
	if bSplitLevelStories then numStory = numStory+num_SL_stories_above_HS+num_SL_stories_below_HS
	distButton = 0.10/4

	rowIntNumStory = 15
	columnNumStory  = 1
	if numStory > rowIntNumStory then
		columnNumStory = INT(numStory/rowIntNumStory)
		remainStory = numStory mod rowIntNumStory
		if remainStory then columnNumStory = columnNumStory + 1
	else
		remainStory = 0
		rowIntNumStory = numStory
	endif

	cp_height = distButton*(rowIntNumStory+2)

	rotx 90
	material cpanel_mat
	block 0.10, cp_height, 0.01

	material button_mat
	for i = 0 to 3
		add distButton/2+distButton*(i), distButton/2, 0.01
		resol 4
		rotz 45
		cone 0.005, 0.01, 0.008, 90, 90
		del 2
	next i

	for j = 1 to columnNumStory
		if j = columnNumStory and remainStory then
			currentNumStory = remainStory
		else
			currentNumStory = rowIntNumStory
		endif
		for i = 1 to currentNumStory
			add distButton/2+distButton*(j-1), cp_height-distButton*(i-0.5), 0.01
			resol 4
			rotz 45
			cone 0.005, 0.01, 0.008, 90, 90
			del 2
		next i
	next j
	del 1
return

"outside control panel":
	material cpanel_mat
	if i > 1 and i < num_connected_stories then
		block 0.08, 0.20, 0.01
		add 0.04, 0.10, 0.01
	else
		if i = 1 then
			block 0.08, 0.10, 0.01
			add 0.04, 0.095, 0.01
		else
			block 0.08, 0.10, 0.01
			add 0.04, 0.005, 0.01
		endif
	endif

	material button_mat
	resol 12
	if i > 1 then
		addy 0.015
		prism_ 8, 0.002,
			-0.005, 0, 15,
			-0.005, 0.012, 15,
			-0.014, 0.012, 15,
			0, 0.027, 15,
			0.014, 0.012, 15,
			0.005, 0.012, 15,
			0.005, 0, 15,
			-0.005, 0, -1
		del 1

		addy 0.065
		cone 0.006, 0.012, 0.01, 90, 90
		del 1
	endif

	if i < num_connected_stories then
		addy -0.015
		muly -1
		prism_ 8, 0.002,
			-0.005, 0, 15,
			-0.005, 0.012, 15,
			-0.014, 0.012, 15,
			0, 0.027, 15,
			0.014, 0.012, 15,
			0.005, 0.012, 15,
			0.005, 0, 15,
			-0.005, 0, -1
		del 2

		addy -0.065
		cone 0.006, 0.012, 0.01, 90, 90
		del 1
	endif
	del 1
return
