
! =============================================================================
! Localization
! =============================================================================

dim stGeneralUI[6]
	stGeneralUI[1]		= `Форма`
	stGeneralUI[2]		= `Количество Сегментов`
	stGeneralUI[3]		= `Тип`
	stGeneralUI[4]		= `Макс. Высота Технического Этажа`
	stGeneralUI[5]		= `Направление Второго Проема`
	stGeneralUI[6]		= `Расположение Противовеса`

dim stDimensionUI[19]
	stDimensionUI[1]	= `Показать Шахту Лифта`
	stDimensionUI[2]	= `Показать Стену Лифта`
	stDimensionUI[3]	= `Толщина Стены Лифта`
	stDimensionUI[4]	= `Наружное Покрытие Стены Лифта`
	stDimensionUI[5]	= `Наружная Поверхность Стены Лифта`
	stDimensionUI[6]	= `Покрытие Торцов Стены Лифта`
	stDimensionUI[7]	= `Внутренняя Ширина Кабины`
	stDimensionUI[8]	= `Внутренняя Глубина Кабины`
	stDimensionUI[9]	= `Наружная Ширина Шахты Лифта`
	stDimensionUI[10]	= `Наружная Глубина Шахты Лифта`
	stDimensionUI[11]	= `Внутренняя Ширина Шахты Лифта`
	stDimensionUI[12]	= `Внутренняя Глубина Шахты Лифта`
	stDimensionUI[13]	= `Ширина Шахты Лифта`
	stDimensionUI[14]	= `Глубина Шахты Лифта`
	stDimensionUI[15]	= `Зазор Между Стеной и Кабиной`
	stDimensionUI[16]	= `Ширина Противовеса`
	stDimensionUI[17]	= `Толщина Противовеса`
	stDimensionUI[18]	= `Толщина Стены Шахты Лифта`
	stDimensionUI[19]	= `Выступы Стены`

dim stCarUI[9]
	stCarUI[1]	= `Расположение Кабины`
	stCarUI[2]	= `Высота Кабины`
	stCarUI[3]	= `Стиль Двери Кабины`
	stCarUI[4]	= `Ширина Проема`
	stCarUI[5]	= `Ширина`
	stCarUI[6]	= `Высота`
	stCarUI[7]	= `Смещение Оси от Центра`
	stCarUI[8]	= `Дверь`
	stCarUI[9]	= `Вторая Дверь`

dim stSlabUI[6]
	stSlabUI[1]	= `Верхняя Плита`
	stSlabUI[2]	= `Верхняя Секция`
	stSlabUI[3]	= `Приямок`
	stSlabUI[4]	= `Нижняя Плита`
	stSlabUI[5]	= `Толщина Верхней Плиты`
	stSlabUI[6]	= `Толщина Нижней Плиты`

dim stStoryUI[7]
	stStoryUI[1]	= `Полуэтажи`
	stStoryUI[2]	= `Количество Этажей Выше Собственного`
	stStoryUI[3]	= `Нормальные Этажи`
	stStoryUI[4]	= `Полуэтажи`
	stStoryUI[5]	= `Количество Этажей Ниже Собственного`
	stStoryUI[6]	= `Собственный Этаж`
	stStoryUI[7]	= `Смещение Полуэтажей Относительно Нормальных Этажей`

dim stShaftUI[7]
	stShaftUI[1]	= `Стиль Двери Шахты`
	stShaftUI[2]	= `Обрамление Проема Шахты`
	stShaftUI[3]	= `Панель Управления`
	stShaftUI[4]	= `Расположение Двери Шахты`
	stShaftUI[5]	= `Имя Редактируемого Этажа`
	stShaftUI[6]	= `Высота Редактируемого Этажа`
	stShaftUI[7]	= `Расположение Двери Редактируемого Этажа`


! =============================================================================
! Setting the Tabpages
! =============================================================================
ui_dialog `Параметры Лифта`
ui_current_page gs_ui_current_page



dim gs_idUI[]

i = 1
gs_idUI[i] = 100	:	i=i+1	! General Settings
gs_idUI[i] = 200	:	i=i+1	! Dimensions
gs_idUI[i] = 300	:	i=i+1	! Elevator Car
if (bShowElevatorShaft & elevator_form_m = FORM_RECT) | (bShowElevatorWall & elevator_form_m = FORM_SEGMENTED) then
	gs_idUI[i] = 400	:	i=i+1	! Slabs and Overruns
	gs_idUI[i] = 500	:	i=i+1	! Story Settings
	gs_idUI[i] = 600	:	i=i+1	! Elevator Shaft
endif
gs_idUI[i] = 700	:	i=i+1	! Representation and Surfaces
gs_idUI[i] = 800	:	i=i+1	! Parameters for Listing

iPageIDRoot 		=-1
iPageIDGeneral		= 0
iPageIDDimensions	= 0
iPageIDCar			= 0
iPageIDSlabs		= 0
iPageIDStory		= 0
iPageIDShaft		= 0
iPageIDRepAndSurface = 0
iPageIDListing		= 0


dim tabIDs[]
dim tabNames[]
dim tabIcons[]

actIdx = 1
for i=1 to vardim1(gs_idUI)
	if gs_idUI[i] = 100 then							! General Settings
		iPageIDGeneral		= actIdx
		actIdx				= actIdx + 1
	endif

	if gs_idUI[i] = 200 then							! Dimensions
		iPageIDDimensions	= actIdx
		actIdx				= actIdx + 1
	endif

	if gs_idUI[i] = 300 then							! Elevator Car
		iPageIDCar			= actIdx
		actIdx				= actIdx + 1
	endif

	if gs_idUI[i] = 400 then							! Slabs and Overruns
		iPageIDSlabs			= actIdx
		actIdx				= actIdx + 1
	endif

	if gs_idUI[i] = 500 then							! Story Settings
		iPageIDStory			= actIdx
		actIdx				= actIdx + 1
	endif

	if gs_idUI[i] = 600 then							! Elevator Shaft
		iPageIDShaft			= actIdx
		actIdx				= actIdx + 1
	endif

	if gs_idUI[i] = 700 then							! Representation and Surfaces
		iPageIDRepAndSurface	= actIdx
		actIdx				= actIdx + 1
	endif

	if gs_idUI[i] = 800 then							! Parameters for Listing
		iPageIDListing		= actIdx
		actIdx				= actIdx + 1
	endif
next i

! =============================================================================
! General Variables
! =============================================================================

yInfield	= 19
yOutField	= 18
xSide		= 10
pageStartY 	= 5
yStart		= pageStartY + 3
xInfield	= 60
xHalfLevel	= 222
xEndLevel	= 444


! ==============================================================================
!UI_PAGES
! ==============================================================================

if iPageIDGeneral > 0 then
	UI_PAGE iPageIDGeneral, iPageIDRoot, `Общие Параметры`, "uiTab_Style_flat(1)"
		gosub "generalPage"
endif

if iPageIDDimensions> 0 then
	UI_PAGE iPageIDDimensions, iPageIDRoot, `Размеры`, "uiTab_Dimension_flat(1)"
		gosub "dimPage"
endif

if iPageIDCar> 0 then
	UI_PAGE iPageIDCar, iPageIDRoot, `Кабина Лифта`, "uiTab_ElevatorCar_flat(1)"
		gosub "carPage"
endif

if iPageIDSlabs> 0 then
	UI_PAGE iPageIDSlabs, iPageIDRoot, `Секции и Плиты`, "uiTab_ElevatorSlabs_flat(1)"
		gosub "slabsPage"
endif

if iPageIDStory> 0 then
	UI_PAGE iPageIDStory, iPageIDRoot, `Параметры Этажей`, "uiTab_ElevatorStory_flat(1)"
		gosub "storyPage"
endif

if iPageIDShaft> 0 then
	UI_PAGE iPageIDShaft, iPageIDRoot, `Шахта Лифта`, "uiTab_ElevatorShaft_flat(1)"
		gosub "shaftPage"
endif

if iPageIDRepAndSurface> 0 then
	UI_PAGE iPageIDRepAndSurface, iPageIDRoot, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
		gosub "repAndSurfacePage"
endif

if iPageIDListing> 0 then
	UI_PAGE iPageIDListing, iPageIDRoot, `Параметры Спецификаций`, "uiTab_Descriptions_flat(1)"
		gosub "listingPage"
endif


! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"generalPage":
! ------------------------------------------------------------------------------

	y	= yStart
	cx	= 105
	cy	= 77
	x1	= 0
	x2	= 109
	x3	= 275
	dy	= 29

	ui_style 0, 1
	ui_outfield stGeneralUI[1],	x1, y, x2-x1-2, yOutField	! Form
	ui_style 0, 0

	ui_infield{3} "elevator_form_m", x2-1, y, cx+2, cy+2,
		2, 1,			!elevator_form.png
		2, 1,
		98, 70, 98, 70,
		1, stElevForm[1], 1,
		2, stElevForm[2], 2
	iFlag = 4 * (elevator_form_m = FORM_SEGMENTED)
	y = y + cy + 12

	ui_outfield stGeneralUI[2],	x1, y, xHalfLevel-xInfield-xSide-x1-1, yOutField, 4 * (elevator_form_m = 1)	! Number of Segments
	ui_infield "segment_num", xHalfLevel-xInfield-xSide+2, y-2, xInfield, yInField 
	y = y + dy

	ui_outfield stGeneralUI[3],	x1, y, x2-x1-1, yOutField, iFlag	! Type
	ui_infield{3} "elevator_type_m", x2, y-2, cx, yInField,
		8, "",
		0, 0,
		0, 0, 0, 0,
		0, stElevType[1], 1,
		0, stElevType[2], 2
	y = y + dy

	ui_style 0, 1
	ui_outfield stGeneralUI[4],	x1, y, xHalfLevel-x1-xSide, yOutField	! Maximum Height of Structural Floor
	ui_style 0, 0
	y = y + yOutField + 1
	ui_infield{3} "gs_max_StrFl_height", x2, y, cx, yInField

	! ----------------------------------------------------------
	ui_separator xHalfLevel+1, pageStartY, xHalfLevel+1, 266
	! ----------------------------------------------------------

	y	= yStart
	ui_style 0, 1
	ui_outfield stGeneralUI[5],	xHalfLevel + xSide, y, xEndLevel - xHalfLevel - xSide, yOutField, iFlag	! Direction of Second Opening
	ui_style 0, 0
	y = y + yOutField + 1

	if elevator_form_m = FORM_SEGMENTED then
		ui_infield{3} "dir_second_opening_m", x3, y, cx+2, cy+2,
			2, "",
			0, 0,
			98, 70, 98, 70,
			0, "", 1,
			0, "", 2,
			0, "", 3,
			0, "", 4
	else
		ui_infield{3} "dir_second_opening_m", x3, y, cx+2, cy+2,
			2, 2,			!elevator_second_opening.png
			4, 1,
			98, 70, 98, 70,
			1, stDirection[1], 1,
			2, stDirection[2], 2,
			3, stDirection[3], 3,
			4, stDirection[4], 4
	endif
	y = y + cy + 12

	ui_style 0,1
	ui_outfield stGeneralUI[6],	xHalfLevel + xSide, y, xEndLevel - xHalfLevel - xSide, yOutField, iFlag	! Counterweight Position
	ui_style 0,0
	y = y + yOutField + 1

	sIdx = (dir_second_opening_m-1) * 3
	if elevator_form_m = FORM_SEGMENTED then
		ui_infield{3} "cweight_pos", x3, y, cx+2, cy+2,
			2, "",
			0, 0,
			98, 70, 98, 70,
			0, "", stPosition[1],
			0, "", stPosition[2],
			0, "", stPosition[3]
	else
		ui_infield{3} "cweight_pos_m", x3, y, cx+2, cy+2,
			2, 3,			!elevator_counterweight.png
			12, 4,
			98, 70, 98, 70,
			sIdx+1, stPosition[1], 1,
			sIdx+2, stPosition[2], 2,
			sIdx+3, stPosition[3], 3
	endif

return

! ------------------------------------------------------------------------------
"dimPage":
! ------------------------------------------------------------------------------

	y			= yStart
	xInfield	= 70

	ui_style 0, 0

	if elevator_form_m = FORM_SEGMENTED then

		ui_infield{3} "bShowElevatorWall", 0, y-2, xHalfLevel, yInfield,
			7, "",
			0, 0,
			0, 0, 0, 0,
			0, stDimensionUI[2], 0,
			0, stDimensionUI[2], 1

		sx	= 130
		sy	= 90

		ui_style 1,0
		if bShowElevatorWall then
			ui_pict 10, sx, sy, 183, 133, 1			! dim_segmented.png
			ui_infield "elev_wall_thick", sx+188, sy + 105, xInfield, yInfield	ui_tooltip stDimensionUI[3] 
			ui_infield "elev_wall_overhang", sx+143, sy + 122 + 16, xInfield, yInfield	ui_tooltip stDimensionUI[19]
		else
			ui_pict 11, sx+11, sy, 172, 117, 1			! dim_segmented_wo_wall.png
		endif
		ui_infield "car_width", sx+60, sy + 122 + 16*bShowElevatorWall, xInfield, yInfield	ui_tooltip stDimensionUI[7] 
		ui_infield "car_depth", sx+188, sy + 40, xInfield, yInfield	ui_tooltip stDimensionUI[8] 

	else

		ui_infield{3} "bShowElevatorShaft", 0, y-2, xHalfLevel, yInfield,
			7, "",
			0, 0,
			0, 0, 0, 0,
			0, stDimensionUI[1], 0,
			0, stDimensionUI[1], 1

		ui_style 1,0



		if elevator_type_m = 1 then

			sx	= 130
			sy	= 86
			ox	= 13
			oy	= 13

			if cweight_pos_m = CW_POS_NORMAL then
	
				if dir_second_opening_m = SECOND_OPENING_NONE then
					if bShowElevatorShaft then
						ui_pict 12, sx, sy, 194, 153, 1				! dim_none_normal.png					
					else
						ui_pict 13, sx+ox, sy+oy, 168, 127, 1		! dim_none_normal_wo_shaft.png
					endif
				endif
	
				if dir_second_opening_m = SECOND_OPENING_SIDE1 then
					if bShowElevatorShaft then
						ui_pict 14, sx, sy, 194, 153, 1				! dim_side_1_normal.png					
					else
						ui_pict 15, sx+ox, sy+oy, 168, 127, 1		! dim_side_1_normal_wo_shaft.png
					endif
				endif
	
				if dir_second_opening_m = SECOND_OPENING_SIDE2 then
					if bShowElevatorShaft then
						ui_pict 16, sx, sy, 194, 153, 1				! dim_side_2_normal.png
					else
						ui_pict 17, sx+ox, sy+oy, 168, 127, 1		! dim_side_2_normal_wo_shaft.png
					endif
				endif
	
				if bShowElevatorShaft then
					ui_infield "elev_shaft_thick", sx+167, sy + 157, xInfield, yInfield	ui_tooltip stDimensionUI[18]
				endif
		
				ui_infield "shaft_inner_width", sx+70, sy - 8 - bShowElevatorShaft*14, xInfield, yInfield	ui_tooltip stDimensionUI[11]
				ui_infield "shaft_inner_depth", sx-60-bShowElevatorShaft*14, sy + 72, xInfield, yInfield	ui_tooltip stDimensionUI[12]
				ui_infield "car_inner_width", sx+70, sy + 157 - not(bShowElevatorShaft)*13, xInfield, yInfield	ui_tooltip stDimensionUI[7]
				ui_infield "car_inner_depth", sx+185+bShowElevatorShaft*14, sy + 79, xInfield, yInfield	ui_tooltip stDimensionUI[8]
				ui_infield "cweight_width", sx+185+bShowElevatorShaft*14, sy + 3 + not(bShowElevatorShaft)*12, xInfield, yInfield	ui_tooltip stDimensionUI[16]
				ui_infield "cweight_depth", sx+185+bShowElevatorShaft*14, sy + 34, xInfield, yInfield	ui_tooltip stDimensionUI[17]
	
			endif
	
			if cweight_pos_m = CW_POS_SIDE1 then
	
				if dir_second_opening_m = SECOND_OPENING_NONE then
					if bShowElevatorShaft then
						ui_pict 18, sx-10, sy+10, 203, 144, 1				! dim_none_side_1.png
					else
						ui_pict 19, sx+ox-10, sy+oy+10, 176, 118, 1			! dim_none_side_1_wo_shaft.png
					endif
				endif
	
				if dir_second_opening_m = SECOND_OPENING_REAR then
					if bShowElevatorShaft then
						ui_pict 20, sx-10, sy+10, 203, 144, 1				! dim_rear_side_1.png
					else
						ui_pict 21, sx+ox-10, sy+oy+10, 177, 118, 1			! dim_rear_side_1_wo_shaft.png
					endif
				endif
	
				if dir_second_opening_m = SECOND_OPENING_SIDE2 then
					if bShowElevatorShaft then
						ui_pict 22, sx-10, sy+10, 203, 144, 1				! dim_side_2_side_1.png
					else
						ui_pict 23, sx+ox-10, sy+oy+10, 177, 118, 1			! dim_side_2_side_1_wo_shaft.png
					endif
				endif
	
				if bShowElevatorShaft then
					ui_infield "elev_shaft_thick", sx+167, sy + 157, xInfield, yInfield	ui_tooltip stDimensionUI[18]
				endif
	
				ui_infield "shaft_inner_width", sx+70, sy + 2 - bShowElevatorShaft*14, xInfield, yInfield	ui_tooltip stDimensionUI[11]
				ui_infield "shaft_inner_depth", sx-70-bShowElevatorShaft*14, sy + 72, xInfield, yInfield	ui_tooltip stDimensionUI[12]
				ui_infield "car_inner_width", sx+70, sy + 157 - not(bShowElevatorShaft)*13, xInfield, yInfield	ui_tooltip stDimensionUI[7]
				ui_infield "car_inner_depth", sx+185+bShowElevatorShaft*14, sy + 79, xInfield, yInfield	ui_tooltip stDimensionUI[8]
				ui_infield "cweight_width", sx-40+14+14*not(bShowElevatorShaft), sy + 2 - bShowElevatorShaft*14, xInfield, yInfield	ui_tooltip stDimensionUI[16]
				ui_infield "cweight_depth", sx-30+14, sy + 157 - not(bShowElevatorShaft)*13, xInfield, yInfield	ui_tooltip stDimensionUI[17]
	
			endif
	
			if cweight_pos_m = CW_POS_SIDE2 then
	
				if dir_second_opening_m = SECOND_OPENING_NONE then
					if bShowElevatorShaft then
						ui_pict 24, sx-10, sy+10, 203, 144, 1				! dim_none_side_2.png
					else
						ui_pict 25, sx+ox-10, sy+oy+10, 177, 118, 1			! dim_none_side_2_wo_shaft.png
					endif
				endif
	
				if dir_second_opening_m = SECOND_OPENING_REAR then
					if bShowElevatorShaft then
						ui_pict 26, sx-10, sy+10, 203, 144, 1				! dim_rear_side_2.png
					else
						ui_pict 27, sx+ox-10, sy+oy+10, 177, 118, 1			! dim_rear_side_2_wo_shaft.png
					endif
				endif
	
				if dir_second_opening_m = SECOND_OPENING_SIDE1 then
					if bShowElevatorShaft then
						ui_pict 28, sx-10, sy+10, 203, 144, 1				! dim_side_1_side_2.png
					else
						ui_pict 29, sx+ox-10, sy+oy+10, 177, 118, 1			! dim_side_1_side_2_wo_shaft.png
					endif
				endif
	
				if bShowElevatorShaft then
					ui_infield "elev_shaft_thick", sx-36, sy + 157, xInfield, yInfield	ui_tooltip stDimensionUI[18]
				endif
	
				ui_infield "shaft_inner_width", sx+60, sy + 2 - bShowElevatorShaft*14, xInfield, yInfield	ui_tooltip stDimensionUI[11]
				ui_infield "shaft_inner_depth", sx-70-bShowElevatorShaft*14, sy + 72, xInfield, yInfield	ui_tooltip stDimensionUI[12]
				ui_infield "car_inner_width", sx+60, sy + 157 - not(bShowElevatorShaft)*13, xInfield, yInfield	ui_tooltip stDimensionUI[7]
				ui_infield "car_inner_depth", sx+185+bShowElevatorShaft*14, sy + 79, xInfield, yInfield	ui_tooltip stDimensionUI[8]
				ui_infield "cweight_width", sx+144+14*bShowElevatorShaft, sy + 2 - bShowElevatorShaft*14, xInfield, yInfield	ui_tooltip stDimensionUI[16]
				ui_infield "cweight_depth", sx+132+14, sy + 157 - not(bShowElevatorShaft)*13, xInfield, yInfield	ui_tooltip stDimensionUI[17]
	
			endif
		else
			! ----------------------- Hydraulic --------------------------------

			sx	= 130
			sy	= 86
			ox	= 13
			oy	= 13


			if dir_second_opening_m = SECOND_OPENING_NONE then
				if bShowElevatorShaft then
					ui_pict 61, sx, sy, 194, 153, 1				! dim_none_hydraulic.png
				else
					ui_pict 62, sx+ox, sy+oy, 168, 127, 1			! dim_none_hydraulic_wo_shaft.png
				endif
			endif

			if dir_second_opening_m = SECOND_OPENING_REAR then
				if bShowElevatorShaft then
					ui_pict 63, sx-10, sy+10, 203, 144, 1				! dim_rear_hydraulic.png
				else
					ui_pict 64, sx+ox-10, sy+oy+10, 177, 118, 1			! dim_rear_hydraulic_wo_shaft.png
				endif
			endif

			if dir_second_opening_m = SECOND_OPENING_SIDE1 then
				if bShowElevatorShaft then
					ui_pict 65, sx, sy, 194, 153, 1				! dim_side1_hydraulic.png
				else
					ui_pict 66, sx+ox, sy+oy, 168, 127, 1			! dim_side1_hydraulic_wo_shaft.png
				endif
			endif

			if dir_second_opening_m = SECOND_OPENING_SIDE2 then
				if bShowElevatorShaft then
					ui_pict 67, sx, sy, 194, 153, 1				! dim_side2_hydraulic.png
				else
					ui_pict 68, sx+ox, sy+oy, 168, 127, 1			! dim_side2_hydraulic_wo_shaft.png
				endif
			endif

			if bShowElevatorShaft then
				ui_infield "elev_shaft_thick", sx-36+204, sy + 157, xInfield, yInfield	ui_tooltip stDimensionUI[18]
			endif

			ui_infield "shaft_inner_width", sx+70, sy - 8 - bShowElevatorShaft*14 + (dir_second_opening_m = SECOND_OPENING_REAR)*10, xInfield, yInfield	ui_tooltip stDimensionUI[11]
			ui_infield "shaft_inner_depth", sx-60-bShowElevatorShaft*14-(dir_second_opening_m = SECOND_OPENING_REAR)*10, sy + 72, xInfield, yInfield	ui_tooltip stDimensionUI[12]
			ui_infield "car_inner_width", sx+70, sy + 157 - not(bShowElevatorShaft)*13, xInfield, yInfield	ui_tooltip stDimensionUI[7]
			ui_infield "car_inner_depth", sx+185+bShowElevatorShaft*14, sy + 79, xInfield, yInfield	ui_tooltip stDimensionUI[8]

		endif
	endif

return

! ------------------------------------------------------------------------------
"carPage":
! ------------------------------------------------------------------------------

	y			= yStart
	cx			= 140
	cy			= 50
	x1			= 0
	x2			= 154
	x3			= 253
	x4			= 374
	dy			= 25
	xInfield	= 70

	ui_style 0, 0

	ui_outfield stCarUI[1],	x1, y, x2-x1-1, yOutField	! Car Position
	ui_infield "car_pos_story", x2, y-2, cx, yInField

	y = y + dy

	ui_outfield stCarUI[2],	x1, y, x2-x1-1, yOutField	! Car Height
	ui_style 1,0
	ui_infield "car_height", x2, y-2, xInfield, yInField
	ui_style 0,0
	y = y + dy

	ui_outfield stCarUI[3],	x1, y, x2-x1-2, yOutField	! Car Door Style
	ui_infield{3} "door_style_m", x2-1, y, cx+2, cy+2,
			2, 4,		!elevator_door.png
			6, 6,
			130, 38, 130, 38,
			1, stDoorStyle[1], 0,
			2, stDoorStyle[2], 1,
			3, stDoorStyle[3], 2,
			4, stDoorStyle[4], 3,
			5, stDoorStyle[5], 4,
			6, stDoorStyle[6], 5
	y = y + cy + 12

	! ----------------------------------------------------------
	ui_separator 0, y, 444, y
	! ----------------------------------------------------------
	y = y + 8

	ui_style 0,1
	ui_outfield stCarUI[4],	x1, y, x2-x1-1, yOutField	! Clear Opening
	y = y + dy
	ui_style 0,0

	ui_outfield stCarUI[5],	x1, y, x2-x1-1, yOutField	! Width
	ui_style 1,0
	ui_infield "door_width", x2, y-2, xInfield, yInField
	ui_style 0,0

	ui_outfield stCarUI[6],	x3, y, x4-x3-1, yOutField	! Width
	ui_style 1,0
	ui_infield "door_height", x4, y-2, xInfield, yInField
	ui_style 0,0
	y = y + dy

	ui_style 0,1
	ui_outfield stCarUI[7],	x1, y, x2-x1-1, yOutField	! Axis from Center
	y = y + dy
	ui_style 0,0

	ui_outfield stCarUI[8],	x1, y, x2-x1-1, yOutField	! Door
	ui_style 1,0
	ui_infield "door_pos", x2, y-2, xInfield, yInField
	ui_style 0,0

	iFlag = 4* (dir_second_opening_m = SECOND_OPENING_NONE | elevator_form_m = FORM_SEGMENTED)
	ui_outfield stCarUI[9],	x3, y, x4-x3-1, yOutField, iFlag	! Second Door
	ui_style 1,0
	ui_infield "second_door_pos", x4, y-2, xInfield, yInField
	ui_style 0,0
	y = y + dy

return

! ------------------------------------------------------------------------------
"slabsPage":
! ------------------------------------------------------------------------------

	y			= yStart
	x1			= 0
	x2			= 170
	x3			= 206
	dy			= 25
	xInfield	= 70

	ui_style 0, 0

	ui_infield{3} "bTopSlab", x1, y-2, x2-x1, yOutfield,
		7, "",
		0, 0,
		0, 0, 0, 0,
		0, stSlabUI[1], 0,
		0, stSlabUI[1], 1
	y = y + dy

	ui_infield{3} "bPenthouse", x1, y-2, x2-x1, yOutfield,
		7, "",
		0, 0,
		0, 0, 0, 0,
		0, stSlabUI[2], 0,
		0, stSlabUI[2], 1
	y = y + dy

	ui_infield{3} "bPith", x1, y-2, x2-x1, yOutfield,
		7, "",
		0, 0,
		0, 0, 0, 0,
		0, stSlabUI[3], 0,
		0, stSlabUI[3], 1
	y = y + dy

	ui_infield{3} "bPitSlab", x1, y-2, x2-x1, yOutfield,
		7, "",
		0, 0,
		0, 0, 0, 0,
		0, stSlabUI[4], 0,
		0, stSlabUI[4], 1
	y = y + dy
	

	cx = 280
	cy = 81

	ui_style 1,0
	! --- Over ---
	if bTopSlab & bPenthouse then
		ui_pict 30, cx, cy-44, 104, 44, 1		!over_o_full.png
		ui_infield "penthouse_height", x3, cy - 29, xInfield, yInField ui_tooltip stSlabUI[2]
		ui_infield "top_slab_thick", x3, cy - 48, xInfield, yInField ui_tooltip stSlabUI[5]
	else
		if bTopSlab then
			ui_pict 31, cx, cy-15, 104, 15, 1		!over_o_slab.png
			ui_infield "top_slab_thick", x3, cy - 21, xInfield, yInField ui_tooltip stSlabUI[5]
		else
			if bPenthouse then
				ui_pict 32, cx, cy-38, 104, 38, 1		!over_o_no_slab.png
				ui_infield "penthouse_height", x3, cy - 33, xInfield, yInField ui_tooltip stSlabUI[2]
			else
				ui_pict 33, cx+11, cy-9, 93, 9		!over_o_none.png
			endif
		endif
	endif

	! --- Base ---
	ui_pict 34, cx+11, cy, 93, 114, 1		!over_base.png

	! --- Under ---
	if bPitSlab & bPith then
		ui_pict 35, cx, cy+114, 104, 64, 1		!over_u_full.png
		ui_infield "pit_depth", x3, cy + 146, xInfield, yInField ui_tooltip stSlabUI[3]
		ui_infield "pit_slab_thick", x3, cy + 165, xInfield, yInField ui_tooltip stSlabUI[4]
	else
		if bPitSlab then
			ui_pict 36, cx, cy+114, 104, 38, 1		!over_u_slab.png
			ui_infield "pit_slab_thick", x3, cy + 139, xInfield, yInField ui_tooltip stSlabUI[4]
		else
			if bPith then
				ui_pict 37, cx, cy+114, 104, 58, 1		!over_u_no_slab.png
				ui_infield "pit_depth", x3, cy + 148, xInfield, yInField ui_tooltip stSlabUI[3]
			else
				ui_pict 38, cx+11, cy+114, 93, 30, 1		!over_u_none.png
			endif
		endif
	endif

return

! ------------------------------------------------------------------------------
"storyPage":
! ------------------------------------------------------------------------------

	y			= yStart
	x1			= 0
	x2			= 171
	x3			= 242
	dy			= 25
	xInfield	= 70

	ui_style 0, 0

	ui_infield{3} "bSplitLevelStories", x1, y-2, x3-x1-1, yOutfield,
		7, "",
		0, 0,
		0, 0, 0, 0,
		0, stStoryUI[1], 0,
		0, stStoryUI[1], 1
	y = y + dy

	ui_style 0,1
	ui_outfield stStoryUI[2], x1, y, x3-x1-1, yOutfield		!Number of Stories above Home Story
	ui_style 0,0
	y = y + dy

	ui_outfield stStoryUI[3], x1, y, x2-x1-1, yOutfield, 4*not(num_above_story)		!Normal Stories
	ui_infield "num_stories_above_HS", x2, y-3, xHalfLevel-x2, yInfield
	y = y + dy

	ui_outfield stStoryUI[4], x1, y, x2-x1-1, yOutfield, 4*(not(bSplitLevelStories) | (num_above_story & not(num_stories_above_HS)))		!Split-Level Stories
	ui_infield "num_SL_stories_above_HS", x2, y-3, xHalfLevel-x2, yInfield
	y = y + dy

	ui_style 0,1
	ui_outfield stStoryUI[5], x1, y, x3-x1-1, yOutfield		!Number of Stories below Home Story`
	ui_style 0,0
	y = y + dy

	ui_outfield stStoryUI[3], x1, y, x2-x1-1, yOutfield, 4*not(num_below_story)		!Normal Stories
	ui_infield "num_stories_below_HS", x2, y-3, xHalfLevel-x2, yInfield
	y = y + dy

	ui_outfield stStoryUI[4], x1, y, x2-x1-1, yOutfield, 4*(not(bSplitLevelStories)	| (num_below_story & not(num_stories_below_HS)))		!Split-Level Stories
	ui_infield "num_SL_stories_below_HS", x2, y-3, xHalfLevel-x2, yInfield
	y = y + dy


	cx = 327
	cy = 81

	if bSplitLevelStories then
		! --- Over ---
		if bTopSlab & bPenthouse then
			ui_pict 40, cx, cy-44, 93, 44, 1				!story_split_o_full.png
		else
			if bTopSlab then
				ui_pict 41, cx, cy-16, 93, 16, 1			!story_split_o_slab.spng
			else
				if bPenthouse then
					ui_pict 42, cx, cy-38, 93, 38, 1		!story_split_o_over.png
				else
					ui_pict 43, cx, cy-9, 93, 9, 1			!story_split_o_none.png
				endif
			endif
		endif
	
		! --- Base ---
		
		ui_pict 44, cx-85, cy + 67, 85, 1, 1						!story_line_top.png
		ui_pict 45, cx-85, cy + 105, 85, 1, 1						!story_line_bot.png
		ui_outfield stStoryUI[6], cx-85, cy + 78, 84, yOutField		! Home Story
		ui_pict 46, cx, cy, 93, 143, 1								!story_split_base.png
		ui_pict 47, cx-11, cy + 67 - 19, 11, 19, 1					!story_dim.png
		ui_style 1,0
		ui_infield "dist_SL_story_to_normal_story", cx-85, cy+49, xInfield, yInField ui_tooltip stStoryUI[7]

	
		! --- Under ---
		if bPitSlab & bPith then
			ui_pict 48, cx, cy+143, 93, 35, 1		!story_split_u_full.png
		else
			if bPitSlab then
				ui_pict 49, cx, cy+143, 93, 9, 1		!story_split_u_slabs.png
			else
				if bPith then
					ui_pict 50, cx, cy+143, 93, 29, 1		!story_split_u_over.png
				else
					ui_pict 51, cx, cy+143, 93, 1, 1		!story_split_u_none.png
				endif
			endif
		endif

	else
		! --- Over ---
		if bTopSlab & bPenthouse then
			ui_pict 52, cx, cy-44, 93, 44, 1		!story_o_full.png
		else
			if bTopSlab then
				ui_pict 53, cx, cy-16, 93, 16, 1		!story_o_slab.spng
			else
				if bPenthouse then
					ui_pict 54, cx, cy-38, 93, 38, 1		!story_o_over.png
				else
					ui_pict 55, cx, cy-9, 93, 9, 1		!story_o_none.png
				endif
			endif
		endif
	
		! --- Base ---
		
		ui_pict 45, cx-85, cy + 67, 85, 1, 1			!story_line_bot.png
		ui_pict 45, cx-85, cy + 105, 85, 1, 1			!story_line_bot.png
		ui_outfield stStoryUI[6], cx-85, cy + 78, 84, yOutField		! Home Story
		ui_pict 56, cx, cy, 93, 143, 1					!story_base.png
	
		! --- Under ---
		if bPitSlab & bPith then
			ui_pict 57, cx, cy+143, 93, 35, 1		!story_u_full.png
		else
			if bPitSlab then
				ui_pict 58, cx, cy+143, 93, 9, 1		!story_u_slabs.png
			else
				if bPith then
					ui_pict 59, cx, cy+143, 93, 29, 1		!story_u_over.png
				else
					ui_pict 60, cx, cy+143, 93, 1, 1		!story_u_none.png
				endif
			endif
		endif
	endif


return

! ------------------------------------------------------------------------------
"shaftPage":
! ------------------------------------------------------------------------------

	y	= 37
	cx	= 140
	x1	= 0
	x2	= 228
	dy	= 25

	ui_style 0, 0

	ui_outfield stShaftUI[1],	x1, y, x2-x1-1, yOutField	! Shaft Door Style
	ui_infield{3} "elev_door_style_m", x2, y-2, cx, yInField,
		8, "",
		0, 0, 
		0, 0, 0, 0,
		0, stElevDoorStyle[1], 1,
		0, stElevDoorStyle[2], 2
	y = y + dy

	ui_infield{3} "bOpeningFrame", x1, y-2, x2+cx-x1, yOutField,
		7, "",
		0, 0, 
		0, 0, 0, 0,
		0, stShaftUI[2], 0,
		0, stShaftUI[2], 1
	y = y + dy

	ui_infield{3} "bControlPanel", x1, y-2, x2+cx-x1, yOutField,
		7, "",
		0, 0, 
		0, 0, 0, 0,
		0, stShaftUI[3], 0,
		0, stShaftUI[3], 1
	y = y + dy

	! ----------------------------------------------------------
	ui_separator 0, y, 444, y
	! ----------------------------------------------------------
	y = y + 7

	ui_style 0, 1
	ui_outfield stShaftUI[4], x1, y, x2+cx-x1, 12	!Door Position on Shaft
	ui_style 0, 0
	y = y + dy

	ui_outfield stShaftUI[5],	x1, y, x2-x1-1, yOutField	! Name of Story to be Edited
	ui_infield "edited_story", x2, y-2, cx, yInField
	y = y + dy

	ui_outfield stShaftUI[6],	x1, y, x2-x1-1, yOutField, 4	! Name of Story to be Edited
	ui_infield "edited_story_height", x2+cx-77, y-2, 77, yInField
	y = y + dy

	iFlag = 4*bLockedDoorEditedStory
	ui_outfield stShaftUI[7],	x1, y, x2-x1-1, yOutField, iFlag	! Door Position of Edited Story
	ui_infield{3} "edited_story_door_pos_m", x2, y-2, cx, yInField,
			8, "",
			0, 0,
			0, 0, 0, 0,
			0, stDoorPosition[1], 1,
			0, stDoorPosition[2], 2,
			0, stDoorPosition[3], 3,
			0, stDoorPosition[4], 4
	y = y + dy

return

! ------------------------------------------------------------------------------
"repAndSurfacePage":
! ------------------------------------------------------------------------------

	ui_listfield 2, 0, pageStartY + 1, 444, 266-36, 0
	
	! ---------------- 2D Representation ---------------------
	i = 1
	ui_listitem i, 2, "", 0, "", `2D-отображение`					: i = i + 1
		ui_listitem i, 2, "gs_detlevel_2D", 1							: i = i + 1
		if elevator_form_m = FORM_RECT & (gs_detlevel_2D_m = 1 | gs_detlevel_2D_m = 2) then
			ui_listitem i, 2, "bShowMechin2D", 1						: i = i + 1
		endif

	! ---------------- 3D Representation ---------------------
	i = 5
	ui_listitem i, 2, "", 0, "", `3D-отображение`					: i = i + 1
		ui_listitem i, 2, "gs_detlevel_3D", 1, "", `3D-детализация`	: i = i + 1
		ui_listitem i, 2, "gs_shadow", 1								: i = i + 1
		if elevator_form_m = FORM_SEGMENTED then
			ui_listitem i, 2, "bShowMullion", 1							: i = i + 1
		endif
		ui_listitem i, 2, "opening_in3D", 1, "", `Открывание Двери в 3D (%)`	: i = i + 1

	! ---------------- Fills and Pens ---------------------
	i = 10
	ui_listitem i, 2, "", 0, "", `Перья и Штриховки`						: i = i + 1
		ui_listitem i, 2, "gs_cont_pen", 1								: i = i + 1
		ui_listitem i, 2, "gs_fill_type", 1								: i = i + 1
		ui_listitem i, 2, "gs_fill_pen", 1								: i = i + 1
		ui_listitem i, 2, "gs_back_pen", 1								: i = i + 1

		if elevator_form_m = FORM_RECT then
			if bShowElevatorShaft then
				ui_listitem i, 2, "gs_wall_cont_pen", 1					: i = i + 1
				ui_listitem i, 2, "gs_wall_fill_type", 1				: i = i + 1
				ui_listitem i, 2, "gs_wall_fill_pen", 1					: i = i + 1
				ui_listitem i, 2, "gs_wall_back_pen", 1					: i = i + 1
				ui_listitem i, 2, "gs_opening_fill_type", 1				: i = i + 1
				ui_listitem i, 2, "gs_opening_fill_pen", 1				: i = i + 1
				ui_listitem i, 2, "gs_opening_back_pen", 1				: i = i + 1
			endif

			ui_listitem i, 2, "gs_gap_fill_type", 1						: i = i + 1
			ui_listitem i, 2, "gs_gap_fill_pen", 1						: i = i + 1
			ui_listitem i, 2, "gs_gap_back_pen", 1						: i = i + 1
		else
			if bShowElevatorWall then
				ui_listitem i, 2, "gs_wall_cont_pen", 1					: i = i + 1
				ui_listitem i, 2, "gs_wall_fill_type", 1				: i = i + 1
				ui_listitem i, 2, "gs_wall_fill_pen", 1					: i = i + 1
				ui_listitem i, 2, "gs_wall_back_pen", 1					: i = i + 1
				ui_listitem i, 2, "gs_opening_fill_type", 1				: i = i + 1
				ui_listitem i, 2, "gs_opening_fill_pen", 1				: i = i + 1
				ui_listitem i, 2, "gs_opening_back_pen", 1				: i = i + 1
			endif
		endif

	! ---------------- Surfaces ---------------------
	i = 40
	! Description without Surface expression
	ui_listitem i, 2, "", 0, "", `Покрытия`									: i = i + 1
		ui_listitem i, 2, "door_panel_mat", 	1, "", `Панель Двери`			: i = i + 1
		ui_listitem i, 2, "door_glass_mat",		1, "", `Стекло Двери`			: i = i + 1
		if	elevator_form_m = FORM_RECT			& bShowElevatorShaft |\
			elevator_form_m = FORM_SEGMENTED	& bShowElevatorWall then
				ui_listitem i, 2, "opening_frame_mat", 	1, "", `Обрамление Проема`		: i = i + 1
		endif
		if bControlPanel then
			ui_listitem i, 2, "cpanel_mat", 	1, "", `Панель Управления`		: i = i + 1
			ui_listitem i, 2, "button_mat", 	1, "", `Кнопки`				: i = i + 1
		endif

		! --- Car ---
		ui_listitem i, 2, "car_ext_mat", 		1, "", `Верх и Низ Кабины`		: i = i + 1
		ui_listitem i, 2, "car_int_mat", 		1, "", `Внутреннее Покрытие Кабины`	: i = i + 1
		ui_listitem i, 2, "car_pave_mat", 		1, "", `Пол Кабины`			: i = i + 1
		ui_listitem i, 2, "car_ceiling_mat", 	1, "", `Потолок Кабины`			: i = i + 1

		if elevator_form_m = FORM_RECT then
			if bShowElevatorShaft then
				ui_listitem i, 2, "elev_shaft_ext_mat", 	1, "", `Наружная Поверхность Шахты`	: i = i + 1
				ui_listitem i, 2, "elev_shaft_int_mat", 	1, "", `Внутренняя Поверхность Шахты`	: i = i + 1
				ui_listitem i, 2, "elev_shaft_edge_mat", 	1, "", `Торцы Шахты Лифта`			: i = i + 1
			endif
		else
			ui_listitem i, 2, "car_front_mat", 	1, "", `Передняя Панель Кабины`		: i = i + 1
			ui_listitem i, 2, "car_back_mat", 	1, "", `Задние Панели Кабины`		: i = i + 1

			if bShowMullion then
				ui_listitem i, 2, "car_mullion_mat", 1, "", `Средник`		: i = i + 1
			endif

			if bShowElevatorWall then
				ui_listitem i, 2, "elev_wall_ext_mat", 	1, "", `Внутренняя Сторона Стены Лифта`		: i = i + 1
				ui_listitem i, 2, "elev_wall_int_mat", 	1, "", `Наружная Поверхность Стены Лифта`		: i = i + 1
				ui_listitem i, 2, "elev_wall_edge_mat", 1, "", `Торцы Стены Лифта`					: i = i + 1
			endif
		endif

		if (elevator_form_m = FORM_RECT & bShowElevatorShaft) |\
			(elevator_form_m = FORM_SEGMENTED & bShowElevatorWall) then
			if bTopSlab then
				ui_listitem i, 2, "top_slab_ext_mat",  	1, "", `Наружная Сторона Верхней Плиты`		: i = i + 1
				ui_listitem i, 2, "top_slab_int_mat",  	1, "", `Внутренняя Сторона Верхней Плиты`		: i = i + 1
				ui_listitem i, 2, "top_slab_edge_mat",  1, "", `Торцы Верхней Плиты`				: i = i + 1
			endif

			if bPitSlab then
				ui_listitem i, 2, "pit_slab_ext_mat",   1, "", `Внутренняя Сторона Нижней Плиты`		: i = i + 1
				ui_listitem i, 2, "pit_slab_int_mat",  	1, "", `Наружная Сторона Нижней Плиты`		: i = i + 1
				ui_listitem i, 2, "pit_slab_edge_mat",  1, "", `Торцы Нижней Плиты`				: i = i + 1
			endif
		endif

return

! ------------------------------------------------------------------------------
"listingPage":
! ------------------------------------------------------------------------------

	ui_listfield 3, 0, pageStartY + 1, 444, 266-36, 0

	i = 1
	ui_listitem i, 3, "", 0, "", `Параметры Спецификаций`				: i = i + 1
		ui_listitem i, 3, "gs_list_cost", 1								: i = i + 1
		ui_listitem i, 3, "gs_list_manufacturer", 1						: i = i + 1
		ui_listitem i, 3, "gs_list_note", 1								: i = i + 1
		ui_listitem i, 3, "gs_list_location", 1							: i = i + 1
		ui_listitem i, 3, "gs_list_accessories", 1						: i = i + 1
		ui_listitem i, 3, "FM_Type", 1									: i = i + 1
		ui_listitem i, 3, "FM_InventoryNumber", 1						: i = i + 1
		ui_listitem i, 3, "FM_SerialNumber", 1							: i = i + 1
		ui_listitem i, 3, "FM_ProductionYear", 1						: i = i + 1
		ui_listitem i, 3, "FM_ObjectWeight", 1							: i = i + 1
		ui_listitem i, 3, "FM_ObjectWeightUnit", 1						: i = i + 1
		ui_listitem i, 3, "FM_FloorSpace", 1							: i = i + 1
		ui_listitem i, 3, "FM_Speed", 1									: i = i + 1
		ui_listitem i, 3, "FM_LiftCapacityNumber", 1					: i = i + 1
		ui_listitem i, 3, "FM_TransportablePersons", 1					: i = i + 1
		ui_listitem i, 3, "FM_HeadRoom", 1								: i = i + 1
		ui_listitem i, 3, "gs_list_custom1", 1							: i = i + 1
		ui_listitem i, 3, "gs_list_custom2", 1							: i = i + 1
		ui_listitem i, 3, "gs_list_custom3", 1							: i = i + 1
		ui_listitem i, 3, "gs_list_custom4", 1							: i = i + 1
		ui_listitem i, 3, "gs_list_custom5", 1							: i = i + 1

	i = 30
	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		ui_listitem i, 3, "", 0, "", `Параметры Спецификаций ONORM`			: i = i + 1
			ui_listitem i, 3, "gs_onorm_name_value", 1					: i = i + 1
			ui_listitem i, 3, "gs_onorm_name", 1						: i = i + 1
			ui_listitem i, 3, "gs_onorm_maxpersons", 1					: i = i + 1
			ui_listitem i, 3, "gs_onorm_objectweight", 1				: i = i + 1
			ui_listitem i, 3, "gs_onorm_dimensions", 1					: i = i + 1
			ui_listitem i, 3, "gs_onorm_power", 1						: i = i + 1
			ui_listitem i, 3, "gs_onorm_elevnum", 1						: i = i + 1
			ui_listitem i, 3, "gs_onorm_manufacturer", 1				: i = i + 1
			ui_listitem i, 3, "gs_onorm_serialnumber", 1				: i = i + 1
			ui_listitem i, 3, "gs_onorm_baseheight_value", 1			: i = i + 1
			ui_listitem i, 3, "gs_onorm_baseheight", 1					: i = i + 1
			ui_listitem i, 3, "gs_onorm_assemblydate", 1				: i = i + 1
			ui_listitem i, 3, "gs_onorm_aks", 1							: i = i + 1
			ui_listitem i, 3, "gs_onorm_leistungbuch", 1				: i = i + 1
			ui_listitem i, 3, "gs_onorm_description", 1					: i = i + 1
			ui_listitem i, 3, "gs_onorm_number", 1						: i = i + 1
			ui_listitem i, 3, "gs_onorm_id", 1							: i = i + 1
			ui_listitem i, 3, "gs_onorm_admission", 1					: i = i + 1
			ui_listitem i, 3, "gs_onorm_modification", 1				: i = i + 1
	endif

return
