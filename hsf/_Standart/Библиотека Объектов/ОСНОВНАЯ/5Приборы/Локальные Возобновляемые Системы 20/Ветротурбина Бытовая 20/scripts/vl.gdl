
! ==============================================================================
! Wind Turbine
! ==============================================================================

hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

dim stDetail[3]
	stDetail[1]=`Детальный`
	stDetail[2]=`Простой`
	stDetail[3]=`Откл.`

values "gs_detlevel_3D" stDetail
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" or gs_detlevel_3D_m = -1 then
	gs_detlevel_3D_m = 2	! Detailed
	if gs_detlevel_3D = stDetail[3] then gs_detlevel_3D_m = 0
	if gs_detlevel_3D = stDetail[2] then gs_detlevel_3D_m = 1
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetail[1]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetail[3]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = stDetail[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

for i = 1 to 255
	put i
next i

values "gs_cont_pen" use(NSP)
values "gs_fill_pen" get(NSP)

values "gs_resol" range [3, )
if gs_detlevel_3D = stDetail[2] then lock "gs_resol"
if gs_detlevel_3D = stDetail[3] then lock "gs_resol", "gs_shadow"

values "nBlade" 3, 2
values "rotorAngle" range[0, 360]
values "hubHeight" range (rotorDiameter/2, )
values "rotorDiameter" range [0.5, hubHeight*2)
values "towerUpperDia" range (0, rotorDiameter/16]
values "towerLowerDia" range [towerUpperDia, hubHeight/tan(87)*2 + towerUpperDia)
parameters A = rotorDiameter
parameters B = rotorDiameter/12*1.5 + recessWidth + rotorDepth
parameters ZZYZX = hubHeight + rotorDiameter/2
lock "A", "B", "ZZYZX"

values "FM_HeatOutput_unit" `З`, `БТЕ/час`
values "FM_RefrigeratingOutput_unit" `З`, `БТЕ/час`


if FM_HeatOutput_unit = `БТЕ/час` then
	hideparameter "FM_HeatOutput"
else
	hideparameter "FM_HeatOutput_btu"
endif

if FM_RefrigeratingOutput_unit = `БТЕ/час` then
	hideparameter "FM_RefrigeratingOutput"
else
	hideparameter "FM_RefrigeratingOutput_btu"
endif


!1000 W = 3414,426 Btu/hour
if GLOB_MODPAR_NAME = "FM_HeatOutput_btu" then
	FM_HeatOutput = FM_HeatOutput_btu / 3.414426
	parameters FM_HeatOutput = FM_HeatOutput
else
	FM_HeatOutput_btu = FM_HeatOutput * 3.414426
	parameters FM_HeatOutput_btu = FM_HeatOutput_btu
endif

!1000 W = 3414,426 Btu/hour
if GLOB_MODPAR_NAME = "FM_RefrigeratingOutput_btu" then
	FM_RefrigeratingOutput = FM_RefrigeratingOutput_btu / 3.414426
	parameters FM_RefrigeratingOutput = FM_RefrigeratingOutput
else
	FM_RefrigeratingOutput_btu = FM_RefrigeratingOutput * 3.414426
	parameters FM_RefrigeratingOutput_btu = FM_RefrigeratingOutput_btu
endif


! ==============================================================================
! 		Uniform surfaces
! ==============================================================================

if GLOB_MODPAR_NAME = "bUniformSurfaces" then
	if bUniformSurfaces then
		matBlade 	= matTower
		matNacelle	= matTower
		matTail	 	= matTower

		parameters 	matBlade 	= matBlade,
					matNacelle	= matNacelle,
					matTail		= matTail
	endif
else
	if bUniformSurfaces then
		if GLOB_MODPAR_NAME = "matTower" then
			matBlade 	= matTower
			matNacelle	= matTower
			matTail	 	= matTower
	
			parameters 	matBlade 	= matBlade,
						matNacelle	= matNacelle,
						matTail		= matTail

		endif	
		if GLOB_MODPAR_NAME = "matBlade" then
			matTower 	= matBlade
			matNacelle	= matBlade
			matTail	 	= matBlade
	
			parameters 	matTower 	= matTower,
						matNacelle	= matNacelle,
						matTail		= matTail

		endif
		if GLOB_MODPAR_NAME = "matNacelle" then
			matTower 	= matNacelle
			matBlade	= matNacelle
			matTail	 	= matNacelle
	
			parameters 	matTower 	= matTower,
						matBlade	= matBlade,
						matTail		= matTail

		endif
		if GLOB_MODPAR_NAME = "matTail" then
			matTower 	= matTail
			matBlade	= matTail
			matNacelle 	= matTail
	
			parameters 	matTower 	= matTower,
						matBlade	= matBlade,
						matNacelle	= matNacelle

		endif
	endif
endif
