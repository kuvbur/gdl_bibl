
! ==============================================================================
! Wind Turbine
! ==============================================================================


if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then shadow off
IF GLOB_CONTEXT > 20 AND GLOB_CONTEXT < 50 THEN gs_detlevel_3D_m = 1

if gs_detlevel_3D_m = 1 then resol 8
if gs_detlevel_3D_m = 2 then resol gs_resol

pen gs_cont_pen

unID = 1
hotspot 0, 0, 0, unID :	unID=unID+1
hotspot -towerLowerDia, 0, 0, unID :	unID=unID+1
hotspot 0, -towerLowerDia, 0, unID :	unID=unID+1
hotspot towerLowerDia, 0, 0, unID :	unID=unID+1
hotspot 0, towerLowerDia, 0, unID :	unID=unID+1

hotspot 0, 0, 0, unID, hubHeight, 1+128: unID=unID+1
hotspot 0, 0, -1, unID, hubHeight, 3: unID=unID+1
hotspot 0, 0, hubHeight, unID, hubHeight, 2: unID=unID+1


group "part1"

	material matTower
	cylind footThk, towerLowerDia
	addz footThk
	cylind footThk, towerLowerDia*3/4
	del 1

	addz 2*footThk
	cone hubHeight - 2*footThk - nacelleDia*0.6, towerLowerDia/2, towerUpperDia/2, 90, 90
	del 1

	add 0, 0, hubHeight
	rotx  bladeAngle - 90
	addz -towerUpperDia/2*1.2

	material matNacelle
	ellips nacelleDepth, nacelleDia/2

	material matBlade
	addz -recessWidth
	mulz -1
	ellips rotorDepth, nacelleDia/2
	del 2

	addz -recessWidth
	cylind recessWidth, nacelleDia/2 - recessWidth
	del 1

	del 3

endgroup


group "part2"

	resol gs_resol

	material matTower
	addz hubHeight - nacelleDia*0.6
	cylind nacelleDia*0.6, towerUpperDia/2*1.2
	del 1

	material matBlade
	addz hubHeight
	rotx bladeAngle
	add 0, (-towerUpperDia/2*1.2 - bladeRadius*2.2), 0

	for ki = 1 to nBlade

	roty rotorAngle + rotorSpeed/60/nFramesPerSecond *360 * (GLOB_FRAME_NR + 1)
	rotz 250
	add 0, bladeRadius, nacelleDia/2

	if gs_detlevel_3D_m = 1 then
		add -bladeRadius, -bladeRadius*1.5, -nacelleDia/2
		block bladeRadius*2, bladeRadius, rotorDiameter/2
		del 1
	endif

	if gs_detlevel_3D_m = 2 then
		bladeResol = 2 * gs_resol
		resol bladeResol
		fi = 180/int(bladeResol/2)
		for hi = 1 to int(bladeResol/2)*2
			put -bladeRadius * cos(180-fi*(hi-1)), bladeRadius * sin(180-fi*(hi-1)) - bladeRadius, 1
		next hi
		for hj = 1 to int(bladeResol/2)+1
			put -bladeRadius * cos(180-fi*(hj-1)), bladeRadius * sin(180-fi*(hj-1)) - bladeRadius, bladeWidestL
		next hj
		for hk = 1 to int(bladeResol/2)-1
			put -bladeRadius * cos(-fi*(hk)), bladeRadius * sin(-fi*(hk)) * 3 - bladeRadius, bladeWidestL
		next hk

		ruled{2} int(bladeResol/2)*2, 1+2+4+16,
		 get (nsp)

		for jj = 1 to 4
			if jj = 1 then
				b01 = 0
				b02 = 0.4
			endif
			if jj = 2 then
				b01 = 0.4
				b02 = 0.7
			endif
			if jj = 3 then
				b01 = 0.7
				b02 = 0.9
			endif
			if jj = 4 then
				b01 = 0.9
				b02 = 1
			endif

			for hj = 1 to int(bladeResol/2)+1
				put -bladeRadius * cos(180-fi*(hj-1)), bladeRadius * sin(180-fi*(hj-1)) - bladeRadius, 1
			next hj
			for hk = 1 to int(bladeResol/2)-1
				put -bladeRadius * cos(-fi*(hk)), bladeRadius * sin(-fi*(hk)) * (3+b01) - bladeRadius, 1
			next hk

			for hj = 1 to int(bladeResol/2)+1
				put -bladeRadius * cos(180-fi*(hj-1)), bladeRadius * sin(180-fi*(hj-1)) - bladeRadius, bladeWidestL/4
			next hj
			for hk = 1 to int(bladeResol/2)-1
				put -bladeRadius * cos(-fi*(hk)), bladeRadius * sin(-fi*(hk)) * (3+b02) - bladeRadius, bladeWidestL/4
			next hk

			addz bladeWidestL + (jj-1) * bladeWidestL/4
			ruled{2} int(bladeResol/2)*2, 1+2+4,
				get (nsp)
			del 1
		next jj

		for hj = 1 to int(bladeResol/2)+1
			put (-bladeRadius * cos(180-fi*(hj-1))), (bladeRadius * sin(180-fi*(hj-1)) - bladeRadius), 1
		next hj
		for hk = 1 to int(bladeResol/2)-1
			put (-bladeRadius * cos(-fi*(hk))), (bladeRadius * sin(-fi*(hk)) * 4 - bladeRadius), 1
		next hk

		addz bladeWidestL*2
		sweep int(bladeResol/2)*2, 5, 11, 0.75, 1+2+4,
			get (nsp),

			0, 0, bladeWidestL*3,
			0, 0, bladeWidestL*8,
			0, 0, bladeWidestL*11,
			0, 0, bladeWidestL*13,
			0, 0, rotorDiameter/2 + bladeWidestL - nacelleDia/2 - rotorDiameter/150
		del 1

		for hj = 1 to int(bladeResol/2)+1
			put (-bladeRadius * cos(180-fi*(hj-1)))*0.75^4, (bladeRadius * sin(180-fi*(hj-1)) - bladeRadius)*0.75^4 + rotorDiameter/110, 1
		next hj
		for hk = 1 to int(bladeResol/2)-1
			put (-bladeRadius * cos(-fi*(hk)))*0.75^4, (bladeRadius * sin(-fi*(hk)) * 4 - bladeRadius)*0.75^4 + rotorDiameter/110, 1
		next hk

		rotz 44
		add 0, -rotorDiameter/110, rotorDiameter/2 - nacelleDia/2 - rotorDiameter/150
		sweep int(bladeResol/2)*2, 10, 0, 0.9, 1 + 2 + 4 + 32 * (GLOB_CONTEXT = 4),
			get (nsp),

			0, 0, 0,
			0, 0, rotorDiameter/260,
			0, 0, rotorDiameter/200,
			0, 0, rotorDiameter/175,
			0, 0, rotorDiameter/165,
			0, 0, rotorDiameter/157.5,
			0, 0, rotorDiameter/153.5,
			0, 0, rotorDiameter/151.5,
			0, 0, rotorDiameter/150.5,
			0, 0, rotorDiameter/150

		del 2
	endif

	add 0, -bladeRadius, -nacelleDia/2
	cylind nacelleDia/2, bladeRadius * 1.2
	del 1

	del 3

	roty 360/nBlade
	next ki
	del nBlade

	del 3

endgroup

placegroup addgroup("part1","part2")
killgroup "part1"
killgroup "part2"

end
