
if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then shadow off
if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then gs_detlevel_3D_m = 1

! --- start of modifying to Shaft

IF bShowMEP THEN

					! --- CONNECTIONS --- !

	IF useSysMat THEN
		tankMat		= sMat
		accMat		= sMat
		gs_con_mat	= sMat
	ENDIF

	PEN gs_cont_pen
	IF gs_detlevel_3D_m = 1 THEN RESOL 8
	IF gs_detlevel_3D_m = 2 THEN RESOL 24

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = (iTankForm = 1) * (16 * (iWaterInlet = 1) + 15 * (iWaterInlet = 2)) + (iTankForm = 2) * (14 * (iWaterInlet = 1) + 15 * (iWaterInlet = 2)),
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 1

ENDIF

! --- end of modifying to Shaft



! --- Model ---------------------------------------------------------------------

pen gs_cont_pen

resol gs_resol * (gs_detlevel_3D_m = 2) + 8 * (gs_detlevel_3D_m = 1)

unID = 1

if iTankForm = 1 then

	hotspot  0,	 0,	0,	unID,	A,	1+256	: unID = unID + 1
	hotspot -1,	 0,	0,	unID,	A,	3		: unID = unID + 1
	hotspot  A,	 0,	0,	unID,	A,	2		: unID = unID + 1

	hotspot  0,	 B,	0,	unID,	A,	1+256	: unID = unID + 1
	hotspot -1,	 B,	0,	unID,	A,	3		: unID = unID + 1
	hotspot  A,	 B,	0,	unID,	A,	2		: unID = unID + 1

	hotspot  0,	 0,	0,	unID,	B,	1+256	: unID = unID + 1
	hotspot  0,	-1,	0,	unID,	B,	3		: unID = unID + 1
	hotspot  0,	 B,	0,	unID,	B,	2		: unID = unID + 1

	hotspot  A,	 0,	0,	unID,	B,	1+256	: unID = unID + 1
	hotspot  A,	-1,	0,	unID,	B,	3		: unID = unID + 1
	hotspot  A,	 B,	0,	unID,	B,	2		: unID = unID + 1



	hotspot  0,	 0,	ZZYZX,	unID,	A,	1+256	: unID = unID + 1
	hotspot -1,	 0,	ZZYZX,	unID,	A,	3		: unID = unID + 1
	hotspot  A,	 0,	ZZYZX,	unID,	A,	2		: unID = unID + 1

	hotspot  0,	 B,	ZZYZX,	unID,	A,	1+256	: unID = unID + 1
	hotspot -1,	 B,	ZZYZX,	unID,	A,	3		: unID = unID + 1
	hotspot  A,	 B,	ZZYZX,	unID,	A,	2		: unID = unID + 1

	hotspot  0,	 0,	ZZYZX,	unID,	B,	1+256	: unID = unID + 1
	hotspot  0,	-1,	ZZYZX,	unID,	B,	3		: unID = unID + 1
	hotspot  0,	 B,	ZZYZX,	unID,	B,	2		: unID = unID + 1

	hotspot  A,	 0,	ZZYZX,	unID,	B,	1+256	: unID = unID + 1
	hotspot  A,	-1,	ZZYZX,	unID,	B,	3		: unID = unID + 1
	hotspot  A,	 B,	ZZYZX,	unID,	B,	2		: unID = unID + 1

	add resizeModA, resizeModB, 0

	group "tankExt"
		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * sin(alpha), - innerY + drad2 - drad2 * cos(alpha), 0, 0
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * cos(alpha), innerY - drad2 + drad2 * sin(alpha), 0, 0
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * sin(alpha), innerY - drad2 + drad2 * cos(alpha), 0, 0
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * cos(alpha), - innerY + drad2 - drad2 * sin(alpha), 0, 0
			alpha = alpha + stepAngle
		next i

		if gs_detlevel_3D_m = 2 then put use(nsp)		!!Points For Strap

		material tankMat

		tube 14, 11 + smallArchResol * 4, 1 + 2,
			0, ZZYZX, 1,
			- 10, 0, 801,
			- dPosX_1, ZZYZX - dPosY_1, 1001,
			- dx + dPosX_2, ZZYZX - dy + dPosY_2, 1,
			- dx, ZZYZX - dy, 1001,

			- dx, joinElev + joinHeight / 2, 0,
			- dx + joinWidth, joinElev + joinHeight / 2, 0,
			- dx + joinWidth, joinElev - joinHeight / 2, 0,
			- dx, joinElev - joinHeight / 2, 0,

			- dx, dy, 1,
			0, -10, 801,
			- dx + dPosX_2, dy - dPosY_2, 1001,
			- dPosX_1, dPosY_1, 1,
			0, 0, 1001,

			- innerX + drad2, - innerY, 0, 0,
			innerX - drad2, - innerY, 0, 0,
			get (smallArchResol * 4),
			innerX, - innerY + drad2, 0, 0,
			innerX, innerY - drad2, 0, 0,
			get (smallArchResol * 4),
			innerX - drad2, innerY, 0, 0,
			- innerX + drad2, innerY, 0, 0,
			get (smallArchResol * 4),
			- innerX, innerY - drad2, 0, 0,
			- innerX, - innerY + drad2, 0, 0,
			get (smallArchResol * 4),
			- innerX + drad2, - innerY, 0, 0,
			innerX - drad2, - innerY, 0, 0,
			innerX - drad2 + drad2 * sin(stepAngle), - innerY + drad2 - drad2 * cos(stepAngle), 0, 0

		!!!! --- Strap ---------------------------------------------------------------------
		if gs_detlevel_3D_m = 2 then
			material accMat
			tube 4, 11 + smallArchResol * 4, 1 + 2,
				- dx, joinElev + joinHeight / 2 - tankProfilThk, 0,
				- dx + joinWidth, joinElev + joinHeight / 2 - tankProfilThk, 0,
				- dx + joinWidth, joinElev - joinHeight / 2 + tankProfilThk, 0,
				- dx, joinElev - joinHeight / 2 + tankProfilThk, 0,

				- innerX + drad2, - innerY, 0, 0,
				innerX - drad2, - innerY, 0, 0,
				get (smallArchResol * 4),
				innerX, - innerY + drad2, 0, 0,
				innerX, innerY - drad2, 0, 0,
				get (smallArchResol * 4),
				innerX - drad2, innerY, 0, 0,
				- innerX + drad2, innerY, 0, 0,
				get (smallArchResol * 4),
				- innerX, innerY - drad2, 0, 0,
				- innerX, - innerY + drad2, 0, 0,
				get (smallArchResol * 4),
				- innerX + drad2, - innerY, 0, 0,
				innerX - drad2, - innerY, 0, 0,
				innerX - drad2 + drad2 * sin(stepAngle), - innerY + drad2 - drad2 * cos(stepAngle), 0, 0

			material tankMat
		endif
		!!!! --- End Of Strap ---------------------------------------------------------------------

		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * sin(alpha), - innerY + drad2 - drad2 * cos(alpha), 8
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * cos(alpha), innerY - drad2 + drad2 * sin(alpha), 8
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * sin(alpha), innerY - drad2 + drad2 * cos(alpha), 8
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * cos(alpha), - innerY + drad2 - drad2 * sin(alpha), 8
			alpha = alpha + stepAngle
		next i

		prism_ 9 + smallArchResol * 4, ZZYZX,
			- innerX + drad2, - innerY, 8,
			innerX - drad2, - innerY, 8,
			get (smallArchResol * 3),
			innerX, - innerY + drad2, 8,
			innerX, innerY - drad2, 8,
			get (smallArchResol * 3),
			innerX - drad2, innerY, 8,
			- innerX + drad2, innerY, 8,
			get (smallArchResol * 3),
			- innerX, innerY - drad2, 8,
			- innerX, - innerY + drad2, 8,
			get (smallArchResol * 3),
			- innerX + drad2, - innerY, -1
	endgroup

	group "tankInt"
		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * sin(alpha), - innerY + drad2 - drad2 * cos(alpha), 0, 0
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * cos(alpha), innerY - drad2 + drad2 * sin(alpha), 0, 0
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * sin(alpha), innerY - drad2 + drad2 * cos(alpha), 0, 0
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * cos(alpha), - innerY + drad2 - drad2 * sin(alpha), 0, 0
			alpha = alpha + stepAngle
		next i

		tube 14, 11 + smallArchResol * 4, 1 + 2,
			0, ZZYZX - tankProfilThk, 1,
			- 10, 0, 801,
			- d2PosX_1, ZZYZX - tankProfilThk - d2PosY_1, 1001,
			- d2x + d2PosX_2, ZZYZX - dy + d2PosY_2, 1,
			- d2x, ZZYZX - dy, 1001,

			- d2x, joinElev + joinHeight / 2 + tankProfilThk, 0,
			- d2x + joinWidth, joinElev + joinHeight / 2 + tankProfilThk, 0,
			- d2x + joinWidth, joinElev - joinHeight / 2 - tankProfilThk, 0,
			- d2x, joinElev - joinHeight / 2 - tankProfilThk, 0,

			- d2x, dy, 1,
			0, -10, 801,
			- d2x + d2PosX_2, dy - d2PosY_2, 1001,
			- d2PosX_1, tankProfilThk + d2PosY_1, 1,
			0, tankProfilThk, 1001,

			- innerX + drad2, - innerY, 0, 0,
			innerX - drad2, - innerY, 0, 0,
			get (smallArchResol * 4),
			innerX, - innerY + drad2, 0, 0,
			innerX, innerY - drad2, 0, 0,
			get (smallArchResol * 4),
			innerX - drad2, innerY, 0, 0,
			- innerX + drad2, innerY, 0, 0,
			get (smallArchResol * 4),
			- innerX, innerY - drad2, 0, 0,
			- innerX, - innerY + drad2, 0, 0,
			get (smallArchResol * 4),
			- innerX + drad2, - innerY, 0, 0,
			innerX - drad2, - innerY, 0, 0,
			innerX - drad2 + drad2 * sin(stepAngle), - innerY + drad2 - drad2 * cos(stepAngle), 0, 0

		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * sin(alpha), - innerY + drad2 - drad2 * cos(alpha), 8
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put innerX - drad2 + drad2 * cos(alpha), innerY - drad2 + drad2 * sin(alpha), 8
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * sin(alpha), innerY - drad2 + drad2 * cos(alpha), 8
			alpha = alpha + stepAngle
		next i
		alpha = 0
		for i = 1 to smallArchResol
			put - innerX + drad2 - drad2 * cos(alpha), - innerY + drad2 - drad2 * sin(alpha), 8
			alpha = alpha + stepAngle
		next i

		addz tankProfilThk
		prism_ 9 + smallArchResol * 4, ZZYZX - tankProfilThk * 2,
			- innerX + drad2, - innerY, 8,
			innerX - drad2, - innerY, 8,
			get (smallArchResol * 3),
			innerX, - innerY + drad2, 8,
			innerX, innerY - drad2, 8,
			get (smallArchResol * 3),
			innerX - drad2, innerY, 8,
			- innerX + drad2, innerY, 8,
			get (smallArchResol * 3),
			- innerX, innerY - drad2, 8,
			- innerX, - innerY + drad2, 8,
			get (smallArchResol * 3),
			- innerX + drad2, - innerY, -1
		del 1
	endgroup

	group "littleTankInt"
		if (gs_detlevel_3D_m = 2) then
			for i = 1 to 2
				addx frontJoinDist / 2
				prism_ 5, joinElev - joinHeight / 2,
					- frontJoinWidth / 2, - B / 2, 15,
					frontJoinWidth / 2, - B / 2, 15,
					frontJoinWidth / 2, - B / 2 + joinWidth, 15,
					- frontJoinWidth / 2, - B / 2 + joinWidth, 15,
					- frontJoinWidth / 2, - B / 2, -1
				del 1
			mulx -1
			next i
			del 2
		endif
	endgroup

	group "littleTankExt"
		if (gs_detlevel_3D_m = 2) then
			for i = 1 to 2
				addx frontJoinDist / 2
				prism_ 5, joinElev - joinHeight / 2,
					- frontJoinWidth / 2 - tankProfilThk, - B / 2, 15,
					frontJoinWidth / 2 + tankProfilThk, - B / 2, 15,
					frontJoinWidth / 2 + tankProfilThk, - B / 2 + joinWidth + tankProfilThk, 15,
					- frontJoinWidth / 2 - tankProfilThk, - B / 2 + joinWidth + tankProfilThk, 15,
					- frontJoinWidth / 2 - tankProfilThk, - B / 2, -1
				del 1
			mulx -1
			next i
			del 2
		endif
	endgroup

	group "plugFrontHoleInt"
		if (gs_detlevel_3D_m = 2) then
			add 0, - B / 2 + dx / 2, plugFrontElev
			rotx 90
			cylind 1, plugFrontRad * 3 / 2
			del 2
		endif
	endgroup

	group "plugFrontHoleExt"
		if (gs_detlevel_3D_m = 2) then
			add 0, - B / 2 + dx / 2 + tankProfilThk, plugFrontElev
			rotx 90
			cylind 1, plugFrontRad * 3 / 2 + tankProfilThk
			del 2
		endif
	endgroup

	!!!! --- Lower Plug ---------------------------------------------------------------------
	add 0, - B / 2 + dx / 2, plugFrontElev
	rotx 90
		plugHeight_temp	= plugFrontHeight
		plugRad_temp	= plugFrontRad
		gosub 100
	del 2
	!!!! --- End Of Lower Plug ---------------------------------------------------------------------

	!!!! --- Upper Plug ---------------------------------------------------------------------
	add (ConPosX_1 - resizeModA) * (iWaterInlet = 2) * (isavailable OR isInArchiCAD = 0), (ConPosY_1 - resizeModB) * (iWaterInlet = 2) * (isavailable OR isInArchiCAD = 0), ZZYZX
		plugHeight_temp	= plugTopHeight
		plugRad_temp	= plugTopRad
		gosub 100
	del 1
	!!!! --- End Of Upper Plug ---------------------------------------------------------------------

	!!!! --- Tap --------------------------------------------------------------------
	group "tapCup"
		material accMat
		add 0, - B / 2 + 0.05 - 0.01, tapElev
		rotx 90
		cylind 0.05, tapRad * 2
		del 2
	endgroup

	if bTap and (gs_detlevel_3D_m = 2) then
		placegroup subgroup("tapCup", "tankExt")
		add 0, - B / 2, tapElev
			gosub 200
		del 1
	endif
	!!!! --- End Of Tap ---------------------------------------------------------------------

	if (GLOB_CONTEXT <> 4 and GLOB_CONTEXT <> 6) or (gs_detlevel_3D_m = 1) then
		tank = subgroup(subgroup("tankExt", "littleTankInt"), "plugFrontHoleInt")
		placegroup tank
	else
		bigTank = subgroup("tankExt", "tankInt")
		littleTank = subgroup(isectgroup("tankExt", "littleTankExt"), "littleTankInt")
		plugFrontHole = subgroup(isectgroup("tankExt", "plugFrontHoleExt"), "plugFrontHoleInt")
		tank = addgroup(addgroup(subgroup(subgroup(bigTank, "littleTankExt"), "plugFrontHoleExt"), littletank), plugFrontHole)
		placegroup tank
	endif

	del 1

endif



if iTankForm = 2 then

	hotspot 0, 0, 0, unID : unID = unID + 1
	hotspot 0, 0, ZZYZX, unID : unID = unID + 1

	hotspot 0, 0, 0, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot 0, A / 2, 0, unID, A2, 3, A2 :unID = unID + 1
	hotspot 0, - A / 2, 0, unID, A2, 2, A2 :unID = unID + 1

	hotspot 0, 0, 0, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot 0, - A / 2, 0, unID, A2, 3, A2 :unID = unID + 1
	hotspot 0, A / 2, 0, unID, A2, 2, A2 :unID = unID + 1

	hotspot 0, 0, 0, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot A / 2, 0, 0, unID, A2, 3, A2 :unID = unID + 1
	hotspot - A / 2, 0, 0, unID, A2, 2, A2 :unID = unID + 1

	hotspot 0, 0, 0, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot - A / 2, 0, 0, unID, A2, 3, A2 :unID = unID + 1
	hotspot A / 2, 0, 0, unID, A2, 2, A2 :unID = unID + 1



	hotspot 0, 0, ZZYZX, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot 0, A / 2, ZZYZX, unID, A2, 3, A2 :unID = unID + 1
	hotspot 0, - A / 2, ZZYZX, unID, A2, 2, A2 :unID = unID + 1

	hotspot 0, 0, ZZYZX, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot 0, - A / 2, ZZYZX, unID, A2, 3, A2 :unID = unID + 1
	hotspot 0, A / 2, ZZYZX, unID, A2, 2, A2 :unID = unID + 1

	hotspot 0, 0, ZZYZX, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot A / 2, 0, ZZYZX, unID, A2, 3, A2 :unID = unID + 1
	hotspot - A / 2, 0, ZZYZX, unID, A2, 2, A2 :unID = unID + 1

	hotspot 0, 0, ZZYZX, unID, A2, 1 + 128, A2 :unID = unID + 1
	hotspot - A / 2, ZZYZX, 0, unID, A2, 3, A2 :unID = unID + 1
	hotspot A / 2, 0, ZZYZX, unID, A2, 2, A2 :unID = unID + 1

	! --- Tank ---------------------------------------------------------------------
	group "tankExt2"
		material tankMat
		roty - 90
		revolve 15, 360, 1 + 2,
			0, A / 2 - tankBotRad, 1,
			0, A, 800,
			tankBotRad, A / 2, 1001,
			joinBotElev - joinBotHeight / 2, A / 2, (gs_detlevel_3D_m = 1),
			joinBotElev - joinBotHeight / 2, A / 2 - joinBotWidth, (gs_detlevel_3D_m = 1),
			joinBotElev + joinBotHeight / 2, A / 2 - joinBotWidth, (gs_detlevel_3D_m = 1),
			joinBotElev + joinBotHeight / 2, A / 2, (gs_detlevel_3D_m = 1),
			joinTopElev - joinTopHeight / 2, A / 2, (gs_detlevel_3D_m = 1),
			joinTopElev - joinTopHeight / 2, A / 2 - joinTopWidth, (gs_detlevel_3D_m = 1),
			joinTopElev + joinTopHeight / 2, A / 2 - joinTopWidth, (gs_detlevel_3D_m = 1),
			joinTopElev + joinTopHeight / 2, A / 2, (gs_detlevel_3D_m = 1),
			shoulderHeight, A / 2, 1,
			shoulderHeight, A / 2 - tankTopSmallRad, 900,
			0, - alphace, 4001,
			ZZYZX, 0, 1000
		del 1
	endgroup

	group "tankInt2"
		material tankMat
		roty - 90
		revolve 15, 360, 1 + 2,
			tankProfilThk, A / 2 - tankBotRad, 1,
			0, A, 800,
			tankBotRad, A / 2 - tankProfilThk, 1001,
			joinBotElev - joinBotHeight / 2 - tankProfilThk, A / 2 - tankProfilThk, 0,
			joinBotElev - joinBotHeight / 2 - tankProfilThk, A / 2 - tankProfilThk - joinBotWidth, 0,
			joinBotElev + joinBotHeight / 2 + tankProfilThk, A / 2 - tankProfilThk - joinBotWidth, 0,
			joinBotElev + joinBotHeight / 2 + tankProfilThk, A / 2 - tankProfilThk, 0,

			joinTopElev - joinTopHeight / 2 - tankProfilThk, A / 2 - tankProfilThk, 0,
			joinTopElev - joinTopHeight / 2 - tankProfilThk, A / 2 - tankProfilThk - joinTopWidth, 0,
			joinTopElev + joinTopHeight / 2 + tankProfilThk, A / 2 - tankProfilThk - joinTopWidth, 0,
			joinTopElev + joinTopHeight / 2 + tankProfilThk, A / 2 - tankProfilThk, 0,
			shoulderHeight, A / 2 - tankProfilThk, 1,
			shoulderHeight, A / 2 - tankTopSmallRad, 900,
			0, - alphace, 4001,
			ZZYZX - tankProfilThk, 0, 1000
		del 1
	endgroup

	group "plugMounting"
	if (gs_detlevel_3D_m = 2) then
		material tankMat
		prism_ 5, plugElev,
			- plugMountWidth / 2, 0, 15,
			plugMountWidth / 2, 0, 15,
			plugMountWidth / 2, - plugMountLength, 15,
			- plugMountWidth / 2, - plugMountLength, 15,
			- plugMountWidth / 2, 0, -1
	endif
	endgroup

	group "littleTankExt2"
		if (gs_detlevel_3D_m = 2) then
			material tankMat
			addy - A / 2
			roty - 90
			revolve 5, 360, 1 + 2,
				0, little_A / 2, 1,
				little_shoulderHeight, little_A / 2, 1,
				little_shoulderHeight, little_A / 2 - little_tankTopSmallRad, 900,
				0, - little_alphace, 4001,
				little_ZZYZX, 0, 1000
			del 1
			del 1
		endif
	endgroup

	group "littleTankInt2"
		if (gs_detlevel_3D_m = 2) then
			material tankMat
			addy - A / 2
			roty - 90
			revolve 5, 360, 1 + 2,
				0, little_A / 2 - little_tankProfilThk, 1,
				little_shoulderHeight, little_A / 2 - little_tankProfilThk, 1,
				little_shoulderHeight, little_A / 2 - little_tankTopSmallRad, 900,
				0, - little_alphace, 4001,
				little_ZZYZX - little_tankProfilThk, 0, 1000
			del 1
			del 1
		endif
	endgroup

	group "lowerPlug2"
		plugHeight_temp	= plugFrontHeight
		plugRad_temp	= plugFrontRad

		add 0, - A / 2 + little_A / 2, plugFrontElev
		rotx 90
			gosub 100
		del 2
	endgroup

	!!!! --- Upper Plug ---------------------------------------------------------------------
	add ConPosX_1 * (iWaterInlet = 2) * (isavailable OR isInArchiCAD = 0), - plugMountLength / 2 * (iWaterInlet = 1) + (ConPosY_1 * (isavailable OR isInArchiCAD = 0) - plugMountLength / 2 * (isavailable = 0 and isInArchiCAD)) * (iWaterInlet = 2), ZZYZX
		plugHeight_temp	= plugTopHeight
		plugRad_temp	= plugTopRad
		gosub 100
	del 1
	!!!! --- End Of Upper Plug ---------------------------------------------------------------------

	!!!! --- Tap --------------------------------------------------------------------
	group "tapCup2"
		material accMat
		add 0, - A / 2 + 0.05 - 0.01, tapElev
		rotx 90
		cylind 0.05, tapRad * 2
		del 2
	endgroup

	if bTap and (gs_detlevel_3D_m = 2) then
		placegroup subgroup("tapCup2", "tankExt2")
		add 0, - A / 2, tapElev
			gosub 200
		del 1
	endif
	!!!! --- End Of Tap ---------------------------------------------------------------------

	!!!! --- Side Inlet Cup --------------------------------------------------------------------
	group "sideCup"
		material accMat
		add 0, A / 2, ConPosZ_1
		rotx 90
		cylind 0.05, ConWidth_1 / 2
		del 2
	endgroup
	if (gs_detlevel_3D_m = 2) and iWaterInlet = 1 and bShowMEP then placegroup subgroup("sideCup", "tankExt2")
	!!!! --- End Of Side Inlet Cup ---------------------------------------------------------------------

	if (GLOB_CONTEXT <> 4 and GLOB_CONTEXT <> 6) or (gs_detlevel_3D_m = 1) then
		placegroup subgroup("plugMounting", "tankExt2")
		tank = subgroup("tankExt2", "littleTankInt2")
		placegroup tank
		placegroup subgroup("lowerPlug2", tank)
	else
		bigTank = subgroup("tankExt2", "tankInt2")
		littleTank = subgroup(isectgroup("tankExt2", "littleTankExt2"), "littleTankInt2")
		placegroup subgroup("plugMounting", "tankExt2")
		tank = addgroup(subgroup(bigTank, "littleTankExt2"), littletank)
		placegroup tank
		placegroup subgroup("lowerPlug2", subgroup("tankExt2","littleTankInt2"))
		placegroup littleTank
	endif

endif

end



100:
!!!! --- Plug ---------------------------------------------------------------------
if (gs_detlevel_3D_m = 2) then
	resol 6
	material accMat
	cylind plugHeight_temp / 2, plugRad_temp - 0.01
	addz plugHeight_temp / 2
	cylind plugHeight_temp / 2, plugRad_temp
	del 1
	resol gs_resol * (gs_detlevel_3D_m = 2) + 8 * (gs_detlevel_3D_m = 1)
endif

return

200:
!!!! --- Tap ---------------------------------------------------------------------
group "tapTube"
	alpha = 90
	for iii = 1 to smallArchResol
		put - 0.04 + 0.04 * cos(alpha), tapHeight - 0.04 + 0.04 * sin(alpha), 0, 0
		alpha = alpha - stepAngle
	next iii

	material accMat
	tube 2, 6 + smallArchResol, 1 + 2 + 16 + 32,
		0, 0, 901,
		tapRad, 360, 4001,

		-1, tapHeight, 0, 0,
		-tapOv + 0.01, tapHeight, 0, 0,
		-0.04, tapHeight, 0, 0,

		get (smallArchResol*4),

		0, tapHeight - 0.04, 0, 0,
		0, 0, 0, 0,
		0, -1, 0, 0

	rotx 90
	cylind 0.01, tapRad + 0.005
	del 1
endgroup

group "tapHandle"
	add - 0.04, tapHeight, 0
	tube 2, 4, 1 + 2 + 16 + 32,
		0, 0, 901,
		tapRad / 2, 360, 4001,

		0, -1, 0, 0,
		0, 0, 0, 0,
		0, tapHeight / 2, 0, 0,
		0, 1, 0, 0

	addy tapHeight / 2 + 0.01
	rotx 90
	cylind 0.01, tapRad
	del 2
	del 1
endgroup

add 0, - tapOv, - tapHeight
rotz - 90
rotx 90
	placegroup addgroup (subgroup("tapHandle", "tapTube"), "tapTube")
del 3

return

300:
material accMat

rotx 90
cylind 0.05, tapRad * 2
del 1

return
