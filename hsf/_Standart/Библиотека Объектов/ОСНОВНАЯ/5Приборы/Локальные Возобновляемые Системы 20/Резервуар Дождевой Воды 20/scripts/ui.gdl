
ui_dialog `Параметры Резервуара Дождевой Вода`

UI_CURRENT_PAGE gs_ui_current_page

dim stDetail[3]
	stDetail[1]=`Детальный`
	stDetail[2]=`Простой`
	stDetail[3]=`Откл.`

dim stDetail2D[3]
	stDetail2D[1]=`Детальный`
	stDetail2D[2]=`Простой`
	stDetail2D[3]=`Упрощенный`

dim stTankForm[2]
	stTankForm[1]=`Прямоугольный`
	stTankForm[2]=`Круглые`

dim stWaterInlet[2]
	stWaterInlet[1]=`Сбоку`
	stWaterInlet[2]=`Сверху`


IF isMEPEnabled THEN
	DIM ui_matpar_names[3][2]
		ui_matpar_names[1][1] = `Покрытие`
		ui_matpar_names[2][1] = `Аксессуары`
		ui_matpar_names[3][1] = `Соединение`

		ui_matpar_names[1][2] = "tankMat"
		ui_matpar_names[2][2] = "accMat"
		ui_matpar_names[3][2] = "gs_con_mat"

	DIM ui_matpar_locked[3]
		ui_matpar_locked[1] = 0
		ui_matpar_locked[2] = 0
		ui_matpar_locked[3] = (NOT(ConStatus_1) AND NOT(ConStatus_2))
ELSE
	DIM ui_matpar_names[2][2]
		ui_matpar_names[1][1] = `Покрытие`
		ui_matpar_names[2][1] = `Аксессуары`

		ui_matpar_names[1][2] = "tankMat"
		ui_matpar_names[2][2] = "accMat"

ENDIF
dim tabTitles[3]
	tabTitles[1] = `Стиль, Отображение и Покрытия`
	tabTitles[2] = `Соединения`
	tabTitles[3] = `Характеристики`

dim tabIcons[3]
	tabIcons[1] = "uiTab_2DRepr_flat(1)"
	tabIcons[2] = "uiTab_Connections_flat(1)"
	tabIcons[3] = "uiTab_Descriptions_flat(1)"

iPageIDRoot 		    =-1
iPageIDStyleRepSurface	= 1
iPageIDConnections	    = 2
iPageIDDescriptions	    = 3

pageStartY = 5
pageStartX = 0
! ==============================================================================
!UI_PAGES
! ==============================================================================

UI_PAGE iPageIDStyleRepSurface, iPageIDRoot, tabTitles[1], tabIcons[1]
	gosub "styleRepSurfacePage"

IF isMEPEnabled THEN
	UI_PAGE iPageIDConnections, iPageIDRoot, tabTitles[2], tabIcons[2]
		gosub "connectionsPage"
ENDIF

UI_PAGE iPageIDDescriptions, iPageIDRoot, tabTitles[3], tabIcons[3]
	gosub "descriptionPage"

FILE_DEPENDENCE "uiTab_2DRepr_flat(1)"
FILE_DEPENDENCE "uiTab_Connections_flat(1)"
FILE_DEPENDENCE "uiTab_Descriptions_flat(1)"

! ==============================================================================
END
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"styleRepSurfacePage":
! ------------------------------------------------------------------------------

	ui_separator pageStartX + 222, pagestartY - 5, pageStartX + 222, pagestartY + 261
	
	ui_style 0, 1
	ui_outfield `Стиль`, 	pageStartX, pageStartY + 6, 	220, 15
	ui_outfield `Покрытие`, 	pageStartX, pageStartY + 102, 	220, 15
	ui_outfield `2D-отображение`, 	pageStartX + 227,   pageStartY + 6, 215, 15
	ui_outfield `3D-отображение`, 	pageStartX + 227, 	pageStartY + 150, 215, 15
	ui_style 0, 0
	
	ui_outfield `Форма Резервуара`, 	  pageStartX, pageStartY + 30, 119, 15
	ui_infield{3} "iTankForm", 	pageStartX + 120, pageStartY + 26, 95, 19,
			8, "", 2, 1,
			0, 0, 0, 0,
			1, stTankForm[1], 1,
			2, stTankForm[2], 2
	
	ui_outfield `Ввод Воды`, 		pageStartX, 		pageStartY + 54, 119, 15
	ui_infield{3} "iWaterInlet", 	pageStartX + 120, 	pageStartY + 50, 95, 19,
			8, "", 2, 1,
			0, 0, 0, 0,
			1, stWaterInlet[1], 1,
			2, stWaterInlet[2], 2
	
	ui_infield{3} "bTap",	pageStartX, 		pageStartY + 79, 220, 15,
			7, "",
			2,
			1, 20, 20, 20, 20,
			0, `Смеситель`, 0,
			1, `Смеситель`, 1
	
	ui_outfield `Покрытие`, 				pageStartX, 		pageStartY + 126,  79, 15
	ui_infield "tankMat", 				pageStartX + 80,	pageStartY + 119, 130, 25
	
	ui_outfield `Аксессуары`, 			pageStartX, 		pageStartY + 154,  79, 15
	ui_infield "accMat", 				pageStartX + 80, 	pageStartY + 147, 130, 25
	
	ui_outfield `Соединение`, 			pageStartX, 		pageStartY + 182,  79, 15
	ui_infield "gs_con_mat", 			pageStartX + 80, 	pageStartY + 175, 130, 25
	
	ui_outfield `2D-детализация`, 		pageStartX + 227, 	pageStartY + 30,   120, 15
	ui_infield{3} "gs_detlevel_2D_m", 	pageStartX + 348, 	pageStartY + 26,   95, 19,
				8, "", 3, 1,
				0, 0, 0, 0,
				1, stDetail2D[1], 2,
				2, stDetail2D[2], 1,
				3, stDetail2D[3], 0
	
	ui_outfield `Перо Контура`, pageStartX + 227, pageStartY + 54, 182, 15
	ui_infield "gs_cont_pen", pageStartX + 410, pageStartY + 51, 30, 19
	
	ui_outfield `Тип Штриховки`, 	pageStartX + 227, pageStartY + 79,  82, 15
	ui_infield "gs_fill_type", 	pageStartX + 310, pageStartY + 72, 130, 25
	
	ui_outfield `Перо Штриховки`, 	pageStartX + 227, pageStartY + 102, 182, 15
	ui_infield "gs_fill_pen", 	pageStartX + 410, pageStartY + 101, 30, 19
	
	ui_outfield `Перо Фона Штриховки`, 	pageStartX + 227, pageStartY + 125, 182, 15
	ui_infield "gs_back_pen",			pageStartX + 410, pageStartY + 125, 30, 19
	
	
	ui_outfield `3D-детализация`, 		pageStartX + 227, pageStartY + 174, 120, 15
	ui_infield{3} "gs_detlevel_3D_m",	pageStartX + 348, pageStartY + 170, 95, 19,
				8, "", 3, 1,
				0, 0, 0, 0,
				1, stDetail[1], 2,
				2, stDetail[2], 1,
				3, stDetail[3], 0

	_iFlag = 0
	if gs_detlevel_3D_m # 2 then _iFlag = 4				!Detailed
	ui_outfield `Аппроксимация`, 	pageStartX + 227, pageStartY + 198, 120, 15, _iFlag
	ui_infield "gs_resol", 		pageStartX + 348, pageStartY + 194, 95, 19
	
	ui_infield{3} "gs_shadow", 	pageStartX + 227, pageStartY + 222, 215, 15,
			7, "",
			2,
			1, 20, 20, 20, 20,
			0, `Отбрасывание Теней`, 0,
			1, `Отбрасывание Теней`, 1
RETURN

! ------------------------------------------------------------------------------
"connectionsPage":
! ------------------------------------------------------------------------------

	DIM ui_con_type[2]
		ui_con_type[1] = 0
		ui_con_type[2] = 0

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS ALL ui_con_type = ui_con_type,
												ui_system_var_pos = 2
RETURN

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------

	dim stParamNames[42]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "gs_list_volts"
		stParamNames[7] = "FM_Type"
		stParamNames[8] = "FM_SystemName"
		stParamNames[9] = "FM_Manufacturer"
		stParamNames[10] = "FM_ProductName"
		stParamNames[11] = "gs_list_GUID"
		stParamNames[12] = "gs_list_material"
		stParamNames[13] = "gs_list_gauge"
		stParamNames[14] = "gs_list_area"
		stParamNames[15] = "gs_list_volume"
		stParamNames[16] = "FM_InventoryNumber"
		stParamNames[17] = "FM_SerialNumber"
		stParamNames[18] = "FM_BarCode"
		stParamNames[19] = "FM_ProductionYear"
		stParamNames[20] = "AirConditi_A"
		stParamNames[21] = "FM_ERPCode"
		stParamNames[22] = "FM_OrderNumber"
		stParamNames[23] = "FM_Condition"
		stParamNames[24] = "FM_Servicer"
		stParamNames[25] = "FM_ObjectWeight"
		stParamNames[26] = "FM_ObjectWeightUnit"
		stParamNames[27] = "FM_HeatOutput"
		stParamNames[28] = "FM_RefrigeratingOutput"
		stParamNames[29] = "FM_PowerConsumption"
		stParamNames[30] = "FM_FatStops"
		stParamNames[31] = "FM_ElectricConnection"
		stParamNames[32] = "FM_PhaseNumber"
		stParamNames[33] = "FM_Voltage"
		stParamNames[34] = "FM_VoltageUnit"
		stParamNames[35] = "FM_ElectroPerformance"
		stParamNames[36] = "FM_ElectroPerformanceUnit"
		stParamNames[37] = "FM_IPProtection"
		stParamNames[38] = "gs_list_custom1"
		stParamNames[39] = "gs_list_custom2"
		stParamNames[40] = "gs_list_custom3"
		stParamNames[41] = "gs_list_custom4"
		stParamNames[42] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= iPageIDDescriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return

