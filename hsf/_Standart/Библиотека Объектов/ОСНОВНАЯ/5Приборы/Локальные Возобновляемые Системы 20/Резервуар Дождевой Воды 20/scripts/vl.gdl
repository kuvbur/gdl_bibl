
hideparameter all "A", "B", "ZZYZX"

call "FM_types" parameters all

dim stDetail[3]
	stDetail[1]=`Детальный`
	stDetail[2]=`Простой`
	stDetail[3]=`Откл.`

dim stDetail2D[3]
	stDetail2D[1]=`Детальный`
	stDetail2D[2]=`Простой`
	stDetail2D[3]=`Упрощенный`

dim stTankForm[2]
	stTankForm[1]=`Прямоугольный`
	stTankForm[2]=`Круглые`

dim stWaterInlet[2]
	stWaterInlet[1]=`Сбоку`
	stWaterInlet[2]=`Сверху`

values "gs_detlevel_3D" stDetail
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2 ! detailed
	if gs_detlevel_3D = stDetail[3] then gs_detlevel_3D_m = 0
	if gs_detlevel_3D = stDetail[2] then gs_detlevel_3D_m = 1
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = stDetail[1]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = stDetail[3]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = stDetail[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

values "gs_detlevel_2D" stDetail2D
values "gs_detlevel_2D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_2D" then
	gs_detlevel_2D_m = 2 ! detailed
	if gs_detlevel_2D = stDetail2D[3] then gs_detlevel_2D_m = 0
	if gs_detlevel_2D = stDetail2D[2] then gs_detlevel_2D_m = 1
	parameters gs_detlevel_2D_m = gs_detlevel_2D_m
else
	gs_detlevel_2D = stDetail[1]
	if gs_detlevel_2D_m = 0 then gs_detlevel_2D = stDetail2D[3]
	if gs_detlevel_2D_m = 1 then gs_detlevel_2D = stDetail2D[2]
	parameters gs_detlevel_2D = gs_detlevel_2D
endif

values "tankForm" stTankForm[1], stTankForm[2]
values "iTankForm" 1, 2
if GLOB_MODPAR_NAME = "tankForm" then
	iTankForm = 1
	if tankForm = stTankForm[2]	then iTankForm = 2
	parameters iTankForm = iTankForm
else
	tankForm = stTankForm[1]
	if iTankForm = 2 then tankForm = stTankForm[2]
	parameters tankForm = tankForm
endif

values "waterInlet" stWaterInlet[1], stWaterInlet[2]
values "iWaterInlet" 1, 2
if GLOB_MODPAR_NAME = "waterInlet" then
	iWaterInlet = 1
	if waterInlet = stWaterInlet[2]	then iWaterInlet = 2
	parameters iWaterInlet = iWaterInlet
else
	waterInlet = stWaterInlet[1]
	if iWaterInlet = 2 then waterInlet = stWaterInlet[2]
	parameters waterInlet = waterInlet
endif

for i = 1 to 255
	put i
next i

values "gs_cont_pen" use(NSP)
values "gs_fill_pen" get(NSP)

values "gs_resol" range [4,)

if gs_detlevel_3D_m # 2 then		!Simple, Off
	lock "gs_resol"
	hideparameter "accMat"
endif
if gs_detlevel_3D_m = 0 then		!Off
	lock "gs_shadow"
endif

if GLOB_MODPAR_NAME = "tankForm" then
	if iTankForm = 1 then
		A = rectDefaultA			! 1.5
		B = rectDefaultB			! 0.65
		ZZYZX = rectDefaultZzyzx	! 0.85
	else
		A = roundDefaultA			! 1.2
		A2 = A / 2
		B = A
		ZZYZX = roundDefaultZzyzx	! 1.3
	endif

	parameters A = A
	parameters B = B
	parameters ZZYZX = ZZYZX
endif

if iTankForm = 1 then
	values "A" RANGE(frontJoinDist + frontJoinWidth + (drad2 + dx) * 2, ]
	values "B" RANGE((drad2 + dx) * 2, ]
	values "ZZYZX" RANGE(dy * 2 + joinHeight, ]

	hideparameter "A2"
else
	lock "B"

	if GLOB_MODPAR_NAME = "A" then
		parameters B = A
		parameters A2 = A / 2
	endif
	if GLOB_MODPAR_NAME = "B" then
		parameters A = B
		parameters A2 = A / 2
	endif
	if GLOB_MODPAR_NAME = "A2" then
		parameters A = A2 * 2
		parameters B = A2 * 2
	endif

	values "A" RANGE(tankTopSmallRad * 4, ]
	values "A2" RANGE(tankTopSmallRad * 2, ]
	values "B" RANGE(tankTopSmallRad * 4, ]
	values "ZZYZX" RANGE(tankBotRad + tankTopSmallRad * 3 + joinBotHeight + joinTopHeight, ]
endif

! --- start of modify to Shaft

IF isMEPEnabled THEN

	IF NOT(ConStatus_1) AND NOT(ConStatus_2) THEN
		LOCK "gs_con_mat"
		HIDEPARAMETER "gs_con_mat"
	ENDIF

	DIM shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	PARAMETERS MEP_NumberConnections = 2


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Соединение Подвода Воды`

	if iWaterInlet = 1 then
		ConDVecX = 0
		ConDVecY = 1
		ConDVecZ = 0

		ConWVecX = 0
		ConWVecY = 0
		ConWVecZ = 1
	else
		ConDVecX = 0
		ConDVecY = 0
		ConDVecZ = 1

		ConWVecX = -1
		ConWVecY = 0
		ConWVecZ = 0
	endif

	IF ConWidth_1 < 0.001 THEN
		ConWidth_1 = 0.001
		PARAMETERS ConWidth_1 = ConWidth_1
	ENDIF
	IF ConWidth_1 > 1 THEN
		ConWidth_1 = 1
		PARAMETERS ConWidth_1 = ConWidth_1
	ENDIF
	VALUES "ConWidth_1" RANGE (0, 1]
	PARAMETERS ConDepth_1 = ConWidth_1
	VALUES "ConLength_1" RANGE [0, ]

	if iWaterInlet = 1 then
		if GLOB_MODPAR_NAME = "tankForm" then
			if iTankForm = 1 then
				PARAMETERS ConPosX_1 = resizeModA
			endif
		endif
		PARAMETERS ConPosY_1 = B / 2 + resizeModB
		if iTankForm = 1 then
			VALUES "ConPosX_1" RANGE[- A / 2 + dx + ConWidth_1 / 2 + resizeModA, A / 2 - dx - ConWidth_1 / 2 + resizeModA]
			VALUES "ConPosZ_1" RANGE[dy + ConWidth_1 / 2, ZZYZX - dy - ConWidth_1 / 2]
		else
			PARAMETERS ConPosX_1 = 0
			VALUES "ConPosZ_1" RANGE[tankBotRad + ConWidth_1, shoulderHeight - ConWidth_1]
		endif
	else
		if GLOB_MODPAR_NAME = "tankForm" then
			if iTankForm = 1 then
				PARAMETERS ConPosX_1 = resizeModA
				PARAMETERS ConPosY_1 = resizeModB
			else
				PARAMETERS ConPosX_1 = resizeModA
				PARAMETERS ConPosY_1 = - plugMountWidth / 2
			endif
		endif
		if GLOB_MODPAR_NAME = "waterInlet" then
			if iTankForm = 1 then
				PARAMETERS ConPosY_1 = resizeModB
			else
				PARAMETERS ConPosY_1 = - plugMountWidth / 2
			endif
		endif
		if iTankForm = 1 then
			VALUES "ConPosX_1" RANGE[- A / 2 + dx + ConWidth_1 / 2 + resizeModA, A / 2 - dx - ConWidth_1 / 2 + resizeModA]
			VALUES "ConPosY_1" RANGE[- B / 2 + dx + ConWidth_1 / 2 + resizeModB, B / 2 - dx - ConWidth_1 / 2 + resizeModB]
		else
			VALUES "ConPosX_1" RANGE[- plugMountWidth / 2 + ConWidth_1 / 2, plugMountWidth / 2 - ConWidth_1 / 2]
			VALUES "ConPosY_1" RANGE[- plugMountLength + ConWidth_1 / 2, - ConWidth_1 / 2]
		endif
		PARAMETERS ConPosZ_1 = ZZYZX + plugTopHeight
	endif

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	PARAMETERS MEP_ConInfo[ConID][2] = ConName
	PARAMETERS MEP_ConInfo[ConID][3] = "0"


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Соединение Отвода Воды`

	ConDVecX = 0
	ConDVecY = 0
	ConDVecZ = -1

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	IF ConWidth_2 < 0.001 THEN
		ConWidth_2 = 0.001
		PARAMETERS ConWidth_2 = ConWidth_2
	ENDIF
	IF ConWidth_2 > 1 THEN
		ConWidth_2 = 1
		PARAMETERS ConWidth_2 = ConWidth_2
	ENDIF
	VALUES "ConWidth_2" RANGE (0, 1]
	PARAMETERS ConDepth_2 = ConWidth_2
	VALUES "ConLength_2" RANGE [0, ]

	PARAMETERS ConPosX_2 = resizeModA
	PARAMETERS ConPosY_2 = - B / 2 - tapOv + resizeModB
	PARAMETERS ConPosZ_2 = tapElev - tapHeight - 0.01

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2,
		ConPosY = ConPosY_2,
		ConPosZ = ConPosZ_2,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
	PARAMETERS MEP_ConInfo[ConID][2] = ConName
	PARAMETERS MEP_ConInfo[ConID][3] = "0"

	IF gs_ui_current_page = 2 THEN
		ac_mep_connectionpage_active = 1
	ELSE
		ac_mep_connectionpage_active = 0
	ENDIF
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active

	IF SetProgram THEN
		SetProgram = 0
		PARAMETERS SetProgram = SetProgram
	ENDIF
	PUT 2
ELSE
	HIDEPARAMETER "gs_connection", "useSysMat", "gs_con_mat", "waterInlet"

	ac_mep_connectionpage_active = 0
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active
ENDIF


