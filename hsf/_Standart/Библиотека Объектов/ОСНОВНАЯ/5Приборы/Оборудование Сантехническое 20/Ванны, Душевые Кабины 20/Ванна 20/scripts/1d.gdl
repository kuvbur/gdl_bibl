
dim stStyles[2]
	stStyles[1] = `Прямоугольный`
	stStyles[2] = `Трапецеидальная`


EPS = 0.0001

TAPGROUP_SINGLE 		= 1
TAPGROUP_TWOHANDLE 		= 2
TAPGROUP_TRADITIONAL 	= 3
TAPGROUP_SENSOR			= 4
TAPGROUP_WALLMOUNTED	= 5
TAPGROUP_NONE 			= 6
TAPGROUP_CUSTOMTAPS		= 7

bWallMountedTap 		= (gs_tap_type_m > 20 and gs_tap_type_m < 28) or (gs_tap_type_m = -1 and iTapGroup = TAPGROUP_WALLMOUNTED)

! -----------------------------------------------------------------------------
! Object Types
! -----------------------------------------------------------------------------

iObjectType = 30
if iStyle = 2 then iObjectType = 32

! -----------------------------------------------------------------------------
! Original tap distance from wall
! -----------------------------------------------------------------------------

yWallPos = 0.040
if iStyle = 2 then yWallPos = 0.025

! -----------------------------------------------------------------------------
! Symbol Types
! -----------------------------------------------------------------------------

dim stSymbolTypes[3]
	stSymbolTypes[1] = `Тип 1`
	stSymbolTypes[2] = `Тип 2`
	stSymbolTypes[3] = `Тип 3`

if not(gs_symbol_type_enable) then
	gs_symbol_type = stSymbolTypes[1]
endif

! -----------------------------------------------------------------------------
! ConPosX/ConPosY Range
! -----------------------------------------------------------------------------

if bWallmountedTap then
	if gs_tap_type_m = 21 then
		widthTap = 0.068
		posTap = 0.041
	else
		if gs_tap_type_m = -1 then
			widthTap = 0.05
			posTap = 0.036
		else
			widthTap = 0.05
			posTap = 0.0765
		endif
	endif
endif

if zzyzx<0.2 then  zzyzx=0.2

! -----------------------------------------------------------------------------
! Show MEP Connections
! -----------------------------------------------------------------------------

isavailable = 0
isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)
isMEPEnabled = (isavailable OR isInArchiCAD = 0)
if isMEPEnabled then
	bShowMEP = 1
	reqBMep = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "showMepConnect", reqBMep)
	if success > 0 then
		bShowMEP = reqBMep
	endif
else
	bShowMEP = 0
endif

! -----------------------------------------------------------------------------
! Geometry parameters for Bathtub Trapezoid
! -----------------------------------------------------------------------------
if iStyle = 2 then
	pthk = 0.0001					! Profile thickness
	ang = atn((B-width2)/A)			! Angle of trapezoid
	radU = 0.11						! Upper radius
	radL = 0.06						! Lower radius
	friW = 0.03						! Fringe width
	friX = 0.05
	siL	= 0.09
	siR = 0.2
	siB	= 0.06
	siT	= 0.06

	alfa1 = atn(1/(((siB+friw)/(siL+friw)+cos(90+ang))/sin(90+ang)))
	alfa2 = atn(1/(((siB+friw)/(siR+friw)+cos(90-ang))/sin(90-ang)))
	alfa3 = atn(1/(((siB+friw+radL)/(siL+friw+radL)+cos(90+ang))/sin(90+ang)))
	alfa4 = atn(1/(((siB+friw+radL)/(siR+friw+radL)+cos(90-ang))/sin(90-ang)))

	! --- Tub points --------------------------------------------------------------
	hLx1 = friw+siL
	hLy1 = B-width2+(friw+siL)/tan(alfa1)+radL/tan(45+ang/2)
	hLx2 = friw+siL+radL-radL*sin(ang)
	hLy2 = B-width2+(friw+siL)/tan(alfa1)+radL/tan(45+ang/2)-radL*cos(ang)
	hLx3 = A-friw-siR-radL-radL*sin(ang)
	hLy3 = (friw+siR)/tan(alfa2)+radL/tan(45-ang/2)-radL*cos(ang)
	hLx4 = A-friw-siR
	hLy4 = (friw+siR)/tan(alfa2)+radL/tan(45-ang/2)
	hLx5 = A-friw-siR
	hLy5 = B-friw-siT-radL
	hLx6 = A-friw-siR-radL
	hLy6 = B-friw-siT
	hLx7 = friw+siL+radL
	hLy7 = B-friw-siT
	hLx8 = friw+siL
	hLy8 = B-friw-siT-radL

	hUx1 = frix
	hUy1 = B-width2+(frix+radU)/tan(45+ang/2)
	hUx2 = frix+radU-radU*sin(ang)
	hUy2 = B-width2+frix/tan(45+ang/2)+radU/tan(45+ang/2)-radU*cos(ang)
	hUx3 = A-frix-radU-radU*sin(ang)
	hUy3 = frix/tan(45-ang/2)+radU/tan(45-ang/2)-radU*cos(ang)
	hUx4 = A-frix
	hUy4 = frix/tan(45-ang/2)+radU/tan(45-ang/2)
	hUx5 = A-frix
	hUy5 = B-frix-radU
	hUx6 = A-frix-radU
	hUy6 = B-frix
	hUx7 = frix+radU
	hUy7 = B-frix
	hUx8 = frix
	hUy8 = B-frix-radU

	hBx1 = siL+friw+radL
	hBy1 = B-width2+(friw+siL+radL)/tan(alfa3)
	hBx2 = A-(friw+siR+radL)
	hBy2 = (friw+siR+radL)/tan(alfa4)
	hBx3 = hBx2
	hBy3 = B-(friw+siT+radL)
	hBx4 = hBx1
	hBy4 = hBy3
endif

