
! -----------------------------------------------------------------------------
! Scale
! -----------------------------------------------------------------------------

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters gs_detlevel_2D_m

! -----------------------------------------------------------------------------
! Hotspots 2D
! -----------------------------------------------------------------------------
unID = 1
hotspot2  0, 0, unID, a, 1+256 : unID = unID + 1 		! anchor to top left corner
hotspot2  a, 0, unID, a, 2 : unID = unID + 1
hotspot2 -1, 0, unID, a, 3 : unID = unID + 1

hotspot2  0, -b, unID, a, 1+256 : unID = unID + 1
hotspot2  a, -b, unID, a, 2 : unID = unID + 1
hotspot2 -1, -b, unID, a, 3 : unID = unID + 1

hotspot2 0,  0, unID, b, 1+256 : unID = unID + 1
hotspot2 0, -b, unID, b, 2 : unID = unID + 1
hotspot2 0,  1, unID, b, 3 : unID = unID + 1

hotspot2 a,  0, unID, b, 1+256 : unID = unID + 1
hotspot2 a, -b, unID, b, 2 : unID = unID + 1
hotspot2 a,  1, unID, b, 3 : unID = unID + 1


! -----------------------------------------------------------------------------
! Basin Styles + Tap
! -----------------------------------------------------------------------------
pen gs_cont_pen
fill gs_fill_type
DrawingNum = 1
drawindex 20

gosub iStyle

if DrawingNum = 1 then	! Top
	if gs_tap_type_m <> 0 and (gs_detlevel_2D_m = 2 or gs_detlevel_2D_m = 3) then
		gosub "tap"
	endif
endif

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------
drawindex 10
gosub "minimalspace"

! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

if bShowMEP then
	gosub "mepconnection"
else
	unID = 100
	add2 0, -b
	hotspot2 0, ConPosY_3, unID, ConPosX_3, 1+128: unID = unID+1
	hotspot2 -0.1, ConPosY_3, unID, ConPosX_3, 3: unID = unID+1
	hotspot2 ConPosX_3, ConPosY_3, unID, ConPosX_3, 2: unID = unID+1

	hotspot2 ConPosX_3, 0, unID, ConPosY_3, 1+128: unID = unID+1
	hotspot2 ConPosX_3, -0.1, unID, ConPosY_3, 3: unID = unID+1
	hotspot2 ConPosX_3, ConPosY_3, unID, ConPosY_3, 2: unID = unID+1
	del 1
endif

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Bathtub Rectangular
! -----------------------------------------------------------------------------
1:

	if DrawingNum = 1 then	! Top

		add2 0, -b

		iSymbolType = 1
		if gs_symbol_type = stSymbolTypes[2] then iSymbolType = 2
		if gs_symbol_type = stSymbolTypes[3] then iSymbolType = 3

		! --- 1/50 ---
		if gs_detlevel_2D_m = 3 then

			circle2 ConPosX_3, ConPosY_3, 0.025

			if iSymbolType = 1 then
				gosub "rectangular_10"
				mul2 a/1.80, b/0.75
				gosub "rectangular_21"
				del 1
			endif
			if iSymbolType = 2 then
				gosub "rectangular_11"
				mul2 a/1.80, b/0.85
				gosub "rectangular_22"
				arc2     1.270925594206, 0.4253823016995, 0.3271549532485, 283.2174720097, 76.06259132899
				add2 0,0.425
				for i=1 to 2
					line2    0.8989804298629, 0.2969504498545, 0.8818454204116, 0.3132733441288
					arc2     1.474821458223, -2.506120885038, 2.834054596918, 93.91218336357, 97.58865706126
					arc2     1.201642773926, -0.3595537390323, 0.6703318275885, 98.67337406652, 104.7473421187
					arc2     0.9194151993343, 0.7413452945929, 0.4662014765802, 275.7121792591, 283.8487538351
					arc2     0.9535559264001, 0.3597523726906, 0.08320196111982, 229.0090338114, 278.4741427282
					arc2     0.8417889642847, 0.2712239536966, 0.05807470114568, 44.72508292084, 80.35466986026
					arc2     0.7523368369937, -0.1970407402376, 0.5347960286996, 79.31213455367, 82.90915398351
					mul2 1,-1
				next i
				del 4
			endif
			if iSymbolType = 3 then
				gosub "rectangular_10"
				mul2 a/1.80, b/0.80
				gosub "rectangular_23"
				arc2     1.046807500889,          0.4, 0.644768605737, 341.533302933, 18.46669706703
				add2 0, 0.400
				for i=1 to 2
					line2    0.8314720812183, 0.3051873285561, 0.8348984771574, 0.2595020493683
					line2    1.055329949239, 0.3120401204342, 1.025634517767, 0.3120401204342
					line2    1.025634517767, 0.3120401204342, 1.025634517767, 0.3051873285561
					line2    1.025634517767, 0.3051873285561, 0.8314720812183, 0.3051873285561
					line2    0.8348984771574, 0.2595020493683, 1.265482233503, 0.2595020493683
					line2    1.489640574569, 0.338592676843, 1.031345177665, 0.338592676843
					line2    1.031345177665, 0.338592676843, 1.031345177665, 0.3154374067656
					arc2     1.489640574569, 0.1654603178493, 0.1731323589937, 12.94091604812,           90
					arc2     1.098578143991, 0.03845929538242, 0.2769781113831, 52.94442023451, 98.98309282964
					mul2 1,-1
				next i
				del 4
			endif
		endif

		! --- 1/100 ---
		if gs_detlevel_2D_m = 2 then

			circle2 ConPosX_3, ConPosY_3, 0.025

			if iSymbolType = 1 then
				gosub "rectangular_10"
				mul2 a/1.80, b/0.75
				gosub "rectangular_21"
				del 1
			endif
			if iSymbolType = 2 then
				gosub "rectangular_11"
				mul2 a/1.80, b/0.85
				gosub "rectangular_22"
				del 1
			endif
			if iSymbolType = 3 then
				gosub "rectangular_10"
				mul2 a/1.80, b/0.80
				gosub "rectangular_23"
				del 1
			endif

		endif

		! --- 1/200 ---
		if gs_detlevel_2D_m = 1 then
			gosub "rectangular_10"
		endif

		del top


	endif

return


! -----------------------------------------------------------------------------
! Bathtub Trapezoid
! -----------------------------------------------------------------------------
2:

	if DrawingNum = 1 then	! Top

		add2 0, -b

		hotspot2 a/2, (B-width2)/2, unID : unID = unID + 1

		unID = 1000
		hotspot2 0, B, unID, width2, 1+256 : unID = unID + 1
		hotspot2 0, B+1, unID, width2, 3+128 : unID = unID + 1
		hotspot2 0, B-width2, unID, width2, 2 : unID = unID + 1

		! --- 1/50 ---
		if gs_detlevel_2D_m = 3 then
			circle2 ConPosX_3, ConPosY_3, 0.025

			poly2_b 9, 1+4, gs_fill_pen, gs_back_pen,
				hUx8, hUy8, 1,
				hUx1, hUy1, 1,
				hUx2, hUy2, 1001,
				hUx3, hUy3, 1,
				hUx4, hUy4, 1001,
				hUx5, hUy5, 1,
				hUx6, hUy6, 1001,
				hUx7, hUy7, 1,
				hUx8, hUy8, 1001

			poly2_b 9, 1+4, gs_fill_pen, gs_back_pen,
				hLx8, hLy8, 1,
				hLx1, hLy1, 1,
				hLx2, hLy2, 1001,
				hLx3, hLy3, 1,
				hLx4, hLy4, 1001,
				hLx5, hLy5, 1,
				hLx6, hLy6, 1001,
				hLx7, hLy7, 1,
				hLx8, hLy8, 1001

			gosub "trapezoid_10"
		endif

		! --- 1/100 ---
		if gs_detlevel_2D_m = 2 then

			circle2 ConPosX_3, ConPosY_3, 0.025

			poly2_b 9, 1+4, gs_fill_pen, gs_back_pen,
				hUx8, hUy8, 1,
				hUx1, hUy1, 1,
				hUx2, hUy2, 1001,
				hUx3, hUy3, 1,
				hUx4, hUy4, 1001,
				hUx5, hUy5, 1,
				hUx6, hUy6, 1001,
				hUx7, hUy7, 1,
				hUx8, hUy8, 1001

			gosub "trapezoid_10"
		endif

		! --- 1/200 ---
		if gs_detlevel_2D_m = 1 then
			gosub "trapezoid_10"
		endif

		del 1
	endif

return

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------

"minimalspace":

	add2 0, -b

	aForMinSpace = A
	diffForMinSpace = 0
	if iStyle = 2 then
		aForMinSpace = sqr (a^2 + (b - width2)^2)
		rotAngleForMinSpace = atn ((b - width2) / a)
		diffForMinSpace = MSFront * tan(rotAngleForMinSpace)

		add2 0, b  -width2
		rot2 -rotAngleForMinSpace
	endif

	call "fa_minimalspace" parameters MSType	= 11,
									unID		= 3000,
									A			= aForMinSpace,
									MSFront		= MSFront,
									MSFront2	= MSFront2,
									MSLength	= MSLength,
									diff		= diffForMinSpace

	if iStyle = 2 then del 2

	del 1

return



! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

"mepconnection":

	add2 0, -b

	! --- OTHER VIEWS --- !

	rot2 -SYMB_ROTANGLE * (DrawingNum > 1 and not(SYMB_MIRRORED)) + SYMB_ROTANGLE * (DrawingNum > 1 and SYMB_MIRRORED)

	if DrawingNum = 2 then project2 -3, 90, 2	! Bottom
	if DrawingNum = 3 then project2 4, 270, 2	! Front
	if DrawingNum = 4 then project2 4, 90, 2	! Back
	if DrawingNum = 5 then project2 4, 180 * (SYMB_MIRRORED), 2		! Right
	if DrawingNum = 6 then project2 4, 180 * not(SYMB_MIRRORED), 2	! Left

	if bWallMountedTap then
		if bFlipTap then
			ConEdit_1 = 12
			ConEdit_2 = 12
			corrMEPx = xTapPos + tapDistCorn
			corrMEPy = 0
		else
			ConEdit_1 = 13
			ConEdit_2 = 13
			corrMEPx = 0
			corrMEPy = b + xTapPos - tapDistCorn
		endif

	else
		ConEdit_1 = 12
		ConEdit_2 = 12

		corrMEPx = 0
		corrMEPy = 0
	endif

	add2 corrMEPx, corrMEPy

				! --- CONNECTIONS --- !

	call "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
		ConShow_3 = 0, ConHotspotShow_3 = 0


	del 1

	call "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConShow_1 = 0, ConHotspotShow_1 = 0,
		ConShow_2 = 0, ConHotspotShow_2 = 0,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 5

	del 2

return

! -----------------------------------------------------------------------------
! Tap
! -----------------------------------------------------------------------------

"tap":

	add2 0, -b
	bForTap = b / 2
	if iStyle = 2 then bForTap = b - (width2 / 2)

	drawindex 40
	if bWallMountedTap then
		if bFlipTap then
			add2 tapDistCorn, b
		else
			add2 0, b - tapDistCorn
		endif
		rot2 -90 * bFlipTap
	else
		add2 yWallPos, bForTap
	endif

	rot2 90
	call "tapType_m" parameters all yWallPos 			= yWallPos,
									bEnableTapEdit		= 1,
									bSinkTap			= 0
	del 1

	if bWallMountedTap then
		del 2
	else
		del 1
	endif

	del 1
return

! -----------------------------------------------------------------------------
! Rectangular details
! -----------------------------------------------------------------------------

"rectangular_10":
	fill gs_fill_type
	poly2_b 5, 1+2+64,gs_fill_pen, gs_back_pen,
		0,0,1,
		0,b,1,
		a,b,1,
		a,0,1,
		0,0,-1
return

"rectangular_11":
	fill gs_fill_type
	R = 0.06
	poly2_b 9, 1+2+64,gs_fill_pen, gs_back_pen,
		R,	0,	1,
		A-R,0,	1,
		A,	R,	1001,
		A,	B-R,1,
		A-R,B,	1001,
		R,	B,	1,
		0,	B-R,1001,
		0,	R,	1,
		R,	0,	1001
return

"rectangular_21":
	poly2_b 12, 1+2+4+64,gs_fill_pen, gs_back_pen,
		0.21, 0.69,     1,
		0.21, 0.54,   900,
		0,    90,    4001,
		0.06, 0.54,     1,
		0.06, 0.21,     1,
		0.21, 0.21,   900,
		0,    90,    4001,
		0.21, 0.06,     1,
		1.425,0.06,     1,
		1.425,0.375,  900,
		0,    180,   4001,
		1.425,0.69,     1
return

"rectangular_22":
	poly2_b 18, 1+2+4+64,gs_fill_pen, gs_back_pen,
		0.09094040888563, 0.6697255312615,      1,
		0.09094040888563, 0.1802744687385,      1,
		0.1619930404647, 0.1802744687385,    900,
		0, 86.93008808525,   4001,
		0.158187856723, 0.1093238025612,      1,
		0.9590934582583, 0.08750000000018,      1,
		1.537977596817, 0.1165372875084,      1,
		1.487462219778, 0.330181303083,    900,
		0, 78.06020182481,   4001,
		1.706934971702, 0.3354043166865,      1,
		1.706934971702, 0.5145956833135,      1,
		1.487462219778, 0.519818696917,    900,
		0, 78.0602018248,   4001,
		1.537977596817, 0.7334627124916,      1,
		0.9590934582583, 0.7624999999998,      1,
		0.158187856723, 0.7406761974388,      1,
		0.1619930404647, 0.6697255312615,    900,
		0, 86.93008808524,   4001
return

"rectangular_23":
	poly2_b 20, 1+2+4+64,gs_fill_pen, gs_back_pen,
		0.1323033222819, 0.6069621046149,      1,
		1.5303986056,          0.4,    900,
		0, 16.84223824052,   4001,
		0.1323083191539, 0.1930041427273,      1,
		0.2569796954315, 0.2104484822089,    900,
		0, 82.03474073358,   4001,
		0.2569796954315, 0.08456259323445,      1,
		1.443382886149, 0.08456259323445,      1,
		1.443487536948, 0.2921915235917,    900,
		0, 71.28220218609,   4001,
		1.640101522843, 0.2254627874762,      1,
		0.789491993422, 0.4000625285799,    900,
		0, 23.19088880373,   4001,
		1.640127183029, 0.5745372125238,      1,
		1.443382886149, 0.5076265396227,    900,
		0, 71.2173694244,   4001,
		1.443382886149, 0.7154374067656,      1,
		0.2569796954315, 0.7154374067656,      1,
		0.2569802489322, 0.5895505612171,    900,
		0, 82.04962158166,   4001
return

! -----------------------------------------------------------------------------
! Trapezoid details
! -----------------------------------------------------------------------------
"trapezoid_10":
	fill gs_fill_type
	poly2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
	0,b-width2,1,
	0,b,1,
	a,b,1,
	a,0,1,
	0,b-width2,-1
return


