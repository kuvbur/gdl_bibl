
if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then shadow off

! -----------------------------------------------------------------------------
! 3D Hotspots
! -----------------------------------------------------------------------------

unID=1
hotspot 0, -b, 0, unID : unID = unID + 1
hotspot 0, 0, zzyzx, unID : unID = unID + 1
hotspot 0, -b, zzyzx, unID : unID = unID + 1
hotspot 0, 0, 0, unID : unID = unID + 1
hotspot a, -b, zzyzx, unID : unID = unID + 1
hotspot a, 0, 0, unID : unID = unID + 1
hotspot a, -b, 0, unID : unID = unID + 1
hotspot a, 0, zzyzx, unID : unID = unID + 1

if iStyle = 2 then
	unID = 10000
	hotspot 0, 0, ZZYZX, unID, width2, 1+256 : unID = unID + 1
	hotspot 0, 1, ZZYZX, unID, width2, 3+128 : unID = unID + 1
	hotspot 0, -width2, ZZYZX, unID, width2, 2 : unID = unID + 1
endif
! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

pen gs_cont_pen

gosub iStyle
if gs_detlevel_3D_m <> 3 and gs_tap_type_m <> 0 then
	gosub "tap"
endif


! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

if bShowMEP then
	gosub "mepconnection"
else
	unID = 100
	add 0, -b, 0
	hotspot 0, ConPosY_3, ConPosZ_3, unID, ConPosX_3, 1+128: unID = unID+1
	hotspot -0.1, ConPosY_3, ConPosZ_3, unID, ConPosX_3, 3: unID = unID+1
	hotspot ConPosX_3, ConPosY_3, ConPosZ_3, unID, ConPosX_3, 2: unID = unID+1

	hotspot ConPosX_3, 0, ConPosZ_3, unID, ConPosY_3, 1+128: unID = unID+1
	hotspot ConPosX_3, -0.1, ConPosZ_3, unID, ConPosY_3, 3: unID = unID+1
	hotspot ConPosX_3, ConPosY_3, ConPosZ_3, unID, ConPosY_3, 2: unID = unID+1
	del 1
endif

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Bathtub Rectangular
! -----------------------------------------------------------------------------

1:

	add 0, -b, 0

	if GLOB_CONTEXT > 40 and GLOB_CONTEXT < 50 then
		block A, B, zzyzx
		end
	endif

	material gs_tap_mat
	dd = 0
	if gs_detlevel_3D_m = 1 then
		gs_resol = 9 : dd = 0
		add ConPosX_3, ConPosY_3, 0.10*zzyzx/0.51
		cylind 0.005, 0.025
		del 1
	endif

	if gs_detlevel_3D_m = 2 then
		dd = 1
		add ConPosX_3, ConPosY_3, 0.10*zzyzx/0.51
		cylind 0.005, 0.025
		del 1
	endif

	if gs_detlevel_3D_m = 3 then
		gs_resol = 9 : dd = 2
	endif

	material gs_tub_mat
	mulx a/1.80
	muly b/0.75
	mulz  zzyzx/0.6

	resol gs_resol
	addz 0.60
	poly_ 16,
		0,0,0,
		1.80,0,0,
		1.80,0.75,0,
		0,0.75,0,
		0,0,-1,
		0.2809,0.06,600,
		-1,0,800,
		0.06,0.2809,1000,
		0.1882,90,200,
		0,1,800,
		0.2809,0.69,1000,
		1.1441,0,200,
		1,0,800,
		1.425,0.06,1000,
		1.1441,180,200,
		0.2809,0.06,-1

material sdmat
	del 1
	rotx 90
	rect 1.80,0.60
	addz -0.75
	rect 1.80,0.60
	del 2
	rotz 90
	rotx 90
	rect  0.75,0.60
	addz 1.80
	rect  0.75,0.60
	del 3

material gs_tub_mat
	if gs_detlevel_3D_m = 3 then
		addz 0.10
		poly_ 5,
			0.06,0.06,0,
			0.06,0.69,0,
			1.74,0.69,0,
			1.74,0.06,0,
			0.06,0.06,-1
		DEL 1
	else
	prism_ 5, 0.10,
		0.06,0.06,0,
		0.06,0.69,0,
		1.74,0.69,0,
		1.74,0.06,0,
		0.06,0.06,-1
	!	0.3059,0.375,900,
	!	0.025,360,4013
	endif

	rotx 90
	roty -90
	add -0.69, 0.10,-1.425
	Len = 1.1441
	gosub 10+dd
	mulx -1
	addx -0.63
	Len = 1.1441
	gosub 10+dd
	del 5
	rotx 90
	add 0.06,0.1,-0.4691
	Len = 0.1882
	gosub 10+dd
	del 2

	roty -90
	add 0.1, 0.4691,-0.2809
	gosub 20+dd
	muly -1
	addy 0.1882
	gosub 20+dd
	del 4

	roty -90
	rotx 180
	add 0.1,-0.375,1.425
	gosub 30+dd
	del 7

return

! -----------------------------------------------------------------------------
! Bathtub Trapezoid
! -----------------------------------------------------------------------------

2:

	add 0, -b, 0

	material gs_tap_mat
	res = 3
	res2 = 12
	if gs_detlevel_3D_m = 1 then
		res2 = 12
		res = 3
		add ConPosX_3, ConPosY_3, 0.10*zzyzx/0.51
		cylind 0.005, 0.025
		del 1
	endif

	if gs_detlevel_3D_m = 2 then
		res2 = gs_resol
		res = int(gs_resol/4)
		add ConPosX_3, ConPosY_3, 0.10*zzyzx/0.51
		cylind 0.005, 0.025
		del 1
	endif

	if gs_detlevel_3D_m = 3 then
		res2 = 12
		res = 4
	endif

	resol res2

	! --- Outer side ---
	material sdmat
	prism_ 5+5, ZZYZX,
		0, B-width2, 15,
		A, 0, 15,
		A, B, 15,
		0, B, 15,
		0, B-width2, -1,

		pthk, B-width2+pthk, 11,
		A-pthk, pthk, 11,
		A-pthk, B-pthk, 11,
		pthk, B-pthk, 11,
		pthk, B-width2+pthk, -1

	! --- Upper roundoff and top ---
	material gs_tub_mat
	bi=75
	rox = frix-friw						! Roundoff width
	roz = rox/sin(bi)-rox/tan(bi)	! Roundoff height

	fi = (90-ang)/res
	gi = (90-ang)/res
	for hi=1 to res-1
		put hUx1+radU-radU*cos(gi), hUy1-radU*sin(gi), ZZYZX-roz, 0
		gi = gi+fi
	next hi

	fi = (90+ang)/res
	gi = (90+ang)/res
	for hi=1 to res-1
		put hUx4-radU+radU*cos(90+ang-gi), hUy4-radU*sin(90+ang-gi), ZZYZX-roz, 0
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hUx5-radU+radU*cos(gi), hUy5+radU*sin(gi), ZZYZX-roz, 0
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hUx7-radU*sin(gi), hUy7-radU+radU*cos(gi), ZZYZX-roz, 0
		gi = gi+fi
	next hi

	cutpolya 7, 1, 0,
		-A, B-pthk, 6,
		A-pthk, B-pthk, 0,
		A-pthk, -B, 6,
		A*2, -B, 6,
		A*2, 2*B, 6,
		-A, 2*B, 6,
		-A, B, -1

	cutpolya 8, 1, 0,
		-A, 2*B, 6,
		pthk, 2*B, 0,
		pthk, B-width2+pthk, 6,
		A-pthk, pthk, 6,
		A*2, pthk, 6,
		A*2, -B, 6,
		-A, -B, 6,
		-A, 2*B, -1

	tube 9, 12+(res-1)*4, 0,		!1+2+16+32+64,
		0,  0,  1,
		-tan(90-bi),  1,  800,
		-rox,  roz,  1001,
		-rox-0.2, roz, 1,
		-rox-0.2, roz-pthk, 1,
		-rox,  roz-pthk,   0,
		1, 0, 800,
		0, 0, 1000,
		0, 0, -1,

		hUx8, hUy8, ZZYZX-roz, 0,
		hUx8, B/4*3, ZZYZX-roz, 0,
		hUx1, hUy1, ZZYZX-roz, 0,
		get((res-1)*4),
		hUx2, hUy2, ZZYZX-roz, 0,
		hUx3, hUy3, ZZYZX-roz, 0,
		get((res-1)*4),
		hUx4, hUy4, ZZYZX-roz, 0,
		hUx5, hUy5, ZZYZX-roz, 0,
		get((res-1)*4),
		hUx6, hUy6, ZZYZX-roz, 0,
		hUx7, hUy7, ZZYZX-roz, 0,
		get((res-1)*4),
		hUx8, hUy8, ZZYZX-roz, 0,
		hUx8, B/4*3, ZZYZX-roz, 0,
		hUx1, hUy1, ZZYZX-roz, 0

	cutend
	cutend
	! --- End of upper roundoff and top ---


	! --- Lower roundoff ---
	biL=75
	roxL = 0.05							! Roundoff width
	rozL = roxL/sin(biL)-roxL/tan(biL)	! Roundoff height

	fi = (90-ang)/res
	gi = (90-ang)/res
	for hi=1 to res-1
		put hLx1+radL-radL*cos(gi), hLy1-radL*sin(gi), 0.1+rozL, 0
		gi = gi+fi
	next hi

	fi = (90+ang)/res
	gi = (90+ang)/res
	for hi=1 to res-1
		put hLx4-radL+radL*cos(90+ang-gi), hLy4-radL*sin(90+ang-gi), 0.1+rozL, 0
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hLx5-radL+radL*cos(gi), hLy5+radL*sin(gi), 0.1+rozL, 0
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hLx7-radL*sin(gi), hLy7-radL+radL*cos(gi), 0.1+rozL, 0
		gi = gi+fi
	next hi

	tube 9, 12+(res-1)*4, 0,	!1+2+16+32+64,
		0,  0,  1,
		tan(90-biL),  -1,  800,
		roxL,  -rozL,  1001,
		roxL+0.01, -rozL, 1,
		roxL+0.01, -rozL-pthk, 1,
		roxL,  -rozL-pthk,   0,
		-1, 0, 800,
		0, 0, 1000,
		0, 0, -1,

		hLx8, hLy8, 0.1+rozL, 0,
		hLx8, B/4*3,0.1+rozL, 0,
		hLx1, hLy1, 0.1+rozL, 0,
		get((res-1)*4),
		hLx2, hLy2, 0.1+rozL, 0,
		hLx3, hLy3, 0.1+rozL, 0,
		get((res-1)*4),
		hLx4, hLy4, 0.1+rozL, 0,
		hLx5, hLy5, 0.1+rozL, 0,
		get((res-1)*4),
		hLx6, hLy6, 0.1+rozL, 0,
		hLx7, hLy7, 0.1+rozL, 0,
		get((res-1)*4),
		hLx8, hLy8, 0.1+rozL, 0,
		hLx8, B/4*3, 0.1+rozL, 0,
		hLx1, hLy1, 0.1+rozL, 0
	! --- End of upper roundoff ---


	! --- Side ---
	fi = (90-ang)/res
	gi = (90-ang)/res
	for hi=1 to res-1
		put hLx1+radL-radL*cos(gi), hLy1-radL*sin(gi), 1
		gi = gi+fi
	next hi

	fi = (90+ang)/res
	gi = (90+ang)/res
	for hi=1 to res-1
		put hLx4-radL+radL*cos(90+ang-gi), hLy4-radL*sin(90+ang-gi), 1
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hLx5-radL+radL*cos(gi), hLy5+radL*sin(gi), 1
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hLx7-radL*sin(gi), hLy7-radL+radL*cos(gi), 1
		gi = gi+fi
	next hi

	fi = (90-ang)/res
	gi = (90-ang)/res
	for hi=1 to res-1
		put hUx1+radU-radU*cos(gi), hUy1-radU*sin(gi), ZZYZX-0.1-rozL-roz
		gi = gi+fi
	next hi

	fi = (90+ang)/res
	gi = (90+ang)/res
	for hi=1 to res-1
		put hUx4-radU+radU*cos(90+ang-gi), hUy4-radU*sin(90+ang-gi), ZZYZX-0.1-rozL-roz
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hUx5-radU+radU*cos(gi), hUy5+radU*sin(gi), ZZYZX-0.1-rozL-roz
		gi = gi+fi
	next hi

	fi = (90)/res
	gi = (90)/res
	for hi=1 to res-1
		put hUx7-radU*sin(gi), hUy7-radU+radU*cos(gi), ZZYZX-0.1-rozL-roz
		gi = gi+fi
	next hi

	addz 0.1 + rozL
	ruled{2} 9+(res-1)*4, 4,		!+16+32+64,

		hLx8, hLy8, 1,
		hLx1, hLy1, 1,
		get((res-1)*3),
		hLx2, hLy2, 1,
		hLx3, hLy3, 1,
		get((res-1)*3),
		hLx4, hLy4, 1,
		hLx5, hLy5, 1,
		get((res-1)*3),
		hLx6, hLy6, 1,
		hLx7, hLy7, 1,
		get((res-1)*3),
		hLx8, hLy8, -1,

		hUx8, hUy8, ZZYZX-0.1-rozL-roz,
		hUx1, hUy1, ZZYZX-0.1-rozL-roz,
		get((res-1)*3),
		hUx2, hUy2, ZZYZX-0.1-rozL-roz,
		hUx3, hUy3, ZZYZX-0.1-rozL-roz,
		get((res-1)*3),
		hUx4, hUy4, ZZYZX-0.1-rozL-roz,
		hUx5, hUy5, ZZYZX-0.1-rozL-roz,
		get((res-1)*3),
		hUx6, hUy6, ZZYZX-0.1-rozL-roz,
		hUx7, hUy7, ZZYZX-0.1-rozL-roz,
		get((res-1)*3),
		hUx8, hUy8, ZZYZX-0.1-rozL-roz
	del 1
	! --- End of side ---


	! --- Bottom ---
	addz 0.1 - pthk
	prism_ 5, pthk,
		hBx1-0.01, hBy1-0.01, 11,
		hBx2+0.01, hBy2-0.02, 11,
		hBx3+0.01, hBy3+0.02, 11,
		hBx4-0.01, hBy4+0.01, 11,
		hBx1-0.01, hBy1-0.01, -1
	del 2

return


! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

"mepconnection":

	add 0, -b, 0

	if gs_detlevel_3D_m = 1 or gs_detlevel_3D_m = 3 then resol 8
	if gs_detlevel_3D_m = 2 then resol 24

	if useSysMat then
		gs_tub_mat = sMat
		gs_con_mat	= sMat
	endif

	if bWallMountedTap then
		if bFlipTap then
			ConEdit_1 = 12
			ConEdit_2 = 12
			corrMEPx = xTapPos + tapDistCorn
			corrMEPy = 0
			corrMEPz = zTapPos
		else
			ConEdit_1 = 13
			ConEdit_2 = 13
			corrMEPx = 0
			corrMEPy = b + xTapPos - tapDistCorn
			corrMEPz = zTapPos
		endif
	else
		ConEdit_1 = 16
		ConEdit_2 = 16

		corrMEPx = 0
		corrMEPy = 0
		corrMEPz = 0
	endif

	add corrMEPx, corrMEPy, corrMEPz

	! --- Hot Water & Cold Water Connection ---
	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
		ConShow_3 = 0, ConHotspotShow_3 = 0

	del 1

	! --- Waste Water ---
	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConShow_1 = 0, ConHotspotShow_1 = 0,
		ConShow_2 = 0, ConHotspotShow_2 = 0,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 15

	del 1

return

! -----------------------------------------------------------------------------
! Taps
! -----------------------------------------------------------------------------

"tap":

	add 0, -b, 0
	bForTap = b / 2
	if iStyle = 2 then bForTap = b - (width2 / 2)

	if bWallMountedTap then
		if bFlipTap then
			add tapDistCorn, b, tapHeight
		else
			add 0, b - tapDistCorn, tapHeight
		endif
		rotz -90 * bFlipTap
	else
		add yWallPos, bForTap, ZZYZX
	endif

	rotz 90
	call "tapType_m" parameters all yWallPos 			= yWallPos,
									bEnableTapEdit		= 1,
									bSinkTap			= 0,
									useOldSimpleTaps	= 0,
									matShowerHead		= gs_tap_mat
	del 1

	if bWallMountedTap then
		del 2
	else
		del 1
	endif
	del 1

return

! -----------------------------------------------------------------------------
! Rectangular model details
! -----------------------------------------------------------------------------

10:
	prism_  9,     Len,
				0.0,     0.0,     15,
				0.170897,    -0.0,     10,
				0.147436,     0.003462,     74,
				0.106941,     0.03528,     74,
				0.096431,     0.066064,     74,
				0.049123,     0.460301,     74,
				0.031009,     0.489466,     74,
				0.0,     0.5,     15,
				0.0,     0.0,     -1
return

11:
	prism_  22,     Len,
			0.0,     0.0,     15,
			0.170897,    -0.0,     10,
			0.160214,     0.000475,     74,
			0.147436,     0.003462,     74,
			0.13537,     0.008623,     74,
			0.124383,     0.015801,     74,
			0.11481,     0.024777,     74,
			0.106941,     0.03528,     74,
			0.101015,     0.046989,     74,
			0.097213,     0.059549,     74,
			0.096431,     0.066064,     74,
			0.049644,     0.455957,     74,
			0.049123,     0.460301,     74,
			0.046588,     0.468674,     74,
			0.042637,     0.47648,     74,
			0.037391,     0.483482,     74,
			0.031009,     0.489466,     74,
			0.023685,     0.494251,     74,
			0.015641,     0.497692,     74,
			0.007122,     0.499683,     74,
			0.0,     0.5,     15,
			0.0,     0.0,     -1
return

12:
	ruled 2, 0,
		0.170897,    0.0,     0,
		0.0,     0.5,     0,

		0.170897,    0.0,     Len,
		0.0,     0.5,     Len
return

20:
	revolve 7, 90,0,
			0.0,     0.05,     1,
			0.003462,     0.073461,     1,
			0.03528,     0.113956,     1,
			0.066064,     0.124466,     1,
			0.460301,     0.171774,     1,
			0.489466,     0.189888,     1,
			0.5,     0.220897,     0
return

21:
	revolve 20, 90,0,
			0.0,     0.05,     1,
			0.000475,     0.060682,     1,
			0.003462,     0.073461,     1,
			0.008623,     0.085527,     1,
			0.015801,     0.096514,     1,
			0.024777,     0.106087,     1,
			0.03528,     0.113956,     1,
			0.046989,     0.119882,     1,
			0.059549,     0.123684,     1,
			0.066064,     0.124466,     1,
			0.455957,     0.171253,     1,
			0.460301,     0.171774,     1,
			0.468674,     0.174309,     1,
			0.47648,     0.17826,     1,
			0.483482,     0.183506,     1,
			0.489466,     0.189888,     1,
			0.494251,     0.197212,     1,
			0.497692,     0.205256,     1,
			0.499683,     0.213775,     1,
			0.5,     0.220897,     0
return

22:
	revolve 2, 90,0,
			0.0,     0.05,     1,
			0.5,     0.220897,     0
return

30:
	revolve  7,   180,0,
			0.0,     0.1441,     1,
			0.003462,     0.167561,     1,
			0.03528,     0.208056,     1,
			0.066064,     0.218566,     1,
			0.460301,     0.265874,     1,
			0.489466,     0.283988,     1,
			0.5,     0.314997,     1
return

31:
	revolve  20,   180,0,
			0.0,     0.1441,     1,
			0.000475,     0.154782,     1,
			0.003462,     0.167561,     1,
			0.008623,     0.179627,     1,
			0.015801,     0.190614,     1,
			0.024777,     0.200187,     1,
			0.03528,     0.208056,     1,
			0.046989,     0.213982,     1,
			0.059549,     0.217784,     1,
			0.066064,     0.218566,     1,
			0.455957,     0.265353,     1,
			0.460301,     0.265874,     1,
			0.468674,     0.268409,     1,
			0.47648,     0.27236,     1,
			0.483482,     0.277606,     1,
			0.489466,     0.283988,     1,
			0.494251,     0.291312,     1,
			0.497692,     0.299356,     1,
			0.499683,     0.307875,     1,
			0.5,     0.314997,     1
return

32:
	revolve  2,   180,0,
			0.0,     0.1441,     1,
			0.5,     0.314997,     1
return

