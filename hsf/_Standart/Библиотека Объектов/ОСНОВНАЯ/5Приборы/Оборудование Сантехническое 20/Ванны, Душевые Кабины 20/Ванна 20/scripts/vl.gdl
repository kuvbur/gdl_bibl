
call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle"  1, 2

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	parameters stStyle = stStyle
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------

dim st_detail3D[4]
	st_detail3D[1] = `Детальный`
	st_detail3D[2] = `Простой`
	st_detail3D[3] = `Упрощенный`
	st_detail3D[4] = `Откл.`

values "gs_detlevel_3D" st_detail3D		!`Detailed`,`Simple`,`Draft`,`Off`
values "gs_detlevel_3D_m" 2, 1, 3, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2	! Detailed
	if gs_detlevel_3D = st_detail3D[4] then gs_detlevel_3D_m = 0
	if gs_detlevel_3D = st_detail3D[2] then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = st_detail3D[3] then gs_detlevel_3D_m = 3
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = st_detail3D[1]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = st_detail3D[4]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = st_detail3D[2]
	if gs_detlevel_3D_m = 3 then gs_detlevel_3D = st_detail3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! -----------------------------------------------------------------------------
! Default Sizes
! -----------------------------------------------------------------------------

values "A" range[0.40, ]
values "B" range[0.20, ]
values "ZZYZX" range[0.20, ]

if a < 0.4 then parameters a = 0.4
if b < 0.2 then parameters b = 0.2
if zzyzx < 0.2 then parameters zzyzx = 0.2

if GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then
	ConPosX_1 = defaultConnectionPos[iStyle][1]
	ConPosY_1 = defaultConnectionPos[iStyle][2]
	ConPosZ_1 = defaultConnectionPos[iStyle][3]
	ConPosX_2 = defaultConnectionPos[iStyle][4]
	ConPosY_2 = defaultConnectionPos[iStyle][5]
	ConPosZ_2 = defaultConnectionPos[iStyle][6]
	ConPosX_3 = defaultConnectionPos[iStyle][7]
	ConPosY_3 = defaultConnectionPos[iStyle][8]
	ConPosZ_3 = defaultConnectionPos[iStyle][9]
	parameters ConPosX_1 = ConPosX_1, ConPosY_1 = ConPosY_1 , ConPosZ_1 = ConPosZ_1
	parameters ConPosX_2 = ConPosX_2, ConPosY_2 = ConPosY_2 , ConPosZ_2 = ConPosZ_2
	parameters ConPosX_3 = ConPosX_3, ConPosY_3 = ConPosY_3 , ConPosZ_3 = ConPosZ_3
endif

! -----------------------------------------------------------------------------
! Symbol Types
! -----------------------------------------------------------------------------

values "gs_symbol_type" stSymbolTypes	! Type 1, Type 2, Type 3
if not(gs_symbol_type_enable) then
	hideparameter "gs_symbol_type"
	lock "gs_symbol_type"
endif

! -----------------------------------------------------------------------------
! 2D detlevel
! -----------------------------------------------------------------------------

call "2dDetailLevel" parameters	all enable2dDetLevels	= 2 + 4 + 8 + 16

! -----------------------------------------------------------------------------
! Tap types
! -----------------------------------------------------------------------------

if bWallMountedTap then
	if not(bFlipTap) then
		xTapRangeMin = tapDistCorn - b
		xTapRangeMax = tapDistCorn
	else
		xTapRangeMin = -tapDistCorn
		xTapRangeMax = a - tapDistCorn
	endif
else
	xTapRangeMin = -b / 2
	xTapRangeMax = b / 2
endif

if iStyle = 1 then : yTapRangeMin = yWallPos - (0.053 * (A / 1.6)) : endif
if iStyle = 2 then : yTapRangeMin = yWallPos-0.05 : endif

call "tapType_m" parameters	all	enableTapGroups			= 2 + 4 + 32 + 64,
								enableSingleLevelTaps	= 2 + 2**3 + 2**5 + 2**18 + 2**19,
								enableTwoHandleTaps		= 2 + 2**4 + 2**8 + 2**10 + 2**11 + 2**12 + 2**13 + 2**20 + 2**21,
								enableTraditionalTaps	= 0,
								enableSensorFaucets		= 0,
								enableWallMountedTaps	= 2 + 2**23 + 2**24 + 2**25,
								enableCustomTaps		= 0,
								bEnableTapEdit			= 1,
								enableTapSymbols		= 2 + 4 + 8 + 32,
								xTapRangeMin 			= xTapRangeMin,
								xTapRangeMax 			= xTapRangeMax,
								yTapRangeMin 			= yTapRangeMin,
								yTapRangeMax 			= yWallPos,
								zTapRangeMin 			= zzyzx - tapHeight,
								returned_parameters tapWidthL, tapWidthR, yTap

if not(bFlipTap) then
	values "tapDistCorn" range[tapWidthR, b - tapWidthL]
else
	values "tapDistCorn" range[tapWidthL, a - tapWidthR]
endif

! -----------------------------------------------------------------------------
! LOCK and HIDEPARAMETERS
! -----------------------------------------------------------------------------

if gs_tap_type_m = 0 or not(bWallMountedTap) then
	lock "tapHeight", "tapDistCorn", "bFlipTap"
	hideparameter "tapHeight", "tapDistCorn", "bFlipTap"
else
	values "tapHeight" range[h_top + 0.1,]
endif
if gs_tap_type_m<>16 & gs_tap_type_m<>18 & gs_tap_type_m<>19 & gs_tap_type_m<>21 then
	lock "bShower"
	hideparameter "bShower"
endif

! -----------------------------------------------------------------------------
! Limitation
! -----------------------------------------------------------------------------

if gs_cont_pen = 0 then parameters gs_cont_pen = 75
if gs_fill_pen = 0 then parameters gs_fill_pen = 19
values "gs_resol" range [4,)

if gs_detlevel_3D_m = 1 then lock "gs_resol"

if iStyle = 1 then hideparameter "width2"

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------

values "MSFront" range [0,)

! -----------------------------------------------------------------------------
! modify to Shaft
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "A" | GLOB_MODPAR_NAME = "B" then
	ConPosY_3 = B/2
	parameters ConPosY_3 = ConPosY_3
endif
values "ConPosX_3" range [ConWidth_3/2, A-ConWidth_3/2]
values "ConPosY_3" range [ConWidth_3/2, B-ConWidth_3/2]

if isMEPEnabled then

	if not(ConStatus_1) and not(ConStatus_2) and not(ConStatus_3) then
		lock "gs_con_mat"
		hideparameter "gs_con_mat"
	endif

	dim shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	parameters MEP_NumberConnections = 3


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Подключение Горячей Воды`

	if ConWidth_1 < 0.001 then
		ConWidth_1 = 0.001
		parameters ConWidth_1 = ConWidth_1
	endif
	if ConWidth_1 > min (A/2, zzyzx/2) then
		ConWidth_1 = min (A/2, zzyzx/2)
		parameters ConWidth_1 = ConWidth_1
	endif
	values "ConWidth_1" range (0, min (A/2, zzyzx/2)]
	parameters ConDepth_1 = ConWidth_1
	values "ConLength_1" range [0, ]

	! Default Position
	if (GLOB_MODPAR_NAME = "gs_tap_type" | GLOB_MODPAR_NAME = "tapGroup" | GLOB_MODPAR_NAME = "bFlipTap") then
		if bWallMountedTap then
			if bFlipTap then
				ConPosX_1 = posTap + widthTap/2
			else
				ConPosY_1 = posTap + widthTap/2
			endif
		else
			ConPosX_1 = 0.1
		endif
		parameters ConPosX_1 = ConPosX_1
		parameters ConPosY_1 = ConPosY_1
	endif

	! range
	if bWallMountedTap then
		pos1st = -posTap - widthTap + ConWidth_1/2
		pos2nd = -posTap - ConWidth_1/2
		pos3rd = +posTap + ConWidth_1/2
		pos4th = +posTap + widthTap - ConWidth_1/2

		if bFlipTap then
			ConDVecX = 0
			ConDVecY = 1
			ConDVecZ = 0

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			! --- X range

			values "ConPosX_1"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosX_2" then
				if ConPosX_2 < 0 & ConPosX_1 < 0 then
					ConPosX_1 = posTap + widthTap/2
					parameters 	ConPosX_1 = ConPosX_1
				endif
				if ConPosX_2 > 0 & ConPosX_1 > 0 then
					ConPosX_1 = -(posTap + widthTap/2)
					parameters 	ConPosX_1 = ConPosX_1
				endif
			endif
			parameters ConPosY_1 = B
		else
			ConDVecX = -1
			ConDVecY = 0
			ConDVecZ = 0

			ConWVecX = 0
			ConWVecY = 1
			ConWVecZ = 0

			! --- X range

			values "ConPosY_1"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosY_2" then
				if ConPosY_2 < 0 & ConPosY_1 < 0 then
					ConPosY_1 = posTap + widthTap/2
					parameters 	ConPosY_1 = ConPosY_1
				endif
				if ConPosY_2 > 0 & ConPosY_1 > 0 then
					ConPosY_1 = -(posTap + widthTap/2)
					parameters 	ConPosY_1 = ConPosY_1
				endif
			endif
			parameters ConPosX_1 = 0
		endif
		ConPosZ_1 = tapHeight
		parameters ConPosZ_1 = ConPosZ_1
	else
		ConDVecX = 0
		ConDVecY = 1-2*ConPos_1
		ConDVecZ = 0

		ConWVecX = -1+2*ConPos_1
		ConWVecY = 0
		ConWVecZ = 0

		values "ConPosX_1" range [ConWidth_1/2, A-ConWidth_1/2]
		parameters ConPosY_1 = B-B*ConPos_1
		values "ConPosZ_1" range [, zzyzx-ConWidth_1/2]
	endif

	if not(ConStatus_1) or bWallMountedTap then hideparameter "ConPos_1"


	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1+(xTapPos + tapDistCorn)*(bWallmountedTap & bFlipTap),
		ConPosY = ConPosY_1-b+(b + xTapPos - tapDistCorn)*(bWallmountedTap & not(bFlipTap)),
		ConPosZ = ConPosZ_1+zTapPos*bWallmountedTap,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 1


	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Подключение Холодной Воды`

	if ConWidth_2 < 0.001 then
		ConWidth_2 = 0.001
		parameters ConWidth_2 = ConWidth_2
	endif
	if ConWidth_2 > min (A/2, zzyzx/2) then
		ConWidth_2 = min (A/2, zzyzx/2)
		parameters ConWidth_2 = ConWidth_2
	endif
	values "ConWidth_2" range (0, min (A/2, zzyzx/2)]
	parameters ConDepth_2 = ConWidth_2
	values "ConLength_2" range [0, ]

	! Default Position
	if (GLOB_MODPAR_NAME = "gs_tap_type" | GLOB_MODPAR_NAME = "tapGroup" | GLOB_MODPAR_NAME = "bFlipTap") then
		if bWallmountedTap then
			if bFlipTap then
				ConPosX_2 = -posTap - widthTap/2
			else
				ConPosY_2 = -posTap - widthTap/2
			endif
		else
			ConPosX_2 = 0.2
		endif
		parameters ConPosX_2 = ConPosX_2
		parameters ConPosY_2 = ConPosY_2
	endif

	! range
	if bWallMountedTap then
		pos1st = -posTap - widthTap + ConWidth_2/2
		pos2nd = -posTap - ConWidth_2/2
		pos3rd = +posTap + ConWidth_2/2
		pos4th = +posTap + widthTap - ConWidth_2/2

		if bFlipTap then
			ConDVecX = 0
			ConDVecY = 1
			ConDVecZ = 0

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			! --- X range

			values "ConPosX_2"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosX_1" then
				if ConPosX_2 < 0 & ConPosX_1 < 0 then
					ConPosX_2 = posTap + widthTap/2
					parameters 	ConPosX_2 = ConPosX_2
				endif
				if ConPosX_2 > 0 & ConPosX_1 > 0 then
					ConPosX_2 = -(posTap + widthTap/2)
					parameters 	ConPosX_2 = ConPosX_2
				endif
			endif
			parameters ConPosY_2 = B
		else
			ConDVecX = -1
			ConDVecY = 0
			ConDVecZ = 0

			ConWVecX = 0
			ConWVecY = 1
			ConWVecZ = 0

			pos1st = -posTap - widthTap + ConWidth_2/2
			pos2nd = -posTap - ConWidth_2/2
			pos3rd = +posTap + ConWidth_2/2
			pos4th = +posTap + widthTap - ConWidth_2/2

			! --- X range

			values "ConPosY_2"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosY_1" then
				if ConPosY_1 < 0 & ConPosY_2 < 0 then
					ConPosY_2 = posTap + widthTap/2
					parameters 	ConPosY_2 = ConPosY_2
				endif
				if ConPosY_1 > 0 & ConPosY_2 > 0 then
					ConPosY_2 = -(posTap + widthTap/2)
					parameters 	ConPosY_2 = ConPosY_2
				endif
			endif
			parameters ConPosX_2 = 0
		endif
		ConPosZ_2 = tapHeight
		parameters ConPosZ_2 = ConPosZ_2
	else
		ConDVecX = 0
		ConDVecY = 1-2*ConPos_2
		ConDVecZ = 0

		ConWVecX = -1+2*ConPos_2
		ConWVecY = 0
		ConWVecZ = 0

		values "ConPosX_2" range [ConWidth_2/2, A-ConWidth_2/2]
		parameters ConPosY_2 = B-B*ConPos_2
		values "ConPosZ_2" range [, zzyzx-ConWidth_2/2]
	endif

	if not(ConStatus_2) or bWallMountedTap then hideparameter "ConPos_2"


	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2+(xTapPos + tapDistCorn)*(bWallmountedTap & bFlipTap),
		ConPosY = ConPosY_2-b+(b + xTapPos - tapDistCorn)*(bWallmountedTap & not(bFlipTap)),
		ConPosZ = ConPosZ_2+zTapPos*bWallmountedTap,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 0


	parameters MEP_ConInfo[ConID][1] = str (ConToolType_2, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


					! --- 3rd CONNECTION --- !

	ConID = 3
	ConName = `Подключение Канализации`

	ConDVecX = 0
	ConDVecY = 0
	ConDVecZ = -1

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if ConWidth_3 < 0.001 then
		ConWidth_3 = 0.001
		parameters ConWidth_3 = ConWidth_3
	endif
	if ConWidth_3 > min (A/2, B/2) then
		ConWidth_3 = min (A/2, B/2)
		parameters ConWidth_3 = ConWidth_3
	endif
	values "ConWidth_3" range (0, min (A/2, B/2)]
	parameters ConDepth_3 = ConWidth_3
	values "ConLength_3" range [0, ]


	parameters ConPosZ_3 = 0


	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_3,
		ConPosY = ConPosY_3-b,
		ConPosZ = ConPosZ_3,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_3,
		ConNominalDepth = ConNominalDepth_3,
		ConWidth = ConWidth_3,
		ConDepth = ConDepth_3,
		ConLength = ConLength_3,
		ConWallThickness = ConWallThickness_3,
		ConConnectorWidth = ConConnectorWidth_3,
		ConConnectorDepth = ConConnectorDepth_3,
		ConConnectorWidth2 = ConConnectorWidth2_3,
		ConToolType = ConToolType_3,
		ConStatus = ConStatus_3,
		ConType_3 = ConType_3,
		ConConnectorType_3 = ConConnectorType_3,
		ConSystem_3 = ConSystem_3,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 0


	parameters MEP_ConInfo[ConID][1] = str (ConToolType_3, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	if gs_ui_current_page = 5 then
		ac_mep_connectionpage_active = 1
	else
		ac_mep_connectionpage_active = 0
	endif
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
else
	hideparameter "gs_connection", "gs_con_mat", "useSysMat"

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active
endif

call "ui_plumbingfixtures" parameters iObjectType 		= iObjectType,
								gs_ui_current_page = gs_ui_current_page

! -----------------------------------------------------------------------------
! Onorm list Settings
! -----------------------------------------------------------------------------

if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif
end

