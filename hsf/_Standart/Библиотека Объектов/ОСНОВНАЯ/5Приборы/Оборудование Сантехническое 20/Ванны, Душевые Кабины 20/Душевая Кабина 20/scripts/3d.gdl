
! ==============================================================================
! Shower Cabin
! ==============================================================================


IF gs_detlevel_3D_m = 0 THEN END

if gs_shadow=0 then SHADOW OFF
if gs_shadow=1 then SHADOW ON


! ==============================================================================
! MEP
! ==============================================================================

if gs_detlevel_3D_m = 1 or gs_detlevel_3D_m = 3 then resol 12
if gs_detlevel_3D_m = 2 then resol gs_resol


if bShowMEP then

	IF useSysMat THEN
		gs_tub_mat = sMat
		gs_con_mat = sMat
	ENDIF

	add -A/2 + xTapPos, 0, 0

	if gs_tap_type_m > 25 then
		ConEdit_1 = 16
		ConEdit_2 = 16
	else
		ConEdit_1 = 12
		ConEdit_2 = 12
	endif

	! Hot Water & Cold Water Connection
	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
		ConShow_3 = 0, ConHotspotShow_3 = 0
	del 1

	! Waste Water
	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConShow_1 = 0, ConHotspotShow_1 = 0,
		ConShow_2 = 0, ConHotspotShow_2 = 0,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 1


endif


! ==============================================================================
! Tray & Ceiling
! ==============================================================================

pen gs_cont_pen
material matTray
frameThk = 0.01
trayChamferUpper = 0.025
trayChamferLower = 0.04
trayFlange = 0.025
trayBottomThk = 0.03
trayChamferCorner = 0.03
nTrayCorner = int(gs_resol/4)
fi = 90/nTrayCorner
gi = 45/ceil(nTrayCorner/2)
trayMargin = trayFlange + trayChamferUpper + trayChamferLower
ceilingThk = 0.02
nLeaf = 1
unID = 1

if iCabinForm = CABINFORM_ROUNDEDCORNER | iCabinForm = CABINFORM_ROUNDEDPENINSULAR | iCabinForm = CABINFORM_ROUND then			! Rounded Corner
	if iDoorType = DOORTYPE_DOUBLELEAF 		then nLeaf = 2
	if iDoorType = DOORTYPE_DOUBLESLIDING 	then nLeaf = 2
endif

if iCabinForm = CABINFORM_SQUAREDALCOVE | iCabinForm = CABINFORM_SQUAREDCORNER | iCabinForm = CABINFORM_SQUAREDPENINSULAR | iCabinForm = CABINFORM_SQUARE then
	hotspot 0, 0, 0, unID : unID = unID + 1
	hotspot -A/2, -B/2, 0, unID : unID = unID + 1
	hotspot  A/2, -B/2, 0, unID : unID = unID + 1
	hotspot  A/2,  B/2, 0, unID : unID = unID + 1
	hotspot -A/2,  B/2, 0, unID : unID = unID + 1

	hotspot 0, 0, ZZYZX, unID : unID = unID + 1
	hotspot -A/2, -B/2, ZZYZX, unID : unID = unID + 1
	hotspot  A/2, -B/2, ZZYZX, unID : unID = unID + 1
	hotspot  A/2,  B/2, ZZYZX, unID : unID = unID + 1
	hotspot -A/2,  B/2, ZZYZX, unID : unID = unID + 1

	if bTray then

		if gs_detlevel_3D_m = 3 then		! --- Draft model

			prism_ 5, trayBottomThk,
				-A/2, -B/2, 11,
				A/2, -B/2, 11,
				A/2,  B/2, 11,
				-A/2,  B/2, 11,
				-A/2, -B/2, -1

			addz trayBottomThk
			prism_ 10, trayThk - trayBottomThk,
				-A/2, -B/2, 14,
				A/2, -B/2, 14,
				A/2,  B/2, 14,
				-A/2,  B/2, 14,
				-A/2, -B/2, -1,
				-A/2 + trayFlange, -B/2 + trayFlange, 15,
				A/2 - trayFlange, -B/2 + trayFlange, 15,
				A/2 - trayFlange,  B/2 - trayFlange, 15,
				-A/2 + trayFlange,  B/2 - trayFlange, 15,
				-A/2 + trayFlange, -B/2 + trayFlange, -1
			del 1

		else							! --- Detailed/Simple model

			for ki = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ki*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(ki*fi), 0, 0
			next ki
			for hi = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(hi*fi), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * sin(hi*fi), 0, 0
			next hi
			for ii = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * sin(ii*fi), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ii*fi), 0, 0
			next ii
			for ji = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ji*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ji*fi), 0, 0
			next ji

			addx A/2
			cutplane 0, 1, 1
			del 1
			addy -B/2
			cutplane 1, 0, 1, 1
			del 1
			addx -A/2
			cutplane 0, 1, 1, 1
			del 1
			cutpolya 4, 1, 0,
				-A, B/2, 0,
				A, B/2, 5,
				A, B, 5,
				-A, B, 5
			tube 10, 8 + nsp/4, 1 + 2 + 16*0 + 32*0 + 64*0,
				0, 0, 1,
				0, trayBottomThk, 1,
				1, 0, 800,
				trayChamferLower, trayBottomThk + trayChamferLower, 1001,
				trayChamferLower, trayThk - trayChamferUpper, 1,
				trayChamferLower + trayChamferUpper, trayThk, 1001,
				trayFlange + trayChamferUpper + trayChamferLower, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, 0, 1,
				0, 0, -1,

				-A/2, B/2 - trayMargin, 0, 0,
				0, B/2 - trayMargin, 0, 0,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 0, 0,
				get(nsp/4),
				A/2 - trayMargin, -B/2 + trayMargin + trayChamferCorner, 0, 0,
				get(nsp/3),
				-A/2 + trayMargin + trayChamferCorner, -B/2 + trayMargin, 0, 0,
				get(nsp/2),
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 0, 0,
				get(nsp),
				0, B/2 - trayMargin, 0, 0,
				A, B/2 - trayMargin, 0, 0
			cutend
			cutend
			cutend
			cutend

			lin_ -A/2, B/2, 0, A/2, B/2, 0
			lin_ -A/2, B/2, trayThk, A/2, B/2, trayThk
			lin_ -A/2, B/2, 0, -A/2, B/2, trayThk
			lin_ A/2, B/2, 0, A/2, B/2, trayThk

			prism_ 11, trayBottomThk,
				A/2 - trayMargin, 0, 8,
				A/2 - trayMargin, B/2 - trayMargin - trayChamferCorner, 8 + 64,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 1008,
				-A/2 + trayMargin + trayChamferCorner, B/2 - trayMargin, 8 + 64,
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 1008,
				-A/2 + trayMargin, 0, 8,
				-A/2 + trayMargin, -B/2 + trayMargin + trayChamferCorner, 8,
				-A/2 + trayMargin + trayChamferCorner, -B/2 + trayMargin, 1008 + 64,
				A/2 - trayMargin - trayChamferCorner, -B/2 + trayMargin, 8,
				A/2 - trayMargin, -B/2 + trayMargin + trayChamferCorner, 1008 + 64,
				A/2 - trayMargin, 0, -1

			if gs_detlevel_3D_m <> 3 then
				add xWasteWaterPos, yWasteWaterPos, trayBottomThk
				cylind 0.005, wasteWaterR
				del 1
			endif
		endif

	endif

	if bCeiling then
		addz ZZYZX - ceilingThk
		prism_ 5, ceilingThk,
			-A/2, -B/2, 15,
			A/2, -B/2, 15,
			A/2, B/2, 15,
			-A/2, B/2, 15,
			-A/2, -B/2, -1
		del 1
	endif
endif

if iCabinForm = CABINFORM_ROUNDEDCORNER then
	hotspot 0, 0, 0, unID : unID = unID + 1
	hotspot -A/2, -B/2, 0, unID : unID = unID + 1
	hotspot  A/2 - doorWidth*cos(45), -B/2, 0, unID : unID = unID + 1
	hotspot  A/2,  -B/2 + doorWidth*cos(45), 0, unID : unID = unID + 1
	hotspot  A/2,  B/2, 0, unID : unID = unID + 1
	hotspot -A/2,  B/2, 0, unID : unID = unID + 1

	hotspot 0, 0, ZZYZX, unID : unID = unID + 1
	hotspot -A/2, -B/2, ZZYZX, unID : unID = unID + 1
	hotspot  A/2 - doorWidth*cos(45), -B/2, ZZYZX, unID : unID = unID + 1
	hotspot  A/2,  -B/2 + doorWidth*cos(45), ZZYZX, unID : unID = unID + 1
	hotspot  A/2,  B/2, ZZYZX, unID : unID = unID + 1
	hotspot -A/2,  B/2, ZZYZX, unID : unID = unID + 1

	trayChamferCorner2 = doorWidth * cos(45) - trayMargin
	if bTray then

		if gs_detlevel_3D_m = 3 then		! --- Draft model

			prism_ 5, trayBottomThk,
				-A/2, B/2, 11,
				-A/2, -B/2, 11,
				A/2 - doorWidth*cos(45), -B/2, 75,
				A/2, -B/2 + doorWidth*cos(45), 1075,
				A/2, B/2, 11

			addz trayBottomThk
			prism_ 12, trayThk - trayBottomThk,
				-A/2, B/2, 14,
				-A/2, -B/2, 14,
				A/2 - doorWidth*cos(45), -B/2, 78,
				A/2, -B/2 + doorWidth*cos(45), 1078,
				A/2, B/2, 14,
				-A/2, B/2, -1,
				-A/2 + trayFlange, B/2 - trayFlange, 15,
				-A/2 + trayFlange, -B/2 + trayFlange, 15,
				A/2 - doorWidth*cos(45), -B/2 + trayFlange, 79,
				A/2 - trayFlange, -B/2 + doorWidth*cos(45), 1079,
				A/2 - trayFlange, B/2 - trayFlange, 15,
				-A/2 + trayFlange, B/2 - trayFlange, -1
			del 1

		else							! --- Detailed/Simple model

			for ki = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ki*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(ki*fi), 0, 0
			next ki
			for hi = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner2 + trayChamferCorner2 * cos(hi*fi), -B/2 + trayMargin + trayChamferCorner2 - trayChamferCorner2 * sin(hi*fi), 0, 0
			next hi
			for ii = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * sin(ii*fi), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ii*fi), 0, 0
			next ii
			for ji = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ji*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ji*fi), 0, 0
			next ji

			addx -A/2
			cutplane 0, 1, 1, 1
			del 1
			cutpolya 4, 1, 0,
				-A, B/2, 0,
				A, B/2, 5,
				A, B, 5,
				-A, B, 5
			cutpolya 8, 1, 0,
				-A/2, -B/2, 7,
				A/2 - doorWidth * cos(45), -B/2, 7 + 64,
				A/2, -B/2 + doorWidth * cos(45), 1007 + 64,
				A/2, B/2, 7,
				A, B/2, 7,
				A, -B, 7,
				-A/2, -B, 7,
				-A/2, -B/2, -1
			tube 10, 8 + nsp/4, 1 + 2 + 16*0 + 32*0 + 64*0,
				0, 0, 1,
				0, trayBottomThk, 1,
				1, 0, 800,
				trayChamferLower, trayBottomThk + trayChamferLower, 1001,
				trayChamferLower, trayThk - trayChamferUpper, 1,
				trayChamferLower + trayChamferUpper, trayThk, 1001,
				trayFlange + trayChamferUpper + trayChamferLower, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, 0, 1,
				0, 0, -1,

				-A/2, B/2 - trayMargin, 0, 0,
				0, B/2 - trayMargin, 0, 0,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 0, 0,
				get(nsp/4),
				A/2 - trayMargin, -B/2 + trayMargin + trayChamferCorner2, 0, 0,
				get(nsp/3),
				-A/2 + trayMargin + trayChamferCorner, -B/2 + trayMargin, 0, 0,
				get(nsp/2),
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 0, 0,
				get(nsp),
				0, B/2 - trayMargin, 0, 0,
				A, B/2 - trayMargin, 0, 0
			cutend
			cutend
			cutend

			lin_ -A/2, B/2, 0, A/2, B/2, 0
			lin_ -A/2, B/2, trayThk, A/2, B/2, trayThk
			lin_ -A/2, B/2, 0, -A/2, B/2, trayThk
			lin_ A/2, B/2, 0, A/2, B/2, trayThk

			prism_ 10, trayBottomThk,
				0, B/2 - trayMargin, 8,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 8 + 64,
				A/2 - trayMargin, B/2 - trayMargin - trayChamferCorner, 1008,
				A/2 - trayMargin, -B/2 + trayMargin + trayChamferCorner2, 8 + 64,
				A/2 - trayMargin - trayChamferCorner2, -B/2 + trayMargin, 1008,
				-A/2 + trayMargin + trayChamferCorner, -B/2 + trayMargin, 8 + 64,
				-A/2 + trayMargin, -B/2 + trayMargin + trayChamferCorner, 1008,
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 8 + 64,
				-A/2 + trayMargin + trayChamferCorner, B/2 - trayMargin, 1008 + 64,
				0, B/2 - trayMargin, -1

			if gs_detlevel_3D_m <> 3 then
				add xWasteWaterPos, yWasteWaterPos, trayBottomThk
				cylind 0.005, wasteWaterR
				del 1
			endif
		endif

	endif

	if bCeiling then
		addz ZZYZX - ceilingThk
		prism_ 6, ceilingThk,
			-A/2, -B/2, 15,
			A/2 - doorWidth * cos(45), -B/2, 15 + 64,
			A/2, -B/2 + doorWidth * cos(45), 1015 + 64,
			A/2, B/2, 15,
			-A/2, B/2, 15,
			-A/2, -B/2, -1
		del 1
	endif
endif

if iCabinForm = CABINFORM_CHAMFERCORNER then
	hotspot 0, 0, 0, unID : unID = unID + 1
	hotspot -A/2, -B/2, 0, unID : unID = unID + 1
	hotspot  A/2 - doorWidth*cos(45), -B/2, 0, unID : unID = unID + 1
	hotspot  A/2,  -B/2 + doorWidth*cos(45), 0, unID : unID = unID + 1
	hotspot  A/2,  B/2, 0, unID : unID = unID + 1
	hotspot -A/2,  B/2, 0, unID : unID = unID + 1

	hotspot 0, 0, ZZYZX, unID : unID = unID + 1
	hotspot -A/2, -B/2, ZZYZX, unID : unID = unID + 1
	hotspot  A/2 - doorWidth*cos(45), -B/2, ZZYZX, unID : unID = unID + 1
	hotspot  A/2,  -B/2 + doorWidth*cos(45), ZZYZX, unID : unID = unID + 1
	hotspot  A/2,  B/2, ZZYZX, unID : unID = unID + 1
	hotspot -A/2,  B/2, ZZYZX, unID : unID = unID + 1

	trayChamferCorner2 = doorWidth * cos(45) - trayMargin
	if bTray then

		if gs_detlevel_3D_m = 3 then		! --- Draft model

			prism_ 5, trayBottomThk,
				-A/2, B/2, 11,
				-A/2, -B/2, 11,
				A/2 - doorWidth*cos(45), -B/2, 11,
				A/2, -B/2 + doorWidth*cos(45), 11,
				A/2, B/2, 11

			addz trayBottomThk
			prism_ 12, trayThk - trayBottomThk,
				-A/2, B/2, 14,
				-A/2, -B/2, 14,
				A/2 - doorWidth*cos(45), -B/2, 14,
				A/2, -B/2 + doorWidth*cos(45), 14,
				A/2, B/2, 14,
				-A/2, B/2, -1,
				-A/2 + trayFlange, B/2 - trayFlange, 15,
				-A/2 + trayFlange, -B/2 + trayFlange, 15,
				A/2 - doorWidth*cos(45) - trayFlange*tan(22.5), -B/2 + trayFlange, 15,
				A/2 - trayFlange, -B/2 + doorWidth*cos(45) + trayFlange*tan(22.5), 15,
				A/2 - trayFlange, B/2 - trayFlange, 14,
				-A/2 + trayFlange, B/2 - trayFlange, -1
			del 1

		else							! --- Detailed/Simple model

			for ki = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ki*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(ki*fi), 0, 0
			next ki
			for hi = 1 to ceil(nTrayCorner/2)
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(hi*gi), -B/2 + doorWidth * cos(45) + (trayMargin + trayChamferCorner) * tan(22.5) - trayChamferCorner * sin(hi*gi), 0, 0
			next hi
			for li = 1 to ceil(nTrayCorner/2)
				put A/2 - doorWidth * cos(45) - (trayMargin + trayChamferCorner) * tan(22.5) + trayChamferCorner * sin(45 - li*gi), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(45-li*gi), 0, 0
			next li
			for ii = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * sin(ii*fi), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ii*fi), 0, 0
			next ii
			for ji = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ji*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ji*fi), 0, 0
			next ji

			addx -A/2
			cutplane 0, 1, 1, 1
			del 1
			cutpolya 4, 1, 0,
				-A, B/2, 0,
				A, B/2, 5,
				A, B, 5,
				-A, B, 5
			cutpolya 8, 1, 0,
				-A/2, -B/2, 7,
				A/2 - doorWidth * cos(45), -B/2, 7,
				A/2, -B/2 + doorWidth * cos(45), 7,
				A/2, B/2, 7,
				A, B/2, 7,
				A, -B, 7,
				-A/2, -B, 7,
				-A/2, -B/2, -1
			tube 10, 10 + nsp/4*0 + nTrayCorner*3 + ceil(nTrayCorner/2)*2, 1 + 2 + 16*0 + 32*0 + 64*0,
				0, 0, 1,
				0, trayBottomThk, 1,
				1, 0, 800,
				trayChamferLower, trayBottomThk + trayChamferLower, 1001,
				trayChamferLower, trayThk - trayChamferUpper, 1,
				trayChamferLower + trayChamferUpper, trayThk, 1001,
				trayFlange + trayChamferUpper + trayChamferLower, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, 0, 1,
				0, 0, -1,

				-A/2, B/2 - trayMargin, 0, 0,
				0, B/2 - trayMargin, 0, 0,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 0, 0,
				get(nTrayCorner*4),
				A/2 - trayMargin, -B/2 + doorWidth * cos(45) + (trayMargin + trayChamferCorner) * tan(22.5), 0, 0,
				get(ceil(nTrayCorner/2)*4),
				A/2 - doorWidth * cos(45) - (trayMargin + trayChamferCorner) * tan(22.5) + trayChamferCorner * cos(45), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(45), 0, 0,
				get(ceil(nTrayCorner/2)*4),
				A/2 - doorWidth * cos(45) - (trayMargin + trayChamferCorner) * tan(22.5), -B/2 + trayMargin, 0, 0,
				-A/2 + trayMargin + trayChamferCorner, -B/2 + trayMargin, 0, 0,
				get(nTrayCorner*4),
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 0, 0,
				get(nTrayCorner*4),
				0, B/2 - trayMargin, 0, 0,
				A, B/2 - trayMargin, 0, 0
			cutend
			cutend
			cutend

			lin_ -A/2, B/2, 0, A/2, B/2, 0
			lin_ -A/2, B/2, trayThk, A/2, B/2, trayThk
			lin_ -A/2, B/2, 0, -A/2, B/2, trayThk
			lin_ A/2, B/2, 0, A/2, B/2, trayThk

			for ki = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ki*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(ki*fi), 8
			next ki
			for hi = 1 to ceil(nTrayCorner/2)
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(hi*gi), -B/2 + doorWidth * cos(45) + (trayMargin + trayChamferCorner) * tan(22.5) - trayChamferCorner * sin(hi*gi), 8 + 7
			next hi
			for li = 1 to ceil(nTrayCorner/2)
				put A/2 - doorWidth * cos(45) - (trayMargin + trayChamferCorner) * tan(22.5) + trayChamferCorner * sin(45 - li*gi), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(45-li*gi), 8
			next li
			for ii = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * sin(ii*fi), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ii*fi), 8
			next ii
			for ji = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ji*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ji*fi), 8
			next ji
			prism_ 8 + nsp/3, trayBottomThk,
				0, B/2 - trayMargin, 8,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 8,
				get(nTrayCorner*3),
				A/2 - trayMargin, -B/2 + doorWidth * cos(45) + (trayMargin + trayChamferCorner) * tan(22.5), 8,
				get(ceil(nTrayCorner/2)*3),
				A/2 - doorWidth * cos(45) - (trayMargin + trayChamferCorner) * tan(22.5) + trayChamferCorner * cos(45), -B/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(45), 8,
				get(ceil(nTrayCorner/2)*3),
				A/2 - doorWidth * cos(45) - (trayMargin + trayChamferCorner) * tan(22.5), -B/2 + trayMargin, 8,
				-A/2 + trayMargin + trayChamferCorner, -B/2 + trayMargin, 8,
				get(nTrayCorner*3),
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 8,
				get(nTrayCorner*3),
				0, B/2 - trayMargin, -1

			if gs_detlevel_3D_m <> 3 then
				add xWasteWaterPos, yWasteWaterPos, trayBottomThk
				cylind 0.005, wasteWaterR
				del 1
			endif
		endif

	endif

	if bCeiling then
		addz ZZYZX - ceilingThk
		prism_ 6, ceilingThk,
			-A/2, -B/2, 15,
			A/2 - doorWidth * cos(45), -B/2, 15,
			A/2, -B/2 + doorWidth * cos(45), 15,
			A/2, B/2, 15,
			-A/2, B/2, 15,
			-A/2, -B/2, -1
		del 1
	endif
endif

if iCabinForm = CABINFORM_ROUNDEDPENINSULAR then
	trayChamferCorner2 = A/2 - trayMargin
	arcAngle = 2 * asn(doorWidth/A)
	if bTray then

		if gs_detlevel_3D_m = 3 then		! --- Draft model

			prism_ 5, trayBottomThk,
				-A/2, B/2, 11,
				-A/2, -B/2 + A/2, 75,
				A/2, -B/2 + A/2, 1075,
				A/2, B/2, 11,
				-A/2, B/2, -1

			addz trayBottomThk
			prism_ 10, trayThk - trayBottomThk,
				-A/2, B/2, 14,
				-A/2, -B/2 + A/2, 78,
				A/2, -B/2 + A/2, 1078,
				A/2, B/2, 14,
				-A/2, B/2, -1,
				-A/2 + trayFlange, B/2 - trayFlange, 15,
				-A/2 + trayFlange, -B/2 + A/2, 79,
				A/2 - trayFlange, -B/2 + A/2, 1079,
				A/2 - trayFlange, B/2 - trayFlange, 15,
				-A/2 + trayFlange, B/2 - trayFlange, -1
			del 1

		else							! --- Detailed/Simple model

			cutpolya 4, 1, 0,
				-A, B/2, 0,
				A, B/2, 5,
				A, B, 5,
				-A, B, 5
			if nLeaf = 1 then
				put -(A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1007 + 64
				put (A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1007 + 64
			else
				put -(A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1007 + 64
				put 0, -B/2, 1007 + 64
				put (A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1007 + 64
			endif
			cutpolya 9 + nsp/3, 1, 0,
				-A, B/2, 7,
				-A/2, B/2, 7,
				-A/2, -B/2 + A/2, 7 + 64,
				get(nsp),
				 A/2, -B/2 + A/2, 1007 + 64,
				 A/2, B/2, 7,
				 A, B/2, 7,
				 A, -B, 7,
				-A, -B, 7,
				-A, B/2, -1
			for ki = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ki*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(ki*fi), 0, 0
			next ki
			for hi = 1 to 2*nTrayCorner + 1
				put A/2 - trayMargin - trayChamferCorner2 + trayChamferCorner2 * cos(hi*gi), -B/2 + trayMargin + trayChamferCorner2 - trayChamferCorner2 * sin(hi*gi), 0, 0
			next hi
				for ji = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ji*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ji*fi), 0, 0
			next ji
			tube 10, 8 + nsp/4, 1 + 2 + 16*0 + 32*0 + 64*0,
				0, 0, 1,
				0, trayBottomThk, 1,
				1, 0, 800,
				trayChamferLower, trayBottomThk + trayChamferLower, 1001,
				trayChamferLower, trayThk - trayChamferUpper, 1,
				trayChamferLower + trayChamferUpper, trayThk, 1001,
				trayFlange + trayChamferUpper + trayChamferLower, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, 0, 1,
				0, 0, -1,

				-A/2, B/2 - trayMargin, 0, 0,
				0, B/2 - trayMargin, 0, 0,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 0, 0,
				get(nTrayCorner*4),
				A/2 - trayMargin, -B/2 + trayMargin + trayChamferCorner2, 0, 0,
				get(nTrayCorner*8+4),
				-A/2 + trayMargin, -B/2 + trayMargin + trayChamferCorner2, 0, 0,
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 0, 0,
				get(nTrayCorner*4),
				0, B/2 - trayMargin, 0, 0,
				A, B/2 - trayMargin, 0, 0
			cutend
			cutend

			lin_ -A/2, B/2, 0, A/2, B/2, 0
			lin_ -A/2, B/2, trayThk, A/2, B/2, trayThk
			lin_ -A/2, B/2, 0, -A/2, B/2, trayThk
			lin_ A/2, B/2, 0, A/2, B/2, trayThk

			for ki = 1 to nTrayCorner
				put A/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ki*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * cos(ki*fi), 8
			next ki
			for hi = 1 to 2*nTrayCorner + 1
				put A/2 - trayMargin - trayChamferCorner2 + trayChamferCorner2 * cos(hi*gi), -B/2 + trayMargin + trayChamferCorner2 - trayChamferCorner2 * sin(hi*gi), 8
			next hi
				for ji = 1 to nTrayCorner
				put -A/2 + trayMargin + trayChamferCorner - trayChamferCorner * cos(ji*fi), B/2 - trayMargin - trayChamferCorner + trayChamferCorner * sin(ji*fi), 8
			next ji
			prism_ 6 + nsp/3, trayBottomThk,
				0, B/2 - trayMargin, 8,
				A/2 - trayMargin - trayChamferCorner, B/2 - trayMargin, 8,
				get(nTrayCorner*3),
				A/2 - trayMargin, -B/2 + trayMargin + trayChamferCorner2, 8,
				get(nTrayCorner*6+3),
				-A/2 + trayMargin, -B/2 + trayMargin + trayChamferCorner2, 8,
				-A/2 + trayMargin, B/2 - trayMargin - trayChamferCorner, 8,
				get(nTrayCorner*3),
				0, B/2 - trayMargin, -1

			if gs_detlevel_3D_m <> 3 then
				add xWasteWaterPos, yWasteWaterPos, trayBottomThk
				cylind 0.005, wasteWaterR
				del 1
			endif
		endif

	endif

	if bCeiling then
		addz ZZYZX - ceilingThk
		if nLeaf = 1 then
			put -(A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1015 + 64
			put (A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1015 + 64
		else
			put -(A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1015 + 64
			put 0, -B/2, 1015 + 64
			put (A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 1015 + 64
		endif
		prism_ 5 + nsp/3, ceilingThk,
			-A/2, B/2, 15,
			-A/2, -B/2 + A/2, 15 + 64,
			get(nsp),
			 A/2, -B/2 + A/2, 1015 + 64,
			 A/2, B/2, 15,
			-A/2, B/2, -1
		del 1
	endif
endif

if iCabinForm = CABINFORM_ROUND then
	trayChamferCorner2 = A/2 - trayMargin
	arcAngle = 2 * asn(doorWidth/A)
	nSegment1 = round_int((360 - arcAngle)/360*gs_resol)
	nSegment2 = gs_resol - nSegment1
	gi = 360/gs_resol
	fi = arcAngle/nSegment2
	arcRadius = A/2 - frameThk
	if bTray then

		if gs_detlevel_3D_m = 3 then		! --- Draft model

			prism_ 2, trayBottomThk,
				0, 0, 975,
				A/2, 360, 4075

			addz trayBottomThk
			prism_ 10, trayThk - trayBottomThk,
				A/2, 0, 78,
				0, 0, 978,
				A/2, 180, 4078,
				A/2, 0, 1078,
				A/2, 0, -1,
				A/2 - trayFlange, 0, 79,
				0, 0, 979,
				A/2 - trayFlange, 180, 4079,
				A/2 - trayFlange, 0, 1079,
				A/2 - trayFlange, 0, -1
			del 1

		else							! --- Detailed/Simple model

			cutpolya 8, 1, 0,
				-A, -arcRadius * cos(arcAngle/2), 64,
				-arcRadius * sin(arcAngle/2), -arcRadius * cos(arcAngle/2), 64,
				-1, tan(arcAngle/2), 800,
				arcRadius * sin(arcAngle/2), -arcRadius * cos(arcAngle/2), 1000 + 64,
				A, -arcRadius * cos(arcAngle/2), 64,
				A, A, 0,
				-A, A, 0,
				-A, -arcRadius * cos(arcAngle/2), -1
			cutpolya 8, 1, 0,
				-A, -arcRadius * cos(arcAngle/2), 64,
				-arcRadius * sin(arcAngle/2), -arcRadius * cos(arcAngle/2), 64,
				0, 0, 900,
				arcRadius * sin(arcAngle/2), -arcRadius * cos(arcAngle/2), 3000 + 64,
				A, -arcRadius * cos(arcAngle/2), 64,
				A, -A, 0,
				-A, -A, 0,
				-A, -arcRadius * cos(arcAngle/2), -1
			for hi = 1 to gs_resol + 3
				put A/2 - trayMargin - trayChamferCorner2 + trayChamferCorner2 * cos(hi*gi), -B/2 + trayMargin + trayChamferCorner2 - trayChamferCorner2 * sin(hi*gi), 0, 0
			next hi
			rotz -90
			tube 10, nsp/4, 1 + 2 + 16*0 + 32*0 + 64*0,
				0, 0, 1,
				0, trayBottomThk, 1,
				1, 0, 800,
				trayChamferLower, trayBottomThk + trayChamferLower, 1001,
				trayChamferLower, trayThk - trayChamferUpper, 1,
				trayChamferLower + trayChamferUpper, trayThk, 1001,
				trayFlange + trayChamferUpper + trayChamferLower, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, trayThk, 1,
				trayFlange + trayChamferUpper + trayChamferLower + 0.1, 0, 1,
				0, 0, -1,

				get(nsp)
			del 1
			cutend
			cutend

			prism_ 5 + 5, trayThk,
				-A/2 * sin(arcAngle/2), -A/2 * cos(arcAngle/2), 15 + 64,
				0, 0, 915 + 64,
				A/2 * sin(arcAngle/2), -A/2 * cos(arcAngle/2), 3015 + 64,
				-A/2 * sin(arcAngle/2), -A/2 * cos(arcAngle/2), 1015 + 64,
				-A/2 * sin(arcAngle/2), -A/2 * cos(arcAngle/2), -1,

				-(A/2 - frameThk)* sin(arcAngle/2), -(A/2 - frameThk) * cos(arcAngle/2), 8 + 64,
				0, 0, 900,
				(A/2 - frameThk) * sin(arcAngle/2), -(A/2 - frameThk) * cos(arcAngle/2), 3008 + 64,
				-(A/2 - frameThk) * sin(arcAngle/2), -(A/2 - frameThk) * cos(arcAngle/2), 1008 + 64,
				-(A/2 - frameThk) * sin(arcAngle/2), -(A/2 - frameThk) * cos(arcAngle/2), -1

			prism_ 2, trayBottomThk,
				0, 0, 908,
				A/2 - trayMargin, 360, 4008

			if gs_detlevel_3D_m <> 3 then
				add 0, yWasteWaterPos, trayBottomThk
				cylind 0.005, wasteWaterR
				del 1
			endif
		endif

	endif

	if bCeiling then
		addz ZZYZX - ceilingThk
		if nLeaf = 1 then
			put -(A/2)*sin(arcAngle/2), -A/2*cos(arcAngle/2), 15 + 64
			put 0, 0, 900
			put (A/2)*sin(arcAngle/2), -A/2*cos(arcAngle/2), 3015 + 64
		else
			put -(A/2)*sin(arcAngle/2), -B/2+A/2-(A/2)*cos(arcAngle/2), 15 + 64
			put 0, 0, 900
			put 0, -B/2, 3015 + 64
			put A/2*sin(arcAngle/2), -A/2*cos(arcAngle/2), 1015 + 64
		endif
		prism_ 2 + nsp/3, ceilingThk,
			get(nsp),
			-A/2 * sin(arcAngle/2), -A/2 * cos(arcAngle/2), 1015 + 64,
			-A/2 * sin(arcAngle/2), -A/2 * cos(arcAngle/2), -1
		del 1
	endif
endif


! ==============================================================================
! Panels
! ==============================================================================

if iCabinType = CABINTYPE_FREESTANDING & gs_tap_type_m = TAPTYPE_OVERHEAD then
	panelWidth = 0.166
	panelHeight = 0.266
	addz zTapPos
	rotx 90
	cutform 6, 1, 1 + 2,
		0, 0, 1, -B,
		-panelWidth/2, 0, 13 - 8,
		-panelWidth/2, -panelHeight/2 + panelWidth/2, 15 + 64,
		 panelWidth/2, -panelHeight/2 + panelWidth/2, 1015 + 64,
		 panelWidth/2,  panelHeight/2 - panelWidth/2, 15 + 64,
		-panelWidth/2,  panelHeight/2 - panelWidth/2, 1015 + 64,
		-panelWidth/2, 0, -1
	del 2
endif
addz trayThk
call "showerCabin_m" parameters all ZZYZX				= ZZYZX - bCeiling * ceilingThk - trayThk,
									GS_HotspotUnIDBegin	= unID,
				returned_parameters unID
del 1

if iCabinType = CABINTYPE_FREESTANDING & gs_tap_type_m = TAPTYPE_OVERHEAD then cutend


! ==============================================================================
! Taps
! ==============================================================================

if gs_tap_type_m <> TAPTYPE_NONE & gs_detlevel_3D_m <> 3 then
	add -A / 2, B / 2, 0
	call "tapType_m" parameters all bEnableTapedit 		= 1,
									bSinkTap			= 0,
									useOldSimpleTaps	= 1,
									yShowerHeadPos		= -B / 2,
									zShowerHeadPos 		= ZZYZX - zTapPos,
									GS_HotspotUnIDBegin	= unID,
				returned_parameters xShowerPos,
									unID
	del 1
endif


! ==============================================================================
! Grab Bar
! ==============================================================================

if gs_detlevel_3D_m <> 3 & bGrabBar & (iCabinForm = CABINFORM_SQUAREDALCOVE | iCabinForm = CABINFORM_SQUAREDCORNER | iCabinForm = CABINFORM_ROUNDEDCORNER\
			 | iCabinForm = CABINFORM_CHAMFERCORNER | (iCabinForm = CABINFORM_SQUAREDPENINSULAR & gs_tap_type_m <> TAPTYPE_PANEL)\
			 | (iCabinForm = CABINFORM_ROUNDEDPENINSULAR & gs_tap_type_m <> TAPTYPE_PANEL)) then
	material gs_tap_mat
	grabBarR = 0.011
	grabBarL = 0.416

	if iCabinForm = CABINFORM_SQUAREDALCOVE | iCabinForm = CABINFORM_SQUAREDCORNER | iCabinForm = CABINFORM_ROUNDEDCORNER | iCabinForm = CABINFORM_CHAMFERCORNER then
		add -A/2 + 4 * grabBarR, 0, 0.8
		rotz 0
		rotx 90
	endif
	if iCabinForm = CABINFORM_SQUAREDPENINSULAR | iCabinForm = CABINFORM_ROUNDEDPENINSULAR then
		add 0, B/2 - 4 * grabBarR, zTapPos - 0.2
		rotz -90
		rotx 90
	endif
	addz -(grabBarL - 0.176)/2
	prism_ 4, grabBarL - 0.176,
		0, -grabBarR, 10 + 64,
		1, 0, 800,
		0, grabBarR, 1015 + 64,
		0, -grabBarR, 1008 + 64
	del 1
	for ki = 1 to 2
		addz (grabBarL - 0.176)/2
		prism_ 5, 0.053,
			-4*grabBarR, -grabBarR, 15,
			0, -grabBarR, 10 + 64,
			0, grabBarR, 1013,
			-4*grabBarR, grabBarR, 15,
			-4*grabBarR, -grabBarR, -1
		del 1
		addz (grabBarL - 0.176)/2 + 0.053
		prism_ 4, 0.035,
			0, -grabBarR, 14 + 64,
			1, 0, 800,
			0, grabBarR, 1015 + 64,
			0, -grabBarR, 1008 + 64
		del 1
	mulz -1
	next ki
	del 2
	del 3
endif

end
