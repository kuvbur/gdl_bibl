
call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle"  1, 2, 3

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	if stStyle = stStyles[3] then iStyle = 3
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	if iStyle = 3 then stStyle = stStyles[3]
	parameters stStyle = stStyle
endif


! -----------------------------------------------------------------------------
! Symbol Types
! -----------------------------------------------------------------------------

values "gs_symbol_type" stSymbolTypes
values "iSymbolType" 1, 2


if GLOB_MODPAR_NAME = "gs_symbol_type" then
	iSymbolType = 1
	if gs_symbol_type = stSymbolTypes[2] then iSymbolType = 2
	parameters iSymbolType = iSymbolType
else
	gs_symbol_type = stSymbolTypes[1]
	if iSymbolType = 2 then gs_symbol_type = stSymbolTypes[2]
	parameters gs_symbol_type = gs_symbol_type
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------
dim st_detail3D[4]
	st_detail3D[1] = `Детальный`
	st_detail3D[2] = `Простой`
	st_detail3D[3] = `Упрощенный`
	st_detail3D[4] = `Откл.`

values "gs_detlevel_3D" st_detail3D
values "gs_detlevel_3D_m" 2, 1, 3, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2	! Detailed
	if gs_detlevel_3D = st_detail3D[4] then gs_detlevel_3D_m = 0
	if gs_detlevel_3D = st_detail3D[2] then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = st_detail3D[3] then gs_detlevel_3D_m = 3
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = st_detail3D[1]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = st_detail3D[4]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = st_detail3D[2]
	if gs_detlevel_3D_m = 3 then gs_detlevel_3D = st_detail3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! -----------------------------------------------------------------------------
! Default Sizes
! -----------------------------------------------------------------------------

values "A" range [0.5, )
values "B" range [0.5, )
values "ZZYZX" range [0.04, )
values "dep" range [0, zzyzx-0.01]
values "xsl" range [0.13, a)
values "ysl" range [0.13, b)

! -----------------------------------------------------------------------------
! 2D detlevel
! -----------------------------------------------------------------------------

call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16

! -----------------------------------------------------------------------------
! LOCK and HIDEPARAMETERS
! -----------------------------------------------------------------------------

if gs_detlevel_3D_m = 1 then lock "gs_resol"
if iStyle = 1 | iStyle = 3 then hideparameter "dep", "xsl", "ysl", "sdmat"
if iStyle = 2 then
	lock "gs_symbol_type", "iSymbolType"
	hideparameter "gs_symbol_type"
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if gs_cont_pen <= 0 then parameters gs_cont_pen = 75
if gs_fill_pen <= 0 then parameters gs_fill_pen = 19
values "gs_resol" range [3,)

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------

if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif

if iStyle = 1 then
	lock "MSWidth", "MSFront_2","MSFront_3"
	hideparameter "MSWidth", "MSFront_2", "MSFront_3"
endif
if iStyle = 2 then
	lock "MSAdditionalLength", "MSWidth", "MSFront", "MSFront_3"
	hideparameter "MSAdditionalLength", "MSWidth", "MSFront", "MSFront_3"
endif
if iStyle = 3 then
	lock "MSAdditionalLength", "MSFront_2", "MSFront"
	hideparameter "MSAdditionalLength", "MSFront_2", "MSFront"
endif

! -----------------------------------------------------------------------------
! modify to Shaft
! -----------------------------------------------------------------------------

if isMEPEnabled then

	if NOT(ConStatus_1) then
		lock "gs_con_mat"
		hideparameter "gs_con_mat"
	endif

	dim shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	parameters MEP_NumberConnections = 1


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Подключение Канализации`

	ConDVecX = 0
	ConDVecY = 0
	ConDVecZ = -1

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if ConWidth_1 < 0.001 then
		ConWidth_1 = 0.001
		parameters ConWidth_1 = ConWidth_1
	endif
	if ConWidth_1 > 0.35*(MIN(A/0.90, B/0.90)) then
		ConWidth_1 = 0.35*(MIN(A/0.90, B/0.90))
		parameters ConWidth_1 = ConWidth_1
	endif
	values "ConWidth_1" range (0, 0.35*(MIN(A/0.90, B/0.90))]
	parameters ConDepth_1 = ConWidth_1
	values "ConLength_1" range [0, ]


	parameters ConPosX_1 = drainPosX
	parameters ConPosY_1 = drainPosY
	parameters ConPosZ_1 = 0


	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 1


	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	if gs_ui_current_page = 4 then
		ac_mep_connectionpage_active = 1
	else
		ac_mep_connectionpage_active = 0
	endif
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
else
	hideparameter "gs_connection", "gs_con_mat", "useSysMat"

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active
endif

call "ui_plumbingfixtures" parameters iObjectType 		= iObjectType,
								gs_ui_current_page = gs_ui_current_page

! -----------------------------------------------------------------------------
! Onorm list Settings
! -----------------------------------------------------------------------------

if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif
end
