if bShowMEP then
	if show_el < EPS then end
	IF gs_detlevel_2D_m < EPS THEN END
	CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el
endif
vk_type_view = 1 : success = LIBRARYGLOBAL ("Libraryflobals-mep", "vk_type_view", vk_type_view)
unID = 1

reqBMinSpace = 0
if MSFront > EPS or MSSide > EPS or MSSide2 > EPS then
	drawindex 10
	call "fa_minimalspace" parameters all MSType = 8, unID = 3000
endif

DrawingNum = 1

! ==========================================================================
! Scale
! ==========================================================================

call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
				returned_parameters gs_detlevel_2D_m

!!! ******************************** 2D

pen gs_cont_pen

hotspot2 0, 0, unID			: unID = unID + 1
hotspot2 -a/2, 0, unID		: unID = unID + 1
hotspot2 a/2, 0, unID		: unID = unID + 1
hotspot2 -a/2, -b, unID		: unID = unID + 1
hotspot2 a/2, -b, unID		: unID = unID + 1

drawindex 10
!!! *** Background Fill
	fill gs_fill_type
	POLY2_B       4, 1+2+4+64,      gs_fill_pen,     gs_back_pen,
		-a/2, 0, 1,
		a/2, 0, 1,
		a/2, -b, 1,
		-a/2, -b, 1

if gs_detlevel_2D_m = 3 then
	POLY2_B       9, 1+4,      gs_fill_pen,     gs_back_pen,
		-a/2+sinkWallThkSide+sinkRad, -sinkWallThkSide, 1,

		a/2-sinkWallThkSide-sinkRad, -sinkWallThkSide, 1,
		a/2-sinkWallThkSide, -sinkWallThkSide-sinkRad, 1001,

		a/2-sinkWallThkSide, -b+sinkWallThkSide+sinkRad, 1,
		a/2-sinkWallThkSide-sinkRad, -b+sinkWallThkSide, 1001,

		-a/2+sinkWallThkSide+sinkRad, -b+sinkWallThkSide, 1,
		-a/2+sinkWallThkSide, -b+sinkWallThkSide+sinkRad, 1001,

		-a/2+sinkWallThkSide, -sinkWallThkSide-sinkRad, 1,
		-a/2+sinkWallThkSide+sinkRad, -sinkWallThkSide, 1001
endif

if gs_detlevel_2D_m = 2 then
	POLY2_B       5, 1+4,      gs_fill_pen,     gs_back_pen,
		-a/2+sinkWallThkSide, -sinkWallThkSide, 1,
		a/2-sinkWallThkSide, -sinkWallThkSide, 1,
		a/2-sinkWallThkSide, -b+sinkWallThkSide, 1,
		-a/2+sinkWallThkSide, -b+sinkWallThkSide, 1,
		-a/2+sinkWallThkSide, -sinkWallThkSide, -1
endif

if gs_detlevel_2D_m = 3 then
	circle2 0, -b/2, escapeDia1/2
endif


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** TAP *** !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if gs_tap_type_m <> 0 and (gs_detlevel_2D_m = 3) then
	drawindex 20
	if bFlipTap then
		add2 A/2, tapDistCent - B/2
	else
		add2 tapDistCent, 0
	endif
	rot2 -90 * bFlipTap

	call "tapType_m" parameters all bShower				= 0,
									bEnableTapEdit		= 0,
									bSinkTap			= 0,
									iTapGroup			= 5

	del 2
endif



! --- start of modifying to Shaft

if bShowMEP then

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left

	if bWallMountedTap then
		if bFlipTap then
			ConEdit_1 = 13
			ConEdit_2 = 13

			corrMEPx = 0
			corrMEPy = tapDistCent - B/2
		else
			ConEdit_1 = 12
			ConEdit_2 = 12

			corrMEPx = tapDistCent
			corrMEPy = 0
		endif
		add2 corrMEPx, corrMEPy
	else
		ConEdit_1 = 12
		ConEdit_2 = 12

		corrMEPx = 0
		corrMEPy = 0
	endif

				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
		ConPosX_3=ConPosX_3-corrMEPx, ConPosY_3=ConPosY_3-corrMEPy, ConLength_3=ConLength_3, ConEdit_3 = 0

	DEL 1

	if bWallmountedTap then del 1

ENDIF

! --- end of modifying to Shaft

END


