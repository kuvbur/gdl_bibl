if bShowMEP then
	if show_el < EPS then end
	IF gs_detlevel_3D_m < EPS THEN END
	CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el
	useSysMat = 1
endif

IF gs_detlevel_3D_m = 0 THEN END
IF gs_detlevel_3D_m = 1 THEN 
	SHADOW OFF
else
	SHADOW ON
endif
resol res
PEN gs_cont_pen


! --- Hotspot Editing ----------------------------------------------------------

unID = 1

hotspot	0, 0, 0, unID, h_top, 1+128								: unID=unID+1
hotspot	0, 0, -1, unID, h_top, 3								: unID=unID+1
hotspot	0, 0, h_top, unID, h_top, 2								: unID=unID+1

addz underSinkHeight
	hotspot	-a/2, 0, 0, unID, sinkHeight, 1+128					: unID=unID+1
	hotspot	-a/2, 0, -1, unID, sinkHeight, 3					: unID=unID+1
	hotspot	-a/2, 0, sinkHeight, unID, sinkHeight, 2			: unID=unID+1

	hotspot	a/2, 0, 0, unID, sinkHeight, 1+128					: unID=unID+1
	hotspot	a/2, 0, -1, unID, sinkHeight, 3						: unID=unID+1
	hotspot	a/2, 0, sinkHeight, unID, sinkHeight, 2				: unID=unID+1

	hotspot	-a/2, -b, 0, unID, sinkHeight, 1+128				: unID=unID+1
	hotspot	-a/2, -b, -1, unID, sinkHeight, 3					: unID=unID+1
	hotspot	-a/2, -b, sinkHeight, unID, sinkHeight, 2			: unID=unID+1

	hotspot	a/2, -b, 0, unID, sinkHeight, 1+128					: unID=unID+1
	hotspot	a/2, -b, -1, unID, sinkHeight, 3					: unID=unID+1
	hotspot	a/2, -b, sinkHeight, unID, sinkHeight, 2			: unID=unID+1
del 1

if iLegType<>1 then
	hotspot	-a/2, 0, 0, unID, underSinkHeight, 1+128			: unID=unID+1
	hotspot	-a/2, 0, -1, unID, underSinkHeight, 3				: unID=unID+1
	hotspot	-a/2, 0, underSinkHeight, unID, underSinkHeight, 2	: unID=unID+1

	hotspot	a/2, 0, 0, unID, underSinkHeight, 1+128				: unID=unID+1
	hotspot	a/2, 0, -1, unID, underSinkHeight, 3				: unID=unID+1
	hotspot	a/2, 0, underSinkHeight, unID, underSinkHeight, 2	: unID=unID+1

	hotspot	-a/2, -b, 0, unID, underSinkHeight, 1+128			: unID=unID+1
	hotspot	-a/2, -b, -1, unID, underSinkHeight, 3				: unID=unID+1
	hotspot	-a/2, -b, underSinkHeight, unID, underSinkHeight, 2	: unID=unID+1

	hotspot	a/2, -b, 0, unID, underSinkHeight, 1+128			: unID=unID+1
	hotspot	a/2, -b, -1, unID, underSinkHeight, 3				: unID=unID+1
	hotspot	a/2, -b, underSinkHeight, unID, underSinkHeight, 2	: unID=unID+1
endif

addz (h_top - sinkHeight) * (iLegType = 1)
	hotspot 0, 0, 0, unID										: unID=unID+1
	hotspot -a/2, 0, 0, unID									: unID=unID+1
	hotspot a/2, 0, 0, unID										: unID=unID+1
	hotspot -a/2, -b, 0, unID									: unID=unID+1
	hotspot a/2, -b, 0, unID									: unID=unID+1
del 1


! --- start of modifying to Shaft ----------------------------------------------

! --- CONNECTIONS --- !

if bShowMEP then

	IF useSysMat THEN
		gs_sink_mat		= sMat
		gs_drain_mat	= sMat
		gs_tap_mat		= sMat
		gs_con_mat		= sMat
	ENDIF

	if bWallMountedTap then
		if bFlipTap then
			ConEdit_1 = 13
			ConEdit_2 = 13

			corrMEPx = 0
			corrMEPy = tapDistCent - B/2
			corrMEPz = zTapPos
		else
			ConEdit_1 = 12
			ConEdit_2 = 12

			corrMEPx = tapDistCent
			corrMEPy = 0
			corrMEPz = zTapPos
		endif
		add corrMEPx ,corrMEPy,corrMEPz
	else
		ConEdit_1 = 16
		ConEdit_2 = 16

		corrMEPx = 0
		corrMEPy = 0
		corrMEPz = 0
	endif

	addz (h_top-sinkHeight)*(gs_tap_type_m=0)

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
		ConPosX_3=ConPosX_3-corrMEPx, ConPosY_3=ConPosY_3-corrMEPy, ConPosZ_3=ConPosZ_3-corrMEPz, ConLength_3=ConLength_3, ConEdit_3 = 1
	del 1

	if bWallmountedTap then del 1

ENDIF

! --- end of modifying to Shaft

! --- Sink ---------------------------------------------------------------------

group "sink cut"
	addz sinkHeight+sinkWallThkBottom
	mulz -1
	hprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
		9, sinkHeight, 0, sinkRad-0.01, 1,

		-a/2+sinkWallThkSide+sinkRad, -sinkWallThkSide, 79,

		a/2-sinkWallThkSide-sinkRad, -sinkWallThkSide, 79,
		a/2-sinkWallThkSide, -sinkWallThkSide-sinkRad, 1079,

		a/2-sinkWallThkSide, -b+sinkWallThkSide+sinkRad, 79,
		a/2-sinkWallThkSide-sinkRad, -b+sinkWallThkSide, 1079,

		-a/2+sinkWallThkSide+sinkRad, -b+sinkWallThkSide, 79,
		-a/2+sinkWallThkSide, -b+sinkWallThkSide+sinkRad, 1079,

		-a/2+sinkWallThkSide, -sinkWallThkSide-sinkRad, 79,
		-a/2+sinkWallThkSide+sinkRad, -sinkWallThkSide, 1079
	del 2
endgroup

group "sink"
	fprism_ gs_sink_mat, gs_sink_mat, gs_sink_mat, gs_sink_mat,
		5, sinkHeight, 45, rimWidth/2,
		-a/2, 0, 15,
		a/2, 0, 15,
		a/2, -b, 15,
		-a/2, -b, 15,
		-a/2, 0, -1
endgroup

group "rim"
	material gs_sink_mat
	prism_ 10, sinkHeight-rimHeight,
		-a/2, 0, 15,
		a/2, 0, 15,
		a/2, -b, 15,
		-a/2, -b, 15,
		-a/2, 0, -1,

		-a/2+rimWidth, -rimWidth, 15,
		a/2-rimWidth, -rimWidth, 15,
		a/2-rimWidth, -b+rimWidth, 15,
		-a/2+rimWidth, -b+rimWidth, 15,
		-a/2+rimWidth, -rimWidth, -1
endgroup

group "escape cut"
	if gs_detlevel_3D_m=2 then
		material gs_sink_mat
		add 0, -b/2, sinkWallThkBottom-escapeHeight
		cone escapeHeight, escapeDia1/2, (escapeDia2)/2, 90, 90
		del 1
	endif
endgroup

addz underSinkHeight
if brim then
	placegroup subgroup (subgroup (subgroup ("sink", "rim"), "sink cut"), "escape cut")
else
	placegroup subgroup (subgroup ("sink", "sink cut"), "escape cut")
endif

del 1

! --- Legs ---------------------------------------------------------------------

if iLegType=2 then
	!Footing

	material matLeg
	prism_ 10, underSinkHeight,
		-a/2+rimWidth*brim, -rimWidth*brim, 15,
		a/2-rimWidth*brim, -rimWidth*brim, 15,
		a/2-rimWidth*brim, -b+rimWidth*brim, 15,
		-a/2+rimWidth*brim, -b+rimWidth*brim, 15,
		-a/2+rimWidth*brim, -rimWidth*brim, -1,

		-a/2+rimWidth*brim+footingWidth, -rimWidth*brim-footingWidth, 15,
		a/2-rimWidth*brim-footingWidth, -rimWidth*brim-footingWidth, 15,
		a/2-rimWidth*brim-footingWidth, -b+rimWidth*brim+footingWidth, 15,
		-a/2+rimWidth*brim+footingWidth, -b+rimWidth*brim+footingWidth, 15,
		-a/2+rimWidth*brim+footingWidth, -rimWidth*brim-footingWidth, -1
endif

if iLegType=3 then
	!Leg
	material matLeg

	group "vert leg"
		for i=1 to 2
			if gs_detlevel_3D_m=1 then
				addz 0
				cylind underSinkHeight, legDia4/2
				del 1
			endif
			if gs_detlevel_3D_m=2 then
				cylind legHeight1, legDia1/2
				addz legHeight1
				cylind legHeight2, legDia2/2
				del 1
				addz legHeight1+legHeight2
				if legHeight3 > EPS then
					cylind legHeight3, legDia3/2
				endif
				del 1
				addz legHeight1+legHeight2+legHeight3
				cylind legHeight4, legDia4/2
				del 1
			endif
		addy -b+legPos*2+rimWidth*brim*2
		next i
		del 2
	endgroup

	group "hor leg"
		if gs_detlevel_3D_m=2 and (legHeight3+legHeight2+legHeight1)-legBandDia/2-legBandElev>eps then
			addz legBandElev
			rotx 90
			cylind b-legPos*2-rimWidth*brim*2, legBandDia/2
			del 2
		endif
	endgroup

	add -a/2+legPos+rimWidth*brim, -legPos-rimWidth*brim, 0
	for ii=1 to 2
		placegroup subgroup ("hor leg", "vert leg")
		placegroup "vert leg"
		addx a-legPos*2-rimWidth*brim*2
	next ii
	del 2
	del 1
endif

if gs_detlevel_3D_m=2 then
	!Escape
	material gs_drain_mat
	add 0, -b/2, sinkWallThkBottom-escapeHeight+underSinkHeight
	cylind escapeHeight, escapeDia1/2
	del 1
endif


! --- Taps ---------------------------------------------------------------------

if gs_detlevel_3D_m >1 and gs_tap_type_m <> 0 then
	if bFlipTap then
		add A/2, tapDistCent - B/2, tapHeight
	else
		add tapDistCent, 0, tapHeight
	endif
	rotz -90 * bFlipTap

	call "tapType_m" parameters all bShower				= 0,
									bEnableTapEdit		= 0,
									bSinkTap			= 0,
									useOldSimpleTaps	= 0,
									iTapGroup			= 5

	del 2
endif
