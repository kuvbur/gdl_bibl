
call "FM_types" parameters all

dim st_detail3D[3]
	st_detail3D[1] = `Детальный`
	st_detail3D[2] = `Простой`
	st_detail3D[3] = `Откл.`

values "gs_detlevel_3D" st_detail3D		!`Detailed`,`Simple`,`Off`
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 then
	gs_detlevel_3D_m = 2	! Detailed
	if gs_detlevel_3D = st_detail3D[3] then gs_detlevel_3D_m = 0
	if gs_detlevel_3D = st_detail3D[2] then gs_detlevel_3D_m = 1
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = st_detail3D[1]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = st_detail3D[3]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = st_detail3D[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3D_m = 1 then hideparameter "gs_resol"
if gs_detlevel_3D_m = 0 then hideparameter "gs_resol", "gs_shadow"

values "legType" stLegType
values "iLegType" 1, 2, 3
if GLOB_MODPAR_NAME = "legType" then
	if legType = stLegType[1]	then iLegType = 1
	if legType = stLegType[2]	then iLegType = 2
	if legType = stLegType[3]	then iLegType = 3
	parameters iLegType = iLegType
else
	legType = stLegType[max(iLegType,1)]
	parameters legType = legType
endif

values "gs_symbol_type" stSymbolTypes	! Type 1, Type 2, Type 3
if GLOB_MODPAR_NAME = "gs_symbol_type" then
	if iSymbolType = 1 then
		A = 0.460
		B = 0.373
	endif
	if iSymbolType = 2 then
		A = 0.460
		B = 0.330
	endif
	parameters A = A, B = B
endif

if gs_symbol_type_enable and iSymbolType = 1 then
	hideparameter "gs_detlevel_2D"
	lock "gs_detlevel_2D"
endif
if not(gs_symbol_type_enable) then
	hideparameter "gs_symbol_type"
	lock "gs_symbol_type"
endif

call "2dDetailLevel" parameters	all enable2dDetLevels	= 2 + 4 + 8 + 16

call "tapType_m" parameters	all	enableTapGroups			= 0,
								enableSingleLevelTaps	= 0,
								enableTwoHandleTaps		= 0,
								enableTraditionalTaps	= 0,
								enableSensorFaucets		= 0,
								enableWallMountedTaps	= 0,
								enableCustomTaps		= 2 + 2**2 + 2**23 + 2**24 + 2**25,
								bEnableTapEdit			= 0,
								bShower 				= 0,
								enableTapSymbols		= 2 + 4 + 8 + 32

IF gs_tap_type_m=0 THEN
	LOCK "tapHeight", "tapDistCent", "bFlipTap"
	HIDEPARAMETER "tapHeight", "tapDistCent", "bFlipTap"
ELSE
	IF (gs_detlevel_2D_m < 3) THEN
		LOCK "TapSymbol"
		HIDEPARAMETER "TapSymbol"
	ENDIF
ENDIF

if gs_detlevel_3D_m = 2 and gs_tap_type_m <> -1 then
	tapLimit = 0.109 * (gs_tap_type_m = 21) + 0.1365 * (gs_tap_type_m = 22) + 0.1515 * (gs_tap_type_m = 23)
else
	tapLimit = 0.1
endif

values "tapDistCent" range [tapLimit - (A * not(bFlipTap) + B * bFlipTap)/2, (A * not(bFlipTap) + B * bFlipTap)/2 - tapLimit]

values "tapHeight" range[ZZYZX + 0.1,]

! --- Limitation ---

if gs_cont_pen=0 then parameters gs_cont_pen=75
if gs_fill_pen=0 then parameters gs_fill_pen=19
VALUES "gs_resol" RANGE [4,)

values "A" range((sinkWallThkSide+sinkRad)*2, ]
values "B" range((sinkWallThkSide+sinkRad)*2, ]
values "sinkHeight" range(sinkWallThkBottom+sinkRad+rimWidth/2, ]
if iLegType=1 then
	values "ZZYZX" range(sinkWallThkBottom+sinkRad, )
else
	values "underSinkHeight" range [0, ]
	values "ZZYZX" range[sinkHeight, )
endif


if iLegType = 1 then
	if GLOB_MODPAR_NAME = "ZZYZX" then
		sinkHeight = ZZYZX
		parameters sinkHeight = sinkHeight
	else
		ZZYZX = sinkHeight
		parameters ZZYZX = ZZYZX
	endif
endif
	if GLOB_MODPAR_NAME = "ZZYZX" then
		h_top = ZZYZX
		parameters h_top = h_top
	else
		ZZYZX = h_top
		parameters ZZYZX = ZZYZX
	endif
	if GLOB_MODPAR_NAME = "h_top" or GLOB_MODPAR_NAME = "ZZYZX" then
		parameters underSinkHeight = h_top-sinkHeight
	else
		h_top = sinkHeight+underSinkHeight
		parameters h_top = h_top
		parameters ZZYZX = h_top
	endif

if iLegType = 1 then
	lock "matLeg"
	hideparameter "underSinkHeight", "underSinkHeight", "matLeg"
endif

! --- Minimal Space ---

if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif
if MSSide < 0 then
	MSSide = 0
	parameters MSSide = MSSide
endif
if MSSide2 < 0 then
	MSSide2 = 0
	parameters MSSide2 = MSSide2
endif



! --- start of modify to Shaft


IF isMEPEnabled THEN

	IF NOT(ConStatus_1) AND NOT(ConStatus_2) AND NOT(ConStatus_3) THEN
		LOCK "gs_con_mat"
		HIDEPARAMETER "gs_con_mat"
	ENDIF

	DIM shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	PARAMETERS MEP_NumberConnections = 3


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Подключение Горячей Воды`

	IF ConWidth_1 < 0.001 THEN
		ConWidth_1 = 0.001
		PARAMETERS ConWidth_1 = ConWidth_1
	ENDIF
	IF ConWidth_1 > sinkHeight THEN
		ConWidth_1 = sinkHeight
		PARAMETERS ConWidth_1 = ConWidth_1
	ENDIF
	VALUES "ConWidth_1" RANGE (0, sinkHeight]
	PARAMETERS ConDepth_1 = ConWidth_1
	VALUES "ConLength_1" RANGE [0, ]

	if bWallMountedTap then
		pos1st = -posTap - widthTap + ConWidth_1/2
		pos2nd = -posTap - ConWidth_1/2
		pos3rd = +posTap + ConWidth_1/2
		pos4th = +posTap + widthTap - ConWidth_1/2

		if bFlipTap then
			ConDVecX = 1
			ConDVecY = 0
			ConDVecZ = 0

			ConWVecX = 0
			ConWVecY = 1
			ConWVecZ = 0

			! --- Y Range

			values "ConPosY_1"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosY_2" then
				if ConPosY_2 < 0 & ConPosY_1 < 0 then
					ConPosY_1 = posTap + widthTap/2
					parameters 	ConPosY_1 = ConPosY_1
				endif
				if ConPosY_2 > 0 & ConPosY_1 > 0 then
					ConPosY_1 = -(posTap + widthTap/2)
					parameters 	ConPosY_1 = ConPosY_1
				endif
			endif
			parameters ConPosX_1 = B/2
		else
			ConDVecX = 0
			ConDVecY = 1
			ConDVecZ = 0

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			! --- X Range

			values "ConPosX_1"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosX_2" then
				if ConPosX_2 < 0 & ConPosX_1 < 0 then
					ConPosX_1 = posTap + widthTap/2
					parameters 	ConPosX_1 = ConPosX_1
				endif
				if ConPosX_2 > 0 & ConPosX_1 > 0 then
					ConPosX_1 = -(posTap + widthTap/2)
					parameters 	ConPosX_1 = ConPosX_1
				endif
			endif

			parameters ConPosY_1 = 0
		endif
		ConPosZ_1 = tapHeight
		parameters ConPosZ_1 = ConPosZ_1
	else
		ConDVecX = 0
		ConDVecY = 1
		ConDVecZ = 0

		ConWVecX = -1
		ConWVecY = 0
		ConWVecZ = 0

		VALUES "ConPosX_1" RANGE [-A/2+ConWidth_1/2, A/2-ConWidth_1/2]
		PARAMETERS ConPosY_1 = 0
		VALUES "ConPosZ_1" RANGE [,sinkHeight-ConWidth_1]
	endif


	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1+(ZZYZX-sinkHeight)*(gs_tap_type_m=0),
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 1


	PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	PARAMETERS MEP_ConInfo[ConID][2] = ConName
	PARAMETERS MEP_ConInfo[ConID][3] = "0"


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Подключение Холодной Воды`

	IF ConWidth_2 < 0.001 THEN
		ConWidth_2 = 0.001
		PARAMETERS ConWidth_2 = ConWidth_2
	ENDIF
	IF ConWidth_2 > sinkHeight THEN
		ConWidth_2 = sinkHeight
		PARAMETERS ConWidth_2 = ConWidth_2
	ENDIF
	VALUES "ConWidth_2" RANGE (0, sinkHeight]
	PARAMETERS ConDepth_2 = ConWidth_2
	VALUES "ConLength_2" RANGE [0, ]


	if bWallmountedTap then
		pos1st = -posTap - widthTap + ConWidth_2/2
		pos2nd = -posTap - ConWidth_2/2
		pos3rd = +posTap + ConWidth_2/2
		pos4th = +posTap + widthTap - ConWidth_2/2

		if bFlipTap then
			ConDVecX = 1
			ConDVecY = 0
			ConDVecZ = 0

			ConWVecX = 0
			ConWVecY = 1
			ConWVecZ = 0

			! --- Y Range

			values "ConPosY_2"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosY_1" then
				if ConPosY_2 < 0 & ConPosY_1 < 0 then
					ConPosY_2 = posTap + widthTap/2
					parameters 	ConPosY_2 = ConPosY_2
				endif
				if ConPosY_2 > 0 & ConPosY_1 > 0 then
					ConPosY_2 = -(posTap + widthTap/2)
					parameters 	ConPosY_2 = ConPosY_2
				endif
			endif

			PARAMETERS ConPosX_2 = B/2
		else
			ConDVecX = 0
			ConDVecY = 1
			ConDVecZ = 0

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			! --- X Range

			values "ConPosX_2"	range [pos1st, pos2nd],
								range [pos3rd, pos4th]

			! --- Change Connection Side
			if GLOB_MODPAR_NAME = "ConPosX_1" then
				if ConPosX_2 < 0 & ConPosX_1 < 0 then
					ConPosX_2 = posTap + widthTap/2
					parameters 	ConPosX_2 = ConPosX_2
				endif
				if ConPosX_2 > 0 & ConPosX_1 > 0 then
					ConPosX_2 = -(posTap + widthTap/2)
					parameters 	ConPosX_2 = ConPosX_2
				endif
			endif

			parameters ConPosY_2 = 0
		endif
		ConPosZ_2 = tapHeight
		parameters ConPosZ_2 = ConPosZ_2
	else
		ConDVecX = 0
		ConDVecY = 1
		ConDVecZ = 0

		ConWVecX = -1
		ConWVecY = 0
		ConWVecZ = 0

		VALUES "ConPosX_2" RANGE [-A/2+ConWidth_2/2, A/2-ConWidth_2/2]
		PARAMETERS ConPosY_2 = 0
		VALUES "ConPosZ_2" RANGE [,sinkHeight-ConWidth_2]
	endif


	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2,
		ConPosY = ConPosY_2,
		ConPosZ = ConPosZ_2+(ZZYZX-sinkHeight)*(gs_tap_type_m=0),
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 0

	PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
	PARAMETERS MEP_ConInfo[ConID][2] = ConName
	PARAMETERS MEP_ConInfo[ConID][3] = "0"


					! --- 3rd CONNECTION --- !

	ConID = 3
	ConName = `Подключение Канализации`

	ConDVecX = 0
	ConDVecY = 0
	ConDVecZ = -1

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	IF ConWidth_3 < 0.001 THEN
		ConWidth_3 = 0.001
		PARAMETERS ConWidth_3 = ConWidth_3
	ENDIF
	IF ConWidth_3 > MIN(A/3, B/3) THEN
		ConWidth_3 = MIN(A/3, B/3)
		PARAMETERS ConWidth_3 = ConWidth_3
	ENDIF
	VALUES "ConWidth_3" RANGE (0, MIN(A/3, B/3)]
	PARAMETERS ConDepth_3 = ConWidth_3
	VALUES "ConLength_3" RANGE [0, ]


	PARAMETERS ConPosX_3 = 0
	PARAMETERS ConPosY_3 = -B/2
	if gs_tap_type_m=0 then
		ConPosZ_3 = 0
	else
		ConPosZ_3 = underSinkHeight
	endif
	PARAMETERS ConPosZ_3 = ConPosZ_3


	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_3,
		ConPosY = ConPosY_3,
		ConPosZ = ConPosZ_3+(ZZYZX-sinkHeight)*(gs_tap_type_m=0),
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_3,
		ConNominalDepth = ConNominalDepth_3,
		ConWidth = ConWidth_3,
		ConDepth = ConDepth_3,
		ConLength = ConLength_3,
		ConWallThickness = ConWallThickness_3,
		ConConnectorWidth = ConConnectorWidth_3,
		ConConnectorDepth = ConConnectorDepth_3,
		ConConnectorWidth2 = ConConnectorWidth2_3,
		ConToolType = ConToolType_3,
		ConStatus = ConStatus_3,
		ConType_3 = ConType_3,
		ConConnectorType_3 = ConConnectorType_3,
		ConSystem_3 = ConSystem_3,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 0

	PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_3, 1, 0)
	PARAMETERS MEP_ConInfo[ConID][2] = ConName
	PARAMETERS MEP_ConInfo[ConID][3] = "0"

	IF gs_ui_current_page = 4 THEN
		ac_mep_connectionpage_active = 1
	ELSE
		ac_mep_connectionpage_active = 0
	ENDIF
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active

	IF SetProgram THEN
		SetProgram = 0
		PARAMETERS SetProgram = SetProgram
	ENDIF
ELSE
	HIDEPARAMETER "gs_connection", "gs_con_mat", "useSysMat"

	ac_mep_connectionpage_active = 0
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active
ENDIF

call "ui_plumbingfixtures" parameters iObjectType 		= 90,
								gs_ui_current_page = gs_ui_current_page

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif

parameters ac_bottomlevel = h_top - ZZYZX
parameters ac_toplevel = h_top

END
