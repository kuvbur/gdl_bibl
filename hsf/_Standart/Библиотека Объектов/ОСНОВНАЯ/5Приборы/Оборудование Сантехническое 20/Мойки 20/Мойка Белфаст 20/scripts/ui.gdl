
UI_DIALOG `Параметры Мойки`
UI_CURRENT_PAGE gs_ui_current_page




IF isMEPEnabled THEN
	DIM ui_matpar_names[3][2]
		ui_matpar_names[1][1] = `Мойка`
		ui_matpar_names[2][1] = `Смеситель`
		ui_matpar_names[3][1] = `Соединение`

		ui_matpar_names[1][2] = "gs_sink_mat"
		ui_matpar_names[2][2] = "gs_tap_mat"
		ui_matpar_names[3][2] = "gs_con_mat"

	DIM ui_matpar_locked[3]
		ui_matpar_locked[1] = 0
		ui_matpar_locked[2] = 0
		ui_matpar_locked[3] = (NOT(ConStatus_1) AND NOT(ConStatus_2) AND NOT(ConStatus_3))
ELSE
	DIM ui_matpar_names[2][2]
		ui_matpar_names[1][1] = `Мойка`
		ui_matpar_names[2][1] = `Смеситель`

		ui_matpar_names[1][2] = "gs_sink_mat"
		ui_matpar_names[2][2] = "gs_tap_mat"

	DIM ui_matpar_locked[2]
		ui_matpar_locked[1] = 0
		ui_matpar_locked[2] = 0
ENDIF

IF gs_tap_type_m=0 THEN ui_matpar_locked[2] = 1


iPageIDRoot 			=-1
iPageIDSink				= 1
iPageIDRepAndSurface	= 2
iPageIDConnections		= 3

pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

	UI_PAGE iPageIDSink, iPageIDRoot, `Стиль Мойки и Смесителя`, "uiTab_Style_flat(1)"
		gosub "sinkPage"


	UI_PAGE iPageIDRepAndSurface, iPageIDRoot, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
		gosub "repAndSurfacePage"


if isMEPEnabled then
	UI_PAGE iPageIDConnections, iPageIDRoot, `Соединения`, "uiTab_Connections_flat(1)"
		gosub "connectionsPage"
endif

! ==============================================================================
END
! ==============================================================================


! ==============================================================================
!SUBROUTINES
! ==============================================================================

! ------------------------------------------------------------------------------
"sinkPage":
! ------------------------------------------------------------------------------

	ui_separator 222, pageStartY + 9, 222, pageStartY + 217

	!!!Sink
	xLeft = 0
	x1 = 25 + xLeft
	x2 = 90
	xEnd = 190 + xLeft
	y = pageStartY + 7

	ui_outfield	`Верх Мойки`, x1, y, x2, 13
	ui_infield	"topHeight", x1 + x2 + 1, y - 2, xEnd - x1 - x2, 18
	y = y + 50

	ui_outfield`Стиль Мойки`, x1, y, x2, 18
	y = y + 25

	x1 = 50
	ui_infield{3} "gs_sink_type", x1, y, 140, 95,
				2, "ui_belfastsinktyp(1)",
				7, 1,
				84, 118, 80, 50,
				1, stSinkType[1], stSinkType[1],
				2, stSinkType[2], stSinkType[2],
				3, stSinkType[3], stSinkType[3],
				4, stSinkType[4], stSinkType[4],
				5, stSinkType[5], stSinkType[5],
				6, stSinkType[6], stSinkType[6],
				7, stSinkType[7], stSinkType[7]

	!!!Tap
	xRight = 212
	x1 = 25 + xRight
	x2 = 90
	xEnd = 190 + xRight
	y = pageStartY + 7

	ui_outfield	`Высота Крана`, x1, y, x2, 13
	ui_infield	"count_th", x1 + x2 + 1, y - 2, xEnd - x1 - x2, 18
	y = y + 50

	ui_outfield`Стиль Смесителя`, x1, y, x2, 18
	y = y + 25

	x1 = 50 + xRight
	ui_infield{3} "gs_tap_type", x1, y, 140, 95,
		2, "ui_taptypes_2(4)", 35, 7,
		84, 118, 60, 90,
		7, `Стиль 1`, `Стиль 1`,
		2, `Стиль 2`, `Стиль 2`,
		3, `Стиль 3`, `Стиль 3`,
		4, `Стиль 4`, `Стиль 4`,
		5, `Стиль 5`, `Стиль 5`,
		9, `Стиль 6`, `Стиль 6`,
		12, `Стиль 7`, `Стиль 7`,
		32, `Простой`, `Простой`,
		34, `Нет`, `Нет`

return

! ------------------------------------------------------------------------------
"repAndSurfacePage":
! ------------------------------------------------------------------------------

	CALL "ui_visualizationtab" PARAMETERS ALL 	bEnableCenterlinePars = 0,
												bEnable3dDetail = 1,
												bEnableUseSystemMat = 0,
												ui_matpar_names = ui_matpar_names,
												ui_matpar_locked = ui_matpar_locked,
												ui_3dviewpar_rowspace = 2,
												bEnable2dDetail = 1,
												iObjectType = 91


return

! ------------------------------------------------------------------------------
"connectionsPage":
! ------------------------------------------------------------------------------

	DIM ui_con_type[5]
		ui_con_type[1] = 0
		ui_con_type[2] = 0
		ui_con_type[3] = 0
		ui_con_type[4] = 0
		ui_con_type[5] = 0

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS ALL ui_con_type = ui_con_type,
												ui_system_var_pos = 2

return

