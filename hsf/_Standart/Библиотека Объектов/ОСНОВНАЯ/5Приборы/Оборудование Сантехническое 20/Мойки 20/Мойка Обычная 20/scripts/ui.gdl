
UI_DIALOG `Параметры Мойки`
UI_CURRENT_PAGE gs_ui_current_page




IF isMEPEnabled THEN
	DIM ui_matpar_names[3][2]
		ui_matpar_names[1][1] = `Мойка`
		ui_matpar_names[2][1] = `Смеситель`
		ui_matpar_names[3][1] = `Соединение`

		ui_matpar_names[1][2] = "gs_sink_mat"
		ui_matpar_names[2][2] = "gs_tap_mat"
		ui_matpar_names[3][2] = "gs_con_mat"

	DIM ui_matpar_locked[3]
		ui_matpar_locked[1] = 0
		ui_matpar_locked[2] = 0
		ui_matpar_locked[3] = (NOT(ConStatus_1) AND NOT(ConStatus_2) AND NOT(ConStatus_3) AND NOT(ConStatus_4) AND NOT(ConStatus_5))
ELSE
	DIM ui_matpar_names[2][2]
		ui_matpar_names[1][1] = `Мойка`
		ui_matpar_names[2][1] = `Смеситель`

		ui_matpar_names[1][2] = "gs_sink_mat"
		ui_matpar_names[2][2] = "gs_tap_mat"

	DIM ui_matpar_locked[2]
		ui_matpar_locked[1] = 0
		ui_matpar_locked[2] = 0
ENDIF

IF gs_tap_type_m=0 THEN ui_matpar_locked[2] = 1


iPageIDRoot 			=-1
iPageIDSink				= 1
iPageIDRepAndSurface	= 2
iPageIDConnections		= 3

pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

	UI_PAGE iPageIDSink, iPageIDRoot, `Стиль Мойки и Смесителя`, "uiTab_Style_flat(1)"
		gosub "sinkPage"


	UI_PAGE iPageIDRepAndSurface, iPageIDRoot, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
		gosub "repAndSurfacePage"


if isMEPEnabled then
	UI_PAGE iPageIDConnections, iPageIDRoot, `Соединения`, "uiTab_Connections_flat(1)"
		gosub "connectionsPage"
endif

! ==============================================================================
END
! ==============================================================================


! ==============================================================================
!SUBROUTINES
! ==============================================================================

! ------------------------------------------------------------------------------
"sinkPage":
! ------------------------------------------------------------------------------

pos_x=5    					! start Position X Coordinate
pos_y=pageStartY - 1   		! start Position Y Coordinate
cell1_x=272					! width of 1.cell
cell1_y=227					! height of 1.cell
x1=3						! distance between cells
cell2_x=158					! width of 1.cell
cell2_y=227					! height of 1.cell

ui_infield{3} "gs_sink_type",pos_x,pos_y,cell1_x,cell1_y,
	1,"ui_sinktyp(13)",15,3,
	83,74,80,50,
	1,st_sinktypes[1],st_sinktypes[1],
	2,st_sinktypes[2],st_sinktypes[2],
	3,st_sinktypes[3],st_sinktypes[3],
	4,st_sinktypes[4],st_sinktypes[4],
	5,st_sinktypes[5],st_sinktypes[5],
	6,st_sinktypes[6],st_sinktypes[6],
	7,st_sinktypes[7],st_sinktypes[7],
	8,st_sinktypes[8],st_sinktypes[8],
	9,st_sinktypes[9],st_sinktypes[9],
	10,st_sinktypes[10],st_sinktypes[10],
	11,st_sinktypes[11],st_sinktypes[11],
	12,st_sinktypes[12],st_sinktypes[12],
	13,st_sinktypes[13],st_sinktypes[13],
	14,st_sinktypes[14],st_sinktypes[14]

	ui_infield{3} "gs_tap_type",pos_x+cell1_x+x1,pos_y,cell2_x,cell2_y,
		1, "ui_taptypes_2(4)", 35, 7,
		68, 110, 60, 90,
		7, `Стиль 1`, `Стиль 1`,
		2, `Стиль 2`, `Стиль 2`,
		3, `Стиль 3`, `Стиль 3`,
		4, `Стиль 4`, `Стиль 4`,
		5, `Стиль 5`, `Стиль 5`,
		9, `Стиль 6`, `Стиль 6`,
		12, `Стиль 7`, `Стиль 7`,
		32, `Простой`, `Простой`,
		34, `Нет`, `Нет`

return

! ------------------------------------------------------------------------------
"repAndSurfacePage":
! ------------------------------------------------------------------------------

CALL "ui_visualizationtab" PARAMETERS ALL 	bEnableCenterlinePars = 0,
											bEnable3dDetail = 1,
											bEnableUseSystemMat = 0,
											ui_matpar_names = ui_matpar_names,
											ui_matpar_locked = ui_matpar_locked,
											ui_3dviewpar_rowspace = 2,
											bEnable2dDetail = 1,
											iObjectType = 93


return

! ------------------------------------------------------------------------------
"connectionsPage":
! ------------------------------------------------------------------------------

	DIM ui_con_type[4]
		ui_con_type[1] = 0
		ui_con_type[2] = 0
		ui_con_type[3] = 0
		ui_con_type[4] = 0

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS ALL ui_con_type = ui_con_type,
												ui_system_var_pos = 2

return

