
! --- Delete when fixing ERR122676! ---
hideparameter "gs_pipe_mat"
! -------------------------------------

call "FM_types" parameters all

values "partitionType" `Кабина`, `Писсуарная Перегородка`, `Объемная Перегородка`
values "iPartitionType" 1, 2, 3
if GLOB_MODPAR_NAME = "partitionType" then
	iPartitionType = PARTITION_TYPE_STALL
	if partitionType = `Писсуарная Перегородка`	then iPartitionType = PARTITION_TYPE_URINAL_SCREEN
	if partitionType = `Объемная Перегородка`	then iPartitionType = PARTITION_TYPE_CHINA_DIVISION
	parameters iPartitionType = iPartitionType
else
	partitionType = `Кабина`
	if iPartitionType = PARTITION_TYPE_URINAL_SCREEN then partitionType = `Писсуарная Перегородка`
	if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then partitionType = `Объемная Перегородка`
	parameters partitionType = partitionType
endif

if iPartitionType = PARTITION_TYPE_STALL then
	lock "screenType", "screenLength", "screenEndLength"
	hideparameter "screenType", "screenLength", "screenEndLength"
endif
if iPartitionType = PARTITION_TYPE_URINAL_SCREEN or iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	hideparameter "stallType", "bSwingLine", "penSwing", "lineTypeSwing", "gs_door_title", "latchType", "doorpullType",
					"openingAngle2d", "openingAngle3d", "matDoor", "pen2dDoor", "lineType2dDoor", "depthStall",
					"ms_openingSDepth", "ms_openingSWidth1", "ms_openingSWidth2",
					"ms_openingSDepth_in", "ms_openingSWidth1_in", "ms_openingSWidth2_in"

	lock "stallType", "bSwingLine", "penSwing", "lineTypeSwing", "gs_door_title",  "latchType", "doorpullType",
					"openingAngle2d", "openingAngle3d", "matDoor", "pen2dDoor", "lineType2dDoor", "depthStall",
					"ms_openingSDepth", "ms_openingSWidth1", "ms_openingSWidth2",
					"ms_openingSDepth_in", "ms_openingSWidth1_in", "ms_openingSWidth2_in"
endif


if iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
	if iScreenType = SCREEN_TYPE_WALL_HUNG then
		lock "matPanel"
		hideparameter "bottomHeightPanel", "heightPanel", "screenEndLength", "matPanel"
	endif
	hideparameter "widthHingeSidePanel", "widthLockSidePanel"
	hideparameter "gs_topBracing"
endif

if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	hideparameter "depthStall", "screenLength", "screenEndLength", "bottomHeightPanel", "heightPanel", "heightTopPanel",
					"thicknessPanel", "widthHingeSidePanel", "widthLockSidePanel"
	hideparameter "bottomEndPanel", "topEndPanel"
	hideparameter "gs_topBracing"
endif


if not(bSwingLine) then
	hideparameter "lineTypeSwing", "penSwing"
	lock "lineTypeSwing", "penSwing"
endif

if not(bShowB) then
	hideparameter "lineTypeTopBracing", "penTopBracing"
	lock "lineTypeTopBracing", "penTopBracing"
endif

values "stallType" `Верхняя Фиксация`, `Напольное`, `Потолочное`
values "iStallType" 1, 2, 3

if GLOB_MODPAR_NAME = "stallType" then
	iStallType = STALL_TYPE_OVERHEAD_BRACED
	if stallType = `Напольное`		then iStallType = STALL_TYPE_FLOOR_MOUNTED
	if stallType = `Потолочное`	then iStallType = STALL_TYPE_CEILING_MOUNTED
	parameters iStallType = iStallType
else
	stallType = `Верхняя Фиксация`
	if iStallType = STALL_TYPE_FLOOR_MOUNTED then stallType = `Напольное`
	if iStallType = STALL_TYPE_CEILING_MOUNTED then stallType = `Потолочное`
	parameters stallType = stallType
endif

lock "urinalWidth", "dividerWidth", "dividerHeight", "dividerDepth"
if iPartitionType = PARTITION_TYPE_STALL or iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
	lock "dividerElev"
endif

if not (bTopBracing) then
	lock "bShowB", "lineTypeTopBracing", "penTopBracing", "matTopBracing"
	hideparameter "bShowB", "lineTypeTopBracing", "penTopBracing", "matTopBracing"
endif

if not (bCeilingMountPanel) then
	lock "matTopPanel", "heightTopPanel"
	hideparameter "matTopPanel", "heightTopPanel"
endif

if not (bFooter) then
	lock "matFooter"
	hideparameter "matFooter"
endif

if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	lock "matSidePanel", "matPanel", "openingDirection", "screenType"
	hideparameter "matSidePanel", "matPanel", "openingDirection", "screenType"
else
	lock "matDivision"
	hideparameter "matDivision"
endif

if iPartitionType = PARTITION_TYPE_STALL or iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
	values "screenType" `Настенное`, `Напольное`
	values "iScreenType" 2, 1
endif

if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	values "screenType" `Настенное`
	values "iScreenType" 2
endif

if GLOB_MODPAR_NAME = "screenType" then
	iScreenType = SCREEN_TYPE_FLOOR_MOUNTED
	if screenType = `Настенное`		then iScreenType = SCREEN_TYPE_WALL_HUNG
	parameters iScreenType = iScreenType
else
	screenType = `Напольное`
	if iScreenType = SCREEN_TYPE_WALL_HUNG then screenType = `Настенное`
	parameters screenType = screenType
endif

values "nStalls" range [1, )

if iFixtureType = FIXTURE_TYPE_URINAL then
	values "res" range [3,64]
else
	values "res" range [3,)
endif


! --- WC ---------------------------------------------------

values "strToiletMounting" `Напольное`, `Настенное`
values "iToiletMounting" 1, 2

if GLOB_MODPAR_NAME = "strToiletMounting" then
	iToiletMounting = 1
	if strToiletMounting = `Настенное` then iToiletMounting = 2
	parameters iToiletMounting = iToiletMounting
else
	strToiletMounting = `Напольное`
	if iToiletMounting = 2 then strToiletMounting = `Настенное`
	parameters strToiletMounting = strToiletMounting
endif

if (bFixture3d & iFixtureType <> 2) | not(bFixture3d) then
	lock "strToiletMounting", "strFlushType"
	hideparameter "strToiletMounting", "strFlushType"
endif

if GLOB_MODPAR_NAME = "strToiletMounting" then
	if strToiletMounting = `Напольное` then
		strFlushType = `Бачок`
		iFlushType 	 = 1
	else
		strFlushType = `Встроенный в Стену Бачок`
		iFlushType 	 = 3
	endif
	parameters 	strFlushType = strFlushType,
				iFlushType 	 = iFlushType
endif

if GLOB_MODPAR_NAME = "iToiletMounting" then
	if iToiletMounting = 1 then
		strFlushType = `Бачок`
		iFlushType 	 = 1
	else
		strFlushType = `Встроенный в Стену Бачок`
		iFlushType 	 = 3
	endif
	parameters 	strFlushType = strFlushType,
				iFlushType 	 = iFlushType
endif

if GLOB_MODPAR_NAME = "strFlushType" then
	iFlushType = 1
	if strFlushType = `Встроенный в Стену Бачок` then iFlushType = 3
	if strFlushType = `Разбрызгивающий Смыватель` 	then iFlushType = 4
	if strFlushType = `Нет` 			then iFlushType = 5
	parameters iFlushType = iFlushType
else
	strFlushType = `Бачок`
	if iFlushType = 3 then strFlushType = `Встроенный в Стену Бачок`
	if iFlushType = 4 then strFlushType = `Разбрызгивающий Смыватель`
	if iFlushType = 5 then strFlushType = `Нет`
	parameters strFlushType = strFlushType
endif

if iToiletMounting = 1 then
	values "strFlushType" `Бачок`, `Встроенный в Стену Бачок`, `Разбрызгивающий Смыватель`, `Нет`
	values "iFlushType" 1,3,4,5
else
	values "strFlushType" `Встроенный в Стену Бачок`, `Разбрызгивающий Смыватель`, `Нет`
	values "iFlushType" 3,4,5
endif

if iFlushType <> 4 then
	lock "strFlushometerType"
	hideparameter "strFlushometerType"
endif

if iPartitionType = PARTITION_TYPE_STALL then
	values "fixtureType" `Откл.`, `Унитаз`, `Писсуар`, `Чаша Генуя`
	values "iFixtureType" 0, 2, 1, 3
endif
if iPartitionType = PARTITION_TYPE_URINAL_SCREEN or iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	values "fixtureType" `Откл.`, `Писсуар`
	values "iFixtureType" 0, 1
endif
if GLOB_MODPAR_NAME = "fixtureType" then
	iFixtureType = FIXTURE_TYPE_OFF
	if fixtureType = `Писсуар`	then iFixtureType = FIXTURE_TYPE_URINAL
	if fixtureType = `Унитаз`	then iFixtureType = FIXTURE_TYPE_TOILET
	if fixtureType = `Чаша Генуя`	then iFixtureType = FIXTURE_TYPE_SQUATTING_TOILET
	parameters iFixtureType = iFixtureType
else
	fixtureType = `Откл.`
	if iFixtureType = FIXTURE_TYPE_URINAL then fixtureType = `Писсуар`
	if iFixtureType = FIXTURE_TYPE_TOILET then fixtureType = `Унитаз`
	if iFixtureType = FIXTURE_TYPE_SQUATTING_TOILET then fixtureType = `Чаша Генуя`
	parameters fixtureType = fixtureType
endif

if GLOB_MODPAR_NAME = "fixtureType" then
	if iFixtureType = FIXTURE_TYPE_TOILET or iFixtureType = FIXTURE_TYPE_SQUATTING_TOILET then
		MSFront = 0.8
		MSSide = 0.3
		MSSide2 = 0.3
	endif
	if iFixtureType = FIXTURE_TYPE_URINAL then
		MSFront = 0.7
		MSSide = 0.2
		MSSide2 = 0.2
	endif
	parameters 	MSFront = MSFront,
				MSSide 	= MSSide,
				MSSide2	= MSSide2
endif

if not(iFixtureType = FIXTURE_TYPE_URINAL) or not(bFixture3d) then
	hideparameter "urinalHeight","bMotionDetector"
	lock "urinalHeight","bMotionDetector"
else
	values "urinalHeight" range [0.7, )
endif

if not(iFixtureType = FIXTURE_TYPE_URINAL ) or not(bMotionDetector) then
	hideparameter "matDetector"
	lock "matDetector"
endif

if iFixtureType = FIXTURE_TYPE_OFF then
	hideparameter	"bFixture3d",
					"penFixture",
					"fillFixture",
					"fillPenFixture",
					"fillPenBgFixture",
					"matFixture",
					"MSFront",
					"MSSide",
					"MSSide2"
	lock	"bFixture3d",
			"penFixture",
			"fillFixture",
			"fillPenFixture",
			"fillPenBgFixture",
			"matFixture",
			"MSFront",
			"MSSide",
			"MSSide2"
endif

if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	hideparameter	"matHandle"
	lock	"matHandle"
endif

values "openingDirection" `Внутрь`, `Наружу`
if GLOB_MODPAR_NAME = "openingDirection" then
	bOutsideOpening = 1
	if openingDirection = `Внутрь`	then bOutsideOpening = 0
	parameters bOutsideOpening = bOutsideOpening
else
	openingDirection = `Наружу`
	if not (bOutsideOpening)	then openingDirection = `Внутрь`
	parameters openingDirection = openingDirection
endif

call "flushometer_m" parameters all	bHideFlushometerParams	= (bFixture3d & iFixtureType <> 2) | not(bFixture3d)

values "gs_detlevel_2D" `Зависит от Масштаба`,`1:50`,`1:100`,`1:200`
values "gs_detlevel_2D_m" 0, 3, 2, 1
if GLOB_MODPAR_NAME = "gs_detlevel_2D" then
	gs_detlevel_2D_m = 0
	if gs_detlevel_2D = `1:200`           then gs_detlevel_2D_m = 1
	if gs_detlevel_2D = `1:100`           then gs_detlevel_2D_m = 2
	if gs_detlevel_2D = `1:50`            then gs_detlevel_2D_m = 3
	parameters gs_detlevel_2D_m = gs_detlevel_2D_m
else
	gs_detlevel_2D = `Зависит от Масштаба`
	if gs_detlevel_2D_m = 1	then gs_detlevel_2D = `1:200`
	if gs_detlevel_2D_m = 2	then gs_detlevel_2D = `1:100`
	if gs_detlevel_2D_m = 3 then gs_detlevel_2D = `1:50`
	parameters gs_detlevel_2D = gs_detlevel_2D
endif

values "gs_detlevel_3D" `Детальный`,`Простая`,`Откл.`
if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3d_m=2
	if gs_detlevel_3D=`Откл.`	then gs_detlevel_3d_m=0
	if gs_detlevel_3D=`Простая` then gs_detlevel_3d_m=1
	parameters gs_detlevel_3d_m = gs_detlevel_3d_m
else
	gs_detlevel_3D=`Детальный`
	if gs_detlevel_3d_m=0 then gs_detlevel_3D=`Откл.`
	if gs_detlevel_3d_m=1 then gs_detlevel_3D=`Простая`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3d_m = 1 then hideparameter "res"
if gs_detlevel_3d_m = 0 then hideparameter "res", "gs_shadow"

values "latchType" `Нет`,`Стиль 1`,`Стиль 2`,`Стиль 3`,`Стиль 4`
values "latchType_m" 0, 1, 2, 3, 4

if GLOB_MODPAR_NAME = "latchType" then
	latchType_m = 0
	if latchType =`Стиль 1`	then latchType_m = 1
	if latchType =`Стиль 2`	then latchType_m = 2
	if latchType =`Стиль 3`	then latchType_m = 3
	if latchType =`Стиль 4`	then latchType_m = 4
	parameters latchType_m = latchType_m
else
	latchType =`Нет`
	if latchType_m = 1 then latchType =`Стиль 1`
	if latchType_m = 2 then latchType =`Стиль 2`
	if latchType_m = 3 then latchType =`Стиль 3`
	if latchType_m = 4 then latchType =`Стиль 4`
	parameters latchType = latchType
endif

values "doorpullType" `Нет`,`Стиль 1`,`Стиль 2`,`Стиль 3`,`Стиль 4`,`Стиль 5`
values "doorpullType_m" 0, 1, 2, 4, 5, 8

if GLOB_MODPAR_NAME = "doorpullType" then
	doorpullType_m = 0
	if doorpullType =`Стиль 1`	then doorpullType_m = 1
	if doorpullType =`Стиль 2`	then doorpullType_m = 2
	if doorpullType =`Стиль 3`	then doorpullType_m = 4
	if doorpullType =`Стиль 4`	then doorpullType_m = 5
	if doorpullType =`Стиль 5`	then doorpullType_m = 8
	parameters doorpullType_m = doorpullType_m
else
	doorpullType =`Нет`
	if doorpullType_m = 1 then doorpullType =`Стиль 1`
	if doorpullType_m = 2 then doorpullType =`Стиль 2`
	if doorpullType_m = 4 then doorpullType =`Стиль 3`
	if doorpullType_m = 5 then doorpullType =`Стиль 4`
	if doorpullType_m = 8 then doorpullType =`Стиль 5`
	parameters doorpullType = doorpullType
endif

values "heightDoor" range (0, heightPanel + bottomHeightPanel - bottomHeightDoor]
values "bottomHeightDoor" range [0,)
values "widthDoor" range [0.15,)
values "widthHingeSidePanel" range [0,)
values "widthLockSidePanel" range [0,)
values "screenEndLength" range [thicknessPanel,)

if not (bOutsideOpening) then
	values "openingAngle2d" range [0, 90]
	values "openingAngle3d" range [0, 90]
else
	values "openingAngle2d" range [0, 180)
	values "openingAngle3d" range [0, 180)
endif
values "heightDoorpull" range [bottomHeightDoor + 0.1, heightDoor + bottomHeightDoor - 0.1]
values "xDoorpullPos" range [0.02, widthDoor - 0.02]
values "heightLatch" range [bottomHeightDoor + 0.1, heightDoor + bottomHeightDoor - 0.1]
values "xLatchPos" range [0.02, widthDoor - 0.02]

if latchType_m = 0 then
	hideparameter "heightLatch", "xLatchPos"
	lock "heightLatch", "xLatchPos"
endif

if doorpullType_m = 0 then
	hideparameter "heightDoorpull", "xDoorpullPos"
	lock "heightDoorpull", "xDoorpullPos"
endif

if latchType_m = 0 & doorpullType_m = 0 then
	hideparameter "matHandle"
	lock "matHandle"
endif

! ------------------------------------------------------------------------------
! Defining heights
! ------------------------------------------------------------------------------

values "bottomHeightPanel" range [0,)
values "bottomEndPanel" range [0,)
values "topEndPanel" range [0,)

if iPartitionType = PARTITION_TYPE_STALL then
	if iStallType = STALL_TYPE_OVERHEAD_BRACED then
		if GLOB_MODPAR_NAME = "ZZYZX" then
			heightPanel = zzyzx - heightTopBracing - bottomHeightPanel
			parameters heightPanel = heightPanel
		else
			zzyzx = heightPanel + heightTopBracing + bottomHeightPanel
			parameters zzyzx = zzyzx
		endif
	endif
	if iStallType = STALL_TYPE_FLOOR_MOUNTED then
		values "heightPanel" range (0,)
		values "zzyzx" range (bottomHeightPanel,)
		if GLOB_MODPAR_NAME = "ZZYZX" then
			heightPanel = zzyzx - bottomHeightPanel
			parameters heightPanel = heightPanel
		else
			zzyzx = heightPanel + bottomHeightPanel
			parameters zzyzx = zzyzx
		endif
		hideparameter "gs_topBracing"
	endif
	if iStallType = STALL_TYPE_CEILING_MOUNTED then
		values "heightPanel" range (0,)
		values "heightTopPanel" range (0,)
		values "zzyzx" range (heightPanel + bottomHeightPanel,)
		if GLOB_MODPAR_NAME = "heightTopPanel" then
			heightPanel = zzyzx - heightTopPanel - bottomHeightPanel
			parameters heightPanel = heightPanel
		else
			heightTopPanel = zzyzx - heightPanel - bottomHeightPanel
			parameters heightTopPanel = heightTopPanel
		endif
		hideparameter "gs_topBracing"
	endif
endif

if iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
	if iScreenType = SCREEN_TYPE_FLOOR_MOUNTED then
		values "ZZYZX" range [bottomHeightPanel,)
		if GLOB_MODPAR_NAME = "ZZYZX" then
			heightPanel = zzyzx - bottomHeightPanel
			parameters heightPanel = heightPanel
		else
			zzyzx = heightPanel + bottomHeightPanel
			parameters zzyzx = zzyzx
		endif
	endif
	if iScreenType = SCREEN_TYPE_WALL_HUNG then
		values "ZZYZX" range [bottomEndPanel,)
		if GLOB_MODPAR_NAME = "ZZYZX" then
			topEndPanel = zzyzx - bottomEndPanel
			parameters topEndPanel = topEndPanel
		else
			zzyzx = topEndPanel + bottomEndPanel
			parameters zzyzx = zzyzx
		endif
	endif
endif

! ------------------------------------------------------------------------------
! Defining length parameter logic
! ------------------------------------------------------------------------------

if iPartitionType = PARTITION_TYPE_STALL then
	if GLOB_MODPAR_NAME = "widthStall" or GLOB_MODPAR_NAME = "widthDoor" then
		temp_a = widthHingeSidePanel / (widthHingeSidePanel + widthLockSidePanel)
		widthHingeSidePanel = (widthStall - widthDoor) * temp_a
		widthLockSidePanel = widthStall - widthDoor - widthHingeSidePanel
		parameters widthHingeSidePanel = widthHingeSidePanel
		parameters widthLockSidePanel = widthLockSidePanel
	endif

	if GLOB_MODPAR_NAME = "widthHingeSidePanel" then
		widthLockSidePanel = widthStall - widthDoor - widthHingeSidePanel
		parameters widthLockSidePanel = widthLockSidePanel
	endif

	if GLOB_MODPAR_NAME = "widthLockSidePanel" then
		widthHingeSidePanel = widthStall - widthDoor - widthLockSidePanel
		parameters widthHingeSidePanel = widthHingeSidePanel
	endif
endif


! ------------------------------------------------------------------------------
! Defining full length
! ------------------------------------------------------------------------------
if GLOB_MODPAR_NAME = "A" then
	widthStall = A / nStalls
	parameters widthStall = widthStall
endif

if GLOB_MODPAR_NAME = "widthStall" or GLOB_MODPAR_NAME = "nStalls" or GLOB_MODPAR_NAME = "A" then
	for i = 1 to nStalls
		stallWidths[i] = widthStall
		fixturePositions[i][1] = 0
		fixturePositions[i][2] = 0
		parameters stallWidths[i] = stallWidths[i]
		parameters fixturePositions[i][1] = fixturePositions[i][1]
		parameters fixturePositions[i][2] = fixturePositions[i][2]
	next i
endif

fullLength = 0
for i = 1 to nStalls
	fullLength = fullLength + stallWidths[i]
next i

if nStalls = 1 then
	stallWidths[1] = widthStall
	a = widthStall
	parameters a = a, stallWidths[1] = stallWidths[1]
else
	a = fullLength
	parameters a = a
endif

! ------------------------------------------------------------------------------
! Defining side-panels length
! ------------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "B" then
	if iPartitionType = PARTITION_TYPE_STALL then
		depthStall = b
		parameters depthStall = depthStall
	endif
	if iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
		screenLength = b
		parameters screenLength = screenLength
	endif
else
	if iPartitionType = PARTITION_TYPE_STALL then
		b = depthStall
	endif
	if iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
		b = screenLength
	endif
	parameters b = b
endif
if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	B = dividerDepth
	parameters B = B
endif


if iPartitionType = PARTITION_TYPE_STALL then
	lengthPanel = depthStall
endif
if iPartitionType = PARTITION_TYPE_URINAL_SCREEN then
	lengthPanel = screenLength
endif
if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	lengthPanel = dividerDepth
endif


if GLOB_MODPAR_NAME = "nStalls" or GLOB_MODPAR_NAME = "partitionType" \
	or GLOB_MODPAR_NAME = "depthStall" or GLOB_MODPAR_NAME = "screenLength"  or GLOB_MODPAR_NAME = "B" then

	lengthEndPanel1 = lengthPanel
	parameters lengthEndPanel1 = lengthEndPanel1

	for i = 1 to nPanels
		panelLengths[i] = lengthPanel
		parameters panelLengths[i] = panelLengths[i]
	next i

	lengthEndPanel2 = lengthPanel
	parameters lengthEndPanel2 = lengthEndPanel2
	panelLengths[nStalls] = lengthEndPanel2
	parameters panelLengths[nStalls] = panelLengths[nStalls]
endif

if GLOB_MODPAR_NAME = "bEndPanel2" or GLOB_MODPAR_NAME = "lengthEndPanel2" then
	if bEndPanel2 then
		panelLengths[nStalls] = lengthEndPanel2
		parameters panelLengths[nStalls] = panelLengths[nStalls]
	else
		panelLengths[nStalls] = 0
		parameters panelLengths[nStalls] = panelLengths[nStalls]
	endif
endif

if nStalls < 3 | iPartitionType <> PARTITION_TYPE_STALL then
	hideparameter "bGraffiti"
endif

if iPartitionType = PARTITION_TYPE_CHINA_DIVISION then
	lock "B"
endif

! ------------------------------------------------------------------------------
! Minimal Space Parameters
! ------------------------------------------------------------------------------

values "MSFront" range [0, )
values "MSSide" range [0, )
values "MSSide2" range [0, )
values "ms_openingSDepth" range [0, )
values "ms_openingSWidth1" range [0, )
values "ms_openingSWidth2" range [0, )
values "ms_openingSDepth_in" range [0, )
values "ms_openingSWidth1_in" range [0, )
values "ms_openingSWidth2_in" range [0, )

if not(bOutsideOpening) then
	hideparameter 	"ms_openingSDepth",
					"ms_openingSWidth1",
					"ms_openingSWidth2"

	lock	"ms_openingSDepth",
			"ms_openingSWidth1",
			"ms_openingSWidth2"
else
	hideparameter 	"ms_openingSDepth_in",
					"ms_openingSWidth1_in",
					"ms_openingSWidth2_in"

	lock	"ms_openingSDepth_in",
			"ms_openingSWidth1_in",
			"ms_openingSWidth2_in"
endif


! ------------------------------------------------------------------------------
! UI control
! ------------------------------------------------------------------------------

if iPartitionType = PARTITION_TYPE_STALL then
	values "gs_ui_current_page" 1,2,3,4,5,6,7
else
	if iFixtureType = FIXTURE_TYPE_OFF then
		values "gs_ui_current_page" 1,2,3,4,5
	else
		values "gs_ui_current_page" 1,2,3,4,5,6
	endif
endif

