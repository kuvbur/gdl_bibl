
unID = 10
GS_HotspotUnIDBegin = 1000

pen gs_cont_pen

for i = 1 to iPanels
	call "fa_PartitionPanel" parameters all GS_HotspotUnIDBegin = GS_HotspotUnIDBegin,
										iPartitionType = 1

	add2 0, thicknessPanel / 2
	for j = 1 to 2
		hotspot2 0,				thicknessPanel / 2, unID, lengthPanel, 1 + 256	: unID = unID + 1
		hotspot2 lengthPanel,	thicknessPanel / 2, unID, lengthPanel, 2 + 128	: unID = unID + 1
		hotspot2 -1,			thicknessPanel / 2, unID, lengthPanel, 3		: unID = unID + 1

		hotspot2 0,				thicknessPanel / 2, unID, lengthPanel, 1 + 128	: unID = unID + 1
		hotspot2 lengthPanel,	thicknessPanel / 2, unID, lengthPanel, 2		: unID = unID + 1
		hotspot2 -1,			thicknessPanel / 2, unID, lengthPanel, 3		: unID = unID + 1
		mul2 1, -1
	next j
	del 3

	add2 -bFrontSidePost * widthPost - leftOverhangTopBracing, thicknessPanel / 2

	if bFrontSidePost then
		for j = 1 to 2
			hotspot2 0,		thicknessPost / 2, unID, a, 1 + 256 : unID = unID + 1
			hotspot2 a,		thicknessPost / 2, unID, a, 2 + 128 : unID = unID + 1
			hotspot2 -1,	thicknessPost /	2, unID, a, 3		: unID = unID + 1
			mul2 1, -1
		next j
		del 2
	endif

	if bBottomBracing and ( thicknessBottomBracing > (thicknessTopBracing * bTopBracing) or abs (thicknessBottomBracing -  thicknessTopBracing) < eps) and \
			( rightOverhangBottomBracing > eps or abs(thicknessBottomBracing - thicknessPanel) > eps ) then
		for j = 1 to 2
			hotspot2 0,		thicknessBottomBracing / 2, unID, a, 1 + 128	: unID = unID + 1
			hotspot2 a,		thicknessBottomBracing / 2, unID, a, 2			: unID = unID + 1
			hotspot2 -1,	thicknessBottomBracing / 2, unID, a, 3			: unID = unID + 1
			mul2 1, -1
		next j
		del 2
	endif

	if bTopBracing and thicknessTopBracing > (thicknessBottomBracing * bBottomBracing) and \
			( rightOverhangTopBracing > eps or abs(thicknessTopBracing - thicknessPanel) > eps ) then
		for j = 1 to 2
			hotspot2 0,		thicknessTopBracing / 2, unID, a, 1 + 128	: unID = unID + 1
			hotspot2 a,		thicknessTopBracing / 2, unID, a, 2			: unID = unID + 1
			hotspot2 -1,	thicknessTopBracing / 2, unID, a, 3			: unID = unID + 1
			mul2 1, -1
		next j
		del 2
	endif

	del 1

	add2 0, distPanels
	GS_HotspotUnIDBegin = GS_HotspotUnIDBegin + 1000
next i
del iPanels

unID = 1
if iPanels > 1 then
	add2 0, -((max(thicknessPanel, thicknessTopBracing, thicknessBottomBracing, thicknessPost))-thicknessPanel)/2
	hotspot2 a/2,	0,		unID, b, 1 + 256	: unID = unID + 1
	hotspot2 a/2,	b,		unID, b, 2			: unID = unID + 1
	hotspot2 a/2,	-1,		unID, b, 3			: unID = unID + 1
	del 1
else
	unID = unID + 3
endif

end
