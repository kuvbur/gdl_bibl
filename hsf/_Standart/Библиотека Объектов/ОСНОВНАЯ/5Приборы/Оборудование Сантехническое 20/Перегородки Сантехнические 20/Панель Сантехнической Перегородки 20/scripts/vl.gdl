
call "FM_types" parameters all


if GLOB_MODPAR_NAME = "A" then
	lengthPanel = a - leftOverhangTopBracing - rightOverhangTopBracing - bFrontSidePost * widthPost
	parameters lengthPanel = lengthPanel
else
	a = lengthPanel + leftOverhangTopBracing + rightOverhangTopBracing + bFrontSidePost * widthPost
	parameters a = a
endif

if GLOB_MODPAR_NAME = "B" then
	distPanels = (b - max(thicknessPanel, thicknessTopBracing, thicknessBottomBracing, thicknessPost)) / (iPanels - 1)
	parameters distPanels = distPanels
else
	b = max(thicknessPanel, thicknessTopBracing, thicknessBottomBracing, thicknessPost) + (iPanels - 1) * distPanels
	parameters b = b
endif
if iPanels = 1 then lock "b"


if bTopBracing then
	if GLOB_MODPAR_NAME = "ZZYZX" then
		parameters bottomHeightTopBracing = zzyzx - heightTopBracing
	else
		parameters zzyzx = bottomHeightTopBracing + heightTopBracing
	endif
else
	if GLOB_MODPAR_NAME = "ZZYZX" then
		parameters heightPanel = zzyzx
	else
		parameters zzyzx = heightPanel
	endif
endif

values "iPanels" range [1,)
values "distPanels" range [thicknessPanel,)

values "a" range [leftOverhangTopBracing + rightOverhangTopBracing + bFrontSidePost * widthPost,)
values "b" range ((max(thicknessPanel, thicknessTopBracing, thicknessBottomBracing, thicknessPost)) * (iPanels - 1),)
values "zzyzx" range (bottomHeightPanel,)

values "lengthPanel" range [0,)
values "bottomHeightTopBracing" range [heightPanel,)
values "topHeightBottomBracing" range [heightBottomBracing, bottomHeightPanel]
values "thicknessTopBracing" range [thicknessPanel,)
values "thicknessBottomBracing" range [thicknessPanel,)
values "thicknessPost" range [thicknessPanel,)
values "leftOverhangBottomBracing" range [0,)
values "rightOverhangBottomBracing" range [0,)

parameters leftOverhangBottomBracing = leftOverhangTopBracing
parameters rightOverhangBottomBracing = rightOverhangTopBracing

if bSameSizes then
	parameters matPost = matFrame, matBottomBracing = matFrame, matTopBracing = matFrame
	hideparameter "matPost", "matTopBracing", "matBottomBracing"
	parameters thicknessTopBracing = thicknessFrame, thicknessBottomBracing = thicknessFrame, thicknessPost = thicknessFrame
	hideparameter "thicknessTopBracing", "thicknessBottomBracing", "thicknessPost"
	parameters heightTopBracing = heightFrame, heightBottomBracing = heightFrame, widthPost = heightFrame
	hideparameter "heightTopBracing", "heightBottomBracing", "widthPost"
else
	hideparameter "matFrame", "thicknessFrame", "heightFrame"
endif

if GLOB_MODPAR_NAME = "bFooter" and bFooter then parameters bBottomBracing = 0
if GLOB_MODPAR_NAME = "bBottomBracing" and bBottomBracing then parameters bFooter = 0

if not (bFooter) then hideparameter "matFooter"

if not (bBottomBracing) then hideparameter "heightBottomBracing", "thicknessBottomBracing", "matBottomBracing", "topHeightBottomBracing"
if not (bTopBracing) then
	hideparameter "heightTopBracing", "thicknessTopBracing", "matTopBracing", "bottomHeightTopBracing"
endif
if not(bTopBracing) or not(bShowB) then
	hideparameter "penTopBracing", "lineTypeTopBracing"
endif

if not(bBottomBracing) or not(bShowB) then
	hideparameter 	"penBottomBracing", "lineTypeBottomBracing",
					"fillTypeBottomBracing", "fillPenBottomBracing",
					"fillBackPenBottomBracing"
endif
if not(bBottomBracing) and not(bTopBracing) then hideparameter "bShowB"
if not(bFrontSidePost) then
	hideparameter 	"penPost", "lineTypePost", "fillTypePost",
					"fillPenPost", "fillBackPenPost",
					"widthPost", "thicknessPost",
					"bDiffPostHeight", "topHeightPost",
					"bottomHeightPost", "matPost"
endif

if not (bDiffPostHeight) then
	hideparameter "bottomHeightPost", "topHeightPost"
	parameters bottomHeightPost = 0
	parameters topHeightPost = zzyzx
endif

