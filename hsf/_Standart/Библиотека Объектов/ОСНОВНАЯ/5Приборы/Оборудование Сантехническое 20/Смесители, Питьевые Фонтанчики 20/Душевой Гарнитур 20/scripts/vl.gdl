
! ==============================================================================
! Shower Kit Tap
! ==============================================================================


call "FM_types" parameters all

dim st_detail3D[3]
	st_detail3D[1] = `Детальный`
	st_detail3D[2] = `Простой`
	st_detail3D[3] = `Откл.`

values "gs_detlevel_3D" st_detail3D		!`Detailed`,`Simple`,`Off`
values "gs_detlevel_3D_m" DETLEV_3D_DETAILED, DETLEV_3D_SIMPLE, DETLEV_3D_OFF

if GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 then
	gs_detlevel_3D_m = DETLEV_3D_DETAILED
	if gs_detlevel_3D = st_detail3D[3] then gs_detlevel_3D_m = DETLEV_3D_OFF
	if gs_detlevel_3D = st_detail3D[2] then gs_detlevel_3D_m = DETLEV_3D_SIMPLE
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = st_detail3D[1]		! Detailed
	if gs_detlevel_3D_m = DETLEV_3D_OFF then gs_detlevel_3D = st_detail3D[3]
	if gs_detlevel_3D_m = DETLEV_3D_SIMPLE then gs_detlevel_3D = st_detail3D[2]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

call "tapType_m" parameters	all	enableTapGroups			= 0,
								enableSingleLevelTaps	= 0,
								enableTwoHandleTaps		= 0,
								enableTraditionalTaps	= 0,
								enableSensorFaucets		= 0,
								enableWallMountedTaps	= 0,
								enableCustomTaps		= 2**26 + 2**27 + 2**28 + 2**29,
								bEnableTapEdit			= 0,
								enableTapSymbols		= 2 + 4 + 8 + 16,
								bShowerTapSymbol		= 1

! --- Limitation

if gs_cont_pen=0 then parameters gs_cont_pen=75
if gs_fill_pen=0 then parameters gs_fill_pen=19
values "gs_resol" range [4, )

if gs_detlevel_3D_m = DETLEV_3D_SIMPLE then hideparameter "gs_resol"

if gs_tap_type_m = 0	then
	zzyzx = 0
	parameters zzyzx = zzyzx
endif
lock "zzyzx"

put 1, 2

! MEP
! ============================================================
if isMEPEnabled then

	nTabs = 3

	IF NOT(ConStatus_1) AND NOT(ConStatus_2) THEN
		LOCK "gs_con_mat"
		HIDEPARAMETER "gs_con_mat"
	ENDIF

	DIM shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	PARAMETERS MEP_NumberConnections = 2


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Подключение Горячей Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	values "ConLength_1" RANGE [0, ]

	! Default Position
	if (GLOB_MODPAR_NAME = "gs_tap_type" | GLOB_MODPAR_NAME = "gs_tap_type_m" | \
	GLOB_MODPAR_NAME = "tapGroup" | GLOB_MODPAR_NAME = "iTapGroup") then
		if gs_tap_type_m = 26 then
			ConPosX_1 = widthTap/4
			ConPosZ_1 = mountingHeight
		else
			if gs_tap_type_m = 27 then
				ConPosX_1 = 0
				ConPosZ_1 = mountingHeight - 0.05
			else
				ConPosX_1 = posTap + widthTap/2
				ConPosZ_1 = mountingHeight
			endif
		endif
		parameters	ConPosX_1 = ConPosX_1,
					ConPosZ_1 = ConPosZ_1
	endif

	! Range
	if gs_tap_type_m > 25 then
		values "ConPosX_1" range [-limit_X + ConWidth_1/2, limit_X - ConWidth_1/2]
		values "ConPosZ_1" range [mountingHeight - limit_Y1 + ConWidth_1/2, mountingHeight + limit_Y2 - ConWidth_1/2]
	else
		! --- X Range
		values "ConPosX_1"	range [posTap + ConWidth_1/2,posTap+widthTap - ConWidth_1/2],
							range [-(posTap+widthTap - ConWidth_1/2),-(posTap + ConWidth_1/2)]

		! --- Change Connection Side
		if GLOB_MODPAR_NAME = "ConPosX_2" then
			if ConPosX_2 < 0 & ConPosX_1 < 0 then
				ConPosX_1 = posTap + widthTap/2
				parameters 	ConPosX_1 = ConPosX_1
			endif
			if ConPosX_2 > 0 & ConPosX_1 > 0 then
				ConPosX_1 = -(posTap + widthTap/2)
				parameters 	ConPosX_1 = ConPosX_1
			endif
		endif

		ConPosZ_1 = mountingHeight
	endif

	parameters 	ConPosX_1 = ConPosX_1
				ConPosZ_1 = ConPosZ_1

	if ConWidth_1 < 0.001 then
		ConWidth_1 = 0.001
		parameters ConWidth_1 = ConWidth_1
	else
		if ConWidth_1 > widthTap then
			ConWidth_1 = widthTap
			parameters ConWidth_1 = ConWidth_1
		endif
	endif

	values "ConWidth_1" RANGE (0, widthTap]
	parameters ConDepth_1 = ConWidth_1

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Подключение Холодной Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	values "ConLength_2" RANGE [0, ]

	! Default Position
	if (GLOB_MODPAR_NAME = "gs_tap_type" | GLOB_MODPAR_NAME = "gs_tap_type_m" | \
	GLOB_MODPAR_NAME = "tapGroup" | GLOB_MODPAR_NAME = "iTapGroup") then
		if gs_tap_type_m = 26 then
			ConPosX_2 = -widthTap/4
			ConPosZ_2 = mountingHeight
		else
			if gs_tap_type_m = 27 then
				ConPosX_2 = 0
				ConPosZ_2 = mountingHeight + 0.05
			else
				ConPosX_2 = -posTap - widthTap/2
				ConPosZ_2 = mountingHeight
			endif
		endif
		parameters	ConPosX_2 = ConPosX_2,
					ConPosZ_2 = ConPosZ_2
	endif

	! Range
	if gs_tap_type_m > 25 then
		values "ConPosX_2" range [-limit_X + ConWidth_2/2, limit_X - ConWidth_2/2]
		values "ConPosZ_2" range [mountingHeight - limit_Y1 + ConWidth_2/2, mountingHeight + limit_Y2 - ConWidth_2/2]
	else

		! --- X Range
		values "ConPosX_2"	range [posTap + ConWidth_2/2,posTap+widthTap - ConWidth_2/2],
							range [-(posTap+widthTap - ConWidth_2/2),-(posTap + ConWidth_2/2)]

		ConPosZ_2 = mountingHeight

		! --- Change Connection Side
		if GLOB_MODPAR_NAME = "ConPosX_1" then
			if ConPosX_1 < 0 & ConPosX_2 < 0 then
				ConPosX_2 = posTap + widthTap/2
				parameters 	ConPosX_2 = ConPosX_2
			endif
			if ConPosX_1 > 0 & ConPosX_2 > 0 then
				ConPosX_2 = -(posTap + widthTap/2)
				parameters 	ConPosX_2 = ConPosX_2
			endif
		endif
	endif

	parameters 	ConPosX_2 = ConPosX_2
				ConPosZ_2 = ConPosZ_2

	if ConWidth_2 < 0.001 then
		ConWidth_2 = 0.001
		parameters ConWidth_2 = ConWidth_2
	else
		if ConWidth_2 > widthTap then
			ConWidth_2 = widthTap
			parameters ConWidth_2 = ConWidth_2
		endif
	endif

	values "ConWidth_2" RANGE (0, widthTap]
	parameters ConDepth_2 = ConWidth_2

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2,
		ConPosY = ConPosY_2,
		ConPosZ = ConPosZ_2,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


	if gs_ui_current_page = 3 then
		ac_mep_connectionpage_active = 1
	else
		ac_mep_connectionpage_active = 0
	endif
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
	put 3
else
	nTabs = 2

	if gs_ui_current_page > 2 then
		gs_ui_current_page = 1
		parameters gs_ui_current_page = gs_ui_current_page
	endif

	hideparameter "gs_connection", "gs_con_mat", "useSysMat"

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active
endif


values "gs_ui_current_page" get(nsp)
