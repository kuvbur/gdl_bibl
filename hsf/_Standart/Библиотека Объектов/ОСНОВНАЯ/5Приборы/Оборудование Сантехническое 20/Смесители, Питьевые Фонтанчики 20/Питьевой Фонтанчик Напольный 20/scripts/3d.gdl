
!! Drinking Fountain 01
!! Modified by Zsolt Feke/CSD/2006 - made more simple model


IF gs_detlevel_3D_m = 0 THEN END

IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN gs_detlevel_3D_m = 3

if gs_detlevel_3D_m = 2 then RESOL gs_resol
if gs_detlevel_3D_m = 1 then RESOL 12
IF gs_detlevel_3D_m = 3 THEN RESOL 3


if gs_shadow=0 then SHADOW OFF
if gs_shadow=1 then SHADOW ON


! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

IF gs_detlevel_3D_m=1 OR gs_detlevel_3D_m=3 THEN RESOL 8
IF gs_detlevel_3D_m=2 THEN RESOL 24

if bShowMEP then

	IF useSysMat THEN
		cover_mat 		= sMat
		top_mat			= sMat
		gs_basin_mat 	= sMat
		gs_tap_mat 		= sMat
		button_mat 		= sMat
		mat				= sMat
		gs_con_mat				= sMat
	ENDIF

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16
ENDIF

! --- end of modifying to Shaft


pen gs_cont_pen
material cover_mat

hotspot -a*0.5,-b*0.5,zzyzx
hotspot a*0.5,-b*0.5,zzyzx
hotspot -a*0.5,b*0.5,zzyzx
hotspot a*0.5,b*0.5,zzyzx
hotspot -a*0.5,-b*0.5,0
hotspot a*0.5,-b*0.5,0
hotspot -a*0.5,b*0.5,0
hotspot a*0.5,b*0.5,0

IF gs_detlevel_3D_m = 3 THEN
	addy b/2
	rotz 180

	rotz 90
	rotx 90
	addz -a/2+lv
	prism_  6, a-2*lv,
			lv, zzyzx-2*r, 15,
			b-lv, 0, 115,
			b-lv, zh, 15,
			-b/15, 0, 115,
			0, -zh+zt, 115,
			0, zt, 15

	material top_mat
	addz -lv
	prism_  6, a,
			0, zzyzx, 15,
			r, 0, 115,
			2*r, zzyzx-r, 15,
			b-2*r, 0, 115,
			0, -r, 115,
			-b, 0, 115
	del 3

	addz zzyzx-2*r
	rotz -90
	prism_  10, 0,
			-a/2, 0, 15,
			a/2, 0, 15,
			a/2, b, 15,
			-a/2, b, 15,
			-a/2, 0, -1,
			-a/2+lv, lv, 15,
			a/2-lv, lv, 15,
			a/2-lv, b-lv, 15,
			-a/2+lv, b-lv, 15,
			-a/2+lv, lv, -1

	del top

ELSE
	addy b/2
	rotz 180

	rotz 90
	rotx 90
	addz -a/2+lv
	prism_  6, a-2*lv,
			lv, zzyzx-2*r, 7,
			b-lv, 0, 115,
			b-lv, zh, 15,
			-b/15, 0, 115,
			0, -zh+zt, 115,
			0, zt, 15

	IF gs_detlevel_3D_m=2 THEN
		material mat
		addz zt
		prism_  4, a-2*lv-2*zt,
				lv+zt, 0, 15,
				0, zt, 107,
				b-2*lv-2*zt-b/15, 0, 115,
				0, -zt, 115
		addz zt
		prism_  4, a-2*lv-4*zt,
				b-b/15-lv, 2*zt, 15,
				0, zh-2*zt, 107,
				b/40, 0, 115,
				0, -zh+2*zt, 115
		del 2

		add b/8, zh/2, -lv/2
		for k=1 to 2
			for j=1 to 2
				for i=1 to 15
					block 3/10*b, 1*lv, lv/2
					addy 2*lv
				next i
				del 15
				addx 0.35*b
			next j
			del 2
			addz a-1.5*lv
		next k
		del 3
	endif

	material top_mat
	addz -lv
	prism_  8, a,
			0, zzyzx, 15,
			r-0.0001, 0, 115,
			0.0001,0,179,
			2*r, zzyzx, 901,
			0, 90, 4000,
			b-2*r, 0, 113,
			0, -r, 107,
			-b, 0, 115
	del 3
	addz zzyzx-2*r
	rotz -90
	prism_  10, 0,
			-a/2, 0, 15,
			a/2, 0, 15,
			a/2, b, 15,
			-a/2, b, 15,
			-a/2, 0, -1,
			-a/2+lv, lv, 15,
			a/2-lv, lv, 15,
			a/2-lv, b-lv, 15,
			-a/2+lv, b-lv, 15,
			-a/2+lv, lv, -1

	del 2

	material gs_basin_mat
	addz zzyzx-r-t
	rotz -90
	prism_  10, t,
			-a/2, 2*r, 0,
			a/2, 2*r, 4,
			a/2, b, 4,
			-a/2, b, 4,
			-a/2, 2*r, -1,
			-a/2+v, 2*r+v, 14,
			a/2-v, 2*r+v, 14,
			a/2-v, b-v, 14,
			-a/2+v, b-v, 14,
			-a/2+v, 2*r+v, -1

	prism_  4, 0,
			-a/2+v, 2*r+v, 15,
			-a/2+s, 2*r+v, 0,
			-a/2+s, b-v, 15,
			-a/2+v, b-v, 15

	add -a/2+s, b-(b-2*r)/2, 0
	roty 90
	rotz -90

	IF gs_detlevel_3D_m=2 THEN
		ruled   13, 0,
				c*cos(0), 0, 1,
				c*cos(15), 0, 1,
				c*cos(30), 0, 1,
				c*cos(45), 0, 1,
				c*cos(60), 0, 1,
				c*cos(75), 0, 1,
				c*cos(90), 0, 1,
				c*cos(105), 0, 1,
				c*cos(120), 0, 1,
				c*cos(135), 0, 1,
				c*cos(150), 0, 1,
				c*cos(175), 0, 1,
				c*cos(180), 0, 1,

				c*cos(0), c/cc*sin(0), z,
				c*cos(15), c/cc*sin(15), z,
				c*cos(30), c/cc*sin(30), z,
				c*cos(45), c/cc*sin(45), z,
				c*cos(60), c/cc*sin(60), z,
				c*cos(75), c/cc*sin(75), z,
				c*cos(90), c/cc*sin(90), z,
				c*cos(105), c/cc*sin(105), z,
				c*cos(120), c/cc*sin(120), z,
				c*cos(135), c/cc*sin(135), z,
				c*cos(150), c/cc*sin(150), z,
				c*cos(175), c/cc*sin(175), z,
				c*cos(180), c/cc*sin(180), z
	ELSE
		ruled   5, 0,
				c*cos(0), 0, 1,
				c*cos(45), 0, 1,
				c*cos(90), 0, 1,
				c*cos(135), 0, 1,
				c*cos(180), 0, 1,

				c*cos(0), c/cc*sin(0), z,
				c*cos(45), c/cc*sin(45), z,
				c*cos(90), c/cc*sin(90), z,
				c*cos(135), c/cc*sin(135), z,
				c*cos(180), c/cc*sin(180), z
	ENDIF

	addz z

	IF gs_detlevel_3D_m=2 THEN
		prism_  13, 0,
				c*cos(0), c/cc*sin(0), 15,
				c*cos(15), c/cc*sin(15), 15,
				c*cos(30), c/cc*sin(30), 15,
				c*cos(45), c/cc*sin(45), 15,
				c*cos(60), c/cc*sin(60), 15,
				c*cos(75), c/cc*sin(75), 15,
				c*cos(90), c/cc*sin(90), 15,
				c*cos(105), c/cc*sin(105), 15,
				c*cos(120), c/cc*sin(120), 15,
				c*cos(135), c/cc*sin(135), 15,
				c*cos(150), c/cc*sin(150), 15,
				c*cos(175), c/cc*sin(175), 15,
				c*cos(180), c/cc*sin(180), 0
	ELSE
		prism_  5, 0,
				c*cos(0), c/cc*sin(0), 15,
				c*cos(45), c/cc*sin(45), 15,
				c*cos(90), c/cc*sin(90), 15,
				c*cos(135), c/cc*sin(135), 15,
				c*cos(180), c/cc*sin(180), 0
	ENDIF
	del 6


	del 1

	IF gs_detlevel_3D_m=2 THEN
		material button_mat
		add -a/2+(s-v)/2+v, b-v-(b/4-r/2-v/2), zzyzx-r-t
		cylind t/2,2*t/3
		addz t/2
		cylind t/2,t/2
		addz t/2
		cylind t/3,t
		del 3

		material mat
		add a/2-v-v/2, b-v-(b-2*v-2*r)/2, zzyzx-r-t-c/cc+t/3
		cylind t/15, v/3
		del 1
	endif

	!!-----!!
	!! Tap !!
	!!-----!!

	material gs_tap_mat
	add -a/2+(s-v)/2+v+14*fh-0.022, b-v-(b/2-r-v), zzyzx-r-t
	rotz 90
	MUL 0.7,0.7,0.7
	call "tapType_m" parameters all gs_tap_type_m		= 13,
									bEnableTapEdit		= 0,
									bSinkTap			= 0,
									useOldSimpleTaps	= 0,
									iTapGroup			= 4

	DEL 1
	del 2

	DEL 2
ENDIF
END
