
! Drinking Fountain Wall Hung 01


DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

	! ==========================================================================
	! Scale
	! ==========================================================================

	call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
					returned_parameters gs_detlevel_2D_m

	! --- 2D ---

	pen gs_cont_pen
	fill gs_fill_pen

	unID=1

	rot2 180
	hotspot2 0,0,unID : unID=unID+1
	hotspot2 0,b/2,unID : unID=unID+1
	hotspot2 a/2,0,unID : unID=unID+1
	hotspot2 a/2,b-a/3,unID : unID=unID+1
	hotspot2 a/2-a/3,b,unID : unID=unID+1
	hotspot2 -a/2+a/3,b,unID : unID=unID+1
	hotspot2 -a/2,b-a/3,unID : unID=unID+1
	hotspot2 -a/2,0,unID : unID=unID+1

	drawindex 20

	fill gs_fill_type
	poly2_b 7, 1+2+4+64, gs_fill_pen, gs_back_pen,
			a/2, 0, 1,
			a/2, b-a/3, 1,
			a/3, 90, 2001,
			-a/2+a/3, b, 1,
			a/3, 90, 2001,
			-a/2, 0, 1,
			a/2, 0, -1

	IF gs_detlevel_2D_m <> 1 THEN
		poly2_b 10, 1+2+4+64, gs_fill_pen, gs_back_pen,
			0, b-a+v, 1,
			a/2-a/3, b-a+v, 1,
			a/3-v, 90, 2001,
			a/2-v, b-a/3, 1,
			a/3-v, 90, 2001,
			-a/2+a/3, b-v, 1,
			a/3-v, 90, 2001,
			-a/2+v, b-a+a/3, 1,
			a/3-v, 90, 2001,
			0, b-a+v, -1
	endif

	! --- 1/50 ---
	IF gs_detlevel_2D_m = 3 THEN
		circle2 a/2-v-h-v/2, b-a/2, v/2
	endif

	del 1

	!MINIMAL SPACE
	drawindex 10
	reqBMinSpace = 0
	if MSFront > EPS or MSSide > EPS or MSSide2 > EPS then
		call "fa_minimalspace" parameters all MSType = 8,unID = 3000,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen,
													MSFillBackPen,
													MSContLineType,
													MSContLinePen
	endif

	if reqBMinSpace <> 0 then
		FILL MSFillType
		line_type MSContLineType
		pen MSContLinePen
		for i = 1 to 2
			poly2_b 6, 1+2+4+64,MSFillPen,MSFillBackPen,
					-a/2,		-b+a/3,	0,
					-a/2+a/3,	-b+a/3,	900,
					-a/2+a/3, 	-b,		3000,
					-a/2+a/3, 	-b,		(abs(MSFront) < EPS),
					-a/2,		-b,		(abs(MSSide) < EPS),
					a/2,		b-a/3,	-1

			mul2 -1, 1
			MSSide = MSSide2
		next i
		del i-1
	endif
ENDIF
DEL TOP

! --- start of modifying to Shaft

if bShowMEP then

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 12

	DEL 1
ENDIF

! --- end of modifying to Shaft
