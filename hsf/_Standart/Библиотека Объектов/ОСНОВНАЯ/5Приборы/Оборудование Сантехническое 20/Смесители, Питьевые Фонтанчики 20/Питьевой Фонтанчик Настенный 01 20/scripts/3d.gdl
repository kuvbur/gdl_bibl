
! Drinking Fountain Wall Hung 01

IF gs_detlevel_3D_m = 0 THEN END
if not(gs_shadow) then SHADOW OFF

! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

IF gs_detlevel_3D_m=1 OR gs_detlevel_3D_m=3 THEN RESOL 8
IF gs_detlevel_3D_m=2 THEN RESOL 24

if bShowMEP then

	IF useSysMat THEN
		cover_mat 		= sMat
		gs_basin_mat 	= sMat
		gs_tap_mat 		= sMat
		button_mat 		= sMat
		mat				= sMat
		gs_con_mat 		= sMat
	ENDIF

	addz h_top
	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16
	del 1
ENDIF

! --- end of modifying to Shaft


if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 and GLOB_CONTEXT<40 then gs_resol=4

pen gs_cont_pen

!!! *** Hotspot Editing
unID=1
hotspot	0,0,0,unID,h_top,1 : unID=unID+1
hotspot	0,0,-1,unID,h_top,3 : unID=unID+1
hotspot	0,0,h_top,unID,h_top,2 : unID=unID+1

addz h_top
hotspot	-A*0.5,  0,0,unID,a,1+256 : unID=unID+1
hotspot	-A*0.5-1,0,0,unID,a,3 : unID=unID+1
hotspot	A*0.5,   0,0,unID,a,2 : unID=unID+1

hotspot	-a/2,0,0,unID,b,1+128 : unID=unID+1
hotspot	-a/2,1,0,unID,b,3 : unID=unID+1
hotspot	-a/2,-b,0,unID,b,2 : unID=unID+1

hotspot	a/2,0,0,unID,b,1+128 : unID=unID+1
hotspot	a/2,1,0,unID,b,3 : unID=unID+1
hotspot	a/2,-b,0,unID,b,2 : unID=unID+1

add -A*0.5, -B, 0
hotspot	0,b,0,unID,ZZYZX,1+128 : unID=unID+1
hotspot	0,b,1,unID,ZZYZX,3 : unID=unID+1
hotspot	0,b,-ZZYZX,unID,ZZYZX,2 : unID=unID+1

hotspot	a,b,0,unID,ZZYZX,1+128 : unID=unID+1
hotspot	a,b,1,unID,ZZYZX,3 : unID=unID+1
hotspot	a,b,-ZZYZX,unID,ZZYZX,2 : unID=unID+1
del 1

ADDZ -ZZYZX
IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN
	RESOL 8
	rotz 180
	prism_  7, zzyzx,
			a/2, 0, 15,
			a/2, b-a/3, 79,
			a/3, 90, 2079,
			-a/2+a/3, b, 79,
			a/3, 90, 2079,
			-a/2, 0, 15,
			a/2, 0, -1
	del 1
ELSE
	IF gs_detlevel_3D_m=2 THEN
		rotz 180
		!!!!!!!!!!!!!!!
		material cover_mat
		RESOL 2*gs_resol
		prism_  17, zzyzx,
				a/2, 0, 15,
				a/2, b-a/3, 79,
				a/3, 90, 2079,
				-a/2+a/3, b, 79,
				a/3, 90, 2079,
				-a/2, 0, 15,
				a/2, 0, -1,
				0, b-a+v, 4,
				a/2-a/3, b-a+v, 4,
				a/3-v, 90, 2004,
				a/2-v, b-a/3, 4,
				a/3-v, 90, 2004,
				-a/2+a/3, b-v, 4,
				a/3-v, 90, 2004,
				-a/2+v, b-a+a/3, 4,
				a/3-v, 90, 2004,
				0, b-a+v, -1

		prism_  10, 0,
				0, b-a+v, 0,
				a/2-a/3, b-a+v, 0,
				a/3-v, 90, 2000,
				a/2-v, b-a/3, 0,
				a/3-v, 90, 2000,
				-a/2+a/3, b-v, 0,
				a/3-v, 90, 2000,
				-a/2+v, b-a+a/3, 0,
				a/3-v, 90, 2000,
				0, b-a+v, -1

		!!--------!!
		!! Button !!
		!!--------!!

		material button_mat

		add 0, b, zzyzx/2
		rotx -90
		cylind 1/4", zzyzx/4.5
		addz 1/4"
		cylind 1/4", zzyzx/6
		addz 1/4"
		cylind 1/4", zzyzx/5
		del 4

		add a/2-a/3, b-a/2, zzyzx-h
		roty -90

		!!-------!!
		!! Basin !!
		!!-------!!

		material gs_basin_mat
		extrude 6, 0, 0,a-a/3*2, 0,
				h, a/2-v, 1,
				h, a/2-v-h, 901,
				0, 90, 4001,
				0, -(a/2-v-h), 1,
				h, -(a/2-v-h), 901,
				0, 90, 4001

		del 2

		add 0, b-a+a/3,zzyzx-h
		rotx -90
		rotz -90

		extrude 6, 0, 0, a-a/3*2, 0,
				h, a/2-v, 1,
				h, a/2-v-h, 901,
				0, 90, 4001,
				0, -(a/2-v-h), 1,
				h, -(a/2-v-h), 901,
				0, 90, 4001

		del 2

		addx a/2-a/3
		roty -90
		rotx 180

		for i=1 to 4
			revolve 4, 90, 0,
					0, 0, 1,
					0, a/3-v-h, 1,
					h, a/3-h-v, 901,
					0, -90, 4001
			addy -a/3
			rotx 90
		next i
		del 12

		!!-----!!
		!! Tap !!
		!!-----!!

		material gs_tap_mat
		add -a/2+v/2+14*fh, b-a/2-5*fh, zzyzx

		rotx 90
		roty 180
		RESOL gs_resol
		prism_ 7, 10*fh,
				13.5*fh, 0, 0,
				(6+170/101)*fh, (18+510/101)*fh, 113,
				1*fh, 38*fh, 15,
				-1*fh, -1*fh, 115,
				5*fh, -100/27*fh, 179,
				7*fh, 18*fh, 1079,
				(1547/232)*fh, (13+1105/232)*fh, 64

		rotx -90
		addx 13.5*fh

		rotx 180
		cutplane
		del 1

		add (6+170/101)*fh, -10*fh, (18+510/101)*fh
		rotz 90
		cutplane atn(20/27)
		del 2

		del 2

		add (1547/232)*fh, (13+1105/232)*fh, 0
		roty -90
		cutplane -atn(13/5)
		del 2

		rotx -90
		addx 13.5*fh

		roty 90-atn(18/6)
		addy -10*fh
		addz -10*fh
		prism_  6, 40*fh,
				-10*fh, 0, 0,
				-0.01*fh, 0, 13,
				0,0,15+64,
				0,5*fh,979,
				0,180,4000,
				-10*fh,0,100
		del 3
		del 2

		addx 13.5*fh
		rotx -90
		roty -atn(5/13)
		rotz 180
		addz -10*fh
		prism_  6, 40*fh,
				-10*fh, 0, 0,
				-0.01*fh, 0, 13,
				0,0,15+64,
				0,5*fh,979,
				0,180,4000,
				-10*fh,0,100
		del 5

		cutend
		cutend
		cutend

		add (1547/232)*fh, (13+1105/232)*fh, 0
		rotz 90+atn(5/13)
		roty 90
		addx -10*fh
		prism_  3, 0,
				0,0,15,
				5*fh,0,915,
				0, -180, 4000
		add 5*fh, 2*fh, 0
		cone 2*fh, 2*fh, 1*fh, 90,90
		del 5

		rotx -90
		addx 13.5*fh
		add (6+170/101)*fh, -10*fh, (18+510/101)*fh
		rotz 90
		rotx atn(20/27)
		rotz -90
		mulx 1/cos(atn(18/6)+atn(20/27)-90)
		prism_  3, 0,
				0,0,0,
				0, 5*fh,900,
				0, 180, 4000


		addy 5*fh
		resol 10
		prism_  2, fh,
				0, 0, 915,
				4*fh, 360, 4015
		resol 36
		addz fh
		cylind fh, 3.5*fh
		addz fh
		cylind 1.2*fh, 2.5*fh

		del 10

		del 3

		material mat
		add a/2-v-h-v/2, b-a/2, zzyzx-h
		cylind h/40, v/2
		del 1

		del 2
	ENDIF

	IF gs_detlevel_3D_m=1 THEN
		rotz 180
		!!!!!!!!!!!!!!!
		material cover_mat
		RESOL 12
		prism_  17, zzyzx,
				a/2, 0, 15,
				a/2, b-a/3, 79,
				a/3, 90, 2079,
				-a/2+a/3, b, 79,
				a/3, 90, 2079,
				-a/2, 0, 15,
				a/2, 0, -1,
				0, b-a+v, 4,
				a/2-a/3, b-a+v, 4,
				a/3-v, 90, 2004,
				a/2-v, b-a/3, 4,
				a/3-v, 90, 2004,
				-a/2+a/3, b-v, 4,
				a/3-v, 90, 2004,
				-a/2+v, b-a+a/3, 4,
				a/3-v, 90, 2004,
				0, b-a+v, -1

		prism_  10, 0,
				0, b-a+v, 0,
				a/2-a/3, b-a+v, 0,
				a/3-v, 90, 2000,
				a/2-v, b-a/3, 0,
				a/3-v, 90, 2000,
				-a/2+a/3, b-v, 0,
				a/3-v, 90, 2000,
				-a/2+v, b-a+a/3, 0,
				a/3-v, 90, 2000,
				0, b-a+v, -1

		add a/2-a/3, b-a/2, zzyzx-h
		roty -90

		!!-------!!
		!! Basin !!
		!!-------!!
		RESOL 8
		material gs_basin_mat
		extrude 6, 0, 0,a-a/3*2, 0,
				h, a/2-v, 1,
				h, a/2-v-h, 901,
				0, 90, 4001,
				0, -(a/2-v-h), 1,
				h, -(a/2-v-h), 901,
				0, 90, 4001

		del 2

		add 0, b-a+a/3,zzyzx-h-0.0001
		rotx -90
		rotz -90

		extrude 6, 0, 0, a-a/3*2, 0,
				h, a/2-v, 1,
				h, a/2-v-h, 901,
				0, 90, 4001,
				0, -(a/2-v-h), 1,
				h, -(a/2-v-h), 901,
				0, 90, 4001

		del 2

		addx a/2-a/3
		roty -90
		rotx 180
		RESOL 12
		for i=1 to 4
			revolve 4, 90, 0,
					0, 0, 1,
					0, a/3-v-h, 1,
					h-h*COS(45), a/3-h-v+h*SIN(45), 1,
					h, a/3-v, 1

!!					h, a/3-h-v, 901,
!!					0, -90, 4001
			addy -a/3
			rotx 90
		next i
		del 12

		!!-----!!
		!! Tap !!
		!!-----!!


		material gs_tap_mat
		add -a/2 + v/2, b-a/2-5*fh, zzyzx


		rotz 90
		MUL 0.5,0.5,0.5
		call "tapType_m" parameters all gs_tap_type_m		= 13,
										bEnableTapEdit		= 0,
										bSinkTap			= 0,
										useOldSimpleTaps	= 0,
										iTapGroup			= 4
endif


IF gs_detlevel_3D_m=3 THEN
	rotz 180
	!!!!!!!!!!!!!!!
	material cover_mat
	RESOL 8
	prism_  17, zzyzx,
			a/2, 0, 15,
			a/2, b-a/3, 79,
			a/3, 90, 2079,
			-a/2+a/3, b, 79,
			a/3, 90, 2079,
			-a/2, 0, 15,
			a/2, 0, -1,
			0, b-a+v, 4,
			a/2-a/3, b-a+v, 4,
			a/3-v, 90, 2004,
			a/2-v, b-a/3, 4,
			a/3-v, 90, 2004,
			-a/2+a/3, b-v, 4,
			a/3-v, 90, 2004,
			-a/2+v, b-a+a/3, 4,
			a/3-v, 90, 2004,
			0, b-a+v, -1

	prism_  10, 0,
			0, b-a+v, 0,
			a/2-a/3, b-a+v, 0,
			a/3-v, 90, 2000,
			a/2-v, b-a/3, 0,
			a/3-v, 90, 2000,
			-a/2+a/3, b-v, 0,
			a/3-v, 90, 2000,
			-a/2+v, b-a+a/3, 0,
			a/3-v, 90, 2000,
			0, b-a+v, -1

		add a/2-a/3, b-a/2, zzyzx-h
		roty -90

		!!-------!!
		!! Basin !!
		!!-------!!

		RESOL 8
		material gs_basin_mat
		extrude 4, 0, 0,a-a/3*2, 0,
				h, a/2-v, 1,
				0, a/2-v-h, 1,
				0, -(a/2-v-h), 1,
				h, -(a/2-v), 1

		del 2

		add 0, b-a+a/3,zzyzx-h-0.0001
		rotx -90
		rotz -90

		extrude 4, 0, 0, a-a/3*2, 0,
				h, a/2-v, 1,
				0, a/2-v-h, 1,
				0, -(a/2-v-h), 1,
				h, -(a/2-v), 1

		del 2

		addx a/2-a/3
		roty -90
		rotx 180
		for i=1 to 4
			revolve 3, 90, 0,
					0, 0, 1,
					0, a/3-v-h, 1,
!					h-h*COS(45), a/3-h-v+h*SIN(45), 1,
					h, a/3-v, 1

			addy -a/3
			rotx 90
		next i
		del 14
	ENDIF
ENDIF
del 1
