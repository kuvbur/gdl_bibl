
! Drinking Fountain Wall Hung 02

IF gs_detlevel_3D_m = 0 THEN END

IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN gs_detlevel_3D_m = 3

if gs_detlevel_3D_m = 2 then resol gs_resol
if gs_detlevel_3D_m = 1 then resol 12

if gs_shadow=0 then SHADOW OFF

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1


! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

IF gs_detlevel_3D_m=1 OR gs_detlevel_3D_m=3 THEN RESOL 8
IF gs_detlevel_3D_m=2 THEN RESOL 24

if bShowMEP then

	IF useSysMat THEN
		cover_mat 		= sMat
		top_mat			= sMat
		gs_basin_mat 	= sMat
		gs_tap_mat 		= sMat
		button_mat 		= sMat
		mat				= sMat
		gs_con_mat  	= sMat
	ENDIF

	addz h_top-zzyzx
	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16
	del 1
ENDIF

! --- end of modifying to Shaft


pen gs_cont_pen

! Hotspot Editing
unID=1

hotspot	0,0,h_top,unID : unID=unID+1

hotspot	0,0,0,unID,h_top,1 : unID=unID+1
hotspot	0,0,-1,unID,h_top,3 : unID=unID+1
hotspot	0,0,h_top,unID,h_top,2 : unID=unID+1

addz h_top
hotspot	-A*0.5,  0,0,unID,a,1+256 : unID=unID+1
hotspot	-A*0.5-1,0,0,unID,a,3 : unID=unID+1
hotspot	A*0.5,   0,0,unID,a,2 : unID=unID+1

hotspot	-a/2,0,0,unID,b,1+128 : unID=unID+1
hotspot	-a/2,1,0,unID,b,3 : unID=unID+1
hotspot	-a/2,-b,0,unID,b,2 : unID=unID+1

hotspot	a/2,0,0,unID,b,1+128 : unID=unID+1
hotspot	a/2,1,0,unID,b,3 : unID=unID+1
hotspot	a/2,-b,0,unID,b,2 : unID=unID+1

add -A*0.5, -B, 0
hotspot	0,b,0,unID,ZZYZX,1+128 : unID=unID+1
hotspot	0,b,1,unID,ZZYZX,3 : unID=unID+1
hotspot	0,b,-ZZYZX,unID,ZZYZX,2 : unID=unID+1

hotspot	a,b,0,unID,ZZYZX,1+128 : unID=unID+1
hotspot	a,b,1,unID,ZZYZX,3 : unID=unID+1
hotspot	a,b,-ZZYZX,unID,ZZYZX,2 : unID=unID+1
del 1

ADDZ -ZZYZX
rotz 180
rotz 90
material cover_mat
rotx 90
addz -a/2+lv

IF gs_detlevel_3D_m=3 THEN

	!!-----!!
	!! Box !!
	!!-----!!

	prism_  6, a-2*lv,
			0, zzyzx-2*r, 15,
			b-lv, 0, 115,
			b-lv, zzyzx-zh, 15,
			-2*b/5, 0, 115,
			2*b/5, 0, 15,
			0, 0, 15


	material top_mat
	addz -lv
	prism_  6, a,
			0, zzyzx, 15,
			b/2-r, 0, 115,

			b/2, zzyzx-r, 15,

			b/2, 0, 115,
			0, -r, 115,
			-b, 0, 115

	del 3

	addz zzyzx-2*r
	rotz -90
	prism_  10, 0,
			-a/2, 0, 15,
			a/2, 0, 15,
			a/2, b, 15,
			-a/2, b, 15,
			-a/2, 0, -1,
			-a/2+lv, lv, 15,
			a/2-lv, lv, 15,
			a/2-lv, b-lv, 15,
			-a/2+lv, b-lv, 15,
			-a/2+lv, lv, -1

	del 5
ELSE

	!!-----!!
	!! Box !!
	!!-----!!

	prism_  6, a-2*lv,
			0, zzyzx-2*r, 7,
			b-lv, 0, 115,
			b-lv, zzyzx-zh, 15,
			-2*b/5, 0, 115,
			2*b/5, 0, 15,
			0, 0, 15

	IF gs_detlevel_3D_m=2 THEN
		material mat
		add b/20, zzyzx/3, -lv/2
		for k=1 to 2
			for j=1 to 2
				for i=1 to 15
					block 3/20*b, 1/60*zzyzx, lv/2
					addy 1/30*zzyzx
				next i
				del 15
				addx (3/20+2/40)*b
			next j
			del 2
			addz a-1.5*lv
		next k
		del 3
	endif

	material top_mat
	addz -lv
	prism_  8, a,
			0, zzyzx, 15,
			b/2-r-0.00001, 0, 115,
			0.00001,0,179,
			b/2, zzyzx, 901,
			0, 90, 4000,
			b/2, 0, 113,
			0, -r, 107,
			-b, 0, 115

	del 3

	addz zzyzx-2*r
	rotz -90
	prism_  10, 0,
			-a/2, 0, 15,
			a/2, 0, 15,
			a/2, b, 15,
			-a/2, b, 15,
			-a/2, 0, -1,
			-a/2+lv, lv, 15,
			a/2-lv, lv, 15,
			a/2-lv, b-lv, 15,
			-a/2+lv, b-lv, 15,
			-a/2+lv, lv, -1

	del 2

	!!!!!!!!!!!!!!!
	material gs_basin_mat
	addz zzyzx-r-t
	rotz -90
	prism_  10, t,
			-a/2, b/2, 0,
			a/2, b/2, 4,
			a/2, b, 4,
			-a/2, b, 4,
			-a/2, b/2, -1,
			-a/2+v, b/2+v, 14,
			a/2-v, b/2+v, 14,
			a/2-v, b-v, 14,
			-a/2+v, b-v, 14,
			-a/2+v, b/2+v, -1

	prism_  4, 0,
			-a/2+v, b/2+v, 15,
			-a/2+s, b/2+v, 0,
			-a/2+s, b-v, 15,
			-a/2+v, b-v, 15

	add -a/2+s, b-(b-b/2)/2, 0
	roty 90
	rotz -90

	IF gs_detlevel_3D_m=2 THEN
		ruled   13, 0,
				c*cos(0), 0, 1,
				c*cos(15), 0, 1,
				c*cos(30), 0, 1,
				c*cos(45), 0, 1,
				c*cos(60), 0, 1,
				c*cos(75), 0, 1,
				c*cos(90), 0, 1,
				c*cos(105), 0, 1,
				c*cos(120), 0, 1,
				c*cos(135), 0, 1,
				c*cos(150), 0, 1,
				c*cos(175), 0, 1,
				c*cos(180), 0, 1,

				c*cos(0), c/cc*sin(0), z,
				c*cos(15), c/cc*sin(15), z,
				c*cos(30), c/cc*sin(30), z,
				c*cos(45), c/cc*sin(45), z,
				c*cos(60), c/cc*sin(60), z,
				c*cos(75), c/cc*sin(75), z,
				c*cos(90), c/cc*sin(90), z,
				c*cos(105), c/cc*sin(105), z,
				c*cos(120), c/cc*sin(120), z,
				c*cos(135), c/cc*sin(135), z,
				c*cos(150), c/cc*sin(150), z,
				c*cos(175), c/cc*sin(175), z,
				c*cos(180), c/cc*sin(180), z
	ELSE
		ruled   5, 0,
				c*cos(0), 0, 1,
				c*cos(45), 0, 1,
				c*cos(90), 0, 1,
				c*cos(135), 0, 1,
				c*cos(180), 0, 1,

				c*cos(0), c/cc*sin(0), z,
				c*cos(45), c/cc*sin(45), z,
				c*cos(90), c/cc*sin(90), z,
				c*cos(135), c/cc*sin(135), z,
				c*cos(180), c/cc*sin(180), z
	ENDIF

	addz z
	IF gs_detlevel_3D_m=2 THEN
		prism_  13, 0,
				c*cos(0), c/cc*sin(0), 15,
				c*cos(15), c/cc*sin(15), 15,
				c*cos(30), c/cc*sin(30), 15,
				c*cos(45), c/cc*sin(45), 15,
				c*cos(60), c/cc*sin(60), 15,
				c*cos(75), c/cc*sin(75), 15,
				c*cos(90), c/cc*sin(90), 15,
				c*cos(105), c/cc*sin(105), 15,
				c*cos(120), c/cc*sin(120), 15,
				c*cos(135), c/cc*sin(135), 15,
				c*cos(150), c/cc*sin(150), 15,
				c*cos(175), c/cc*sin(175), 15,
				c*cos(180), c/cc*sin(180), 0
	ELSE
		prism_  5, 0,
				c*cos(0), c/cc*sin(0), 15,
				c*cos(45), c/cc*sin(45), 15,
				c*cos(90), c/cc*sin(90), 15,
				c*cos(135), c/cc*sin(135), 15,
				c*cos(180), c/cc*sin(180), 0
	ENDIF
	del 6

	!!!!!!!!!!!!!!!

	IF gs_detlevel_3D_m=2 THEN
		add b-lv, -a/2+r+lv, zzyzx-zp
		block lv, a/4, 1.5*r
		del 1
		add b-lv, a/2-r-a/4-lv, zzyzx-zp
		block lv, a/4, 1.5*r
		del 1

		material button_mat
		add b, -a/2+r+0.25*r+lv, zzyzx-zp+0.25*r
		block 2*lv, a/4-0.5*r, r
		del 1
		add b, a/2-r-a/4+0.25*r-lv, zzyzx-zp+0.25*r
		block 2*lv, a/4-0.5*r, r
		del 1
	endif
	del 1

	material mat
	add a/2-v-v/2, b-(b-b/2)/2, zzyzx-r-t-c/cc+t/3
	cylind t/15, v/3
	del 2

	!!-----!!
	!! Tap !!
	!!-----!!

	material gs_tap_mat
	add a/2-v-0.05, -b+(b-b/2)/2, zzyzx-r-t
	rotz -90
	MUL 0.7,0.7,0.7
	call "tapType_m" parameters all gs_tap_type_m		= 13,
									bEnableTapEdit		= 0,
									bSinkTap			= 0,
									useOldSimpleTaps	= 0,
									iTapGroup			= 4
	DEL 1
	del 3
ENDIF
DEL 1
end
