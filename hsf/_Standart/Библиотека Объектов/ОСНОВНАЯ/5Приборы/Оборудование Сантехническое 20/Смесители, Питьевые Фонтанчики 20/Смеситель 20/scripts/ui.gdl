
! ==============================================================================
! Tap
! ==============================================================================

UI_CURRENT_PAGE gs_ui_current_page



UI_DIALOG `Параметры Смесителя`

DIM ui_matpar_names[1][2]
	ui_matpar_names[1][1] = `Смеситель`
	ui_matpar_names[1][2] = "gs_tap_mat"

DIM ui_matpar_locked[2]
	ui_matpar_locked[1] = 0
	ui_matpar_locked[2] = 0

IF gs_tap_type_m=0 THEN ui_matpar_locked[2] = 1


iPageIDRoot 			=-1
iPageIDSink				= 1
iPageIDRepAndSurface	= 2

pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

UI_PAGE iPageIDSink, iPageIDRoot, `Стиль Смесителя`, "uiTab_Style_flat(1)"
	gosub "sinkPage"


UI_PAGE iPageIDRepAndSurface, iPageIDRoot, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "repAndSurfacePage"


! ==============================================================================
END
! ==============================================================================


! ==============================================================================
!SUBROUTINES
! ==============================================================================

! ------------------------------------------------------------------------------
"sinkPage":
! ------------------------------------------------------------------------------

	xPos1	= 8    					! start Position X Coordinate Left
	yCurr	= pageStartY + 7   		! start Position Y Coordinate
	diffY	= 25					! Line Height
	xSep	= 202					! separator Position X Coordinate
	xPos2	= xSep + 10				! start Position X Coordinate Right
	wLeft	= xSep - xPos1 - 10		! Left Block Width
	wRight	= 444 - xPos2 - 1		! Right Block Width
	infHgt	= 125					! height of cell


	ui_separator xSep, pageStartY + 9, xSep, pageStartY + 73


	ui_outfield `Тип`, xPos1, yCurr, wLeft, 15

	yCurr = yCurr + diffY

	ui_infield{3} "iTapGroup", xPos1, yCurr - 4, wLeft, 19,
			8, "", 0, 0,
			0, 0, 0, 0,
			0, `Однорычажный Смеситель`,			TAPGROUP_SINGLE,
			0, `Смеситель с Двумя Вентилями`,				TAPGROUP_TWOHANDLE,
			0, `Традиционный Смеситель с Опорой`,	TAPGROUP_TRADITIONAL,
			0, `Сенсорный Кран`,					TAPGROUP_SENSOR,
			0, `Крепление к Стене`,					TAPGROUP_WALLMOUNTED,
			0, `Нет`,							TAPGROUP_NONE

	yCurr = yCurr + diffY

	ui_outfield `Стиль`, xPos1, yCurr, wLeft, 15

	yCurr = yCurr + diffY

	if iTapGroup = TAPGROUP_SINGLE then
		ui_infield{3} "gs_tap_type_m", xPos1 , yCurr, 435, infHgt,
			1, "ui_taptypes_2(4)", 35, 7,
			66, 120, 60, 90,
			1,	`Стиль 1`,	1,
			3,	`Стиль 2`,	3,
			7,	`Стиль 3`,	7,
			16,	`Стиль 4`,	16,
			17,	`Стиль 5`,	17,
			28,	`Простой`,	-1
	endif

	if iTapGroup = TAPGROUP_TWOHANDLE then
		ui_infield{3} "gs_tap_type_m", xPos1 , yCurr, 435, infHgt,
			1, "ui_taptypes_2(4)", 35, 7,
			66, 120, 60, 90,
			2,	`Стиль 1`,	2,
			4,	`Стиль 2`,	4,
			5,	`Стиль 3`,	5,
			6,	`Стиль 4`,	6,
			9,	`Стиль 5`,	9,
			10,	`Стиль 6`,	10,
			11,	`Стиль 7`,	11,
			12,	`Стиль 8`,	12,
			18,	`Стиль 9`,	18,
			19,	`Стиль 10`,	19,
			29,	`Простой`,	-1
	endif

	if iTapGroup = TAPGROUP_TRADITIONAL then
		ui_infield{3} "gs_tap_type_m", xPos1, yCurr, 435, infHgt,
			1, "ui_taptypes_2(4)", 35, 7,
			80, 120, 60, 90,
			8,	`Стиль 1`,	8,
			14,	`Стиль 2`,	14,
			15,	`Стиль 3`,	15,
			30,	`Простой`,	-1
	endif

	if iTapGroup = TAPGROUP_SENSOR then
		ui_infield{3} "gs_tap_type_m", xPos1, yCurr, 435, infHgt,
			1, "ui_taptypes_2(4)", 35, 7,
			80, 120, 60, 90,
			13,	`Стиль 1`,	13,
			20,	`Стиль 2`,	20,
			31,	`Простой`,	-1
	endif

	yCurr = pageStarty + 7

	ui_outfield	`Монтажная Высота`,	xPos2,				yCurr,		wRight-82,	15
	ui_infield	"mountingHeight",	xPos2+wRight-80,	yCurr-4,	80,			19

	yCurr = yCurr + diffY

	if gs_tap_type_m = 16 | gs_tap_type_m = 18 | gs_tap_type_m = 19 then
		ui_infield{3} "bShower", xPos2, yCurr-4, wRight, 19,
			7, "",
			2,
			1, 19, 19, 19, 19,
			0, `Душевой Комплект`, 0,
			1, `Душевой Комплект`, 1
	endif

return

! ------------------------------------------------------------------------------
"repAndSurfacePage":
! ------------------------------------------------------------------------------

	CALL "ui_visualizationtab" PARAMETERS ALL 	iObjectType = 100,
												bEnableCenterlinePars = 0,
												bEnable2dDetail = 1,
												bEnable3dDetail = 1,
												bEnableUseSystemMat = 0,
												ui_matpar_names = ui_matpar_names,
												ui_matpar_locked = ui_matpar_locked,
												ui_3dviewpar_rowspace = 12

return

