
! ==============================================================================
! Wall Mounted Tap
! ==============================================================================

UI_CURRENT_PAGE gs_ui_current_page



UI_DIALOG `Параметры Настенного Смесителя`

if isMEPEnabled then
	DIM ui_matpar_names[2][2]
		ui_matpar_names[1][1] = `Смеситель`
		ui_matpar_names[2][1] = `Соединение`

		ui_matpar_names[1][2] = "gs_tap_mat"
		ui_matpar_names[2][2] = "gs_con_mat"

	DIM ui_matpar_locked[3]
		ui_matpar_locked[1] = 0
		ui_matpar_locked[2] = 0
		ui_matpar_locked[3] = (NOT(ConStatus_1) AND NOT(ConStatus_2))
else
	DIM ui_matpar_names[1][2]
		ui_matpar_names[1][1] = `Смеситель`
		ui_matpar_names[1][2] = "gs_tap_mat"

	DIM ui_matpar_locked[2]
		ui_matpar_locked[1] = 0
		ui_matpar_locked[2] = 0
endif



IF gs_tap_type_m=0 THEN ui_matpar_locked[2] = 1


iPageIDRoot 			=-1
iPageIDTap				= 1
iPageIDRepAndSurface	= 2
iPageIDConnections		= 3

pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

	UI_PAGE iPageIDTap, iPageIDRoot, `Стиль Смесителя`, "uiTab_Style_flat(1)"
		gosub "tapPage"


	UI_PAGE iPageIDRepAndSurface, iPageIDRoot, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
		gosub "repAndSurfacePage"


if isMEPEnabled then
	UI_PAGE iPageIDConnections, iPageIDRoot, `Соединения`, "uiTab_Connections_flat(1)"
		gosub "connectionsPage"
endif

! ==============================================================================
END
! ==============================================================================


! ==============================================================================
!SUBROUTINES
! ==============================================================================

! ------------------------------------------------------------------------------
"tapPage":
! ------------------------------------------------------------------------------

	xPos1	= 8    					! start Position X Coordinate 1
	xPos2	= xPos1 + 122			! start Position X Coordinate 2
	wLeft	= 202					! Left Block Width
	yCurr	= pageStartY + 7   		! start Position Y Coordinate
	diffY	= 25					! Line Height
	infHgt	= 125					! height of cell


	ui_outfield	`Монтажная Высота`,	xPos1,	yCurr,		120,	15
	ui_infield	"mountingHeight",	xPos2,	yCurr-4,	80,		19

	yCurr = yCurr + diffY

	if gs_tap_type_m = 21 then
		ui_infield{3} "bShower", xPos1, yCurr-4, wLeft, 19,
			7, "", 2, 1,
			19, 19, 19, 19,
			1, `Душевой Комплект`, 0,
			2, `Душевой Комплект`, 1
	endif

	yCurr = yCurr + diffY

	ui_outfield `Стиль`, xPos1, yCurr, wLeft, 15

	yCurr = yCurr + diffY

	ui_infield{3} "gs_tap_type_m", xPos1, yCurr-4, 435, infHgt,
		1,"ui_taptypes_2(4)", 35, 7,
		66, 120, 60, 90,
		21,	`Стиль 1`,	21,
		22,	`Стиль 2`,	22,
		23,	`Стиль 3`,	23,
		33,	`Простой`,	-1

return

! ------------------------------------------------------------------------------
"repAndSurfacePage":
! ------------------------------------------------------------------------------

	CALL "ui_visualizationtab" PARAMETERS ALL 	iObjectType = 100,
												bEnableCenterlinePars = 0,
												bEnable2dDetail = 1,
												bEnable3dDetail = 1,
												bEnableUseSystemMat = 0,
												ui_matpar_names = ui_matpar_names,
												ui_matpar_locked = ui_matpar_locked,
												ui_3dviewpar_rowspace = 2

return

! ------------------------------------------------------------------------------
"connectionsPage":
! ------------------------------------------------------------------------------

	DIM ui_con_type[2]
		ui_con_type[1] = 0
		ui_con_type[2] = 0

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS ALL ui_con_type = ui_con_type,
												ui_system_var_pos = 2

return


