
ac_toplevel = h_top
ac_bottomlevel = ac_toplevel - zzyzx
parameters ac_toplevel = ac_toplevel, ac_bottomlevel = ac_bottomlevel

call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle"  1, 2, 3, 4, 5, 6

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	if stStyle = stStyles[3] then iStyle = 3
	if stStyle = stStyles[4] then iStyle = 4
	if stStyle = stStyles[5] then iStyle = 5
	if stStyle = stStyles[6] then iStyle = 6
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	if iStyle = 3 then stStyle = stStyles[3]
	if iStyle = 4 then stStyle = stStyles[4]
	if iStyle = 5 then stStyle = stStyles[5]
	if iStyle = 6 then stStyle = stStyles[6]
	parameters stStyle = stStyle
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------

dim st_detail3D[4]
	st_detail3D[1] = `Детальный`
	st_detail3D[2] = `Простой`
	st_detail3D[3] = `Упрощенный`
	st_detail3D[4] = `Откл.`

values "gs_detlevel_3D" st_detail3D		!`Detailed`,`Simple`,`Draft`,`Off`
values "gs_detlevel_3D_m" 2, 1, 3, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2	! Detailed
	if gs_detlevel_3D = st_detail3D[4] then gs_detlevel_3D_m = 0
	if gs_detlevel_3D = st_detail3D[2] then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = st_detail3D[3] then gs_detlevel_3D_m = 3
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = st_detail3D[1]
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = st_detail3D[4]
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = st_detail3D[2]
	if gs_detlevel_3D_m = 3 then gs_detlevel_3D = st_detail3D[3]
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! -----------------------------------------------------------------------------
! Default Sizes
! -----------------------------------------------------------------------------

if istyle = 6 then
	values "A" range[0.20, ]
	values "A2" range [0.10, ]
	values "B" range [0.25, ]
	values "ZZYZX" range[0.101, ]
	values "h_top" range[0, ]
else
	values "A" range[0.20, ]
	values "A2" range [0.10, ]
	values "B" range[0.10, ]
	values "ZZYZX" range[0.05, ]
	values "h_top" range[0, ]
endif

if GLOB_MODPAR_NAME = "A2" then
	A = 2 * A2
	parameters A = A
else
	A2 = A / 2
	parameters A2 = A2
endif


if GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then

	a = defaultSizes[iStyle][1]
	b = defaultSizes[iStyle][2]
	ZZYZX = defaultSizes[iStyle][3]
	h_top = defaultSizes[iStyle][4]
	tapHeight = defaultSizes[iStyle][5]

	parameters a = a, b = b, ZZYZX = ZZYZX, h_top = h_top, tapHeight = tapHeight, a2 = a / 2

	ConPosX_1 = defaultConnectionPos[iStyle][1]
	ConPosY_1 = defaultConnectionPos[iStyle][2]
	ConPosZ_1 = defaultConnectionPos[iStyle][3]
	ConPosX_2 = defaultConnectionPos[iStyle][4]
	ConPosY_2 = defaultConnectionPos[iStyle][5]
	ConPosZ_2 = defaultConnectionPos[iStyle][6]
	ConPosX_3 = defaultConnectionPos[iStyle][7]
	ConPosY_3 = defaultConnectionPos[iStyle][8]
	ConPosZ_3 = defaultConnectionPos[iStyle][9]

	parameters ConPosX_1 = ConPosX_1, ConPosY_1 = ConPosY_1 , ConPosZ_1 = ConPosZ_1
	parameters ConPosX_2 = ConPosX_2, ConPosY_2 = ConPosY_2 , ConPosZ_2 = ConPosZ_2
	parameters ConPosX_3 = ConPosX_3, ConPosY_3 = ConPosY_3 , ConPosZ_3 = ConPosZ_3

endif


! -----------------------------------------------------------------------------
! Symbol Types
! -----------------------------------------------------------------------------

values "gs_symbol_type" stSymbolTypes
values "iSymbolType" 1, 2

if GLOB_MODPAR_NAME = "gs_symbol_type" then
	iSymbolType = 1
	if gs_symbol_type = stSymbolTypes[2] then iSymbolType = 2
	parameters iSymbolType = iSymbolType
else
	gs_symbol_type = stSymbolTypes[2]
	if iSymbolType = 1 then gs_symbol_type = stSymbolTypes[1]
	parameters gs_symbol_type = gs_symbol_type
endif

if GLOB_MODPAR_NAME = "gs_symbol_type" and iStyle = 1 then
	if iStyle = 1 then
		if iSymbolType = 1 then : a = 0.460 : b = 0.373 : else : a = 0.460 : b = 0.330 : endif
	endif
	if iStyle = 3 then
		if iSymbolType = 1 then : a = 0.600 : b = 0.510 : else : a = 0.600 : b = 0.450 : endif
	endif
	parameters A = A, B = B
endif

if gs_symbol_type_enable and iSymbolType = 1 then
	hideparameter "gs_detlevel_2D"
	lock "gs_detlevel_2D"
endif
if not(gs_symbol_type_enable) then
	hideparameter "gs_symbol_type"
	lock "gs_symbol_type"
endif

! -----------------------------------------------------------------------------
! 2D detlevel
! -----------------------------------------------------------------------------

call "2dDetailLevel" parameters	all enable2dDetLevels	= 2 + 4 + 8 + 16

! -----------------------------------------------------------------------------
! Tap types
! -----------------------------------------------------------------------------

if iStyle = 1 then : yTapRangeMin = -0.035 : endif
if iStyle = 2 then : yTapRangeMin = -yWallPos - (0.125 * (B / 0.48)) : endif
if iStyle = 3 then : yTapRangeMin = -0.035 : endif
if iStyle = 4 then : yTapRangeMin = -yWallPos - (0.1 * (b/0.475902989507)) : endif
if iStyle = 5 then : yTapRangeMin = -yWallPos - (0.145 * (b/0.58)) : endif
if iStyle = 6 then : yTapRangeMin = -0.05  : endif

call "tapType_m" parameters	all	enableTapGroups			= 2 + 4 + 8 + 16 + 32 + 64,
								enableSingleLevelTaps	= 2 + 2**3 + 2**5 + 2**9 + 2**18 + 2**19,
								enableTwoHandleTaps		= 2 + 2**4 + 2**6 + 2**7 + 2**8 + 2**11 + 2**12 + 2**13 + 2**14 + 2**20 + 2**21,
								enableTraditionalTaps	= 2 + 2**10 + 2**16 + 2**17,
								enableSensorFaucets		= 2 + 2**15 + 2**22,
								enableWallMountedTaps	= 2 + 2**23 + 2**24 + 2**25,
								enableCustomTaps		= 0,
								bEnableTapEdit			= 1,
								bShower 				= 0,
								enableTapSymbols		= 2 + 4 + 8 + 32,
								xTapRangeMin 			= -A/2,
								xTapRangeMax 			= A/2,
								yTapRangeMin 			= yTapRangeMin,
								yTapRangeMax			= -yWallPos,
								zTapRangeMin 			= h_top - tapHeight

! -----------------------------------------------------------------------------
! Support types
! -----------------------------------------------------------------------------

dim stSuptyp[4]
	stSuptyp[1] = `Пьедестал`
	stSuptyp[2] = `Полупьедестал`
	stSuptyp[3] = `Настенный`
	stSuptyp[4] = `нет`


if stStyle <> stStyles[3] then
	values "suptyp" stSuptyp[4]
	values "isuptyp" 0
else
	values "suptyp" stSuptyp[1], stSuptyp[2], stSuptyp[3]
	values "isuptyp" 1, 2, 3
endif

if GLOB_MODPAR_NAME = "suptyp" then
	isuptyp = 1
	if suptyp = stSuptyp[2] then isuptyp = 2
	if suptyp = stSuptyp[3] then isuptyp = 3
	if suptyp = stSuptyp[4] then isuptyp = 0
	parameters isuptyp = isuptyp
else
	suptyp = stSuptyp[1]
	if isuptyp = 2 then suptyp = stSuptyp[2]
	if isuptyp = 3 then suptyp = stSuptyp[3]
	if isuptyp = 0 then suptyp = stSuptyp[4]
	parameters suptyp = suptyp
endif

if iStyle = 3 and suptyp <> stSuptyp[4] then
	if GLOB_MODPAR_NAME = "suptyp" then
		for iSup = 1 to vardim1(stSuptyp) - 1
			if suptyp = stSuptyp[iSup] then isuptyp = iSup
		next iSup
		parameters isuptyp = isuptyp
	else
		suptyp = stSuptyp[isuptyp]
		parameters suptyp = suptyp
	endif

	if iSuptyp = PEDESTAL then
		if GLOB_MODPAR_NAME = "ZZYZX" then
			h_top = ZZYZX
			parameters h_top = h_top
		else
			ZZYZX = h_top
			parameters ZZYZX = ZZYZX
		endif
	else
		if iSuptyp = SEMI_PEDESTAL then
			parameters ZZYZX = 0.25+0.17
		else		! `Wall-Mount`
			parameters ZZYZX = 0.17
		endif
		lock "ZZYZX"
	endif
endif


! -----------------------------------------------------------------------------
! LOCK and HIDEPARAMETERS
! -----------------------------------------------------------------------------

if gs_tap_type_m = 0 or not(bWallMountedTap) then
	lock "tapHeight"
	hideparameter "tapHeight"
else
	values "tapHeight" range[h_top + 0.1,]
endif

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif

if iStyle <> 3 then hideparameter "suptyp"

! -----------------------------------------------------------------------------
! Limitation
! -----------------------------------------------------------------------------

if gs_cont_pen = 0 then parameters gs_cont_pen = 75
if gs_fill_pen = 0 then parameters gs_fill_pen = 19
values "gs_resol" range [4,)



! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------

if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif
if MSSide < 0 then
	MSSide = 0
	parameters MSSide = MSSide
endif
if MSSide2 < 0 then
	MSSide2 = 0
	parameters MSSide2 = MSSide2
endif

! -----------------------------------------------------------------------------
! modify to Shaft
! -----------------------------------------------------------------------------

if isMEPEnabled then

	if not(ConStatus_1) and not(ConStatus_2) and not(ConStatus_3) then
		lock "gs_con_mat"
		hideparameter "gs_con_mat"
	endif

	dim shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	parameters MEP_NumberConnections = 3
	if iStyle = 1  | iStyle = 2  | iStyle = 6 then	ConWidthNumMax = ZZYZX
	if iStyle = 3 then	ConWidthNumMax = 0.17
	if iStyle = 4  | iStyle = 5  then	ConWidthNumMax = a / 2

					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Подключение Горячей Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0


	if iStyle = 5 then
		if ConWidth_1 < 0.001 then
			ConWidth_1 = 0.001
			parameters ConWidth_1 = ConWidth_1
		endif
		if ConWidth_1 > A/2 then
			ConWidth_1 = A/2
			parameters ConWidth_1 = ConWidth_1
		endif
		values "ConWidth_1" RANGE (0, A/2]
	else
		if ConWidth_1 < 0.001 then
			ConWidth_1 = 0.001
			parameters ConWidth_1 = ConWidth_1
		endif
		if ConWidth_1 > ConWidthNumMax then
			ConWidth_1 = ConWidthNumMax
			parameters ConWidth_1 = ConWidth_1
		endif
		values "ConWidth_1" range (0, ConWidthNumMax]
	endif

	parameters ConDepth_1 = ConWidth_1
	values "ConLength_1" range [0, ]
	parameters ConPosY_1 = 0


	! Default Position
	if (GLOB_MODPAR_NAME = "gs_tap_type" | GLOB_MODPAR_NAME = "gs_tap_type_m" | \
	GLOB_MODPAR_NAME = "tapGroup" | GLOB_MODPAR_NAME = "iTapGroup") then
		if bWallMountedTap then
			ConPosX_1 = posTap + widthTap/2
		else
			if iStyle = 5 then
				ConPosX_1 = -0.09
			else
				ConPosX_1 = -0.05
			endif
		endif
		parameters ConPosX_1 = ConPosX_1
	endif


	if bWallmountedTap then

		pos1st = -posTap - widthTap + ConWidth_1/2
		pos2nd = -posTap - ConWidth_1/2

		pos3rd = +posTap + ConWidth_1/2
		pos4th = +posTap + widthTap - ConWidth_1/2

		! --- X range

		values "ConPosX_1"	range [pos1st, pos2nd],
							range [pos3rd, pos4th]


		! --- Change Connection Side
		if GLOB_MODPAR_NAME = "ConPosX_2" then
			if ConPosX_2 < 0 & ConPosX_1 < 0 then
				ConPosX_1 = posTap + widthTap/2
				parameters 	ConPosX_1 = ConPosX_1
			endif
			if ConPosX_2 > 0 & ConPosX_1 > 0 then
				ConPosX_1 = -(posTap + widthTap/2)
				parameters 	ConPosX_1 = ConPosX_1
			endif
		endif

		if iStyle = 5 then
			ConPosZ_1 = tapHeight - h_top
		else
			ConPosZ_1 = ZZYZX + tapHeight - h_top
		endif
		parameters	ConPosX_1 = ConPosX_1,
					ConPosZ_1 = ConPosZ_1
	else
		if iStyle <> 3 and iStyle <> 4 and iStyle <> 5 then
			values "ConPosX_1" range [-A/2+ConWidth_1/2, A/2-ConWidth_1/2]
			values "ConPosZ_1" range [ZZYZX/2, ZZYZX-ConWidth_1/2]
		endif
		if iStyle = 3 then
			ConPosZ_1 = h_top-0.045
			parameters ConPosZ_1 = ConPosZ_1
		endif
		if iStyle = 4 then
			values "ConPosX_1" range [-0.262*(A/0.600722)+ConWidth_1/2, 0.262*(A/0.600722)-ConWidth_1/2]
			values "ConPosZ_1" range [, -ConWidth_1/2]
		endif
		if iStyle = 5 then
			values "ConPosX_1" RANGE [-A/2+ConWidth_1/2, A/2-ConWidth_1/2]
			values "ConPosZ_1" RANGE [, -ConWidth_1/2]
		endif
	endif

	if iStyle = 5 then
		corrPosZ1 = h_top
	else
		corrPosZ1 = h_top - ZZYZX
	endif

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1 + corrPosZ1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Подключение Холодной Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if iStyle = 5 then
		if ConWidth_2 < 0.001 then
			ConWidth_2 = 0.001
			parameters ConWidth_2 = ConWidth_2
		endif
		if ConWidth_2 > A/2 then
			ConWidth_2 = A/2
			parameters ConWidth_2 = ConWidth_2
		endif
		values "ConWidth_2" range (0, A/2]
	else
		if ConWidth_2 < 0.001 then
			ConWidth_2 = 0.001
			parameters ConWidth_2 = ConWidth_2
		endif
		if ConWidth_2 > ConWidthNumMax then
			ConWidth_2 = ConWidthNumMax
			parameters ConWidth_2 = ConWidth_2
		endif
		values "ConWidth_2" range (0, ConWidthNumMax]
	endif

	parameters ConDepth_2 = ConWidth_2
	values "ConLength_2" range [0, ]
	parameters ConPosY_2 = 0


	! Default Position
	if (GLOB_MODPAR_NAME = "gs_tap_type" | GLOB_MODPAR_NAME = "gs_tap_type_m" | \
			GLOB_MODPAR_NAME = "tapGroup" | GLOB_MODPAR_NAME = "iTapGroup") then
		if bWallMountedTap then
			ConPosX_2 = -posTap - widthTap/2
		else
			if iStyle = 5 then
				ConPosX_2 = -0.09
			else
				ConPosX_2 = -0.05
			endif
		endif
		parameters ConPosX_2 = ConPosX_2
	endif

	if bWallmountedTap then

		pos1st = -posTap - widthTap + ConWidth_2/2
		pos2nd = -posTap - ConWidth_2/2

		pos3rd = +posTap + ConWidth_2/2
		pos4th = +posTap + widthTap - ConWidth_2/2

		! --- X range

		values "ConPosX_2"	range [pos1st, pos2nd],
							range [pos3rd, pos4th]

		! --- Change Connection Side
		if GLOB_MODPAR_NAME = "ConPosX_1" then
			if ConPosX_1 < 0 & ConPosX_2 < 0 then
				ConPosX_2 = posTap + widthTap/2
				parameters 	ConPosX_2 = ConPosX_2
			endif
			if ConPosX_1 > 0 & ConPosX_2 > 0 then
				ConPosX_2 = -(posTap + widthTap/2)
				parameters 	ConPosX_2 = ConPosX_2
			endif
		endif

		if iStyle <> 3 and iStyle <> 5 then
			ConPosZ_2 = ZZYZX + tapHeight - h_top
		endif
		if iStyle = 3 then
			ConPosZ_2 = tapHeight
		endif
		if iStyle = 5 then
			ConPosZ_2 = tapHeight - h_top
		endif
		parameters	ConPosX_2 = ConPosX_2,
					ConPosZ_2 = ConPosZ_2
	else
		if iStyle <> 3 and iStyle <> 4 and iStyle <> 5 then
			values "ConPosX_2" range [-A/2+ConWidth_2/2, A/2-ConWidth_2/2]
			values "ConPosZ_2" range [ZZYZX/2, ZZYZX-ConWidth_2/2]
		endif
		if iStyle = 3 then
			ConPosZ_2 = h_top-0.045
			parameters ConPosZ_2 = ConPosZ_2
		endif
		if iStyle = 4 then
			values "ConPosX_2" range [-0.262*(A/0.600722)+ConWidth_2/2, 0.262*(A/0.600722)-ConWidth_2/2]
			values "ConPosZ_2" range [, -ConWidth_2/2]
		endif
		if iStyle = 5 then
			values "ConPosX_2" range [-A/2+ConWidth_2/2, A/2-ConWidth_2/2]
			values "ConPosZ_2" range [, -ConWidth_2/2]
		endif
	endif

	if iStyle = 5 then
		corrPosZ2 = h_top
	else
		corrPosZ2 = h_top - ZZYZX
	endif

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2,
		ConPosY = ConPosY_2,
		ConPosZ = ConPosZ_2 + corrPosZ2,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 0

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


					! --- 3rd CONNECTION --- !

	ConID = 3
	ConName = `Подключение Канализации`

	if iStyle <> 3 then
		ConDVecX = 0
		ConDVecY = 0
		ConDVecZ = -1

		ConWVecX = -1
		ConWVecY = 0
		ConWVecZ = 0

		if ConWidth_3 < 0.001 then
			ConWidth_3 = 0.001
			parameters ConWidth_3 = ConWidth_3
		endif
		if iStyle = 1 | iStyle = 2 | iStyle = 3 | iStyle = 6 then
			if ConWidth_3 > min (A/3, B/3) then
				ConWidth_3 = min (A/3, B/3)
				parameters ConWidth_3 = ConWidth_3
			endif
			values "ConWidth_3" range (0, MIN(A/3, B/3)]
		endif
		if iStyle = 4 then
			if ConWidth_3 > 0.06*(min (A / 0.600722, B / 0.475903)) then
				ConWidth_3 = 0.06*(min (A / 0.600722, B / 0.475903))
				parameters ConWidth_3 = ConWidth_3
			endif
			values "ConWidth_3" range (0, 0.06*(min (A / 0.600722, B / 0.475903))]
		endif
		if iStyle = 5 then
			if ConWidth_3 > A/15 then
				ConWidth_3 = A/15
				parameters ConWidth_3 = ConWidth_3
			endif
			values "ConWidth_3" range (0, A/15]
		endif

		parameters ConDepth_3 = ConWidth_3
		values "ConLength_3" range [0, ]


		parameters ConPosX_3 = 0
		if iStyle <> 3 then parameters ConPosY_3 = _drainPosY
		if iStyle <> 5 then
			parameters ConPosZ_3 = 0
		else
			parameters ConPosZ_3 = ZZYZX*0.6 - (h+h/2+h/4+h/8+h/16+h/32)
		endif
	else
		if ConWidth_3 < 0.001 then
			ConWidth_3 = 0.001
			parameters ConWidth_3 = ConWidth_3
		endif

		if iSuptyp = WALL_MOUNT then
			if ConWidth_3 > 0.0175*2*(B/0.33) then
				ConWidth_3 = 0.0175*2*(B/0.33)
				parameters ConWidth_3 = ConWidth_3
			endif
			values "ConWidth_3" range (0, 0.0175*2*(B/0.33)]
			parameters ConDepth_3 = ConWidth_3
			values "ConLength_3" range [0, ]


			parameters ConPosX_3 = 0
			parameters ConPosY_3 = -0.1519*(B/0.33)
			parameters ConPosZ_3 = h_top-0.17

			ConDVecX = 0
			ConDVecY = 0
			ConDVecZ = -1

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0
		endif

		if iSuptyp = SEMI_PEDESTAL then
			if ConWidth_3 > 0.115 then
				ConWidth_3 = 0.115
				parameters ConWidth_3 = ConWidth_3
			endif
			values "ConWidth_3" range (0, 0.115]
			parameters ConDepth_3 = ConWidth_3
			values "ConLength_3" range [0, ]


			parameters ConPosX_3 = 0
			parameters ConPosY_3 = 0
			values "ConPosZ_3" range [h_top-0.42+ConWidth_3/2, h_top-0.17-ConWidth_3/2]

			ConDVecX = 0
			ConDVecY = 1
			ConDVecZ = 0

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0
		endif

		if iSuptyp = PEDESTAL then
			if ConWidth_3 > 0.08 then
				ConWidth_3 = 0.08
				parameters ConWidth_3 = ConWidth_3
			endif
			values "ConWidth_3" range (0, 0.08]
			parameters ConDepth_3 = ConWidth_3
			values "ConLength_3" range [0, ]


			parameters ConPosX_3 = 0
			parameters ConPosY_3 = -0.1675
			parameters ConPosZ_3 = 0

			ConDVecX = 0
			ConDVecY = 0
			ConDVecZ = -1

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0
		endif
	endif

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_3,
		ConPosY = ConPosY_3,
		ConPosZ = ConPosZ_3+h_top-ZZYZX,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_3,
		ConNominalDepth = ConNominalDepth_3,
		ConWidth = ConWidth_3,
		ConDepth = ConDepth_3,
		ConLength = ConLength_3,
		ConWallThickness = ConWallThickness_3,
		ConConnectorWidth = ConConnectorWidth_3,
		ConConnectorDepth = ConConnectorDepth_3,
		ConConnectorWidth2 = ConConnectorWidth2_3,
		ConToolType = ConToolType_3,
		ConStatus = ConStatus_3,
		ConType_3 = ConType_3,
		ConConnectorType_3 = ConConnectorType_3,
		ConSystem_3 = ConSystem_3,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1,
		gs_ui = 0

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_3, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	if gs_ui_current_page = 5 then
		ac_mep_connectionpage_active = 1
	else
		ac_mep_connectionpage_active = 0
	endif
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
else
	hideparameter "gs_connection", "gs_con_mat", "useSysMat"

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active
endif


call "ui_plumbingfixtures" parameters iObjectType 		= iObjectType,
								gs_ui_current_page = gs_ui_current_page


! -----------------------------------------------------------------------------
! Onorm list Settings
! -----------------------------------------------------------------------------

if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif



parameters ac_bottomlevel = h_top - ZZYZX
parameters ac_toplevel = h_top

