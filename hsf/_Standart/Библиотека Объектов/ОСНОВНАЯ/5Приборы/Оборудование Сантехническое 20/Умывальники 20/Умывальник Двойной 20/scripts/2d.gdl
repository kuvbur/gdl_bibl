
! ==============================================================================
! Basin Double
! ==============================================================================


unID = 1
hotspot2 0, 0, unID : unID = unID + 1      ! Default Hotspot

! === Minimal Space ========================================================

reqBMinSpace = 0
if MSFront > EPS or MSSide > EPS or MSSide2 > EPS then
	drawindex 10
	call "fa_minimalspace" parameters all MSType = 8, unID = 3000,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen,
													MSFillBackPen,
													MSContLineType,
													MSContLinePen

	if reqBMinSpace <> 0 then
		add2 0, -B
		mul2 a/1.120034,b/0.5
		FILL MSFillType
		line_type MSContLineType
		pen MSContLinePen
		for i = 1 to 2
			add2 -0.56, 0
			poly2_b 9, 1+2+4+64, MSFillPen, MSFillBackPen,
					0, 0, 0,
					0, 0.22, 0,
					0.1879914857355, 0.22, 900,
					0, 58.10920336546, 4000,
					0.08867521565753, 0.06038459596004, 0,
					0.3, 0.4, 900,
					0, 31.89079663456, 4000,
					0.3, 0, 0,
					0, 0, -1

			del 1
			mul2 -1, 1
			MSSide2 = MSSide
		next i
		del 3
		del 1
	endif
	line_type 1
endif

DrawingNum = 1
IF DrawingNum = 1 THEN	! Top

	! ==========================================================================
	! Scale
	! ==========================================================================

	call "2dDetailLevel" parameters gs_detlevel_2D_m = gs_detlevel_2D_m,
					returned_parameters gs_detlevel_2D_m

	pen gs_cont_pen
	fill gs_fill_type


	! === Basin Symbol =========================================================

	unID = 10
	drawindex 20

	if gs_detlevel_2D_m = 3 then
		circle2 ConPosX_3,ConPosY_3,0.0175
		circle2 ConPosX_4,ConPosY_4,0.0175
	endif

	add2 -a/2, -b
	mul2 a/1.12,b/0.5

	hotspot2 0.56, 0.5,  unID : unID = unID + 1
	hotspot2 0.30, 0,    unID : unID = unID + 1
	hotspot2 0,    0.22, unID : unID = unID + 1
	hotspot2 0,    0.5,  unID : unID = unID + 1
	hotspot2 1.12, 0.22, unID : unID = unID + 1
	hotspot2 1.12, 0.5,  unID : unID = unID + 1
	hotspot2 0.82, 0,    unID : unID = unID + 1
	hotspot2 0.56, 0,    unID : unID = unID + 1

	if gs_detlevel_2D_m <> 1 then		! 1/100, 1/50
		for n = 1 to 2

			line2 0.188017, 0.3795,  0.397000, 0.3795
			arc2  0.2925085, 0.4, 0.3461, 239.15, 300.85
			for i = 1 to 2
				line2 0.05, 0.276,    0.05, 0.22
				arc2  0.188016974489, 0.22, 0.138,  180, 238.1092033656
				add2 0.188, 0
				mul2 138/103.5, 1
				arc2 0, 0.276, 0.1035, 90, 180
				del 2
				add2 0.585, 0
				mul2 -1, 1
			next i
			del 4
			add2 0.535, 0
		next n
		del 2
	endif

	poly2_b 17, 1+2+4+64,gs_fill_pen,gs_back_pen,
		0, 0.22,      1,
		0.1880169857357, 0.22,    900,
		0, 58.10920336569,   4001,
		0.0886962079033, 0.06037736823769,      1,
		0.30, 0.4,    900,
		0, 31.89079663445,   4001,
		0.30, 0,      1,
		0.82, 0,      1,
		0.82, 0.4,    900,
		0, 31.89079663445,   4001,
		1.031337792097, 0.06037736823769,      1,
		0.9320170142644, 0.22,    900,
		0, 58.10920336569,   4001,
		1.12, 0.22,      1,
		1.12, 0.5,      1,
		0, 0.5,      1,
		0, 0.22,      -1

	del 2

	! === Tap ==================================================================

	if gs_tap_type_m <> 0 and (gs_detlevel_2D_m = 2 or gs_detlevel_2D_m = 3) then
		drawindex 40
		add2 ConPosX_3, 0
		for n = 1 to 2
			add2 0, yWallPos * NOT(bWallMountedTap)

				call "tapType_m" parameters all bShower 				= 0,
												yWallPos 				= yWallPos,
												bEnableTapEdit			= 1,
												bSinkTap				= 0,
												GS_HotspotUnIDBegin		= 10000 * n

			del 1

			add2 -ConPosX_3 * 2, 0
		next n
		del 3
	endif

ENDIF


! --- start of modifying to Shaft

if bShowMEP then

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left

	if bWallmountedTap then
		corrMEPx = a/1.120034*0.2675+xTapPos
		add2 corrMEPx,0
	else
		corrMEPx = 0
	endif
	! --- CONNECTIONS --- !


	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 12,
		ConPosX_3=ConPosX_3 - corrMEPx, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 0,
		ConPosX_4=ConPosX_4 - corrMEPx, ConPosY_4=ConPosY_4, ConLength_4=ConLength_4, ConEdit_4 = 0,
		ConShow_5 = 0, ConHotspotShow_5 = 0,
		ConShow_6 = 0, ConHotspotShow_6 = 0

	if bWallmountedTap then del 1

	if bWallmountedTap then
		corrMEPx = -a/1.120034*0.2675+xTapPos
		add2 corrMEPx,0

		CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
			ui_current_con = ui_current_con,
			MEP_NumberConnections = MEP_NumberConnections,
			MEP_NumConnectionData = gs_Connections,
			gs_AddConnections = gs_AddConnections,
			DrawingNum = DrawingNum,
			MEP_InsShow = 0,
			MEP_cline_show = 0,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type=gs_fill_type,
			gs_fill_pen=gs_fill_pen,
			gs_back_pen=gs_back_pen,
			ConShow_1 = 0, ConHotspotShow_1 = 0,
			ConShow_2 = 0, ConHotspotShow_2 = 0,
			ConShow_3 = 0, ConHotspotShow_3 = 0,
			ConShow_4 = 0, ConHotspotShow_4 = 0,
			ConPosX_5=ConPosX_5, ConPosY_5=ConPosY_5, ConLength_5=ConLength_5, ConEdit_5 = 12,
			ConPosX_6=ConPosX_6, ConPosY_6=ConPosY_6, ConLength_6=ConLength_6, ConEdit_6 = 12
		DEL 1
	endif
del 1
ENDIF

! --- end of modifying to Shaft
