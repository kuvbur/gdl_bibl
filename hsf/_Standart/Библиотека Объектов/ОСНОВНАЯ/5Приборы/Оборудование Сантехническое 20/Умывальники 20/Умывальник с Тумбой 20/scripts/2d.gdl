
! ==============================================================================
! Basin Cabinet
! ==============================================================================


DrawingNum = 1
IF DrawingNum = 1 THEN	! Top
	sink_width = gs_sink_width
	if gs_sink_type_m = 1 then sink_width = gs_sink_depth
	call "fa_basin_builtin" PARAMETERS ALL 	iBasinType		= 2,
											nSink 			= 1,
											sink_width		= sink_width,
											sink_depth		= gs_sink_depth,
											gs_tap_type_m	= gs_tap_type_m,
											bUnderMounted	= 0

	unID=1
	IF not(cab_size) THEN
		! ------ Left Overhang of Cabinet
		HOTSPOT2 0,front_cab,unID, left_cab, 1+128 :unID=unID+1
		HOTSPOT2 -1,front_cab,unID, left_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,front_cab,unID, left_cab, 2: unID=unID+1

		HOTSPOT2 0,B-rear_cab,unID, left_cab, 1+128 :unID=unID+1
		HOTSPOT2 -1,B-rear_cab,unID, left_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,B-rear_cab,unID, left_cab, 2: unID=unID+1

		! ------ Right Overhang of Cabinet
		HOTSPOT2 A,front_cab,unID, right_cab, 1+128 :unID=unID+1
		HOTSPOT2 A+1,front_cab,unID, right_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,front_cab,unID, right_cab, 2: unID=unID+1

		HOTSPOT2 A,B-rear_cab,unID, right_cab, 1+128 :unID=unID+1
		HOTSPOT2 A+1,B-rear_cab,unID, right_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,B-rear_cab,unID, right_cab, 2: unID=unID+1

		! ------ Front Overhang of Cabinet
		HOTSPOT2 left_cab,0,unID, front_cab, 1+128 :unID=unID+1
		HOTSPOT2 left_cab,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,front_cab,unID, front_cab, 2: unID=unID+1

		HOTSPOT2 A-right_cab,0,unID, front_cab, 1+128 :unID=unID+1
		HOTSPOT2 A-right_cab,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,front_cab,unID, front_cab, 2: unID=unID+1

		! ------ Rear Overhang of Cabinet
		HOTSPOT2 left_cab,B,unID, rear_cab, 1+128 :unID=unID+1
		HOTSPOT2 left_cab,B+1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

		HOTSPOT2 A-right_cab,B,unID, rear_cab, 1+128 :unID=unID+1
		HOTSPOT2 A-right_cab,B+1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 A-right_cab,B-rear_cab,unID, rear_cab, 2: unID=unID+1

		CALL "Resize_A_B_ZZYZX" Parameters ALL
	ELSE
		! ------ Hotspots on bounding box
		HOTSPOT2 0,B,unID :unID=unID+1
		HOTSPOT2 0,0,unID :unID=unID+1
		HOTSPOT2 A,0,unID :unID=unID+1
		HOTSPOT2 A,B,unID :unID=unID+1

		! ------ Left Overhang of Cabinet
		HOTSPOT2 0,front_cab+temp_b/2,unID, left_cab, 1+256 :unID=unID+1
		HOTSPOT2 -1,front_cab+temp_b/2,unID, left_cab, 3: unID=unID+1
		HOTSPOT2 left_cab,front_cab+temp_b/2,unID, left_cab, 2: unID=unID+1

		! ------ Right Overhang of Cabinet
		HOTSPOT2 left_cab+temp_a,front_cab+temp_b/2,unID, right_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+temp_a-1,front_cab+temp_b/2,unID, right_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+temp_a+right_cab,front_cab+temp_b/2,unID, right_cab, 2: unID=unID+1

		! ------ Front Overhang of Cabinet
		HOTSPOT2 left_cab+temp_a/2,0,unID, front_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,-1,unID, front_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,front_cab,unID, front_cab, 2: unID=unID+1

		! ------ Rear Overhang of Cabinet
		HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b,unID, rear_cab, 1+256 :unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b-1,unID, rear_cab, 3: unID=unID+1
		HOTSPOT2 left_cab+temp_a/2,front_cab+temp_b+rear_cab,unID, rear_cab, 2: unID=unID+1

		A=a-(left_cab+right_cab)
		B=b-(front_cab+rear_cab)

		ADD2 left_cab,front_cab
		CALL "Resize_A_B_ZZYZX" Parameters ALL
		DEL 1

		A=a+(left_cab+right_cab)
		B=b+(front_cab+rear_cab)
	ENDIF

	pen gs_cont_pen

	If iCabSymbolType2D <> 1 Then

		IF iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 Then
				LINE2 0,0.02,a,0.02
				LINE2 door_w,0,door_w,0.02
		Endif
		IF iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 Then
				LINE2 0,0,cos(15)*door_w,-sin(15)*door_w
				LINE2 a,0,a-(cos(15)*(a-door_w)),-sin(15)*(a-door_w)
				arc2 0,0,door_w,-15,0
				arc2 a,0,a-door_w,180,195
		Endif

		If dob_face = 1 Then

			add2 0,b
			mul2 1,-1

			IF iCabSymbolType2D = 2 OR iCabSymbolType2D = 4 Then
					LINE2 0,0.02,a,0.02
					LINE2 door_w,0,door_w,0.02
			Endif
			IF iCabSymbolType2D = 3 OR iCabSymbolType2D = 4 Then
					LINE2 0,0,cos(15)*door_w,-sin(15)*door_w
					LINE2 a,0,a-(cos(15)*(a-door_w)),-sin(15)*(a-door_w)
					arc2 0,0,door_w,-15,0
					arc2 a,0,a-door_w,180,195
			Endif

			Del 2
		Endif

	Endif

	drawindex 10

	call "fa_minimalspace" parameters all MSType = 20, unID = 3000
ENDIF

! --- start of modifying to Shaft

if bShowMEP then

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !
	if bWallmountedTap then
		add2 A/2+xTapPos,0
		corrMEPx = A/2+xTapPos
	else
		corrMEPx = 0
	endif
	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 12,
		ConShow_3 = 0, ConHotspotShow_3 = 0

	if bWallmountedTap then del 1

	! Waste Water
	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConShow_1 = 0, ConHotspotShow_1 = 0,
		ConShow_2 = 0, ConHotspotShow_2 = 0,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 12

	DEL 1
ENDIF

! --- end of modifying to Shaft
