
! ==============================================================================
! Basin Cabinet
! ==============================================================================


unID = 1

IF gs_detlevel_3D_m = 0 THEN END

IF gs_shadow=0 THEN SHADOW OFF

IF GLOB_CONTEXT>20 and GLOB_CONTEXT<40 THEN gs_detlevel_3D_m=1


! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

IF gs_detlevel_3D_m=1 OR gs_detlevel_3D_m = 3 THEN RESOL 8
IF gs_detlevel_3D_m=2 THEN RESOL 24

if bShowMEP then

	IF useSysMat THEN
		toe_mat			= sMat
		count_mat		= sMat
		bs_mat			= sMat
		gs_cabinet_mat	= sMat
		doorglas_mat	= sMat
		doorframe_mat	= sMat
		doorframe_mat_end	= sMat
		gs_sink_mat		= sMat
		gs_tap_mat		= sMat
		gs_knob_mat		= sMat
		gs_ShelfMat		= sMat
		DrawerMat		= sMat
		gs_drain_mat	= sMat
		gs_con_mat		= sMat
	ENDIF

	if bWallMountedTap then
		ConEdit_1 = 12
		ConEdit_2 = 12

		corrMEPx = A/2+xTapPos
		corrMEPz = zTapPos

		add corrMEPx,0,corrMEPz
	else
		ConEdit_1 = 16
		ConEdit_2 = 16

		corrMEPx = 0
		corrMEPz = 0
	endif

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = ConEdit_1,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = ConEdit_2,
		ConShow_3 = 0, ConHotspotShow_3 = 0

	if bWallmountedTap then del 1

	! Waste Water
	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConShow_1 = 0, ConHotspotShow_1 = 0,
		ConShow_2 = 0, ConHotspotShow_2 = 0,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 16


ENDIF

! --- end of modifying to Shaft


IF gs_detlevel_3D_m=1 THEN RESOL 4
IF gs_detlevel_3D_m=2 THEN RESOL gs_resol


!!!! Handling overhungs in case of end panels !!!
if dob_face then rear_cab = rear_cab
if rear_panel then rear_cab = rear_cab + 0.02
if left_panel then left_cab = left_cab +0.02
if right_panel then right_cab = right_cab +0.02


!!!!!!!!!!!!!!!!!!!!!!! *** 3D Hotspot in Bounding Boxs

	hotspot a, b, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a, b, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot 0, b, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, b, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, b, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot a, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot a, 0, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot a, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1

	hotspot 0, 0, 0, unID, zzyzx, 1+128 : unID=unID+1
	hotspot 0, 0, -1, unID, zzyzx, 3 : unID=unID+1
	hotspot 0, 0, zzyzx, unID, zzyzx, 2 : unID=unID+1


IF bDrawer THEN
	PEN gs_cont_pen
	MATERIAL gs_cabinet_mat

	ADD 0,0,zzyzx-count_th-drawer_h-0.01
	IF gs_sink_type_m=0 then
	base
		prism_	5, 0.02,
				left_cab+0.02,front_cab+fr_th1, 15,
				left_cab+0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,b-rear_cab-0.02-((fr_th1-0.02)*dob_face), 15,
				a-right_cab-0.02,front_cab+fr_th1, 15,
				left_cab+0.02,front_cab+fr_th1, -1
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4
	body 1

	ENDIF
	DEL 1
ENDIF

IF gs_door_bw=0 THEN
	!! Door Panel Streching
		IF door=1 OR (numDrawers = 2 & bDrawer) THEN
			bit=256	!! flags of editable base hotspot
			hotspot left_cab,front_cab,toe_h+door_h,unID,door_w,1+bit+128 : unID=unID+1
			hotspot -1,front_cab,toe_h+door_h,unID,door_w,3 : unID=unID+1
			hotspot door_w+left_cab,front_cab,toe_h+door_h,unID,door_w,2 : unID=unID+1
		ENDIF
ENDIF

!!!!!****************************************** CALL GENERAL BASE CABINET
if gs_sink_type_m=0 then
	sink=0
else
	sink=1
endif

	CALL "fa_gen_basecabinet" PARAMETERS a=a, b=b, zzyzx=zzyzx,
			gs_detlevel_3D_m=gs_detlevel_3D_m, dob_face=dob_face, count_th=count_th,
			front_cab=front_cab, rear_cab=rear_cab, left_cab=left_cab,
			right_cab=right_cab, toe_h=toe_h, side_vis_m=side_vis_m,
			sink=sink, shelf_num=shelf_num, gs_cont_pen=gs_cont_pen, gs_shadow=gs_shadow,
			toe_mat=toe_mat, gs_cabinet_mat=gs_cabinet_mat, drawer_h=drawer_h,
			gs_toe_type_m=gs_toe_type_m, toe_d=toe_d, ToeHtps3D=1,
			fr_th1=fr_th1,gs_ShelfMat=gs_ShelfMat,
			basecab_unID=unID,
			returned_parameters unID


!!!!!****************************************** CALL COUNTERTOP and SINK

ADDz ZZYZX
sink_width = gs_sink_width
if gs_sink_type_m = 1 then sink_width = gs_sink_depth
call "fa_basin_builtin" parameters all	iBasinType			= 2,
										nSink 				= 1,
										sink_width			= sink_width,
										sink_depth			= gs_sink_depth,
										gs_tap_type_m		= gs_tap_type_m,
										h_top				= zzyzx,
										bUnderMounted		= 0,
										basinbuiltin_unID	= unID,
					returned_parameters unID
DEL 1

!!!!!****************************************** CALL DRAWER and DOOR PANEL


FOR ii=1 TO (dob_face)+1									!*** double face
	IF numDrawers = 1 & bDrawer then
		ADD left_cab,front_cab+fr_th1,ZZYZX-count_th-drawer_h		!*** DRAWER
		GOSUB 10
		ADD (a-(left_cab+right_cab))/2,-0.02,drawer_h/2
		ROTY 90
		IF gs_sink_type_m=0 then GOSUB 100	!*** Knob
		DEL 3
	ENDIF

	IF numDrawers = 2 & bDrawer then
		ADD left_cab,front_cab+fr_th1,ZZYZX-count_th-drawer_h		!*** DRAWER
		GOSUB 11
		ADDx door_w
		GOSUB 12
		del 2
		ADD left_cab+door_w/2,front_cab-0.02,ZZYZX-count_th-drawer_h/2
		ROTY 90
		IF gs_sink_type_m=0 and gs_detlevel_3D_m=2 then GOSUB 100	!*** Knob
		ADDz door_w/2+(a-door_w-left_cab-right_cab)/2
		IF gs_sink_type_m=0 and gs_detlevel_3D_m=2 then GOSUB 100	!*** Knob
		DEL 3
	ENDIF

	ADD left_cab,front_cab+fr_th1,toe_h					!*** FRONT DOORS
		IF door=1 then GOSUB 20						!*** FRONT LEFT DOOR
	ADDx a-left_cab-right_cab
		mulx -1
		IF door=1 then GOSUB 30						!*** FRONT RIGHT DOOR
		del 1
	DEL 2

	door_open=0

	ADDY front_cab
	ROTZ 180
	ADD	-A-left_cab+right_cab,-B+front_cab,0

NEXT ii
DEL (dob_face+1)*3


!!!!!****************************************** CALL END PANELS

	IF rear_panel=1 and dob_face=0 then
		door_w=a-door_w-left_cab-right_cab: door_h=ZZYZX-toe_h-count_th
		ADD door_w+left_cab,b-rear_cab,toe_h
		ROTZ 180
			GOSUB 40
			door_w=a-door_w-left_cab-right_cab
		ADDX -door_w
			GOSUB 40
		DEL 3
	ENDIF



	IF left_panel=1 then	!*** LEFT END PANEL
		door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face) : door_h=ZZYZX-toe_h-count_th
		ADD left_cab,door_w+front_cab+fr_th1,toe_h
		ROTZ -90
			GOSUB 40
		DEL 2
	ENDIF

	IF right_panel=1 then
		door_w=b-front_cab-rear_cab-fr_th1-(fr_th1*dob_face): door_h=ZZYZX-toe_h-count_th
		ADD a-right_cab,front_cab+fr_th1,toe_h
		ROTZ 90
			GOSUB 40
		DEL 2
	ENDIF


END

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!! *** RETURN *** !!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!!****************************************** DRAWER
10:

	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=a-left_cab-right_cab, door_h=drawer_h, gs_door_type_m=1,gs_shadow=gs_shadow,
			doorframe_w=DrawerMat, gs_cont_pen=gs_cont_pen,fr_th1=fr_th1,
			doorframe_mat=DrawerMat, gs_knob_type_m=0, !,door_open=0,
			cabdoortyp_unID=unID, gs_bevel=gs_bevel,
		returned_parameters unID

RETURN

!!!!!****************************************** DRAWER
11:

	IF gs_door_bw=1 THEN	door_w=(a-left_cab-right_cab)/2

	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=door_w, door_h=drawer_h, gs_door_type_m=1,gs_shadow=gs_shadow,
			doorframe_w=DrawerMat, gs_cont_pen=gs_cont_pen,
			doorframe_mat=DrawerMat, gs_knob_type_m=0,fr_th1=fr_th1,
			cabdoortyp_unID=unID, gs_bevel=gs_bevel,
		returned_parameters unID

RETURN

!!!!!****************************************** DRAWER
12:

	CALL "fa_cabdoortyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,gs_full_edit=0,
			door_w=a-door_w-left_cab-right_cab, door_h=drawer_h, gs_door_type_m=1,gs_shadow=gs_shadow,
			doorframe_w=DrawerMat, gs_cont_pen=gs_cont_pen,fr_th1=fr_th1,
			doorframe_mat=DrawerMat, gs_knob_type_m=0,
			cabdoortyp_unID=unID, gs_bevel=gs_bevel,
		returned_parameters unID

RETURN

!!!!!****************************************** FRONT DOOR_LEFT
20:

	IF gs_door_bw=1 THEN	door_w=(a-left_cab-right_cab)/2

CALL "fa_cabdoortyp" PARAMETERS ALL \
		cabdoortyp_unID=unID,
		knob_pl_y = knob_pl_y - drawer_h,
		returned_parameters unID

RETURN

!!!!!!!**************************************** FRONT DOOR_RIGHT
30:

IF gs_door_bw=1 THEN
	door_w=(a-left_cab-right_cab)/2
ELSE
	door_w=a-door_w-left_cab-right_cab
ENDIF

CALL "fa_cabdoortyp" PARAMETERS ALL \
		cabdoortyp_unID=unID,
		knob_pl_y = knob_pl_y - drawer_h,
		returned_parameters unID

RETURN

!!!!!****************************************** END PANELS
40:
	!! on rear panel there is no glas and grilles and knobs!!
	door_ang=0
	gs_knob_type_m=0

		If iEndPanelType = 1 	then gs_door_type_m=1		!! Style1
		If iEndPanelType = 2 	then gs_door_type_m=2		!! Style1
		If iEndPanelType = 4 	then gs_door_type_m=11		!! Style1
		If iEndPanelType = 7 	then gs_door_type_m=4		!! Style1
		If iEndPanelType = 9 	then gs_door_type_m=6		!! Style1
		If iEndPanelType = 99 	then gs_door_type_m=10		!! Style1


	gs_full_edit=0

	CALL "fa_cabdoortyp" PARAMETERS ALL cabdoortyp_unID=unID,
		fr_th1=0.02,gs_cust_p=gs_cust_p_end,doorframe_w=doorframe_w_end,
		doorframe_mat=doorframe_mat_end,doorglas_mat=doorframe_mat_end,
		returned_parameters unID

RETURN

!!!!!****************************************** KNOBS/Drawer
100:

IF gs_detlevel_3D_m=2 THEN

		if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=0 then
			ADDz 0
			addy -fr_th1+0.02
			!!! knob on top drawer
			CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
					gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
					gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
					gs_knob_mat=gs_knob_mat
			del 2
		else
			if gs_knob_type_m=8 then
				roty 180
				Addz knob_size/2
				addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
				del 3
			else
				if gs_knob_type_m=4 then
					Addz 0.05
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				else
					Addz knob_size/2
					addy -fr_th1+0.02
					CALL "fa_knobtyp" PARAMETERS gs_detlevel_3D_m=gs_detlevel_3D_m,
							gs_knob_type_m=gs_knob_type_m,gs_resol=gs_resol,
							gs_shadow=gs_shadow,gs_cont_pen=gs_cont_pen,
							gs_knob_mat=gs_knob_mat,knob_size=knob_size
					del 2
				endif
			endif
		endif
	ENDIF
RETURN
