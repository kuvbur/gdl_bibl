
! -----------------------------------------------------------------------------
! Scale
! -----------------------------------------------------------------------------

if gs_detlevel_2D_m = 0 then			! Scale sensitive
	gs_detlevel_2D_m	= 2

	if GLOB_SCALE > 150 then gs_detlevel_2D_m = 1
	if GLOB_SCALE > 75 and GLOB_SCALE <= 150 then gs_detlevel_2D_m = 2
	if GLOB_SCALE <= 75 then gs_detlevel_2D_m = 3
endif

! -----------------------------------------------------------------------------
! Hotspots 2D
! -----------------------------------------------------------------------------
unID = 1
hotspot2    0,  	    0, unID : unID = unID + 1
hotspot2 -a/2,  	    0, unID : unID = unID + 1
hotspot2 -a/2, -b + 0.170 * (b / 0.500), unID : unID = unID + 1
hotspot2    0,         -b, unID : unID = unID + 1
hotspot2  a/2, -b + 0.170 * (b / 0.500), unID : unID = unID + 1
hotspot2  a/2,  	    0, unID : unID = unID + 1

! -----------------------------------------------------------------------------
! Bidet Types
! -----------------------------------------------------------------------------
pen gs_cont_pen
fill gs_fill_type
DrawingNum = 1
drawindex 20
gosub iStyle

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------
drawindex 10
gosub "minimalspace"

! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------
if bShowMEP then
	gosub "mepconnection"
endif


end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Bidet Floor Mounted
! -----------------------------------------------------------------------------

1:

	if DrawingNum = 1 then	! Top

		mul2 a/0.37, b/0.50
		line2        0.185017, 2.55802920621E-016,     0.185017,        -0.33
		line2        0.185017, 7.12725613888E-016,    -0.185017, -8.4496992577E-017
		line2       -0.185017, -8.4496992577E-017,    -0.185017,        -0.33
		arc2     0.0224999967701,        -0.33, 0.16250000149, 306.869897646,            0
		arc2     -0.0225000008937, -0.329999999142, 0.16250000149,          180, 233.1301023541
		arc2     3.52242540691E-013, -0.29999999795, 0.200000001192, 233.1301023541, 306.8698965533
		arc2     -0.0225000008937, -0.329999999142, 0.16250000149,          180, 233.1301023541
		arc2     1.28670886966E-009, -0.299999998379, 0.200000001621, 233.1301019857, 269.9999999997

		poly2_b 8, 2+64, gs_fill_pen, gs_back_pen,
			-0.185,0,0,
			 0.185,0,0,
			 0.185,-0.33,0,
			 0,-1,800,
			 0.1200,-0.46,1000,
			-0.12,-0.46,1000,
			-0.185,-0.33,1000,
			-0.185,0,-1

		! --- 1/100 ---
		if gs_detlevel_2D_m > 2 then
			circle2  0,      -0.1425,        0.015
		endif

		! --- 1/200 ---
		if gs_detlevel_2D_m > 1 then
			line2        0.125002,        -0.12,     0.125002,        -0.33
			line2       -0.125002,        -0.12,    -0.125002,        -0.33
			line2          -0.125,        -0.12,        0.125,        -0.12
			arc2     -0.0224999985099,        -0.33, 0.10250000149,          180, 233.1301023541
			arc2     2.38418573772E-009, -0.299999998808, 0.14000000298, 233.1301023541, 306.8698965528
			arc2     0.0224999985099,        -0.33, 0.10250000149, 306.869897646, 1.861788079159E-013
		endif
		del 1
	endif

return

! -----------------------------------------------------------------------------
! Bidet Wall Hung
! -----------------------------------------------------------------------------

2:

	gosub 1 ! same 2D representation ---

return


! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------

"minimalspace":

	reqBMinSpace = 0
	if MSFront > EPS or MSSide > EPS or MSSide2 > EPS then
		call "fa_minimalspace" parameters all MSType = 8, unID = 3000,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen,
													MSFillBackPen,
													MSContLineType,
													MSContLinePen
	endif
	if reqBMinSpace <> 0 then                !! FILL
		mul2 a/0.37,b/0.50
		fill MSFillType
		line_type MSContLineType
		pen MSContLinePen
		for i = 1 to 2
			poly2_b 6, 2+4+64, MSFillPen, MSFillBackPen,
					0.185,  -0.33, 0,
					0,      -1,    800,
					0.1200, -0.46, 1000,
					0,      -0.5,  1000,
					0.185,  -0.5,  1,
					0.185,  -0.33, -1
			mul2 -1, 1
		next i
		del 3
	endif
	line_type 1

return

! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

"mepconnection":

	pen gs_cont_pen
	if gs_fill_pen then pen gs_fill_pen
	project2{3} 3, 270, 3+32, 4, gs_back_pen, 0, 0, 0, parameters gs_detlevel_3D_m = 1, show3D = 0
	pen gs_cont_pen
	project2{3} 3, 270, 2, 8, 0, 0, 0, 0, parameters gs_detlevel_3D_m = 1, show3D = 0

				! --- OTHER VIEWS --- !

	rot2 -SYMB_ROTANGLE * (DrawingNum > 1 and not(SYMB_MIRRORED)) + SYMB_ROTANGLE * (DrawingNum > 1 and SYMB_MIRRORED)

	if DrawingNum = 2 then project2 -3, 90, 2	! Bottom
	if DrawingNum = 3 then project2 4, 270, 2	! Front
	if DrawingNum = 4 then project2 4, 90, 2	! Back
	if DrawingNum = 5 then project2 4, 180 * (SYMB_MIRRORED), 2		! Right
	if DrawingNum = 6 then project2 4, 180 * not(SYMB_MIRRORED), 2	! Left

	if DirEffluent_ID = 1 then
		ConEdit_3 = 1
	else
		ConEdit_3 = 3
	endif

				! --- CONNECTIONS --- !

	call "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConShow_1 = 0, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConShow_2 = 0, ConEdit_2 = 12,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConShow_3 = 0, ConEdit_3 = ConEdit_3


return



