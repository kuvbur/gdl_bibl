
call "FM_types" parameters all

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

values "stStyle" stStyles
values "iStyle"  1, 2

if GLOB_MODPAR_NAME = "stStyle" then
	iStyle = 1
	if stStyle = stStyles[2] then iStyle = 2
	parameters iStyle = iStyle
else
	stStyle = stStyles[1]
	if iStyle = 2 then stStyle = stStyles[2]
	parameters stStyle = stStyle
endif

! -----------------------------------------------------------------------------
! 3D detlevel
! -----------------------------------------------------------------------------
dim st_detail3D[4]
	st_detail3D[1] = `Детальный`
	st_detail3D[2] = `Простой`
	st_detail3D[3] = `Упрощенный`
	st_detail3D[4] = `Откл.`

values "gs_detlevel_3D" st_detail3D
values "gs_detlevel_3D_m" 2, 1, 3, 0


if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` then gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` then gs_detlevel_3D_m = 1
	IF gs_detlevel_3D = `Упрощенный` then gs_detlevel_3D_m = 3
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	gs_detlevel_3D = `Детальный`
	IF gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 then gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 3 then gs_detlevel_3D = `Упрощенный`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

! -----------------------------------------------------------------------------
! 2D detlevel
! -----------------------------------------------------------------------------

call "2dDetailLevel" parameters	all enable2dDetLevels = 2 + 4 + 8 + 16	! Scale Sensitive, 1:50 - Detailed, 1:100 - Middle, 1:200 - Simple

! -----------------------------------------------------------------------------
! Default Sizes
! -----------------------------------------------------------------------------

values "A" range [0.20, ]
values "B" range [0.20, ]
values "ZZYZX" range [0.20, ]
values "gs_resol" range [4,)

if GLOB_MODPAR_NAME = "stStyle" or GLOB_MODPAR_NAME = "iStyle" then

	zzyzx = defaultSizes[iStyle]
	parameters zzyzx = zzyzx, ac_bottomlevel = ac_bottomlevel, ac_toplevel = ac_toplevel

	ConPosX_1 = defaultConnectionPos[iStyle][1]
	ConPosY_1 = defaultConnectionPos[iStyle][2]
	ConPosZ_1 = defaultConnectionPos[iStyle][3]
	ConPosX_2 = defaultConnectionPos[iStyle][4]
	ConPosY_2 = defaultConnectionPos[iStyle][5]
	ConPosZ_2 = defaultConnectionPos[iStyle][6]
	ConPosX_3 = defaultConnectionPos[iStyle][7]
	ConPosY_3 = defaultConnectionPos[iStyle][8]
	ConPosZ_3 = defaultConnectionPos[iStyle][9]
	parameters ConPosX_1 = ConPosX_1, ConPosY_1 = ConPosY_1, ConPosZ_1 = ConPosZ_1
	parameters ConPosX_2 = ConPosX_2, ConPosY_2 = ConPosY_2, ConPosZ_2 = ConPosZ_2
	parameters ConPosX_3 = ConPosX_3, ConPosY_3 = ConPosY_3, ConPosZ_3 = ConPosZ_3

	if iStyle = 1 then
		ac_bottomlevel = 1.000
		ac_toplevel = 0.000
	else
		ac_bottomlevel = h_sitting - zzyzx
		ac_toplevel = h_sitting
	endif

endif


! -----------------------------------------------------------------------------
! LOCK and HIDEPARAMETERS
! -----------------------------------------------------------------------------

if iStyle = 1 then
	hideparameter "h_sitting"
else
	hideparameter "DirEffluent"
endif

! -----------------------------------------------------------------------------
! Limitations
! -----------------------------------------------------------------------------

if gs_cont_pen = 0 then parameters gs_cont_pen = 75
if gs_fill_pen = 0 then parameters gs_fill_pen = 19
if gs_detlevel_3D_m = 1 or gs_detlevel_3D_m = 3 then lock "gs_resol"

! -----------------------------------------------------------------------------
! Minimal Space
! -----------------------------------------------------------------------------

! --- Minimal Space ---
if MSFront < 0 then
	MSFront = 0
	parameters MSFront = MSFront
endif
if MSSide < 0 then
	MSSide = 0
	parameters MSSide = MSSide
endif
if MSSide2 < 0 then
	MSSide2 = 0
	parameters MSSide2 = MSSide2
endif

! -----------------------------------------------------------------------------
! modify to Shaft
! -----------------------------------------------------------------------------

values "DirEffluent" DirEffluentValues
values "DirEffluent_ID" 1, 2

if GLOB_MODPAR_NAME = "DirEffluent" then
	if DirEffluent = DirEffluentValues[1] then DirEffluent_ID = 1
	if DirEffluent = DirEffluentValues[2] then DirEffluent_ID = 2

	parameters DirEffluent_ID = DirEffluent_ID
else
	if DirEffluent_ID = 1 then DirEffluent = DirEffluentValues[1]
	if DirEffluent_ID = 2 then DirEffluent = DirEffluentValues[2]

	parameters DirEffluent = DirEffluent
endif


if isMEPEnabled then

	if not(ConStatus_1) and not(ConStatus_2) and not(ConStatus_3) then
		lock "gs_con_mat"
		hideparameter "gs_con_mat"
	endif

	dim shape_typ_to_validation[3]
		shape_typ_to_validation[1] = 0		! Rectangle
		shape_typ_to_validation[2] = 1		! Circle
		shape_typ_to_validation[3] = 0		! Oval

					! --- CONNECTIONS --- !

	parameters MEP_NumberConnections = 3


					! --- 1st CONNECTION --- !

	ConID = 1
	ConName = `Подключение Горячей Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if iStyle = 1 then
		if ConWidth_1 < 0.001 then
			ConWidth_1 = 0.001
			parameters ConWidth_1 = ConWidth_1
		endif
		values "ConWidth_1" range (0, ]
		parameters ConDepth_1 = ConWidth_1
		values "ConLength_1" range [0, ]

		values "ConPosZ_1" range [ConWidth_1/2, zzyzx-ConWidth_1/2]
	else
		if ConWidth_1 < 0.001 then
			ConWidth_1 = 0.001
			parameters ConWidth_1 = ConWidth_1
		endif
		if ConWidth_1 > MIN(A/2, 0.08*(zzyzx/0.2465)) then
			ConWidth_1 = MIN(A/2, 0.08*(zzyzx/0.2465))
			parameters ConWidth_1 = ConWidth_1
		endif
		values "ConWidth_1" range (0, MIN(A/2, 0.08*(zzyzx/0.2465))]
		parameters ConDepth_1 = ConWidth_1
		values "ConLength_1" range [0, ]

		values "ConPosZ_1" range [h_sitting-zzyzx+ConWidth_1/2, h_sitting-ConWidth_1/2]
	endif
	values "ConPosX_1" range [-A/2+ConWidth_1/2, A/2-ConWidth_1/2]
	ConPosY_1 = 0
	parameters ConPosY_1 = ConPosY_1

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_1,
		ConPosY = ConPosY_1,
		ConPosZ = ConPosZ_1,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_1,
		ConNominalDepth = ConNominalDepth_1,
		ConWidth = ConWidth_1,
		ConDepth = ConDepth_1,
		ConLength = ConLength_1,
		ConWallThickness = ConWallThickness_1,
		ConConnectorWidth = ConConnectorWidth_1,
		ConConnectorDepth = ConConnectorDepth_1,
		ConConnectorWidth2 = ConConnectorWidth2_1,
		ConToolType = ConToolType_1,
		ConStatus = ConStatus_1,
		ConType_1 = ConType_1,
		ConConnectorType_1 = ConConnectorType_1,
		ConSystem_1 = ConSystem_1,
		ui_current_con = ui_current_con,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


					! --- 2nd CONNECTION --- !

	ConID = 2
	ConName = `Подключение Холодной Воды`

	ConDVecX = 0
	ConDVecY = 1
	ConDVecZ = 0

	ConWVecX = -1
	ConWVecY = 0
	ConWVecZ = 0

	if iStyle = 1 then
		if ConWidth_2 < 0.001 then
			ConWidth_2 = 0.001
			parameters ConWidth_2 = ConWidth_2
		endif
		values "ConWidth_2" range (0, ]
		parameters ConDepth_2 = ConWidth_2
		values "ConLength_2" range [0, ]

		values "ConPosZ_2" range [ConWidth_2/2, zzyzx-ConWidth_2/2]
	else
		if ConWidth_2 < 0.001 then
			ConWidth_2 = 0.001
			parameters ConWidth_2 = ConWidth_2
		endif
		if ConWidth_2 > MIN(A/2, 0.08*(zzyzx/0.2465)) then
			ConWidth_2 = MIN(A/2, 0.08*(zzyzx/0.2465))
			parameters ConWidth_2 = ConWidth_2
		endif
		values "ConWidth_2" range (0, MIN(A/2, 0.08*(zzyzx/0.2465))]
		parameters ConDepth_2 = ConWidth_2
		values "ConLength_2" range [0, ]

		values "ConPosZ_2" range [h_sitting-zzyzx+ConWidth_2/2, h_sitting-ConWidth_2/2]
	endif
	values "ConPosX_2" range [-A/2+ConWidth_2/2, A/2-ConWidth_2/2]
	ConPosY_2 = 0
	parameters ConPosY_2 = ConPosY_2

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_2,
		ConPosY = ConPosY_2,
		ConPosZ = ConPosZ_2,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_2,
		ConNominalDepth = ConNominalDepth_2,
		ConWidth = ConWidth_2,
		ConDepth = ConDepth_2,
		ConLength = ConLength_2,
		ConWallThickness = ConWallThickness_2,
		ConConnectorWidth = ConConnectorWidth_2,
		ConConnectorDepth = ConConnectorDepth_2,
		ConConnectorWidth2 = ConConnectorWidth2_2,
		ConToolType = ConToolType_2,
		ConStatus = ConStatus_2,
		ConType_2 = ConType_2,
		ConConnectorType_2 = ConConnectorType_2,
		ConSystem_2 = ConSystem_2,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_2, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"


					! --- 3rd CONNECTION --- !

	ConID = 3
	ConName = `Подключение Канализации`

	if iStyle = 1 then
		if ConWidth_3 < 0.001 then
			ConWidth_3 = 0.001
			parameters ConWidth_3 = ConWidth_3
		endif
		values "ConWidth_3" range (0, ]
		parameters ConDepth_3 = ConWidth_3
		values "ConLength_3" range [0, ]

		if DirEffluent_ID = 1 then
			ConDVecX = 0
			ConDVecY = 1
			ConDVecZ = 0

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			parameters ConPosX_3 = 0
			parameters ConPosY_3 = 0
			if	GLOB_MODPAR_NAME = "DirEffluent"	|\
				GLOB_MODPAR_NAME = "DirEffluent_ID"	then

				parameters ConPosZ_3 = ConWidth_3/2+0.05
			endif
		else
			ConDVecX = 0
			ConDVecY = 0
			ConDVecZ = -1

			ConWVecX = -1
			ConWVecY = 0
			ConWVecZ = 0

			parameters ConPosX_3 = 0
			parameters ConPosZ_3 = 0
			if	GLOB_MODPAR_NAME = "DirEffluent"	|\
				GLOB_MODPAR_NAME = "DirEffluent_ID"	|\
				GLOB_MODPAR_NAME = "stStyle"		|\
				GLOB_MODPAR_NAME = "iStyle"			then

				parameters ConPosY_3 = -ConWidth_3/2-0.05
			endif
		endif
	else
		ConDVecX = 0
		ConDVecY = 1
		ConDVecZ = 0

		ConWVecX = -1
		ConWVecY = 0
		ConWVecZ = 0

		if ConWidth_3 < 0.001 then
			ConWidth_3 = 0.001
			parameters ConWidth_3 = ConWidth_3
		endif
		if ConWidth_3 > 0.10*(A/0.37) then
			ConWidth_3 = 0.10*(A/0.37)
			parameters ConWidth_3 = ConWidth_3
		endif
		values "ConWidth_3" range (0, 0.10*(A/0.37)]
		parameters ConDepth_3 = ConWidth_3
		values "ConLength_3" range [0, ]

		parameters ConPosX_3 = 0
		parameters ConPosY_3 = 0
		values "ConPosZ_3" range [h_sitting-zzyzx+ConWidth_3/2, h_sitting-ConWidth_3/2]
	endif

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		gs_Connections = gs_Connections,
		MEP_NumConnectionData = MEP_NumConnectionData,
		MEP_StrConnectionData = MEP_StrConnectionData,
		MEP_NumberConnections = MEP_NumberConnections,
		gs_AddConnections = gs_AddConnections,
		ConName = ConName,
		ConID = ConID,
		ConPosX = ConPosX_3,
		ConPosY = ConPosY_3,
		ConPosZ = ConPosZ_3,
		ConDVecX = ConDVecX,
		ConDVecY = ConDVecY,
		ConDVecZ = ConDVecZ,
		ConWVecX = ConWVecX,
		ConWVecY = ConWVecY,
		ConWVecZ = ConWVecZ,

		ConNominalWidth = ConNominalWidth_3,
		ConNominalDepth = ConNominalDepth_3,
		ConWidth = ConWidth_3,
		ConDepth = ConDepth_3,
		ConLength = ConLength_3,
		ConWallThickness = ConWallThickness_3,
		ConConnectorWidth = ConConnectorWidth_3,
		ConConnectorDepth = ConConnectorDepth_3,
		ConConnectorWidth2 = ConConnectorWidth2_3,
		ConToolType = ConToolType_3,
		ConStatus = ConStatus_3,
		ConType_3 = ConType_3,
		ConConnectorType_3 = ConConnectorType_3,
		ConSystem_3 = ConSystem_3,
		shape_typ_to_validation = shape_typ_to_validation,
		MEP_enabled_geometry_mod = 1

	parameters MEP_ConInfo[ConID][1] = STR(ConToolType_3, 1, 0)
	parameters MEP_ConInfo[ConID][2] = ConName
	parameters MEP_ConInfo[ConID][3] = "0"

	if gs_ui_current_page = 4 then
		ac_mep_connectionpage_active = 1
	else
		ac_mep_connectionpage_active = 0
	endif
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active

	if SetProgram then
		SetProgram = 0
		parameters SetProgram = SetProgram
	endif
else
	hideparameter"gs_connection", "gs_con_mat", "useSysMat", "DirEffluent"

	ac_mep_connectionpage_active = 0
	parameters ac_mep_connectionpage_active = ac_mep_connectionpage_active
endif

call "ui_plumbingfixtures" parameters iObjectType 		= iObjectType,
								gs_ui_current_page = gs_ui_current_page

! -----------------------------------------------------------------------------
! Onorm list Settings
! -----------------------------------------------------------------------------

if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif

end

