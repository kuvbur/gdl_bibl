
fill gs_fill_type
pen gs_cont_pen

! ===============================================================================
! Disabled
! origo: toilet axis on wall
! ===============================================================================

! ------ corner hsp
newunID = 8000
hotspot2 dAxisFromWall, 0,		unID : unID = unID + 1


! --- WC Bowl and Cistern ---------------------------------------------------


call "CompleteToiletMacro" parameters all	iWCType 		= 1,
											bShowHotspots 	= 1,
											bDisabledWC		= 1


! ------ Scale Settings

call "2dDetailLevel" parameters gs_detlevel_2D 	= gs_detlevel_2D,
							gs_detlevel_2D_m 	= gs_detlevel_2D_m,
							enable2dDetLevels	= 30,
							returned_parameters det2D



add2 0, -distanceFromWall

!axis from wall hsp edit
hotspot2 0, 0,				newunID, dAxisFromWall, 1+256: newunID = newunID + 1
hotspot2 -1, 0,				newunID, dAxisFromWall, 3: newunID = newunID + 1
hotspot2 dAxisFromWall, 0,	newunID, dAxisFromWall, 2+128: newunID = newunID + 1


if iFlushType = 2 then
	drawindex 30
else
	drawindex 50
endif

! ------ Toilet paper dispenser wall-mounted
if	bCorner & bToiletPaper & det2D = 3 then

	!bottom center hsp edit
	add2 dAxisFromWall, distanceFromWall
	hotspot2 0, 0,					newunID, tpd_distFromWall,	1+128: newunID = newunID + 1
	hotspot2 0, 1,					newunID, tpd_distFromWall,	3: newunID = newunID + 1
	hotspot2 0, -tpd_distFromWall,	newunID, tpd_distFromWall,	2: newunID = newunID + 1

	add2 -0.163, -tpd_distFromWall
	POLY2_B 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0,		-tpd_halfwidth, 1,
		0.163,	-tpd_halfwidth, 1,
		0.163,	 tpd_halfwidth, 1,
		0,		 tpd_halfwidth, 1,
		0,		-tpd_halfwidth, -1

	del 2
endif

del 1

! ------ Grabs

if iGrabBars = GRAB_CORNER then
	if reargb then								!rear grab bar

		!length hsps edit
		hotspot2 0, 0,					newunID, reargb_length1,	1+128: newunID = newunID + 1
		hotspot2 1, 0,					newunID, reargb_length1,	3: newunID = newunID + 1
		hotspot2 -reargb_length1, 0,	newunID, reargb_length1,	2: newunID = newunID + 1
		hotspot2 0, 0,					newunID, reargb_length2,	1+128: newunID = newunID + 1
		hotspot2 -1, 0,					newunID, reargb_length2,	3: newunID = newunID + 1
		hotspot2 reargb_length2, 0,		newunID, reargb_length2,	2: newunID = newunID + 1

		add2 -reargb_length1, 0

		ror = 0.05
		di = grabBarDiameter
		aa = reargb_length1 + reargb_length2
		bb = 0.008
		tempID = unID
		gosub "bar_wall"
		unID = tempID

		del 1
	endif
	if sgb then									!side grab bar

		add2 dAxisFromWall, -sgb_dist

		tempID = unID
		tempnewID = newunID
		gosub "grabbar_sidewall"
		unID = tempID
		newunID = tempnewID

		del 1
	endif
	if vertgb & det2D = 3 then					!vertical grab bar

		add2 dAxisFromWall, -vertgb_distFromWall

			if LibraryLangCode # "USA" then
				!position hsp edit
				hotspot2	0, vertgb_distFromWall, 	newunID, vertgb_distFromWall,	1+128: newunID = newunID + 1
				hotspot2	0, vertgb_distFromWall+1, 	newunID, vertgb_distFromWall,	3: newunID = newunID + 1
				hotspot2	0, 0, 						newunID, vertgb_distFromWall,	2: newunID = newunID + 1
			endif

		rot2 -90
			ror = 0.05
			di = grabBarDiameter
			bb = 0.008

			hotspot2 0,	0, unID: unID = unID + 1

			POLY2_B 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
				-di/2-0.01, 0, 1,
				di/2+0.01, 0, 1,
				di/2+0.01, -0.005, 1,
				di/2, -0.005, 1,
				di/2, -bb-0.072, 1,
				-di/2, -bb-0.072, 1001,
				-di/2, -0.005, 1,
				-di/2-0.01, -0.005, 1,
				-di/2-0.01, 0, -1
			line2 -di/2, -0.005, di/2, -0.005
		del 2
	endif
endif

if iGrabBars = GRAB_PARALLEL then
	if iPositionGrabBars = GRAB_FLOOR then
		add2 -grabWidth/2, 0
		if bToiletPaper & det2D = 3 then gosub "toiletPaper"
		gosub "grabbar_floor"
		add2 grabWidth, 0
		gosub "grabbar_floor"
	endif

	if iPositionGrabBars = GRAB_TOILET then
		drawindex 10
		add2 -grabWidth/2, 0
		if bToiletPaper & det2D = 3 then gosub "toiletPaper"
		del 1
		gosub "grabbar_toilet"
		mul2 -1, 1
		gosub "grabbar_toilet"
		del 1
	endif

	if iPositionGrabBars = GRAB_REARREAR then
		add2 -grabWidth/2, 0
		if bToiletPaper & det2D = 3 then gosub "toiletPaper"
		gosub "grabbar_rear"
		add2 grabWidth, 0
		gosub "grabbar_rear"
		del 2
	endif

	if iPositionGrabBars = GRAB_SIDEREAR then
		add2 -grabWidth/2, 0
		gosub "grabbar_rear"
		add2 grabWidth/2 + dAxisFromWall, -sgb_dist

		tempID = newunID
		tempnewID = newunID
		gosub "grabbar_sidewall"
		newunID = tempnewID

		del 2
	endif

	if iPositionGrabBars = GRAB_SIDESIDE then
		add2 dAxisFromWall, -sgb_dist

		tempID = newunID
		tempnewID = newunID
		gosub "grabbar_sidewall"
		del 1
		mul2 -1, 1
		add2 MSSide + bowlWidth/2, -sgb_dist
		gosub "grabbar_sidewall"
		newunID = tempnewID

		del 2
	endif

endif

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"bar_wall":
! ==============================================================================
	hotspot2 0,	0,	tempID: tempID = tempID + 1
	hotspot2 aa, 0, tempID: tempID = tempID + 1

	POLY2_B 25, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-di/2-0.01,		0,					1,
		di/2+0.01,		0,					1,
		di/2+0.01,		-0.005,				1,
		di/2,			-0.005,				1,
		di/2,			-bb+ror-0.072,		1,
		ror,			-bb+ror-0.072,		901,
		0,				90,					4001,
		aa-ror,			-bb+di/2-0.072,		1,
		aa-ror,			-bb+ror-0.072,		901,
		0,				90,					4001,
		aa-di/2,		-0.005,				1,
		aa-di/2-0.01,	-0.005,				1,
		aa-di/2-0.01,	0,					1,
		aa+di/2+0.01,	0,					1,
		aa+di/2+0.01,	-0.005,				1,
		aa+di/2,		-0.005,				1,
		aa+di/2,		-bb+ror-0.072,		1,
		aa-ror,			-bb+ror-0.072,		901,
		0,				-90,				4001,
		ror,			-bb-di/2-0.072,		1,
		ror,			-bb+ror-0.072,		901,
		0,				-90,				4001,
		-di/2,			-0.005,				1,
		-di/2-0.01,		-0.005,				1,
		-di/2-0.01,		0,					-1

	line2 -di/2, -0.005, di/2, -0.005
	line2 aa-di/2, -0.005, aa+di/2, -0.005
return

!================================================================================
"grabbar_sidewall":
!
!	set tempID = tempnewID for continuous ID's
!	otherwise bar_wall will use temp_ID
!
!================================================================================
	if tempID = tempnewID then bNewID = 1 else bNewID = 0

	!length hsps edit
	hotspot2	0, sgb_dist, 		tempnewID, sgb_dist,		1+128: tempnewID = tempnewID + 1
	hotspot2	0, sgb_dist+1, 		tempnewID, sgb_dist,		3: tempnewID = tempnewID + 1
	hotspot2	0, 0, 				tempnewID, sgb_dist,		2: tempnewID = tempnewID + 1
	hotspot2	0, 0, 				tempnewID, sgb_length,		1+128: tempnewID = tempnewID + 1
	hotspot2	0, 1, 				tempnewID, sgb_length,		3: tempnewID = tempnewID + 1
	hotspot2	0, -sgb_length, 	tempnewID, sgb_length,		2: tempnewID = tempnewID + 1

	rot2 -90

	ror = 0.05
	di = grabBarDiameter
	aa = sgb_length
	bb = 0.008
	if bNewID then tempID = tempnewID
	gosub "bar_wall"
	if bNewID then tempnewID = tempID

	del 1

return


! ==============================================================================
"grabbar_floor":
! ==============================================================================
	hotspot2 0,	0,			 unID: unID = unID + 1
	hotspot2 0,	-grabLength, unID: unID = unID + 1

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-grabRadius-0.01, -0.005, 1,
		grabRadius+0.01, -0.005, 1,
		grabRadius+0.01, 0, 1,
		-grabRadius-0.01, 0, 1,
		-grabRadius-0.01, -0.005, -1
	
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-grabRadius, -0.005, 1,
		grabRadius, -0.005, 1,
		grabRadius, -grabLength+gripLength, 1,
		-grabRadius, -grabLength+gripLength, 1,
		-grabRadius, -0.005, -1
	
	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-gripRadius, -grabLength+gripLength, 1,
		gripRadius, -grabLength+gripLength, 1,
		gripRadius, -grabLength, 1,
		-gripRadius, -grabLength, 1,
		-gripRadius, -grabLength+gripLength, -1
return


! ==============================================================================
"grabbar_toilet":
! ==============================================================================
	poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0, yHoldingPos-grabRadius, 1,
		-grabWidth/2+grabRadius, yHoldingPos-grabRadius, 1,
		-grabWidth/2+grabRadius, yHoldingPos, 1,
		-grabWidth/2, yHoldingPos+grabRadius, 1001,
		0, yHoldingPos+grabRadius, 0,
		0, yHoldingPos-grabRadius, -1

	add2 -grabWidth/2, 0
	hotspot2 0,	yHoldingPos, unID: unID = unID + 1
	hotspot2 0,	-grabLength, unID: unID = unID + 1

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		grabRadius, yHoldingPos, 1,
		grabRadius, -grabLength+gripLength, 1,
		-grabRadius, -grabLength+gripLength, 1,
		-grabRadius, yHoldingPos, 1,
		grabRadius, yHoldingPos, 1001

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-gripRadius, -grabLength+gripLength, 1,
		gripRadius, -grabLength+gripLength, 1,
		gripRadius, -grabLength, 1,
		-gripRadius, -grabLength, 1,
		-gripRadius, -grabLength+gripLength, -1
	del 1

return


! ==============================================================================
"grabbar_rear":
! ==============================================================================
	hotspot2 0,	0, 			 newunID: newunID = newunID + 1
	hotspot2 0, -grabLength, newunID: newunID = newunID + 1

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-wPlate/2, -0.005, 1,
		wPlate/2, -0.005, 1,
		wPlate/2, 0, 1,
		-wPlate/2, 0, 1,
		-wPlate/2, -0.005, -1

	poly2_b 5, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-wPlate/2 + ePlate, -0.005, 1,
		wPlate/2 - ePlate, -0.005, 1,
		wPlate/2 - ePlate, -0.005 - lConsole, 1,
		-wPlate/2 + ePlate, -0.005 - lConsole, 1,
		-wPlate/2 + ePlate, -0.005, -1

	poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-grabRadius, -0.005, 1,
		grabRadius, -0.005, 1,
		grabRadius, -grabLength, 1,
		0, -grabLength, 901,
		0, -180, 4001,
		-grabRadius, -0.005, -1
return


! ==============================================================================
"toiletPaper":
! ==============================================================================
	if iGrabBars = GRAB_PARALLEL & iPositionGrabBars = GRAB_REARREAR then
		gr2 = grabRadius
	else
		gr2 = gripRadius
	endif
	poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-0.06, -grabLength+gripLength*1.5-roundoff1-0.1, 1,
		-gr2, -grabLength+gripLength*1.5-roundoff1-0.1, 0,
		gr2, -grabLength+gripLength*1.5-roundoff1-0.1, 1,
		0.06, -grabLength+gripLength*1.5-roundoff1-0.1, 1,
		0.06, -grabLength+gripLength*1.5-roundoff1, 1,
		grabRadius, -grabLength+gripLength*1.5-roundoff1, 0,
		-grabRadius, -grabLength+gripLength*1.5-roundoff1, 1,
		-0.06, -grabLength+gripLength*1.5-roundoff1, 1,
		-0.06, -grabLength+gripLength*1.5-roundoff1-0.1, -1
return
