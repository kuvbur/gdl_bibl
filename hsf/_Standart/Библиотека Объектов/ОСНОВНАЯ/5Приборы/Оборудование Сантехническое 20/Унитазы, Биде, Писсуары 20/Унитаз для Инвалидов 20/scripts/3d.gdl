
! ==============================================================================
! Disabled
! ==============================================================================
newunID = 9000

pen gs_cont_pen
if gs_detlevel_3D_m = DETLEV_3D_OFF then end

if not(gs_shadow) then SHADOW OFF

if gs_detlevel_3D_m = DETLEV_3D_DRAFT then
	gs_resol = 8
	gs_resol_con = 3
endif
resol gs_resol
pen gs_cont_pen

! ------ origo: corner
add dAxisFromWall, 0, 0
hotspot 0, 0, 0, 	unID: unID = unID + 1

add 0, -distanceFromWall, 0

!axis from wall hsp edit
hotspot -dAxisFromWall, 0, 0,		newunID, dAxisFromWall, 1+256: newunID = newunID + 1
hotspot -dAxisFromWall - 1, 0, 0,	newunID, dAxisFromWall, 3: newunID = newunID + 1
hotspot	0, 0, 0,					newunID, dAxisFromWall, 2+128: newunID = newunID + 1


! ------ Toilet paper dispenser wall-mounted

if	bCorner & bToiletPaper then

	addy distanceFromWall

	!bottom center hsps edit
	add 0, 0, tpd_distFromFloor
	hotspot 0, 0, 0,					newunID, tpd_distFromWall,	1+128: newunID = newunID + 1
	hotspot 0, 1, 0,					newunID, tpd_distFromWall,	3: newunID = newunID + 1
	hotspot 0, -tpd_distFromWall, 0,	newunID, tpd_distFromWall,	2: newunID = newunID + 1
	del 1

	add 0, -tpd_distFromWall, 0
	hotspot 0, 0, 0,					newunID, tpd_distFromFloor,	1+128: newunID = newunID + 1
	hotspot 0, 0, -1,					newunID, tpd_distFromFloor,	3: newunID = newunID + 1
	hotspot 0, 0, tpd_distFromFloor,	newunID, tpd_distFromFloor,	2: newunID = newunID + 1
	del 1

	add 0, -tpd_distFromWall + tpd_halfwidth, tpd_distFromFloor
	rotz -90

	mulx 0.44/0.7

	addz 0.08
	addy -0.08633

	rotz 90
	rotx 90
	addz 0.01

	MATERIAL matPaper
		RESOL 2*gs_resol
		PRISM_       7,      0.18,
				0.07,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
			   -0.07,     0.0,      79,
				0.0,     0.0,    979,
				0.0,     180.0,   4079,
				0.07,     0.0,     -1

	del 1
	material matDispenser

	PRISM_      5,      0.2,
			0.08433,     0.08,      15,
			0.08633,     0.08,      15,
			0.08633,    -0.08,      15,
			0.08433,    -0.08,      15,
			0.08433,     0.08,     -1

	RESOL 2.5*gs_resol
	PRISM_     12,      0.2,
			0.08433,     0.01,      15,
			0.076348,     0.01,      79,
		   -0.0,     0.0,    900,
			0.0,     202.5379,   4001,
		   -0.066684,    -0.0385,      15,
		   -0.064952,    -0.0375,      79,
			0.0,    -0.0,    900,
			0.0,    -202.3377,   4001,
			0.07433,     0.01,      15,
			0.07433,    -0.01,      15,
			0.08433,    -0.01,      15,
			0.08433,     0.01,     -1

	addz 0.005
	RESOL gs_resol
	PRISM_      7,      0.005,
			0.022913,    -0.01,      15,
			0.08433,    -0.01,      15,
			0.08433,     0.01,      15,
			0.022913,     0.01,      79,
		   -0.0,    -0.0,    900,
			0.0,     312.8436,   4001,
			0.022913,    -0.01,     -1
	addz 0.185
	PRISM_      7,      0.005,
			0.022913,    -0.01,      15,
			0.08433,    -0.01,      15,
			0.08433,     0.01,      15,
			0.022913,     0.01,      79,
		   -0.0,    -0.0,    900,
			0.0,     312.8436,   4001,
			0.022913,    -0.01,     -1

	del 10
endif

del 1

! ------ Grabs

if	iGrabBars = GRAB_CORNER then
	if vertgb then			!vertical grabbar
		add 0, -vertgb_distFromWall, vertgb_distFromFloor

		!pos hsp edit
		if LibraryLangCode # "USA" then
			hotspot	0, vertgb_distFromWall, 0,		newunID, vertgb_distFromWall,	1+128: newunID = newunID + 1
			hotspot	0, vertgb_distFromWall + 1, 0,	newunID, vertgb_distFromWall,	3: newunID = newunID + 1
			hotspot	0, 0, 0,						newunID, vertgb_distFromWall,	2: newunID = newunID + 1
		endif

		!length hsps edit
		hotspot	0, 0, 0,							newunID, vertgb_length,		1+128: newunID = newunID + 1
		hotspot	0, 0, -1,							newunID, vertgb_length,		3: newunID = newunID + 1
		hotspot	0, 0, vertgb_length,				newunID, vertgb_length,		2: newunID = newunID + 1
		hotspot	0, 0, -vertgb_distFromFloor,		newunID, vertgb_distFromFloor,		1+128: newunID = newunID + 1
		hotspot	0, 0, -vertgb_distFromFloor - 1,	newunID, vertgb_distFromFloor,		3: newunID = newunID + 1
		hotspot	0, 0, 0,							newunID, vertgb_distFromFloor,		2: newunID = newunID + 1

		rotz -90
		roty -90
		ror = 0.05
		di = grabBarDiameter
		aa = vertgb_length
		GOSUB "bar_wall"
		del 3
	endif

	if reargb then			!rear grabbar
		addx -dAxisFromWall
		hotspot	-reargb_length1, 0, 0, 	unID :unID = unID + 1

		addz rearsidegb_distFromFloor

		!length hsps edit
		hotspot	0, 0, 0,				newunID, reargb_length1,	1+128: newunID = newunID + 1
		hotspot	1, 0, 0,				newunID, reargb_length1,	3: newunID = newunID + 1
		hotspot	-reargb_length1, 0, 0,	newunID, reargb_length1,	2: newunID = newunID + 1
		hotspot	0, 0, 0,				newunID, reargb_length2,	1+128: newunID = newunID + 1
		hotspot	-1, 0, 0,				newunID, reargb_length2,	3: newunID = newunID + 1
		hotspot	reargb_length2, 0, 0,	newunID, reargb_length2,	2: newunID = newunID + 1

		!height hsp edit
		hotspot	0, -0.08, -rearsidegb_distFromFloor,			newunID, rearsidegb_distFromFloor,		1+128: newunID = newunID + 1
		hotspot	0, -0.08, -rearsidegb_distFromFloor - 1,		newunID, rearsidegb_distFromFloor,		3: newunID = newunID + 1
		hotspot	0, -0.08, 0,									newunID, rearsidegb_distFromFloor,		2: newunID = newunID + 1


		add -reargb_length1, 0, 0
		ror = 0.05
		di = grabBarDiameter
		aa = reargb_length1 + reargb_length2
		GOSUB "bar_wall"
		del 3
	endif

	if sgb then				!side grabbar
		gosub "grabbar_sidewall"
	endif
endif

addx -dAxisFromWall

if iGrabBars = GRAB_PARALLEL then
	resol gs_resol
	if iPositionGrabBars = GRAB_FLOOR then gosub "grabbar_floor"

	if iPositionGrabBars = GRAB_TOILET then gosub "grabbar_toilet"

	if iPositionGrabBars = GRAB_REARREAR then
		addx -grabWidth/2
		gosub "grabbar_rear"
		addx grabWidth
		gosub "grabbar_rear"
		del 2
		if bToiletPaper then
			addz -dBars
			gosub "toiletPaper"
			del 1
		endif
	endif

	if iPositionGrabBars = GRAB_SIDEREAR then
		addx -grabWidth/2
		gosub "grabbar_rear"
		del 1
		addx dAxisFromWall
		gosub "grabbar_sidewall"
		del 1
	endif

	if iPositionGrabBars = GRAB_SIDESIDE then
		addx dAxisFromWall
		gosub "grabbar_sidewall"
		mulx -1
		addx dAxisFromWall + MSSide + bowlWidth / 2
		gosub "grabbar_sidewall"
		del 3
	endif
endif


! ==============================================================================
! WC Bowl and Cistern
! ==============================================================================

if iFlushType = 1 then
	if gs_flushHandlePlace_m <> 0 and (gs_detlevel_3D_m = DETLEV_3D_DETAILED or gs_detlevel_3D_m = DETLEV_3D_SIMPLE) then		!none or draft
		addy -distanceFromWall
		gosub "flushHandlePos"
		del 1
	endif
endif

if iFlushType = 1 then
	!cistern height hsp edit
	add 0, -distanceFromWall, gs_seat_height
	hotspot 0, 0, 0,				newunID, cisternHeight,	1+128: newunID = newunID + 1
	hotspot 0, 0, -1,				newunID, cisternHeight,	3: newunID = newunID + 1
	hotspot 0, 0, cisternHeight,	newunID, cisternHeight,	2: newunID = newunID + 1
	del 1
endif
if iFlushType = 3 then
	!cistern height hsp edit
	hotspot 0, 0, 0,				newunID, carrierHeight,	1+128: newunID = newunID + 1
	hotspot 0, 0, -1,				newunID, carrierHeight,	3: newunID = newunID + 1
	hotspot 0, 0, carrierHeight,	newunID, carrierHeight,	2: newunID = newunID + 1
endif
	
call "CompleteToiletMacro" parameters all	iWCType 		= 1,
											bShowHotspots 	= 1,
											bDisabledWC		= 1

del 1
! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


!================================================================================
"bar_wall":
!================================================================================

	hotspot	0, 0, 0, unID :unID = unID + 1
	hotspot	aa, 0, 0, unID :unID = unID + 1

	material matGrabBar
	pen gs_cont_pen

	add ror, -0.08, 0
		add -ror, ror, 0
		rotx -90
			addz 0.08-ror-0.005
			RESOL 1.5*gs_resol
			prism_ 2, 0.005,
				0, 0,				979,
				di/2 + 0.01, 360,	4079
			del 1
		
			RESOL gs_resol
			prism_ 2,.08-ror-0.005,
				0, 0,				978,
				di/2, 360,			4078
		del 2

		add aa-ror,ror,0
		rotx -90
			RESOL 1.5*gs_resol
			addz 0.08-ror-0.005
			prism_ 2, 0.005,
				0, 0,				979,
				di/2 + 0.01, 360,	4079
			del 1
	
			RESOL gs_resol
			prism_ 2,0.08-ror-0.005,
				0, 0,				978,
				di/2, 360,			4078
		del 2

		roty 90
			prism_ 2,aa-2*ror,
				0, 0,				974,
				di/2, 360,			4074

			if GLOB_VIEW_TYPE = 4 | GLOB_VIEW_TYPE = 5 | GLOB_VIEW_TYPE = 7 then	!section, elevation, detail
				flag = 4 + 8 + 16 + 32
			else
				flag = 4 + 8
			endif
			rotx 90
			addy aa-2*ror
			addz -ror
				revolve 2,90, flag,
					0, ror,			901,
					di/2, 360,		4001
	
				addy -aa+2*ror
				rotz 180
					revolve 2,90, flag,
						0, ror,		901,
						di/2, 360,	4001

	del 7
return

!================================================================================
"grabbar_sidewall":
!================================================================================
	add 0, -sgb_dist, rearsidegb_distFromFloor
	hotspot	0, -sgb_length, -rearsidegb_distFromFloor, unID :unID = unID + 1

	!length hsps edit
	hotspot	0, sgb_dist, 0,			newunID, sgb_dist,		1+128: newunID = newunID + 1
	hotspot	0, sgb_dist + 1, 0,		newunID, sgb_dist,		3: newunID = newunID + 1
	hotspot	0, 0, 0,				newunID, sgb_dist,		2: newunID = newunID + 1
	hotspot	0, 0, 0,				newunID, sgb_length,	1+128: newunID = newunID + 1
	hotspot	0, 1, 0,				newunID, sgb_length,	3: newunID = newunID + 1
	hotspot	0, -sgb_length, 0,		newunID, sgb_length,	2: newunID = newunID + 1

	!height hsp edit
	hotspot	0, 0, -rearsidegb_distFromFloor,			newunID, rearsidegb_distFromFloor,		1+128: newunID = newunID + 1
	hotspot	0, 0, -rearsidegb_distFromFloor - 1,		newunID, rearsidegb_distFromFloor,		3: newunID = newunID + 1
	hotspot	0, 0, 0,									newunID, rearsidegb_distFromFloor,		2: newunID = newunID + 1

	rotz -90
	ror = 0.05
	di = grabBarDiameter
	aa = sgb_length
	GOSUB "bar_wall"
	del 2

return


!================================================================================
"grabbar_floor":
!================================================================================
	for hj = 1 to 2
		!height hsp edit
		add -grabWidth/2, -grabLength, 0
		hotspot	0, 0, 0,			newunID, rearsidegb_distFromFloor,	1+128: newunID = newunID + 1
		hotspot	0, 0, -1,			newunID, rearsidegb_distFromFloor,	3: newunID = newunID + 1
		hotspot	0, 0, grabHeight,	newunID, rearsidegb_distFromFloor,	2: newunID = newunID + 1
		del 1

		material matGrabBar
	
		add -grabWidth/2, 0, grabHeight
		rotx 90
		cylind 0.005, grabRadius + 0.01
		del 2
	
		add -grabWidth/2, -0.005, grabHeight
		rotx 90
		cylind grabLength-gripLength-0.005, grabRadius
		del 2
	
		add -grabWidth/2, -grabLength + gripLength*2, 0
		cylind 0.005, grabRadius + 0.01
		del 1
	
		rotx 90
		cutpolya 2, 1, 0,
			-grabWidth/2, grabHeight, 907+64,
			grabRadius, 360, 4007+64
		del 1

		add -grabWidth/2, -grabLength + gripLength*2, 0.005
		cylind grabHeight-0.005, grabRadius
		del 1
	
		cutend

		if hj = 1 & bToiletPaper then gosub "toiletPaper"

		material matGrip
		add -grabWidth/2, gripLength-grabLength, grabHeight

		rotz -90
		if gs_detlevel_3D_m = DETLEV_3D_DETAILED then
			revolve 6, 360, 1+2+4+8+16+32,
				0, 0, 1,
				0, grabRadius, 1,
				gripRadius-grabRadius, gripRadius, 1001,
				gripLength-(gripRadius-grabRadius), gripRadius, 1,
				gripLength, grabRadius, 1001,
				gripLength, 0, 1
		else
			revolve 2, 360, 1+2+4+8+16+32,
				0, gripRadius, 0,
				gripLength, gripRadius, 0
		endif
		del 2
		addx grabWidth
	next hj
	del 2

return


!================================================================================
"grabbar_toilet":
!================================================================================
	material matGrabBar
	for hj = 1 to 2
		nc = ceil(gs_resol/2)
		for hi = 1 to nc
			alfa = hi * 90/nc
			put -grabWidth/2+roundoff2-roundoff2*sin(alfa), yHoldingPos, zHoldingPos+roundoff2-roundoff2*cos(alfa), -90
		next hi
	
		for hk = 1 to nc
			fi = 90 - atn(2/3)
			beta = hk * fi/nc
			put -grabWidth/2, yHoldingPos-roundoff2+roundoff2*cos(beta), grabHeight-roundoff2*2-roundoff2*tan(fi/2)+roundoff2*sin(beta), 0
		next hk

		for hm = 1 to nc
			gi = atn(2/3)
			ceta = hm * gi/nc
			put -grabWidth/2, yHoldingPos-roundoff2*3-roundoff2*tan(gi/2)+roundoff2*sin(ceta*(nc-hm)), grabHeight-roundoff2+roundoff2*cos(ceta*(nc-hm)), 0
		next hm

		if iToiletMounting = 1 then
			dstart = bowlWidth*0.14
			if iFloorMountedBowlType = 2 then dstart = bowlWidth*0.19
			if iFloorMountedBowlType = 3 then dstart = bowlWidth*0.17
			if iFloorMountedBowlType = 4 then dstart = bowlWidth*0.35
			if iFloorMountedBowlType = 5 then dstart = bowlWidth*0.12
			if iFloorMountedBowlType = 6 then dstart = bowlWidth*0.4
		else
			dstart = bowlWidth*0.25
			if iWallHungBowlType = 2 then dstart = bowlWidth*0.32
			if iWallHungBowlType = 3 then dstart = bowlWidth*0.35
			if iWallHungBowlType = 4 then dstart = bowlWidth*0.4
		endif

		tube 2, 7+nsp/4, 1+2+16+32,
			0, 0, 901,
			grabRadius, 360, 4001,

			0, yHoldingPos, zHoldingPos, -90,
			-dstart, yHoldingPos, zHoldingPos, -90,
			-grabWidth/2+roundoff2, yHoldingPos, zHoldingPos, -90,
			get(nsp/3),
			-grabWidth/2, yHoldingPos, grabHeight-roundoff2*2-roundoff2*tan(fi/2), 0,
			get(nsp/2),
			-grabWidth/2, yHoldingPos-roundoff2*3+roundoff2*tan(gi/2), grabHeight-roundoff2+roundoff2*cos(gi), 0,
			get(nsp),
			-grabWidth/2, -grabLength+gripLength, grabHeight, 0,
			-grabWidth/2, -grabLength, grabHeight, 0
		mulx -1
	next hj
	del 2

	if bToiletPaper then gosub "toiletPaper"

	material matGrip
	for hj = 1 to 2

		!height hsp edit
		add -grabWidth/2, -grabLength, 0
		hotspot	0, 0, 0,			newunID, rearsidegb_distFromFloor,	1+128: newunID = newunID + 1
		hotspot	0, 0, -1,			newunID, rearsidegb_distFromFloor,	3: newunID = newunID + 1
		hotspot	0, 0, grabHeight,	newunID, rearsidegb_distFromFloor,	2: newunID = newunID + 1

		add 0, gripLength, grabHeight
		rotz -90
		if gs_detlevel_3D_m = DETLEV_3D_DETAILED then
			revolve 6, 360, 1+2+4+8+16+32,
				0, 0, 1,
				0, grabRadius, 1,
				gripRadius-grabRadius, gripRadius, 1001,
				gripLength-(gripRadius-grabRadius), gripRadius, 1,
				gripLength, grabRadius, 1001,
				gripLength, 0, 1
		else
			revolve 2, 360, 1+2+4+8+16+32,
				0, gripRadius, 0,
				gripLength, gripRadius, 0
		endif
		del 3
		addx grabWidth
	next hj
	del 2

return


!================================================================================
"grabbar_rear":
!================================================================================
	material matGrabBar

	add -wPlate / 2, -0.005, rearsidegb_distFromFloor - hPlate + hConsole / 2 + ePlate
	block wPlate, 0.005, hPlate
	add ePlate, 0, hPlate - ePlate
	rotx -90
	roty 90
	hotspot	lConsole - hConsole/2, hConsole/2, wConsole + grabRadius
	prism_ 6, wConsole,
		0, 0, 15,
		lConsole - hConsole/2, 0, 79,
		lConsole - hConsole/2, hConsole/2, 979,
		0, 180, 4079,
		0, hConsole, 15,
		0, 0, -1
	addz wConsole
	prism_ 2, 2 * grabradius,
		lConsole - hConsole/2, hConsole/2, 979,
		hConsole/2, 360, 4079
	del 3
	addx 2 * grabRadius + wConsole
	rotx -90
	roty 90
	prism_ 6, wConsole,
		0, 0, 15,
		lConsole - hConsole/2, 0, 79,
		lConsole - hConsole/2, hConsole/2, 979,
		0, 180, 4079,
		0, hConsole, 15,
		0, 0, -1
	del 5

	add 0, -grabLength + dBars/2, grabHeight

	!height hsp edit
	hotspot	0, 0, -grabHeight,		newunID, rearsidegb_distFromFloor,	1+128: newunID = newunID + 1
	hotspot	0, 0, -grabHeight-1,	newunID, rearsidegb_distFromFloor,	3: newunID = newunID + 1
	hotspot	0, 0, 0,				newunID, rearsidegb_distFromFloor,	2: newunID = newunID + 1

	rotx 90
	addy -dBars/2
		revolve 2, 180, 0,
					0, dBars/2, 901,
					grabRadius, 360, 4001

		if GLOB_VIEW_TYPE = 4 | GLOB_VIEW_TYPE = 5 | GLOB_VIEW_TYPE = 7 then	!section, elevation, detail
			flag = 79
		else
			flag = 78
		endif

		mulz -1
		addy dBars/2
		prism_ 2, grabLength - dBars/2 - 0.005 - lConsole + hConsole/2,
			0, 0, 900 + flag,
			grabRadius, 360, 4000 + flag

		addy -dBars
		prism_ 2, grabLength - dBars/2 - 0.005,
			0, 0, 900 + flag,
			grabRadius, 360, 4000 + flag
	del 6
return


!================================================================================
"flushHandlePos":
!================================================================================

	material flushHandleMat

	delnum = 0

	if gs_flushHandlePlace_m = 2 then
		mulx -1
		delnum = delnum + 1
	endif

	if iToiletMounting = 1 and (iFloorMountedBowlType = 5 or iFloorMountedBowlType = 6) then
		if iFloorMountedBowlType = 5 then
			addx -(0.407*bowlWidth + 0.015/2)
			addy -bowlDepth/4
			addz gs_seat_height	+ 0.100
			rotz 10
			delnum = delnum + 4
		endif

		if iFloorMountedBowlType = 6 then
			addx -(bowlWidth/2 + 0.015/2)
			addy -bowlDepth/7
			addz gs_seat_height + 0.150
			delnum = delnum + 3
		endif

		roty  90
		rotz -90
		delnum = delnum + 2

		if iFloorMountedBowlType = 5 then
			rotx  -15
			delnum = delnum + 1
		endif
	else
		addx -(cisternWidth/2 + 0.015/2)
		delnum = delnum + 1

		if iCisternType = 1 then addx 0.015/0.435*cisternWidth
		if iCisternType = 2 then addx 0.006/0.271*cisternWidth
		if iCisternType = 3 then addx 0.063/0.374*cisternWidth
		if iCisternType = 4 then addx 0
		if iCisternType = 5 then addx 0.004/0.400*cisternWidth
		delnum = delnum + 1

		addy -cisternDepth/2
		addz gs_seat_height + cisternHeight*0.6
		delnum = delnum + 2

		if iCisternType = 3 then
			rotz 25
			delnum = delnum + 1
		endif

		roty  90
		rotz -90
		delnum = delnum + 2

		if iCisternType = 1 then rotx  7
		if iCisternType = 2 then rotx -7
		if iCisternType = 3 then rotx  9
		if iCisternType = 4 then rotx  0
		if iCisternType = 5 then rotx  3
		delnum = delnum + 1
	endif

	gosub "flushHandle"

	del delnum

return


!================================================================================
"flushHandle":
!================================================================================

	addz -0.015/2

	if gs_detlevel_3D_m = DETLEV_3D_DETAILED then
		prism_ 20, 0.015,
				0.047807,		-0.02,			15+64,
				0.047807,		-0.01,			900,
				0,				 -104.036243,	4015+64,
				0.03810633,		-0.00757464,	15+64,
				0.03219223,		-0.00609611,	900,
				0,				104.0362,		4015+64,
				0.032192235,	0,				15+64,
				0,				0,				15+64,
				-0.006893660,	-0.02757464,	15+64,
				0.002807764,	-0.03,			900,
				0,				104.0362,		4015+64,
				0.002807764,	-0.04,			15+64,
				0.08,			-0.04,			15+64,
				0.08,			-0.03,			900,
				0,				90,				4015+64,
				0.09,			-0.03,			15+64,
				0.08,			-0.03,			900,
				0,				90,				4015+64,
				0.08,			-0.02,			15+64,
				0.04780776,		-0.02,			15+64
	else
		prism_ 7, 0.015,
				 -0.01,		-0.04,	15,
				0.09,		-0.04,	15,
				0.09,		-0.02,	15,
				0.035,		-0.02,	15,
				0.04,		0,		15,
				0,			0,		15,
				-0.01,		-0.04,	15
	endif

	del 1

return


!================================================================================
"toiletPaper":
!================================================================================
	rotx 90
	cutpolya 2, 1, 0,
		-grabWidth/2, grabHeight, 907+64,
		grabRadius, 360, 4007+64
	del 1

	for hi = 1 to ceil(gs_resol/2)
		alfa = hi * 90/ceil(gs_resol/2)
		put -grabWidth/2, -grabLength+gripLength*1.5-roundoff1+roundoff1*cos(alfa), grabHeight-0.1+roundoff1-roundoff1*sin(alfa), 0
	next hi

	tube 2, 5+nsp/4, 1+2+16+32,
		0, 0, 901,
		grabRadius, 360, 4001,

		-grabWidth/2, -grabLength+gripLength*1.5, grabHeight+1, 0,
		-grabWidth/2, -grabLength+gripLength*1.5, grabHeight, 0,
		-grabWidth/2, -grabLength+gripLength*1.5, grabHeight-0.1+roundoff1, 0,
		get(nsp),
		-grabWidth/2, -grabLength+gripLength*1.5-gripLength, grabHeight-0.1, 0,
		-grabWidth/2, -grabLength, grabHeight-0.1, 0

	cutend

	material matPaper
	resol 2*gs_resol
	add -grabWidth/2, -grabLength+gripLength*1.5-roundoff1, grabHeight-0.1225+grabRadius
	rotx 90
	prism_ 5+2, 0.1,
		-0.06, -0.09, 15,
		-0.06, 0, 15+64,
		0.06, 0, 1015+64,
		-0.06+0.00001, 0, 1015+64,
		-0.06, -0.09, -1,

		0, 0, 979,
		0.0225, 360, 4079
	del 2

return
