
hideparameter all "A", "B", "ZZYZX"

! ==============================================================================
! WC Bowl and Cistern
! ==============================================================================

call "CompleteToiletMacro" parameters all	iWCType 		= 1,
											bShowHotspots 	= 1,
											bDisabledWC		= 1



! ==============================================================================
! Disabled
! ==============================================================================

if not(gs_showtank) then
	lock "gs_flushHandlePlace", "flushHandleMat", "gs_tank_mat"
	hideparameter "gs_flushHandlePlace", "flushHandleMat", "gs_tank_mat"
endif

values "gs_flushHandlePlace" st_flushPlace
values "gs_flushHandlePlace_m" 0, 1, 2

if GLOB_MODPAR_NAME = "gs_flushHandlePlace"  then
	if gs_flushHandlePlace = st_flushPlace[1] then gs_flushHandlePlace_m = 1 	!right
	if gs_flushHandlePlace = st_flushPlace[2] then gs_flushHandlePlace_m = 2	!left
	if gs_flushHandlePlace = st_flushPlace[3] then gs_flushHandlePlace_m = 0	!none
	parameters gs_flushHandlePlace_m = gs_flushHandlePlace_m
else
	if gs_flushHandlePlace_m =  1 then gs_flushHandlePlace = st_flushPlace[1] 	!right
	if gs_flushHandlePlace_m =  2 then gs_flushHandlePlace = st_flushPlace[2] 	!left
	if gs_flushHandlePlace_m =  0 then gs_flushHandlePlace = st_flushPlace[3] 	!none
	parameters gs_flushHandlePlace = gs_flushHandlePlace
endif

if gs_cont_pen=0 then parameters gs_cont_pen=75
if gs_fill_pen=0 then parameters gs_fill_pen=19

if ConStatus_1 = 0 and ConStatus_2 = 0 then HIDEPARAMETER "gs_2d"

values "strGrabBars" st_strGrabBars
values "iGrabBars" 1,2

if GLOB_MODPAR_NAME = "strGrabBars" then
	iGrabBars = GRAB_PARALLEL
	if strGrabBars = st_strGrabBars[2] then iGrabBars = GRAB_CORNER
	parameters iGrabBars = iGrabBars
else
	strGrabBars = st_strGrabBars[1]
	if iGrabBars =  2 then strGrabBars = st_strGrabBars[2]
	parameters strGrabBars = strGrabBars
endif

values "tpd_distFromFloor" range[0.38, 1.06]
if not(bToiletPaper) then
	lock "tpd_distFromSeat", "tpd_distFromWall", "tpd_distFromFloor"
	hideparameter "tpd_distFromSeat", "tpd_distFromWall", "tpd_distFromFloor"
endif
if GLOB_MODPAR_NAME = "tpd_distFromWall" then
	tpd_distFromSeat = tpd_distFromWall - distanceFromWall - bowlDepth
	PARAMETERS tpd_distFromSeat = tpd_distFromSeat
else
	tpd_distFromWall = tpd_distFromSeat + distanceFromWall + bowlDepth
	PARAMETERS tpd_distFromWall = tpd_distFromWall
endif
lock "tpd_distFromSeat"
hideparameter "tpd_distFromSeat"

if sgb and reargb and (sgb_dist - grabBarDiameter/2) < (0.08 + grabBarDiameter/2)	then	!they would collide in corner
	reargb_maxl2 = dAxisFromWall - (0.08 + grabBarDiameter)
else
	reargb_maxl2 = dAxisFromWall - (grabBarDiameter/2 + 0.01)
endif

values "dAxisFromWall" range [minaxis, maxaxis]

if LibraryLangCode = "USA" then
	values "tpd_distFromSeat"  range[0.18, 0.23]
	values "tpd_distFromWall"  range[distanceFromWall + bowlDepth + 0.18, distanceFromWall + bowlDepth + 0.23]

	if iGrabBars = GRAB_PARALLEL then
		values "MSSide" range [(0.89 - bowlWidth) / 2, (0.94 - bowlWidth) / 2]
		values "MSSide2" range [(0.89 - bowlWidth) / 2, (0.94 - bowlWidth) / 2]
		values "MSFront" range [1.525 - distanceFromWall - bowlDepth, ]
	else
		values "MSSide" range [ 1.525 - dAxisFromWall - bowlWidth/2, ]
		if iFlushType = 1 then !cistern
			values "MSFront" range [1.5 - distanceFromWall - bowlDepth, ]
		else
			values "MSFront" range [1.42 - distanceFromWall - bowlDepth, ]
		endif
	endif

	values "reargb_length1" range [0.61, ]
	values "reargb_length2" range [0.305, reargb_maxl2]
	values "vertgb_distFromWall" 1.016
	lock "vertgb_distFromWall"
	values "sgb_length" range [1.065, ]
else
	values "tpd_distFromSeat"  range[0.15, 0.3]
	values "tpd_distFromWall"  range[distanceFromWall + bowlDepth + 0.15, distanceFromWall + bowlDepth + 0.3]

	values "reargb_length1" range [0.2, ]
	values "reargb_length2" range [0.2, reargb_maxl2]
	if sgb then
		values "vertgb_distFromWall" range [distanceFromWall + bowlDepth, sgb_dist + sgb_length]
	else
		values "vertgb_distFromWall" range [distanceFromWall + bowlDepth, distanceFromWall + bowlDepth + MSFront - (grabBarDiameter/2 + 0.01)]
	endif
	values "sgb_length" range [0.1, ]
endif


if bCorner then		!bar on side wall
	MSSide2 = dAxisFromWall - bowlWidth / 2
	parameters MSSide2 = MSSide2
	lock "MSSide2"
endif


if not(reargb) then
	lock "reargb_length1", "reargb_length2"
	hideparameter "reargb_length1", "reargb_length2"
endif

values "sgb_dist" range[(grabBarDiameter/2 + 0.01), ]

if (iGrabBars = GRAB_CORNER and not(sgb)) or \
	(iGrabBars = GRAB_PARALLEL and not(iPositionGrabBars = GRAB_SIDEREAR or iPositionGrabBars = GRAB_SIDESIDE)) then

	lock "sgb_length", "sgb_dist"
	hideparameter "sgb_length", "sgb_dist"
endif

values "vertgb_length" range [0.1, ]
values "vertgb_distFromFloor" range [grabBarDiameter/2 + 0.01, ]
if not(vertgb) then
	lock "vertgb_length", "vertgb_distFromFloor", "vertgb_distFromWall"
	hideparameter "vertgb_length", "vertgb_distFromFloor", "vertgb_distFromWall"
endif

values "rearsidegb_distFromFloor" range[0.8382, 0.9144]
if not(reargb or sgb or (iGrabBars = GRAB_PARALLEL)) then
	lock "rearsidegb_distFromFloor"
	hideparameter "rearsidegb_distFromFloor"
endif

if not(reargb or sgb or vertgb or (iGrabBars = GRAB_PARALLEL)) then
	lock "matGrabBar"
	hideparameter "matGrabBar"
endif

values "grabBarDiameter" range[0.02, 0.05]
values "gripBarDiameter" range[grabBarDiameter+0.01, 0.11]


if not(bEnableParGrabBars) then
	parameters strGrabBars = st_strGrabBars[2]
	parameters iGrabBars = GRAB_CORNER
	lock "strGrabBars","iGrabBars","iPositionGrabBars","matGrip","gripBarDiameter"
	hideparameter "strGrabBars","iGrabBars","iPositionGrabBars","matGrip","gripBarDiameter"
else
	if iGrabBars = GRAB_PARALLEL then
		lock "reargb", "sgb", "vertgb", "vertgb_length", "vertgb_distFromFloor"
		hideparameter "reargb", "sgb", "vertgb", "vertgb_length", "vertgb_distFromFloor"
		if not(iPositionGrabBars = GRAB_SIDEREAR | iPositionGrabBars = GRAB_SIDESIDE) then
			lock "matDispenser"
			hideparameter "matDispenser"
		endif
	else
		lock "iPositionGrabBars", "matGrip","gripBarDiameter"
		hideparameter "iPositionGrabBars", "matGrip","gripBarDiameter"
	endif
endif

if iGrabBars = GRAB_CORNER and not(reargb or sgb or vertgb) then
	lock "grabBarDiameter"
	hideparameter "grabBarDiameter"
endif

if iGrabBars = GRAB_PARALLEL and not(iPositionGrabBars = GRAB_FLOOR or iPositionGrabBars = GRAB_TOILET) then
	lock "gripBarDiameter"
	hideparameter "gripBarDiameter"
endif

values "gs_resol" range [3, )

! =============================================================================
! Onorm list Settings
! =============================================================================
if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
	call "Onorm_Plumbing" parameters all
else
	hideparameter "gs_onorm_Title"
endif
