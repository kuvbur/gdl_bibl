
if gs_detlevel_3D_m = 0 then end

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

W=a
H=ZZYZX
D=b

!!W=Width
!!H=Height
!!D=Depth

ADD W/2,D/2,0
ROTZ 180



HOTSPOT 0,0,0
HOTSPOT W/2, D/2-0.01,0
HOTSPOT W/2,-D/2,0
HOTSPOT -W/2, D/2-0.01,0
HOTSPOT -W/2,-D/2,0
HOTSPOT W/2, D/2,PLH
HOTSPOT W/2,-D/2,H
HOTSPOT -W/2,-D/2,H
HOTSPOT -W/2, D/2,PLH


PEN gs_cont_pen
RESOL HVAC_res
MATERIAL HVAC_matLeg

!***** Legs
PRISM 4,legH,
	W/2, D/2-0.01,
	W/2,-D/2,
	W/2-legW,-D/2,
	W/2-legW, D/2-0.01
PRISM 4,legH,
	-W/2, D/2-0.01,
	-W/2,-D/2,
	-W/2+legW,-D/2,
	-W/2+legW, D/2-0.01

!***** Cover
MATERIAL HVAC_matCover
addz legH
RULED 4,1+4+16+32,
	 W/2, D/2,0,
	 W/2,-D/2,0,
	-W/2,-D/2,0,
	-W/2, D/2,0,

	 W/2, D/2,PLH-legH,
	 W/2,-D/2,H-legH,
	-W/2,-D/2,H-legH,
	-W/2, D/2,PLH-legH

addz PLH-legH
addy D/2
addx -W/2
rotz -90
roty -ang

POLY_ 4,
	0,0,1,
	0,legW,1,
	D1,legW,1,
	D1,0,1

addy W-legW

POLY_ 4,
	0,0,1,
	0,legW,1,
	D1,legW,1,
	D1,0,1

addy -(W-2*legW)

!**** Grille
MATERIAL HVAC_matGrille

POLY_ 10,
	 0, 0,1,
	 0,W-2*legW,1,
	 D1,W-2*legW,1,
	 D1, 0,1,
	 0, 0,-1,
	 0.02,0.02,1,
	 0.02,W-2*legW-0.02,0,
	 D1-0.02,W-2*legW-0.02,1,
	 D1-0.02, 0.02,0,
	 0.02, 0.02,-1

For i=0.03 to D1-0.02 step 0.02
	POLY_ 4,
		i,0.02,1,
		i,W-2*legW-0.02,0,
		i+0.01,W-2*legW-0.02,1,
		i+0.01,0.02,0
	LIN_ i-0.01,0.02,0,i,0.02,0
	LIN_ i-0.01,W-2*legW-0.02,0,i,W-2*legW-0.02,0
Next i

DEL top
END
