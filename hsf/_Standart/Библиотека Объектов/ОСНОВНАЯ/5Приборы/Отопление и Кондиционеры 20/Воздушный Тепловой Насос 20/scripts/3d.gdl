
! ==============================================================================
! Air Source Heat Pump
! ==============================================================================


if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then shadow off


IF GLOB_CONTEXT > 40 AND GLOB_CONTEXT < 50 THEN gs_detlevel_3D_m = 1

! --- start of modifying to Shaft

IF bShowMEP THEN

					! --- CONNECTIONS --- !

	IF useSysMat THEN
		matBox		= sMat
		matGrill	= sMat
		matFan		= sMat
		gs_con_mat 	= sMat
	ENDIF

	PEN gs_cont_pen
	IF gs_detlevel_3D_m = 1 THEN RESOL 8
	IF gs_detlevel_3D_m = 2 THEN RESOL 24

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 17,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 17,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 15

ENDIF

! --- end of modifying to Shaft


IF gs_detlevel_3D_m = 1 THEN RESOL 12
IF gs_detlevel_3D_m = 2 THEN RESOL gs_resol

maskSection = 4		!0
if GLOB_CONTEXT = 4 then maskSection = 4


define texture "fan_grill" 1,		!"fan_grill.png"
0.6*0.1, 0.1*0.1, 1 + 2, 0

define material "fan_grill" 20,
	0, 0, 0,
	0.58, 0.85, 0.2, 0.01,
	50,
	4,
	0.6, 0.6, 0.6,
	0, 0, 0,
	0,
	0, 1,
	ind(texture, "fan_grill")

	! ======================================================================
	! Black Plastic Surface
	! ======================================================================

	define material	"builtInMatBlackPlastic" 0,
	  0.333333,   0.313725,   0.294118,
	 0.15,  0.15,  0.45,     0,      5,     0,
	  0.784314,   0.784314,   0.784314,
	         0,          0,          0,     0

PEN gs_cont_pen

unID = 1
hotspot 0, 0, 0, unID : unID = unID + 1
hotspot A, 0, 0, unID : unID = unID + 1
hotspot A, B, 0, unID : unID = unID + 1
hotspot 0, B, 0, unID : unID = unID + 1
hotspot 0, 0, ZZYZX, unID : unID = unID + 1
hotspot A, 0, ZZYZX, unID : unID = unID + 1
hotspot A, B, ZZYZX, unID : unID = unID + 1
hotspot 0, B, ZZYZX, unID : unID = unID + 1

add A/2, B/2, 0

if gs_detlevel_3D_m = 1 then

	material matBox

	if bLeg then
		for mi = 1 to 2
		add -A/2+1/5*A, B/2, 0
		rotx 90
		prism_ 5, B,
			-2*footOvhg, 0, 15,
			2*footOvhg, 0, 15,
			2*footOvhg, footHeight, 15,
			-2*footOvhg, footHeight, 15,
			-2*footOvhg, 0, -1
		del 2
		mulx -1
		next mi
		del 2
	endif

	material "builtInMatBlackPlastic"
	add -A/2 + borderWidth, boxY/4 + eps, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter
	rotx 90
	prism_ 4, eps,
		0, holeDiameter/2, 15+64,
		0, -1, 800,
		holeDiameter, holeDiameter/2, 1015+64,
		0, holeDiameter/2, 1015+64
	del 2

	material matBox
	add -A/2 + borderWidth, B/4 + eps, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter
	rotx 90
	cutform 4, 1, 1,
		0, 0, 1, B,
		0, holeDiameter/2, 31+64,
		0, -1, 800,
		holeDiameter, holeDiameter/2, 1031+64,
		0, holeDiameter/2, 1031+64
	del 2

	add A/2 - boxRecess, 0, 0
	rotz 90
	rotx 90
	cutform 4, 1, 1,
		0, 0, 1, 10*boxRecess,
		boxY/2 - borderWidth, footHeight + borderWidth, 31,
		0, footHeight + borderWidth, 31,
		0, footHeight + borderWidth + B/2, 31,
		boxY/2 - borderWidth, footHeight + borderWidth + B/2, 31
	del 3

	addz footHeight
	prism_ 5, ZZYZX - footHeight,
		-A/2, -boxY/2, 15,
		A/2, -boxY/2, 15,
		A/2, boxY/2, 15,
		-A/2, boxY/2, 15,
		-A/2, -boxY/2, -1
	del 1

	cutend
	cutend

	material matFan
	add -A/2 + borderWidth + holeDiameter/2, boxY/4, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter/2
	rotx 90
	cylind boxY*0.6, axisR
	del 2

	fi = 60
	rs = 0.07
	dx = -0.01

	add -A/2 + borderWidth + holeDiameter/2 - axisR, B/2, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter/2 - axisR
	rotx 90
	cutform 4, 1, 1,
		0, 0, 1, B,
		0, axisR, 31+64,
		0, -1, 800,
		2*axisR, axisR, 1031+64,
		0, axisR, 1031+64
	del 2

	fanResol = 2
	fanAngle = 70
	fanLength = holeDiameter/2 - 0.03

	add -A/2 + borderWidth + holeDiameter/2, -boxY/16, ZZYZX -  topFringeHeight - borderWidth - boxRecess - holeDiameter/2

	for hi = 1 to 5

		for hj = 1 to fanResol
			put (0.9*axisR)*cos(fanAngle/(fanResol-1)*(hj-1)), (0.9*axisR)*sin(fanAngle/(fanResol-1)*(hj-1)), -0.01*(hj-1)
		next hj
		for hk = 1 to fanResol
			put fanLength*cos(fanAngle/(fanResol-1)*(hk-1)), fanLength*sin(fanAngle/(fanResol-1)*(hk-1)), -0.01*(hk-1)
		next hk

		rotx  -90
		mulz 100/(fanResol-1)*boxY/4
		coons fanResol, 2, 1+2+16+32,
			get (nsp/2),
			get (nsp),

			(0.9*axisR), 0, 0,
			fanLength, 0, 0,

			(0.9*axisR)*cos(fanAngle), (0.9*axisR)*sin(fanAngle), -0.01*(fanResol-1),
			fanLength*cos(fanAngle), fanLength*sin(fanAngle), -0.01*(fanResol-1)
		del 2

	roty 360/5
	next hi
	del 5 + 1

	cutend

	if GLOB_CONTEXT <> 5 then
		material "fan_grill"
		plane 4,
			-A/2 + borderWidth, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter,
			-A/2 + borderWidth + holeDiameter, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter,
			-A/2 + borderWidth + holeDiameter, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess,
			-A/2 + borderWidth, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess

		base
		tx = -A/2 + borderWidth + holeDiameter/2
		ty = 0
		tz = ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter/2
		add tx, ty, tz
			VERT 0, 0, 0
			VERT 1, 0, 0
			VERT 0, 1, 0
			VERT 0, 0, 1
		del 1
		coor 2+256, -1, -2, -3, -4
		body -1
	endif

	material matGrill
	addy -boxY/2 - 2*eps*(GLOB_CONTEXT = 5)
	rotx 90
	prism_ 5 + 5, grillFrameWidth,
		-A/2 + borderWidth - borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter - borderWidth/4, 15,
		-A/2 + borderWidth + holeDiameter + borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter - borderWidth/4, 15,
		-A/2 + borderWidth + holeDiameter + borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess + borderWidth/4, 15,
		-A/2 + borderWidth - borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess + borderWidth/4, 15,
		-A/2 + borderWidth - borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter - borderWidth/4, -1,

		-A/2 + borderWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter, 15,
		-A/2 + borderWidth + holeDiameter, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter, 15,
		-A/2 + borderWidth + holeDiameter, ZZYZX - topFringeHeight - borderWidth -boxRecess, 15,
		-A/2 + borderWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess, 15,
		-A/2 + borderWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter, -1
	del 2

endif


if gs_detlevel_3D_m = 2 then

	material matBox

	if bLeg then
		add A/2 - boxRecess, 0, 0
		rotz 90
		rotx 90
		cutform 10, 1, 1+8+16,
			0, 0, 1, 10*boxRecess,
			-B/2, footHeight*2, 31,
			-B/2, profileThk, 31,
			-boxY/2 + boxRoundoffHor, footHeight, 31,
			-boxY/2 + boxRoundoffHor + 3*footHeight, footHeight, 31,
			-boxY/2 + boxRoundoffHor + 3*footHeight + footOvhg + boxRoundoffHor, profileThk, 31,
			boxY/2 - boxRoundoffHor - 3*footHeight - footOvhg - boxRoundoffHor, profileThk, 31,
			boxY/2 - boxRoundoffHor - 3*footHeight, footHeight, 31,
			boxY/2 - boxRoundoffHor, footHeight, 31,
			B/2, profileThk, 31,
			B/2, 2*footHeight, 31
		del 3

		for mi = 1 to 2
			add -A/2+1/5*A, B/2, 0
			rotx 90
			prism_ 9, B,
				-2*footOvhg, 0, 15,
				2*footOvhg, 0, 15,
				2*footOvhg, footHeight, 15,
				2*footOvhg-profileThk, footHeight, 15,
				2*footOvhg-profileThk, profileThk, 15,
				-2*footOvhg+profileThk, profileThk, 15,
				-2*footOvhg+profileThk, footHeight, 15,
				-2*footOvhg, footHeight, 15,
				-2*footOvhg, 0, -1
			del 2
		mulx -1
		next mi
		del 2

		cutend
	endif

	material "builtInMatBlackPlastic"
	resol 2 * gs_resol
	add -A/2 + borderWidth, boxY/4 + eps, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter
	rotx 90
	prism_ 4, eps,
		0, holeDiameter/2, 15+64,
		0, -1, 800,
		holeDiameter, holeDiameter/2, 1015+64,
		0, holeDiameter/2, 1015+64
	del 2

	material matBox
	add -A/2 + borderWidth, boxY/4 + eps, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter
	rotx 90
	cutform 4, 1, 1,
		0, 0, 1, B,
		0, holeDiameter/2, 31+64,
		0, -1, 800,
		holeDiameter, holeDiameter/2, 1031+64,
		0, holeDiameter/2, 1031+64
	del 2
	resol gs_resol

	if A > ZZYZX - footHeight - topfringeHeight - boxRecess + 3 * borderWidth then
		for ki = 1 to 5
			add 0, -boxY/2 + boxRecess, (holeDiameter/2 + 1/4*borderWidth - 3*boxRecess)/5
			rotx 90
			cutform 5, 1, 1,
				0, 0, 1, 10*boxRecess,
				-A/2 + borderWidth + holeDiameter + borderWidth/4 + 3/4*borderWidth, footHeight + 3/4*borderWidth, 31,
				A/2 - 3/4*borderWidth, footHeight + 3/4*borderWidth, 31,
				A/2 - 3/4*borderWidth, footHeight + 3/4*borderWidth + 3 * boxRecess, 31,
				-A/2 + borderWidth + holeDiameter + borderWidth/4 + 3/4*borderWidth, footHeight + 3/4*borderWidth + 3 * boxRecess, 31,
				-A/2 + borderWidth + holeDiameter + borderWidth/4 + 3/4*borderWidth, footHeight + 3/4*borderWidth, -1
			del 2
		addz (holeDiameter/2 + 1/4*borderWidth - 3*boxRecess)/5
		next ki
		del 5
	endif

	if sideHoleWidth > 0 & sideHoleHeight > 0 then
		for hh = 1 to nSideHoleVert
			for hi = 1 to 2
				add -A/2, 0, 0
				rotz 90
				rotx 90

				material "builtInMatBlackPlastic"
				addz borderWidth/4 - 0.002
				poly 4,
					-boxY/2 + borderWidth/2, footHeight + borderWidth/2,
					-boxY/2 + borderWidth/2 + sideHoleWidth, footHeight + borderWidth/2,
					-boxY/2 + borderWidth/2 + sideHoleWidth, footHeight + borderWidth/2 + sideholeHeight,
					-boxY/2 + borderWidth/2, footHeight + borderWidth/2 + sideholeHeight
				del 1

				material matBox
				addz -borderWidth/4
				cutform 4, 1, 1,
					0, 0, 1, borderWidth/2 + eps,
					-boxY/2 + borderWidth/2, footHeight + borderWidth/2, 31,
					-boxY/2 + borderWidth/2 + sideHoleWidth, footHeight + borderWidth/2, 31,
					-boxY/2 + borderWidth/2 + sideHoleWidth, footHeight + borderWidth/2 + sideholeHeight, 31,
					-boxY/2 + borderWidth/2, footHeight + borderWidth/2 + sideholeHeight, 31
				del 1

				del 3
			addy sideHoleWidth + borderWidth/4
			next hi
			del 2
		addz sideHoleHeight + borderWidth/4
		next hh
		del nSideHoleVert
	endif

	add A/2 - boxRecess, 0, 0
	rotz 90
	rotx 90
	cutform 4, 1, 1,
		0, 0, 1, 10*boxRecess,
		boxY/2 - borderWidth, footHeight + borderWidth, 31,
		0, footHeight + borderWidth, 31,
		0, footHeight + borderWidth + B/2, 31,
		boxY/2 - borderWidth, footHeight + borderWidth + B/2, 31
	del 3

	addz ZZYZX - topFringeHeight - boxRecess
	mulz -1
	hprism_ matBox, matBox, matBox, matBox,
		10, ZZYZX - topFringeHeight - boxRecess - footHeight, 0, boxRoundoffVer, 1,
		0, -boxY/2, 9+maskSection,
		A/2-boxRoundoffHor, -boxY/2, 11+64+maskSection,
		A/2, -boxY/2+boxRoundoffHor, 11+1064+maskSection,
		A/2, boxY/2-boxRoundoffHor, 11+64+maskSection,
		A/2-boxRoundoffHor, boxY/2, 11+1064+maskSection,
		-A/2+boxRoundoffHor, boxY/2, 11+64+maskSection,
		-A/2, boxY/2-boxRoundoffHor, 11+1064+maskSection,
		-A/2, -boxY/2+boxRoundoffHor, 11+64+maskSection,
		-A/2+boxRoundoffHor, -boxY/2, 11+1064+maskSection,
		0, -boxY/2, -1
	del 2

	cutend

	if sideHoleWidth > 0 & sideHoleHeight > 0 then
		for hh = 1 to nSideHoleVert
			for hi = 1 to 2
				cutend
			next hi
		next hh
	endif

	for ki = 1 to 5
			if A > ZZYZX - footHeight - topfringeHeight - boxRecess + 3 * borderWidth then cutend
	next ki
	cutend

	if A > ZZYZX - footHeight - topfringeHeight - boxRecess + 3 * borderWidth then
		addy -boxY/2
		rotx 90
		prism_ 5, boxRecess,
			-A/2 + borderWidth + holeDiameter + borderWidth/4 + 3/4*borderWidth, footHeight + borderWidth + holeDiameter/2 + 3/4*borderWidth, 15,
			A/2 - 3/4*borderWidth, footHeight + borderWidth + holeDiameter/2 + 3/4*borderWidth, 15,
			A/2 - 3/4*borderWidth, footHeight + borderWidth + holeDiameter + 1/4*borderWidth, 15,
			-A/2 + borderWidth + holeDiameter + borderWidth/4 + 3/4*borderWidth, footHeight + borderWidth + holeDiameter + 1/4*borderWidth, 15,
			-A/2 + borderWidth + holeDiameter + borderWidth/4 + 3/4*borderWidth, footHeight + borderWidth + holeDiameter/2 + 3/4*borderWidth, -1
		del 2
	endif

	if - 3/4*borderWidth > - borderWidth - holeDiameter/2 + 7/4*borderWidth then
		addx A/2
		rotz 90
		rotx 90
		prism_ 5, boxRecess,
			-boxY/2 + borderWidth, ZZYZX - topFringeHeight - boxRecess - borderWidth - holeDiameter/2 + 7/4*borderWidth, 15,
			0, ZZYZX - topFringeHeight - boxRecess - borderWidth - holeDiameter/2 + 7/4*borderWidth, 15,
			0, ZZYZX - topFringeHeight - boxRecess - 3/4*borderWidth, 15,
			-boxY/2 + borderWidth, ZZYZX - topFringeHeight - boxRecess - 3/4*borderWidth, 15,
			-boxY/2 + borderWidth, ZZYZX - topFringeHeight - boxRecess - borderWidth - holeDiameter/2 + 7/4*borderWidth, -1
		del 3
	endif

	addz ZZYZX - topFringeHeight - boxRecess
	prism_ 10, boxRecess,
		0, -boxY/2+boxRecess, 13,
		A/2-boxRoundoffHor, -boxY/2+boxRecess, 15+64,
		A/2-boxRecess, -boxY/2+boxRoundoffHor, 15+1064,
		A/2-boxRecess, boxY/2-boxRoundoffHor, 15+64,
		A/2-boxRoundoffHor, boxY/2-boxRecess, 15+1064,
		-A/2+boxRoundoffHor, boxY/2-boxRecess, 15+64,
		-A/2+boxRecess, boxY/2-boxRoundoffHor, 15+1064,
		-A/2+boxRecess, -boxY/2+boxRoundoffHor, 15+64,
		-A/2+boxRoundoffHor, -boxY/2+boxRecess, 15+1064,
		0, -boxY/2+boxRecess, -1
	del 1

	for kj = 1 to 3
		add 0, 0, ZZYZX - boxRecess
		cutform 5, 1, 1,
			0, 0, 1, 10*boxRecess,
			-A/2 + borderWidth, -boxY/2 + borderWidth, 31,
			A/2 - borderWidth, -boxY/2 + borderWidth, 31,
			A/2 - borderWidth,  -boxY/2 + borderWidth + (boxY - 4*borderWidth)/3, 31,
			-A/2 + borderWidth,  -boxY/2 + borderWidth + (boxY - 4*borderWidth)/3, 31,
			-A/2 + borderWidth, -boxY/2 + borderWidth, -1
		del 1
	addy (boxY - 4*borderWidth)/3 + borderWidth
	next kj
	del 3

	addz ZZYZX - topFringeHeight
	hprism_ matBox, matBox, matBox, matBox,
		10, topFringeHeight, 0, boxRoundoffVer, 1,
		0, -boxY/2, 9+maskSection,
		A/2-boxRoundoffHor, -boxY/2, 11+64+maskSection,
		A/2, -boxY/2+boxRoundoffHor, 11+1064+maskSection,
		A/2, boxY/2-boxRoundoffHor, 11+64+maskSection,
		A/2-boxRoundoffHor, boxY/2, 11+1064+maskSection,
		-A/2+boxRoundoffHor, boxY/2, 11+64+maskSection,
		-A/2, boxY/2-boxRoundoffHor, 11+1064+maskSection,
		-A/2, -boxY/2+boxRoundoffHor, 11+64+maskSection,
		-A/2+boxRoundoffHor, -boxY/2, 11+1064+maskSection,
		0, -boxY/2, -1
	del 1

	cutend
	cutend
	cutend

	material matFan
	add -A/2 + borderWidth + holeDiameter/2, boxY/4, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter/2
	rotx 90
	cylind boxY*0.6, axisR
	del 2

	fi = 60
	rs = 0.07
	dx = -0.01

	add -A/2 + borderWidth + holeDiameter/2 - axisR, B/2, ZZYZX -  topFringeHeight - borderWidth -boxRecess - holeDiameter/2 - axisR
	rotx 90
	cutform 4, 1, 1,
		0, 0, 1, B,
		0, axisR, 31+64,
		0, -1, 800,
		2*axisR, axisR, 1031+64,
		0, axisR, 1031+64
	del 2

	fanResol = 3*int(gs_resol/5)
	fanAngle = 70
	fanLength = holeDiameter/2 - 0.03

	add -A/2 + borderWidth + holeDiameter/2, -boxY/16, ZZYZX -  topFringeHeight - borderWidth - boxRecess - holeDiameter/2
	for hi = 1 to 5

		for hj = 1 to fanResol
			put (0.9*axisR)*cos(fanAngle/(fanResol-1)*(hj-1)), (0.9*axisR)*sin(fanAngle/(fanResol-1)*(hj-1)), -0.01*(hj-1)
		next hj
		for hk = 1 to fanResol
			put fanLength*cos(fanAngle/(fanResol-1)*(hk-1)), fanLength*sin(fanAngle/(fanResol-1)*(hk-1)), -0.01*(hk-1)
		next hk

		rotx  -90
		mulz 100/(fanResol-1)*boxY/4
		coons fanResol, 6, 1+2+16+32,
			get (nsp/2),
			get (nsp),

			(0.9*axisR), 0, 0,
			(0.9*axisR)+(fanLength-(0.9*axisR))*0.2, -0.017, 0,
			(0.9*axisR)+(fanLength-(0.9*axisR))*0.4, -0.025, 0,
			(0.9*axisR)+(fanLength-(0.9*axisR))*0.6, -0.025, 0,
			(0.9*axisR)+(fanLength-(0.9*axisR))*0.8, -0.017, 0,
			fanLength, 0, 0,

			(0.9*axisR)*cos(fanAngle), (0.9*axisR)*sin(fanAngle), -0.01*(fanResol-1),
			((0.9*axisR)+(fanLength-(0.9*axisR))*0.2)*cos(fanAngle)+0.017*sin(fanAngle), ((0.9*axisR)+(fanLength-(0.9*axisR))*0.2)*sin(fanAngle)-0.017*cos(fanAngle), -0.01*(fanResol-1),
			((0.9*axisR)+(fanLength-(0.9*axisR))*0.4)*cos(fanAngle)+0.025*sin(fanAngle), ((0.9*axisR)+(fanLength-(0.9*axisR))*0.4)*sin(fanAngle)-0.025*cos(fanAngle), -0.01*(fanResol-1),
			((0.9*axisR)+(fanLength-(0.9*axisR))*0.6)*cos(fanAngle)+0.025*sin(fanAngle), ((0.9*axisR)+(fanLength-(0.9*axisR))*0.6)*sin(fanAngle)-0.025*cos(fanAngle), -0.01*(fanResol-1),
			((0.9*axisR)+(fanLength-(0.9*axisR))*0.8)*cos(fanAngle)+0.017*sin(fanAngle), ((0.9*axisR)+(fanLength-(0.9*axisR))*0.8)*sin(fanAngle)-0.017*cos(fanAngle), -0.01*(fanResol-1),
			fanLength*cos(fanAngle), fanLength*sin(fanAngle), -0.01*(fanResol-1)
		del 2

	roty 360/5
	next hi
	del 5 + 1

	cutend

	if GLOB_CONTEXT <> 5 then
		material "fan_grill"
		plane 4,
			-A/2 + borderWidth, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter,
			-A/2 + borderWidth + holeDiameter, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter,
			-A/2 + borderWidth + holeDiameter, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess,
			-A/2 + borderWidth, -boxY/2 - grillFrameWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess

		base
		tx = -A/2 + borderWidth + holeDiameter/2
		ty = 0
		tz = ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter/2
		add tx, ty, tz
			VERT 0, 0, 0
			VERT 1, 0, 0
			VERT 0, 1, 0
			VERT 0, 0, 1
		del 1
		coor 2+256, -1, -2, -3, -4
		body -1
	endif

	material matGrill
	addy -boxY/2 - 2*eps*(GLOB_CONTEXT = 5)
	rotx 90
	prism_ 5 + 5, grillFrameWidth,
		-A/2 + borderWidth - borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter - borderWidth/4, 15,
		-A/2 + borderWidth + holeDiameter + borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter - borderWidth/4, 15,
		-A/2 + borderWidth + holeDiameter + borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess + borderWidth/4, 15,
		-A/2 + borderWidth - borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess + borderWidth/4, 15,
		-A/2 + borderWidth - borderWidth/4, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter - borderWidth/4, -1,

		-A/2 + borderWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter, 15,
		-A/2 + borderWidth + holeDiameter, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter, 15,
		-A/2 + borderWidth + holeDiameter, ZZYZX - topFringeHeight - borderWidth -boxRecess, 15,
		-A/2 + borderWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess, 15,
		-A/2 + borderWidth, ZZYZX - topFringeHeight - borderWidth -boxRecess - holeDiameter, -1
	del 2

endif

del 1

end
