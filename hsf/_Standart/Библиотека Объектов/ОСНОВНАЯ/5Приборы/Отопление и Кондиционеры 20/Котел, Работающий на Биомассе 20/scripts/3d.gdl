
! ==============================================================================
! Biomass Boiler
! ==============================================================================


if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then shadow off

! ======================================================================
! Define Surfaces
! ======================================================================

	define material	"builtInMatBlackPlastic" 0,
	  0.333333,   0.313725,   0.294118,
	 0.15,  0.15,  0.45,     0,      5,     0,
	  0.784314,   0.784314,   0.784314,
	         0,          0,          0,     0
			 
			 
	! ======================================================================
	! Red Button Surface
	! ======================================================================	

		define material	"builtInRedButton" 0,
		   0.92549,          0,          0,
		  0.9,   0.9,   0.2,     0,  13.03,     0,
				 1,          1,          1,
				 0,          0,          0,		 0
				 

! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

PEN gs_cont_pen
IF gs_detlevel_3D_m = 1 THEN RESOL 8
IF gs_detlevel_3D_m = 2 THEN RESOL 24


IF bShowMEP THEN
	IF useSysMat THEN
		matBoiler	= sMat
		matDoor	= sMat
		matHandle	= sMat
		gs_con_mat	= sMat
	ENDIF

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 15
ENDIF

! --- end of modifying to Shaft


if gs_detlevel_3D_m = 1 then resol 8
if gs_detlevel_3D_m = 2 then resol gs_resol

pen gs_cont_pen

unID = 1
hotspot 0, 0, 0, unID : unID = unID + 1
hotspot A, 0, 0, unID : unID = unID + 1
hotspot A, B, 0, unID : unID = unID + 1
hotspot 0, B, 0, unID : unID = unID + 1
hotspot 0, 0, ZZYZX, unID : unID = unID + 1
hotspot A, 0, ZZYZX, unID : unID = unID + 1
hotspot A, B, ZZYZX, unID : unID = unID + 1
hotspot 0, B, ZZYZX, unID : unID = unID + 1

add A/2, B/2, 0

material matBoiler
add 0, B/2-yBox, footHeight + zBox - boxChamfer
rotx 45
addz -displayRecess-eps*20
cutform 4, 1, 1,
	0, 0, 1, displayRecess+eps*20,
	-A/2 + displayFrameW, 2*displayFrameW, 31,
	A/2 - displayFrameW, 2*displayFrameW, 31,
	A/2 - displayFrameW, boxChamfer*sqr(2) - 2*displayFrameW, 31,
	-A/2 + displayFrameW, boxChamfer*sqr(2) - 2*displayFrameW, 31
del 3

add A/2, B/2, footHeight
rotz -90
rotx 90
prism_ 6, A,
	0, 0, 15,
	yBox, 0, 15,
	yBox, zBox - boxChamfer, 15,
	yBox - boxChamfer, zBox, 15,
	0, zBox, 15,
	0, 0, -1
del 3

cutend

material "builtInMatBlackPlastic"
add 0, B/2-yBox, footHeight + zBox - boxChamfer
rotx 45
addz -displayRecess
plane 4,
	-A/2 + displayFrameW, 4*displayFrameW, 0,
	A/2 - displayFrameW, 4*displayFrameW, 0,
	A/2 - displayFrameW, boxChamfer*sqr(2) - 4*displayFrameW, 0,
	-A/2 + displayFrameW, boxChamfer*sqr(2) - 4*displayFrameW, 0
del 3

material matDoor
for hi = 1 to 2
	add A/2, B/2, 0
	rotz -90
	rotx 90
	prism_ 5, A,
		yBox/10, 0, 15,
		yBox/10 + footThk, 0, 15,
		yBox/10 + footThk, footHeight, 15,
		yBox/10, footHeight, 15,
		yBox/10, 0, -1
	del 3
addy -0.8*B
next hi
del 2


if gs_detlevel_3D_m = 1 then

	material matDoor
	add A/2, B/2, footHeight
	rotz -90
	rotx 90
	prism_ 5, A,
		yBox, 0, 15,
		yBox + doorThk, 0, 15,
		yBox + doorThk, (zBox - boxChamfer)/11*10, 15,
		yBox, (zBox - boxChamfer)/11*10, 15,
		yBox, 0, -1
	del 3

endif


if gs_detlevel_3D_m = 2 then

	material "builtInMatBlackPlastic"
	for bj = 1 to 2
		add 0, B/2-yBox, footHeight + zBox - boxChamfer
		rotx 45
		add A/2 - displayFrameW - 2*buttonW, boxChamfer*sqr(2)/2, -displayRecess
		cylind 3*buttonThk, buttonH/2
		del 3
	addx -2*buttonW
	next bj
	del 2

	material "builtInRedButton"
	for bi = 1 to 2
		add 0, B/2-yBox, footHeight + zBox - boxChamfer
		rotx 45
		add -A/2 + displayFrameW + buttonW, boxChamfer*sqr(2)/2 + buttonH/2, -displayRecess
		rotx 90
		prism_ 7, buttonH,
			0, 0, 15,
			buttonW, 0, 15,
			buttonW, buttonThk, 15,
			buttonW/2 + buttonW/2*cos(15), buttonThk + buttonW/2*sin(15), 15,
			buttonW/2, buttonThk, 15,
			0, buttonThk, 15,
			0, 0, -1
		del 4
	addx 2*buttonW
	next bi
	del 2

	material matDoor
	add A/2, B/2, footHeight
	rotz -90
	rotx 90
	prism_ 5, A,
		yBox, 0, 15,
		yBox + doorThk, 0, 15,
		yBox + doorThk, (zBox - boxChamfer)/11*3, 15,
		yBox, (zBox - boxChamfer)/11*3, 15,
		yBox, 0, -1
	del 3

	add A/2, B/2, footHeight
	rotz -90
	rotx 90
	prism_ 7, A,
		yBox, (zBox - boxChamfer)/11*3 + doorGap, 15,
		yBox + doorThk/2, (zBox - boxChamfer)/11*3 + doorGap, 15,
		yBox + doorThk, (zBox - boxChamfer)/11*3 + doorThk/2 + doorGap, 15,
		yBox + doorThk, (zBox - boxChamfer)/11*5 - doorThk/2 - doorGap, 15,
		yBox + doorThk/2, (zBox - boxChamfer)/11*5 - doorGap, 15,
		yBox, (zBox - boxChamfer)/11*5 - doorGap, 15,
		yBox, (zBox - boxChamfer)/11*3 + doorGap, -1
	del 3

	add A/2, B/2, footHeight
	rotz -90
	rotx 90
	prism_ 5, A,
		yBox, (zBox - boxChamfer)/11*5, 15,
		yBox + doorThk, (zBox - boxChamfer)/11*5, 15,
		yBox + doorThk, (zBox - boxChamfer)/11*10, 15,
		yBox, (zBox - boxChamfer)/11*10, 15,
		yBox, (zBox - boxChamfer)/11*5, -1
	del 3

	material matHandle
	for hj = 1 to 2
		add -A/2 + xHandle, -B/2, footHeight + (zBox - boxChamfer)/11
		rotx 90
		cylind handleL, handleD/2
		del 2

		add -A/2 + xHandle, -B/2-handleL*0.75, footHeight + (zBox - boxChamfer)/11
		rotx 90
		cutform 2, 1, 1+8+16,
			0, 0, 1, 1,
			0, 0, 931+64,
			handleD/2, 360, 4031+64
		roty 90
		rotx 90
		cylind handleL, handleD/2*0.8
		del 2
		cutend
		del 2
	addz (zBox - boxChamfer)/11*5
	next hj
	del 2

endif

del 1

end
