
!!Gas Heater.gsm


IF gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
ENDIF

IF gs_detlevel_3D_m = 0 THEN END

IF gs_shadow=0 THEN SHADOW OFF
IF gs_shadow=1 THEN SHADOW ON

hotspot 0,0,0
hotspot a,0,0
hotspot a,b,0
hotspot 0,b,0
hotspot 0,0,zzyzx
hotspot a,0,zzyzx
hotspot a,b,zzyzx
hotspot 0,b,zzyzx


! --- start of modifying to Shaft

				! --- CONNECTIONS --- !

PEN gs_cont_pen
IF gs_detlevel_3D_m = 1 THEN RESOL 8
IF gs_detlevel_3D_m = 2 THEN RESOL 24


IF bShowMEP THEN

	IF useSysMat THEN
		bomat	= sMat
		ctmat	= sMat
		gs_con_mat	= sMat
		kmat	= sMat
	ENDIF

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		cShow3D = 1,
		gs_ConMat = gs_con_mat,
		MEP_InsShow = 0,
		MEP_cline_show_3D = 0,
		gs_cont_pen = gs_cont_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 16,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 16,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConPosZ_3=ConPosZ_3, ConLength_3=ConLength_3, ConEdit_3 = 16,
		ConPosX_4=ConPosX_4, ConPosY_4=ConPosY_4, ConPosZ_4=ConPosZ_4, ConLength_4=ConLength_4, ConEdit_4 = 1
ENDIF

! --- end of modifying to Shaft


pen gs_cont_pen


Height=zzyzx
r1=ConWidth_4/2+ConWallThickness_4     !! Radius of the Tube
r2=ConWidth_4/1.6
h=0.02
d=0.1               !! Distance between wall and GasHeater

!!!!!!!!!!!!!!!!!!!!  Base

MATERIAL bomat
add A/20, B/20, 0
Block 9*A/10, 9/10*B, Height/11
del 1

!!!!!!!!!!!!!!!!!!!Box

roty 90
PRISM_ 6,A,
	  -Height/11,    0, 15,
	-Height*9/11,    0, 15,
	-Height*9/11, B/20, 15,
		 -Height, B/20, 15,
		 -Height,    B, 15,
	  -Height/11,    B, 15
del 1

!!!!!!!!!!!!!!!!!!! Counter

MATERIAL ctmat
addz Height*9/11
Block A, B/20, Height*2/11


!!!!!!!!!!!!!!! Knobs

IF gs_detlevel_3D_m = 2 THEN
	material kmat
	rotx 90	
	resol gs_resol
	
	add A*4/6, height/11, 0
	cylind  0.02, min(height*2/11,A/6)/3
	addx A/6
	cylind  0.02, min(height*2/11,A/6)/3
	del 2
	add A/8, (height*2/11)/3, 0
	block A/8, (height*2/11)/3, 0.02
	del 2
ENDIF
del 1


!!!!!!!!!!!!!!!!!! PIPE

if pipe=1 then
	Material gs_con_mat
	add A/2,B/2,zzyzx
	roty -90
	Revolve 3,360,0,
			 0, r2, 1,
			 h, r1, 1,
		0.20+h, r1, 1
	del 1

	addz 0.20+h
	rotz 90
	elbow r1, 90, r1
	add r1, 0, r1

	Revolve 2,360,0,
		0, r1, 1,
		B/2-r1, r1, 1
endif
del top

!End of Script
