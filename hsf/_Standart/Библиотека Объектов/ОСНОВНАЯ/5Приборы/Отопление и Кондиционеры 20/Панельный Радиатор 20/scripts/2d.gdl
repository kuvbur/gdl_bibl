!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el

! -----------------------------------------------------------------------------
! Scale
! -----------------------------------------------------------------------------
call "Resize_A_B_ZZYZX" Parameters ALL

! -----------------------------------------------------------------------------
! Radiator Types
! -----------------------------------------------------------------------------

pen gs_cont_pen
fill gs_fill_type
DrawingNum = 1
gosub "radiatorpanel"

! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------
gosub "mepconnection"

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================


! -----------------------------------------------------------------------------
! Radiator Panel Horizontal
! -----------------------------------------------------------------------------

"radiatorpanel":

	if DrawingNum = 1 then	! Top

		add2 0, b
		poly2_b 5, 2+64,gs_fill_pen,gs_back_pen,
			0, 0, 1,
			a, 0, 1,
			a, -b, 1,
			0, -b, 1,
			0, 0, -1

		hotspot2 a/2, 0
		hotspot2 0, 0
		hotspot2 a, 0
		hotspot2 a, -b
		hotspot2 0, -b
		hotspot2 a/2, -b/2

		rect2 0, 0, a, -b
		del 1
	endif

return

! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

"mepconnection":

	isavailable = 0
	isInArchiCAD = APPLICATION_QUERY ("MEPMODELER", "IsAvailable()", isavailable)

	if bShowMEP then

		pen gs_cont_pen

					! --- OTHER VIEWS --- !

		rot2 -SYMB_ROTANGLE * (DrawingNum > 1 and NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 and SYMB_MIRRORED)

		if DrawingNum = 2 then project2 -3, 90, 2	! Bottom
		if DrawingNum = 3 then project2 4, 270, 2	! Front
		if DrawingNum = 4 then project2 4, 90, 2	! Back
		if DrawingNum = 5 then project2 4, 180 * (SYMB_MIRRORED), 2		! Right
		if DrawingNum = 6 then project2 4, 180 * not(SYMB_MIRRORED), 2	! Left


					! --- CONNECTIONS --- !

		call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
			ui_current_con = ui_current_con,
			MEP_NumberConnections = MEP_NumberConnections,
			MEP_NumConnectionData = gs_Connections,
			gs_AddConnections = gs_AddConnections,
			DrawingNum = DrawingNum,
			MEP_InsShow = 0,
			MEP_cline_show = 0,
			gs_cont_pen = gs_cont_pen,
			gs_fill_type=gs_fill_type,
			gs_fill_pen=gs_fill_pen,
			gs_back_pen=gs_back_pen,
			ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
			ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 1, MEP_StrConnectionData = MEP_StrConnectionData

		del 1
	endif

return




