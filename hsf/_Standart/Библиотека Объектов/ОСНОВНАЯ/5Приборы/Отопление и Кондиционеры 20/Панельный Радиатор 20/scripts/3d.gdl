!-----------3d---------------------
if show_el < EPS then end
IF gs_detlevel_3D_m < EPS THEN END
CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, sMat, gs_InsMat, MEP_cline_pen, show_el

if gs_detlevel_3D_m = 0 then end
if not(gs_shadow) then SHADOW OFF

! -----------------------------------------------------------------------------
! 3D hotspots
! -----------------------------------------------------------------------------
unID = 1

hotspot a*0.5, 	b, 0, unID : unID = unID + 1
hotspot 0, 		0, 0, unID : unID = unID + 1
hotspot 0, 		b, 0, unID : unID = unID + 1
hotspot a,		b, 0, unID : unID = unID + 1
hotspot a, 		0, 0, unID : unID = unID + 1

hotspot 0, 0, zzyzx, unID : unID = unID + 1
hotspot 0, b, zzyzx, unID : unID = unID + 1
hotspot a, b, zzyzx, unID : unID = unID + 1
hotspot a, 0, zzyzx, unID : unID = unID + 1

! -----------------------------------------------------------------------------
! Styles
! -----------------------------------------------------------------------------

pen gs_cont_pen

gosub iStyle

! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

gosub "mepconnection"

end

! =============================================================================
! SUBRUTINES
!
! =============================================================================

! -----------------------------------------------------------------------------
! Radiator Panel Horizontal
! -----------------------------------------------------------------------------

1:
	material matRadiator

	if gs_detlevel_3D_m = 2 then
		addy 0.08
		addy -0.065

		if a > 0.1 then
			addx 0.05
			addz zzyzx-0.01
			block a-0.1, 0.05, 0
			del 2
		endif

		addy -0.015

		for r = 1 to 2
			block 0.05, 0.08, zzyzx
			addx a-0.05
		next r
		del 3

		if a > 0.1 then
			gg = 0.01
			addx 0.05
			rotx 90

			if zzyzx > 0.09 then
				nn=int((zzyzx+gg)/(0.09+gg))
				aa=(zzyzx-(nn-1)*gg)/nn
			endif
			if zzyzx <= 0.09 then
				aa=zzyzx
				nn=1
			endif
			for f = 1 to 2
				for n = 1 to nn
					block a-0.1, aa, 0.005
					addy aa+gg
				next n
				del nn
			addz -0.055
			next f
		endif
	else
		block A, B, zzyzx
	endif
	del top

return

! -----------------------------------------------------------------------------
! Radiator Panel Vertical
! -----------------------------------------------------------------------------

2:

	material matRadiator
	if gs_detlevel_3D_m = 2 then

		addy 0.08
		addy -0.08
		block a, 0.08, 0.05
		addz zzyzx-0.05
		block a, 0.08, 0.05
		del 2

		if a > 0.02 then
			if zzyzx > 0.1 then
				addy -0.07
				addz 0.05
				addx 0.01
				block 0.01,0.07,zzyzx-0.1
				addx a-0.03
				block 0.01,0.07,zzyzx-0.1
				del 4
			endif
		endif


		if zzyzx > 0.1 then
			addz 0.05
			gg=0.01
			rotx 90
			for t = 1 to 2
				if a > 0.09 then
					nn=int((a+gg)/(0.09+gg))
					aa=(a-(nn-1)*gg)/nn
				endif
				if a <= 0.09 then
					aa=a
					nn=1
				endif
				for n=1 to nn
					block aa,zzyzx-0.10,0.005
					addx aa+gg
				next n
				del nn
				addz 0.07
			next t
		endif
	else
		block A, B, zzyzx
	endif
	del top

return


! -----------------------------------------------------------------------------
! MEP Connections
! -----------------------------------------------------------------------------

"mepconnection":

	pen gs_cont_pen
	if gs_detlevel_3D_m = 1 then resol 8
	if gs_detlevel_3D_m = 2 then resol 24

	if bShowMEP then

		if useSysMat then matRadiator = sMat

		call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
			ui_current_con = ui_current_con,
			MEP_NumberConnections = MEP_NumberConnections,
			MEP_NumConnectionData = gs_Connections,
			gs_AddConnections = gs_AddConnections,
			cShow3D = 1,
			gs_ConMat = matRadiator,
			MEP_InsShow = 0,
			MEP_cline_show_3D = 0,
			gs_cont_pen = gs_cont_pen,
			ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConPosZ_1=ConPosZ_1, ConLength_1=ConLength_1, ConEdit_1 = 14,
			ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConPosZ_2=ConPosZ_2, ConLength_2=ConLength_2, ConEdit_2 = 14, MEP_StrConnectionData = MEP_StrConnectionData
	endif

return




