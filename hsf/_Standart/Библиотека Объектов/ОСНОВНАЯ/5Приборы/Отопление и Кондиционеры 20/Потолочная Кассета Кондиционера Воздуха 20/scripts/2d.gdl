
if gs_symbtype_2D_m = SYMB2D_ASINMVO then
	call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m
endif

call "RequestSystemGlobals" parameters all returned_parameters 	gs_cont_pen,
																gs_fill_type,
																gs_fill_pen,
																gs_back_pen

pen gs_cont_pen
fill gs_fill_type

unID = 1
hotspot2 0, 0, unID : unID = unID + 1
hotspot2 -a/2 + radPanel, -b/2 + radPanel, unID : unID = unID + 1
hotspot2  a/2 - radPanel, -b/2 + radPanel, unID : unID = unID + 1
hotspot2  a/2 - radPanel,  b/2 - radPanel, unID : unID = unID + 1
hotspot2 -a/2 + radPanel,  b/2 - radPanel, unID : unID = unID + 1


poly2_b 9, 1+2+4+64, gs_fill_pen , gs_back_pen,
		-a/2 + radPanel,	-b/2,				1,
		 a/2 - radPanel,	-b/2,				1,
		 a/2,				-b/2 + radPanel,	1001,
		 a/2,				 b/2 - radPanel,	1,
		 a/2 - radPanel,	 b/2,				1001,
		-a/2 + radPanel,	 b/2,				1,
		-a/2,				 b/2 - radPanel,	1001,
		-a/2,				-b/2 + radPanel,	1,
		-a/2 + radPanel,	-b/2,				1001

if gs_symbtype_2D_m = SYMB2D_REAL | gs_symbtype_2D_m = SYMB2D_ELECTRIC then
	poly2_b 9, 1+4, gs_fill_pen, gs_back_pen,
		-gs_boxLength/2,			 gs_boxWidth/2 - 0.16,	1,
		-gs_boxLength/2 + 0.16,		 gs_boxWidth/2,			1,
		 gs_boxLength/2 - 0.16,		 gs_boxWidth/2,			1,
		 gs_boxLength/2,			 gs_boxWidth/2 - 0.16,	1,
		 gs_boxLength/2,			-gs_boxWidth/2 + 0.16,	1,
		 gs_boxLength/2 - 0.16,		-gs_boxWidth/2,			1,
		-gs_boxLength/2 + 0.1875,	-gs_boxWidth/2,			1,
		-gs_boxLength/2 + 0.1875,	-gs_boxWidth/2 + 0.1,	1,
		-gs_boxLength/2,			-gs_boxWidth/2 + 0.1,	1
endif

if gs_symbtype_2D_m = SYMB2D_RCP then
	poly2_b 8, 1+4, gs_fill_pen, gs_back_pen,
		-gs_boxLength/2 + 0.12,	 gs_boxWidth/2 - 0.16,	1,
		-gs_boxLength/2 + 0.16,	 gs_boxWidth/2 - 0.12,	1,
		 gs_boxLength/2 - 0.16,  gs_boxWidth/2 - 0.12,	1,
		 gs_boxLength/2 - 0.12,  gs_boxWidth/2 - 0.16,	1,
		 gs_boxLength/2 - 0.12,	-gs_boxWidth/2 + 0.16,	1,
		 gs_boxLength/2 - 0.16,	-gs_boxWidth/2 + 0.12,	1,
		-gs_boxLength/2 + 0.16,	-gs_boxWidth/2 + 0.12,	1,
		-gs_boxLength/2 + 0.12,	-gs_boxWidth/2 + 0.16,	1

	for i = 1 to 4
		if i = 1 | i = 3 then
			firstParam = gs_boxLength
			secondParam = gs_boxWidth
		else
			firstParam = gs_boxWidth
			secondParam = gs_boxLength
		endif

		poly2_b 4, 1+4, gs_fill_pen, gs_back_pen,
			-firstParam/2 + 0.01,	 secondParam/2 - 0.16, 1,
			-firstParam/2 + 0.01,	-secondParam/2 + 0.16, 1,
			-firstParam/2 + 0.07,	-secondParam/2 + 0.17, 1,
			-firstParam/2 + 0.07,	 secondParam/2 - 0.17, 1
		rot2 90
	next i
	del 4
endif

if bShowMEP then
	pen gs_cont_pen

	DrawingNum = 1

	call "MEP_m_ConnectionsACL_4" parameters SetProgram = SetProgram,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 12,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 12,
		ConPosX_3=ConPosX_3, ConPosY_3=ConPosY_3, ConLength_3=ConLength_3, ConEdit_3 = 15

	add2 ConPosX_3, ConPosY_3

	cWidth = ConWidth_3 + ConWallThickness_3 * 2
	poly2_b 3, 1+4, gs_fill_pen, gs_back_pen,
		-cWidth/2, 0, 1,
		 cWidth/2, 0, 1,
		 cWidth/2, cWidth, 1

	del 1
endif
