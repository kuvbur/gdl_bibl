
!!Radiator Horizontal Bars.gsm


unID = 1	
DrawingNum = 1
pen gs_cont_pen
fill gs_fill_type
call "Resize_A_B_ZZYZX" Parameters ALL

hotspot2 A / 2, connectLength + bd, unID : unID = unID + 1
hotspot2 0, 0, unID : unID = unID + 1
hotspot2 A, 0, unID : unID = unID + 1
hotspot2 0, connectLength + bd, unID : unID = unID + 1
hotspot2 A, connectLength + bd, unID : unID = unID + 1
Hotspot2 A / 2, (connectLength + bd) / 2, unID : unID = unID + 1

if iRadiatorType = RADIATORTYPE_BARS then
	add2 0, bd + connectLength

	poly2_b 9, 1+2+64, gs_fill_pen, gs_back_pen,
	0,0,1,
	0,-bd-connectLength,1,
	a,-bd-connectLength,1,
	a,0,1,
	a-bd,0,1,
	a-bd,-connectLength,1,
	bd,-connectLength,1,
	bd,0,1,
	0,0,-1

	line2 0,0,0,-connectLength-bd
	line2 a,0,a,-connectLength-bd
	line2 bd,0,bd,-connectLength
	line2 a-bd,0,a-bd,-connectLength
	line2 0,-connectLength-bd,a,-connectLength-bd
	line2 0,-connectLength,a,-connectLength
	line2 0,0,bd,0
	line2 a-bd,0,a,0
	
	del 1
endif

if iRadiatorType = RADIATORTYPE_FLAT | iRadiatorType = RADIATORTYPE_CURVED then

	if iRadiatorType = RADIATORTYPE_FLAT then
		add2 0, 0
	else
		add2 0, panelThk
	endif

	add2 panelOverhang, 0

	for i = 1 to 2
		poly2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0, 0, 1,
			bd, 0, 1,
			bd, bd + connectLength, 1,
			0, bd + connectLength, 1,
			0, 0, -1
			
		line2 0, bd, bd, bd
		add2 A - bd - panelOverhang * 2, 0
	next i
	
	del 3
	
	poly2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
		0, 0, 1,
		A, 0, 1,
		A, -panelThk, 1,
		0, -panelThk, 1,
		0, 0, -1
		
	del 1
		
endif

! --- start of modifying to Shaft


IF bShowMEP THEN

	PEN gs_cont_pen

				! --- OTHER VIEWS --- !

	ROT2 -SYMB_ROTANGLE * (DrawingNum > 1 AND NOT(SYMB_MIRRORED))+SYMB_ROTANGLE * (DrawingNum > 1 AND SYMB_MIRRORED)

	IF DrawingNum = 2 THEN PROJECT2 -3, 90, 2	! Bottom
	IF DrawingNum = 3 THEN PROJECT2 4, 270, 2	! Front
	IF DrawingNum = 4 THEN PROJECT2 4, 90, 2	! Back
	IF DrawingNum = 5 THEN PROJECT2 4, 180*(SYMB_MIRRORED), 2		! Right
	IF DrawingNum = 6 THEN PROJECT2 4, 180*NOT(SYMB_MIRRORED), 2	! Left


				! --- CONNECTIONS --- !

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
		ui_current_con = ui_current_con,
		MEP_NumberConnections = MEP_NumberConnections,
		MEP_NumConnectionData = gs_Connections,
		gs_AddConnections = gs_AddConnections,
		DrawingNum = DrawingNum,
		MEP_InsShow = 0,
		MEP_cline_show = 0,
		gs_cont_pen = gs_cont_pen,
		gs_fill_type=gs_fill_type,
		gs_fill_pen=gs_fill_pen,
		gs_back_pen=gs_back_pen,
		ConPosX_1=ConPosX_1, ConPosY_1=ConPosY_1, ConLength_1=ConLength_1, ConEdit_1 = 1,
		ConPosX_2=ConPosX_2, ConPosY_2=ConPosY_2, ConLength_2=ConLength_2, ConEdit_2 = 1

	DEL 1

	if iRadiatorType = RADIATORTYPE_FLAT then

		material mat1

		add2 ConPosX_1, ConPosY_1
		poly2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0, ConWidth_1/2, 1,
			0, -ConWidth_1/2, 1,
			panelOverhang, -ConWidth_1/2, 1,
			panelOverhang, ConWidth_1/2, 1,
			0, ConWidth_1/2, -1
		del 1

		add2 ConPosX_2, ConPosY_2
		poly2_b 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0, ConWidth_2/2, 1,
			0, -ConWidth_2/2, 1,
			-panelOverhang, -ConWidth_2/2, 1,
			-panelOverhang, ConWidth_2/2, 1,
			0, ConWidth_2/2, -1
		del 1

	endif

ENDIF

! --- end of modifying to Shaft
