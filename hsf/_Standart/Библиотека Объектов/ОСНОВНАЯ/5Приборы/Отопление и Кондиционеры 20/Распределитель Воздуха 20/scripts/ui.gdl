
UI_DIALOG `Параметры Диффузора Воздуха`
UI_CURRENT_PAGE gs_ui_current_page




DIM ui_matpar_locked[2]
	ui_matpar_locked[1] = 0
	ui_matpar_locked[2] = 0

bEnableUseSystemMat = 0

IF isMEPEnabled THEN
	bEnableUseSystemMat = 1
	IF useSysMat AND MEP_StrConnectionData[1][2] <> "0" THEN
		ui_matpar_locked[1] = 1
		ui_matpar_locked[2] = 1
	ENDIF
ENDIF

iPageIDRoot 	    	=-1
iPageIDGeneral	    	= 1
iPageIDRepAndSurface	= 2
iPageIDConnections  	= 3

pageStartY = 5

! ==============================================================================
!UI_PAGES
! ==============================================================================

UI_PAGE iPageIDGeneral, iPageIDRoot, `Общие Параметры`, "uiTab_Dimension_flat(1)"
	gosub "generalPage"

UI_PAGE iPageIDRepAndSurface, iPageIDRoot, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "repAndSurfacePage"

IF isMEPEnabled THEN
	UI_PAGE iPageIDConnections, iPageIDRoot, `Соединения`, "uiTab_Connections_flat(1)"
		gosub "connectionsPage"
ENDIF

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"generalPage":
! ------------------------------------------------------------------------------

DIM stToolTips[7]
	stToolTips[1] = `Высота Пленума`
	stToolTips[2] = `Ширина Пленума (Диаметр)`
	stToolTips[3] = `Длина Пленума`
	stToolTips[4] = `Внешняя Ширина Пленума (Диаметр)`
	stToolTips[5] = `Внешняя Длина Пленума`
	stToolTips[6] = `Ширина Патрубка (Диаметр)`
	stToolTips[7] = `Длина Патрубка`

UI_SEPARATOR 205, pageStartY + 9, 205, pageStartY + 83
UI_SEPARATOR 15, pageStartY + 87, 430, pageStartY + 87

distX 			= 218
distY 			= pageStartY + 9
textWidthOut 	= 118
textWidthIn		= 95
textHeight	 	= 15
adX 			= 120
adY	 			= 26

UI_STYLE 0, 0
next_row = 0
UI_OUTFIELD `Тип Пленума`, distX, distY+adY*next_row+2, textWidthOut, textHeight, 0
UI_INFIELD "PlenumType", distX+adX, distY+adY*next_row, textWidthIn, textHeight

distY = distY + adY
UI_OUTFIELD `Ширина Решетки (Диаметр)`, distX, distY+2, textWidthOut, textHeight, 0
UI_INFIELD "GrilleWidth", distX+adX, distY+adY*next_row, textWidthIn, textHeight

distY = distY + adY
bFlag = 4
IF iPlenumType = 2 THEN bFlag = 0
UI_OUTFIELD `Длина Решетки`, distX, distY+2, textWidthOut, textHeight, bFlag
UI_INFIELD "GrilleLength", distX+adX, distY+adY*next_row, textWidthIn, textHeight

UI_STYLE 0, 1
UI_OUTFIELD `Стиль Панели`, 10, pageStartY + 9, 73, 15, 0
UI_OUTFIELD `Вид Спереди`, 10, pageStartY + 95, 85, 15, 0
UI_OUTFIELD `Вид Сбоку`, 230, pageStartY + 95, 85, 15, 0

textWidthIn		= 80
textHeight	 	= 15

UI_STYLE 0, 0
UI_INFIELD "PlenumHeight", 10, pageStartY + 140, textWidthIn, textHeight ui_tooltip stToolTips[1]
UI_INFIELD "PlenumWidth", 112, pageStartY + 95, textWidthIn, textHeight ui_tooltip stToolTips[2]
UI_INFIELD "PlenumLength", distX+adX, pageStartY + 95, textWidthIn, textHeight ui_tooltip stToolTips[3]
UI_INFIELD "NeckWidth", 10, pageStartY + 206, textWidthIn, textHeight ui_tooltip stToolTips[6]
UI_INFIELD "PanelOuterWidth", 112, pageStartY + 206, textWidthIn, textHeight ui_tooltip stToolTips[4]
UI_INFIELD "NeckLength", 225, pageStartY + 206, textWidthIn, textHeight ui_tooltip stToolTips[7]
UI_INFIELD "PanelOuterLength", distX+adX, pageStartY + 206, textWidthIn, textHeight ui_tooltip stToolTips[5]

UI_STYLE 0, 0
IF iPlenumType = 1 THEN
	UI_INFIELD{3} "DiffType", 85, pageStartY + 9, 110, 70,
		2, 4, 6, 2,
		70, 70, 60, 60,
		1,DiffType_dim[1],DiffType_dim[1],
		2,DiffType_dim[2],DiffType_dim[2],
		3,DiffType_dim[3],DiffType_dim[3],
		4,DiffType_dim[4],DiffType_dim[4],
		5,DiffType_dim[5],DiffType_dim[5],
		6,DiffType_dim[6],DiffType_dim[6]
ENDIF
IF iPlenumType = 2 THEN
	UI_INFIELD{3} "DiffType", 85, pageStartY + 9, 110, 70,
		2, 4, 6, 2,
		70, 70, 60, 60,
		4,DiffType_dim[4],DiffType_dim[4],
		5,DiffType_dim[5],DiffType_dim[5],
		6,DiffType_dim[6],DiffType_dim[6]
ENDIF
IF iPlenumType = 3 THEN
	UI_INFIELD{3} "DiffType", 85, pageStartY + 9, 110, 70,
		2, 4, 6, 2,
		70, 70, 60, 60,
		1,DiffType_dim[1],DiffType_dim[1],
		2,DiffType_dim[2],DiffType_dim[2],
		3,DiffType_dim[3],DiffType_dim[3]
ENDIF

UI_PICT 1, 90, pageStartY + 112, 111, 90, 1

UI_PICT 2, 66, pageStartY + 189, 24, 13, 1

UI_PICT 3, 280, pageStartY + 112, 154, 90, 1

RETURN

! ------------------------------------------------------------------------------
"repAndSurfacePage":
! ------------------------------------------------------------------------------

DIM ui_matpar_names[2][2]
	ui_matpar_names[1][1] = `Пленум`
	ui_matpar_names[2][1] = `Решетка`

	ui_matpar_names[1][2] = "HVAC_matPlenum"
	ui_matpar_names[2][2] = "HVAC_matGrille"

CALL "ui_visualizationtab" PARAMETERS ALL 	bEnableCenterlinePars = 0,
											bEnable3dDetail = 1,
											bEnableUseSystemMat = bEnableUseSystemMat,
											ui_matpar_names = ui_matpar_names,
											ui_matpar_locked = ui_matpar_locked,
											ui_3dviewpar_rowspace = 12,
											bResol = 0

RETURN

! ------------------------------------------------------------------------------
"connectionsPage":
! ------------------------------------------------------------------------------

	DIM ui_con_type[1]
		ui_con_type[1] = 0

	CALL "MEP_m_ConnectionsACL_4" PARAMETERS ALL ui_con_type = ui_con_type,
												ui_system_var_pos = 2

RETURN
