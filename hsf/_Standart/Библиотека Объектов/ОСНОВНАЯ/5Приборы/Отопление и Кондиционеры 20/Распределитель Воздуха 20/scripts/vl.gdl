
call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

call "FM_types" parameters all


! ***************
! Common
! ***************

DIM shape_typ_to_validation[3]
	shape_typ_to_validation[1] = 1		! Rectangle
	shape_typ_to_validation[2] = 1		! Circle
	shape_typ_to_validation[3] = 0		! Oval

IF GLOB_MODPAR_NAME = "gs_detlevel_3D" OR gs_detlevel_3D_m = -1 THEN
	gs_detlevel_3D_m = 2	! Detailed
	IF gs_detlevel_3D = `Откл.` THEN gs_detlevel_3D_m = 0
	IF gs_detlevel_3D = `Простой` THEN gs_detlevel_3D_m = 1
	PARAMETERS gs_detlevel_3D_m = gs_detlevel_3D_m
ELSE
	IF gs_detlevel_3D_m = 0 THEN gs_detlevel_3D = `Откл.`
	IF gs_detlevel_3D_m = 1 THEN gs_detlevel_3D = `Простой`
	IF gs_detlevel_3D_m = 2 THEN gs_detlevel_3D = `Детальный`
	PARAMETERS gs_detlevel_3D = gs_detlevel_3D
ENDIF

VALUES "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
VALUES "gs_detlevel_3D_m" 2, 1, 0


! ***************
! Special
! ***************

VALUES "PlenumHeight" RANGE[0.1, ]
VALUES "PlenumWidth" RANGE(0, ]
VALUES "NeckWidth" RANGE(0, PlenumWidth)
VALUES "PanelOuterWidth" RANGE (GrilleWidth,]
VALUES "GrilleWidth" RANGE (0, PanelOuterWidth)

IF iPlenumType = 2 THEN
	VALUES "PlenumLength" RANGE(0, ]
	VALUES "NeckLength" RANGE(0, PlenumLength)
	VALUES "PanelOuterLength" RANGE (GrilleLength,]
	VALUES "GrilleLength" RANGE (0, PanelOuterLength)
ENDIF

A = PlenumWidth
B = MAX(PanelOuterWidth,PlenumWidth)
zzyzx = PlenumHeight+Nh

PARAMETERS A=A, B=B, zzyzx=zzyzx

lock "A","B","zzyzx"

if gs_cont_pen=0 then parameters gs_cont_pen=8
if gs_fill_pen=0 then parameters gs_fill_pen=91

VALUES "PlenumType" PlenumType_dim

IF PlenumType = PlenumType_dim[1] THEN
	VALUES "DiffType" DiffType_dim
	VALUES "iDiffType" 1, 2, 3, 4, 5, 6
ENDIF
IF PlenumType = PlenumType_dim[2] THEN
	VALUES "DiffType" DiffType_dim[4], DiffType_dim[5], DiffType_dim[6]
	VALUES "iDiffType" 4, 5, 6
ENDIF
IF PlenumType = PlenumType_dim[3] THEN
	VALUES "DiffType" DiffType_dim[1], DiffType_dim[2], DiffType_dim[3]
	VALUES "iDiffType" 1, 2, 3
ENDIF
VALUES "iPlenumType" 1, 2, 3

IF iPlenumType <> 2 THEN LOCK "GrilleLength", "PanelOuterLength", "PlenumLength", "NeckLength"

PUT 1, 2

! ***************
! MEP parts
! ***************

IF MEP_StrConnectionData[1][2] = "0" THEN LOCK "useSysMat"


					! --- THE ONLY CONNECTION --- !

ConID = 1
ConName = `Соединение Конца Воздуховода`

ConDVecX = 1
ConDVecY = 0
ConDVecZ = 0

ConWVecX = 0
ConWVecY = 1
ConWVecZ = 0


IF NOT(SetProgram) THEN
	IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
		VALUES "ConWidth_1" RANGE (0, MIN(PlenumWidth-ConWallThickness_1*2, PlenumHeight-ConWallThickness_1*2)]
	ELSE
		VALUES "ConWidth_1" RANGE (0, PlenumWidth-ConWallThickness_1*2]
		VALUES "ConDepth_1" RANGE (0, PlenumHeight-ConWallThickness_1*2]
	ENDIF

	VALUES "ConLength_1" RANGE [0, ]
	PARAMETERS ConPosX_1 = PlenumWidth/2
	IF PlenumType = PlenumType_dim[1] THEN
		VALUES "ConPosY_1" RANGE [-PlenumWidth/2+ConWidth_1/2+ConWallThickness_1, PlenumWidth/2-ConWidth_1/2-ConWallThickness_1]
	ELSE
		ConPosY_1 = 0
		PARAMETERS ConPosY_1 = ConPosY_1
	ENDIF
	IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN
		VALUES "ConPosZ_1" RANGE [Nh+ConWidth_1/2+ConWallThickness_1, Nh+PlenumHeight-ConWidth_1/2-ConWallThickness_1]
	ELSE
		VALUES "ConPosZ_1" RANGE [Nh+ConDepth_1/2+ConWallThickness_1, Nh+PlenumHeight-ConDepth_1/2-ConWallThickness_1]
	ENDIF
	IF ABS(MEP_NumConnectionData[1][21] - 2) < EPS THEN ConDepth_1 = ConWidth_1
	VALUES "ConWallThickness_1" RANGE [0, MIN((PlenumWidth-ConWidth_1)/2, (PlenumHeight-ConDepth_1)/2)]
ENDIF


CALL "MEP_m_ConnectionsACL_4" PARAMETERS SetProgram = SetProgram,
	gs_Connections = gs_Connections,
	MEP_NumConnectionData = MEP_NumConnectionData,
	MEP_StrConnectionData = MEP_StrConnectionData,
	MEP_NumberConnections = MEP_NumberConnections,
	gs_AddConnections = gs_AddConnections,
	ConName = ConName,
	ConID = ConID,
	ConPosX = ConPosX_1,
	ConPosY = ConPosY_1,
	ConPosZ = ConPosZ_1,
	ConDVecX = ConDVecX,
	ConDVecY = ConDVecY,
	ConDVecZ = ConDVecZ,
	ConWVecX = ConWVecX,
	ConWVecY = ConWVecY,
	ConWVecZ = ConWVecZ,

	ConNominalWidth = ConNominalWidth_1,
	ConNominalDepth = ConNominalDepth_1,
	ConWidth = ConWidth_1,
	ConDepth = ConDepth_1,
	ConLength = ConLength_1,
	ConWallThickness = ConWallThickness_1,
	ConConnectorWidth = ConConnectorWidth_1,
	ConConnectorDepth = ConConnectorDepth_1,
	ConConnectorWidth2 = ConConnectorWidth2_1,
	ConToolType = ConToolType_1,
	ConStatus = ConStatus_1,
	ConType_1 = ConType_1,
	ConConnectorType_1 = ConConnectorType_1,
	ConSystem_1 = ConSystem_1,
	shape_typ_to_validation = shape_typ_to_validation,
	MEP_enabled_geometry_mod = 1

PARAMETERS MEP_ConInfo[ConID][1] = STR(ConToolType_1, 1, 0)
PARAMETERS MEP_ConInfo[ConID][2] = ConName
PARAMETERS MEP_ConInfo[ConID][3] = "0"

IF SetProgram THEN
	SetProgram = 0
	PARAMETERS SetProgram = SetProgram
ENDIF

FOR k = 1 TO MEP_NumberConnections
	FOR i = 1 TO 9
		j = 1+INT((i-0.5)/3)
		MEP_NumConnectionData[k][i] = outCoords[j+3*(k-1)][i-3*(j-1)]
		PARAMETERS MEP_NumConnectionData[k][i] = MEP_NumConnectionData[k][i]
	NEXT i
NEXT k


IF isMEPEnabled THEN
	IF useSysMat AND MEP_StrConnectionData[1][2] <> "0" THEN LOCK "HVAC_matPlenum", "HVAC_matGrille"

	nTabs = 3
	IF gs_ui_current_page = 3 THEN
		ac_mep_connectionpage_active = 1
	ELSE
		ac_mep_connectionpage_active = 0
	ENDIF
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active
	PUT 3
ELSE
	nTabs = 2

	HIDEPARAMETER 	"useSysMat",
					"ConSystem_1",
					"ConConnectorType_1",
					"ConWallThickness_1",
					"ConConnectorWidth_1"

	ac_mep_connectionpage_active = 0
	PARAMETERS ac_mep_connectionpage_active = ac_mep_connectionpage_active
ENDIF

VALUES "gs_ui_current_page" GET(NSP)

END
