

call "GetPlanViewGlobals" parameters all returned_parameters gs_symbtype_2D_m

call "FM_types" parameters all

if GLOB_MODPAR_NAME = "type" then
	iType = SMOKE_DETECTOR
	if type = stType[2] then iType = HEAT_DETECTOR
	if type = stType[3] then iType = COMBI_DETECTOR
	parameters iType = iType
else
	type = stType[min(iType,vardim1(stType))]
	parameters type = type
endif

values "type" stType
values "iType" 1, 2, 3


lock "b", "ZZYZX"

values "gs_detlevel_3D" `Детальный`,`Простой`,`Откл.`
values "gs_detlevel_3D_m" 2, 1, 0

if GLOB_MODPAR_NAME = "gs_detlevel_3D" then
	if gs_detlevel_3D = `Детальный` then gs_detlevel_3D_m = 2
	if gs_detlevel_3D = `Простой` then gs_detlevel_3D_m = 1
	if gs_detlevel_3D = `Откл.` then gs_detlevel_3D_m = 0
	parameters gs_detlevel_3D_m = gs_detlevel_3D_m
else
	if gs_detlevel_3D_m = 2 then gs_detlevel_3D = `Детальный`
	if gs_detlevel_3D_m = 1 then gs_detlevel_3D = `Простой`
	if gs_detlevel_3D_m = 0 then gs_detlevel_3D = `Откл.`
	parameters gs_detlevel_3D = gs_detlevel_3D
endif

if gs_detlevel_3D_m = 1 then
	lock "gs_resol"
	hideparameter "gs_resol"
endif
if gs_detlevel_3D_m = 0 then
	lock "gs_resol", "gs_shadow"
	hideparameter "gs_resol", "gs_shadow"
endif

IF gs_cont_pen=0 THEN PARAMETERS gs_cont_pen=8
IF gs_fill_pen=0 THEN PARAMETERS gs_fill_pen=91

values "gs_resol" range [4, )

IF gs_detlevel_3D_m = 1 then lock "gs_resol"

IF iType = SMOKE_DETECTOR or iType= COMBI_DETECTOR then parameters ZZYZX=0.065
IF iType = HEAT_DETECTOR then parameters ZZYZX=0.058

if gs_symbtype_2D_m = 1 then
	lock 			"bShowText2D",
					"text2D",
					"fontType",
					"textHeight",
					"textHeightPoint",
					"bTextBold",
					"bTextItalic",
					"bTextUnderline",
					"typeTextRotation"
	hideparameter 	"bShowText2D",
					"text2D",
					"fontType",
					"textHeight",
					"textHeightPoint",
					"bTextBold",
					"bTextItalic",
					"bTextUnderline",
					"typeTextRotation"
endif

call "electricalItems_m" parameters all	iGroupType		= 1,
										iObjectType		= 5,
										gs_text_pen 	= gs_cont_pen,
										bFulleditText	= 1,
										bEnableText2D	= 1,
										bEnableBox		= 0,
										bEnablePlate	= 0,
										bEnableSwitch	= 0,
										bEnableOutlet	= 0,
										bEnable3D		= 1,
										type			= type,
										iItemType		= iType

