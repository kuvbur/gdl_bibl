
! --- Oriented View ------------------------------ [

angleViewRot = 0
if bShowText then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]

pen gs_cont_pen

add2 a/2, b/2
!-----------2d---------------------
if show_el < EPS then end
IF gs_detlevel_2D_m < EPS THEN END
if hide_conventions and show_conventions then end
if GLOB_PREVIEW_MODE = 0 and GLOB_FEEDBACK_MODE = 0 then CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pen, gs_fill_type, gs_fill_pen, gs_back_pen, MEP_cline_type, MEP_cline_pen, show_el
PEN gs_cont_pen
IF gs_fill_pen THEN SET FILL gs_fill_type


unID = 1

if iFireCabPlacing = 1 then
	rect2 -a/2+2*th, -b/2, a/2-2*th, -b/2+th
	rect2 -a/2, -b/2+th, a/2, -b/2+2*th
	rect2 -a/2+2*th, -b/2+2*th, a/2-2*th, b/2

	if gs_back_pen <> 0 then
		fill gs_fill_type
		poly2_b 13, 2+4+64, gs_fill_pen, gs_back_pen,
			-a/2+2*th,	-b/2,		1,
			 a/2-2*th,	-b/2,		1,
			 a/2-2*th,	-b/2+th,	1,
			 a/2,		-b/2+th,	1,
			 a/2,		-b/2+2*th,	1,
			 a/2-2*th,	-b/2+2*th,	1,
			 a/2-2*th,	 b/2,		1,
			-a/2+2*th,	 b/2,		1,
			-a/2+2*th,	-b/2+2*th,	1,
			-a/2,		-b/2+2*th,	1,
			-a/2,		-b/2+th,	1,
			-a/2+2*th,	-b/2+th,	1,
			-a/2+2*th,	-b/2,		-1
	endif

	hotspot2 -a/2+2*th, -b/2+2*th, unID :unID = unID + 1
	hotspot2  0,		-b/2+2*th, unID :unID = unID + 1
	hotspot2  a/2-2*th, -b/2+2*th, unID :unID = unID + 1
endif

if iFireCabPlacing = 2 then
	hotspot2 -a/2+2*th, 0, unID :unID = unID + 1
	hotspot2  a/2-2*th,	0, unID :unID = unID + 1

	rect2 -a/2+2*th,	-b/2,		a/2-2*th,	-b/2+th
	rect2 -a/2,			-b/2+th,	a/2,		 0
	rect2 -a/2+2*th,	 0,			a/2-2*th,	 b/2

	if gs_back_pen<>0 then
		fill gs_fill_type
		poly2_b 13, 2+4+64, gs_fill_pen, gs_back_pen,
			-a/2+2*th,	-b/2,		1,
			 a/2-2*th,	-b/2,		1,
			 a/2-2*th,	-b/2+th,	1,
			 a/2,		-b/2+th,	1,
			 a/2,		 0,			1,
			 a/2-2*th,	 0,			1,
			 a/2-2*th,	 b/2,		1,
			-a/2+2*th,	 b/2,		1,
			-a/2+2*th,	 0,			1,
			-a/2,		 0,			1,
			-a/2,		-b/2+th,	1,
			-a/2+2*th,	-b/2+th,	1,
			-a/2+2*th,	-b/2,		-1
	endif
endif

if iFireCabPlacing = 3 then
	rect2 -a/2+2*th,	-b/2,		a/2-2*th,	-b/2+th
	rect2 -a/2,			-b/2+th,	a/2,		 b/2

	if gs_back_pen <> 0 then
		fill gs_fill_type
		poly2_b 9, 2+4+64, gs_fill_pen, gs_back_pen,
			-a/2+2*th,	-b/2,		1,
			 a/2-2*th,	-b/2,		1,
			 a/2-2*th,	-b/2+th,	1,
			 a/2,		-b/2+th,	1,
			 a/2,		 b/2,		1,
			-a/2,		 b/2,		1,
			-a/2,		-b/2+th,	1,
			-a/2+2*th,	-b/2+th,	1,
			-a/2+2*th,	-b/2,		-1
	endif
endif

unID = 100
hotspot2   0, b/2,	unID :unID = unID + 1
hotspot2   0, 0,	unID :unID = unID + 1


del 1

! ==============================================================================
! --- Resize 2D symbol ---
! ==============================================================================

hotspot2   0,	0, 	unID, a, 1+256 	:unID = unID + 1
hotspot2  -1,	0, 	unID, a, 3+128 	:unID = unID + 1
hotspot2   a, 	0, 	unID, a, 2 		:unID = unID + 1

hotspot2   0, 	b, 	unID, a, 1+256 	:unID = unID + 1
hotspot2  -1,  	b, 	unID, a, 3+128 	:unID = unID + 1
hotspot2   a, 	b, 	unID, a, 2 		:unID = unID + 1

hotspot2   0,  	 0, unID, b, 1+256 	:unID = unID + 1
hotspot2   0,	-1, unID, b, 3+128 	:unID = unID + 1
hotspot2   0, 	 b,	unID, b, 2 		:unID = unID + 1

hotspot2   a, 	 0,	unID, b, 1+256 	:unID = unID + 1
hotspot2   a, 	-1,	unID, b, 3+128 	:unID = unID + 1
hotspot2   a,  	 b,	unID, b, 2 		:unID = unID + 1



! ==============================================================================
! --- 2D TEXT ---
! ==============================================================================

if bShowText then
	nTrans = 0
	pen textPen
	define style fire_hose_text 	fontType, 10 / GLOB_SCALE * fontSize_mm, 5,
									gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
	style fire_hose_text

	add2 a/2 + txtX, b/2 + txtY
	nTrans = nTrans + 1

	! Text Rotation - Readable / Always Horizontal / Align with Symbol
	! ----------------------------------------------------------------------
	if typeTextRotation_m = 2 then											! Readable
		rot2 180 * (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps))	! 1/0
		nTrans = nTrans + 1
	else
		if typeTextRotation_m = 1 then										! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED))  		! 1/-1
			nTrans = nTrans + 1
		endif
	endif
	! ----------------------------------------------------------------------

	text2 0,0,txt
	del nTrans

! --- Text hotspots ---

	hotspot2 a/2,			b/2 + txtY,	unID,	txtX,1+128 	:unID=unID+1
	hotspot2 a/2 + txtX,	b/2 + txtY,	unID,	txtX,2 		:unID=unID+1
	hotspot2 a/2 - 1,		b/2 + txtY,	unID,	txtX,3 		:unID=unID+1

	hotspot2 a/2 + txtX,	b/2,		unID,	txtY,1+128 	:unID=unID+1
	hotspot2 a/2 + txtX,	b/2 + txtY,	unID,	txtY,2 		:unID=unID+1
	hotspot2 a/2 + txtX,	b/2 - 1,	unID,	txtY,3 		:unID=unID+1
endif

