
values "gs_ui_current_page" 1, 2, 3, 4

values "pointerType" stPointerType
values "iPointerType" 1, 2
if GLOB_MODPAR_NAME = "pointerType" then
	for i = 1 to vardim1(stPointerType)
	   if pointerType = stPointerType[i]	then iPointerType = i
	next i
	parameters iPointerType = iPointerType
else
	pointerType = stPointerType[max(min(iPointerType,vardim1(stPointerType)),1)]
	parameters pointerType = pointerType
endif

values{2} "iLineConnType"	LINECONNTYPE_UP,		stLineConnType[1],
							LINECONNTYPE_CENTER,	stLineConnType[2],
							LINECONNTYPE_LO,		stLineConnType[3],
							LINECONNTYPE_UTEXT,		stLineConnType[4],
							LINECONNTYPE_UTITLE,	stLineConnType[5]


if bShowLabelFrame and iShapeHeadBox <> HEADSHAPE_RECT then
	iLineConnType = LINECONNTYPE_CENTER
	parameters iLineConnType = iLineConnType
	lock			"iLineConnType"
	hideparameter	"iLineConnType"
endif

values "arrowHeadType" stArrowHeadType
values "iArrowHeadType" 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14
if GLOB_MODPAR_NAME = "arrowHeadType" then
	for i = 1 to vardim1(stArrowHeadType)
	   if arrowHeadType = stArrowHeadType[i]	then iArrowHeadType = i
	next i
	parameters iArrowHeadType = iArrowHeadType
else
	arrowHeadType = stArrowHeadType[max(min(iArrowHeadType,vardim1(stArrowHeadType)),1)]
	parameters arrowHeadType = arrowHeadType
endif

values "textOrientation" stTextOrientation
values "iTextOrientation" 1, 2
if GLOB_MODPAR_NAME = "textOrientation" then
	for i = 1 to vardim1(stTextOrientation)
	   if textOrientation = stTextOrientation[i]	then iTextOrientation = i
	next i
	parameters iTextOrientation = iTextOrientation
else
	textOrientation = stTextOrientation[min(max(iTextOrientation, 1), vardim1(stTextOrientation))]
	parameters textOrientation = textOrientation
endif

values "titleTextAlign" stTextAlign
values "iTitleTextAlign" 1, 2, 3
if GLOB_MODPAR_NAME = "titleTextAlign" then
	for i = 1 to vardim1(stTextAlign)
	   if titleTextAlign = stTextAlign[i]	then iTitleTextAlign = i
	next i
	parameters iTitleTextAlign = iTitleTextAlign
else
	titleTextAlign = stTextAlign[max(min(iTitleTextAlign,vardim1(stTextAlign)),1)]
	parameters titleTextAlign = titleTextAlign
endif

values "labelTextAlign" stTextAlign
values "iLabelTextAlign" 1, 2, 3
if GLOB_MODPAR_NAME = "labelTextAlign" then
	for i = 1 to vardim1(stTextAlign)
	   if labelTextAlign = stTextAlign[i]	then iLabelTextAlign = i
	next i
	parameters iLabelTextAlign = iLabelTextAlign
else
	labelTextAlign = stTextAlign[max(min(iLabelTextAlign,vardim1(stTextAlign)),1)]
	parameters labelTextAlign = labelTextAlign
endif

values "shapeHeadBox" stShapeHeadBox
values "iShapeHeadBox" 1, 2, 3, 4, 5, 6, 7, 8
if GLOB_MODPAR_NAME = "shapeHeadBox" then
	for i = 1 to vardim1(stShapeHeadBox)
		if shapeHeadBox = stShapeHeadBox[i]	then  iShapeHeadBox = i
	next i
	parameters iShapeHeadBox = iShapeHeadBox
else
	shapeHeadBox = stShapeHeadBox[min(max(iShapeHeadBox,1),vardim1(stShapeHeadBox))]
	parameters shapeHeadBox = shapeHeadBox
endif

values "iPointerNumSide1" range[0, 5]
values "iPointerNumSide2" range[0, 5]

if (textBoxLengthPaper < minTextBoxLengthPaper) or (textBoxLengthPaper_M < minTextBoxLengthPaper / 1000) then
	textBoxLengthPaper = minTextBoxLengthPaper
	textBoxLengthPaper_M = minTextBoxLengthPaper / 1000

	parameters 	textBoxLengthPaper = textBoxLengthPaper,
				textBoxLengthPaper_M = textBoxLengthPaper_M
endif

if GLOB_MODPAR_NAME <> "" then
	if	GLOB_MODPAR_NAME = "textBoxLengthPaper_M" or\
		GLOB_MODPAR_NAME = "titleTextWidthFactor" or GLOB_MODPAR_NAME = "titleTextSpacingFactor" or\
		GLOB_MODPAR_NAME = "labelTextWidthFactor" or GLOB_MODPAR_NAME = "labelTextSpacingFactor" then

		if	GLOB_MODPAR_NAME = "textBoxLengthPaper_M" or\
			((titleTextWidthMod < labelTextWidthMod) and (GLOB_MODPAR_NAME = "titleTextWidthFactor" or GLOB_MODPAR_NAME = "titleTextSpacingFactor")) or\
			((titleTextWidthMod > labelTextWidthMod) and (GLOB_MODPAR_NAME = "labelTextWidthFactor" or GLOB_MODPAR_NAME = "labelTextSpacingFactor")) then

			textBoxLengthPaper = textBoxLengthPaper_M * 1000
		else
			if GLOB_MODPAR_NAME = "titleTextWidthFactor" or GLOB_MODPAR_NAME = "titleTextSpacingFactor" then

				textBoxLengthPaper = textBoxLengthPaper_M * 1000 * titleTextWidthMod
			endif
			if GLOB_MODPAR_NAME = "labelTextWidthFactor" or GLOB_MODPAR_NAME = "labelTextSpacingFactor" then

				textBoxLengthPaper = textBoxLengthPaper_M * 1000 * labelTextWidthMod
			endif
		endif

		parameters textBoxLengthPaper = textBoxLengthPaper
	else
		textBoxLengthPaper_M = textBoxLengthPaper / 1000
		parameters textBoxLengthPaper_M = textBoxLengthPaper_M
	endif
endif

if GLOB_MODPAR_NAME = "textBoxHeightPaper_M" then
	textBoxHeightPaper = textBoxHeightPaper_M * 1000
	parameters textBoxHeightPaper = textBoxHeightPaper
else
	textBoxHeightPaper_M = textBoxHeightPaper / 1000
	parameters textBoxHeightPaper_M = textBoxHeightPaper_M
endif

if iShapeHeadBox = HEADSHAPE_CIRCLE then
	if bAutoHeadBox then
		headBoxOffsetLeftRightPaper = SQR(textBoxLengthPaper**2 + textBoxHeightPaper**2) / 2 - MAX(textBoxLengthPaper,textBoxHeightPaper) / 2
		headBoxOffsetUpDownPaper 	= headBoxOffsetLeftRightPaper
		headBoxOffsetLeftRightPaper_M = headBoxOffsetLeftRightPaper / 1000
		headBoxOffsetUpDownPaper_M = headBoxOffsetUpDownPaper / 1000

		parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
					headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
					headBoxOffsetLeftRightPaper_M = headBoxOffsetLeftRightPaper_M,
					headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M

	endif

	if	GLOB_MODPAR_NAME = "headBoxOffsetLeftRightPaper" then

		headBoxLengthPaper	= MAX(textBoxLengthPaper, textBoxHeightPaper) + headBoxOffsetLeftRightPaper * 2
		parameters	headBoxLengthPaper	= headBoxLengthPaper

		headBoxWidthPaper	= headBoxLengthPaper
		parameters	headBoxWidthPaper	= headBoxWidthPaper

		headBoxOffsetUpDownPaper	= headBoxOffsetLeftRightPaper
		parameters	headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper

		headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper / 1000
		parameters	headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M

		headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper / 1000
		parameters	headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M
	else
		if GLOB_MODPAR_NAME = "headBoxLengthPaper" or GLOB_MODPAR_NAME = "headBoxWidthPaper" then

			headBoxOffsetLeftRightPaper	= (headBoxLengthPaper - MAX(textBoxLengthPaper, textBoxHeightPaper)) / 2
			headBoxOffsetUpDownPaper	= headBoxOffsetLeftRightPaper
			headBoxOffsetLeftRightPaper_M = headBoxOffsetLeftRightPaper / 1000
			headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper / 1000

			parameters	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headBoxOffsetLeftRightPaper_M = headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M = headBoxOffsetUpDownPaper_M
		else
			if GLOB_MODPAR_NAME = "headBoxOffsetLeftRightPaper_M" then
				headBoxLengthPaper	= MAX(textBoxLengthPaper, textBoxHeightPaper) + headBoxOffsetLeftRightPaper_M * 1000 * 2
				headBoxWidthPaper	= headBoxLengthPaper
				headBoxOffsetLeftRightPaper = headBoxOffsetLeftRightPaper_M * 1000
				headBoxOffsetUpDownPaper = headBoxOffsetLeftRightPaper_M * 1000
				headBoxOffsetUpDownPaper_M = headBoxOffsetLeftRightPaper_M

				parameters	headBoxLengthPaper	= headBoxLengthPaper,
							headBoxWidthPaper	= headBoxWidthPaper,
							headBoxOffsetLeftRightPaper = headBoxOffsetLeftRightPaper,
							headBoxOffsetUpDownPaper = headBoxOffsetUpDownPaper,
							headBoxOffsetUpDownPaper_M = headBoxOffsetUpDownPaper_M 
		else
			headBoxLengthPaper	= MAX(textBoxLengthPaper, textBoxHeightPaper) + headBoxOffsetLeftRightPaper * 2
			headBoxWidthPaper	= headBoxLengthPaper
			parameters	headBoxLengthPaper	= headBoxLengthPaper,
						headBoxWidthPaper	= headBoxWidthPaper
			endif
		endif
	endif

	values "headBoxLengthPaper" range [MAX(textBoxLengthPaper, textBoxHeightPaper),)
	values "headBoxWidthPaper" range [MAX(textBoxLengthPaper, textBoxHeightPaper),)

	values "headBoxOffsetLeftRightPaper" range [0,)
	values "headBoxOffsetUpDownPaper" range [0,)

else
	if bAutoHeadBox then
		if iShapeHeadBox = HEADSHAPE_RECT then
			headBoxOffsetLeftRightPaper = 0
			headBoxOffsetUpDownPaper 	= 0
			headBoxOffsetLeftRightPaper_M = 0
			headBoxOffsetUpDownPaper_M 	= 0

			parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M


		endif
		if iShapeHeadBox = HEADSHAPE_DIAMOND then
			headBoxOffsetLeftRightPaper = textBoxHeightPaper / 2
			headBoxOffsetUpDownPaper 	= textBoxLengthPaper / 2
			headBoxOffsetLeftRightPaper_M = textBoxHeightPaper / 2 / 1000
			headBoxOffsetUpDownPaper_M 	= textBoxLengthPaper / 2 / 1000

			parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M

		endif
		if iShapeHeadBox = HEADSHAPE_ELLIPSE then
			headBoxOffsetLeftRightPaper = textBoxHeightPaper / 2
			headBoxOffsetUpDownPaper 	= textBoxHeightPaper / 2
			headBoxOffsetLeftRightPaper_M = textBoxHeightPaper / 2 / 1000
			headBoxOffsetUpDownPaper_M 	= textBoxHeightPaper / 2 / 1000

			parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M

		endif
		if iShapeHeadBox = HEADSHAPE_OVAL then
			headBoxOffsetLeftRightPaper = textBoxHeightPaper / 2
			headBoxOffsetUpDownPaper 	= 0
			headBoxOffsetLeftRightPaper_M = textBoxHeightPaper / 2 / 1000
			headBoxOffsetUpDownPaper_M 	= 0 / 1000

			parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M

		endif
		if iShapeHeadBox = HEADSHAPE_HEXAGON then
			headBoxOffsetLeftRightPaper 	= textBoxHeightPaper / 2
			headBoxOffsetUpDownPaper 		= textBoxHeightPaper / 2
			headBoxOffsetLeftRightPaper_M 	= textBoxHeightPaper / 2 / 1000
			headBoxOffsetUpDownPaper_M 		= textBoxHeightPaper / 2 / 1000
			headShapeSidePointXPosPaper 	= textBoxHeightPaper
			headShapeSidePointXPosPaper_M 	= textBoxHeightPaper / 1000

			parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headShapeSidePointXPosPaper = headShapeSidePointXPosPaper,
						headShapeSidePointXPosPaper_M = headShapeSidePointXPosPaper_M,
						headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M

		endif
		if iShapeHeadBox = HEADSHAPE_OCTOGON then
			headBoxOffsetLeftRightPaper = 0
			headBoxOffsetUpDownPaper 	= textBoxHeightPaper
			headBoxOffsetLeftRightPaper_M = 0
			headBoxOffsetUpDownPaper_M 	= textBoxHeightPaper / 1000
			headShapeSidePointXPosPaper		= headBoxLengthPaper / 3
			headShapeSidePointXPosPaper_M	= headBoxLengthPaper / 1000 / 3

			parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headShapeSidePointXPosPaper	= headShapeSidePointXPosPaper,
						headShapeSidePointXPosPaper_M = headShapeSidePointXPosPaper_M,
						headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M
		endif
		if iShapeHeadBox = HEADSHAPE_POINTED_RECT then
			headBoxOffsetLeftRightPaper = textBoxHeightPaper / 3
			headBoxOffsetUpDownPaper 	= 0
			headBoxOffsetLeftRightPaper_M = textBoxHeightPaper / 3 / 1000
			headBoxOffsetUpDownPaper_M 	= 0

			parameters 	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper,
						headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M

		endif
	endif

	if	GLOB_MODPAR_NAME = "headBoxOffsetLeftRightPaper" or GLOB_MODPAR_NAME = "headBoxOffsetUpDownPaper" then

		if GLOB_MODPAR_NAME = "headBoxOffsetLeftRightPaper" then
			headBoxLengthPaper	= textBoxLengthPaper + headBoxOffsetLeftRightPaper * 2
			parameters	headBoxLengthPaper	= headBoxLengthPaper

			headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper / 1000
			parameters	headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M
		else
			headBoxWidthPaper	= textBoxHeightPaper + headBoxOffsetUpDownPaper * 2
			parameters	headBoxWidthPaper	= headBoxWidthPaper

			headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper/ 1000
			parameters	headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper_M
		endif
	else
		if GLOB_MODPAR_NAME = "headBoxLengthPaper" or GLOB_MODPAR_NAME = "headBoxWidthPaper" then

			headBoxOffsetLeftRightPaper	= (headBoxLengthPaper - textBoxLengthPaper) / 2
			headBoxOffsetUpDownPaper	= (headBoxWidthPaper - textBoxHeightPaper) / 2
			parameters	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
						headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper

			headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper / 1000
			headBoxOffsetUpDownPaper_M	= headBoxOffsetUpDownPaper/ 1000
			parameters	headBoxOffsetLeftRightPaper_M	= headBoxOffsetLeftRightPaper_M,
						headBoxOffsetUpDownPaper_M = headBoxOffsetUpDownPaper_M
			
		else
			if GLOB_MODPAR_NAME = "headBoxOffsetLeftRightPaper_M" or GLOB_MODPAR_NAME = "headBoxOffsetUpDownPaper_M" then

				if GLOB_MODPAR_NAME = "headBoxOffsetLeftRightPaper_M" then
				headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper_M * 1000
				headBoxLengthPaper	= textBoxLengthPaper + headBoxOffsetLeftRightPaper_M * 1000 * 2
	
					parameters	headBoxOffsetLeftRightPaper	= headBoxOffsetLeftRightPaper,
								headBoxLengthPaper	= headBoxLengthPaper
				else
					headBoxOffsetUpDownPaper	= headBoxOffsetUpDownPaper_M * 1000
				headBoxWidthPaper	= textBoxHeightPaper + headBoxOffsetUpDownPaper_M * 1000 * 2
	
				parameters	headBoxOffsetUpDownPaper = headBoxOffsetUpDownPaper,
							headBoxWidthPaper	= headBoxWidthPaper
				endif
		else
			headBoxLengthPaper	= textBoxLengthPaper + headBoxOffsetLeftRightPaper * 2
			headBoxWidthPaper	= textBoxHeightPaper + headBoxOffsetUpDownPaper * 2
			parameters	headBoxLengthPaper	= headBoxLengthPaper,
						headBoxWidthPaper	= headBoxWidthPaper
			endif
		endif
	endif

	values "headBoxLengthPaper" range [textBoxLengthPaper,)
	values "headBoxWidthPaper" range [textBoxHeightPaper,)

	values "headBoxOffsetLeftRightPaper" range [0,)
	values "headBoxOffsetUpDownPaper" range [0,)
endif

if	GLOB_MODPAR_NAME = "headShapeSidePointXPosPaper_M" then
	headShapeSidePointXPosPaper = headShapeSidePointXPosPaper_M * 1000
	parameters	headShapeSidePointXPosPaper = headShapeSidePointXPosPaper
else
	headShapeSidePointXPosPaper_M	= headShapeSidePointXPosPaper / 1000
	parameters	headShapeSidePointXPosPaper_M	= headShapeSidePointXPosPaper_M
endif

if	GLOB_MODPAR_NAME = "totalCommonPointLeftPosX" or GLOB_MODPAR_NAME = "textBoxPosX" then

	commonPointLeftPosX	= totalCommonPointLeftPosX - textBoxPosX
	parameters	commonPointLeftPosX	= commonPointLeftPosX
else
	totalCommonPointLeftPosX	= textBoxPosX + commonPointLeftPosX
	parameters	totalCommonPointLeftPosX	= totalCommonPointLeftPosX
endif

if	GLOB_MODPAR_NAME = "totalCommonPointRightPosX" or GLOB_MODPAR_NAME = "textBoxPosX" then

	commonPointRightPosX		= totalCommonPointRightPosX - textBoxPosX 
	parameters	commonPointRightPosX	= commonPointRightPosX
else
	totalCommonPointRightPosX	= commonPointRightPosX + textBoxPosX 
	parameters	totalCommonPointRightPosX	= totalCommonPointRightPosX
endif

values "commonPointRightPosX" range[0,]

if NOT(bHideArrowHead) then
	lock			"hideArrowHeadFrom"

	hideparameter	"hideArrowHeadFrom"
endif
if NOT(bShowLabelFrame) then
	lock			"shapeHeadBox",
					"iShapeHeadBox",
					"bAutoHeadBox",
					"headBoxLengthPaper",
					"headBoxWidthPaper",
					"bFrameOpaque",
					"frameOpaquePen",
					"iFrameOpaquePen"

	hideparameter	"shapeHeadBox",
					"iShapeHeadBox",
					"bAutoHeadBox",
					"headBoxLengthPaper",
					"headBoxWidthPaper",
					"bFrameOpaque",
					"frameOpaquePen",
					"iFrameOpaquePen"
endif
if bAutoHeadBox then
	lock			"headBoxLengthPaper",
					"headBoxWidthPaper"
else
	lock			"textBoxHeightPaper"
endif
if NOT(bFrameOpaque) then
	lock			"frameOpaquePen",
					"iFrameOpaquePen"

	hideparameter	"frameOpaquePen",
					"iFrameOpaquePen"
endif
if iShapeHeadBox = HEADSHAPE_CIRCLE then
	lock			"headBoxWidthPaper"
endif

if titleTextLeading < 50 then
	titleTextLeading = 50
	parameters titleTextLeading = titleTextLeading
endif
if titleTextLeading > 1000 then
	titleTextLeading = 1000
	parameters titleTextLeading = titleTextLeading
endif
if titleTextWidthFactor < 50 then
	titleTextWidthFactor = 50
	parameters titleTextWidthFactor = titleTextWidthFactor
endif
if titleTextWidthFactor > 1000 then
	titleTextWidthFactor = 1000
	parameters titleTextWidthFactor = titleTextWidthFactor
endif
if titleTextSpacingFactor < 75 then
	titleTextSpacingFactor = 75
	parameters titleTextSpacingFactor = titleTextSpacingFactor
endif
if titleTextSpacingFactor > 1000 then
	titleTextSpacingFactor = 1000
	parameters titleTextSpacingFactor = titleTextSpacingFactor
endif

if labelTextLeading < 50 then
	labelTextLeading = 50
	parameters labelTextLeading = labelTextLeading
endif
if labelTextLeading > 1000 then
	labelTextLeading = 1000
	parameters labelTextLeading = labelTextLeading
endif
if labelTextWidthFactor < 50 then
	labelTextWidthFactor = 50
	parameters labelTextWidthFactor = labelTextWidthFactor
endif
if labelTextWidthFactor > 1000 then
	labelTextWidthFactor = 1000
	parameters labelTextWidthFactor = labelTextWidthFactor
endif
if labelTextSpacingFactor < 75 then
	labelTextSpacingFactor = 75
	parameters labelTextSpacingFactor = labelTextSpacingFactor
endif
if labelTextSpacingFactor > 1000 then
	labelTextSpacingFactor = 1000
	parameters labelTextSpacingFactor = labelTextSpacingFactor
endif

if NOT(bPointerOnBothSides) then
	lock			"iPointerNumSide2"

	hideparameter	"iPointerNumSide2"
endif

if NOT(bEnablePointersOnSide1) and NOT(bEnablePointersOnSide2) then
	lock			"pointerType",
					"iPointerType",
					"pointerLineType",
					"iLineConnType",
					"bHideArrowHead",
					"hideArrowHeadFrom",
					"arrowHeadType",
					"iArrowHeadType",
					"arrowHeadSizePaper",
					"arrowHeadPen",
					"iArrowHeadPen"

	hideparameter	"pointerType",
					"iPointerType",
					"pointerLineType",
					"iLineConnType",
					"bHideArrowHead",
					"hideArrowHeadFrom",
					"arrowHeadType",
					"iArrowHeadType",
					"arrowHeadSizePaper",
					"arrowHeadPen",
					"iArrowHeadPen"

	if NOT(bShowLabelFrame) then
		lock			"pointerPen",
						"iPointerPen"

		hideparameter	"pointerPen",
						"iPointerPen"
	endif
endif

hideparameter "A", "B"
lock "A", "B"

!=============================================================================
! Pen Colors
!=============================================================================

if pointerPen			< 1 then pointerPen			= 1
if arrowHeadPen			< 1 then arrowHeadPen		= 1
if titleTextPen			< 1 then titleTextPen		= 1
if labelTextPen			< 1 then labelTextPen		= 1
if frameOpaquePen		< -1 then frameOpaquePen	= -1

if pointerPen			> 255 then pointerPen		= 255
if arrowHeadPen			> 255 then arrowHeadPen		= 255
if titleTextPen			> 255 then titleTextPen		= 255
if labelTextPen			> 255 then labelTextPen		= 255
if frameOpaquePen		> 255 then frameOpaquePen	= 255

parameters 	pointerPen		= pointerPen,
			arrowHeadPen	= arrowHeadPen,
			titleTextPen	= titleTextPen,
			labelTextPen	= labelTextPen,
			frameOpaquePen	= frameOpaquePen

if iPointerPen			< 1 then iPointerPen		= 1
if iArrowHeadPen		< 1 then iArrowHeadPen		= 1
if iTitleTextPen		< 1 then iTitleTextPen		= 1
if iLabelTextPen		< 1 then iLabelTextPen		= 1
if iFrameOpaquePen		< -1 then iFrameOpaquePen	= -1

if iPointerPen			> 255 then iPointerPen		= 255
if iArrowHeadPen		> 255 then iArrowHeadPen	= 255
if iTitleTextPen		> 255 then iTitleTextPen	= 255
if iLabelTextPen		> 255 then iLabelTextPen	= 255
if iFrameOpaquePen		> 255 then iFrameOpaquePen	= 255

parameters 	iPointerPen		= iPointerPen,
			iArrowHeadPen	= iArrowHeadPen,
			iTitleTextPen	= iTitleTextPen,
			iLabelTextPen	= iLabelTextPen,
			iFrameOpaquePen	= iFrameOpaquePen

if GLOB_MODPAR_NAME = "iPointerPen" then
	pointerPen = iPointerPen
	parameters pointerPen = pointerPen
else
	iPointerPen = pointerPen
	parameters iPointerPen = iPointerPen
endif

if GLOB_MODPAR_NAME = "iArrowHeadPen" then
	arrowHeadPen = iArrowHeadPen
	parameters arrowHeadPen = arrowHeadPen
else
	iArrowHeadPen = arrowHeadPen
	parameters iArrowHeadPen = iArrowHeadPen
endif

if GLOB_MODPAR_NAME = "iTitleTextPen" then
	titleTextPen = iTitleTextPen
	parameters titleTextPen = titleTextPen
else
	iTitleTextPen = titleTextPen
	parameters iTitleTextPen = iTitleTextPen
endif

if GLOB_MODPAR_NAME = "iLabelTextPen" then
	labelTextPen = iLabelTextPen
	parameters labelTextPen = labelTextPen
else
	iLabelTextPen = labelTextPen
	parameters iLabelTextPen = iLabelTextPen
endif

if GLOB_MODPAR_NAME = "iFrameOpaquePen" then
	frameOpaquePen = iFrameOpaquePen
	parameters frameOpaquePen = frameOpaquePen
else
	iFrameOpaquePen = frameOpaquePen
	parameters iFrameOpaquePen = iFrameOpaquePen
endif


