
paperToModel = GLOB_SCALE/1000

if scaleValues_m = SCALEVALUES_MODEL then
	font_size = font_size_model / paperToModel
endif
DEFINE STYLE fe1 fontType, font_size, iFontAlignment, fe
STYLE fe1

WS = MAX(STW(col_name_name),STW(col_name[1]))
for i = 2 to row_count
	if STW(col_name[i]) > WS then WS = STW(col_name[i])
next i

autorowheight = font_size*paperToModel*2
autocolwidth = (WS)*paperToModel
autofillwidth = MAX(STW(col_name_fill), font_size/100*2)*paperToModel
autonumwidth = STW(col_name_numb)*paperToModel

if autosize then
	Cell_W3 = autocolwidth
	Cell_W2 = autofillwidth
	Cell_W1 = autonumwidth
	Cell_H = autorowheight
endif

if bLockSymbol then
	! --- Oriented View ------------------------------ [

	angleViewRot = 0
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
	totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

	! --- Oriented View ------------------------------ ]

	add2 0,Cell_H
	rot2 -totalRotate
	add2 0,-Cell_H
endif

UnID=1
pen Font_pen

! ----- Sizing -----

autocolwidth = 1.12*autocolwidth
autofillwidth = autofillwidth+.12*autocolwidth
autonumwidth = autonumwidth+.12*autocolwidth

if autosize then
	colwidth 	= autocolwidth
	fillwidth 	= autofillwidth
	numwidth 	= autonumwidth
	rowheight 	= autorowheight
else
	hotspot2 0,	0,			UnID,	Cell_H,	129	: UnID=UnID+1
	hotspot2 0,	1,			UnID,	Cell_H,	3	: UnID=UnID+1
	hotspot2 0,	-Cell_H,	UnID,	Cell_H,	2	: UnID=UnID+1

	if name_row then
		hotspot2 0,			0,	UnID,	Cell_W1,	129	: UnID=UnID+1
		hotspot2 -1,		0,	UnID,	Cell_W1,	3	: UnID=UnID+1
		hotspot2 Cell_W1,	0,	UnID,	Cell_W1,	2	: UnID=UnID+1
	else
		Cell_W1 = 0
	endif

	hotspot2 Cell_W1,			0,	UnID,	Cell_W2,	129	: UnID=UnID+1
	hotspot2 Cell_W1-1,			0,	UnID,	Cell_W2,	3	: UnID=UnID+1
	hotspot2 Cell_W1+Cell_W2,	0,	UnID,	Cell_W2,	2	: UnID=UnID+1

	hotspot2 Cell_W1+Cell_W2,			0,	UnID,	Cell_W3,	129	: UnID=UnID+1
	hotspot2 Cell_W1+Cell_W2-1,			0,	UnID,	Cell_W3,	3	: UnID=UnID+1
	hotspot2 Cell_W1+Cell_W2+Cell_W3,	0,	UnID,	Cell_W3,	2	: UnID=UnID+1

	colwidth = Cell_W3
	fillwidth = Cell_W2
	numwidth = Cell_W1
	rowheight = Cell_H
endif

if rowheight < font_size/100+0.02 then
	notext = 1
else
	notext = 0
endif

tablewidth = colwidth + fillwidth + numwidth * name_row
tableheight = row_count * rowheight

if name_tab then tableheight = tableheight + rowheight
if name_col then tableheight = tableheight + rowheight

if NOT(autosize) then
	add2 0,rowheight

	hotspot2 0,				0,				UnID	: UnID=UnID+1
	hotspot2 tablewidth,	0,	UnID	: UnID=UnID+1
	hotspot2 0,				-tableheight,	UnID	: UnID=UnID+1
	hotspot2 tablewidth,	-tableheight,	UnID	: UnID=UnID+1

	del 1
endif


! BG FILL
fill gs_fill_type

add2 0,rowheight
poly2_B	5,2,gs_fill_pen,gs_back_pen,
		0,				0,				1,
		tablewidth,		0,				1,
		tablewidth,		-tableheight,	1,
		0,				-tableheight,	1,
		0,				0,				-1
del 1

! Border Lines
if Border then
	pen Border_pen
	line_type Border_line
		rect2 0,(not(name_tab) & not(name_col)) * rowheight,tablewidth ,-tableheight+rowheight
endif

if Vert_line then
	pen Vert_line_pen
	line_type Vert_line_line

	if name_row then
		line2 numwidth,					(not(name_tab) & not(name_col)) *rowheight,	numwidth,			-tableheight+rowheight
		line2 numwidth + fillwidth,	(not(name_tab) & not(name_col)) *rowheight,	numwidth+fillwidth,	-tableheight+rowheight
	else
		line2 fillwidth,				0,	fillwidth,	-tableheight+rowheight
	endif
endif


if Hor_line then
	pen Hor_line_pen
	line_type Hor_line_line
	nTransLine  = 0
	for i = 1 to row_count
		if i <> 1 | (name_tab | name_col) then
			add2 0,-rowheight
			nTransLine  = nTransLine  + 1
		endif
		line2 0,	0,	tablewidth,	0
	next i
	del nTransLine
endif

nTrans  = 0
! Name of Table
if name_tab then
	if iFontAlignment = FONT_ALIGN_LEFT then		add2 0,			    rowheight/2
	if iFontAlignment = FONT_ALIGN_CENTER then	add2 tablewidth/2,	rowheight/2
	if iFontAlignment = FONT_ALIGN_RIGHT then	add2 tablewidth,	rowheight/2

	pen Font_pen
	text2 0,0,Name
	del 1

	add2 0,-rowheight
	nTrans = nTrans + 1
endif

! Header
if name_col then
	if name_row then
		if iFontAlignment = FONT_ALIGN_LEFT then		add2 0,				rowheight/2
		if iFontAlignment = FONT_ALIGN_CENTER then	add2 numwidth/2,	rowheight/2
		if iFontAlignment = FONT_ALIGN_RIGHT then	add2 numwidth,		rowheight/2

		pen Font_pen
		text2 0,0,col_name_numb
		del 1

		add2 numwidth,0
		nTrans = nTrans + 1
	endif

	if iFontAlignment = FONT_ALIGN_LEFT then		add2 0,				rowheight/2
	if iFontAlignment = FONT_ALIGN_CENTER then	add2 fillwidth/2,	rowheight/2
	if iFontAlignment = FONT_ALIGN_RIGHT then	add2 fillwidth,		rowheight/2

	pen Font_pen
	text2 0,0,col_name_fill
	del 1

	add2 fillwidth,0
	nTrans = nTrans + 1

	if iFontAlignment = FONT_ALIGN_LEFT then		add2 0,				rowheight/2
	if iFontAlignment = FONT_ALIGN_CENTER then	add2 colwidth/2,	rowheight/2
	if iFontAlignment = FONT_ALIGN_RIGHT then	add2 colwidth,		rowheight/2

	pen Font_pen
	text2 0,0,col_name_name
	del 1
endif

del nTrans

nTrans = 0
if name_tab then
	add2 0,-rowheight
	nTrans = nTrans + 1
endif
if name_col then
	add2 0,-rowheight
	nTrans = nTrans + 1
endif

! Table
for i = 1 to row_count

	if name_row then
		if iFontAlignment = FONT_ALIGN_LEFT then		add2 0,				rowheight/2
		if iFontAlignment = FONT_ALIGN_CENTER then	add2 numwidth/2,	rowheight/2
		if iFontAlignment = FONT_ALIGN_RIGHT then	add2 numwidth,		rowheight/2

		pen Font_pen
		text2 0,0,i
		del 1
		add2 numwidth,0
	endif

	fill col_fill[i]
	Offset=0.12*min(autofillwidth,autorowheight)
	poly2_B{2} 5,3+4+8,col_pen[i],col_backpen[i],
			0,					Offset,			0,

			Offset,				Offset,			1,
			Offset,				rowheight-Offset,	1,
			fillwidth-Offset,	rowheight-Offset,	1,
			fillwidth-Offset,	Offset,			1,
			Offset,				Offset,			-1

	add2 fillwidth,0
	nTrans = nTrans + 1

	if iFontAlignment = FONT_ALIGN_LEFT then		add2 0,			rowheight/2
	if iFontAlignment = FONT_ALIGN_CENTER then	add2 colwidth/2,	rowheight/2
	if iFontAlignment = FONT_ALIGN_RIGHT then	add2 colwidth,		rowheight/2

	pen Font_pen
	text2 0,0,col_name[i]
	del 1

	del 1 + name_row
	add2 0,-rowheight
next i
del nTrans

if autosize then
	add2 0,rowheight
	hotspot2 0,				0,	UnID : UnID=UnID+1
	hotspot2 tablewidth,	0, 	UnID : UnID=UnID+1
	hotspot2 0,				-tableheight,	UnID : UnID=UnID+1
	hotspot2 tablewidth,	-tableheight,	UnID : UnID=UnID+1
	hotspot2 tablewidth/2,	-tableheight/2,	UnID : UnID=UnID+1
	del 1
endif

if bLockSymbol then del 1
