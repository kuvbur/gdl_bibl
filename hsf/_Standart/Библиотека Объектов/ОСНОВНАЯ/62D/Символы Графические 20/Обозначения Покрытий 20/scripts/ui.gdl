
UI_dialog `Параметры Списка`

ui_current_page gs_ui_current_page

file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_LineTypes_flat(1)"
file_dependence "uiTab_Text_flat(1)"

TABID_ROOT				= -1
TABID_SCHEDULE			= 1
TABID_2DREPRESENTATION	= 2
TABID_LINES				= 3
TABID_TEXT				= 4

if TABID_SCHEDULE > 0 then
	ui_page TABID_SCHEDULE, TABID_ROOT, `Список`, "uiTab_Dimension_flat(1)"
	gosub "schedulePage"
endif

if TABID_2DREPRESENTATION > 0 then
	ui_page TABID_2DREPRESENTATION, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DRepPage"
endif

if TABID_LINES > 0 then
	ui_page TABID_LINES, TABID_ROOT, `Линии`, "uiTab_LineTypes_flat(1)"
	gosub "linesPage"
endif

if TABID_TEXT > 0 then
	ui_page TABID_TEXT, TABID_ROOT, `Текст`, "uiTab_Text_flat(1)"
	gosub "textPage"
endif

! =============================================================================
end
! =============================================================================

"schedulePage":
! === Page 1 ================================================================================

	pageStartY = 5
	yStart = pageStartY 
	y1 = pageStartY + 173
	hRow = 23
	xStart = 5
	x1 = 165
	x2 = 275
	x3 = x1 + 30
	
	UI_OUTFIELD	`Количество Строк`,	xStart,	yStart+4,	x3-xStart-2,	13 UI_TOOLTIP `Количество Строк`
	ui_infield	"row_count",		x3,		yStart,		63,	 			20 UI_TOOLTIP `Количество Строк`
	
	
	ui_infield{3}	"name_tab",		xStart,	yStart+hRow,	x3-xStart-2, 15,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показ Имена Списка`, 0,
			2, `Показ Имена Списка`, 1
	
	ui_infield	"name",	  			x3,		yStart+hRow,	x3-xStart-2, 16 UI_TOOLTIP `Имя Списка`
	
	ui_infield{3}	"name_col",		xStart,	yStart+hRow*2,	x3-xStart-2, 15,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показ Имени Столбца`, 0,
			2, `Показ Имени Столбца`, 1
	
	ui_infield{3}	"name_row",		xStart,	yStart+hRow*3-2+16,	x3-xStart-2, 15,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показ Номера Строки`, 0,
			2, `Показ Номера Строки`, 1
	
	ui_infield	"col_name_numb",	x3,		  yStart+hRow*2-4, 46,	20 UI_TOOLTIP `Номер`
	ui_infield	"col_name_fill",	x3+50,	  yStart+hRow*2-4, 62,	20 UI_TOOLTIP `Штриховка`
	ui_infield	"col_name_name",	x3+50+65, yStart+hRow*2-4, 95,	20 UI_TOOLTIP `Имя`
	
	if name_row then
		UI_PICT	1,	x3,	yStart+hRow*3-6,	47,	54
	else
		UI_PICT	12,	x3,	yStart+hRow*3-6,	47,	54, 1
	endif
	UI_PICT	2,x3+47,	yStart+hRow*3-6,	142,54
	
	
	ui_infield{3}	"bUniformFillPen",		xStart,	yStart+hRow*5-2+10,	x2-xStart-5, 15,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Единые Параметры Пера Штриховки`, 0,
			2, `Единые Параметры Пера Штриховки`, 1
	
	flagPen1 = 0
	flagPen2 = 4
	if not(bUniformFillPen) then
		flagPen1 = 4
		flagPen2 = 0
	endif
	
	!ui_infield	"col_fill",		x2,	yStart+hRow*5-2+18-4,	62,	20 UI_TOOLTIP `Fill`
	!ui_infield	"col_name",		226,	234,	120,	20 UI_TOOLTIP `Fill Name`
	
	ui_outfield `Перо Штриховки`, 	        x2,     yStart+hRow*5-2+10,    120, 15, flagPen1
	ui_infield	"uniformPen",		    x2+125, yStart+hRow*5-2+10-4,   42, 20 UI_TOOLTIP `Перо Штриховки`
	ui_outfield `Перо Фона Штриховки`, 	x2,     yStart+hRow*6-2+10,    120, 15, flagPen1
	ui_infield	"uniformBackPen",	    x2+125,	yStart+hRow*6-2+10-4,	42,	20 UI_TOOLTIP `Перо Фона`
	
	ui_separator 0, y1-8, 444, pageStartY + 165
	
	UI_STYLE 0, 1
	ui_outfield `Выбор Строку`, xStart, pageStartY + 173, 80, 15
	UI_STYLE 0, 0
	
	ui_infield	"row_num",	           x1,        y1-4,	x2-x1-10,  20 UI_TOOLTIP `Строка`
	
	ui_outfield `Тип Штриховки`,       xStart,   y1+hRow-5, x1-xStart-1,  15
	ui_infield	"row_fill",		       x1,	 y1+hRow-5,	x2-x1-10,  20 UI_TOOLTIP `Штриховка`
	
	ui_outfield `Имя Покрытия`,   xStart, y1+hRow*2-7, x1-xStart-1,  15
	ui_infield	"row_name",		       x1, y1+hRow*2-7, x2-x1-10,  20 UI_TOOLTIP `Имя Штриховки`
	
	ui_outfield `Перо Штриховки`,   	       x2,   y1+hRow-5, 120,  15, flagPen2
	ui_infield	"row_pen",		   x2+125,	 y1+hRow-13,  42,  20 UI_TOOLTIP `Перо Штриховки`
	
	ui_outfield `Перо Фона Штриховки`, x2, y1+hRow*2-7, 120,  15, flagPen2
	ui_infield	"row_backpen",	   x2+125, y1+hRow*2-13,  42,  20 UI_TOOLTIP `Перо Фона`
return

"2DRepPage":
! === Page 2 ================================================================================

	pageStartY = 5

	UI_PICT	11, 5, pageStartY + 55, 187, 114,1
	
	ui_infield "Cell_W1", 20, pageStartY + 33, 50, 20 UI_TOOLTIP `Ширина Столбца Нумерации`
	ui_infield "Cell_W2", 74, pageStartY + 33, 50, 20 UI_TOOLTIP `Ширина Столбца Штриховки`
	ui_infield "Cell_W3", 132,pageStartY + 33, 50, 20 UI_TOOLTIP `Ширина Столбца Имен`
	ui_infield "Cell_H", 200, pageStartY + 75, 60, 20 UI_TOOLTIP `Высота Строк`
	if scaleValues_m = SCALEVALUES_PAPER then
		form = ""
		n = REQUEST ("Model_text_size_unit", "", form)
		unit = "mm"
		n = SPLIT (form, "%%.%n%s", variable1, variable2, unit)
	else
		form = ""
		n = request("Working_length_unit", "", form)
		unit = ""
		if STRSTR(form, "m") then
			n = SPLIT (form, "%%.%n%s", variable1, variable2, unit)
			if n = 0 then
				n = SPLIT (form, "%%0.%n%s", variable1, variable2, unit)
			endif
		endif
	endif
	ui_outfield unit, 190, pageStartY + 40, 30, 20
	
	ui_infield{3} "autosize", 16, pageStartY + 5, 190, 22,
			7, "", 2,
			1, 16, 16, 16, 16,
			1, `Авторазмер Ячеек`, 0,
			2, `Авторазмер Ячеек`, 1
	
	! --- 2D ---
	
	UI_STYLE 0, 1
	ui_outfield `Штриховка Фона`, 280, pageStartY + 5, 120, 15
	UI_STYLE 0, 0
	
	UI_OUTFIELD	`Тип`,	280,	pageStartY + 31,	120,	15 UI_TOOLTIP `Тип Штриховки Ячеек`
	ui_infield	"gs_fill_type",	440-120,	pageStartY + 47,	120,	24 UI_TOOLTIP `Тип Штриховки Ячеек`
	
	UI_OUTFIELD	`Перо`,		280,	pageStartY + 86,	100,	13 UI_TOOLTIP `Перо Штриховки`
	ui_infield	"gs_fill_pen",	390,	pageStartY + 82,	50,		20 UI_TOOLTIP `Перо Штриховки`
	
	UI_OUTFIELD	`Перо Фона`,	280,	pageStartY + 115,	100,	13 UI_TOOLTIP `Перо Фона`
	ui_infield	"gs_back_pen",		390,	pageStartY + 111,	50,		20 UI_TOOLTIP `Перо Фона`
return
	
"linesPage":
	! === Page 3 ================================================================================
	
	pageStartY = 5

	IF Border THEN
		IF Hor_Line AND Vert_line THEN UI_PICT				3, 5, pageStartY + 55, 187, 114,1
		IF Hor_Line AND NOT(Vert_line) THEN UI_PICT			4, 5, pageStartY + 55, 187, 114,1
		IF NOT(Hor_Line) AND Vert_line THEN UI_PICT			5, 5, pageStartY + 55, 187, 114,1
		IF NOT(Hor_Line) AND NOT(Vert_line) THEN UI_PICT	6, 5, pageStartY + 55, 187, 114,1
	ELSE
		IF Hor_Line AND Vert_line THEN UI_PICT				7, 5, pageStartY + 55, 187, 114,1
		IF Hor_Line AND NOT(Vert_line) THEN UI_PICT			8, 5, pageStartY + 55, 187, 114,1
		IF NOT(Hor_Line) AND Vert_line THEN UI_PICT			9, 5, pageStartY + 55, 187, 114,1
		IF NOT(Hor_Line) AND NOT(Vert_line) THEN UI_PICT	10,5, pageStartY + 55, 187, 114,1
	ENDIF
	
	UI_OUTFIELD `(1)`, 195,	pageStartY + 55,	16,	16 UI_TOOLTIP `Рамка`
	UI_OUTFIELD `(2)`, 195,	pageStartY + 110,	16,	16 UI_TOOLTIP `Горизонтальные Линии`
	UI_OUTFIELD `(3)`, 125,	pageStartY + 170,	16,	16 UI_TOOLTIP `Вертикальные Линии`
	
	xPos = 220
	yPos = pageStartY + 3
	
	UI_STYLE 0, 1
	ui_infield{3} "Border", xPos, yPos, 200, 16,
			7, "", 2,
			1, 16, 16, 16, 16,
			1, `(1) Рамка`, 0,
			2, `(1) Рамка`, 1
	UI_STYLE 0, 0
	IF Border THEN
		UI_OUTFIELD `Тип Линии`,   xPos+34,	yPos+24, 50,	16
		ui_infield	"Border_line", xPos+90,	yPos+18, 100, 26 UI_TOOLTIP `Тип Контура`
		UI_OUTFIELD `Перо`,         xPos+34,	yPos+49, 40,	16
		ui_infield	"Border_pen",  xPos+90,	yPos+46, 40,	20 UI_TOOLTIP `Перо Контура`
	ENDIF
	
	yPos = yPos + 77
	UI_STYLE 0, 1
	ui_infield{3} "Hor_Line", xPos, yPos, 200, 16,
			7, "", 2,
			1, 16, 16, 16, 16,
			1, `(2) Горизонтальные Линии`, 0,
			2, `(2) Горизонтальные Линии`, 1
	UI_STYLE 0, 0
	IF Hor_Line THEN
		UI_OUTFIELD `Тип Линии`,     xPos+34,	yPos+24, 50, 16
		ui_infield	"Hor_Line_line", xPos+90, 	yPos+18, 100, 26 UI_TOOLTIP `Тип Горизонтальных Линий`
		UI_OUTFIELD `Перо`,           xPos+34,	yPos+49, 40, 16
		ui_infield	"Hor_Line_pen",  xPos+90,	yPos+46, 40, 20 UI_TOOLTIP `Перо Горизонтальных Линий`
	ENDIF
	
	yPos = yPos + 77
	UI_STYLE 0, 1
	ui_infield{3} "Vert_line", xPos, yPos, 200, 16,
			7, "", 2,
			1, 16, 16, 16, 16,
			1, `(3) Вертикальные Линии`, 0,
			2, `(3) Вертикальные Линии`, 1
	UI_STYLE 0, 0
	IF Vert_line THEN
		UI_OUTFIELD `Тип Линии`,      xPos+34,	yPos+24, 50, 16
		ui_infield	"Vert_line_line", xPos+90, 	yPos+18, 100, 26 UI_TOOLTIP `Тип Вертикальных Линий`
		UI_OUTFIELD `Перо`,            xPos+34,	yPos+49, 40, 16
		ui_infield	"Vert_line_pen",  xPos+90,	yPos+46, 40, 20 UI_TOOLTIP `Перо Вертикальных Линий`
	ENDIF
return

"textPage":
! === Page 4 ================================================================================

	! --- Font ---
	pageStartY = 5
	xStart = 10
	yStart = pageStartY + 10
	hRow   = 16
	hGap   = 18
	xTextStart = 50
	xTextCell = 150
	
	ui_pict "ui_font_style_flat(1)",    xStart,    yStart,                   24,	17, 1
	ui_pict "ui_alignment_flat(1)",    xStart,    yStart+(hRow+hGap)*2,     27,	14, 1
	ui_pict "ui_text_pen_flat(1)",  xStart,    yStart+(hRow+hGap)*3,     24,	17, 1
	ui_pict "ui_FontSize_flat(1)",  xStart,    yStart+(hRow+hGap)*4,     27,	14, 1
	
	ui_style 0,1
	ui_outfield `Шрифт`, xTextStart,yStart,xTextCell,hRow
	ui_style 0,0
	
	ui_infield "fontType", xTextStart+xTextCell+5,		yStart,	xTextCell-15,	20 UI_TOOLTIP `Тип Шрифта`
	
	ui_outfield `Тип`, xTextStart,yStart+(hRow+hGap),xTextCell,hRow
	ui_infield{3} "BOL", xTextStart+xTextCell+5, yStart+(hRow+hGap)-4, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_bold_flat(1)", "", 0,
			"ui_bold_flat(1)", "", 1
	
	ui_infield{3} "ITA", xTextStart+xTextCell+5+25, yStart+(hRow+hGap)-4, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_italic_flat(1)", "", 0,
			"ui_italic_flat(1)", "", 1
	
	ui_infield{3} "UND", xTextStart+xTextCell+5+50, yStart+(hRow+hGap)-4, 24, 24,
			6, "",
			2,
			1, 16, 16, 16, 16,
			"ui_underline_flat(1)", "", 0,
			"ui_underline_flat(1)", "", 1
	
	ui_outfield `Выравнивание`, xTextStart,yStart+(hRow+hGap)*2,xTextCell,hRow
	ui_infield{3} "iFontAlignment", xTextStart+xTextCell+5, yStart+(hRow+hGap)*2-4, xTextCell-15, 20,
			8, "",
			3,
			1, 16, 16, 16, 16,
			1, `Слева`, FONT_ALIGN_LEFT,
			2, `По центру`, FONT_ALIGN_CENTER,
			3, `Справа`, FONT_ALIGN_RIGHT
	
	ui_outfield `Перо`, xTextStart,yStart+(hRow+hGap)*3,xTextCell,hRow
	ui_infield "Font_pen",		xTextStart+xTextCell+5,	yStart+(hRow+hGap)*3-4,	35,		20 UI_TOOLTIP `Цвет Шрифта`
	
	ui_outfield `Высота`, xTextStart,yStart+(hRow+hGap)*4,xTextCell,hRow
	if scaleValues_m = SCALEVALUES_PAPER then
		form = ""
		n = REQUEST ("Model_text_size_unit", "", form)
		unit = "mm"
		n = SPLIT (form, "%%.%n%s", variable1, variable2, unit)
		ui_outfield unit, xTextStart+xTextCell+55+8,yStart+(hRow+hGap)*4,xTextCell,hRow
		if unit = "mm" then
			ui_infield "font_size",    xTextStart+xTextCell+5,	    yStart+(hRow+hGap)*4-4,	 55,	20 UI_TOOLTIP `Размер Шрифта`
		else
			ui_infield "font_size_pt",    xTextStart+xTextCell+5,	    yStart+(hRow+hGap)*4-4,	 55,	20 UI_TOOLTIP `Размер Шрифта`
		endif
	else
		form = ""
		n = request("Working_length_unit", "", form)
		unit = ""
		if STRSTR(form, "m") then
			n = SPLIT (form, "%%.%n%s", variable1, variable2, unit)
			if n = 0 then
				n = SPLIT (form, "%%0.%n%s", variable1, variable2, unit)
			endif
		endif
		ui_infield "Font_size_model",    xTextStart+xTextCell+5,	    yStart+(hRow+hGap)*4-4,	 55,	20 UI_TOOLTIP `Размер Шрифта`
		ui_outfield unit, xTextStart+xTextCell+55+8,yStart+(hRow+hGap)*4,xTextCell,hRow
	endif
	
	UI_SEPARATOR 3, pageStartY + 175, 444, pageStartY + 175
	yStart = pageStartY + 175
	ui_style 0,1
	ui_outfield `Масштаб`, xTextStart,yStart+hGap,xTextCell,hRow
	ui_style 0,0
	
	UI_RADIOBUTTON "scaleValues_m", SCALEVALUES_MODEL, `В Масштабе Плана (Модель)`, xTextStart+xTextCell+5, yStart+hGap, 440-(xTextStart+xTextCell+5), hRow
	UI_RADIOBUTTON "scaleValues_m", SCALEVALUES_PAPER, `Не Масштабировать (Бумага)`, xTextStart+xTextCell+5, yStart+hGap+hRow+5, 440-(xTextStart+xTextCell+5), hRow
return
