
values "gs_ui_current_page" 1, 2, 3, 4

if GLOB_MODPAR_NAME = "row_num" then
	parameters row_fill = col_fill[row_num] : row_fill = col_fill[row_num]
	parameters row_name = col_name[row_num] : row_name = col_name[row_num]
	parameters row_pen = col_pen[row_num] : row_pen = col_pen[row_num]
	parameters row_backpen = col_backpen[row_num] : row_backpen = col_backpen[row_num]
endif

if GLOB_MODPAR_NAME = "row_fill" then
	parameters col_fill[row_num] = row_fill : col_fill[row_num] = row_fill
	x = request ("Name_of_fill", row_fill, fillName)
	if x then
		parameters row_name = fillName
		parameters col_name[row_num] = fillName
	endif
else
	row_fill = col_fill[row_num]
	parameters row_fill = row_fill
endif

if bUniformFillPen then
	for iRow = 1 to row_count
		parameters col_pen[iRow] = uniformPen
		parameters col_backpen[iRow] = uniformBackPen
	next iRow
endif

if GLOB_MODPAR_NAME = "row_name" then
	parameters col_name[row_num] = row_name : col_name[row_num] = row_name
else
	row_name = col_name[row_num]
	parameters row_name = row_name
endif

if GLOB_MODPAR_NAME = "row_pen" then
	parameters col_pen[row_num] = row_pen : col_pen[row_num] = row_pen
else
	row_pen = col_pen[row_num]
	parameters row_pen = row_pen
endif

if GLOB_MODPAR_NAME = "row_backpen" then
	parameters col_backpen[row_num] = row_backpen : col_backpen[row_num] = row_backpen
else
	row_backpen = col_backpen[row_num]
	parameters row_backpen = row_backpen
endif

VALUES "row_count" range [1,)
for i = 1 to row_count
	put i
next i
VALUES "row_num" GET (row_count)
VALUES "Cell_W1" range [0.05, )
VALUES "Cell_W2" range [0.05, )
VALUES "Cell_W3" range [0.05, )
VALUES "Cell_H"  range [0.05, )


if not(name_tab) then
	hideparameter "Name"
	lock "Name"
endif
if not(name_col) then
	hideparameter "col_name_numb","col_name_fill","col_name_name"
	lock "col_name_numb","col_name_fill","col_name_name"
endif
if bUniformFillPen then
	hideparameter "col_fill","col_name","col_pen","col_backpen","row_pen","row_backpen"
	lock "col_fill","col_name","col_pen","col_backpen","row_pen","row_backpen"
else
	hideparameter "uniformPen", "uniformBackPen"
	lock "uniformPen", "uniformBackPen"
endif
if autosize then lock "Cell_W1","Cell_W2","Cell_W3","Cell_H"
if not(Border) then
	hideparameter "Border_line","Border_pen"
	lock "Border_line","Border_pen"
endif
if not(Vert_line) then
	hideparameter "Vert_line_line","Vert_line_pen"
	lock "Vert_line_line","Vert_line_pen"
endif
if not(Hor_line) then
	hideparameter "Hor_line_line","Hor_line_pen"
	lock "Hor_line_line","Hor_line_pen"
endif

values{2} "iFontAlignment" 	FONT_ALIGN_LEFT, `Слева`,
							FONT_ALIGN_CENTER, `По центру`,
							FONT_ALIGN_RIGHT, `Справа`
values "font_size" range (0,]
values "font_size_model" range (0,]
values "scaleValues" `В Масштабе Плана (Модель)`,`Не Масштабировать (Бумага)`
values "scaleValues_m" SCALEVALUES_MODEL, SCALEVALUES_PAPER

if GLOB_MODPAR_NAME = "scaleValues" then
	if scaleValues = `В Масштабе Плана (Модель)` then
		scaleValues_m = SCALEVALUES_MODEL
	else
		scaleValues_m = SCALEVALUES_PAPER
	endif
	parameters scaleValues_m = scaleValues_m
else
	if scaleValues_m = SCALEVALUES_MODEL then
		scaleValues = `В Масштабе Плана (Модель)`
	else
		scaleValues = `Не Масштабировать (Бумага)`
	endif
	parameters scaleValues = scaleValues
endif

if scaleValues_m = SCALEVALUES_PAPER then
	hideparameter "font_size_model"
else
	hideparameter "font_size","font_size_pt"
endif

FE = 0
if BOL then
	FE = FE+1
endif
if ITA then
	FE = FE+2
endif
if UND then
	FE = FE+4
endif
parameters FE = FE

Dot_in_mm = 25.4 / 72
if GLOB_MODPAR_NAME = "font_size_pt" then
	font_size = font_size_pt*Dot_in_mm
	parameters font_size = font_size
else
	font_size_pt = font_size/Dot_in_mm
	parameters font_size_pt = font_size_pt
endif

