
if bLockSymbol then
	! --- Oriented View ------------------------------ [

	angleViewRot = 0
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
	totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

	! --- Oriented View ------------------------------ ]

	add2 0,1
	rot2 -totalRotate
	add2 0,-1
endif

if GLOB_SCALE > 0 then
	textPaperSize = 2 * 100 / GLOB_SCALE
else
	textPaperSize = 2
endif

define style "NumberStyle" fontType, textPaperSize, 7, 0
style "NumberStyle"
pen textPen

unID = 1
nRow = 0
nVertDistance	= 0
nHorizDistance 	= 0
posText = 0.35

for i = 1 to nFills

	text2 0.02, 0.01, i
	x = request ("Name_of_fill", i, fillName)

	if x then

		fill i
		poly2_b       7,      2,      fillPen,     fillBkgPen,
					0.6,     0.0,      1,
					1.0,     0.0,      1,
					1.0,     1.0,      1,
					0.0,     1.0,      1,
					0.0,     0.4,      1,
					0.6,     0.4,      1,
					0.6,     0.0,     -1

		rect2 0,0,1,1
		line2 0,0.4, 0.6,0.4
		line2 0.6,0.4, 0.6,0

		text2 0, -posText , fillName
	endif

	add2 horizDistance,0
	nHorizDistance = nHorizDistance + 1

	if i % nFillsInARow = 0 then
		del nFillsInARow
		nHorizDistance = nHorizDistance - nFillsInARow
		add2 0, -vertDistance
		nVertDistance= nVertDistance + 1
	endif

next i
!del int(i / nFillsInARow)
del nVertDistance + nHorizDistance

hotspot2 0,										1,												unID 	: unID = unID  + 1
hotspot2 0, 									-(nVertDistance * vertDistance)- posText,		unID 	: unID = unID  + 1
hotspot2 (nFillsInARow-1) * horizDistance+1,	-(nVertDistance * vertDistance)- posText,		unID 	: unID = unID  + 1
hotspot2 (nFillsInARow-1) * horizDistance+1,	1,												unID 	: unID = unID  + 1
hotspot2 ((nFillsInARow-1) * horizDistance+1)/2,(-(nVertDistance * vertDistance)- posText+1)/2, unID 	: unID = unID  + 1

if bLockSymbol then del 1
