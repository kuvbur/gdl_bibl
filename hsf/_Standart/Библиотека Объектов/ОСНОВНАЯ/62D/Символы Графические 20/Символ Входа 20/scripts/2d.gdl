
! --- Oriented View ------------------------------ [

angleViewRot = 0
if dt then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]
pen gs_cont_pen

unID = 1

hotspot2 a/2,b,unID :	unID=unID+1
hotspot2 0,0,unID	:	unID=unID+1
hotspot2 a,0,unID	:	unID=unID+1

If gs_style_m = 1 then
	line2 0,0,a/2,b
	line2 a,0,a/2,b
endif

If gs_style_m = 2 then
	line2 0,0,a,0
	line2 0,0,a/2,b
	line2 a,0,a/2,b

	fill gs_fill_type
	poly2_b 4, 2,gs_fill_pen,gs_back_pen,
		0,0,0,
		a/2,b,0,
		a,0,0,
		0,0,-1
endif

If gs_style_m = 3 then
	line2 0,0,a/2,b
	line2 a,0,a/2,b
	line2 0,0,a/2,b/4
	line2 a,0,a/2,b/4

	HOTSPOT2 a/2,b/4,unID :	unID=unID+1

	fill gs_fill_type
	poly2_b 5, 2,gs_fill_pen,gs_back_pen,
		0,0,0,
		a/2,b,0,
		a,0,0,
		a/2,b/4,0,
		0,0,-1
endif

! ======================================================================
! 2D TEXT
! ======================================================================
if dt then
	pen tpen
	define style textStyle fontType, 10/GLOB_SCALE*fsz, 5,
							gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline
	style textStyle
	nTrans = 0
	add2 a/2+textPosX,-0.1+textPosY
	nTrans = nTrans + 1

	! Text Rotation - Readable / Always Horizontal / Align with Symbol
	! ----------------------------------------------------------------------
	if typeTextRotation_m = 2 then								! Readable
		if (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps)) then
			rot2 180
			nTrans = nTrans + 1
		endif
	else
		if typeTextRotation_m = 1 then							! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED)) ! 1/-1
			nTrans = nTrans + 1
		endif
	endif
	! ----------------------------------------------------------------------

	text2 0,0,txt
	del nTrans

	hotspot2 A/2,			-0.1+textPosY,	unID,textPosX,1+128		:unID=unID+1
	hotspot2 A/2+textPosX,-0.1+textPosY,	unID,textPosX,2			:unID=unID+1
	hotspot2 -1,			-0.1+textPosY,	unID,textPosX,3			:unID=unID+1

	hotspot2 A/2+textPosX,-0.1,				unID,textPosY,1+128		:unID=unID+1
	hotspot2 A/2+textPosX,-0.1+textPosY,	unID,textPosY,2			:unID=unID+1
	hotspot2 A/2+textPosX,-1,				unID,textPosY,3			:unID=unID+1

endif
