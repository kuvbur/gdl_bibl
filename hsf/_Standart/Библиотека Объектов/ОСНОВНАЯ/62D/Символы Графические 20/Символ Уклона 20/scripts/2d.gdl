
define style textStyle fontType,fsz,7,
			gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline

style textStyle

paperToModel = GLOB_SCALE/1000

! --- Oriented View ------------------------------ [

angleViewRot = 0
if dt then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]

pen gs_cont_pen

unID = 1
HOTSPOT2 0,0,unID :	unID=unID+1

	HOTSPOT2 0,0,unID,a,1+128 :unID=unID+1
	HOTSPOT2 a,0,unID,a,2 :unID=unID+1
	HOTSPOT2 -1,0,unID,a,3 :unID=unID+1

HOTSPOT2 a/2,0,unID :	unID=unID+1

if iSlopeSymbolStyle = SYMBOL_STYLE_1 or iSlopeSymbolStyle = SYMBOL_STYLE_4 or iSlopeSymbolStyle = SYMBOL_STYLE_5 or iSlopeSymbolStyle = SYMBOL_STYLE_6 then valt=a
if iSlopeSymbolStyle = SYMBOL_STYLE_2 then valt=a-arrowx
if iSlopeSymbolStyle = SYMBOL_STYLE_3 then valt=a-(arrowx*2/3)
	line2 0,0,valt,0


If iSlopeSymbolStyle = SYMBOL_STYLE_1 then
	line2 a-arrowx,arrowy/2,a,0
	line2 a-arrowx,-arrowy/2,a,0

endif

If iSlopeSymbolStyle = SYMBOL_STYLE_2 then
	line2 a-arrowx,arrowy/2,a,0
	line2 a-arrowx,-arrowy/2,a,0
	line2 a-arrowx,-arrowy/2,a-arrowx,arrowy/2

	fill gs_fill_type

	POLY2_b 4, 2,gs_fill_pen,gs_back_pen,
		a,0,0,
		a-arrowx,arrowy/2,0,
		a-arrowx,-arrowy/2,0,
		a,0,-1

endif

If iSlopeSymbolStyle = SYMBOL_STYLE_3 then
	line2 a-arrowx,arrowy/2,a,0
	line2 a-arrowx,-arrowy/2,a,0
	line2 a-arrowx,arrowy/2,a-(arrowx*2/3),0
	line2 a-arrowx,-arrowy/2,a-(arrowx*2/3),0

	fill gs_fill_type

	POLY2_b 5, 2,gs_fill_pen,gs_back_pen,
		a,0,0,
		a-arrowx,arrowy/2,0,
		a-(arrowx*2/3),0,0,
		a-arrowx,-arrowy/2,0,
		a,0,-1

endif

If iSlopeSymbolStyle = SYMBOL_STYLE_4 then
	line2 a-arrowx,arrowy/2,a,0

endif

If iSlopeSymbolStyle = SYMBOL_STYLE_5 then
	line2 a-arrowx,arrowy/2,a,0
	line2 a-arrowx,arrowy/2,a-arrowx,0

	fill gs_fill_type

	POLY2_b 4, 2,gs_fill_pen,gs_back_pen,
		a,0,0,
		a-arrowx,arrowy/2,0,
		a-arrowx,0,0,
		a,0,-1

endif

If iSlopeSymbolStyle = SYMBOL_STYLE_6 then
	line2 a-arrowx,arrowy/2,a,0
	line2 a-arrowx,arrowy/2,a-(arrowx*2/3),0

	fill gs_fill_type

	POLY2_b 4, 2,gs_fill_pen,gs_back_pen,
		a,0,0,
		a-arrowx,arrowy/2,0,
		a-(arrowx*2/3),0,0,
		a,0,-1

endif


! ======================================================================
! 2D TEXT
! ======================================================================
nTrans = 0
if dt then
	pen tpen

	style textStyle
	nnn = request ("Height_of_style", "textStyle", textHeight)
	textHeight = textHeight * paperToModel
	textWidth = stw(txt) * paperToModel

	add2 txtx,txty
	nTrans = nTrans + 1

	! Text Rotation - Readable / Always Horizontal / Align with Symbol
	! ----------------------------------------------------------------------
	if typeTextRotation_m = 2 then								! Readable
		if (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps)) then
			rot2 180
			add2 -textWidth ,-textHeight
			nTrans = nTrans + 2
		endif
	else
		if typeTextRotation_m = 1 then							! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED)) ! 1/-1
			nTrans = nTrans + 1
		endif
	endif
	! ----------------------------------------------------------------------
	Text2 0,0,txt
	del nTrans

	!Text hotspots

	HOTSPOT2 0, txty, unID,  txtx,  1+128   : unID=unID+1
	HOTSPOT2 txtx, txty, unID,  txtx,  2    : unID=unID+1
	HOTSPOT2 -1, txty, unID,  txtx,  3    : unID=unID+1

	HOTSPOT2 txtx, 0, unID,  txty,  1+128   : unID=unID+1
	HOTSPOT2 txtx, txty, unID,  txty,  2    : unID=unID+1
	HOTSPOT2 txtx, -1, unID,  txty,  3    : unID=unID+1

endif

