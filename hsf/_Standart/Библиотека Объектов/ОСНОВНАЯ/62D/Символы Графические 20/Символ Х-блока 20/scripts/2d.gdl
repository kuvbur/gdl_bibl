
! --- Oriented View ------------------------------ [

angleViewRot = 0
if dt then
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
endif
totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

! --- Oriented View ------------------------------ ]

pen gs_cont_pen

unID = 1

hotspot2 a/2,0,unID :	unID=unID+1
rect2 0,0,a,b
line2 0,0,a,b
line2 a,0,0,b

CALL "Resize_A_B_ZZYZX" Parameters ALL

fill gs_fill_type

poly2_b 5, 2,gs_fill_pen,gs_back_pen,
		0,0,0,
		0,b,0,
		a,b,0,
		a,0,0,
		0,0,-1

! ======================================================================
! 2D TEXT
! ======================================================================

if dt then
	pen tpen
	define style textStyle fontType,10/GLOB_SCALE*fsz,5,
							gs_text_style_bold + 2 * gs_text_style_italic + 4 * gs_text_style_underline

	style textStyle

	add2 a/2+txtp[1][1],-0.1+txtp[1][2]
	nTrans = nTrans + 1

	! Text Rotation - Readable / Always Horizontal / Align with Symbol
	! ----------------------------------------------------------------------
	if typeTextRotation_m = 2 then								! Readable
		if (totalRotate > (gs_readable_angle + eps) & totalRotate < (gs_readable_angle + 180 + eps)) then
			rot2 180
			nTrans = nTrans + 1
		endif
	else
		if typeTextRotation_m = 1 then							! Horizontal
			rot2 totalRotate * (SYMB_MIRRORED - not(SYMB_MIRRORED)) ! 1/-1
			nTrans = nTrans + 1
		endif
	endif
	! ----------------------------------------------------------------------
	Text2 0,0,txt
	DEL nTrans


	! Text hotspots

	HOTSPOT2 A/2,-0.1+txtp[1][2],unID,txtp[1][1],1+128 :unID=unID+1
	HOTSPOT2 A/2+txtp[1][1],-0.1+txtp[1][2],unID,txtp[1][1],2 :unID=unID+1
	HOTSPOT2 -1,-0.1+txtp[1][2],unID,txtp[1][1],3 :unID=unID+1

	HOTSPOT2 A/2+txtp[1][1],-0.1,unID,txtp[1][2],1+128 :unID=unID+1
	HOTSPOT2 A/2+txtp[1][1],-0.1+txtp[1][2],unID,txtp[1][2],2 :unID=unID+1
	HOTSPOT2 A/2+txtp[1][1],-1,unID,txtp[1][2],3 :unID=unID+1

endif
