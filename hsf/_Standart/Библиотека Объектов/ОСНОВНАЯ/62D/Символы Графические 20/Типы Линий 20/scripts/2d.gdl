
define style "NumberStyle" fontType, 2, 7, 0
style "NumberStyle"

unID = 1
nRow = 0
nVertDistance	= 0
nHorizDistance 	= 0
posText = 0.5

if bLockSymbol then
	! --- Oriented View ------------------------------ [

	angleViewRot = 0
	rrr = REQUEST ("View_Rotangle", "", angleViewRot)
	totalRotate = (SYMB_ROTANGLE + angleViewRot) MOD 360

	! --- Oriented View ------------------------------ ]

	add2 0,vertDistance-posText
	rot2 -totalRotate
	add2 0,-(vertDistance-posText)
endif


for i = 1 to nFills
	pen linepen

	text2 0, -posText, i
	x = REQUEST ("Name_of_line_type", i, namefill)

	if x then
		LINE_TYPE i
		line2 0,0,length,0
		pen textPen
		text2 0.6, -posText,namefill
	endif

	add2 horizDistance,0
	nHorizDistance = nHorizDistance + 1

	if i % nFillsInARow = 0 then
		del nFillsInARow
		nHorizDistance = nHorizDistance - nFillsInARow
		add2 0, -vertDistance
		nVertDistance= nVertDistance + 1
	endif

next i
!del int(i / nFillsInARow)
del nVertDistance + nHorizDistance
if nFills / nFillsInARow = int(nFills / nFillsInARow) then nVertDistance  = nVertDistance  -1

hotspot2 0,												1,												unID 	: unID = unID  + 1
hotspot2 0, 											-(nVertDistance * vertDistance)- posText,		unID 	: unID = unID  + 1
hotspot2 (nFillsInARow-1) * horizDistance + length,		-(nVertDistance * vertDistance)- posText,		unID 	: unID = unID  + 1
hotspot2 (nFillsInARow-1) * horizDistance + length,		1,												unID 	: unID = unID  + 1
hotspot2 ((nFillsInARow-1) * horizDistance + length)/2,	(-(nVertDistance * vertDistance)- posText+1)/2, unID 	: unID = unID  + 1

if bLockSymbol then del 1

