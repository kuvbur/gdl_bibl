
if gs_debug then
	pen 10

	crossSize = 0.01
	line2 -crossSize,-crossSize, crossSize, crossSize	! Origo
	line2  crossSize,-crossSize,-crossSize, crossSize
endif


if bPositioningPreview then
	if gs_position_side_int = POS_TOP | gs_position_side_int = POS_BOTT then		! Top or Bottom
		previewRatio = titleRectX / savedTitleRectX
	else
		previewRatio = titleRectY / savedTitleRectY
	endif
endif



hID = 1		! Hotspot ID

if AC_UseUniformTextFormat then
	gs_drawing_name_font_type		= AC_TextFont
	gs_drawing_name_font_size		= AC_TextSize
	gs_drawing_name_font_pen		= AC_TextPen
	drawingNameFontStyle			= AC_TextStyle

	gs_drawing_scale_font_type		= AC_TextFont
	gs_drawing_scale_font_size		= AC_TextSize
	gs_drawing_scale_font_pen		= AC_TextPen
	drawingScaleFontStyle			= AC_TextStyle

	gs_drawing_number_font_type		= AC_TextFont
	gs_drawing_number_font_size		= AC_TextSize
	gs_drawing_number_font_pen		= AC_TextPen
	drawingNumberFontStyle			= AC_TextStyle

	gs_source_filename_font_type	= AC_TextFont
	gs_source_filename_font_size	= AC_TextSize
	gs_source_filename_font_pen		= AC_TextPen
	sourceFileNameFontStyle			= AC_TextStyle

	gs_drawing_magn_font_type		= AC_TextFont
	gs_drawing_magn_font_size		= AC_TextSize
	gs_drawing_magn_font_pen		= AC_TextPen
	drawingMagnFontStyle			= AC_TextStyle

	gs_project_filename_font_type	= AC_TextFont
	gs_project_filename_font_size	= AC_TextSize
	gs_project_filename_font_pen	= AC_TextPen
	projectFileNameFontStyle		= AC_TextStyle

	gs_layout_name_font_type		= AC_TextFont
	gs_layout_name_font_size		= AC_TextSize
	gs_layout_name_font_pen			= AC_TextPen
	layoutNameFontStyle				= AC_TextStyle

	gs_layout_number_font_type		= AC_TextFont
	gs_layout_number_font_size		= AC_TextSize
	gs_layout_number_font_pen		= AC_TextPen
	layoutNumberFontStyle			= AC_TextStyle
else
	drawingNameFontStyle		= gs_drawing_name_font_bold +		2 * gs_drawing_name_font_italic +		4 * gs_drawing_name_font_under
	drawingScaleFontStyle		= gs_drawing_scale_font_bold +		2 * gs_drawing_scale_font_italic +		4 * gs_drawing_scale_font_under
	drawingNumberFontStyle		= gs_drawing_number_font_bold +		2 * gs_drawing_number_font_italic +		4 * gs_drawing_number_font_under
	sourceFileNameFontStyle		= gs_source_filename_font_bold +	2 * gs_source_filename_font_italic +	4 * gs_source_filename_font_under
	drawingMagnFontStyle		= gs_drawing_magn_font_bold +		2 * gs_drawing_magn_font_italic +		4 * gs_drawing_magn_font_under
	projectFileNameFontStyle	= gs_project_filename_font_bold +	2 * gs_project_filename_font_italic +	4 * gs_project_filename_font_under
	layoutNameFontStyle			= gs_layout_name_font_bold +		2 * gs_layout_name_font_italic +		4 * gs_layout_name_font_under
	layoutNumberFontStyle		= gs_layout_number_font_bold +		2 * gs_layout_number_font_italic +		4 * gs_layout_number_font_under
endif

bInside = (AC_titleFitMode = 2 | AC_titleFitMode = 3)	! Layout or Cell; Position of the title inside or outside the drawing

anchorDrawingName			= 5
anchorDrawingScale			= 5
anchorSourceFileName		= 5
anchorDrawingMagnification	= 5
anchorProjectFileName		= 5
anchorLayoutName			= 5
anchorDrawingNumber			= 5
anchorLayoutNumber			= 5

define style "styleDrawingName" 	gs_drawing_name_font_type,
									gs_drawing_name_font_size,
									anchorDrawingName,
									drawingNameFontStyle

define style "styleDrawingScale"	gs_drawing_scale_font_type,
									gs_drawing_scale_font_size,
									anchorDrawingScale,
									drawingScaleFontStyle

define style "styleDrawingNumber"	gs_drawing_number_font_type,
									gs_drawing_number_font_size,
									anchorDrawingNumber,
									drawingNumberFontStyle

define style "styleSourceFileName" 	gs_source_filename_font_type,
									gs_source_filename_font_size,
									anchorSourceFileName,
									sourceFileNameFontStyle

define style "styleDrawingMagnification" 	gs_drawing_magn_font_type,
											gs_drawing_magn_font_size,
											anchorDrawingMagnification,
											drawingMagnFontStyle

define style "styleProjectFilename" gs_project_filename_font_type,
									gs_project_filename_font_size,
									anchorProjectFileName,
									projectFileNameFontStyle

define style "styleLayoutName" 		gs_layout_name_font_type,
									gs_layout_name_font_size,
									anchorLayoutName,
									layoutNameFontStyle

define style "styleLayoutNumber"	gs_layout_number_font_type,
									gs_layout_number_font_size,
									anchorLayoutNumber,
									layoutNumberFontStyle



if gs_debug & GLOB_CONTEXT <> 5 then
	pen 10
	circle2 AC_LayoutRect[1], AC_LayoutRect[2], 0.01
	circle2 AC_LayoutRect[3], AC_LayoutRect[4], 0.01

	pen 7
	circle2 AC_GridRect[1], AC_GridRect[2], 0.01
	circle2 AC_GridRect[3], AC_GridRect[4], 0.01

	pen 4
	circle2 AC_DrawingRect[1], AC_DrawingRect[2], 0.01
	circle2 AC_DrawingRect[3], AC_DrawingRect[4], 0.01
endif


! ==============================================================================
! Input Parameter Definitions
! ==============================================================================

AC_ProjectName = ""
AC_ProjectPath = ""
n = request ("HomeDB_info", "", n, AC_LayoutNumber, AC_LayoutName, n)
n = request ("Name_of_plan", "", n, AC_ProjectName, AC_ProjectPath)

if gs_drawing_name_custom then
	drawingName = gs_drawing_name_custom_text
else
	if AC_SourceFileName <> "" & gs_drawing_name_extension then
		drawingName = AC_SourceFileName
	else
		drawingName = AC_DrawingName
	endif
endif

if gs_drawing_scale_original then
	drawingScale			= gs_drawing_scale_prefix + AC_OriginalDrawingScale
else
	drawingScale			= gs_drawing_scale_prefix + AC_DrawingScale
endif
drawingNumber			= gs_drawing_number_prefix + AC_DrawingNumber
drawingMagnification	= STR(AC_Magnification, 1,0) + gs_drawing_magn_suffix
layoutNumber			= gs_layout_number_prefix + AC_LayoutNumber
if gs_source_filename_path then
	drawingPath			= AC_SourceFilePath
else
	drawingPath			= AC_SourceFileName
endif
if gs_project_filename_path then
	filePath			= AC_ProjectPath
else
	filePath			= AC_ProjectName
endif
layoutName				= AC_LayoutName


! ==============================================================================
! Draw Title
! ==============================================================================

if gs_debug then

	add2 titlePosX, titlePosY
	rot2 titleAlpha

	if not(AC_ManualPosition) then
		rect2 0,0, titleRectX, titleRectY	! Selected Drawing

		hotspot2 0,0
		hotspot2 0, titleRectY
		hotspot2 titleRectX, 0
		hotspot2 titleRectX, titleRectY
	endif

	del 2
endif


! ==============================================================================
! Precalculations
! ==============================================================================

drawingNameFontH	 = gs_show_drawing_name *			(1.6 * PAPER_TO_MODEL * gs_drawing_name_font_size)
drawingScaleFontH	 = gs_show_drawing_scale *			(1.6 * PAPER_TO_MODEL * gs_drawing_scale_font_size)
drawingNumberFontH	 = gs_show_drawing_number *			(1.6 * PAPER_TO_MODEL * gs_drawing_number_font_size)
sourceFileNameFontH	 = gs_show_source_filename *		(1.6 * PAPER_TO_MODEL * gs_source_filename_font_size)
drawingMagnFontH	 = gs_show_drawing_magnification *	(1.6 * PAPER_TO_MODEL * gs_drawing_magn_font_size)
projectFileNameFontH = gs_show_project_filename *		(1.6 * PAPER_TO_MODEL * gs_project_filename_font_size)
layoutNameFontH		 = gs_show_layout_name *			(1.6 * PAPER_TO_MODEL * gs_layout_name_font_size)
layoutNumberFontH	 = gs_show_layout_number *			(1.6 * PAPER_TO_MODEL * gs_layout_number_font_size)

titleWidth = gs_title_width

if gs_title_configuration_int = CONFIG_STYLE1 then	! Style 1
	set style "styleDrawingNumber"
	widthDrawingNumber	= gs_show_drawing_number * stw(drawingNumber) * PAPER_TO_MODEL

	set style "styleDrawingScale"
	widthDrawingScale	= gs_show_drawing_scale * stw(drawingScale) * PAPER_TO_MODEL

	set style "styleLayoutNumber"
	widthLayoutNumber	= gs_show_layout_number * stw(layoutNumber) * PAPER_TO_MODEL

	titleWidth	= 1.1 * max(widthLayoutNumber, widthDrawingNumber)
	titleHeight	= max(layoutNumberFontH, drawingNumberFontH)
endif

if gs_title_configuration_int = CONFIG_STYLE2 then	! Style 2
	set style "styleDrawingNumber"
	widthDrawingNumber	= gs_show_drawing_number * stw(drawingNumber) * PAPER_TO_MODEL

	set style "styleDrawingScale"
	widthDrawingScale	= gs_show_drawing_scale * stw(drawingScale) * PAPER_TO_MODEL

	set style "styleLayoutNumber"
	widthLayoutNumber	= gs_show_layout_number * stw(layoutNumber) * PAPER_TO_MODEL

	titleWidth	= 2.2 * max(widthLayoutNumber, widthDrawingNumber)
	titleHeight	= 2 * max(layoutNumberFontH, drawingNumberFontH)
endif

if gs_title_configuration_int = CONFIG_STYLE3 then	! Style 3
	set style "styleDrawingNumber"
	widthDrawingNumber	= gs_show_drawing_number * stw(drawingNumber) * PAPER_TO_MODEL

	set style "styleDrawingScale"
	widthDrawingScale	= gs_show_drawing_scale * stw(drawingScale) * PAPER_TO_MODEL

	set style "styleLayoutNumber"
	widthLayoutNumber	= gs_show_layout_number * stw(layoutNumber) * PAPER_TO_MODEL

	titleWidth	= 2.2 * max(widthLayoutNumber, max(widthDrawingNumber, widthDrawingScale))
	titleHeight	= max(layoutNumberFontH, 2 * max(drawingNumberFontH, drawingScaleFontH))
endif

if gs_symbol_style_int = SYMBST_CIRCLE then		! Circle
	titleWidth = max(titleWidth, titleHeight, gs_title_width)
	titleHeight = titleWidth
	if gs_title_width < titleWidth then
		gs_title_width		= titleWidth
		gs_title_half_width	= titleWidth / 2
	endif
endif
if gs_symbol_style_int = SYMBST_ELLIPT then		! Elliptoid
	titleWidth = max(titleWidth, titleHeight, gs_title_width)
	if gs_title_width < titleWidth then
		gs_title_width		= titleWidth
		gs_title_half_width	= titleWidth / 2
	endif
endif
if gs_symbol_style_int = SYMBST_RECTANG then		! Rectangular
	titleWidth = max(titleWidth, titleHeight, gs_title_width)
	if gs_title_width < titleWidth then
		gs_title_width		= titleWidth
		gs_title_half_width	= titleWidth / 2
	endif
endif


! ==============================================================================
! Positioning
! ==============================================================================

if SYMB_MIRRORED & not(bPositioningPreview) then mul2 -1, 1
if GLOB_CONTEXT = 5 then rot2 -SYMB_ROTANGLE

add2 titlePosX, titlePosY
rot2 titleAlpha

if gs_position_side_int = POS_TOP then		! Top
	if bInside then
		add2 0,titleRectY - gs_offset
	else
		add2 0,titleRectY + titleHeight + gs_offset
	endif
endif
if gs_position_side_int = POS_LEFT then		! Left
	if bInside then
		add2 gs_offset, 0
	else
		if bSpecPos then
			add2 -gs_offset - titleWidth, 0
		else
			add2 -gs_offset - titleHeight, 0
		endif
	endif
	rot2 (gs_title_rotate_with_position) * 90
endif
if gs_position_side_int = POS_RIGHT then		! Right
	if bInside then
		add2 titleRectX - titleHeight - gs_offset, 0
	else
		add2 titleRectX + gs_offset, 0
	endif
	rot2 (gs_title_rotate_with_position) * 90
endif
if gs_position_side_int = POS_BOTT then		! Bottom
	if bInside then
		add2 0, titleHeight + gs_offset
	else
		add2 0, -gs_offset
	endif
endif

if AC_titleFitMode = 1 & not(gs_title_rotate_with_position) then
	if gs_title_rotate_with_drawing then
		rot2 -gs_title_rotation_angle
	else
		rot2 -titleAlpha - SYMB_ROTANGLE - gs_title_rotation_angle
	endif
endif

if gs_position_alignment_int = ALIGN_LEFT then	! Left
	if bSpecPos then
		add2 0, titleHeight
	else
		add2 0, 0
	endif

		hotspot2  0,				-titleHeight / 2,	hID,	gs_title_width, 1 + (AC_ManualPosition + 1) * 128
		hotspot2 -1,				-titleHeight / 2,	hID+1,	gs_title_width, 3
		hotspot2  gs_title_width,	-titleHeight / 2,	hID+2,	gs_title_width, 2
		hID = hID + 3

	add2 0, 0
endif
if gs_position_alignment_int = ALIGN_CENTER then	! Center
	if bSpecPos then
		add2 -(titleRectY - gs_title_width) / 2, (titleRectY + titleHeight) / 2
	else
		add2 0, 0
	endif

		if gs_position_side_int = POS_TOP | gs_position_side_int = POS_BOTT then	! Top or Bottom
			hotspot2 titleRectX / 2,						-titleHeight / 2,	hID,	gs_title_half_width, 1 + 128
			hotspot2 titleRectX / 2 - 1,					-titleHeight / 2,	hID+1,	gs_title_half_width, 3
			hotspot2 titleRectX / 2 + gs_title_half_width,	-titleHeight / 2,	hID+2,	gs_title_half_width, 2
			hID = hID + 3

			hotspot2 titleRectX / 2,						-titleHeight / 2,	hID,	gs_title_half_width, 1 + 128
			hotspot2 titleRectX / 2 + 1,					-titleHeight / 2,	hID+1,	gs_title_half_width, 3
			hotspot2 titleRectX / 2 - gs_title_half_width,	-titleHeight / 2,	hID+2,	gs_title_half_width, 2
			hID = hID + 3

			add2 (titleRectX - gs_title_width) / 2, 0
		else
			hotspot2 titleRectY / 2,						-titleHeight / 2,	hID,	gs_title_half_width, 1 + 128
			hotspot2 titleRectY / 2 - 1,					-titleHeight / 2,	hID+1,	gs_title_half_width, 3
			hotspot2 titleRectY / 2 + gs_title_half_width,	-titleHeight / 2,	hID+2,	gs_title_half_width, 2
			hID = hID + 3

			hotspot2 titleRectY / 2,						-titleHeight / 2,	hID,	gs_title_half_width, 1 + 128
			hotspot2 titleRectY / 2 + 1,					-titleHeight / 2,	hID+1,	gs_title_half_width, 3
			hotspot2 titleRectY / 2 - gs_title_half_width,	-titleHeight / 2,	hID+2,	gs_title_half_width, 2
			hID = hID + 3

		add2 (titleRectY - gs_title_width) / 2, 0
	endif
endif
if gs_position_alignment_int = ALIGN_RIGHT then	! Right
	if bSpecPos then
		add2 -titleRectY + titleWidth, titleRectY
	else
		add2 0, 0
	endif

		if gs_position_side_int = POS_TOP | gs_position_side_int = POS_BOTT then	! Top or Bottom
			hotspot2 titleRectX,					-titleHeight / 2,	hID,	gs_title_width, 1 + (AC_ManualPosition + 1) * 128
			hotspot2 titleRectX + 1,				-titleHeight / 2,	hID+1,	gs_title_width, 3
			hotspot2 titleRectX - gs_title_width,	-titleHeight / 2,	hID+2,	gs_title_width, 2
			hID = hID + 3

			add2 titleRectX - gs_title_width, 0
		else
			hotspot2 titleRectY,					-titleHeight / 2,	hID,	gs_title_width, 1 + (AC_ManualPosition + 1) * 128
			hotspot2 titleRectY + 1,				-titleHeight / 2,	hID+1,	gs_title_width, 3
			hotspot2 titleRectY - gs_title_width,	-titleHeight / 2,	hID+2,	gs_title_width, 2
			hID = hID + 3

		add2 titleRectY - gs_title_width, 0
	endif
endif

if SYMB_MIRRORED & not(bPositioningPreview) then
	add2 gs_title_width,0
	mul2 -1, 1
endif


! ==============================================================================
! Title Preview
! ==============================================================================

if bPositioningPreview then
	pen gs_frame_pen
	line_type gs_frame_linetype

	fill gs_frame_fill

	poly2_b 4, 7, gs_frame_fill_fg, gs_frame_fill_bg,
		0,			 0,					1,
		0,			-titleHeight,		1,
		titleWidth,	-titleHeight,		1,
		titleWidth,	 0,					1
endif

! ==============================================================================
! Title Configuration 1
! ==============================================================================

if gs_title_configuration_int = CONFIG_STYLE1 & not(bPositioningPreview) then		! Style 1

! Marker =======================================================================

	! Marker
	gosub 100

! Data =========================================================================

	! Drawing Number
	posX =  titleWidth / 2
	posY = -titleHeight / 2
	gosub 202

	! Layout Number
	posX =  titleWidth / 2
	posY = -titleHeight / 2
	gosub 207

endif


! ==============================================================================
! Title Configuration 2
! ==============================================================================

if gs_title_configuration_int = CONFIG_STYLE2 & not(bPositioningPreview) then		! Style 2

! Marker =======================================================================

	gosub 100


! Data =========================================================================

	! Drawing Scale
	posX =  titleWidth / 2
	posY = -3 * titleHeight / 4
	gosub 201

	! Drawing Number
	posX =  titleWidth / 2
	posY = -titleHeight / 4
	gosub 202

	! Layout Number
	posX =  titleWidth / 2
	posY = -titleHeight / 4
	gosub 207

endif


! ==============================================================================
! Title Configuration 3
! ==============================================================================

if gs_title_configuration_int = CONFIG_STYLE3 & not(bPositioningPreview) then		! Style 3

! Marker =======================================================================

	gosub 100


! Data =========================================================================

	! Drawing Scale
	posX =  0.75 * titleWidth
	posY = -3 * titleHeight / 4
	gosub 201

	! Drawing Number
	posX =  0.75 * titleWidth
	posY = -titleHeight / 4
	gosub 202

	! Layout Number
	posX =  0.25 * titleWidth
	posY = -titleHeight / 2
	gosub 207
endif


! ==============================================================================

del 4 + 2 * (SYMB_MIRRORED & not(bPositioningPreview))

! ==============================================================================

end


100:
! ==============================================================================
! Drawing title marker symbol
! ==============================================================================
! in:
!	titleWidth:
!	titleHeight:
! ==============================================================================

	pen gs_symbol_pen
	fill gs_symbol_fill

	vPos = 0.5		! Position of the vertical line

	if gs_symbol_style_int = SYMBST_CIRCLE then		! Circle
		py = sqr((titleWidth / 2)^2 - (abs(0.5-vPos) * titleWidth)^2)

		hotarc2 titleWidth / 2, -titleHeight / 2, titleWidth / 2, 0, 180
		hotarc2 titleWidth / 2, -titleHeight / 2, titleWidth / 2, 180, 0

		if gs_title_configuration_int = CONFIG_STYLE1 then
			fill gs_symbol_fill
			poly2_b 2, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				titleWidth / 2, -titleHeight / 2, 901,
				titleWidth / 2, 360, 4001
		endif
		if gs_title_configuration_int = CONFIG_STYLE2 then
			fill gs_symbol_fill
			poly2_b 3, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				0,-titleHeight / 2, 1,
				0, -1, 801,
				titleWidth,-titleHeight / 2, 1000

			fill gs_symbol_fill_2
			poly2_b 3, 7, gs_symbol_fill_fg_2, gs_symbol_fill_bg_2,
				0,-titleHeight / 2, 1,
				0, 1, 801,
				titleWidth,-titleHeight / 2, 1000

			line2 0,-titleHeight / 2, titleWidth,-titleHeight / 2

			hotline2 0,-titleHeight / 2, titleWidth,-titleHeight / 2
		endif
		if gs_title_configuration_int = CONFIG_STYLE3 then

			if abs(0.5-vPos) < EPS then
				vx = -1
				vy =  0
			else
				alpha = atn(py / ((0.5-vPos) * titleWidth))
				if alpha < 0 then alpha = 180 - alpha
				vx = -abs(sin(alpha))
				vy = -cos(alpha)
			endif

			fill gs_symbol_fill
			poly2_b 4, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				vPos * titleWidth,	-titleHeight / 2,		   0,
				vPos * titleWidth,	-titleHeight / 2 - py,	   1,
				-vx,				 vy,					 801,
				titleWidth,			-titleHeight / 2,		1000

			fill gs_symbol_fill_2
			poly2_b 3, 7, gs_symbol_fill_fg_2, gs_symbol_fill_bg_2,
				vPos * titleWidth, -titleHeight / 2 + py,	   1,
				vx,					vy,						 801,
				vPos * titleWidth, -titleHeight / 2 - py,	1000

			fill gs_symbol_fill_3
			poly2_b 4, 7, gs_symbol_fill_fg_3, gs_symbol_fill_bg_3,
				vPos * titleWidth,	-titleHeight / 2,		   0,
				vPos * titleWidth,	-titleHeight / 2 + py,	   1,
				-vx,				-vy,					 800,
				titleWidth,			-titleHeight / 2,		1000

			line2 vPos * titleWidth,-titleHeight / 2, titleWidth, -titleHeight / 2
			line2 vPos * titleWidth,-titleHeight / 2 + py, vPos * titleWidth, -titleHeight / 2 - py

			hotline2 vPos * titleWidth,-titleHeight / 2, titleWidth, -titleHeight / 2
			hotline2 vPos * titleWidth,-titleHeight / 2 + py, vPos * titleWidth, -titleHeight / 2 - py
		endif
	endif
	if gs_symbol_style_int = SYMBST_ELLIPT then		! Elliptoid
		add2 titleWidth / 2, -titleHeight / 2
		mul2 1, titleHeight / titleWidth

		hotarc2 0, 0, titleWidth / 2, 0, 180
		hotarc2 0, 0, titleWidth / 2, 180, 0

		if gs_title_configuration_int = CONFIG_STYLE1 then
			fill gs_symbol_fill
			poly2_b 2, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				0, 0, 901,
				titleWidth / 2, 360, 4001
		endif
		if gs_title_configuration_int = CONFIG_STYLE2 then
			fill gs_symbol_fill
			poly2_b 3, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				-titleWidth/2,0, 1,
				 0, -1, 801,
				 titleWidth/2,0, 1000

			fill gs_symbol_fill_2
			poly2_b 3, 7, gs_symbol_fill_fg_2, gs_symbol_fill_bg_2,
				-titleWidth/2,0, 1,
				 0, 1, 801,
				 titleWidth/2,0, 1000

			line2 -titleWidth/2,0, titleWidth/2, 0

			hotline2 -titleWidth/2,0, titleWidth/2, 0
		endif
		if gs_title_configuration_int = CONFIG_STYLE3 then
			py = sqr((titleWidth / 2)^2 - (abs(0.5-vPos) * titleWidth)^2)

			if abs(0.5-vPos) < EPS then
				vx = -1
				vy =  0
			else
				alpha = atn(py / ((0.5-vPos) * titleWidth))
				if alpha < 0 then alpha = 180 - alpha
				vx = -abs(sin(alpha))
				vy = -cos(alpha)
			endif

			fill gs_symbol_fill
			poly2_b 4, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				-(0.5-vPos) * titleWidth,	 0,	   0,
				-(0.5-vPos) * titleWidth,	-py,   1,
				-vx,				 vy, 801,
				 titleWidth/2,			 0,	1000

			fill gs_symbol_fill_2
			poly2_b 3, 7, gs_symbol_fill_fg_2, gs_symbol_fill_bg_2,
				-(0.5-vPos) * titleWidth,	 py,	   1,
				 vx,						 vy,	 801,
				-(0.5-vPos) * titleWidth,	-py,	1000

			fill gs_symbol_fill_3
			poly2_b 4, 7, gs_symbol_fill_fg_3, gs_symbol_fill_bg_3,
				-(0.5-vPos) * titleWidth,	 0,	   0,
				-(0.5-vPos) * titleWidth,	 py,   1,
				-vx,						-vy, 800,
				titleWidth/2,				 0,	1000

			line2 -(0.5-vPos) * titleWidth, 0, titleWidth/2, 0
			line2 -(0.5-vPos) * titleWidth, py, -(0.5-vPos) * titleWidth, -py

			hotline2 -(0.5-vPos) * titleWidth, 0, titleWidth/2, 0
			hotline2 -(0.5-vPos) * titleWidth, py, -(0.5-vPos) * titleWidth, -py
		endif

		del 2
	endif
	if gs_symbol_style_int = SYMBST_RECTANG then		! Rectangular

		hotline2 0,			 0,			 0,				-titleHeight
		hotline2 titleWidth, 0,			 titleWidth,	-titleHeight
		hotline2 0,			 0,			 titleWidth,	 0
		hotline2 0,			-titleHeight,titleWidth,	-titleHeight

		if gs_title_configuration_int = CONFIG_STYLE1 then
			fill gs_symbol_fill
			poly2_b 4, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				0,			 0,				1,
				0,			-titleHeight,	1,
				titleWidth, -titleHeight,	1,
				titleWidth,  0,				1
		endif
		if gs_title_configuration_int = CONFIG_STYLE2 then
			fill gs_symbol_fill
			poly2_b 4, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				0,			-titleHeight,	1,
				0,			-titleHeight/2,	0,
				titleWidth, -titleHeight/2,	1,
				titleWidth, -titleHeight,	1

			fill gs_symbol_fill_2
			poly2_b 4, 7, gs_symbol_fill_fg_2, gs_symbol_fill_bg_2,
				0,			 0,				1,
				0,			-titleHeight/2,	0,
				titleWidth, -titleHeight/2,	1,
				titleWidth,  0,				1

			line2 0,-titleHeight / 2, titleWidth, -titleHeight / 2

			hotline2 0,-titleHeight / 2, titleWidth, -titleHeight / 2
		endif
		if gs_title_configuration_int = CONFIG_STYLE3 then
			fill gs_symbol_fill
			poly2_b 4, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
				vPos * titleWidth,	-titleHeight,	0,
				vPos * titleWidth,	-titleHeight/2,	0,
				titleWidth,			-titleHeight/2,	1,
				titleWidth,			-titleHeight,	1

			fill gs_symbol_fill_2
			poly2_b 4, 7, gs_symbol_fill_fg_2, gs_symbol_fill_bg_2,
				0,					 0,				1,
				0,					-titleHeight,	1,
				vPos * titleWidth,	-titleHeight,	0,
				vPos * titleWidth,	 0,				1

			fill gs_symbol_fill_3
			poly2_b 4, 7, gs_symbol_fill_fg_3, gs_symbol_fill_bg_3,
				vPos * titleWidth,	 0,				0,
				vPos * titleWidth,	-titleHeight/2,	0,
				titleWidth,			-titleHeight/2,	1,
				titleWidth, 		 0,				1

			line2 vPos * titleWidth,-titleHeight / 2, titleWidth, -titleHeight / 2
			line2 vPos * titleWidth, 0, vPos * titleWidth, -titleHeight

			hotline2 vPos * titleWidth,-titleHeight / 2, titleWidth, -titleHeight / 2
			hotline2 vPos * titleWidth, 0, vPos * titleWidth, -titleHeight
		endif
	endif
return


200:
! ==============================================================================
! Text Drawing Name
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_name then

		set style "styleDrawingName"
		pen gs_drawing_name_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_name_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_name_pos_y") then
			textWidth	= stw(drawingName) * PAPER_TO_MODEL
			textHeight	= drawingNameFontH
			textAnchor	= anchorDrawingName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_drawing_name_pos_x,				posY + gs_drawing_name_pos_y,
				  posX + gs_drawing_name_pos_x + textWidth,	posY + gs_drawing_name_pos_y + textHeight
			del 1
		else
			text2 posX + gs_drawing_name_pos_x, posY + gs_drawing_name_pos_y, drawingName
		endif

		hotspot2 posX,							posY + gs_drawing_name_pos_y,	hID,	gs_drawing_name_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_drawing_name_pos_y,	hID+1,	gs_drawing_name_pos_x, 3
		hotspot2 posX + gs_drawing_name_pos_x,	posY + gs_drawing_name_pos_y,	hID+2,	gs_drawing_name_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_drawing_name_pos_x,	posY,							hID,	gs_drawing_name_pos_y, 1+128
		hotspot2 posX + gs_drawing_name_pos_x,	posY - 1,						hID+1,	gs_drawing_name_pos_y, 3
		hotspot2 posX + gs_drawing_name_pos_x,	posY + gs_drawing_name_pos_y,	hID+2,	gs_drawing_name_pos_y, 2
		hID = hID + 3
	endif
return

201:
! ==============================================================================
! Text Drawing Scale
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_scale then

		set style "styleDrawingScale"
		pen gs_drawing_scale_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_scale_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_scale_pos_y") then
			textWidth	= stw(drawingScale) * PAPER_TO_MODEL
			textHeight	= drawingScaleFontH
			textAnchor	= anchorDrawingScale
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_drawing_scale_pos_x,				posY + gs_drawing_scale_pos_y,
				  posX + gs_drawing_scale_pos_x + textWidth,	posY + gs_drawing_scale_pos_y + textHeight
			del 1
		else
			text2 posX + gs_drawing_scale_pos_x, posY + gs_drawing_scale_pos_y, drawingScale
		endif

		hotspot2 posX,							posY + gs_drawing_scale_pos_y,	hID,	gs_drawing_scale_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_drawing_scale_pos_y,	hID+1,	gs_drawing_scale_pos_x, 3
		hotspot2 posX + gs_drawing_scale_pos_x,	posY + gs_drawing_scale_pos_y,	hID+2,	gs_drawing_scale_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_drawing_scale_pos_x,	posY,							hID,	gs_drawing_scale_pos_y, 1+128
		hotspot2 posX + gs_drawing_scale_pos_x,	posY - 1,						hID+1,	gs_drawing_scale_pos_y, 3
		hotspot2 posX + gs_drawing_scale_pos_x,	posY + gs_drawing_scale_pos_y,	hID+2,	gs_drawing_scale_pos_y, 2
		hID = hID + 3
	endif
return

202:
! ==============================================================================
! Text Drawing Number
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_number then

		set style "styleDrawingNumber"
		pen gs_drawing_number_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_number_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_number_pos_y") then
			textWidth	= stw(drawingNumber) * PAPER_TO_MODEL
			textHeight	= drawingNumberFontH
			textAnchor	= anchorDrawingNumber
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_drawing_number_pos_x,				posY + gs_drawing_number_pos_y,
				  posX + gs_drawing_number_pos_x + textWidth,	posY + gs_drawing_number_pos_y + textHeight
			del 1
		else
			text2 posX + gs_drawing_number_pos_x, posY + gs_drawing_number_pos_y, drawingNumber
		endif

		hotspot2 posX,								posY + gs_drawing_number_pos_y,	hID,	gs_drawing_number_pos_x, 1+128
		hotspot2 posX - 1,							posY + gs_drawing_number_pos_y,	hID+1,	gs_drawing_number_pos_x, 3
		hotspot2 posX + gs_drawing_number_pos_x,	posY + gs_drawing_number_pos_y,	hID+2,	gs_drawing_number_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_drawing_number_pos_x,	posY,							hID,	gs_drawing_number_pos_y, 1+128
		hotspot2 posX + gs_drawing_number_pos_x,	posY - 1,						hID+1,	gs_drawing_number_pos_y, 3
		hotspot2 posX + gs_drawing_number_pos_x,	posY + gs_drawing_number_pos_y,	hID+2,	gs_drawing_number_pos_y, 2
		hID = hID + 3
	endif
return

203:
! ==============================================================================
! Text Drawing Path
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_source_filename then

		set style "styleSourceFileName"
		pen gs_source_filename_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_source_filename_pos_x" | gs_GLOB_MODPAR_NAME = "gs_source_filename_pos_y") then
			textWidth	= stw(drawingPath) * PAPER_TO_MODEL
			textHeight	= sourceFileNameFontH
			textAnchor	= anchorSourceFileName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_source_filename_pos_x,				posY + gs_source_filename_pos_y,
				  posX + gs_source_filename_pos_x + textWidth,	posY + gs_source_filename_pos_y + textHeight
			del 1
		else
			text2 posX + gs_source_filename_pos_x, posY + gs_source_filename_pos_y, drawingPath
		endif

		hotspot2 posX,								posY + gs_source_filename_pos_y,	hID,	gs_source_filename_pos_x, 1+128
		hotspot2 posX - 1,							posY + gs_source_filename_pos_y,	hID+1,	gs_source_filename_pos_x, 3
		hotspot2 posX + gs_source_filename_pos_x,	posY + gs_source_filename_pos_y,	hID+2,	gs_source_filename_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_source_filename_pos_x,	posY,								hID,	gs_source_filename_pos_y, 1+128
		hotspot2 posX + gs_source_filename_pos_x,	posY - 1,							hID+1,	gs_source_filename_pos_y, 3
		hotspot2 posX + gs_source_filename_pos_x,	posY + gs_source_filename_pos_y,	hID+2,	gs_source_filename_pos_y, 2
		hID = hID + 3
	endif
return

204:
! ==============================================================================
! Text Magnification
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_magnification then

		set style "styleDrawingMagnification"
		pen gs_drawing_magn_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_magn_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_magn_pos_y") then
			textWidth	= stw(drawingMagnification) * PAPER_TO_MODEL
			textHeight	= drawingMagnFontH
			textAnchor	= anchorDrawingMagnification
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_drawing_magn_pos_x,				posY + gs_drawing_magn_pos_y,
				  posX + gs_drawing_magn_pos_x + textWidth,	posY + gs_drawing_magn_pos_y + textHeight
			del 1
		else
			text2 posX + gs_drawing_magn_pos_x, posY + gs_drawing_magn_pos_y, drawingMagnification
		endif

		hotspot2 posX,							posY + gs_drawing_magn_pos_y,	hID,	gs_drawing_magn_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_drawing_magn_pos_y,	hID+1,	gs_drawing_magn_pos_x, 3
		hotspot2 posX + gs_drawing_magn_pos_x,	posY + gs_drawing_magn_pos_y,	hID+2,	gs_drawing_magn_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_drawing_magn_pos_x,	posY,							hID,	gs_drawing_magn_pos_y, 1+128
		hotspot2 posX + gs_drawing_magn_pos_x,	posY - 1,						hID+1,	gs_drawing_magn_pos_y, 3
		hotspot2 posX + gs_drawing_magn_pos_x,	posY + gs_drawing_magn_pos_y,	hID+2,	gs_drawing_magn_pos_y, 2
		hID = hID + 3
	endif
return

205:
! ==============================================================================
! Text File Path
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_project_filename then

		set style "styleProjectFilename"
		pen gs_project_filename_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_project_filename_pos_x" | gs_GLOB_MODPAR_NAME = "gs_project_filename_pos_y") then
			textWidth	= stw(filePath) * PAPER_TO_MODEL
			textHeight	= projectFileNameFontH
			textAnchor	= anchorProjectFileName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_project_filename_pos_x,				posY + gs_project_filename_pos_y,
				  posX + gs_project_filename_pos_x + textWidth,	posY + gs_project_filename_pos_y + textHeight
			del 1
		else
			text2 posX + gs_project_filename_pos_x, posY + gs_project_filename_pos_y, filePath
		endif

		hotspot2 posX,								posY + gs_project_filename_pos_y,	hID,	gs_project_filename_pos_x, 1+128
		hotspot2 posX - 1,							posY + gs_project_filename_pos_y,	hID+1,	gs_project_filename_pos_x, 3
		hotspot2 posX + gs_project_filename_pos_x,	posY + gs_project_filename_pos_y,	hID+2,	gs_project_filename_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_project_filename_pos_x,	posY,								hID,	gs_project_filename_pos_y, 1+128
		hotspot2 posX + gs_project_filename_pos_x,	posY - 1,							hID+1,	gs_project_filename_pos_y, 3
		hotspot2 posX + gs_project_filename_pos_x,	posY + gs_project_filename_pos_y,	hID+2,	gs_project_filename_pos_y, 2
		hID = hID + 3
	endif
return

206:
! ==============================================================================
! Text Layout Name
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_layout_name then

		set style "styleLayoutName"
		pen gs_layout_name_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_layout_name_pos_x" | gs_GLOB_MODPAR_NAME = "gs_layout_name_pos_y") then
			textWidth	= stw(layoutName) * PAPER_TO_MODEL
			textHeight	= layoutNameFontH
			textAnchor	= anchorLayoutName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_layout_name_pos_x,				posY + gs_layout_name_pos_y,
				  posX + gs_layout_name_pos_x + textWidth,	posY + gs_layout_name_pos_y + textHeight
			del 1
		else
			text2 posX + gs_layout_name_pos_x, posY + gs_layout_name_pos_y, layoutName
		endif

		hotspot2 posX,							posY + gs_layout_name_pos_y,	hID,	gs_layout_name_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_layout_name_pos_y,	hID+1,	gs_layout_name_pos_x, 3
		hotspot2 posX + gs_layout_name_pos_x,	posY + gs_layout_name_pos_y,	hID+2,	gs_layout_name_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_layout_name_pos_x,	posY,							hID,	gs_layout_name_pos_y, 1+128
		hotspot2 posX + gs_layout_name_pos_x,	posY - 1,						hID+1,	gs_layout_name_pos_y, 3
		hotspot2 posX + gs_layout_name_pos_x,	posY + gs_layout_name_pos_y,	hID+2,	gs_layout_name_pos_y, 2
		hID = hID + 3
	endif
return

207:
! ==============================================================================
! Text Layout Number
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_layout_number then

		set style "styleLayoutNumber"
		pen gs_layout_number_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_layout_number_pos_x" | gs_GLOB_MODPAR_NAME = "gs_layout_number_pos_y") then
			textWidth	= stw(layoutNumber) * PAPER_TO_MODEL
			textHeight	= layoutNumberFontH
			textAnchor	= anchorLayoutNumber
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_layout_number_pos_x,				posY + gs_layout_number_pos_y,
				  posX + gs_layout_number_pos_x + textWidth,	posY + gs_layout_number_pos_y + textHeight
			del 1
		else
			text2 posX + gs_layout_number_pos_x, posY + gs_layout_number_pos_y, layoutNumber
		endif

		hotspot2 posX,							posY + gs_layout_number_pos_y,	hID,	gs_layout_number_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_layout_number_pos_y,	hID+1,	gs_layout_number_pos_x, 3
		hotspot2 posX + gs_layout_number_pos_x,	posY + gs_layout_number_pos_y,	hID+2,	gs_layout_number_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_layout_number_pos_x,	posY,							hID,	gs_layout_number_pos_y, 1+128
		hotspot2 posX + gs_layout_number_pos_x,	posY - 1,						hID+1,	gs_layout_number_pos_y, 3
		hotspot2 posX + gs_layout_number_pos_x,	posY + gs_layout_number_pos_y,	hID+2,	gs_layout_number_pos_y, 2
		hID = hID + 3
	endif
return


300:
! ==============================================================================
! Text anchor relative position
! ==============================================================================
! in:
!	textWidth:		width of the text
!	textHeight:		height of the text
!	textAnchor:		text anchor code
! out:
!	anchorY:		anchor relative position X
!	anchorY:		anchor relative position Y
! ==============================================================================

	if textAnchor = 1 | textAnchor = 4 | textAnchor = 7 then
		anchorX = 0
	endif
	if textAnchor = 2 | textAnchor = 5 | textAnchor = 8 then
		anchorX = -textWidth / 2
	endif
	if textAnchor = 3 | textAnchor = 6 | textAnchor = 9 then
		anchorX = -textWidth
	endif
	if textAnchor = 1 | textAnchor = 2 | textAnchor = 3 then
		anchorY = -textHeight
	endif
	if textAnchor = 4 | textAnchor = 5 | textAnchor = 6 then
		anchorY = -textHeight / 2
	endif
	if textAnchor = 7 | textAnchor = 8 | textAnchor = 9 then
		anchorY = 0
	endif
return


