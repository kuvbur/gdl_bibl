
PAPER_TO_MODEL = GLOB_SCALE / 1000	! Convert paper size to model size


if gs_debug then
	pen 10

	crossSize = 0.01
	line2 -crossSize,-crossSize, crossSize, crossSize	! Origo
	line2  crossSize,-crossSize,-crossSize, crossSize
endif


if bPositioningPreview then
	if gs_position_side_int = POS_TOP | gs_position_side_int = POS_BOTT then		! Top or Bottom
		previewRatioX = titleRectX / savedTitleRectX
		previewRatioY = titleRectY / savedTitleRectY
	else
		previewRatioY = titleRectX / savedTitleRectX
		previewRatioX = titleRectY / savedTitleRectY
	endif
endif


hID = 1		! Hotspot ID

if AC_UseUniformTextFormat then
	gs_drawing_name_font_type		= AC_TextFont
	gs_drawing_name_font_size		= AC_TextSize
	gs_drawing_name_font_pen		= AC_TextPen
	drawingNameFontStyle			= AC_TextStyle

	gs_drawing_scale_font_type		= AC_TextFont
	gs_drawing_scale_font_size		= AC_TextSize
	gs_drawing_scale_font_pen		= AC_TextPen
	drawingScaleFontStyle			= AC_TextStyle

	gs_drawing_number_font_type		= AC_TextFont
	gs_drawing_number_font_size		= AC_TextSize
	gs_drawing_number_font_pen		= AC_TextPen
	drawingNumberFontStyle			= AC_TextStyle

	gs_source_filename_font_type	= AC_TextFont
	gs_source_filename_font_size	= AC_TextSize
	gs_source_filename_font_pen		= AC_TextPen
	sourceFileNameFontStyle			= AC_TextStyle

	gs_drawing_magn_font_type		= AC_TextFont
	gs_drawing_magn_font_size		= AC_TextSize
	gs_drawing_magn_font_pen		= AC_TextPen
	drawingMagnFontStyle			= AC_TextStyle

	gs_project_filename_font_type	= AC_TextFont
	gs_project_filename_font_size	= AC_TextSize
	gs_project_filename_font_pen	= AC_TextPen
	projectFileNameFontStyle		= AC_TextStyle

	gs_layout_name_font_type		= AC_TextFont
	gs_layout_name_font_size		= AC_TextSize
	gs_layout_name_font_pen			= AC_TextPen
	layoutNameFontStyle				= AC_TextStyle

	gs_layout_number_font_type		= AC_TextFont
	gs_layout_number_font_size		= AC_TextSize
	gs_layout_number_font_pen		= AC_TextPen
	layoutNumberFontStyle			= AC_TextStyle
else
	drawingNameFontStyle		= gs_drawing_name_font_bold +		2 * gs_drawing_name_font_italic +		4 * gs_drawing_name_font_under
	drawingScaleFontStyle		= gs_drawing_scale_font_bold +		2 * gs_drawing_scale_font_italic +		4 * gs_drawing_scale_font_under
	drawingNumberFontStyle		= gs_drawing_number_font_bold +		2 * gs_drawing_number_font_italic +		4 * gs_drawing_number_font_under
	sourceFileNameFontStyle		= gs_source_filename_font_bold +	2 * gs_source_filename_font_italic +	4 * gs_source_filename_font_under
	drawingMagnFontStyle		= gs_drawing_magn_font_bold +		2 * gs_drawing_magn_font_italic +		4 * gs_drawing_magn_font_under
	projectFileNameFontStyle	= gs_project_filename_font_bold +	2 * gs_project_filename_font_italic +	4 * gs_project_filename_font_under
	layoutNameFontStyle			= gs_layout_name_font_bold +		2 * gs_layout_name_font_italic +		4 * gs_layout_name_font_under
	layoutNumberFontStyle		= gs_layout_number_font_bold +		2 * gs_layout_number_font_italic +		4 * gs_layout_number_font_under
endif


bInside = (AC_titleFitMode = 2 | AC_titleFitMode = 3)	! Layout or Cell; Position of the title inside or outside the drawing
bInvert = (gs_position_side_int = POS_TOP | gs_position_side_int = POS_LEFT)	! Top or Left; Invert title configuration


anchorDrawingName			= 3 + not(bInvert) * 6		! 3 or 9
anchorDrawingScale			= 1 + not(bInvert) * 2		! 7 or 9
anchorSourceFileName		= 3 + not(bInvert) * 6		! 3 or 9
anchorDrawingMagnification	= 1 + not(bInvert) * 2		! 7 or 9
anchorProjectFileName		= 3 + not(bInvert) * 6		! 3 or 9
anchorLayoutName			= 3 + not(bInvert) * 6		! 3 or 9
if gs_show_layout_number & gs_show_drawing_number then
	anchorDrawingNumber	= 8
	anchorLayoutNumber	= 2
else
	anchorDrawingNumber	= 5
	anchorLayoutNumber	= 5
endif


define style "styleDrawingName" 	gs_drawing_name_font_type,
									gs_drawing_name_font_size,
									anchorDrawingName,
									drawingNameFontStyle

define style "styleDrawingScale"	gs_drawing_scale_font_type,
									gs_drawing_scale_font_size,
									5,
									drawingScaleFontStyle

define style "styleDrawingNumber"	gs_drawing_number_font_type,
									gs_drawing_number_font_size,
									anchorDrawingNumber,
									drawingNumberFontStyle

define style "styleSourceFileName" 	gs_source_filename_font_type,
									gs_source_filename_font_size,
									anchorSourceFileName,
									sourceFileNameFontStyle

define style "styleDrawingMagnification" 	gs_drawing_magn_font_type,
											gs_drawing_magn_font_size,
											5,
											drawingMagnFontStyle

define style "styleProjectFilename" gs_project_filename_font_type,
									gs_project_filename_font_size,
									anchorProjectFileName,
									projectFileNameFontStyle

define style "styleLayoutName" 		gs_layout_name_font_type,
									gs_layout_name_font_size,
									anchorLayoutName,
									layoutNameFontStyle

define style "styleLayoutNumber"	gs_layout_number_font_type,
									gs_layout_number_font_size,
									anchorLayoutNumber,
									layoutNumberFontStyle



if gs_debug & GLOB_CONTEXT <> 5 then
	pen 10
	circle2 AC_LayoutRect[1], AC_LayoutRect[2], 0.01
	circle2 AC_LayoutRect[3], AC_LayoutRect[4], 0.01

	pen 7
	circle2 AC_GridRect[1], AC_GridRect[2], 0.01
	circle2 AC_GridRect[3], AC_GridRect[4], 0.01

	pen 4
	circle2 AC_DrawingRect[1], AC_DrawingRect[2], 0.01
	circle2 AC_DrawingRect[3], AC_DrawingRect[4], 0.01
endif


! ==============================================================================
! Input Parameter Definitions
! ==============================================================================

AC_ProjectName = ""
AC_ProjectPath = ""
n = request ("HomeDB_info", "", n, AC_LayoutNumber, AC_LayoutName, n)
n = request ("Name_of_plan", "", n, AC_ProjectName, AC_ProjectPath)

if gs_drawing_name_custom then
	drawingName = gs_drawing_name_custom_text
else
	if AC_SourceFileName <> "" & gs_drawing_name_extension then
		drawingName = AC_SourceFileName
	else
		drawingName = AC_DrawingName
	endif
endif

if gs_drawing_scale_original then
	drawingScale			= gs_drawing_scale_prefix + AC_OriginalDrawingScale
else
	drawingScale			= gs_drawing_scale_prefix + AC_DrawingScale
endif
drawingNumber			= gs_drawing_number_prefix + AC_DrawingNumber
drawingMagnification	= STR(AC_Magnification, 1,0) + gs_drawing_magn_suffix
layoutNumber			= gs_layout_number_prefix + AC_LayoutNumber
if gs_source_filename_path then
	drawingPath			= AC_SourceFilePath
else
	drawingPath			= AC_SourceFileName
endif
if gs_project_filename_path then
	filePath			= AC_ProjectPath
else
	filePath			= AC_ProjectName
endif
layoutName				= AC_LayoutName


! ==============================================================================
! Draw Title
! ==============================================================================

if gs_debug then

	add2 titlePosX, titlePosY
	rot2 titleAlpha

	if not(AC_ManualPosition) then
		rect2 0,0, titleRectX, titleRectY	! Selected Drawing

		hotspot2 0,0
		hotspot2 0, titleRectY
		hotspot2 titleRectX, 0
		hotspot2 titleRectX, titleRectY
	endif

	del 2
endif


! ==============================================================================
! Precalculations
! ==============================================================================

drawingNameFontH	 = gs_show_drawing_name *			(1.6 * PAPER_TO_MODEL * gs_drawing_name_font_size)
drawingScaleFontH	 = gs_show_drawing_scale *			(1.6 * PAPER_TO_MODEL * gs_drawing_scale_font_size)
drawingNumberFontH	 = gs_show_drawing_number *			(1.6 * PAPER_TO_MODEL * gs_drawing_number_font_size)
sourceFileNameFontH	 = gs_show_source_filename *		(1.6 * PAPER_TO_MODEL * gs_source_filename_font_size)
drawingMagnFontH	 = gs_show_drawing_magnification *	(1.6 * PAPER_TO_MODEL * gs_drawing_magn_font_size)
projectFileNameFontH = gs_show_project_filename *		(1.6 * PAPER_TO_MODEL * gs_project_filename_font_size)
layoutNameFontH		 = gs_show_layout_name *			(1.6 * PAPER_TO_MODEL * gs_layout_name_font_size)
layoutNumberFontH	 = gs_show_layout_number *			(1.6 * PAPER_TO_MODEL * gs_layout_number_font_size)

leftRowHeight	= max(drawingScaleFontH, drawingMagnFontH)
lowerRowHeight	= max(drawingNameFontH, sourceFileNameFontH, projectFileNameFontH, layoutNumberFontH)
titleRowHeight	= drawingNumberFontH + layoutNumberFontH
titleRowWidth	= max(gs_show_drawing_number * stw(drawingNumber), gs_show_layout_number * stw(layoutNumber)) * PAPER_TO_MODEL

titleThickness	= max(leftRowHeight, lowerRowHeight, titleRowHeight, titleRowWidth)

if not(gs_link_title_width) | AC_ManualPosition then
	if bInside then
		titleWidth	= gs_title_width  - titleThickness - 2 * gs_offset
		titleHeight	= gs_title_height - titleThickness - 2 * gs_offset
	else
		titleWidth	= gs_title_width  + 2 * gs_offset
		titleHeight	= gs_title_height + 2 * gs_offset
	endif
else
	if gs_position_side_int = POS_TOP | gs_position_side_int = POS_BOTT then		! Top or Bottom
		if bInside then
			titleWidth	= titleRectX - titleThickness - 2 * gs_offset
			titleHeight	= titleRectY - titleThickness - 2 * gs_offset
		else
			titleWidth	= titleRectX + 2 * gs_offset
			titleHeight	= titleRectY + 2 * gs_offset
		endif
	else
		if bInside then
			titleWidth	= titleRectY - titleThickness - 2 * gs_offset
			titleHeight	= titleRectX - titleThickness - 2 * gs_offset
		else
			titleWidth	= titleRectY + 2 * gs_offset
			titleHeight	= titleRectX + 2 * gs_offset
		endif
	endif
endif



! ==============================================================================
! Positioning
! ==============================================================================

nExtratransformations = 0
if SYMB_MIRRORED & not(bPositioningPreview) then
	mul2 -1, 1
	nExtratransformations = nExtratransformations + 1
endif
if GLOB_CONTEXT = 5 then
	rot2 -SYMB_ROTANGLE
	nExtratransformations = nExtratransformations + 1
endif

add2 titlePosX, titlePosY
rot2 titleAlpha

if gs_position_side_int = POS_TOP then		! Top
	if bInside then
		add2 titleThickness + gs_offset,titleRectY - titleThickness - gs_offset
	else
		add2 -gs_offset, titleRectY + gs_offset
	endif
endif
if gs_position_side_int = POS_LEFT then		! Left
	if bInside then
		add2 titleThickness + gs_offset, titleThickness + gs_offset
	else
		add2 -gs_offset, -gs_offset
	endif
	rot2 90
endif
if gs_position_side_int = POS_RIGHT then		! Right
	if bInside then
		add2 titleRectX - titleThickness - gs_offset, titleThickness + gs_offset
	else
		add2 titleRectX + gs_offset, -gs_offset
	endif
	rot2 90
endif
if gs_position_side_int = POS_BOTT then		! Bottom
	if bInside then
		add2 titleThickness + gs_offset, titleThickness + gs_offset
	else
		add2 -gs_offset, -gs_offset
	endif
endif


! ==============================================================================
! Rectangular Title
! ==============================================================================

iInvert = not(bInvert) - bInvert

! Marker =======================================================================

add2 -titleThickness, -not(bInvert) * titleThickness
gosub 100	! Marker
del 1

posX = -titleThickness / 2
posY = -titleThickness / 2 + bInvert * titleThickness
gosub 202	! Drawing Number
gosub 207	! Layout Number


! Lines ========================================================================

pen gs_title_pen
line_type gs_title_linetype

if gs_title_line_1 then line2 -titleThickness, 0, titleWidth, 0
hotline2 -titleThickness, 0, titleWidth, 0
if gs_title_line_2 then
	if bInvert then
		line2 0,-titleHeight, 0,titleThickness
		hotline2 0,-titleHeight, 0,titleThickness
	else
		line2 0,titleHeight, 0,-titleThickness
		hotline2 0,titleHeight, 0,-titleThickness
	endif
endif

! Hotspots =====================================================================

hotspot2 0,0

if gs_link_title_width & not(AC_ManualPosition) then
	hotspot2 titleWidth, 0

	if bInvert then
		hotspot2 0,-titleHeight
	else
		hotspot2 0,titleHeight
	endif
else
	posMod = (not(bInside) - bInside) * 2 * gs_offset - bInside * titleThickness

	hotspot2  posMod,					0,	hID,	gs_title_width, 1 + 128
	hotspot2 -1,						0,	hID+1,	gs_title_width, 3
	hotspot2  posMod + gs_title_width,	0,	hID+2,	gs_title_width, 2
	hID = hID + 3

	if bInvert then
		hotspot2 0, -posMod,					hID,	gs_title_height, 1 + 128
		hotspot2 0, -1,							hID+1,	gs_title_height, 3
		hotspot2 0, -posMod - gs_title_height,	hID+2,	gs_title_height, 2
		hID = hID + 3
	else
		hotspot2 0, posMod,						hID,	gs_title_height, 1 + 128
		hotspot2 0,-1,							hID+1,	gs_title_height, 3
		hotspot2 0, posMod + gs_title_height,	hID+2,	gs_title_height, 2
		hID = hID + 3
	endif
endif


! Data =========================================================================

posX =  titleWidth
posY = -iInvert * drawingNameFontH
gosub 200	! Drawing Name

posX =  titleWidth
posY = -iInvert * sourceFileNameFontH
gosub 203	! Drawing Path

posX =  titleWidth
posY = -iInvert * projectFileNameFontH
gosub 205	! File Path

posX =  titleWidth
posY = -iInvert * layoutNameFontH
gosub 206	! Layout Name


rot2 90
posX = iInvert * titleHeight
posY = drawingScaleFontH
gosub 201	! Drawing Scale

posX = iInvert * titleHeight
posY = drawingMagnFontH
gosub 204	! Magnification
del 1

! ==============================================================================

del 3 + nExtratransformations

! ==============================================================================

end



100:
! ==============================================================================
! Drawing title marker symbol
! ==============================================================================
! in:
!	titleWidth:
!	titleHeight:
! out:
!	upperLineDx:
!	middleLineDx:
!	bottomLineDx
! ==============================================================================

	if not(gs_show_symbol) then		! Off
		upperLineDx		= 0
		middleLineDx	= 0
		bottomLineDx	= 0
	else
		pen gs_symbol_pen
		fill gs_symbol_fill

		poly2_b 4, 7, gs_symbol_fill_fg, gs_symbol_fill_bg,
			titleThickness,	titleThickness,	0,
			titleThickness,	0,				0,
			0,				0,				0,
			0,				titleThickness,	0

		upperLineDx		= titleHeight
		middleLineDx	= titleHeight
		bottomLineDx	= titleHeight
	endif
return



200:
! ==============================================================================
! Text Drawing Name
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_name then

		set style "styleDrawingName"
		pen gs_drawing_name_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_name_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_name_pos_y") then
			textWidth	= stw(drawingName) * PAPER_TO_MODEL
			textHeight	= drawingNameFontH
			textAnchor	= anchorDrawingName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_drawing_name_pos_x,				posY + gs_drawing_name_pos_y,
				  posX + gs_drawing_name_pos_x + textWidth,	posY + gs_drawing_name_pos_y + textHeight
			del 1
		else
			text2 posX + gs_drawing_name_pos_x, posY + gs_drawing_name_pos_y, drawingName
		endif

		hotspot2 posX,							posY + gs_drawing_name_pos_y,	hID,	gs_drawing_name_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_drawing_name_pos_y,	hID+1,	gs_drawing_name_pos_x, 3
		hotspot2 posX + gs_drawing_name_pos_x,	posY + gs_drawing_name_pos_y,	hID+2,	gs_drawing_name_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_drawing_name_pos_x,	posY,							hID,	gs_drawing_name_pos_y, 1+128
		hotspot2 posX + gs_drawing_name_pos_x,	posY - 1,						hID+1,	gs_drawing_name_pos_y, 3
		hotspot2 posX + gs_drawing_name_pos_x,	posY + gs_drawing_name_pos_y,	hID+2,	gs_drawing_name_pos_y, 2
		hID = hID + 3
	endif
return

201:
! ==============================================================================
! Text Drawing Scale
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_scale then

		set style "styleDrawingScale"
		pen gs_drawing_scale_font_pen

		textWidth	= stw(drawingScale) * PAPER_TO_MODEL
		textHeight	= drawingScaleFontH

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_scale_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_scale_pos_y") then
			textAnchor	= anchorDrawingScale
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + iInvert * gs_drawing_scale_pos_x,				posY + gs_drawing_scale_pos_y,
				  posX + iInvert * gs_drawing_scale_pos_x + textWidth,	posY + gs_drawing_scale_pos_y + textHeight
			del 1
		else
			add2 posX + iInvert * gs_drawing_scale_pos_x, posY + gs_drawing_scale_pos_y
			add2 -iInvert * textWidth/2, -textHeight / 2
			if (gs_position_side_int = POS_RIGHT | gs_position_side_int = POS_LEFT) then
				rot2 180
			else
				rot2 0
			endif
			text2 0, 0, drawingScale

			del 3
		endif

		hotspot2 posX,										posY + gs_drawing_scale_pos_y,	hID,	gs_drawing_scale_pos_x, 1+128
		hotspot2 posX - iInvert,							posY + gs_drawing_scale_pos_y,	hID+1,	gs_drawing_scale_pos_x, 3
		hotspot2 posX + iInvert * gs_drawing_scale_pos_x,	posY + gs_drawing_scale_pos_y,	hID+2,	gs_drawing_scale_pos_x, 2
		hID = hID + 3

		hotspot2 posX + iInvert * gs_drawing_scale_pos_x,	posY,							hID,	gs_drawing_scale_pos_y, 1+128
		hotspot2 posX + iInvert * gs_drawing_scale_pos_x,	posY - 1,						hID+1,	gs_drawing_scale_pos_y, 3
		hotspot2 posX + iInvert * gs_drawing_scale_pos_x,	posY + gs_drawing_scale_pos_y,	hID+2,	gs_drawing_scale_pos_y, 2
		hID = hID + 3
	endif
return

202:
! ==============================================================================
! Text Drawing Number
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_number then

		set style "styleDrawingNumber"
		pen gs_drawing_number_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_number_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_number_pos_y") then
			textWidth	= stw(drawingNumber) * PAPER_TO_MODEL
			textHeight	= drawingNumberFontH
			textAnchor	= anchorDrawingNumber
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_drawing_number_pos_x,				posY + gs_drawing_number_pos_y,
				  posX + gs_drawing_number_pos_x + textWidth,	posY + gs_drawing_number_pos_y + textHeight
			del 1
		else
			text2 posX + gs_drawing_number_pos_x, posY + gs_drawing_number_pos_y, drawingNumber
		endif

		hotspot2 posX,								posY + gs_drawing_number_pos_y,	hID,	gs_drawing_number_pos_x, 1+128
		hotspot2 posX - 1,							posY + gs_drawing_number_pos_y,	hID+1,	gs_drawing_number_pos_x, 3
		hotspot2 posX + gs_drawing_number_pos_x,	posY + gs_drawing_number_pos_y,	hID+2,	gs_drawing_number_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_drawing_number_pos_x,	posY,							hID,	gs_drawing_number_pos_y, 1+128
		hotspot2 posX + gs_drawing_number_pos_x,	posY - 1,						hID+1,	gs_drawing_number_pos_y, 3
		hotspot2 posX + gs_drawing_number_pos_x,	posY + gs_drawing_number_pos_y,	hID+2,	gs_drawing_number_pos_y, 2
		hID = hID + 3
	endif
return

203:
! ==============================================================================
! Text Drawing Path
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_source_filename then

		set style "styleSourceFileName"
		pen gs_source_filename_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_source_filename_pos_x" | gs_GLOB_MODPAR_NAME = "gs_source_filename_pos_y") then
			textWidth	= stw(drawingPath) * PAPER_TO_MODEL
			textHeight	= sourceFileNameFontH
			textAnchor	= anchorSourceFileName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_source_filename_pos_x,				posY + gs_source_filename_pos_y,
				  posX + gs_source_filename_pos_x + textWidth,	posY + gs_source_filename_pos_y + textHeight
			del 1
		else
			text2 posX + gs_source_filename_pos_x, posY + gs_source_filename_pos_y, drawingPath
		endif

		hotspot2 posX,								posY + gs_source_filename_pos_y,	hID,	gs_source_filename_pos_x, 1+128
		hotspot2 posX - 1,							posY + gs_source_filename_pos_y,	hID+1,	gs_source_filename_pos_x, 3
		hotspot2 posX + gs_source_filename_pos_x,	posY + gs_source_filename_pos_y,	hID+2,	gs_source_filename_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_source_filename_pos_x,	posY,								hID,	gs_source_filename_pos_y, 1+128
		hotspot2 posX + gs_source_filename_pos_x,	posY - 1,							hID+1,	gs_source_filename_pos_y, 3
		hotspot2 posX + gs_source_filename_pos_x,	posY + gs_source_filename_pos_y,	hID+2,	gs_source_filename_pos_y, 2
		hID = hID + 3
	endif
return

204:
! ==============================================================================
! Text Magnification
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_drawing_magnification then

		set style "styleDrawingMagnification"
		pen gs_drawing_magn_font_pen

		textWidth	= stw(drawingMagnification) * PAPER_TO_MODEL
		textHeight	= drawingMagnFontH

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_drawing_magn_pos_x" | gs_GLOB_MODPAR_NAME = "gs_drawing_magn_pos_y") then
			textAnchor	= anchorDrawingMagnification
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + iInvert * gs_drawing_magn_pos_x,				posY + gs_drawing_magn_pos_y,
				  posX + iInvert * gs_drawing_magn_pos_x + textWidth,	posY + gs_drawing_magn_pos_y + textHeight
			del 1
		else
			add2 posX + iInvert * gs_drawing_magn_pos_x, posY + gs_drawing_magn_pos_y
			add2 -iInvert * textWidth/2, -textHeight / 2
			if (gs_position_side_int = POS_RIGHT | gs_position_side_int = POS_LEFT) then
				rot2 180
			else
				rot2 0
			endif

			text2 0, 0, drawingMagnification
			del 3
		endif

		hotspot2 posX,										posY + gs_drawing_magn_pos_y,	hID,	gs_drawing_magn_pos_x, 1+128
		hotspot2 posX - iInvert,							posY + gs_drawing_magn_pos_y,	hID+1,	gs_drawing_magn_pos_x, 3
		hotspot2 posX + iInvert * gs_drawing_magn_pos_x,	posY + gs_drawing_magn_pos_y,	hID+2,	gs_drawing_magn_pos_x, 2
		hID = hID + 3

		hotspot2 posX + iInvert * gs_drawing_magn_pos_x,	posY,							hID,	gs_drawing_magn_pos_y, 1+128
		hotspot2 posX + iInvert * gs_drawing_magn_pos_x,	posY - 1,						hID+1,	gs_drawing_magn_pos_y, 3
		hotspot2 posX + iInvert * gs_drawing_magn_pos_x,	posY + gs_drawing_magn_pos_y,	hID+2,	gs_drawing_magn_pos_y, 2
		hID = hID + 3
	endif
return

205:
! ==============================================================================
! Text File Path
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_project_filename then

		set style "styleProjectFilename"
		pen gs_project_filename_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_project_filename_pos_x" | gs_GLOB_MODPAR_NAME = "gs_project_filename_pos_y") then
			textWidth	= stw(filePath) * PAPER_TO_MODEL
			textHeight	= projectFileNameFontH
			textAnchor	= anchorProjectFileName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_project_filename_pos_x,				posY + gs_project_filename_pos_y,
				  posX + gs_project_filename_pos_x + textWidth,	posY + gs_project_filename_pos_y + textHeight
			del 1
		else
			text2 posX + gs_project_filename_pos_x, posY + gs_project_filename_pos_y, filePath
		endif

		hotspot2 posX,								posY + gs_project_filename_pos_y,	hID,	gs_project_filename_pos_x, 1+128
		hotspot2 posX - 1,							posY + gs_project_filename_pos_y,	hID+1,	gs_project_filename_pos_x, 3
		hotspot2 posX + gs_project_filename_pos_x,	posY + gs_project_filename_pos_y,	hID+2,	gs_project_filename_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_project_filename_pos_x,	posY,								hID,	gs_project_filename_pos_y, 1+128
		hotspot2 posX + gs_project_filename_pos_x,	posY - 1,							hID+1,	gs_project_filename_pos_y, 3
		hotspot2 posX + gs_project_filename_pos_x,	posY + gs_project_filename_pos_y,	hID+2,	gs_project_filename_pos_y, 2
		hID = hID + 3
	endif
return

206:
! ==============================================================================
! Text Layout Name
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_layout_name then

		set style "styleLayoutName"
		pen gs_layout_name_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_layout_name_pos_x" | gs_GLOB_MODPAR_NAME = "gs_layout_name_pos_y") then
			textWidth	= stw(layoutName) * PAPER_TO_MODEL
			textHeight	= layoutNameFontH
			textAnchor	= anchorLayoutName
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_layout_name_pos_x,				posY + gs_layout_name_pos_y,
				  posX + gs_layout_name_pos_x + textWidth,	posY + gs_layout_name_pos_y + textHeight
			del 1
		else
			text2 posX + gs_layout_name_pos_x, posY + gs_layout_name_pos_y, layoutName
		endif

		hotspot2 posX,							posY + gs_layout_name_pos_y,	hID,	gs_layout_name_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_layout_name_pos_y,	hID+1,	gs_layout_name_pos_x, 3
		hotspot2 posX + gs_layout_name_pos_x,	posY + gs_layout_name_pos_y,	hID+2,	gs_layout_name_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_layout_name_pos_x,	posY,							hID,	gs_layout_name_pos_y, 1+128
		hotspot2 posX + gs_layout_name_pos_x,	posY - 1,						hID+1,	gs_layout_name_pos_y, 3
		hotspot2 posX + gs_layout_name_pos_x,	posY + gs_layout_name_pos_y,	hID+2,	gs_layout_name_pos_y, 2
		hID = hID + 3
	endif
return

207:
! ==============================================================================
! Text Layout Number
! ==============================================================================
! in:
!	posX:	position X
!	posY:	position Y
! ==============================================================================

	if gs_show_layout_number then

		set style "styleLayoutNumber"
		pen gs_layout_number_font_pen

		if (GLOB_CONTEXT = 22 | GLOB_CONTEXT = 28) & (gs_GLOB_MODPAR_NAME = "gs_layout_number_pos_x" | gs_GLOB_MODPAR_NAME = "gs_layout_number_pos_y") then
			textWidth	= stw(layoutNumber) * PAPER_TO_MODEL
			textHeight	= layoutNumberFontH
			textAnchor	= anchorLayoutNumber
			gosub 300

			add2 anchorX, anchorY

			rect2 posX + gs_layout_number_pos_x,				posY + gs_layout_number_pos_y,
				  posX + gs_layout_number_pos_x + textWidth,	posY + gs_layout_number_pos_y + textHeight
			del 1
		else
			text2 posX + gs_layout_number_pos_x, posY + gs_layout_number_pos_y, layoutNumber
		endif

		hotspot2 posX,							posY + gs_layout_number_pos_y,	hID,	gs_layout_number_pos_x, 1+128
		hotspot2 posX - 1,						posY + gs_layout_number_pos_y,	hID+1,	gs_layout_number_pos_x, 3
		hotspot2 posX + gs_layout_number_pos_x,	posY + gs_layout_number_pos_y,	hID+2,	gs_layout_number_pos_x, 2
		hID = hID + 3

		hotspot2 posX + gs_layout_number_pos_x,	posY,							hID,	gs_layout_number_pos_y, 1+128
		hotspot2 posX + gs_layout_number_pos_x,	posY - 1,						hID+1,	gs_layout_number_pos_y, 3
		hotspot2 posX + gs_layout_number_pos_x,	posY + gs_layout_number_pos_y,	hID+2,	gs_layout_number_pos_y, 2
		hID = hID + 3
	endif
return


300:
! ==============================================================================
! Text anchor relative position
! ==============================================================================
! in:
!	textWidth:		width of the text
!	textHeight:		height of the text
!	textAnchor:		text anchor code
! out:
!	anchorY:		anchor relative position X
!	anchorY:		anchor relative position Y
! ==============================================================================

	if textAnchor = 1 | textAnchor = 4 | textAnchor = 7 then
		anchorX = 0
	endif
	if textAnchor = 2 | textAnchor = 5 | textAnchor = 8 then
		anchorX = -textWidth / 2
	endif
	if textAnchor = 3 | textAnchor = 6 | textAnchor = 9 then
		anchorX = -textWidth
	endif
	if textAnchor = 1 | textAnchor = 2 | textAnchor = 3 then
		anchorY = -textHeight
	endif
	if textAnchor = 4 | textAnchor = 5 | textAnchor = 6 then
		anchorY = -textHeight / 2
	endif
	if textAnchor = 7 | textAnchor = 8 | textAnchor = 9 then
		anchorY = 0
	endif
return


