
hId = 1

lh = AC_MarkerSize * PAPER_TO_MODEL / 2		! Line Height

! ==============================================================================
! Text Style Definitions
! ==============================================================================

define style  "innerStyleCenter" AC_TextFont_1,
								AC_textSize_1,
								5,
								AC_textStyle_1

set style "innerStyleCenter"


! ==============================================================================
! Inner Text Content
! ==============================================================================

if AC_MarkerText_1 = "" then AC_MarkerText_1 = "-"
if AC_MarkerText_2 = "" then AC_MarkerText_2 = "-"

iNumItems =	gs_inner_text_ie_id + \
			gs_inner_text_ie_name + \
			(gs_inner_text_int = 1) * gs_inner_text_layout_id + \	! Automatic
			(gs_inner_text_int = 2) * (gs_custom_inner_text <> "")	! Custom

dim sInnerText[3]
	sInnerText[1] = ""
	sInnerText[2] = ""
	sInnerText[3] = ""

j = 1

if gs_inner_text_ie_id then
	sInnerText[j] = AC_MarkerText_1
	j = j + 1
endif

if gs_inner_text_ie_name then
	sInnerText[j] = AC_MarkerText_2
	j = j + 1
endif

if gs_inner_text_int = 1 then
	! Automatic

	if gs_inner_text_layout_id then
		sInnerText[j] = "<MARKERSHEETNUMBER_R>" + AC_DrawingGUID_1
		j = j + 1
	endif
else
	! Custom

	if gs_custom_inner_text <> "" then
		sInnerText[j] = gs_custom_inner_text
		j = j + 1
	endif
endif

hotspot2 0,						0,	hID,	gs_marker_half_width, 1 + 128
hotspot2 -1,					0,	hID+1,	gs_marker_half_width, 3
hotspot2 gs_marker_half_width,	0,	hID+2,	gs_marker_half_width, 2
hID = hID + 3

width = max(gs_marker_width, width)


! ==============================================================================
! Marker Drawing
! ==============================================================================

height	= iNumItems * lh

hotspot2   0,		 0,			hId: hId = hId + 1
hotspot2  -width/2, -height/2,	hId: hId = hId + 1
hotspot2  -width/2,  height/2,	hId: hId = hId + 1
hotspot2   width/2, -height/2,	hId: hId = hId + 1
hotspot2   width/2,  height/2,	hId: hId = hId + 1


! ==============================================================================

fillFrame	= 5 + 2 * gs_backfill	! 5 / 7
fillType	= gs_backfill_type
fillPen		= gs_backfill_pen_fg
fillBackPen	= gs_backfill_pen_bg

fill fillType
pen SYMB_VIEW_PEN

if gs_marker_style = 1 then
	vertLines	= 1
	horLines	= 1
	sepLines	= 1
endif

if gs_marker_style = 2 then
	vertLines	= 0
	horLines	= 1
	sepLines	= 1
endif

if gs_marker_style = 3 then
	vertLines	= 0
	horLines	= 0
	sepLines	= 1
endif

if gs_marker_style = 4 then
	vertLines	= 0
	horLines	= 0
	sepLines	= 0
endif

poly2_b 4, fillFrame, fillPen, fillBackPen,
	-width/2, -height/2,	vertLines,
	-width/2,  height/2,	horLines,
	 width/2,  height/2,	vertLines,
	 width/2, -height/2,	horLines

if sepLines then
	for i=1 to iNumItems - 1
		line2 -width/2, -height/2 + i * lh,
			   width/2, -height/2 + i * lh
	next i
endif

add2 0, height/2 - lh/2

pen AC_TextPen_1

if iNumItems > 0 then text2 0, 0,		sInnerText[1]
if iNumItems > 1 then text2 0,-lh,		sInnerText[2]
if iNumItems > 2 then text2 0,-2 * lh,	sInnerText[3]

del 1

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ==============================================================================
