
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "628844EF-60BA-413A-9997-1DA605C144C2"
	_startID	= "05CF41C7-5B3A-4855-997B-7D5E252FC3B9"
	_endID		= "3EE66C5C-19F4-4952-B91D-08DC9E654AF3"
gosub "NewRequestForProperties_FWM"

	_startID	= "3EE66C5C-19F4-4952-B91D-08DC9E654AF3"
	_endID		= "76EF2BD6-E3D3-4F63-8147-951165F7199F"
gosub "NewUnitSizesOption_FWM"

	_startID	= "76EF2BD6-E3D3-4F63-8147-951165F7199F"
	_endID		= "68D515DA-5447-4023-B8FA-51573681E4FE"
gosub "TnCShowCatOnly_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! NewRequestForProperties
! ==============================================================================
"NewRequestForProperties_FWM":
	if actualGUID = _startID | actualGUID = _autoID then

		_bUseOldLongName	= 0
		_iOldDisplayData	= 3
		bHas = DELETED_PAR_VALUE ("bDisplayPropertySetName", _bUseOldLongName) & \
				STORED_PAR_VALUE ("iTncDisplayData", _iOldDisplayData)

		_iNewDisplayData = _iOldDisplayData

		if _bUseOldLongName then
			if _iOldDisplayData = 1 then		! DISPLAYDATABY_NAMEVALUE
				_iNewDisplayData = 5			! DISPLAYDATABY_GROUPNAMEVALUE
			else
				if _iOldDisplayData = 2 then	! DISPLAYDATABY_NAME then
					_iNewDisplayData = 4		! DISPLAYDATABY_GROUPNAME
				endif
			endif
		endif

		iTncDisplayData = _iNewDisplayData
		parameters iTncDisplayData = iTncDisplayData

		actualGUID = _endID
	endif

return

! ==============================================================================
! NewUnitSizesOption
! ==============================================================================
"NewUnitSizesOption_FWM":
	if actualGUID = _startID then
		iWorkUnitLinear		= 10	! DIMUNIT_PROJECT
		iWorkUnitSillHgt	= 10	! DIMUNIT_PROJECT
	
		iWorkUnitLinearDecimal	= 1
		iWorkUnitSillHgtDecimal	= 1
	
		parameters	iWorkUnitLinear			= iWorkUnitLinear,
					iWorkUnitSillHgt		= iWorkUnitSillHgt,
					iWorkUnitLinearDecimal	= iWorkUnitLinearDecimal,
					iWorkUnitSillHgtDecimal	= iWorkUnitSillHgtDecimal
	
		actualGUID = _endID
	endif
return

! ==============================================================================
! TnCShowCatOnly
! ==============================================================================
"TnCShowCatOnly_FWM":
	if actualGUID = _startID then
		_prevDisplayContent = 1
        DETLEV_SCALESENS	= 1
		DETLEV_DETAILED		= 2
		DETLEV_SIMPLE		= 3
		bHas =  STORED_PAR_VALUE ("iTnCSelectParams", _prevDisplayContent)                          &\
                STORED_PAR_VALUE ("iDetLevel", _iDetLevel)                                          &\
                STORED_PAR_VALUE ("bShowAdditionalTags_detailed", _bShowAdditionalTags_detailed)    &\
                STORED_PAR_VALUE ("bShowAdditionalTags_simple", _bShowAdditionalTags_simple)
	
		if _prevDisplayContent = 2              &\		! PARAMSELECTION_DISPLAYALL
            ((_iDetLevel = DETLEV_SCALESENS     &\
            (_bShowAdditionalTags_detailed = 1  |\
            _bShowAdditionalTags_simple = 1))   |\
            (_iDetLevel = DETLEV_DETAILED       &\
            _bShowAdditionalTags_detailed = 1)  |\
            (_iDetLevel = DETLEV_SIMPLE         &\
            _bShowAdditionalTags_simple = 1))   then
            
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
	
return

