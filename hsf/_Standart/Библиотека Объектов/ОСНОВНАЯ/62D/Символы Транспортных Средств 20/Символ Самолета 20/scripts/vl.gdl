
call "FM_types" parameters all

if gs_cont_pen = 0 then gs_cont_pen = 1
parameters gs_cont_pen = gs_cont_pen

! -----------------------------------------------------------------------------
! Dimension limits
! -----------------------------------------------------------------------------
if iElevtyp = 1 then
	values "a" range [20, )
	values "b" range [15, )
endif
if iElevtyp = 2 then
	values "a" range [20, )
	values "b" range [5, )
endif
if iElevtyp = 3 then
	values "a" range [15, )
	values "b" range [5, )
endif

! -----------------------------------------------------------------------------
! Elevation types
! -----------------------------------------------------------------------------

values "elevtyp" stElevTypes
values "iElevTyp" 1, 2, 3

if GLOB_MODPAR_NAME = "elevtyp" then
	iElevTyp = 1
	if elevtyp = stElevTypes[2] then iElevTyp = 2
	if elevtyp = stElevTypes[3] then iElevTyp = 3
	parameters iElevTyp = iElevTyp
else
	elevtyp = stElevTypes[1]
	if iElevTyp = 2 then elevtyp = stElevTypes[2]
	if iElevTyp = 3 then elevtyp = stElevTypes[3]
	parameters elevtyp = elevtyp
endif

! -----------------------------------------------------------------------------
! Symbol types
! -----------------------------------------------------------------------------

values "stSymbolType" stSymbolTypes
values "iSymbolType" 1, 2, 3

if GLOB_MODPAR_NAME = "stSymbolType" then
	iSymbolType = 1
	if stSymbolType = stSymbolTypes[2] then iSymbolType = 2
	parameters iSymbolType = iSymbolType
else
	stSymbolType = stSymbolTypes[1]
	if iSymbolType = 2 then stSymbolType = stSymbolTypes[2]
	parameters stSymbolType = stSymbolType
endif

! -----------------------------------------------------------------------------
! Deafault sizes
! -----------------------------------------------------------------------------

if GLOB_MODPAR_NAME = "iElevTyp" or GLOB_MODPAR_NAME = "elevtyp" or \
	GLOB_MODPAR_NAME = "stSymbolType" or GLOB_MODPAR_NAME = "iSymbolType" then
	if iElevtyp = 1 then
		a = defaultSizes[iSymbolType][1] : b = defaultSizes[iSymbolType][2]
	endif
	if iElevtyp = 2 then
		a = defaultSizes[iSymbolType][1] : b = defaultSizes[iSymbolType][3]
	endif
	if iElevtyp = 3 then
		a = defaultSizes[iSymbolType][2] : b = defaultSizes[iSymbolType][3]
	endif
endif

parameters a = a
parameters b = b

! -----------------------------------------------------------------------------
! UI
! -----------------------------------------------------------------------------

values "gs_ui_current_page" 1

