
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "88B13381-9B2F-42B8-B93B-32DBEFEF6AC2"
	_startID	= "14EF86EE-A240-4E6D-B57E-8EB945DBA9D9"
	_endID		= "E120A57A-0ADC-40B9-8EFA-2B38DFB131FA"
gosub "NewRequestForProperties_FWM"

	_startID	= "E120A57A-0ADC-40B9-8EFA-2B38DFB131FA"
	_endID		= "521D3F0A-D0B0-47EF-9A67-49F993497DE9"
gosub "TnCShowCatOnly_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! NewRequestForProperties
! ==============================================================================
"NewRequestForProperties_FWM":
	if actualGUID = _startID | actualGUID = _autoID then

		_bUseOldLongName	= 0
		_iOldDisplayData	= 3
		bHas = DELETED_PAR_VALUE ("bDisplayPropertySetName", _bUseOldLongName) & \
				STORED_PAR_VALUE ("iTncDisplayData", _iOldDisplayData)

		_iNewDisplayData = _iOldDisplayData

		if _bUseOldLongName then
			if _iOldDisplayData = 1 then		! DISPLAYDATABY_NAMEVALUE
				_iNewDisplayData = 5			! DISPLAYDATABY_GROUPNAMEVALUE
			else
				if _iOldDisplayData = 2 then	! DISPLAYDATABY_NAME then
					_iNewDisplayData = 4		! DISPLAYDATABY_GROUPNAME
				endif
			endif
		endif

		iTncDisplayData = _iNewDisplayData
		parameters iTncDisplayData = iTncDisplayData

		actualGUID = _endID
	endif
return

! ==============================================================================
! TnCShowCatOnly
! ==============================================================================
"TnCShowCatOnly_FWM":
	if actualGUID = _startID then
		_prevDisplayContent = 1
		bHas = STORED_PAR_VALUE ("iTnCSelectParams", _prevDisplayContent)
	
		if _prevDisplayContent = 2 then		! PARAMSELECTION_DISPLAYALL
			actualGUID = ""
		else
			actualGUID = _endID
		endif
	endif
	
return

