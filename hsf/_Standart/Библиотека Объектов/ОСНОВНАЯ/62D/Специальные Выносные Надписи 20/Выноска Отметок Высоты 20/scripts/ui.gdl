
! ==============================================================================
! Placeholder if there is not valid content for parent tool
! ==============================================================================

if not(_isEnabledContent) then
	gosub "callPlaceholder"
	END
endif


! ==============================================================================
! Tabpage IDs, Titles and Icons
! ==============================================================================

PAGE_ID_CONTENT		= 1
PAGE_ID_GENERAL		= 2
PAGE_ID_INFORMATION	= 3

dim tabTitles[3]
	tabTitles[1] = `Данные Выносной Надписи`
	tabTitles[2] = `Общие Параметры`
	tabTitles[3] = `Информация`

dim tabIcons[3]
	tabIcons[1] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[2] = "uiTab_areaContent_flat(1)"
	tabIcons[3] = "uiTab_Info_flat(1)"

file_dependence "uiTab_MarkerLabelContent_flat(1)"
file_dependence "uiTab_areaContent_flat(1)"
file_dependence "uiTab_Info_flat(1)"
file_dependence "ui_InfoIcon_flat(1)"


ui_current_page gs_ui_current_page
ui_page gs_ui_current_page

! ==============================================================================
! Call tabpage selector
! ==============================================================================
call "ui_tab_popup" parameters 	nTabs 				= vardim1(tabTitles),
								gs_ui_current_page 	= gs_ui_current_page,
								tabTitles			= tabTitles,
								tabIcons			= tabIcons


! ==============================================================================
! Tabpages
! ==============================================================================

ui_page PAGE_ID_CONTENT
	gosub "ContentSettingsPage"

ui_page PAGE_ID_GENERAL
	gosub "TableSettingsPage"

ui_page PAGE_ID_INFORMATION
	gosub "callPlaceholder"


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"ContentSettingsPage":
! ------------------------------------------------------------------------------
	inFieldHeight = 19
	outFieldHeight = 15

	inFieldWidth1 = 140

	x1 = 1
	x2 = 210
	x3 = x2 + inFieldWidth1

	yCurr = 40

	dy = 23

	ui_style 0,1
	ui_outfield `Этаж Привязки`,	x1, yCurr, x2-x1-1, outFieldHeight
	ui_style 0,0
	ui_infield{3} "iRefStory", x2, yCurr-4, x3-x2, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", _stRefStory[REF_STORY_CURR], REF_STORY_CURR,
			"", _stRefStory[REF_STORY_PROJECT], REF_STORY_PROJECT,
			"", _stRefStory[3], 3,"", _stRefStory[4], 4,"", _stRefStory[5], 5,"", _stRefStory[6], 6,"", _stRefStory[7], 7

	yCurr = yCurr + dy

	ui_separator 0, yCurr, 443, yCurr
	yCurr = yCurr + dy/3 * 2

	if not(bShowElevTop) then
		_hiddenFlag = 4
	else
		_hiddenFlag = 0
	endif

	ui_style 0,1
	ui_infield{3} "bShowElevTop",	x1, yCurr-4, x2-x1-1, inFieldHeight,
			7, "", 3, 1, 0, 0, 0, 0,
			"", `Верхняя Отметка`, 1,
			"", `Верхняя Отметка`, 0
	ui_style 0,0
	ui_infield{3} "iElevationTopMeasuredTo", x2, yCurr-4, x3-x2, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", _sElevationMeasuredTo[ELEV_MESURED_FINISH], ELEV_MESURED_FINISH,
			"", _sElevationMeasuredTo[ELEV_MESURED_CORE], ELEV_MESURED_CORE
	yCurr = yCurr + dy

	ui_outfield `Название Параметра`,	x1, yCurr, x2-x1-1, outFieldHeight, _hiddenFlag
	ui_infield "sDescriptionElevTop", x2, yCurr-4, x3-x2, inFieldHeight
	yCurr = yCurr + dy

	ui_outfield `Суффикс`,	x1, yCurr, x2-x1-1, outFieldHeight, _hiddenFlag
	ui_infield "strSuffixElevTop", x2, yCurr-4, x3-x2, inFieldHeight
	yCurr = yCurr + dy + dy/4 * 2

	if not(bSHowElevBottom) then
		_hiddenFlag = 4
	else
		_hiddenFlag = 0
	endif

	ui_style 0,1
	ui_infield{3} "bSHowElevBottom",	x1, yCurr-4, x2-x1-1, inFieldHeight,
			7, "", 3, 1, 0, 0, 0, 0,
			"", `Отметка Низа`, 1,
			"", `Отметка Низа`, 0
	ui_style 0,0
	ui_infield{3} "iElevationBottomMeasuredTo", x2, yCurr-4, x3-x2, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", _sElevationMeasuredTo[ELEV_MESURED_FINISH], ELEV_MESURED_FINISH,
			"", _sElevationMeasuredTo[ELEV_MESURED_CORE], ELEV_MESURED_CORE
	yCurr = yCurr + dy

	ui_outfield `Название Параметра`,	x1, yCurr, x2-x1-1, outFieldHeight, _hiddenFlag
	ui_infield "sDescriptionElevBottom", x2, yCurr-4, x3-x2, inFieldHeight
	yCurr = yCurr + dy

	ui_outfield `Суффикс`,	x1, yCurr, x2-x1-1, outFieldHeight, _hiddenFlag
	ui_infield "strSuffixElevBottom", x2, yCurr-4, x3-x2, inFieldHeight
	yCurr = yCurr + dy

	if iElevationTopMeasuredTo = ELEV_MESURED_CORE | iElevationBottomMeasuredTo = ELEV_MESURED_CORE then
		_stNote = `Вариант Ядро доступен только для Перекрытий. При ассоциировании с другими элементами в выноске всегда отображается значение Всего Элемента.`
		ui_pict "ui_InfoIcon_flat(1)", x1, yCurr, 14, 14, 1
		ui_outfield _stNote, 25, yCurr, 415, outFieldHeight*2
	endif
return


! ==============================================================================
"TableSettingsPage":
! ------------------------------------------------------------------------------
	! all drawing from macro
	! only the units are drawing from here

	_yBottomPosGeneral = 210

	call "label_table_form" parameters AC_CustomReadability = AC_CustomReadability,
		bUseBuiltinPointer			= not(LABEL_CUSTOM_ARROW),
		anglePosition 				= LABEL_ROTANGLE,
		bUsePrefixColumn			= bUsePrefixColumn & (sDescriptionElevTop <> "" or sDescriptionElevBottom <> ""),
		bUseContentColumn			= 1,

		bWrapText					= LABEL_TEXT_WRAP,
		widthTextBoxPrefix_mm		= widthTextBoxPrefix_mm,
		widthTextBoxPrefix_pt		= widthTextBoxPrefix_pt,
		widthTextBoxContent_mm		= widthTextBoxContent_mm,
		widthTextBoxContent_pt		= widthTextBoxContent_pt,

		bShowFrame					= LABEL_FRAME_ON,
		penFrame					= LABEL_ARROW_PEN,
		lineTypeFrame				= LABEL_ARROW_LINETYPE,
		iFrameStyle					= iFrameStyle,
		iReferencePoint				= iReferencePoint,
		iLabelOrientation 			= iLabelOrientation,
		idTab_HeaderStyle			= 0,
		idTab_FrameStyle			= 2,
		yBottomPosGeneral			= _yBottomPosGeneral,
		gs_ui_current_page			= gs_ui_current_page

	inFieldHeight = 19
	outFieldHeight = 15
	inFieldWidth1 = 160
	inFieldWidth2 = 100
	inFieldWidthContMini = 40
	inFieldWidthShort = 125

	yCurr = _yBottomPosGeneral + 5
	x1 = 1
	x2 = 130
	x3 = x2 + inFieldWidth1 + 8
	x4 = x3 + inFieldWidth2
	endx = 444

	ui_separator 0, _yBottomPosGeneral, 443, _yBottomPosGeneral
	yCurr = yCurr + 4

	ui_style 0,1
	ui_infield{3} "bUsePrefixColumn", x1, yCurr, endx-x1, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показать Столбец Названий Параметров`, 0,
			2, `Показать Столбец Названий Параметров`, 1
	ui_style 0,0
	yCurr = yCurr + dy

	ui_style 0,1
	ui_infield{3} "bShowUnit", x1, yCurr - 4,	x2-x1-1, 26,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Единицы Измерения`, 0,
			2, `Единицы Измерения`, 1
	ui_style 0,0

	ui_infield{3} "iWorkUnitLinear", x2, yCurr, inFieldWidth1, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", stUnitFormats[1], DIMUNIT_MM,
			"", stUnitFormats[2], DIMUNIT_CM,
			"", stUnitFormats[3], DIMUNIT_DM,
			"", stUnitFormats[4], DIMUNIT_METER,
			"", stUnitFormats[5], DIMUNIT_INCH,
			"", stUnitFormats[6], DIMUNIT_PROJECT

	ui_outfield `Десятичные`,  x3, yCurr  + 4,	inFieldWidth2, outFieldHeight, 4 * _bLockLinearUnitDecimal
	ui_infield{3} "iWorkUnitLinearDecimal", x4 , yCurr,	inFieldWidthContMini, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4

return


! =============================================================================
"callPlaceholder":
! -----------------------------------------------------------------------------
	! - Offered Tools: All except Object, Lamp, Window, Door, Wall, Column, Slab, Beam, Curtain Wall
	call "label_placeholder" parameters bitAvailableTool 	= 2+4+8+16+32+64+128+4096+8192,
										bEnableUIContent = _isEnabledContent

return
