
! ==============================================================================
! Placeholder if there is not valid content for parent tool
! ==============================================================================

if not(_isEnabledContent) then
	gosub "callPlaceholder"
	END
endif


! ==============================================================================
! Tabpage IDs, Titles and Icons
! ==============================================================================

PAGE_ID_GENERAL		= 1
PAGE_ID_GEOMETRY	= 2
PAGE_ID_INFORMATION	= 3

dim tabTitles[3]
	tabTitles[1] = `Общие Параметры`
	tabTitles[2] = `Геометрия Выноски`
	tabTitles[3] = `Информация`

dim tabIcons[3]
	tabIcons[1] = "uiTab_areaContent_flat(1)"
	tabIcons[2] = "uiTab_labelGeometry_flat(1)"
	tabIcons[3] = "uiTab_Info_flat(1)"

file_dependence "uiTab_areaContent_flat(1)"
file_dependence "uiTab_labelGeometry_flat(1)"
file_dependence "uiTab_Info_flat(1)"
file_dependence "ui_InfoIcon_flat(1)"

ui_current_page gs_ui_current_page
ui_page gs_ui_current_page

! ==============================================================================
! Call tabpage selector
! ==============================================================================
call "ui_tab_popup" parameters 	nTabs 				= vardim1(tabTitles),
								gs_ui_current_page 	= gs_ui_current_page,
								tabTitles			= tabTitles,
								tabIcons			= tabIcons
								

! ==============================================================================
! Tabpages
! ==============================================================================

ui_page PAGE_ID_GENERAL
	gosub "GeneralSettingsPage"

ui_page PAGE_ID_GEOMETRY
	gosub "HeadStyleAndGeometryPage"

ui_page PAGE_ID_INFORMATION
	gosub "callPlaceholder"


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"GeneralSettingsPage":
! ------------------------------------------------------------------------------

	inFieldHeight = 19
	outFieldHeight = 15

	inFieldWidth = 135
	inFieldWidthShort = 125

	x1 = 1
	x2 = 160
	x3 = x2 + inFieldWidth
	x4 = 305
	x5 = x4 + infieldWidthShort
	endx = 440

	yCurr = 35

	dy = 23

	ui_style 0,1
	ui_style 0,1
	ui_outfield `Покрытия Поверхности`,	x1, yCurr, x2-x1-1, outFieldHeight
	ui_style 0,0
	ui_infield{3} "iSurfaceToDisplay",		x2, yCurr, endx-x2, 30,
		2, "", 1, 1,
		35, 35, 23, 23,
		sAvailableMaterialPict, sAvailableMaterial, iAvailableMaterial

	yCurr = yCurr + dy + 30 - outFieldHeight

	if iSurfaceToDisplay = 99 then
		ui_outfield `Специальный Текст`,	x1, yCurr, x2-x1-1, outFieldHeight
		ui_infield "sCustomSurface", x2, yCurr-4, endx-x2, inFieldHeight
	endif
	yCurr = yCurr + dy

	ui_outfield `Префикс`,	x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield "sPrefixSurface", x2, yCurr-4, x3-x2, inFieldHeight
	yCurr = yCurr + dy

	ui_outfield `Суффикс`,	x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield "sSuffixSurface", x2, yCurr-4, x3-x2, inFieldHeight
	yCurr = yCurr + dy

	if (GLOB_ELEM_TYPE = ELEM_TYPE_WALL and WALL_CROSSSECTION_TYPE = 0) |\
		(GLOB_ELEM_TYPE = ELEM_TYPE_COLUMN and COLU_CROSSSECTION_TYPE = 0) |\
		(GLOB_ELEM_TYPE = ELEM_TYPE_BEAM and BEAM_CROSSSECTION_TYPE = 0)  then
	
		ui_pict "ui_InfoIcon_flat(1)", x1, yCurr, 14, 14, 1
		ui_outfield `Данные Покрытий недоступны для Сложных Профилей. Введите Специальный Текст.`, x1+15, yCurr, endx-x1-15, outFieldHeight

	endif
	yCurr = yCurr + dy

	ui_separator 0, yCurr, 443, yCurr
	yCurr = yCurr + dy/3

	ui_style 0,1
	ui_outfield `Ориентация`,	x1, yCurr, x2-x1-1, outFieldHeight
	ui_style 0,0
	ui_infield{3} "iLabelOrientation",		x2, yCurr, x3-x2, 40,
		2, "ui_label_orientation_flat(1)", 6, 2, ! UI_label_orientation.png
		35, 35, 32, 32,
		5, _stLabelOrientation[ORIENTATION_PARALLEL],	ORIENTATION_PARALLEL,
		2, _stLabelOrientation[ORIENTATION_VERTICAL],	ORIENTATION_VERTICAL,
		1, _stLabelOrientation[ORIENTATION_HORIZONTAL], ORIENTATION_HORIZONTAL,
		3, _stLabelOrientation[ORIENTATION_CUSTOM],		ORIENTATION_CUSTOM

	yCurr = yCurr + dy + 40-outFieldHeight

	ui_outfield `Точка Привязки Текста`, x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield{3} "iReferencePoint",	x2, yCurr-4, x3-x2, 45,
			3, "ui_anchorpoint_flat(1)", 9, 3, 41, 41, 41, 41, ! ui_anchorpoint_flat(1).png
			1, _stRefPoint[1], 1,
			2, _stRefPoint[2], 2,
			3, _stRefPoint[3], 3,
			4, _stRefPoint[4], 4,
			5, _stRefPoint[5], 5,
			6, _stRefPoint[6], 6,
			7, _stRefPoint[7], 7,
			8, _stRefPoint[8], 8,
			9, _stRefPoint[9], 9

return


! =============================================================================
"HeadStyleAndGeometryPage":
! -----------------------------------------------------------------------------
	call "label_marker_form" parameters AC_CustomReadability = AC_CustomReadability,
		bUseBuiltinPointer			= not(LABEL_CUSTOM_ARROW),

		! Position
		iReferencePoint				= iReferencePoint,
		iLabelOrientation 			= iLabelOrientation,

		! Paragraph Format
		bEnablePrefixColumn			= 0,
		bUsePrefixColumn			= 0,
		bWrapText					= LABEL_TEXT_WRAP,
		widthTextBoxContent_mm		= widthTextBoxContent_mm,
		widthTextBoxContent_pt		= widthTextBoxContent_pt,

		! Head apperance
		bEnableCustomHeaderStyle 	= 0,

		! Body skin list appearance
		bUseTextBoxOnLine			= 0,

		! Label Head Representation
		bEnableHeadSwitch			= 0,							! Enable to switch the Show Label Head ON-OFF
		bShowHead					= 1,
		iShapeHead					= iShapeHead,
		bFitFrameToText				= bFitFrameToText,
		widthHead_mm				= widthHead_mm,
		heightHead_mm				= heightHead_mm,
		bLinkSizes					= bLinkSizes,
		bShowHeadContour			= LABEL_FRAME_ON,
		bShowFill					= 1,
		penForeground				= LABEL_TEXT_BG_PEN,

		! Label Line Representation
		distanceLabelHead_mm		= 0,
		bEnableLeaderLine			= 0,
		iMyPage_Geometry 			= PAGE_ID_GEOMETRY,
		iMyPage_HeadStyle			= 0,
		iMyPage_CustomSkins			= 0

return


! =============================================================================
"callPlaceholder":
! -----------------------------------------------------------------------------
	! - Offered Tools: Wall, Column, Slab, Roof, Beam, Shell, Skylight, Mesh, Morph
	call "label_placeholder" parameters bitAvailableTool 	= 32+64+128+256+1024+4096+262144+1048576,
										bEnableUIContent = _isEnabledContent

return

