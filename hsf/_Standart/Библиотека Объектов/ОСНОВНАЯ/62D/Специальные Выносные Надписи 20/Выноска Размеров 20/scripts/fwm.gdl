
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_autoID		= "2FA1B77A-7CC3-4FC0-94DB-15DC8741A5B7"
	_startID	= "7E80BFAA-1B4B-4893-8CC7-0CED91BC9CD0"
	_endID		= "B28679B0-A899-46C9-AD75-476870A60D61"
gosub "NewOrderingOption_FWM"

	_startID	= "B28679B0-A899-46C9-AD75-476870A60D61"
	_endID		= "6B4579C5-B597-4985-B4AE-32538CC2AA10"
gosub "SurfaceAreaCorrections_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! NewOrderingOption
! ==============================================================================
"NewOrderingOption_FWM":
	if actualGUID = _startID | actualGUID = _autoID then
		bHas =	STORED_PAR_VALUE ("bShowLinearDimension",	_bCurrLinearDimension) & \
				STORED_PAR_VALUE ("bShowSurfaceArea",		_bCurrSurfaceArea) & \
				STORED_PAR_VALUE ("bShowProjectedArea",		_bCurrProjectedArea) & \
				STORED_PAR_VALUE ("bShowVolume",			_bCurrVolume)

		dim	iContentOrderDim[4]
			iContentOrderDim[1] = DIM_WDT
			iContentOrderDim[2] = DIM_LGT
			iContentOrderDim[3] = DIM_THK
			iContentOrderDim[4] = DIM_HGT

		dim functionOrder[]
			functionOrder[1] = CONTENT_DIM

		iRow = 1

		if _bCurrLinearDimension then
			functionOrder[iRow] = CONTENT_DIM
			iRow = iRow + 1
		endif

		if _bCurrSurfaceArea then
			functionOrder[iRow] = CONTENT_SURF
			iRow = iRow + 1
		endif

		if _bCurrProjectedArea then
			functionOrder[iRow] = CONTENT_AREA
			iRow = iRow + 1
		endif

		if _bCurrVolume then
			functionOrder[iRow] = CONTENT_VOL
			iRow = iRow + 1
		endif

		parameters	iContentOrderDim	= iContentOrderDim,
					functionOrder		= functionOrder

		actualGUID = _endID
	endif
return

! ==============================================================================
! SurfaceAreaCorrections
! ==============================================================================
"SurfaceAreaCorrections_FWM":
	if actualGUID = _startID then
		_bShowSurfaceArea = 0
		bHas =	STORED_PAR_VALUE ("bShowSurfaceArea",	_bShowSurfaceArea)

		if _bShowSurfaceArea then
			if	GLOB_ELEM_TYPE = ELEM_TYPE_FILL | \
				GLOB_ELEM_TYPE = ELEM_TYPE_MORPH then

				iValueSumSurface = SUMSURFACE_ALL
				parameters iValueSumSurface = iValueSumSurface
			endif
		endif

		actualGUID = _endID
	endif
return

