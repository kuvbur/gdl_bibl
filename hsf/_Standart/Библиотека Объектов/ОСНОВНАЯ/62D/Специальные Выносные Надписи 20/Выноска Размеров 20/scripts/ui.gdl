
! ==============================================================================
! Placeholder if there is not valid content for parent tool
! ==============================================================================

if not(_isEnabledContent) then
	gosub "callPlaceholder"
	END
endif


! ==============================================================================
! Tabpage IDs, Titles and Icons
! ==============================================================================

PAGE_ID_CONTENT		= 1
PAGE_ID_DISPORDER	= 2
PAGE_ID_GENERAL		= 3
PAGE_ID_GEOMETRY	= 4
PAGE_ID_INFORMATION	= 5

dim tabTitles[5]
	tabTitles[1] = `Настройка Данных`
	tabTitles[2] = `Порядок Показа`
	tabTitles[3] = `Общие Параметры`
	tabTitles[4] = `Геометрия Выноски`
	tabTitles[5] = `Информация`

dim tabIcons[5]
	tabIcons[1] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[2] = "uiTab_ContentOrder_flat(1)"
	tabIcons[3] = "uiTab_areaContent_flat(1)"
	tabIcons[4] = "uiTab_labelGeometry_flat(1)"
	tabIcons[5] = "uiTab_Info_flat(1)"

file_dependence "uiTab_MarkerLabelContent_flat(1)"
file_dependence "uiTab_ContentOrder_flat(1)"
file_dependence "uiTab_areaContent_flat(1)"
file_dependence "uiTab_labelGeometry_flat(1)"
file_dependence "uiTab_Info_flat(1)"
file_dependence "ui_InfoIcon_flat(1)"
file_dependence "ui_warningIcon_flat(1)"
file_dependence "ui_list_contentSetup_flat(1)"

file_dependence "ui_toolpict_wall_flat(1)"	
file_dependence "ui_toolpict_door_flat(1)"	
file_dependence "ui_toolpict_window_flat(1)"	
file_dependence "ui_toolpict_column_flat(1)"	
file_dependence "ui_toolpict_beam_flat(1)"	
file_dependence "ui_toolpict_slab_flat(1)"	
file_dependence "ui_toolpict_roof_flat(1)"	
file_dependence "ui_toolpict_fill_flat(1)"	
file_dependence "ui_toolpict_shell_flat(1)"	
file_dependence "ui_toolpict_skylight_flat(1)"
file_dependence "ui_toolpict_cw_flat(1)"		
file_dependence "ui_toolpict_morph_flat(1)"	
file_dependence "ui_toolpict_object_flat(1)"	
file_dependence "ui_toolpict_lamp_flat(1)"	
file_dependence "ui_toolpict_mesh_flat(1)"	
file_dependence "ui_toolpict_cwMember_flat(1)"

ui_current_page gs_ui_current_page
ui_page gs_ui_current_page

! ==============================================================================
! Call tabpage selector
! ==============================================================================
call "ui_tab_popup" parameters 	nTabs 				= vardim1(tabTitles),
								gs_ui_current_page 	= gs_ui_current_page,
								tabTitles			= tabTitles,
								tabIcons			= tabIcons
								

! ==============================================================================
! Tabpages
! ==============================================================================

gosub "HeadStyleAndGeometryPage"

ui_page PAGE_ID_CONTENT
	gosub "ContentSettingsPage"

ui_page PAGE_ID_DISPORDER
	gosub "OrderSettingsPage"

ui_page PAGE_ID_GENERAL
	gosub "GeneralSettingsPage"

ui_page PAGE_ID_INFORMATION
	gosub "callPlaceholder"


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"ContentSettingsPage":
! ------------------------------------------------------------------------------

	inFieldHeight = 19
	outFieldHeight = 15

	endx = 440

	inFieldWidth1 = 160
	inFieldWidth2 = 50
	inFieldWidth3 = 70

	x1 = 1
	x2 = 85
	x3 = 140
	x4 = 375

	yCurr = 33

	dy = 24

	ui_style 0,1
	ui_outfield `Показ параметров следующих Инструментов` + ":", x1, yCurr, x4-x2, outFieldHeight
	ui_style 0,0

	yCurr = yCurr + outFieldHeight + 1

	buttonWidth = 22
	buttonHeight = 22
	xPos = x1
	xdiff = 8 + buttonWidth

	dim _bParamTool[20]
	dim _pictParamTool[20]
	dim _pictToolTip[20]
		_bParamTool[1]	= "bUseWithWall"		: _pictParamTool[1]		= "ui_toolpict_wall_flat(1).png"		:_pictToolTip[1]		= `Стена`
		_bParamTool[2]	= "bUseWithDoor"		: _pictParamTool[2]		= "ui_toolpict_door_flat(1).png"		:_pictToolTip[2]		= `Дверь`
		_bParamTool[3]	= "bUseWithWindow"		: _pictParamTool[3]		= "ui_toolpict_window_flat(1).png"		:_pictToolTip[3]		= `Окно, Угловое Окно`
		_bParamTool[4]	= "bUseWithColumn"		: _pictParamTool[4]		= "ui_toolpict_column_flat(1).png"		:_pictToolTip[4]		= `Колонна`
		_bParamTool[5]	= "bUseWithBeam"		: _pictParamTool[5]		= "ui_toolpict_beam_flat(1).png"		:_pictToolTip[5]		= `Балка`
		_bParamTool[6]	= "bUseWithSlab"		: _pictParamTool[6]		= "ui_toolpict_slab_flat(1).png"		:_pictToolTip[6]		= `Перекрытие`
		_bParamTool[7]	= "bUseWithRoof"		: _pictParamTool[7]		= "ui_toolpict_roof_flat(1).png"		:_pictToolTip[7]		= `Крыша`
		_bParamTool[8]	= "bUseWithFill"		: _pictParamTool[8]		= "ui_toolpict_fill_flat(1).png"		:_pictToolTip[8]		= `Штриховка`
		_bParamTool[9]	= "bUseWithShell"		: _pictParamTool[9]		= "ui_toolpict_shell_flat(1).png"		:_pictToolTip[9]		= `Оболочка`
		_bParamTool[10]	= "bUseWithSkylight"	: _pictParamTool[10]	= "ui_toolpict_skylight_flat(1).png"	:_pictToolTip[10]		= `Световой Люк`
		_bParamTool[11]	= "bUseWithCW"			: _pictParamTool[11]	= "ui_toolpict_cw_flat(1).png"			:_pictToolTip[11]		= `Навесная Стена`
		_bParamTool[12]	= "bUseWithMorph"		: _pictParamTool[12]	= "ui_toolpict_morph_flat(1).png"		:_pictToolTip[12]		= `Морф`
		_bParamTool[13]	= "bUseWithObject"		: _pictParamTool[13]	= "ui_toolpict_object_flat(1).png"		:_pictToolTip[13]		= `Объект, Лестница`
		_bParamTool[14]	= "bUseWithLamp"		: _pictParamTool[14]	= "ui_toolpict_lamp_flat(1).png"		:_pictToolTip[14]		= `Источник Света`
		_bParamTool[15]	= "bUseWithMesh"		: _pictParamTool[15]	= "ui_toolpict_mesh_flat(1).png"		:_pictToolTip[15]		= `3D-сетка`
		_bParamTool[16]	= "bUseWithCWMembers"	: _pictParamTool[16]	= "ui_toolpict_cwMember_flat(1).png"	:_pictToolTip[16]		= `Элементы Навесной Стены`

	for iIcon = 1 to 16
		ui_infield{4} _bParamTool[iIcon], xPos, yCurr, buttonWidth, buttonHeight,
			6, "",
			2,
			1, 18, 18, 18, 18,
			_pictParamTool[iIcon], "", 0,
			_pictParamTool[iIcon], "", 1\
			ui_tooltip _pictToolTip[iIcon]
		xPos = xPos + xdiff

		if iIcon = 8 then
			ui_button UI_FUNCTION, `Выбрать Все`, endx-135, yCurr, 135, buttonHeight, 1000

			xPos = x1
			yCurr = yCurr + buttonHeight + 6
		endif
		if iIcon = 16 then
			ui_button UI_FUNCTION, `Отмена Выбора`, endx-135, yCurr, 135, buttonHeight, 2000
		endif
	next iIcon
	yCurr = yCurr + buttonHeight + 5


	ui_style 0,1
	ui_outfield `Настройка Данных Выносной Надписи` + ":", x1, yCurr, x4-x2, outFieldHeight
	ui_style 0,0
	yCurr = yCurr + outFieldHeight + 1

	iParam = 1
	dim paramNames[]
	dim imageRelative[]
	dim paramtitle[]

		if _iDisplayContentGroupUI[CONTENT_DIM] > INVALID then

			paramNames[iParam] 		= "bShowLinearDimension"
			if _iDisplayContentParametersUI[DIM_SHOW] = RELATIVE then
				imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
			else
				imageRelative[iParam]	= ""
			endif
			paramtitle[iParam] = 0
			iParam = iParam + 1

			if bShowLinearDimension then
				paramNames[iParam] 		= "sDescriptionDimension"
				imageRelative[iParam]	= ""
				paramtitle[iParam] = 1
				iParam = iParam + 1	

				if _iDisplayContentParametersUI[DIM_WIDTH] > INVALID then
					paramNames[iParam] 		= "bShowWidth"
					if _iDisplayContentParametersUI[DIM_WIDTH] = RELATIVE then
						imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
					else
						imageRelative[iParam]	= ""
					endif
					paramtitle[iParam] = 1
					iParam = iParam + 1
				endif

				if _iDisplayContentParametersUI[DIM_LENGTH] > INVALID then
					paramNames[iParam] 		= "bShowLength"
					if _iDisplayContentParametersUI[DIM_LENGTH] = RELATIVE then
						imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
					else
						imageRelative[iParam]	= ""
					endif
					paramtitle[iParam] = 1
					iParam = iParam + 1
				endif

				if _iDisplayContentParametersUI[DIM_HEIGHT] > INVALID then
					paramNames[iParam] 		= "bShowHeight"
					if _iDisplayContentParametersUI[DIM_HEIGHT] = RELATIVE then
						imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
					else
						imageRelative[iParam]	= ""
					endif
					paramtitle[iParam] = 1
					iParam = iParam + 1
				endif

				if _iDisplayContentParametersUI[DIM_THICK] > INVALID then
					paramNames[iParam] 		= "bShowThickness"
					if _iDisplayContentParametersUI[DIM_THICK] = RELATIVE then
						imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
					else
						imageRelative[iParam]	= ""
					endif
					paramtitle[iParam] = 1
					iParam = iParam + 1
				endif

				paramNames[iParam] 		= "sSeparator"
				imageRelative[iParam]	= ""
				paramtitle[iParam] = 1
				iParam = iParam + 1
			endif
		endif

		if _iDisplayContentGroupUI[CONTENT_SURF] > INVALID then

			paramNames[iParam] 		= "bShowSurfaceArea"
			if _iDisplayContentParametersUI[SURF_SHOW] = RELATIVE then
				imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
			else
				imageRelative[iParam]	= ""
			endif
			paramtitle[iParam] = 0
			iParam = iParam + 1

			if bShowSurfaceArea then
				paramNames[iParam] 		= "sDescriptionSurfaceArea"
				imageRelative[iParam]	= ""
				paramtitle[iParam] = 1
				iParam = iParam + 1

				paramNames[iParam] 		= "iValueSumSurface"
				imageRelative[iParam]	= ""
				paramtitle[iParam] = 1
				iParam = iParam + 1	

				if iValueSumSurface <> SUMSURFACE_ALL then
					if _iDisplayContentParametersUI[SURF_TOP] > INVALID then
						paramNames[iParam] 		= "bAddTopSurface"
						if _iDisplayContentParametersUI[SURF_TOP] = RELATIVE then
							imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
						else
							imageRelative[iParam]	= ""
						endif
						paramtitle[iParam] = 1
						iParam = iParam + 1
					endif
	
					if _iDisplayContentParametersUI[SURF_BOTTOM] > INVALID then
						paramNames[iParam] 		= "bAddBottomSurface"
						if _iDisplayContentParametersUI[SURF_BOTTOM] = RELATIVE then
							imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
						else
							imageRelative[iParam]	= ""
						endif
						paramtitle[iParam] = 1
						iParam = iParam + 1
					endif
	
					if _iDisplayContentParametersUI[SURF_SIDE] > INVALID then
						paramNames[iParam] 		= "bAddSideSurface"
						if _iDisplayContentParametersUI[SURF_SIDE] = RELATIVE then
							imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
						else
							imageRelative[iParam]	= ""
						endif
						paramtitle[iParam] = 1
						iParam = iParam + 1
					endif
	
					if _iDisplayContentParametersUI[SURF_EDGE] > INVALID then
						paramNames[iParam] 		= "bAddEdgeSurface"
						if _iDisplayContentParametersUI[SURF_EDGE] = RELATIVE then
							imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
						else
							imageRelative[iParam]	= ""
						endif
						paramtitle[iParam] = 1
						iParam = iParam + 1
					endif
	
					if _iDisplayContentParametersUI[SURF_REFSIDE] > INVALID then
						paramNames[iParam] 		= "bAddRefSideSurface"
						if _iDisplayContentParametersUI[SURF_REFSIDE] = RELATIVE then
							imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
						else
							imageRelative[iParam]	= ""
						endif
						paramtitle[iParam] = 1
						iParam = iParam + 1
					endif
	
					if _iDisplayContentParametersUI[SURF_OPSIDE] > INVALID then
						paramNames[iParam] 		= "bAddOppSideSurface"
						if _iDisplayContentParametersUI[SURF_OPSIDE] = RELATIVE then
							imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
						else
							imageRelative[iParam]	= ""
						endif
						paramtitle[iParam] = 1
						iParam = iParam + 1
					endif
				endif
			endif
		endif

		if _iDisplayContentGroupUI[CONTENT_AREA] > INVALID then

			paramNames[iParam] 		= "bShowProjectedArea"
			if _iDisplayContentParametersUI[AREA_SHOW] = RELATIVE then
				imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
			else
				imageRelative[iParam]	= ""
			endif
			paramtitle[iParam] = 0
			iParam = iParam + 1

			if bShowProjectedArea then
				paramNames[iParam] 		= "sDescriptionProjectedArea"
				imageRelative[iParam]	= ""
				paramtitle[iParam] = 1
				iParam = iParam + 1
			endif
		endif

		if _iDisplayContentGroupUI[CONTENT_VOL] > INVALID then

			paramNames[iParam] 		= "bShowVolume"
			if _iDisplayContentParametersUI[VOLUME_SHOW] = RELATIVE then
				imageRelative[iParam]	= "ui_list_contentSetup_flat(1).png"
			else
				imageRelative[iParam]	= ""
			endif
			paramtitle[iParam] = 0
			iParam = iParam + 1

			if bShowVolume then
				paramNames[iParam] 		= "sDescriptionVolume"
				imageRelative[iParam]	= ""
				paramtitle[iParam] = 1
				iParam = iParam + 1
			endif

		endif

	! List with header without icon column
	UIGroup = 2
	ui_listfield UIGroup, x1, yCurr, endx-x1, 266-yCurr-outFieldHeight*1.2, 1

	itemID = 10
	for i = 1 to vardim1(paramNames)
		ui_listitem itemID, UIGroup, paramNames[i], paramtitle[i], imageRelative[i], ""
		itemID = itemID + 1
	next i

	yCurr = 266-outFieldHeight
	if iParam = 1 then ! empty listfield
		ui_pict "ui_warningIcon_flat(1)", x1, yCurr, 14, 14, 1
		ui_outfield `Инструмент не выбран.`, x1+15, yCurr, endx-x1-15, outFieldHeight
	else
		! if there is any relative content then note
		for i = 1 to vardim1(paramNames)
			if imageRelative[i] <> "" then
				ui_pict "ui_InfoIcon_flat(1)", x1, yCurr, 14, 14, 1
				ui_pict "ui_contentSetup_flat(1)", x1+15, yCurr, 14, 15, 1
				ui_outfield `Помеченные типы Данных недоступны для выбранных Инструментов.`, x1+2*15, yCurr, endx-x1-2*15-2, outFieldHeight
				i = vardim1(paramNames)
			endif
		next i
	endif

return


! ==============================================================================
"OrderSettingsPage":
! ------------------------------------------------------------------------------

	dy = 21
	yCurr = 33
	outFieldWidth1 = 160
	outFieldHeight = 15

	buttonX	= 13
	buttonY = 13
	buttonOffsetY = 2
	x1	= 10
	x2	= x1 + buttonX + buttonOffsetY
	x3	= x2 + buttonX + buttonOffsetY + 15
	endx = 222 + x3 + outFieldWidth1


	if _numShownContentGroupUI > EPS then

		ui_style 0, 1
		ui_outfield `Порядок Данных`, x1, yCurr, endx-x1, outFieldHeight
		yCurr = yCurr + dy

		ui_style 0, 0
		for iRow = 1 to vardim1(functionOrder)
			if iRow = 7 then
				x1	= 232
				x2	= x1 + buttonX + buttonOffsetY
				x3	= x2 + buttonX + buttonOffsetY + 15

				yCurr = 33 + dy
			endif

			! Down
			if (iRow <> vardim1(functionOrder)) then
				ui_pict_button ui_function, "", "ui_arrow_down_flat(1)", x1, yCurr+buttonOffsetY, buttonX, buttonY, 1000 + iRow ui_tooltip `Переместить Ниже`
			endif

			if iRow <> 1 then
				ui_pict_button ui_function, "", "ui_arrow_up_flat(1)", x2, yCurr+buttonOffsetY,	buttonX, buttonY, 2000 + iRow ui_tooltip `Переместить Выше`
			endif

			ui_outfield _stContentDescription[functionOrder[iRow]], x3, yCurr, outFieldWidth1, outFieldHeight
			yCurr = yCurr + dy

		next iRow
	endif

	! --------------------------------------------------------------------------
	ui_separator 0, 184,	444, 184
	! --------------------------------------------------------------------------

	buttonX	= 27
	buttonY = 13
	buttonOffset = 2
	x1	= 10

	outFieldWidth2 = 83
	yCurr = 199

	xOffset	= buttonOffset + buttonX
	diffX	= outFieldWidth2 + 2 * buttonOffset + buttonX

	xCurr = 11

	ui_style 0, 1
	ui_outfield `Порядок Размеров`, x1, yCurr, endx-x1, outFieldHeight
	yCurr = yCurr + dy

	ui_style 0, 0
	for iCol = 1 to vardim1(iContentOrderDim)

		if iCol <> 1 then
			ui_pict_button ui_function, "", "ui_arrow_swap_flat(1)", xCurr-xOffset, yCurr+1, buttonX, buttonY, 3000 + iCol ui_tooltip `Изменить Порядок`
		endif

		_fieldFlag = 2
		if iCol = 1 then _fieldFlag = 0
		if iCol = vardim1(iContentOrderDim) then _fieldFlag = 1

		ui_outfield _stDimContentDescription[iContentOrderDim[iCol]], xCurr, yCurr, outFieldWidth2, outFieldHeight, _fieldFlag
		xCurr = xCurr + diffX
	next iCol

	ui_pict "ui_InfoIcon_flat(1)", x1, 250, 14, 14, 1
	ui_outfield `Добавить/удалить Данные можно на вкладке 'Настройки Данных'`, x1+15, 250, endx-x1-15, outFieldHeight

return


! ==============================================================================
"GeneralSettingsPage":
	! ------------------------------------------------------------------------------

	inFieldWidth1 = 135
	inFieldWidth2 = 120
	inFieldWidth3 = 155
	inFieldWidth4 = 95
	inFieldWidth5 = 50
	yCurr = 35
	dy = 24

	x1 = 1
	x2 = x1 + inFieldWidth1
	x3 = x2 + inFieldWidth2
	x4 = x2 + inFieldWidth3
	x5 = x4 + 5
	x6 = x5 + inFieldWidth4 



	ui_outfield `Ориентация`,	x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield{3} "iLabelOrientation",		x2, yCurr, x3-x2, 48,
		2, "ui_label_orientation_flat(1)", 6, 2, ! UI_label_orientation.png
		35, 35, 32, 32,
		5, _stLabelOrientation[ORIENTATION_PARALLEL],	ORIENTATION_PARALLEL,
		2, _stLabelOrientation[ORIENTATION_VERTICAL],	ORIENTATION_VERTICAL,
		1, _stLabelOrientation[ORIENTATION_HORIZONTAL], ORIENTATION_HORIZONTAL,
		3, _stLabelOrientation[ORIENTATION_CUSTOM],		ORIENTATION_CUSTOM

	yCurr = yCurr + dy + 48-outFieldHeight

	ui_outfield `Точка Привязки`, x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield{3} "iReferencePoint",	x2, yCurr, x3-x2, 48,
			3, "ui_anchorpoint_flat(1)", 9, 3, 41, 41, 41, 41, ! ui_anchorpoint_flat(1).png
			1, _stRefPoint[1], 1,
			2, _stRefPoint[2], 2,
			3, _stRefPoint[3], 3,
			4, _stRefPoint[4], 4,
			5, _stRefPoint[5], 5,
			6, _stRefPoint[6], 6,
			7, _stRefPoint[7], 7,
			8, _stRefPoint[8], 8,
			9, _stRefPoint[9], 9

	yCurr = yCurr + dy + 50 - outFieldHeight
	ui_separator x1, yCurr, endx, yCurr
	yCurr = yCurr + dy/2 - 5

	ui_style 0,1
	ui_infield{3} "bUsePrefixColumn", x1, yCurr, endx-x1, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показать Столбец Названий Параметров`, 0,
			2, `Показать Столбец Названий Параметров`, 1
	yCurr = yCurr + dy - 2

	ui_infield{3} "bShowUnit", x1, yCurr,	x3, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Единицы Измерения`, 0,
			2, `Единицы Измерения`, 1
	ui_style 0,0
	yCurr = yCurr + dy - 2

	ui_outfield `Линейные`, x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield{3} "iWorkUnitLinear", x2, yCurr-4, x4-x2-1, inFieldHeight,
			8, "", 0, 0, 0, 0, 0, 0,
			"", stUnitFormats[1], DIMUNIT_MM,
			"", stUnitFormats[2], DIMUNIT_CM,
			"", stUnitFormats[3], DIMUNIT_DM,
			"", stUnitFormats[4], DIMUNIT_METER,
			"", stUnitFormats[5], DIMUNIT_INCH,
			"", stUnitFormats[6], DIMUNIT_PROJECT

	ui_outfield `Десятичные`,  x5, yCurr,	inFieldWidth4, outFieldHeight, 4 * _bLockLinearUnitDecimal
	ui_infield{3} "iWorkUnitLinearDecimal", x6, yCurr-4,	inFieldWidth5, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4
	yCurr = yCurr + dy

	ui_outfield `Площадь`, x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield{3} "iWorkUnitArea", x2, yCurr-4, x4-x2-1, inFieldHeight,
			8, "", 0, 0, 0, 0, 0, 0,
			"", stUnitFormatsArea[1], SURFUNIT_SQM,
			"", stUnitFormatsArea[2], SURFUNIT_SQCM,
			"", stUnitFormatsArea[3], SURFUNIT_SQMM,
			"", stUnitFormatsArea[4], SURFUNIT_SQFT,
			"", stUnitFormatsArea[5], SURFUNIT_SQINCH,
			"", stUnitFormatsArea[6], SURFUNIT_PROJECT

	ui_outfield `Десятичные`,  x5, yCurr,	inFieldWidth4, outFieldHeight, 4 * _bLockAreaUnitDecimal
	ui_infield{3} "iWorkUnitAreaDecimal", x6, yCurr-4,	inFieldWidth5, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4
	yCurr = yCurr + dy

	ui_outfield `Объем`, x1, yCurr, x2-x1-1, outFieldHeight
	ui_infield{3} "iWorkUnitVolume", x2, yCurr-4, x4-x2-1, inFieldHeight,
			8, "", 0, 0, 0, 0, 0, 0,
			"", stUnitFormatsVol[1], VOLUNIT_CM,
			"", stUnitFormatsVol[2], VOLUNIT_L,
			"", stUnitFormatsVol[3], VOLUNIT_CCM,
			"", stUnitFormatsVol[4], VOLUNIT_CMM,
			"", stUnitFormatsVol[5], VOLUNIT_CFEET,
			"", stUnitFormatsVol[6], VOLUNIT_CINCH,
			"", stUnitFormatsVol[7], VOLUNIT_CYARD,
			"", stUnitFormatsVol[8], VOLUNIT_GAL,
			"", stUnitFormatsVol[9], VOLUNIT_PROJECT

	ui_outfield `Десятичные`,  x5, yCurr,	inFieldWidth4, outFieldHeight, 4 * _bLockVolumeUnitDecimal
	ui_infield{3} "iWorkUnitVolumeDecimal", x6, yCurr-4,	inFieldWidth5, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4

return


! =============================================================================
"HeadStyleAndGeometryPage":
! -----------------------------------------------------------------------------
	call "label_marker_form" parameters AC_CustomReadability = AC_CustomReadability,
		bUseBuiltinPointer			= not(LABEL_CUSTOM_ARROW),

		! Position
		iReferencePoint				= iReferencePoint,
		iLabelOrientation 			= iLabelOrientation,

		! Paragraph Format
		bEnablePrefixColumn			= 1,
		bUsePrefixColumn			= bUsePrefixColumn,
		bWrapText					= LABEL_TEXT_WRAP,
		widthTextBoxPrefix_mm		= widthTextBoxPrefix_mm,
		widthTextBoxPrefix_pt		= widthTextBoxPrefix_pt,
		widthTextBoxContent_mm		= widthTextBoxContent_mm,
		widthTextBoxContent_pt		= widthTextBoxContent_pt,

		! Head apperance
		bEnableCustomHeaderStyle 	= 0,

		! Body skin list appearance
		bUseTextBoxOnLine			= 0,

		! Label Head Representation
		bEnableHeadSwitch			= 0,							! Enable to switch the Show Label Head ON-OFF
		bShowHead					= 1,
		iShapeHead					= iShapeHead,
		bFitFrameToText				= bFitFrameToText,
		widthHead_mm				= widthHead_mm,
		heightHead_mm				= heightHead_mm,
		bLinkSizes					= bLinkSizes,
		bShowHeadContour			= LABEL_FRAME_ON,
		bShowFill					= 1,
		penForeground				= LABEL_TEXT_BG_PEN,

		! Label Line Representation
		distanceLabelHead_mm		= 0,
		bEnableLeaderLine			= 0,
		iMyPage_Geometry 			= PAGE_ID_GEOMETRY,
		iMyPage_HeadStyle			= 0,
		iMyPage_CustomSkins			= 0

return


! =============================================================================
"callPlaceholder":
! -----------------------------------------------------------------------------
	! - Offered Tools: All Tools exept Independent
	call "label_placeholder" parameters bitAvailableTool 	= 2+4+8+16+32+64+128+256+512+1024+2048+4096+8192+16384+32768+262144+524288+1048576,
										bEnableUIContent = _isEnabledContent

return
