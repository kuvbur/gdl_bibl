

! ==============================================================================
! Placeholder if there is not valid content for parent tool
! ==============================================================================

if not(_isEnabledContent) then
	gosub "callPlaceholder"
	END
endif


! ==============================================================================
! Tabpage IDs, Titles and Icons
! ==============================================================================

PAGE_ID_GENERAL		= 1
PAGE_ID_GEOMETRY	= 2
PAGE_ID_HEADSTYLE	= 3
PAGE_ID_CUSTOMCONT	= 4
PAGE_ID_INFORMATION	= 5

dim tabTitles[5]
	tabTitles[1] = `Общие Параметры`
	tabTitles[2] = `Геометрия Выноски`
	tabTitles[3] = `Стиль Маркера над Флажком`
	tabTitles[4] = `Специальные Данные`
	tabTitles[5] = `Информация`

dim tabIcons[5]
	tabIcons[1] = "uiTab_areaContent_flat(1)"
	tabIcons[2] = "uiTab_labelGeometry_flat(1)"
	tabIcons[3] = "uiTab_labelHeadStyle_flat(1)"
	tabIcons[4] = "uiTab_MarkerLabelContent_flat(1)"
	tabIcons[5] = "uiTab_Info_flat(1)"

file_dependence "uiTab_areaContent_flat(1)"
file_dependence "uiTab_labelHeadStyle_flat(1)"
file_dependence "uiTab_labelGeometry_flat(1)"
file_dependence "uiTab_MarkerLabelContent_flat(1)"
file_dependence "uiTab_Info_flat(1)"

ui_current_page gs_ui_current_page
ui_page gs_ui_current_page

! ==============================================================================
! Call tabpage selector
! ==============================================================================
call "ui_tab_popup" parameters 	nTabs 				= vardim1(tabTitles),
								gs_ui_current_page 	= gs_ui_current_page,
								tabTitles			= tabTitles,
								tabIcons			= tabIcons
								

! ==============================================================================
! Tabpages
! ==============================================================================

gosub "GeometryAndCustomSkinsPages"

ui_page PAGE_ID_GENERAL
	gosub "GeneralSettingsPage"

ui_page PAGE_ID_INFORMATION
	gosub "callPlaceholder"


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"GeneralSettingsPage":
! ------------------------------------------------------------------------------

	inFieldHeight = 19
	outFieldHeight = 15
	inFieldWidthDesc = 135
	inFieldWidthCont = 125
	inFieldWidthContMini = 35

	x1 = 2
	x2 = 160
	x3 = x2 + inFieldWidthDesc
	x4 = 305
	x5 = 440

	yCurr = 40

	dy = 24

	ui_style 0,1
	ui_outfield `Показать Слой по`,			x1, yCurr, x2-x1-1, outFieldHeight
	ui_style 0,0

	if	GLOB_ELEM_TYPE = ELEM_TYPE_FILL &\
		not(FILL_FILL_CATEGORY = 1 & FILL_BMAT_NAME <> "") then

		ui_outfield `Имя Штриховки`, x2, yCurr, x3-x2, outFieldHeight

		ui_pict "ui_InfoIcon_flat(1)", x1, yCurr + dy, 14, 14, 1
		if FILL_FILL_CATEGORY = 1 then
			ui_outfield `Этот тип Штриховки (Сечений) не имеет данных Строительного Материала.`, x1 + 17, yCurr + dy, x5-x1-17, outFieldHeight
		else
			if FILL_FILL_CATEGORY = 2 then
				ui_outfield `Этот тип Штриховки (Поверхностей) не имеет данных Строительного Материала.`, x1 + 17, yCurr + dy, x5-x1-17, outFieldHeight
			else
				ui_outfield `Этот тип Штриховки (Чертежей) не имеет данных Строительного Материала.`, x1 + 17, yCurr + dy, x5-x1-17, outFieldHeight
			endif
		endif
	else
		ui_infield{3} "iTypeSkinContent", x2, yCurr-4, x3-x2, inFieldHeight,
				8, "", 3, 1, 0, 0, 0, 0,
				"", _stTypeSkinContentValues[CONTENT_BUILDING_MATERIAL], 	CONTENT_BUILDING_MATERIAL,
				"", _stTypeSkinContentValues[CONTENT_BUILDING_ID], 			CONTENT_BUILDING_ID,
				"", _stTypeSkinContentValues[CONTENT_BUILDING_MANUFACTURER], CONTENT_BUILDING_MANUFACTURER,
				"", _stTypeSkinContentValues[CONTENT_BUILDING_DESCRIPTION], 	CONTENT_BUILDING_DESCRIPTION,
				"", _stTypeSkinContentValues[CONTENT_CUT_FILL], 				CONTENT_CUT_FILL
	endif

	ui_infield{3} "bChangeSkinDirection", x4, yCurr-4 - inFieldHeight*0.25, x5-x4, inFieldHeight*1.5,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Обратный Порядок Слоев`, 0,
			2, `Обратный Порядок Слоев`, 1

	yCurr = yCurr + 2 * dy

	ui_style 0,1
	ui_infield{3} "bShowID", x1, yCurr-4,	x2-x1-1, inFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показ ID`, 0,
			2, `Показ ID`, 1

	ui_style 0,0

	ui_infield{3} "iIDText", x2, yCurr-4, x3-x2, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", sIDTextValues[1], IDVALUE_IDNUMBER,
			"", sIDTextValues[4], IDVALUE_MASTERID,
			"", sIDTextValues[3], IDVALUE_FULLIDNUM,
			"", sIDTextValues[2], IDVALUE_CUSTOMTEXT		ui_tooltip `Данные Маркера`

	if iIDText = IDVALUE_CUSTOMTEXT then
		ui_infield "sIDTextCustom", x4, yCurr-4, x5-x4, inFieldHeight
	endif

! ------------------------------------------------------------------------------
! Unit and Thickness
! ------------------------------------------------------------------------------
	yCurr = yCurr + dy + 5

	ui_separator 0, yCurr, 443, yCurr

	yCurr = yCurr + 16

	ui_style 0,1
	ui_infield{3} "bShowSkinThickness", x1, yCurr-4, x3-x1, inFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Показать Толщину`, 0,
			2, `Показать Толщину`, 1

	yCurr = yCurr + dy + 5

	ui_infield{3} "bShowUnit", x1, yCurr-7, x2-x1-1, inFieldHeight+6,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Единицы Измерения`, 0,
			2, `Единицы Измерения`, 1
	ui_style 0,0

	ui_infield{3} "iWorkUnitLinear", x2, yCurr-4, x3-x2, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", stUnitFormats[1], DIMUNIT_MM,
			"", stUnitFormats[2], DIMUNIT_CM,
			"", stUnitFormats[3], DIMUNIT_DM,
			"", stUnitFormats[4], DIMUNIT_METER,
			"", stUnitFormats[5], DIMUNIT_INCH,
			"", stUnitFormats[6], DIMUNIT_PROJECT

	ui_outfield `Десятичные`,  x4, yCurr,	x5-x4 - inFieldWidthContMini, outFieldHeight, 4 * _bLockLinearUnitDecimal
	ui_infield{3} "iWorkUnitLinearDecimal",x5 - inFieldWidthContMini, yCurr-4,	inFieldWidthContMini, inFieldHeight,
			8, "", 3, 1, 0, 0, 0, 0,
			"", '0', 0,
			"", '1', 1,
			"", '2', 2,
			"", '3', 3,
			"", '4', 4

! ------------------------------------------------------------------------------
! Label Orientation
! ------------------------------------------------------------------------------
	yCurr = yCurr + dy

	ui_separator 0, yCurr, 443, yCurr

	yCurr = yCurr + 16

	ui_style 0,1
	ui_outfield `Ориентация`,	x1, yCurr, x2-x1-1, outFieldHeight
	ui_style 0,0
	ui_infield{3} "iLabelOrientation",		x2, yCurr, x3-x2, 40,
		2, "ui_label_orientation_flat(1)", 6, 2, ! UI_label_orientation.png
		35, 35, 32, 32,
		5, _stLabelOrientation[ORIENTATION_PARALLEL],	ORIENTATION_PARALLEL,
		2, _stLabelOrientation[ORIENTATION_VERTICAL],	ORIENTATION_VERTICAL,
		1, _stLabelOrientation[ORIENTATION_HORIZONTAL], ORIENTATION_HORIZONTAL,
		3, _stLabelOrientation[ORIENTATION_CUSTOM],		ORIENTATION_CUSTOM

	ui_infield{3} "bFlipLabel", x4, yCurr,	x5-x4, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Развернуть Выноску`, 0,
			2, `Развернуть Выноску`, 1

	yCurr = yCurr + dy

	ui_infield{3} "bMirrorLabel", x4, yCurr,	x5-x4, outFieldHeight,
			7, "", 2, 1, 0, 0, 0, 0,
			1, `Зеркальный Маркер`, 0,
			2, `Зеркальный Маркер`, 1

! ------------------------------------------------------------------------------
! End Separator
! ------------------------------------------------------------------------------
	yCurr = yCurr + dy + 5

	ui_separator 0, yCurr, 443, yCurr

return


! =============================================================================
"GeometryAndCustomSkinsPages":
! -----------------------------------------------------------------------------
	if 	(not(bShowID) | (iIDText = IDVALUE_CUSTOMTEXT & sIDTextCustom = "")) &\
		(bFitFrameToText & not(LABEL_TEXT_WRAP)) then

		_bShowHead = 0
	else
		_bShowHead = bShowHead
	endif

	call "label_marker_form" parameters AC_CustomReadability = AC_CustomReadability,
		bUseBuiltinPointer			= not(LABEL_CUSTOM_ARROW),

		! Position
		iReferencePoint				= 5,
		iLabelOrientation 			= iLabelOrientation,
		bFlipLabel 					= bFlipLabel,
		anglePosition 				= LABEL_ROTANGLE,

		! Paragraph Format
		bEnablePrefixColumn			= 0,
		bUsePrefixColumn			= 0,
		bWrapText					= LABEL_TEXT_WRAP,
		widthTextBoxContent_mm		= widthTextBoxContent_mm,
		widthTextBoxContent_pt		= widthTextBoxContent_pt,

		! Head apperance
		bEnableCustomHeaderStyle 	= 1,
		bUseCustomHeaderStyle		= bUseCustomHeaderStyle,
		penTextHeader				= penTextHeader,
		fontTypeHeader				= fontTypeHeader,
		sizeFontHeader_mm			= sizeFontHeader_mm,
		sizeFontHeader_pt			= sizeFontHeader_pt,
		bBoldStyleHeader			= bBoldStyleHeader,
		bItalicStyleHeader			= bItalicStyleHeader,
		bUnderlineStyleHeader		= bUnderlineStyleHeader,
		bStrikethroughStyleHeader	= bStrikethroughStyleHeader,
		iAlignmentHeader			= 2,	! 1: left aligned, 2: center aligned, 3: right aligned, 4: full justified
		sizeLeadingHeader			= 1,

		! Body skin list appearance
		bUseTextBoxOnLine				= 1,
		listTextTopDistance_mm			= listTextTopDistance_mm,
		listTextTopDistance_pt			= listTextTopDistance_pt,
		bShowSeparatorLines				= bShowSeparatorLines,
		bCustomListContent				= bCustomListContent,
		dataCustomSkinRow				= dataCustomSkinRow,

		! Body skin Text Style
		penTextBoxOnLine				= LABEL_TEXT_PEN,
		sTypeFontTextBoxOnLine			= LABEL_FONT_NAME,
		iStyleFontTextBoxOnLine			= LABEL_FONT_STYLE2,
		sizeFontTextBoxOnLine			= LABEL_TEXT_SIZE,
		iAlignmentTextBoxOnLine			= LABEL_TEXT_ALIGN,			! 0: custom, 1: left aligned, 2: center aligned, 3: right aligned, 4: full justified
		sizeLeadingTextBoxOnLine		= LABEL_TEXT_LEADING,

		! Label Head Representation
		bEnableHeadSwitch			= 1,							! Enable to switch the Show Label Head ON-OFF
		bShowHead					= _bShowHead,
		iShapeHead					= iShapeHead,
		bFitFrameToText	= bFitFrameToText,
		widthHead_mm				= widthHead_mm,
		heightHead_mm				= heightHead_mm,
		bLinkSizes					= bLinkSizes,
		bShowHeadContour			= LABEL_FRAME_ON,
		bShowFill					= 1,
		penForeground				= LABEL_TEXT_BG_PEN,

		! Label Line Representation
		distanceLabelHead_mm		= distanceLabelHead_mm,
		distanceLabelHead_pt		= distanceLabelHead_pt,
		bEnableLeaderLine			= 1,
		bShowLeaderLine				= bShowLeaderLine,
		iLeaderLineStyle			= iLeaderLineStyle,
		thkLeaderLine_mm			= thkLeaderLine_mm,
		thkLeaderLine_pt			= thkLeaderLine_pt,
		iExtensionLineStyle 		= iExtensionLineStyle,
		lengthExtensionLine_mm		= lengthExtensionLine_mm,
		lengthExtensionLine_pt		= lengthExtensionLine_pt,
		thkExtensionLine_mm			= thkExtensionLine_mm,
		thkExtensionLine_pt			= thkExtensionLine_pt,
		iMyPage_Geometry 			= PAGE_ID_HEADSTYLE,	! Switch tabpage order
		iMyPage_HeadStyle			= PAGE_ID_GEOMETRY,
		iMyPage_CustomSkins			= PAGE_ID_CUSTOMCONT,
		gs_ui_sub_tab				= gs_ui_sub_tab	

return


! =============================================================================
"callPlaceholder":
! -----------------------------------------------------------------------------
	! - Offered Tools: Wall, Slab, Roof, Shell, Fill
	call "label_placeholder" parameters	bitAvailableTool = 1+32+128+256+512+262144,
										bEnableUIContent = _isEnabledContent

return
