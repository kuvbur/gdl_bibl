
EPS = 0.0001

dim _stLabelOrientation[4]
	_stLabelOrientation[1] = `Параллельно`
	_stLabelOrientation[2] = `Перпендикулярно`
	_stLabelOrientation[3] = `Горизонтально`
	_stLabelOrientation[4] = `Вертикально`

ORIENTATION_PARALLEL			= 1
ORIENTATION_PERPENDICULAR		= 2
ORIENTATION_HORIZONTAL			= 3
ORIENTATION_VERTICAL			= 4


dim _stRefPoint[9]
	_stRefPoint[1] = `Верхняя Левая`
	_stRefPoint[2] = `Верх Центр`
	_stRefPoint[3] = `Верхняя Правая`
	_stRefPoint[4] = `Средняя Левая`
	_stRefPoint[5] = `Середина Центр`
	_stRefPoint[6] = `Средняя Правая`
	_stRefPoint[7] = `Нижняя Левая`
	_stRefPoint[8] = `Низ Центр`
	_stRefPoint[9] = `Нижняя Правая`

dim _valuesRefPoint[9]
for i = 1 to 9
	_valuesRefPoint[i] = i
next i

dim _stValueSumSurface[2]
	_stValueSumSurface[1] = `Площадь Всех Поверхностей`
	_stValueSumSurface[2] = `Сумма Выбранных Поверхностей`

SUMSURFACE_ALL = 1
SUMSURFACE_SELECTED = 2

dim stUnitFormats[6]
	stUnitFormats[1] = `мм`
	stUnitFormats[2] = `см`
	stUnitFormats[3] = `дм`
	stUnitFormats[4] = `м`
	stUnitFormats[5] = `дюйм`
	stUnitFormats[6] = `по Параметрам Проекта`

dim stUnitFormatsVol[9]
	stUnitFormatsVol[1] = `куб м`
	stUnitFormatsVol[2] = `литр`
	stUnitFormatsVol[3] = `куб см`
	stUnitFormatsVol[4] = `куб мм`
	stUnitFormatsVol[5] = `куб фут`
	stUnitFormatsVol[6] = `куб дюйм`
	stUnitFormatsVol[7] = `куб ярд`
	stUnitFormatsVol[8] = `галлон`
	stUnitFormatsVol[9] = `по Параметрам Проекта`

dim stUnitFormatsArea[6]
	stUnitFormatsArea[1] = `кв м`
	stUnitFormatsArea[2] = `кв см`
	stUnitFormatsArea[3] = `кв мм`
	stUnitFormatsArea[4] = `кв фут`
	stUnitFormatsArea[5] = `кв дюйм`
	stUnitFormatsArea[6] = `по Параметрам Проекта`

dim _stRefStory[2]
	_stRefStory[1] = `Текущий Этаж`
	_stRefStory[2] = `Начало Проекта`

! ---- Values for iRefStory
REF_STORY_CURR		= 1
REF_STORY_PROJECT	= 2


dim _sElevationMeasuredTo[]
	_sElevationMeasuredTo[1] = `Отделки`
	_sElevationMeasuredTo[2] = `Ядра`

ELEV_MESURED_FINISH = 1
ELEV_MESURED_CORE	= 2

dim sIDTextValues [4]
	sIDTextValues[1] = `Компактный ID Элемента`
	sIDTextValues[2] = `Специальный`
	sIDTextValues[3] = `Полный ID Элемента`
	sIDTextValues[4] = `Основной ID`

IDVALUE_IDNUMBER	= 1
IDVALUE_CUSTOMTEXT	= 2
IDVALUE_FULLIDNUM	= 3
IDVALUE_MASTERID	= 4

dim _stTypeSkinContentValues[5]
	_stTypeSkinContentValues[1] = `Название Строительного Материала`
	_stTypeSkinContentValues[2] = `Название Штриховки Сечения`
	_stTypeSkinContentValues[3] = `ID Строительного Материала`
	_stTypeSkinContentValues[4] = `Производитель Строительного Материала`
	_stTypeSkinContentValues[5] = `Описание Строительного Материала`

! iTypeSkinContent
CONTENT_BUILDING_MATERIAL		= 1
CONTENT_CUT_FILL				= 2
CONTENT_BUILDING_ID				= 3
CONTENT_BUILDING_MANUFACTURER	= 4
CONTENT_BUILDING_DESCRIPTION	= 5

! --------------------------------------------------------------------------
! For Content Dimension
! --------------------------------------------------------------------------

! iContentOrderDim
DIM_WDT	= 1
DIM_LGT	= 2
DIM_THK	= 3
DIM_HGT	= 4

_maxDimensionNum = 4

dim	_stDimContentDescription[4]
	_stDimContentDescription[1] = `Ширина`
	_stDimContentDescription[2] = `Ширина`
	_stDimContentDescription[3] = `Толщина`
	_stDimContentDescription[4] = `Высота`

! ------------------------------------------------------------------------------
! Unit Types - values for iUnitLocal parameter - label_content_macro
! ------------------------------------------------------------------------------

DIMUNIT_MM			= 11  
DIMUNIT_CM			= 12
DIMUNIT_METER		= 13
DIMUNIT_INCH		= 14
DIMUNIT_DM			= 15
DIMUNIT_PROJECT		= 10

SURFUNIT_SQM		= 21
SURFUNIT_SQCM		= 22
SURFUNIT_SQMM		= 23
SURFUNIT_SQFT		= 24
SURFUNIT_SQINCH		= 25
SURFUNIT_PROJECT	= 20

VOLUNIT_CM			= 31
VOLUNIT_L			= 32
VOLUNIT_CCM			= 33
VOLUNIT_CMM			= 34
VOLUNIT_CFEET		= 35
VOLUNIT_CINCH		= 36
VOLUNIT_CYARD		= 37
VOLUNIT_GAL			= 38
VOLUNIT_PROJECT		= 30

! ------------------------------------------------------------------------------
! Content Types - label_content_macro
! ------------------------------------------------------------------------------

CONTENT_NONE		= 1
CONTENT_DIM 		= 2			
CONTENT_VOL			= 3
CONTENT_SURF		= 4
CONTENT_ID			= 5
CONTENT_ELEMENT_NAME= 6
CONTENT_STRUCT_NAME	= 7
CONTENT_CUSTOM		= 8
CONTENT_ELEV		= 9
CONTENT_SKIN		= 10
CONTENT_MATERIAL	= 11
CONTENT_AREA		= 12

_maxContentValues = 12
_maxFunctionOrder = 11

dim _stContentDescription[12]
	_stContentDescription[1] = `Нет`
	_stContentDescription[2] = `Размер`
	_stContentDescription[3] = `Объем`
	_stContentDescription[4] = `Площадь Поверхности`
	_stContentDescription[5] = `ID`
	_stContentDescription[6] = `Имя`
	_stContentDescription[7] = `Состав/Профиль/Материал`
	_stContentDescription[8] = `Специальный Текст`
	_stContentDescription[9] = `Возвышение`
	_stContentDescription[10] = `Данные Слоев`
	_stContentDescription[11] = `Покрытие`
	_stContentDescription[12] = `Площадь Проекции`

dim _bShowContent[12]
	_bShowContent[CONTENT_NONE] 			= 0
	_bShowContent[CONTENT_DIM] 				= bShowLinearDimension
	_bShowContent[CONTENT_VOL] 				= bShowVolume
	_bShowContent[CONTENT_SURF] 			= bShowSurfaceArea
	_bShowContent[CONTENT_ID] 				= bShowID
	_bShowContent[CONTENT_ELEMENT_NAME] 	= bShowElementName
	_bShowContent[CONTENT_STRUCT_NAME] 		= bShowStructuralName
	_bShowContent[CONTENT_CUSTOM] 			= bShowCustomText
	_bShowContent[CONTENT_ELEV] 			= bShowElevation
	_bShowContent[CONTENT_SKIN] 			= bShowSkinList
	_bShowContent[CONTENT_MATERIAL] 		= bShowSurface
	_bShowContent[CONTENT_AREA] 			= bShowProjectedArea

! ==============================================================================
! values for GLOB_ELEM_TYPE
! ==============================================================================
ELEM_TYPE_LABEL		= 0
ELEM_TYPE_OBJECT	= 1
ELEM_TYPE_LAMP		= 2
ELEM_TYPE_WINDOW	= 3
ELEM_TYPE_DOOR		= 4
ELEM_TYPE_WALL		= 5
ELEM_TYPE_COLUMN	= 6
ELEM_TYPE_SLAB		= 7
ELEM_TYPE_ROOF		= 8
ELEM_TYPE_FILL		= 9
ELEM_TYPE_MESH		= 10
ELEM_TYPE_ZONE		= 11
ELEM_TYPE_BEAM		= 12
ELEM_TYPE_CW		= 13
ELEM_TYPE_CWFRAME	= 14
ELEM_TYPE_CWPANEL	= 15
ELEM_TYPE_CWJUNCT	= 16
ELEM_TYPE_CWACC		= 17
ELEM_TYPE_SHELL		= 18
ELEM_TYPE_SKYLIGHT	= 19
ELEM_TYPE_MORPH		= 20

! ==============================================================================
! Content by tools
! ==============================================================================

! ------------------------------------------------------------------------------
! Initializing Enabling bools
! ------------------------------------------------------------------------------


ID_SHOW				= 1
NAME_SHOW			= 2
ELEV_SHOW			= 3
ELEV_TOP			= 4
ELEV_BOTT			= 5
DIM_SHOW			= 6
DIM_WIDTH			= 7
DIM_LENGTH			= 8
DIM_HEIGHT			= 9
DIM_THICK			= 10
SURF_SHOW			= 11
SURF_TOP			= 12
SURF_BOTTOM			= 13
SURF_SIDE			= 14
SURF_EDGE			= 15
SURF_REFSIDE		= 16
SURF_OPSIDE			= 17
AREA_SHOW			= 18
VOLUME_SHOW			= 19
SKIN_SHOW			= 20
STRUCT_NAME_SHOW	= 21
MATERIAL_SHOW		= 22
CUSTOM_SHOW			= 23

_iParentTool = GLOB_ELEM_TYPE + 1	! because Label tool num is zero
_numTool	= 21
_numContentParameter = 23

dim _iEnabledContentGroup[21][12]
! - contents after label_content_macro (1 - > 12)
	for iTool = 1 to _numTool
		for iParam = 1 to _maxContentValues
			_iEnabledContentGroup[iTool][iParam] = 0
		next iParam
	next iTool

dim _iEnabledContentParameter[21][23]
	for iTool = 1 to _numTool
		for iParam = 1 to _numContentParameter
			_iEnabledContentParameter[iTool][iParam] = 0
		next iParam
	next iTool

dim _bToolFilter[]
_bToolFilter[ELEM_TYPE_LABEL	+1]		= bUseWithLabel
_bToolFilter[ELEM_TYPE_OBJECT	+1]		= bUseWithObject
_bToolFilter[ELEM_TYPE_LAMP		+1]		= bUseWithLamp
_bToolFilter[ELEM_TYPE_WINDOW	+1]		= bUseWithWindow
_bToolFilter[ELEM_TYPE_DOOR		+1]		= bUseWithDoor
_bToolFilter[ELEM_TYPE_WALL		+1]		= bUseWithWall
_bToolFilter[ELEM_TYPE_COLUMN	+1]		= bUseWithColumn
_bToolFilter[ELEM_TYPE_SLAB		+1]		= bUseWithSlab
_bToolFilter[ELEM_TYPE_ROOF		+1]		= bUseWithRoof
_bToolFilter[ELEM_TYPE_FILL		+1]		= bUseWithFill
_bToolFilter[ELEM_TYPE_MESH		+1]		= bUseWithMesh
_bToolFilter[ELEM_TYPE_ZONE		+1]		= 0
_bToolFilter[ELEM_TYPE_BEAM		+1]		= bUseWithBeam
_bToolFilter[ELEM_TYPE_CW		+1]		= bUseWithCW
_bToolFilter[ELEM_TYPE_CWFRAME	+1]		= bUseWithCWMembers
_bToolFilter[ELEM_TYPE_CWPANEL	+1]		= bUseWithCWMembers
_bToolFilter[ELEM_TYPE_CWJUNCT	+1]		= bUseWithCWMembers
_bToolFilter[ELEM_TYPE_CWACC	+1]		= bUseWithCWMembers
_bToolFilter[ELEM_TYPE_SHELL	+1]		= bUseWithShell
_bToolFilter[ELEM_TYPE_SKYLIGHT+1]		= bUseWithSkylight
_bToolFilter[ELEM_TYPE_MORPH	+1]		= bUseWithMorph

INVALID		= 0
VALID		= 1	! valid
RELATIVE	= 2 ! valid only at UI  but there is another invalid tool (need relativ complement sign to display)


for iTool = 1 to _numTool

	! ID ------------------------------------------------------------------------------

	_iEnabledContentGroup[iTool][CONTENT_ID]			= VALID
	_iEnabledContentParameter[iTool][ID_SHOW]			= VALID

	! Element Name --------------------------------------------------------------------

	_iEnabledContentGroup[iTool][CONTENT_ELEMENT_NAME]	= VALID
	_iEnabledContentParameter[iTool][NAME_SHOW]			= VALID

	! Elevation -----------------------------------------------------------------------

	if iTool = (ELEM_TYPE_OBJECT	+1) |\
		iTool = (ELEM_TYPE_LAMP		+1) |\
		iTool = (ELEM_TYPE_WINDOW	+1) |\
		iTool = (ELEM_TYPE_DOOR		+1) |\
		iTool = (ELEM_TYPE_WALL		+1) |\
		iTool = (ELEM_TYPE_COLUMN	+1) |\
		iTool = (ELEM_TYPE_SLAB		+1) |\
		iTool = (ELEM_TYPE_BEAM		+1) |\
		iTool = (ELEM_TYPE_CW		+1) then
		_iEnabledContentGroup[iTool][CONTENT_ELEV]		= VALID
		_iEnabledContentParameter[iTool][ELEV_SHOW]		= VALID
		_iEnabledContentParameter[iTool][ELEV_TOP]		= VALID
		_iEnabledContentParameter[iTool][ELEV_BOTT]		= VALID
	endif

	! Dimensions ----------------------------------------------------------------------

	if 	iTool <> (ELEM_TYPE_LABEL	+1) &\
		iTool <> (ELEM_TYPE_MORPH	+1) &\
		iTool <> (ELEM_TYPE_FILL	+1) then
		_iEnabledContentGroup[iTool][CONTENT_DIM]		= VALID
		_iEnabledContentParameter[iTool][DIM_SHOW]		= VALID
	endif

	if	iTool = (ELEM_TYPE_OBJECT	+1) |\
		iTool = (ELEM_TYPE_LAMP		+1) |\
		iTool = (ELEM_TYPE_COLUMN	+1) |\
		iTool = (ELEM_TYPE_BEAM		+1) |\
		iTool = (ELEM_TYPE_CWFRAME	+1) |\
		iTool = (ELEM_TYPE_CWPANEL	+1) |\
		iTool = (ELEM_TYPE_CWJUNCT	+1) |\
		iTool = (ELEM_TYPE_CWACC	+1) then

		_iEnabledContentParameter[iTool][DIM_WIDTH]		= VALID
		_iEnabledContentParameter[iTool][DIM_LENGTH]	= VALID
		_iEnabledContentParameter[iTool][DIM_HEIGHT]	= VALID
	endif

	if	iTool = (ELEM_TYPE_WINDOW	+1) |\
		iTool = (ELEM_TYPE_DOOR		+1) |\
		iTool = (ELEM_TYPE_SKYLIGHT	+1) then
		_iEnabledContentParameter[iTool][DIM_WIDTH]		= VALID
		_iEnabledContentParameter[iTool][DIM_HEIGHT]	= VALID
	endif

	if iTool = (ELEM_TYPE_WALL	+1) |\
		iTool = (ELEM_TYPE_CW	+1)  then
		_iEnabledContentParameter[iTool][DIM_LENGTH]	= VALID
		_iEnabledContentParameter[iTool][DIM_HEIGHT]	= VALID
		_iEnabledContentParameter[iTool][DIM_THICK]		= VALID
	endif

	if iTool = (ELEM_TYPE_SLAB		+1) |\
		iTool = (ELEM_TYPE_ROOF		+1) |\
		iTool = (ELEM_TYPE_SHELL	+1) then
		_iEnabledContentParameter[iTool][DIM_THICK]		= VALID
	endif

	if iTool = (ELEM_TYPE_MESH		+1) then
		_iEnabledContentParameter[iTool][DIM_HEIGHT]	= VALID
	endif

	! Surface Area ----------------------------------------------------------------------

	if iTool = (ELEM_TYPE_WALL		+1)  |\
		iTool = (ELEM_TYPE_COLUMN	+1)  |\ 
		iTool = (ELEM_TYPE_SLAB		+1)  |\
		iTool = (ELEM_TYPE_ROOF		+1)  |\
		iTool = (ELEM_TYPE_MESH		+1)  |\
		iTool = (ELEM_TYPE_BEAM		+1)  |\
		iTool = (ELEM_TYPE_SHELL	+1)  |\
		iTool = (ELEM_TYPE_MORPH	+1)  |\
		iTool = (ELEM_TYPE_FILL		+1) then
		_iEnabledContentGroup[iTool][CONTENT_SURF]		= VALID
		_iEnabledContentParameter[iTool][SURF_SHOW]		= VALID
	endif

	if iTool = (ELEM_TYPE_WALL		+1)  |\
		iTool = (ELEM_TYPE_SHELL	+1)  then
		_iEnabledContentParameter[iTool][SURF_EDGE]		= VALID
		_iEnabledContentParameter[iTool][SURF_REFSIDE]	= VALID
		_iEnabledContentParameter[iTool][SURF_OPSIDE]	= VALID
	endif

	if iTool = (ELEM_TYPE_COLUMN	+1)  then
		_iEnabledContentParameter[iTool][SURF_TOP]		= VALID
		_iEnabledContentParameter[iTool][SURF_BOTTOM]	= VALID
		_iEnabledContentParameter[iTool][SURF_SIDE]		= VALID
	endif

	if iTool = (ELEM_TYPE_SLAB		+1)  |\
		iTool = (ELEM_TYPE_ROOF		+1)  then
		_iEnabledContentParameter[iTool][SURF_TOP]		= VALID
		_iEnabledContentParameter[iTool][SURF_BOTTOM]	= VALID
		_iEnabledContentParameter[iTool][SURF_EDGE]		= VALID
	endif

	if iTool = (ELEM_TYPE_FILL		+1)  then
		_iEnabledContentParameter[iTool][SURF_TOP]		= VALID
	endif

	if iTool = (ELEM_TYPE_BEAM 		+1) then
		_iEnabledContentParameter[iTool][SURF_TOP]		= VALID
		_iEnabledContentParameter[iTool][SURF_BOTTOM]	= VALID
		_iEnabledContentParameter[iTool][SURF_EDGE]		= VALID
		_iEnabledContentParameter[iTool][SURF_REFSIDE]	= VALID
		_iEnabledContentParameter[iTool][SURF_OPSIDE]	= VALID
	endif

	if iTool = (ELEM_TYPE_MESH		+1) then
		_iEnabledContentParameter[iTool][SURF_TOP]		= VALID
		_iEnabledContentParameter[iTool][SURF_BOTTOM]	= VALID
		_iEnabledContentParameter[iTool][SURF_EDGE]		= VALID
	endif

	! Projected Area ----------------------------------------------------------------------

	if iTool = (ELEM_TYPE_WALL		+1) then
		_iEnabledContentGroup[iTool][CONTENT_AREA]		= VALID
		_iEnabledContentParameter[iTool][AREA_SHOW]		= VALID
	endif

	! Volume ----------------------------------------------------------------------

	if iTool = (ELEM_TYPE_WALL		+1)  |\
		iTool = (ELEM_TYPE_COLUMN	+1)  |\ 
		iTool = (ELEM_TYPE_SLAB		+1)  |\
		iTool = (ELEM_TYPE_ROOF		+1)  |\
		iTool = (ELEM_TYPE_MESH		+1)  |\
		iTool = (ELEM_TYPE_BEAM		+1)  |\
		iTool = (ELEM_TYPE_SHELL	+1)  |\
		iTool = (ELEM_TYPE_MORPH	+1)  then
		_iEnabledContentGroup[iTool][CONTENT_VOL]		= VALID
		_iEnabledContentParameter[iTool][VOLUME_SHOW]	= VALID
	endif

	! Skin List -----------------------------------------------------------------------

	if iTool = (ELEM_TYPE_LABEL		+1) |\
		iTool = (ELEM_TYPE_WALL		+1) |\
		iTool = (ELEM_TYPE_SLAB 	+1) |\
		iTool = (ELEM_TYPE_ROOF		+1) |\
		iTool = (ELEM_TYPE_SHELL	+1) |\
		iTool = (ELEM_TYPE_FILL		+1)  then
		_iEnabledContentGroup[iTool][CONTENT_SKIN]		= VALID
		_iEnabledContentParameter[iTool][SKIN_SHOW]		= VALID
	endif

	! Structure Name ------------------------------------------------------------------

	if 	iTool = (ELEM_TYPE_WALL		+1) |\
		iTool = (ELEM_TYPE_SLAB		+1) |\
		iTool = (ELEM_TYPE_ROOF		+1) |\
		iTool = (ELEM_TYPE_SHELL	+1) |\
		iTool = (ELEM_TYPE_COLUMN	+1) |\ 
		iTool = (ELEM_TYPE_BEAM		+1) |\
		iTool = (ELEM_TYPE_MESH		+1) |\ 
		iTool = (ELEM_TYPE_MORPH	+1) |\
		iTool = (ELEM_TYPE_FILL		+1) then
		_iEnabledContentGroup[iTool][CONTENT_STRUCT_NAME]	= VALID
		_iEnabledContentParameter[iTool][STRUCT_NAME_SHOW] = VALID
	endif

	! Surface / Material ------------------------------------------------------------------

	if 	iTool = (ELEM_TYPE_WALL		+1) |\
		iTool = (ELEM_TYPE_SLAB		+1) |\
		iTool = (ELEM_TYPE_ROOF		+1) |\
		iTool = (ELEM_TYPE_SHELL	+1) |\
		iTool = (ELEM_TYPE_COLUMN	+1) |\ 
		iTool = (ELEM_TYPE_BEAM		+1) |\
		iTool = (ELEM_TYPE_MESH		+1) |\ 
		iTool = (ELEM_TYPE_MORPH	+1) then 
		_iEnabledContentGroup[iTool][CONTENT_MATERIAL]		= VALID
		_iEnabledContentParameter[iTool][MATERIAL_SHOW]		= VALID
	endif

	! Custom Text ---------------------------------------------------------------------

	_iEnabledContentGroup[iTool][CONTENT_CUSTOM]		= VALID
	_iEnabledContentParameter[iTool][CUSTOM_SHOW]		= VALID

next iTool


! ==============================================================================
! UI Display and Ordering
! ==============================================================================

EMPTY		= -1
INVALID		= 0
VALID		= 1	! valid
RELATIVE	= 2 ! valid only at UI  but there is another invalid tool (need relativ complement sign to display)


dim _iDisplayContentGroupUI[]
dim _iDisplayContentGroup2D[]
	for iIni = 1 to _maxContentValues
		_iDisplayContentGroupUI[iIni] = EMPTY
		_iDisplayContentGroup2D[iIni] = EMPTY
	next iIni

for iTool = 1 to _numTool
	if _bToolFilter[iTool] then
		for iContent = 1 to _maxContentValues
			if _iEnabledContentGroup[iTool][iContent] then
				if _iDisplayContentGroupUI[iContent] = EMPTY then _iDisplayContentGroupUI[iContent] = VALID
				if _iDisplayContentGroupUI[iContent] = INVALID then _iDisplayContentGroupUI[iContent] = RELATIVE
			else
				if _iDisplayContentGroupUI[iContent] = EMPTY then _iDisplayContentGroupUI[iContent] = INVALID
				if _iDisplayContentGroupUI[iContent] = VALID then _iDisplayContentGroupUI[iContent] = RELATIVE
			endif
		next iContent
	endif
next iTool

! numbers of UI valid content groups
_numShownContentGroupUI = 0
for iContent = 1 to _maxContentValues
	if _iDisplayContentGroupUI[iContent] > INVALID and _bShowContent[iContent] then
		_numShownContentGroupUI = _numShownContentGroupUI + 1
	endif
next iContent

! numbers of 2D valid content groups depend on parent tool
for iContent = 1 to _maxContentValues
	if _iEnabledContentGroup[_iParentTool][iContent] = VALID &\
		_iDisplayContentGroupUI[iContent] > INVALID &\
		 _bShowContent[iContent] then

		_iDisplayContentGroup2D[iContent] = VALID

	else
		_iDisplayContentGroup2D[iContent] = INVALID
	endif
next iContent

dim _iDisplayContentParametersUI[]
dim _iDisplayContentParameters2D[]
	for iIni = 1 to _numContentParameter
		_iDisplayContentParametersUI[iIni] = EMPTY
		_iDisplayContentParameters2D[iIni] = EMPTY
	next iIni

for iTool = 1 to _numTool
	if _bToolFilter[iTool] then
		for iContentParams = 1 to _numContentParameter
			if _iEnabledContentParameter[iTool][iContentParams] then
				if _iDisplayContentParametersUI[iContentParams] = EMPTY then _iDisplayContentParametersUI[iContentParams] = VALID
				if _iDisplayContentParametersUI[iContentParams] = INVALID then _iDisplayContentParametersUI[iContentParams] = RELATIVE
			else
				if _iDisplayContentParametersUI[iContentParams] = EMPTY then _iDisplayContentParametersUI[iContentParams] = INVALID
				if _iDisplayContentParametersUI[iContentParams] = VALID then _iDisplayContentParametersUI[iContentParams] = RELATIVE
			endif
		next iContentParams
	endif
next iTool

for iContentParams = 1 to _numContentParameter
	if _iEnabledContentParameter[_iParentTool][iContentParams] and _iDisplayContentParametersUI[iContentParams] > INVALID then
		_iDisplayContentParameters2D[iContentParams] = VALID
	else
		_iDisplayContentParameters2D[iContentParams] = INVALID
	endif
next iContentParams

! for function ordering tablap
dim _iDisplayOrderingGroupUI[]
for iContent = 1 to _maxContentValues
	if _iDisplayContentGroupUI[iContent] > INVALID and _bShowContent[iContent] then
		_iDisplayOrderingGroupUI[iContent] = VALID
	else
		_iDisplayOrderingGroupUI[iContent] = INVALID
	endif
next iContent


! Surface Specialization
! =============================================================================

bMorphSurfacePlacehold = 0

if iValueSumSurface = SUMSURFACE_ALL then
	bAddTopSurface = 1
	bAddBottomSurface = 1
	bAddSideSurface = 1
	bAddEdgeSurface = 1
	bAddRefSideSurface = 1
	bAddOppSideSurface = 1
else
	if GLOB_ELEM_TYPE = ELEM_TYPE_MORPH then
		bMorphSurfacePlacehold = 1
	endif
endif

_bLockLinearUnitDecimal	= (iWorkUnitLinear = DIMUNIT_INCH | iWorkUnitLinear = DIMUNIT_PROJECT)
_bLockAreaUnitDecimal	= (iWorkUnitArea = SURFUNIT_SQFT | iWorkUnitArea = SURFUNIT_SQINCH | iWorkUnitArea = SURFUNIT_PROJECT)
_bLockVolumeUnitDecimal	= (iWorkUnitVolume = VOLUNIT_PROJECT)

