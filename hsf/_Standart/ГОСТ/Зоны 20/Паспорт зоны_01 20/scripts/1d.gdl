

! Zone Content
! -------------------------------------------------------------------------
! ==============================================================================
! Content
! 1 : Zone Name				->	title
! 2 : Zone No.				->	title
! 3 : Flooring				->	title
! 4 : Room Area				->	title + value with unit
! 5 : Perimeter				->	title + value with unit
! 6 : Ceiling Height		->	title + value with unit
! ==============================================================================

maxRowNumber = 6

dim contentZoneProperty[6][2]
for i = 1 to maxRowNumber
	for j = 1 to 2
		contentZoneProperty[i][j] = ""
	next j
next i

ORDER_ZONE_NAME		= 1
ORDER_ZONE_NUMBER	= 2
ORDER_FLOORING		= 3
ORDER_ROOM_AREA		= 4
ORDER_PERIMETER		= 5
ORDER_CEIL_HEIGHT	= 6

iDataName	= 1
iDataValue	= 2

dim bParam[6]
	bParam[ORDER_ZONE_NAME]		= name
	bParam[ORDER_ZONE_NUMBER]	= num
	bParam[ORDER_FLOORING]		= floor
	bParam[ORDER_ROOM_AREA]		= area
	bParam[ORDER_PERIMETER]		= peri
	bParam[ORDER_CEIL_HEIGHT]	= ceil_s

! Future Development for quantity macro
_iQuantityString	= 0
_iQuantityLinear	= 1 	! Linear_dimension
_iQuantityCalcLength= 10 	! Calc_length_unit
_iQuantityArea		= 9 	! Area_dimension
_iQuantityCalcArea	= 11 	! Calc_area_unit
_iQuantityVolume	= 12	! Calc_volume_unit
_iQuantityLevel		= 5		! Level_dimension

dim iQuantitiyFormats[6]
	iQuantitiyFormats[ORDER_ZONE_NAME]		= _iQuantityString
	iQuantitiyFormats[ORDER_ZONE_NUMBER]	= _iQuantityString
	iQuantitiyFormats[ORDER_FLOORING]		= _iQuantityString
	iQuantitiyFormats[ORDER_ROOM_AREA]		= _iQuantityArea
	iQuantitiyFormats[ORDER_PERIMETER]		= _iQuantityLinear
	iQuantitiyFormats[ORDER_CEIL_HEIGHT]	= _iQuantityLinear

bCalculateAreaFormat = 0
for i = 1 to maxRowNumber
	if bParam[i] then
		if iQuantitiyFormats[i] = _iQuantityArea then
			bCalculateAreaFormat = 1
		endif
	endif
next i

bCalculateLengthFormat = 0
_useCalcUnit = 0
for i = 1 to maxRowNumber
	if bParam[i] then
		if iQuantitiyFormats[i] = _iQuantityLinear then
			bCalculateLengthFormat = 1
		endif
	endif
next i


! Linear String Form and Unit
! -------------------------------------------------------------------------
linearFormat	= ""
linearUnit		= ""

if bCalculateLengthFormat then
	rrr = request("Linear_dimension", "", linearFormat)
	if strstr (linearFormat, "m") then
		! the most common case
		linearUnit = " м"
		if strstr(linearFormat, "cm")	then linearUnit = " cм"
		if strstr(linearFormat, "mm")	then linearUnit = " мм"
	else
		if strstr(linearFormat, "i") | strstr (linearFormat, "f") then linearUnit = ""
	endif
endif

! Area String Form and Unit
! -------------------------------------------------------------------------
areaFormat	= ""
areaUnit	= ""

if bCalculateAreaFormat then
	rrr = request ("Area_dimension", "", areaFormat)
	if strstr (areaFormat, "sqm") then
		! the most common case
		areaUnit= " m2"
		if strstr (areaFormat, "sqmm")	then areaUnit = " mm2"
	else
		if strstr (areaFormat, "sqcm")	then areaUnit = " cm2"
		if strstr (areaFormat, "sqf")	then areaUnit = " sq ft"
		if strstr (areaFormat, "sqi")	then areaUnit = " sq inch"
	endif
endif

if bParam[ORDER_ZONE_NAME] then
	contentZoneProperty[ORDER_ZONE_NAME][iDataName] = "название зоны"	!`Zone Name`

	catname = ""
	catcode = ""
	_ValueName = ""
	rrr=request("Zone_category", "", catname, catcode)

	! Display Category Name as Zone name
	if cn_zn then			
		_ValueName = catname
	else
		_ValueName = ROOM_NAME
	endif
	! Prints "BGF" to the stamp  
	if catcode = "0 BGF" then
		_ValueName = "ОБЩАЯ ПЛОЩАДЬ"
	endif

	contentZoneProperty[ORDER_ZONE_NAME][iDataValue] = _ValueName
endif
if bParam[ORDER_ZONE_NUMBER] then
	contentZoneProperty[ORDER_ZONE_NUMBER][iDataName] = "номер зоны"	!`Zone Number`
	contentZoneProperty[ORDER_ZONE_NUMBER][iDataValue] = ROOM_NUMBER
endif
if bParam[ORDER_FLOORING] then
	contentZoneProperty[ORDER_FLOORING][iDataName] = "сведения о поле"	!`Flooring`
	contentZoneProperty[ORDER_FLOORING][iDataValue] = floor_f
endif
if bParam[ORDER_ROOM_AREA] then
	contentZoneProperty[ORDER_ROOM_AREA][iDataName] = "площадь"	!`Area`
	_contArea = str (areaFormat, ROOM_AREA)
	if not(T) then
		_contArea = "A: " +  _contArea + areaUnit
	endif
	contentZoneProperty[ORDER_ROOM_AREA][iDataValue] = _contArea
endif
if bParam[ORDER_PERIMETER] then
	contentZoneProperty[ORDER_PERIMETER][iDataName] = "периметр"	!`Perimeter`
	_contPerim = str (linearFormat, ROOM_PERIM)
	if not(T) then
		_contPerim = "P: " + _contPerim + linearUnit
	endif
	contentZoneProperty[ORDER_PERIMETER][iDataValue] = _contPerim
endif
if bParam[ORDER_CEIL_HEIGHT] then
	contentZoneProperty[ORDER_CEIL_HEIGHT][iDataName] = "высоту потолка"	! `Ceiling Height`
	_contHeight = str (linearFormat, ROOM_HEIGHT)
	if not(T) then
		_contHeight = "H: " + _contHeight + linearUnit
	endif
	contentZoneProperty[ORDER_CEIL_HEIGHT][iDataValue] = _contHeight
endif

