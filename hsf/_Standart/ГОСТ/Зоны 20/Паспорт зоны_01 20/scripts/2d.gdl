!Zone_Stamp_01NGK / Â©GRAPHISOFT 1996/ VL

define style  "idestyle"  AC_TextFont_1 , ROOM_LSIZE , 1, 1
mul2 GLOB_SCALE/1000,GLOB_SCALE/1000
style idestyle

rrr=request("Height_of_style","idestyle", shight)


IF not(T) then
	max_len = 0
	_numRow = 0
	_offsetFrame = 3
	for i = 1 to maxRowNumber
		_xWidth = 0
		if bParam[i] then
			if i = ORDER_ZONE_NAME then
				_xWidth = stw(contentZoneProperty[i][iDataValue])
			else
				_xWidth = stw(contentZoneProperty[i][iDataValue]) + _offsetFrame
			endif
			_numRow = _numRow + 1
		endif
		if max_len < _xWidth then max_len = _xWidth
	next i
	max_h = - _numRow * shight

	vlen = - shight
	rot2 w
	add2 -max_len/2, vlen - max_h/2

	for i = 1 to maxRowNumber
		if bParam[i] then
			if i = ORDER_ZONE_NAME then
				text2 0, -vlen, contentZoneProperty[i][iDataValue]	
			else
				vlen = vlen + shight
				text2 2, -vlen, contentZoneProperty[i][iDataValue]	
			endif
		endif
	next i
	_numRowWithoutName = _numRow - bParam[ORDER_ZONE_NAME]
	vlen = vlen+shight

	hotspot2 0, shight
	hotspot2 max_len, shight
	hotspot2 0, max_h + shight
	hotspot2 max_len, max_h + shight
	hotspot2 max_len/2, max_h/2 + shight
	if hspots then
	   hotspot2 max_len/2, shight
	   hotspot2 max_len/2, max_h + shight
	   hotspot2 0, max_h/2 + shight
	   hotspot2 max_len, max_h/2 + shight
	endif

	if box_y and _numRowWithoutName > 0 then

	   line2  0 , 0, 0, -vlen
	   line2  max_len , 0, max_len , -vlen
	   for i = 0 to _numRowWithoutName
		  line2 0,-i*shight, max_len, -i*shight
	   next i
	endif
	end
else

	define style  "idestyle1"  AC_TextFont_1 , ROOM_LSIZE , 4, 1
	style idestyle1
	rot2 w
	n=110
	
	dim _xoffset[6]
		_xoffset[ORDER_ZONE_NAME]		= 15
		_xoffset[ORDER_ZONE_NUMBER]		= 2
		_xoffset[ORDER_FLOORING]		= 115
		_xoffset[ORDER_ROOM_AREA]		= 95
		_xoffset[ORDER_PERIMETER]		= 115 + 30 * bParam[ORDER_FLOORING]
		_xoffset[ORDER_CEIL_HEIGHT]		= 115 + 30 * bParam[ORDER_FLOORING] + 30 * bParam[ORDER_PERIMETER]

	for i = 1 to maxRowNumber
		if bParam[i] then
			text2 _xoffset[i], -U/2, contentZoneProperty[i][iDataValue]
		endif
		if i = ORDER_FLOORING | i = ORDER_PERIMETER | i = ORDER_CEIL_HEIGHT then
			if box_y and n then
			   line2  n+30 , 0, n+30, -U
			   line2  n , 0, n+30, 0
				line2  n , -U, n+30, -U
			endif
			n=n+30
		endif
	next i

	if box_y and n then

	   line2  0 , 0, 0, -U
	   line2  10 , 0, 10, -U
	   line2  90 , 0, 90, -U
	   line2  110 , 0, 110, -U
	   line2  0 , 0, 110,0
	   line2  0 , -U, 110, -U
	endif
	end
endif


end



