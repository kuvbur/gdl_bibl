DEFINE STYLE DetailTextBig AC_TextFont_1, AC_TextSize_1, 7, 0
DEFINE STYLE DetailTextSmallA AC_TextFont_1, CS_TextSize_2, 7, 0
DEFINE STYLE DetailTextSmallB AC_TextFont_1, CS_TextSize_2, 2, 0
unID=0

Xo=0: Yo=0
IF abs(AC_RefCoord[1][1]) > EPS THEN add2 AC_RefCoord[1][1], 0
IF abs(AC_RefCoord[1][2]) > EPS THEN add2 0, AC_RefCoord[1][2]

t1=AC_MarkerText_1:	t2=CS_Markertext2: t3=AC_MarkerText_3: t4=''
IF GS_ShowSheetNo THEN
	IF AC_PM_AutoSheet THEN
		t4='лист '
		IF AC_MarkerText_3="" THEN t3 = "PM"
	ELSE
		t3 = GS_SheetNoEdit
	ENDIF
ELSE
	IF NOT(AC_PM_AutoSheet) THEN
		t3 = ""
	ENDIF
ENDIF

!<-Начало: Отрисовка текста и полки->
! Вычисления
IF t2 <> "" THEN t2 = "(" + t2 + ")"
STYLE DetailTextBig: text_l1 = STW(t1)
STYLE DetailTextSmallA: text_l2 = STW(t2)
STYLE DetailTextSmallB: text_l3 = STW(t3)
text_l12 = text_l1 + text_l2 + CS_TTOffset
text_addpos=abs(text_l12-text_l3)/2
IF text_l12>text_l3 THEN
	texttop_pos=CS_ATOffset
	textbot_pos=CS_ATOffset+text_addpos
ELSE
	texttop_pos=CS_ATOffset+text_addpos
	textbot_pos=CS_ATOffset
ENDIF

! Отрисовка
IF Shelf_sX<0 THEN Shelf_Dir=-1 ELSE Shelf_Dir=1
Shelf_length=(min(texttop_pos, textbot_pos) + max(text_l12, text_l3) + CS_ATOffset)/1000*GLOB_SCALE
Shelf_eX=Shelf_sX + Shelf_Dir*Shelf_length
Shelf_eY=Shelf_sY
!STYLE DetailTextBig: TEXT2 min(Shelf_sX, Shelf_eX) + texttop_pos/1000*A_, Shelf_sY + CS_LTOffset/1000*A_, t1
!STYLE DetailTextSmallA: TEXT2 min(Shelf_sX, Shelf_eX) + (texttop_pos+text_l1+CS_TTOffset)/1000*A_, Shelf_sY + CS_LTOffset/1000*A_, t2
!STYLE DetailTextSmallB: TEXT2 min(Shelf_sX, Shelf_eX) + textbot_pos/1000*A_, Shelf_sY - CS_LTOffset/1000*A_, t3
STYLE DetailTextBig: TEXT2 min(Shelf_sX, Shelf_eX) + texttop_pos/1000*GLOB_SCALE, Shelf_sY + CS_LTOffset/1000*GLOB_SCALE, t1
!STYLE DetailTextSmallA: TEXT2 min(Shelf_sX, Shelf_eX) + (texttop_pos+text_l1+CS_TTOffset)/1000*A_, Shelf_sY + CS_LTOffset/1000*A_, t2
STYLE DetailTextSmallA: TEXT2 min(Shelf_sX, Shelf_eX) + (texttop_pos+text_l1+CS_TTOffset)/1000*GLOB_SCALE, Shelf_sY + CS_LTOffset/1000*GLOB_SCALE+(AC_TextSize_1-CS_TextSize_2)/4000*GLOB_SCALE, t2
STYLE DetailTextSmallB: TEXT2 min(Shelf_sX, Shelf_eX) + Shelf_length/2, Shelf_sY - CS_LTOffset/1000*GLOB_SCALE, t4+t3
LINE2 Shelf_sX, Shelf_sY, Shelf_eX, Shelf_eY

! Редактируемые узловые точки полки
HOTSPOT2 Xo, Yo+Shelf_sY, unID, Shelf_sX, 1+128 : unID=unID+1
HOTSPOT2 Xo+Shelf_sX, Yo+Shelf_sY, unID, Shelf_sX, 2 : unID=unID+1
HOTSPOT2 Xo-0.1, Yo+Shelf_sY, unID, Shelf_sX, 3 : unID=unID+1
HOTSPOT2 Xo+Shelf_sX, Yo, unID, Shelf_sY, 1+128 : unID=unID+1
HOTSPOT2 Xo+Shelf_sX, Yo+Shelf_sY, unID, Shelf_sY, 2 : unID=unID+1
HOTSPOT2 Xo+Shelf_sX, Yo-0.1, unID, Shelf_sY, 3 : unID=unID+1
!<-Конец: Отрисовка текста и полки->

!<-Начало: Отрисовка фигуры->
! Вычисления
R=min(abs(Oval_X), abs(Oval_Y))
L=max(abs(Oval_X), abs(Oval_Y))-R
if L/(L+R)<0.3 then
	R=R+L
	L=0
	Oval_X=R*abs(Oval_X)/Oval_X
	Oval_Y=R*abs(Oval_Y)/Oval_Y
endif

! Отрисовка
if abs(Oval_X)>=abs(Oval_Y) then	! горизонтальная фигура
	ARC2 Xo+L, Yo, R, -90, 90
	LINE2 Xo+L, Yo-R, Xo-L, Yo-R
	LINE2 Xo+L, Yo+R, Xo-L, Yo+R
	ARC2 Xo-L, Yo, R, 90, -90
else								! вертикальная фигура
	ARC2 Xo, Yo+L, R, 0, 180
	LINE2 Xo-R, Yo+L, Xo-R, Yo-L
	LINE2 Xo+R, Yo+L, Xo+R, Yo-L
	ARC2 Xo, Yo-L, R, 180, 0
endif

! Редактируемые узловые точки фигуры
HOTSPOT2 Xo, Yo+Oval_Y, unID, Oval_X, 1+128 : unID=unID+1
HOTSPOT2 Xo+Oval_X, Yo+Oval_Y, unID, Oval_X, 2 : unID=unID+1
HOTSPOT2 Xo-0.1, Yo+Oval_Y, unID, Oval_X, 3 : unID=unID+1
HOTSPOT2 Xo+Oval_X, Yo, unID, Oval_Y, 1+128 : unID=unID+1
HOTSPOT2 Xo+Oval_X, Yo+Oval_Y, unID, Oval_Y, 2 : unID=unID+1
HOTSPOT2 Xo+Oval_X, Yo-0.1, unID, Oval_Y, 3 : unID=unID+1
!<-Конец: Отрисовка фигуры->

!<-Начало: Отрисовка линии выноски->
! Вычисления
k=(Yo-Shelf_sY)/(Xo-Shelf_sX)
c=Shelf_sY*Xo-Yo*Shelf_sX
if abs(Oval_X)>abs(Oval_Y) then
	if Shelf_sY>0 then Yp=R else Yp=-R
	Xp=(Yp-c)/k
	if Xp<L and Xp>-L then goto 10
else
	if Shelf_sX>0 then Xp=R else Xp=-R
	Yp=k*Xp+c
	if Yp<L and Yp>-L then goto 10
endif

if Shelf_sY>0 then Yarc=Yo+L	else Yarc=Yo-L		! ищем пересечение с правой либо с левой окружностью
if Shelf_sX>0 then Xarc=Xo+L else Xarc=Xo-L			! ищем пересечение с верхней либо с нижней окружностью
if abs(Oval_X)>abs(Oval_Y) then Yarc=0 else Xarc=0	! горизонтальная либо вертикальная фигура

a=k^2+1
b=2*(k*c-k*Yarc-Xarc)
e=Xarc^2+c^2-2*c*Yarc+Yarc^2-R^2
D=(b^2-4*a*e)
if D<=0 then	! нет пересечения
	if abs(Oval_X)>abs(Oval_Y) then
		if Shelf_sY>0 then Yp=R else Yp=-R
		Xp=(Yp-c)/k
	else
		if Shelf_sX>0 then Xp=R else Xp=-R
	endif
else
	if Shelf_sX>0 then Xp=(-b+sqr(D))/(2*a) else Xp=(-b-sqr(D))/(2*a)
endif
Yp=k*Xp+c

! Отрисовка
10: LINE2 Xp, Yp, Shelf_sX, Shelf_sY
!<-Конец: Отрисовка линии выноски->

end
