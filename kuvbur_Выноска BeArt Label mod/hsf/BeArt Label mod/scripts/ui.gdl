
ui_dialog "Выносная надпись",444,296

name =  ""
n = REQUEST ("ASSOCLP_NAME", "", name)

nd1 = request("Assoclp_parvalue", "param_name_out", namindd1, typed1, flagd1, dim1d1, dim2d1, param_name_out)
if strstr(param_name_out[1],"!!Профиль",0)>0 and strstr(param_name_out[1],"Лист",0)=0 then
	nd1 = request("Assoclp_parvalue", "name_prof_i", namindd1, typed1, flagd1, dim1d1, dim2d1, name_prof_i)
else
	pr1 = strstr(name,"Двутавр") <> 0
	pr2 = strstr(name,"Квадратнопрямоуг") <> 0
	pr3 = strstr(name,"Круглая труба") <> 0
	pr4 = strstr(name,"Ручник") <> 0
	pr5 = strstr(name,"голок") <> 0
	pr6 = strstr(name,"Швеллер") <> 0
	prof = pr1 + pr2 + pr3 + pr4 + pr5 + pr6
endif

UI_PAGE 1, -1, "Отображение"
UI_INFIELD	"param_name",			5,5,160,15

sx = 5
sy = 140-10
UI_OUTFIELD	`Отображать в режиме`,sx,sy,130,15
UI_INFIELD	"upr_rezhim",			sx+155,sy,95,15

sy = sy + 22 : UI_OUTFIELD	`Отображать в режиме (доп)`,sx,sy,150,15
				UI_INFIELD	"upr_rezhim_1",			sx+155,sy,95,15

sy = sy + 22 : UI_OUTFIELD	`Без выноски`,sx+20,sy,120,15
				UI_INFIELD	"napd",			sx,sy,15,15

sy = sy + 22 : UI_OUTFIELD	`Сброс мультивыноски`,sx+20,sy,120,15
				UI_INFIELD	"reset_px",			sx,sy,15,15

UI_OUTFIELD	`Тип мультивыноски`,sx+20+150,sy,120,15
				UI_INFIELD	"type_px",			sx+20+150,sy+25,120,15

sy = sy + 22 : UI_OUTFIELD	`Стрелка, мм.`,sx,sy,70,15
				UI_INFIELD	"sizeArrow",	sx+80,sy,50,15

sy = sy + 22 : UI_INFIELD{3} "gs_style", sx, sy, 164,20,
							2, "_", 9,
							1, 140, 123, 164, 20,
							1, "Стрелка", "Стиль 1",
							2, "Стрелка (заливка)", "Стиль 2",
							3, "Точка (контур)", "Стиль 3",
							4, "Точка (заливка) ", "Стиль 4",
							5, "Без стрелки и точки", "Стиль 5",
							6, "Стильная стрелка", "Стиль 6",
							7, "Линия сечения", "Стиль 7",
							8, "Засечка", "Стиль 8",
							9, "Заливка", "Стиль 9"

UI_INFIELD	"_LABEL_ARROWHEAD_PEN",	sx+170,sy,50,20

if gs_style = "Стиль 9" or gs_style = "Стиль 7" then
	UI_INFIELD	"zast",		sx+170+55,sy,50,20 UI_tooltip 'Выступ за конструкцию'
	UI_INFIELD	"zast_mun",		sx+170+55+55,sy,20,20 UI_tooltip 'Ручная толщина конструкции'
	if zast_mun then UI_INFIELD	"PosArrow",		sx+170+130,sy,50,20 UI_tooltip 'Длина линии'
endif

gosub "hlp"

if param_name = tparam_name[SHOW_FLAG] then
	dim	parentPropertiesArray[]
	n = request ("Properties_Of_Parent", "", parentPropertiesArray)
	UI_GROUPBOX "Чтение состава из свойства", 5, 30, 253, 95
	UI_OUTFIELD	`Всегда использовать свойство`,	40,	50,	160,	16
	UI_INFIELD	`property_layer_force`, 15,	48,	20, 20 UI_tooltip 'Всегда использовать свойство'
	UI_INFIELD	`property_layer_name`, 10,	48+25,	245, 20 UI_tooltip 'Имя свойства с составом'
	if property_layer_name="Из списка ниже" then
		_stSelected	= ""
		_stPropID	= property_layer
		gosub "getPropertyName"
		ui_custom_popup_infield "property_layer", 10, 48+50, 245, 20,
			1, 3, 1, _stSelected,
			parentPropertiesArray UI_tooltip "Имя свойства с составом"
	endif
	h_box = 50
	if somestuff_read_type=1 then h_box = h_box + 23*5.8
	if somestuff_read_type=2 then h_box = h_box+15+(vardim1(somestuff_out)-1)*23+(vardim1(somestuff_out)=3)*20
	UI_GROUPBOX "Чтение из других элементов", 265, 30, 210, h_box
	sx = 275
	sy = 50
	UI_OUTFIELD	`Начать нумерацию с`,sx,sy,110,15
	UI_INFIELD	"numstart",			sx+115,sy,70,15
	sy = sy + 23
	UI_INFIELD	`somestuff_read_type`, sx,	sy,	190, 20 UI_tooltip 'Читать из привязанных элементов'
	if somestuff_read_type=1 then
		sy = sy + 23
		UI_OUTFIELD	`Поменять местами`,	sx+23,	sy+2,	170,	16
		UI_INFIELD	`element_swap`, sx,	sy,	20, 20 UI_tooltip 'Поменять местами'
		sy = sy + 23
		if property_layer_name="Из списка ниже" then
			UI_INFIELD	`somestuff_prop_1`, sx,	sy,	20, 20
			UI_OUTFIELD	`Состав СВЕРХУ`,	sx+23,	sy+2,	170,	16
			sy = sy + 23
			_stSelected	= ""
			_stPropID	= somestuff_infostr_1
			gosub "getPropertyName"
			ui_custom_popup_infield "somestuff_infostr_1", sx,	sy, 190, 20,
				1, 3, 1, _stSelected,
				parentPropertiesArray UI_tooltip "Состав СВЕРХУ"
			sy = sy + 23
			UI_INFIELD	`somestuff_prop_2`, sx,	sy,	20, 20
			UI_OUTFIELD	`Состав СНИЗУ`,	sx+23,	sy+2,	170,	16
			sy = sy + 23
			_stSelected	= ""
			_stPropID	= somestuff_infostr_2
			gosub "getPropertyName"
			ui_custom_popup_infield "somestuff_infostr_2", sx,	sy, 190, 20,
				1, 3, 1, _stSelected,
				parentPropertiesArray UI_tooltip "Состав СНИЗУ"
		else
			UI_INFIELD	`somestuff_prop_1`, sx,	sy,	20, 20
			UI_OUTFIELD	`Состав СВЕРХУ`,	sx+23,	sy+2,	170,	16
			sy = sy + 23
			UI_INFIELD	`somestuff_infostr_1`, sx,	sy,	190, 20 UI_tooltip 'Состав СВЕРХУ'
			sy = sy + 23
			UI_INFIELD	`somestuff_prop_2`, sx,	sy,	20, 20
			UI_OUTFIELD	`Состав СНИЗУ`,	sx+23,	sy+2,	170,	16
			sy = sy + 23
			UI_INFIELD	`somestuff_infostr_2`, sx,	sy,	190, 20 UI_tooltip 'Состав СНИЗУ'
		endif
	endif
	if somestuff_read_type=2 then
		sy = sy + 23
		UI_OUTFIELD	`Читать из привязанных элементов`,	290,	sy,	180,	16
		UI_INFIELD	`somestuff_read`, 270,	sy-2,	20, 20 UI_tooltip 'Читать из привязанных элементов'
		if strlen(somestuff_subguid_1)<6 and strlen(somestuff_subguid_2)<6 and strlen(somestuff_subguid_3)<6 then
			UI_OUTFIELD	`НЕТ ПРИВЯЗАННЫХ ЭЛЕМЕНТОВ`,	290,sy + 20,	180,	16
		else
			sx = 275
			sy = 50
			if vardim1(somestuff_out)=3 then
				sy = sy + 23
				UI_OUTFIELD	`Поменять местами`,	290,	sy,	180,	16
				UI_INFIELD	`element_swap`, 270,	sy-2,	20, 20 UI_tooltip 'Поменять местами'
			endif
			for i=1 to vardim1(somestuff_out)-1
				sy = sy + 23
				UI_INFIELD{3} element[i],	sx,	sy, 190,	20
			next i
		endif
	endif

	UI_PAGE 2, -1, tparam_name[SHOW_FLAG]
	sx = 10
	sy = 1
	UI_STYLE 0, 1
	UI_INFIELD	"str_format",	sx,	sy, 400, 30
	sy = sy + 30
	sx = sx + 20
	UI_STYLE 0, 1
	UI_OUTFIELD	`№ пред.`,	sx,	sy,	48,	20
	UI_OUTFIELD	`Содержание`,	sx+100,	sy,	80,	20
	UI_STYLE 0, 0
	UI_INFIELD	"reset_format",		sx+200,sy,15,15
	UI_OUTFIELD	`Сброс`,	sx+220,	sy,	40,	20
	UI_STYLE 0, 1
	UI_OUTFIELD	`Толщина`,	sx+275,	sy,	80,	20
	UI_OUTFIELD	`Замена штрих./мат.`,	sx+335,	sy,	120,	20

	UI_STYLE 0, 0
	SkiN=WALL_SKINS_NUMBER*(GLOB_ELEM_TYPE=5)+\
		SLAB_SKINS_NUMBER*(GLOB_ELEM_TYPE=7)+\
		ROOF_SKINS_NUMBER*(GLOB_ELEM_TYPE=8)
	for i=1 to 8!max_ins
		sy = sy + 23
		UI_INFIELD{3}	i_ins_after[i],	sx,	sy, 40,	20
		UI_INFIELD{3}	txt_ins_after[i][1],	sx+45,	sy, 220,	20
		UI_INFIELD{3}	txt_ins_after[i][2],	sx+270,	sy, 60,	20

		UI_INFIELD{3}	use_fil[i],	sx+400,	sy, 20,	20 UI_tooltip 'Замена штриховки'
		if use_fil[i] then UI_INFIELD{3}	fil_ins_after[i],	sx+335,	sy, 60,	20 UI_tooltip 'Замена штриховки'

		UI_INFIELD{3}	use_bmat[i],	sx+420,	sy, 20,	20 UI_tooltip 'Замена материала'
		if use_bmat[i] then UI_INFIELD{3}	bmat_ins_after[i],	sx+335,	sy, 60,	20 UI_tooltip 'Замена материала'
		if i_ins_after[i]<>0 then
			if i_ins_after[i]>0 and i_ins_after[i]<=SkiN+(SkiN=0) then
				if STRSTR(txt_ins_after[i][1], "*")=1 then
					ttype_str = "Зам"
				else
					ttype_str = "Изм"
				endif
			else
				ttype_str = "Нов"
			endif
			if STRSTR(txt_ins_after[i][1], "-*")=1 then ttype_str = "--"
			if STRSTR(txt_ins_after[i][1], "+*")=1 then ttype_str = "++"
			UI_OUTFIELD	ttype_str,	sx-25,	sy+4,	21,	20
		endif
	next i

	UI_PAGE 4, 2, "Настройка флажка"
	sx = 5
	sy = 30
	UI_OUTFIELD	`Мин. толщина, мм`,sx+40,sy-30,100,15
	UI_OUTFIELD	`Скрытия слоя`,sx,sy-10,75,15
	
	UI_OUTFIELD	`Скрытия толщины`,sx+80,sy-10,100,15
	UI_INFIELD	"min_lay_thk",			sx+5,sy+10,70,15
	UI_INFIELD	"min_thk",			sx+95,sy+10,70,15
	sy = sy + 35
	
	UI_OUTFIELD	`Изм. направл.`,sx+20,sy+1,70,15
	UI_INFIELD	"bo",			sx,sy,15,15

	UI_OUTFIELD	`Изменить нум.`,sx+20+80+20,sy+1,80,15
	UI_INFIELD	"chnum",			sx+20+80,sy,15,15

	sy = sy + 22
	UI_OUTFIELD	`Начать нумерацию с`,sx,sy+1,110,15
	UI_INFIELD	"numstart",			sx+115,sy,70,15
	sy = sy + 22
	UI_OUTFIELD	`Формат нум.`,sx,sy,80,15
	UI_INFIELD	"isnum",			sx+90,sy,95,15
	sy = sy + 22
	UI_OUTFIELD	`Объединять`,sx,sy,80,15
	UI_INFIELD	"merge_eq",			sx+90,sy,95,15
	sy = sy + 25
	UI_OUTFIELD	`Привязка толщин слоёв к краю полки`,sx+20,sy+1,210,15
	UI_INFIELD	"thk_alg",			sx,sy,15,15
	sy = sy + 22
	UI_OUTFIELD	`Преф. толщин`,sx,sy,80,15
	UI_OUTFIELD	`Ед. измерения`,sx+90,sy,95,15
	UI_OUTFIELD	`Суфф. толщин`,sx+190,sy,95,15
	sy = sy + 22
	UI_INFIELD	"thk_prefix",	sx,sy,80,15
	UI_INFIELD	"unittype",	sx+90,sy,95,15
	UI_INFIELD	"thk_suffix",	sx+190,sy,95,15
	sy = sy + 22
	UI_OUTFIELD	`Тип отображаемых слоёв`,sx,sy,150,15
	UI_OUTFIELD	`Суфф. строк`,sx+190,sy,95,15
	sy = sy + 22
	UI_INFIELD	"show_by_type",	sx,sy,80,15
	UI_INFIELD	"str_suffix",	sx+190,sy,95,15
	
	sx = 200
	sy = 0
	UI_OUTFIELD	`Надпись над флажком`,sx+50,sy,140,15
	UI_INFIELD	"add_param", sx,sy+20,160,15
	UI_INFIELD	"Shrift_add_param", sx+165,sy+20,80,15
	gosub "hlp"
	UI_PAGE 5, 2, "Шаблоны"
	sx = 0
	sy = 10
	for ii=1 to vardim1(out_var)
		if sy>240 then
			sx = sx + 220
			sy = 10
		endif
		UI_INFIELD{3} out_var_temp[ii],	sx,	sy, 70, 15
		UI_OUTFIELD	out_var[ii][2],sx+75,sy,140,15
		sy = sy + 25
	next ii
	gosub "hlp"
endif

if param_name = tparam_name[SHOW_PARAM] or param_name = tparam_name[SHOW_STRUCT] or param_name = tparam_name[SHOW_BYMVO] then

	sy = 30
	UI_OUTFIELD	`Форма маркера`,sx,sy,100,20
	UI_INFIELD	"type_f", sx,sy+20,100,20


	UI_PAGE 3, -1, "Параметры для отображения"
	sx = 30
	sy = 20
	if param_name = tparam_name[SHOW_PARAM] then
		UI_GROUPBOX "Первая строка", sx-10, sy-10, 400, 70
		UI_INFIELD	"par_fist_str",	sx,	sy, 380, 30
		isprof=0
		if strstr(param_name_out[1], "!!Профиль")>0 or prof then
			UI_INFIELD	"prof_fist_str",	sx,	sy+35, 20, 20
			isprof=20
		endif
!		UI_outFIELD	par_fist_str_out, sx+isprof, sy+40, 	380,	15
		sy = sy+80
		UI_GROUPBOX "Вторая строка", sx-10, sy-10, 400, 70
		UI_INFIELD	"par_sec_str",	sx,	sy, 380, 30
		if strstr(param_name_out[1], "!!Профиль")>0 or prof then UI_INFIELD	"prof_sec_str",	sx,	sy+35, 20, 20
!		UI_outFIELD	par_sec_str_out, sx+isprof, sy+40, 	380,	15
		sy = sy+80
	endif
	if param_name = tparam_name[SHOW_STRUCT] then
		sx = 20
		for ii=1 to vardim1(param_otobr)
			UI_OUTFIELD param_otobr[ii][1],	sx,	sy+10, 90, 30
			UI_OUTFIELD "1:",	sx+80,	sy, 10, 30
			UI_OUTFIELD "2:",	sx+80,	sy+20, 10, 30
			UI_INFIELD{3} param_otobr[ii][2],	sx+95,	sy-5, 280, 30
			UI_INFIELD{3} param_otobr[ii][3],	sx+95,	sy-5+20, 280, 30
			sy = sy+45
		next ii
		sx = 20
		sy = sy+10
	endif

	if param_name = tparam_name[SHOW_BYMVO] then
		sx = 20
		UI_OUTFIELD "Имя параметра ПМВ",	sx+220,	sy-20, 150, 20
		UI_INFIELD	"mvo_paramname",	sx+180,	sy, 260, 20

		UI_OUTFIELD "Имя ПМВ",	sx+40,	sy-20, 120, 20
		UI_INFIELD	"mvo_filename",	sx-10,	sy, 180, 20
		sy = sy+20
		UI_OUTFIELD "Значения параметра",	sx,	sy, 120, 20
		UI_OUTFIELD "Шаблоны строк",	sx+200,	sy, 120, 20
		sy = sy+20
		isvkl = 0 : if strstr(mvo_paramname, "Включение") then isvkl = 1
		for ii=1 to vardim1(param_otobr)*(isvkl=0)+2*isvkl
			UI_GROUPBOX "", sx-10, sy-10, 450, 53
			if isvkl then
				UI_OUTFIELD param_otobr[ii][1],	sx+50,	sy, 90, 20
			else
				UI_INFIELD{3} param_otobr[ii][1],	sx,	sy, 90, 20
			endif
			UI_INFIELD{3} mvo_show[ii],	sx,	sy+20, 90, 20
			if mvo_show[ii]="Скрывать выноску" then
				UI_INFIELD{3} mvo_show[ii],	sx+120,	sy+10, 310, 20
			else
				UI_OUTFIELD "1:",	sx+100,	sy, 10, 20
				UI_OUTFIELD "2:",	sx+100,	sy+20, 10, 20
				UI_INFIELD{3} param_otobr[ii][2],	sx+120,	sy, 310, 20
				UI_INFIELD{3} param_otobr[ii][3],	sx+120,	sy+20, 310, 20
!				UI_INFIELD{3} mvo_is_custom[ii],	sx+120+295,	sy+10, 20, 20
			endif
			sy = sy+46
		next ii
		UI_OUTFIELD "По умолчанию:",	sx+40,	sy+2, 80, 20
		UI_INFIELD	"mvo_defult",	sx+120,	sy, 180, 20
		sx = 20
		sy = sy+10
	endif


	gosub "hlp"
	UI_PAGE 6, 3, "Шаблоны"
	sx = 0
	sy = 10
	for i=1 to vardim1(param_name_out)
		if strstr(param_name_out[i], "!!")=0 then 
			if sy>240 then
				sx = sx + 220
				sy = 10
			endif
			UI_outFIELD param_name_out[i], sx, sy,350,20
			sy = sy + 25
		endif
	next i
	gosub "hlp"
endif


end
"hlp":
	UI_BUTTON UI_LINK, "Справка v"+version, 444-85,296-55,85,21,0,help_url
return

"getPropertyName":
	r = REQUEST ("Property_Name", _stPropID, _typeName, _groupName, _propertyName)
	if r > 2 then
		_stSelected = _propertyName
	else
		_stSelected = ""
	endif
return
