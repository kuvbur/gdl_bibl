
!Parameter script
!Enjoy true Art BeArt (beart@inbox.ru)
!Edited by kuvbur@gmail.com
dim var_m[]

VALUES "LibraryLangCode" "RUS", "INT"

values "upr_rezhim" tupr_rezhim
values "upr_rezhim_1" tupr_rezhim
VALUES "param_name" tparam_name!, CUSTOM
if param_name = "Откл" then parameters param_name = tparam_name[SHOW_FLAG]
values "mvo_show" tmvo_show

if param_name = tparam_name[SHOW_BYMVO] then
	VALUES "mvo_paramname" tmvo_paramname, CUSTOM
	if mvo_paramname = tmvo_paramname[1] or mvo_paramname = tmvo_paramname[2] then
		param_otobr[1][1] = "Вкл"
		param_otobr[2][1] = "Откл"
		param_otobr[3][1] = "-"
		param_otobr[4][1] = "-"
		parameters param_otobr=param_otobr
		LOCK "mvo_filename"
	endif
	if mvo_paramname = tmvo_paramname[3] or mvo_paramname = tmvo_paramname[4] then LOCK "mvo_filename"
	dim mvo_defult_m[] : n = 1
	mvo_defult_m[n] = tmvo_show[2]
	for i=1 to vardim1(param_otobr)
		if strlen(param_otobr[i][1])>0 and param_otobr[i][1]<>"-" and param_otobr[i][1]<>" " then
			n = n + 1
			mvo_defult_m[n] = param_otobr[i][1]
		endif
	next i
	VALUES "mvo_defult" mvo_defult_m
	dim mvo_values_[]
	for i=1 to vardim1(mvo_values)
		if strlen(mvo_values[i][1])<1 then
			mvo_values_[i] = mvo_values[i][2]
		else
			mvo_values_[i] = mvo_values[i][1]
		endif
	next i
	VALUES "mvo_filename" mvo_values_
else
	LOCK "mvo_paramname"
endif

!!============== Обработка данных от аддона =============================
VALUES{2} "somestuff_read_type" isomestuff_read_type, tsomestuff_read_type
if somestuff_read_type=2 then
	if property_layer_name = "Из списка ниже" then
		parameters somestuff_property_guid = property_layer
		parameters somestuff_property_name = ""
	else
		parameters somestuff_property_guid = ""
		parameters somestuff_property_name = property_layer_name
	endif
	values "element" somestuff_out
	
	if GLOB_MODPAR_NAME="element_swap" and element_swap then
		ttt_ = element[1]
		element[1] = element[2]
		element[2] = ttt_
		parameters element=element, element_swap = 0
	endif
	if strlen(somestuff_subguid_1)<6 and strlen(somestuff_subguid_2)<6 and strlen(somestuff_subguid_3)<6 then parameters somestuff_read = 0
else
	parameters somestuff_property_guid = ""
	parameters somestuff_property_name = ""
	parameters somestuff_read = 0
	if GLOB_MODPAR_NAME="element_swap" and element_swap then
		ttt_ = somestuff_infostr_1
		somestuff_infostr_1 = somestuff_infostr_2
		somestuff_infostr_2 = ttt_
		parameters somestuff_infostr_1=somestuff_infostr_1, somestuff_infostr_2=somestuff_infostr_2, element_swap = 0
	endif
	if not(somestuff_prop_1) then LOCK "somestuff_infostr_1"
	if not(somestuff_prop_2) then LOCK "somestuff_infostr_2"
endif
if param_name <> tparam_name[SHOW_FLAG] then parameters somestuff_read = 0

!!!========================================================================

VALUES "property_layer_name" "Из списка ниже", CUSTOM
VALUES "id_type" "Упрощённый ID", "Родительский ID", "Полный ID"
if param_name <> tparam_name[SHOW_STRUCT] and param_name <> tparam_name[SHOW_BYMVO] then
	LOCK 'param_otobr'
	HIDEPARAMETER 'param_otobr'
endif
if param_name = tparam_name[SHOW_FLAG] then PARAMETERS vin = 0
if param_name <> tparam_name[SHOW_FLAG] then PARAMETERS vin = 1
VALUES "unittype" ".0mm",".1mm", "Из глобальных настроек",CUSTOM
VALUES{2} "show_by_type" 0, "Все", 1, "Без отделки", 2, "Только ядро", 3, "Последний слой ядра"
values{2} "type_view" itype_view, ttype_view
values{2} "type_px" itype_px, ttype_px

VALUES "percentage" 0,25,50,75,100
VALUES "merge_eq" "Не объединять", " в %ns% слоя", "(%ns% слоя)" ,CUSTOM

if GLOB_MODPAR_NAME="tx" or GLOB_MODPAR_NAME="ty" then
	AngleT = 0
	if tx>EPS and ty>EPS then AngleT=ATN(ty/tx)
	if tx>EPS and ty<EPS then AngleT=ATN(ty/tx)+360
	if tx<EPS and abs(tx)>EPS then AngleT=ATN(ty/tx)+180
	if abs(tx)<EPS and ty>EPS then AngleT=90
	if abs(tx)<EPS and ty<EPS then AngleT=270
	AngleT=AngleT-90
	if AngleT+90>90 and AngleT+90<180 and PosA>0 then PosA =PosA * (-1)
	if AngleT+90>0 and AngleT+90<90 and PosA<0 then PosA =PosA * (-1)
	if AngleT+90>180 and AngleT+90<270 and PosA>0 then PosA =PosA * (-1)
	if AngleT+90>270 and AngleT+90<360 and PosA<0 then PosA =PosA * (-1)
	parameters PosA = PosA
endif

VALUES "add_param" "Нет", "%id%", "%m_id%", "%f_id%", CUSTOM 
values{2} "Shrift_add_param" itype_shrift_tip, ttype_shrift_tip

dim str_format_arr[] : i=0
i=i+1 : str_format_arr[i]="%sFill% %dtxt% %thk%"
i=i+1 : str_format_arr[i]="%sFill% %thk%"
i=i+1 : str_format_arr[i]="%dtxt% %thk%"
i=i+1 : str_format_arr[i]="%mname% %dtxt% %thk%"

VALUES "str_format" str_format_arr, CUSTOM
VALUES "isnum" "Откл", "%n%", "%n%.", "%n%)", CUSTOM 

if param_name <> tparam_name[SHOW_PARAM] and param_name <> tparam_name[SHOW_STRUCT] then
	HIDEPARAMETER "BeArtLabel_A"
else
	LOCK "par_fist_str_out", "par_sec_str_out"
	if param_name = tparam_name[SHOW_STRUCT] then
		LOCK "par_fist_str", "par_sec_str"
	else
		values "par_fist_str" par_str_arr, CUSTOM
		values "par_sec_str" par_str_arr, CUSTOM
	endif
endif

if napd then
	LOCK "reset_px", "sizeArrow", "gs_style"
endif

values{2} "type_f" itype_f, ttype_f
VALUES "angle" 0, 90, 180, 270
VALUES "angle" range [0,360]
VALUES "gs_style" 	`Стиль 1`,`Стиль 2`,`Стиль 3`,`Стиль 4`,`Стиль 5`,"Стиль 6",`Стиль 7`,`Стиль 8`,`Стиль 9`

parameters total_thk = total_thk
if not(total_thk) then 
	VALUES "PosArrow" RANGE (,1]
else
	VALUES "PosArrow" total_thk, RANGE (,1]
endif

if GLOB_MODPAR_NAME="pxx" then
	n_str_px=1
	for i = 1 to vardim1(pxx)
		flag_clear=0
		if abs(pxx[i][1]-tx)<=0.01 and abs(pxx[i][2]-ty)<=0.01 then flag_clear=1
		if abs(pxx[i][1]-xn)<=0.01 and abs(pxx[i][2]-yn)<=0.01 then flag_clear=1
		if abs(pxx[i][1]-start_px)<=0.01 and abs(pxx[i][2]-start_py)<=0.01 then flag_clear=1
		if abs(pxx[i][1]-0.05)<=0.01 and abs(pxx[i][2]-0.05)<=0.01 then flag_clear=1
		if flag_clear then
			parameters pxx[i][1]=start_px
			parameters pxx[i][2]=start_py
		else
			n_str_px=n_str_px+1
		endif
	next i
	parameters n_str_px=n_str_px
endif

if not(thk_alg) then HIDEPARAMETER "thk_alg_out"

if vardim1(txt_ins_after)<>max_ins or reset_format then
	dim emp_num[]
	dim emp_txt[][2]
	for i=1 to max_ins
		emp_num[i]=0
		emp_txt[i][1]="%sFill% %thk%"
		emp_txt[i][2]=""
	next i
	parameters txt_ins_after=emp_txt,i_ins_after=emp_num, fil_ins_after=emp_num, use_fil=emp_num, reset_format=0
endif

if reset_px then
	for i = 1 to vardim1(pxx)
		pxx[i][1]=start_px
		pxx[i][2]=start_py
	next i
	n_str_px=1
	parameters pxx=pxx, n_str_px=n_str_px, reset_px=0
endif

if param_name = tparam_name[SHOW_STRUCT] then
	param_otobr[1][1] = tupr_rezhim[4]
	param_otobr[2][1] = tupr_rezhim[2]
	param_otobr[3][1] = tupr_rezhim[3]
	parameters param_otobr=param_otobr
endif

parameters out_var_temp = out_var_temp
if vardim2(param_otobr)<3 then
	dim emp[][]
	for i=1 to vardim1(param_otobr)
		emp[i][1] = param_otobr[i][1]
		emp[i][2] = param_otobr[i][2]
		emp[i][3] = ""
	next i
	parameters param_otobr=emp
endif


parameters version = "27"
parameters help_url = "https://docs.google.com/document/d/e/2PACX-1vQSWSuZ6Yx13igD4XAakNAZvPefgMh5KFk7TrswtJaL8pfUtOPpwbcx2C2n8ZcVyNPCQqCoFOfy-bMD/pub"
