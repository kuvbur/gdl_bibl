
ui_dialog "Выносная надпись",444,296

UI_PAGE 1, -1, "Отображение"
UI_INFIELD	"param_name",			5,5,160,15

sx = 5
sy = 140-30
UI_OUTFIELD	`Отображать в режиме`,sx,sy,130,15
UI_INFIELD	"upr_rezhim",			sx+155,sy,95,15

sy = sy + 25 : UI_OUTFIELD	`Отображать в режиме (доп)`,sx,sy,160,15
				UI_INFIELD	"upr_rezhim_1",			sx+155,sy,95,15

sy = sy + 25 : UI_OUTFIELD	`Без выноски`,sx+20,sy,120,15
				UI_INFIELD	"napd",			sx,sy,15,15

sy = sy + 25 : UI_OUTFIELD	`Сброс мультивыноски`,sx+20,sy,120,15
				UI_INFIELD	"reset_px",			sx,sy,15,15

UI_OUTFIELD	`Тип мультивыноски`,sx+20+150,sy,120,15
				UI_INFIELD	"type_px",			sx+20+150,sy+25,120,15

sy = sy + 25 : UI_OUTFIELD	`Стрелка, мм.`,sx,sy,70,15
				UI_INFIELD	"sizeArrow",	sx+80,sy,50,15

sy = sy + 25 : UI_INFIELD{3} "gs_style", sx, sy, 164,20,
							2, "_", 9,
							1, 140, 123, 164, 20,
							1, "Стрелка", "Стиль 1",
							2, "Стрелка (заливка)", "Стиль 2",
							3, "Точка (контур)", "Стиль 3",
							4, "Точка (заливка) ", "Стиль 4",
							5, "Без стрелки и точки", "Стиль 5",
							6, "Стильная стрелка", "Стиль 6",
							7, "Линия сечения", "Стиль 7",
							8, "Засечка", "Стиль 8",
							9, "Заливка", "Стиль 9"

UI_INFIELD	"_LABEL_ARROWHEAD_PEN",	sx+170,sy,50,20

if gs_style = "Стиль 9" or gs_style = "Стиль 7" then
	UI_INFIELD	"zast",		sx+170+55,sy,50,20 UI_tooltip 'Выступ за конструкцию'
	UI_INFIELD	"zast_mun",		sx+170+55+55,sy,20,20 UI_tooltip 'Ручная толщина конструкции'
	if zast_mun then UI_INFIELD	"PosArrow",		sx+170+130,sy,50,20 UI_tooltip 'Длина линии'
endif

gosub "hlp"

if param_name = "Откл" then
	dim	parentPropertiesArray[]
	n = request ("Properties_Of_Parent", "", parentPropertiesArray)
	UI_OUTFIELD	`Свойство с составом`,	5+25,	35,	160,	16
	UI_INFIELD	`property_layer_force`, 5,	32,	20, 20 UI_tooltip 'Всегда использовать свойство'
	UI_INFIELD	`property_layer_name`, 5,	30+25,	160, 20 UI_tooltip 'Имя свойства с составом'
	_stSelected	= ""
	_stPropID	= property_layer
	gosub "getPropertyName"
	ui_custom_popup_infield "property_layer", 5, 30+25+25, 160, 20,
		1, 3, 1, _stSelected,
		parentPropertiesArray UI_tooltip "Имя свойства с составом"

	UI_PAGE 2, -1, "Состав конструкции"
	sx = 10
	sy = 1
	UI_STYLE 0, 1
	UI_INFIELD	"str_format",	sx,	sy, 400, 30
	sy = sy + 25
	UI_STYLE 2, 2
	UI_OUTFIELD	"-" + str_format_out+"-",	sx+10,	sy+2,	420,	20
	sy = sy + 25
	sx = sx + 20
	UI_STYLE 0, 1
	UI_OUTFIELD	`№ пред.`,	sx,	sy,	48,	20
	UI_OUTFIELD	`Содержание`,	sx+100,	sy,	80,	20
	UI_STYLE 0, 0
	UI_INFIELD	"reset_format",		sx+200,sy,15,15
	UI_OUTFIELD	`Сброс`,	sx+220,	sy,	40,	20
	UI_STYLE 0, 1
	UI_OUTFIELD	`Толщина`,	sx+275,	sy,	80,	20
	UI_OUTFIELD	`Замена штрих.`,	sx+335,	sy,	80,	20
	UI_STYLE 0, 0
	for i=1 to 8!max_ins
		sy = sy + 23
		UI_INFIELD{3}	i_ins_after[i],	sx,	sy, 40,	20
		UI_INFIELD{3}	txt_ins_after[i][1],	sx+45,	sy, 220,	20
		UI_INFIELD{3}	txt_ins_after[i][2],	sx+270,	sy, 60,	20
		UI_INFIELD{3}	fil_ins_after[i],	sx+335,	sy, 60,	20
		UI_INFIELD{3}	use_fil[i],	sx+400,	sy, 20,	20
		if i_ins_after[i]<>0 then
			if i_ins_after[i]>0 and i_ins_after[i]<=SkiN+(SkiN=0) then
				if STRSTR(txt_ins_after[i][1], "*")=1 then
					ttype_str = "Зам"
				else
					ttype_str = "Изм"
				endif
			else
				ttype_str = "Нов"
			endif
			if STRSTR(txt_ins_after[i][1], "-*")=1 then ttype_str = "--"
			if STRSTR(txt_ins_after[i][1], "+*")=1 then ttype_str = "++"
			UI_OUTFIELD	ttype_str,	sx-25,	sy+4,	21,	20
		endif
	next i

	UI_PAGE 4, 2, "Настройка флажка"
	sx = 5
	sy = 30
	UI_OUTFIELD	`Мин. толщина, мм`,sx+40,sy-30,100,15
	UI_OUTFIELD	`Скрытия слоя`,sx,sy-10,75,15
	
	UI_OUTFIELD	`Скрытия толщины`,sx+80,sy-10,100,15
	UI_INFIELD	"min_lay_thk",			sx+5,sy+10,70,15
	UI_INFIELD	"min_thk",			sx+95,sy+10,70,15
	sy = sy + 35
	
	UI_OUTFIELD	`Изм. направл.`,sx+20,sy+1,80,15
	UI_INFIELD	"bo",			sx,sy,15,15
	sy = sy + 22
	UI_OUTFIELD	`Изменить нум.`,sx+20,sy+1,80,15
	UI_INFIELD	"chnum",			sx,sy,15,15
	sy = sy + 22
	UI_OUTFIELD	`Формат нум.`,sx,sy,80,15
	UI_INFIELD	"isnum",			sx+90,sy,95,15
	sy = sy + 22
	UI_OUTFIELD	`Объединять`,sx,sy,80,15
	UI_INFIELD	"merge_eq",			sx+90,sy,95,15
	sy = sy + 25
	UI_OUTFIELD	`Привязка толщин слоёв к краю полки`,sx+20,sy+1,210,15
	UI_INFIELD	"thk_alg",			sx,sy,15,15
	sy = sy + 22
	UI_OUTFIELD	`Преф. толщин`,sx,sy,80,15
	UI_OUTFIELD	`Ед. измерения`,sx+90,sy,95,15
	UI_OUTFIELD	`Суфф. толщин`,sx+190,sy,95,15
	sy = sy + 22
	UI_INFIELD	"thk_prefix",	sx,sy,80,15
	UI_INFIELD	"unittype",	sx+90,sy,95,15
	UI_INFIELD	"thk_suffix",	sx+190,sy,95,15
	sy = sy + 22
	UI_OUTFIELD	`Тип слоёв`,sx,sy,80,15
	UI_OUTFIELD	`Суфф. строк`,sx+190,sy,95,15
	sy = sy + 22
	UI_INFIELD	"show_by_type",	sx,sy,80,15
	UI_INFIELD	"str_suffix",	sx+190,sy,95,15
	
	sx = 200
	sy = 0
	UI_OUTFIELD	`Надпись над флажком`,sx+50,sy,140,15
	UI_INFIELD	"add_param", sx,sy+20,160,15
	UI_INFIELD	"Shrift_add_param", sx+165,sy+20,80,15
	gosub "hlp"
	UI_PAGE 5, 2, "Шаблоны"
	sx = 0
	sy = 10
	for ii=1 to vardim1(out_var)
		if sy>240 then
			sx = sx + 220
			sy = 10
		endif
		UI_INFIELD{3} out_var_temp[ii],	sx,	sy, 70, 15
		UI_OUTFIELD	out_var[ii][2],sx+75,sy,140,15
		sy = sy + 25
	next ii
	gosub "hlp"
endif

if param_name = "Параметры" or param_name = "Отделка/модель/ядро" then

	sy = 30
	UI_OUTFIELD	`Форма маркера`,sx,sy,100,20
	UI_INFIELD	"type_f", sx,sy+20,100,20


	UI_PAGE 3, -1, "Параметры для отображения"
	sx = 30
	sy = 20
	if param_name = "Параметры" then
		UI_GROUPBOX "Первая строка", sx-10, sy-10, 400, 70
		UI_INFIELD	"par_fist_str",	sx,	sy, 380, 30
		isprof=0
		if strstr(param_name_out[1], "!!Профиль")>0 or prof then
			UI_INFIELD	"prof_fist_str",	sx,	sy+35, 20, 20
			isprof=20
		endif
!		UI_outFIELD	par_fist_str_out, sx+isprof, sy+40, 	380,	15
		sy = sy+80
		UI_GROUPBOX "Вторая строка", sx-10, sy-10, 400, 70
		UI_INFIELD	"par_sec_str",	sx,	sy, 380, 30
		if strstr(param_name_out[1], "!!Профиль")>0 or prof then UI_INFIELD	"prof_sec_str",	sx,	sy+35, 20, 20
!		UI_outFIELD	par_sec_str_out, sx+isprof, sy+40, 	380,	15
		sy = sy+80
	else
		sx = 20
		for ii=1 to vardim1(param_otobr)
			UI_OUTFIELD param_otobr[ii][1],	sx,	sy+10, 90, 30
			UI_OUTFIELD "1:",	sx+80,	sy, 10, 30
			UI_OUTFIELD "2:",	sx+80,	sy+20, 10, 30
			UI_INFIELD{3} param_otobr[ii][2],	sx+95,	sy-5, 200, 30
			UI_INFIELD{3} param_otobr[ii][3],	sx+95,	sy-5+20, 200, 30
			sy = sy+45
		next ii
		sx = 20
		sy = sy+10
	endif

	gosub "hlp"
	UI_PAGE 6, 3, "Шаблоны"
	sx = 0
	sy = 10
	for i=1 to vardim1(param_name_out)
		if strstr(param_name_out[i], "!!")=0 then 
			if sy>240 then
				sx = sx + 220
				sy = 10
			endif
			UI_outFIELD param_name_out[i], sx, sy,350,20
			sy = sy + 25
		endif
	next i
	gosub "hlp"
endif


end
"hlp":
	UI_BUTTON UI_LINK, "Справка v"+version, 444-85,296-60,85,21,0,help_url
return

"getPropertyName":
	r = REQUEST ("Property_Name", _stPropID, _typeName, _groupName, _propertyName)
	if r > 2 then
		_stSelected = _propertyName
	else
		_stSelected = ""
	endif
return
