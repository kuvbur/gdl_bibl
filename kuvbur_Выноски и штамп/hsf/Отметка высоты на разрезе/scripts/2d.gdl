
if auto_z and not(pl) then
	add2 0,-SYMB_POS_Y+symb-auto_smesh
	HOTSPOT2	0, 0, unID, auto_smesh, 1+128: unID=unID+1
	HOTSPOT2	0, auto_smesh, unID, auto_smesh,     2: unID=unID+1
	HOTSPOT2	0, -1, unID, auto_smesh,     3: unID=unID+1
	add2 0,auto_smesh
endif


if isreadeble then
	r = request ("View_Rotangle", "", _angleViewRot)
	_totalRotate	= (SYMB_ROTANGLE + _angleViewRot) MOD 360
	if (_totalRotate > (90 + EPS) & _totalRotate < (270 + EPS)) then
		rot2 180
		_nTrans = _nTrans + 1
	endif
endif

! ======================================================
	unID=1
	
! ======================================================
! Удержание ориентации маркера (верх/низ) при редактировании нескольких отметок сразу. (работает)
! Удержание разблокированного отступа полки на нужном растоянии при смене масштаба. (работает)
! Удержание разблокированного отступа полки на нужном растоянии
! при редактировании нескольких отметок сразу. (сделать неудалось)
! ======================================================
	IF txtOrientMarker = "Вверху" THEN
		IF orientMarker_sc > 0 AND orientMarker_sc < UpPos * PAPER_TO_MODEL   THEN	orientMarker_sc = UpPos * PAPER_TO_MODEL
		IF FixMarkerSize = 0 THEN orientMarker_sc  = MAX(UpPos * PAPER_TO_MODEL, orientMarker_sc)
		IF FixMarkerSize = 1 THEN orientMarker_fix = UpPos
		IF FixMarkerSize = 1 THEN orientMarker_sc  = UpPos * PAPER_TO_MODEL
	ENDIF
	IF txtOrientMarker = "Внизу" THEN
		IF orientMarker_sc < 0 AND orientMarker_sc > DownPos * PAPER_TO_MODEL THEN	orientMarker_sc = DownPos * PAPER_TO_MODEL
		IF FixMarkerSize = 0 THEN orientMarker_sc  = MIN(DownPos * PAPER_TO_MODEL, orientMarker_sc)
		IF FixMarkerSize = 1 THEN orientMarker_fix = DownPos
		IF FixMarkerSize = 1 THEN orientMarker_sc  = DownPos * PAPER_TO_MODEL
	ENDIF
	
! ======================================================
! Преобразования
! ======================================================
	IF FixMarkerSize THEN
		orientMarker_sc = orientMarker_fix * PAPER_TO_MODEL
		orientMarker = orientMarker_fix * PAPER_TO_MODEL
		orientMarker_fix = orientMarker_fix * PAPER_TO_MODEL
	ELSE
		orientMarker = orientMarker_sc
	ENDIF
	
	IF scalability = "Фиксированная" THEN
		Gap = Gap_fix * PAPER_TO_MODEL
		Gap_fix = Gap_fix * PAPER_TO_MODEL
		LineLength = LineLength_fix * PAPER_TO_MODEL
		LineLength_fix = LineLength_fix * PAPER_TO_MODEL
	ENDIF
	
! ======================================================
! Сокращения
! ======================================================
	sizeArrow   = sizeArrow * PAPER_TO_MODEL
	hFontStyle1 = hFontStyle1 * PAPER_TO_MODEL
	hFontStyle2 = hFontStyle2 * PAPER_TO_MODEL
	
! ======================================================
! Фоновая штриховка
! ======================================================
	DRAWINDEX 10
	IF bOpaque THEN
		FILL GLOB_FILL_INDEX_SOLID
		PEN penOpaque
			POLY2_B  5, 2, penOpaque, penOpaque,
					 0, orientMarker, 1,
					-LOpaque, orientMarker, 1,
					-LOpaque, orientMarker + hFontStyle1 + dx, 1,
					 0, orientMarker + hFontStyle1 + dx, 1,
					 0, orientMarker, -1
		if strlen(addText)>EPS then
			POLY2_B  5, 2, penOpaque, penOpaque,
					 0, orientMarker, 1,
					-LOpaqueDown, orientMarker, 1,
					-LOpaqueDown, orientMarker - hDownTextBlock * PAPER_TO_MODEL-dx_2, 1,
					 0, orientMarker - hDownTextBlock * PAPER_TO_MODEL-dx_2, 1,
					 0, orientMarker, -1
		endif
	ENDIF
	
! ======================================================
! Варианты выносной линии и точки на ней
! ======================================================
	DRAWINDEX 20
	PEN penLine
	
	IF NOT(wLine1) THEN
		HOTSPOT2 A, 0, unID: unID=unID+1
	ENDIF
	
	IF wLine2 THEN
		LINE2 -sizeArrow, 0, sizeArrow, 0
	ENDIF
	
	IF wLine3 THEN
			LINE2 -sizeArrow, 0, LineLength, 0
		IF scalability = "Фиксированная" THEN
			HOTSPOT2           0, 0, unID, LineLength, 1+128, LineLength_fix: unID=unID+1
			HOTSPOT2  LineLength, 0, unID, LineLength,     2, LineLength_fix: unID=unID+1
			HOTSPOT2          -1, 0, unID, LineLength,     3, LineLength_fix: unID=unID+1
		ELSE
			HOTSPOT2           0, 0, unID, LineLength, 1+128: unID=unID+1
			HOTSPOT2  LineLength, 0, unID, LineLength,     2: unID=unID+1
			HOTSPOT2          -1, 0, unID, LineLength,     3: unID=unID+1
		ENDIF
	ENDIF
	
	IF wLine4 THEN
		ADD2 A, 0
			LINE2 MAX(-Gap, -(A - sizeArrow)), 0,  -(A + sizeArrow), 0
		IF scalability = "Фиксированная" THEN
			HOTSPOT2                          0, 0, unID, Gap, 1+128, Gap_fix: unID=unID+1
			HOTSPOT2  MAX(-Gap, -A + sizeArrow), 0, unID, Gap,     2, Gap_fix: unID=unID+1
			HOTSPOT2                          1, 0, unID, Gap,     3, Gap_fix: unID=unID+1
		ELSE
			HOTSPOT2                          0, 0, unID, Gap, 1+128: unID=unID+1
			HOTSPOT2  MAX(-Gap, -A + sizeArrow), 0, unID, Gap,     2: unID=unID+1
			HOTSPOT2                          1, 0, unID, Gap,     3: unID=unID+1
		ENDIF
		DEL 1
	ENDIF
	
! ======================================================
! Полка, линия отступа и точка определения положения (верх/низ)
! ======================================================
	bSerif = 1
	IF iArrowType = 1 OR iArrowType = 3 THEN bSerif = 0
	
		LINE2 0, 0, 0, orientMarker + sizeArrow *bSerif *SGN(OrientMarker)
		LINE2 sizeArrow *bSerif, orientMarker, -LShelf, orientMarker
	
	IF FixMarkerSize THEN
		HOTSPOT2 0,               0, unID, orientMarker_sc, 1, orientMarker_fix: unID=unID+1
		HOTSPOT2 0, orientMarker_sc, unID, orientMarker_sc, 2, orientMarker_fix: unID=unID+1
		HOTSPOT2 0,              -1, unID, orientMarker_sc, 3, orientMarker_fix: unID=unID+1
	ELSE
		HOTSPOT2 0,               0, unID, orientMarker_sc, 1: unID=unID+1
		HOTSPOT2 0, orientMarker_sc, unID, orientMarker_sc, 2: unID=unID+1
		HOTSPOT2 0,              -1, unID, orientMarker_sc, 3: unID=unID+1
	ENDIF
	
! ======================================================
! Варианты стрелки
! ======================================================
	DRAWINDEX 50
	IF iArrowType < 6 THEN	PEN penArrow
	
		LINE2 0, 0, sizeArrow, sizeArrow *SGN(OrientMarker)
		LINE2 sizeArrow *(iArrowType > 4), sizeArrow *(iArrowType > 4) *SGN(OrientMarker), -sizeArrow, sizeArrow *SGN(OrientMarker)
	
	FILL 64 ! "Solid Fill"
	IF iArrowType > 2 AND iArrowType < 6 THEN
		  POLY2_  3, 7 + 32,
				  0, 0, 1,
				  sizeArrow, sizeArrow *SGN(OrientMarker), 1,
				 -sizeArrow *(iArrowType < 5), sizeArrow *SGN(OrientMarker), 1
	ENDIF
	
! ======================================================
! Звездочка
! ======================================================
	IF txtOption = "Специальный текст" OR DimOrigin > 0 OR RoundValue > 0.001 THEN
		IF GLOB_CONTEXT > 20 AND GLOB_CONTEXT < 30 THEN
			ADD2 hFontStyle1 /2, orientMarker + hFontStyle1 /2
				LINE2  0, -hFontStyle2 /3, 0, hFontStyle2 /3
				LINE2 -hFontStyle2 /3 *COS(30), -hFontStyle2 /3 *SIN(30), hFontStyle2 /3 *COS(30),  hFontStyle2 /3 *SIN(30)
				LINE2 -hFontStyle2 /3 *COS(30),  hFontStyle2 /3 *SIN(30), hFontStyle2 /3 *COS(30), -hFontStyle2 /3 *SIN(30)
			DEL 1
		ENDIF
	ENDIF
	
! ======================================================
! Тексты
! ======================================================

	PEN penFont
		RICHTEXT2 -sizeArrow, orientMarker + dx, "UpTextBlock"
		RICHTEXT2 -sizeArrow - 0.3 *sizeArrow *(SGN(OrientMarker) > EPS), orientMarker - hDownTextBlock * PAPER_TO_MODEL - dx_2, "DownTextBlock"
	
! ======================================================
	END
! ======================================================

