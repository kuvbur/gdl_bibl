
EPS=0.00001 !- погрешность
jt = 15
mask = 1+2+4+16+32
hotspot 0,0,0

ROTZ cornerZ !Поворот узла с заменой переменных размеров основы
If cornerZ > EPS And type_base = TYPE_PLATE_RECT  Then
	ws = A
	ds = B
	LA = consol_plate_left_B	!Размер консоли слева по длинной стороне (мм)
	RA = consol_plate_right_B	!Размер консоли справа по длинной стороне (мм)
	LB = consol_plate_left_L	!Размер консоли снизу по короткой стороне (мм)
	RB = consol_plate_right_L	!Размер консоли сверху по короткой стороне (мм)
Endif


!=============== Группа подливки под колонну: ===============
Group "gravy"
	if gravy_mat>0 then
		building_material gravy_mat !Назначение материала
		top_material = 0
		bSucceed = request{2} ("Building_Material_info", gravy_mat, "gs_bmat_surface", top_material)
	endif
	IF 	gravy_t<0.05 THEN gravy_t =0.05
	ADDZ -pl_t
	GOSUB "prof_gravy"
	n = NSP/3
	IF NSP>3 THEN EXTRUDE n, 0, 0, -gravy_t, mask, use(NSP)
	z=0
	gosub "set_hotspot"
		if NSP>3 then PLANE NSP/3, use(NSP)
		for i=1 to NSP step 3
			x=get(1) :	y=get(1) :  z=get(1)
			hotspot x, y, z
			hotspot x, y, z-gravy_t
		next i
	DEL 1
EndGroup

!============ Группа прямоугольной опорной плиты: ===============
Group "plate_rect"
	tmater = mater_plate : GOSUB "set_mat"
	GOSUB "prof_plate_rect"
	n = NSP/3
	if NSP>3 then EXTRUDE n, 0, 0, -pl_t, mask, use(NSP)
	z=0
	gosub "set_hotspot"
	if NSP>3 then PLANE NSP/3, use(NSP)
	for i=1 to NSP step 3
		x=get(1) :	y=get(1) :  z=get(1)
		hotspot x, y, z
		hotspot x, y, z-pl_t
	next i
EndGroup
!=============== Группа круглой опорной плиты: ==================
Group "plate_circle"
	tmater = mater_plate : GOSUB "set_mat" !Назначение материала сталь
	If (ws/2+LA) < ws Then RC = ws
	ADDZ -pl_t
	CYLIND pl_t, (ws/2+LA)
	DEL 1
	HOTSPOT (ws/2+LA), 0, 0
	HOTSPOT 0, (ws/2+LA), 0
	HOTSPOT -(ws/2+LA), 0, 0
	HOTSPOT 0, -(ws/2+LA), 0
EndGroup

!=============== Группа траверса: ===============
Group "traverse"
	tmater = mater_travers : GOSUB "set_mat" !Назначение материала
	if 	t_tr_h<EPS OR t_tr_h>tr_h then t_tr_h =0
	GOSUB "prof_traverse"
	n = NSP/3
	if NSP>3 then EXTRUDE n, 0, 0, tr_t, mask, use(NSP)
	z=0
	gosub "set_hotspot"
	if NSP>3 then PLANE NSP/3, use(NSP)
	for i=1 to NSP step 3
		x=get(1) :	y=get(1) :  z=get(1)
		hotspot x, y, z
		hotspot x, y, tr_t
	next i
		!Добавление сварки
	ang_axis_weld = 270 ! Угол поворота сварки
	pos_weld[1][1] = -ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = -ds*0.5-LB 	!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
		!Добавление сварки
	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = ds*0.5+RB 		!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
			!Добавление сварки
	ADDZ tr_t
	ang_axis_weld = 90 ! Угол поворота сварки
	pos_weld[1][1] = -ds*0.5-LB		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = ds*0.5+RB 		!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
	DEL 1
		!Добавление сварки
	pos_weld[1][1] = -ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0	 			!Начало, Z
	pos_weld[2][1] = -ds*0.5 		!Конец, X
	pos_weld[2][2] = tr_h			!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	IF LB < EPS THEN
		ang_axis_weld = 0 ! Угол поворота сварки
		gosub "set_weld_hands"  ! Вызов функции СВАРКИ
	ELSE
		ang_axis_weld = 0 ! Угол поворота сварки
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
	ENDIF
		!Добавление сварки
	pos_weld[1][1] = ds*0.5 		!Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0	 			!Начало, Z
	pos_weld[2][1] = ds*0.5 		!Конец, X
	pos_weld[2][2] = tr_h			!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	IF RB < EPS THEN
		ang_axis_weld = 0 ! Угол поворота сварки
		gosub "set_weld_hands"  ! Вызов функции СВАРКИ
	ELSE
		ang_axis_weld = 270 ! Угол поворота сварки
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
	ENDIF
EndGroup

!=================================================================
!================= НАЧАЛО ПОСТРОЕНИЯ БАЗЫ КОЛОННЫ ================


If type_gravy = TYPE_GRAVY_YES Then 			!Построение подливки под базу
	IF type_base = TYPE_PLATE_RECT Then
		gravy_v = (LA+ws+RA+gravy_L*2)*(LB+ds+RB+gravy_L*2)*gravy_t
		PLACEGROUP "gravy"
	Endif
	IF type_base = TYPE_PLATE_CIRCLE Then
		gravy_v = (LA*2+ws+gravy_L*2)^2*gravy_t
		PLACEGROUP "gravy"
	Endif
Endif
	!------ Прямоугольная плита ------!
IF type_base = TYPE_PLATE_RECT Then
	PLACEGROUP "plate_rect"

	If type_traverse = TYPE_TRAVERSE_1 AND edge_n <> EDGE_N_4_45 AND edge_n <> EDGE_N_8  AND edge_n <> EDGE_N_6 Then		!Построение 1ой траверсы
		ROTX 90
		ROTY 90
		ADDZ ws*0.5
		PLACEGROUP "traverse"
		DEL 3
	Endif

	If type_traverse = TYPE_TRAVERSE_2 AND edge_n <> EDGE_N_4_45 AND edge_n <> EDGE_N_8  AND edge_n <> EDGE_N_6 Then		!Построение 2х траверс
		ROTX 90
		ROTY 90
		ADDZ ws*0.5
		PLACEGROUP "traverse"
		DEL 1
		MULZ -1
		ADDZ ws*0.5
		PLACEGROUP "traverse"
		DEL 4
	Endif

	IF edge_n = EDGE_N_2 THEN										!Два ребра с одной стороны
		Li = RA
		ROTX 90
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 3
	ENDIF

	IF edge_n = EDGE_N_4 THEN										!Четыре ребра
		Li = RA
		ROTX 90
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		Li = LA
		ROTY 180
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 4
	ENDIF

	IF edge_n = EDGE_N_6 THEN										!Шесть ребрер
		type_traverse = TYPE_TRAVERSE_0
		Li = RA
		ROTX 90
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		Li = LA
		ROTY 180
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 4
		Li = LB
		ROTY -90
		ADD ds*0.5, 0, -ws/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ds*0.5, 0, -ws/2
		GOSUB "edge"
		DEL 4
	ENDIF

	IF edge_n = EDGE_N_8 THEN										!Восемь ребрер
		type_traverse = TYPE_TRAVERSE_0
		Li = RA
		ROTX 90
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		Li = LA
		ROTY 180
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ws*0.5, 0, -ds/2
		GOSUB "edge"
		DEL 4
		Li = LB
		ROTY -90
		ADD ds*0.5, 0, -ws/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ds*0.5, 0, -ws/2
		GOSUB "edge"
		DEL 3
		Li = RB
		ROTY 90
		ADD ds*0.5, 0, -ws/2
		GOSUB "edge"
		DEL 1
		MULZ -1
		ADD ds*0.5, 0, -ws/2
		GOSUB "edge"
		DEL 4
	ENDIF

	IF edge_n = EDGE_N_1 THEN										!Одно по центру
		Li = RA
		ROTX 90
		ADDX ws*0.5
		GOSUB "edge"
		DEL 2
	ENDIF

	IF edge_n = EDGE_N_1_1 THEN										!Два зеркально
		Li = RA
		ROTX 90
		ADD ws*0.5, 0, -edge_t/2
		GOSUB "edge"
		DEL 1
		Li = LA
		ROTY 180
		ADD ws*0.5, 0, -edge_t/2
		GOSUB "edge"
		DEL 3
	ENDIF

	IF edge_n = EDGE_N_4_45 THEN									!Четыре ребра 45 градусов
		type_traverse = TYPE_TRAVERSE_0
		ROTX 90
		ADD ws/2, 0, -ds/2
		ROTY ATN(RB/RA)
		ADDZ -edge_t/2
		Li = SQR(RA^2 + RB^2)-ed_t/2
		GOSUB "edge"
		DEL 3
		ADD ws/2, 0, ds/2
		ROTY -ATN(LB/RA)
		ADDZ -edge_t/2
		Li = SQR(RA^2 + LB^2)-ed_t/2
		GOSUB "edge"
		DEL 3
		MULX -1
		ADD ws/2, 0, ds/2
		ROTY -ATN(LB/LA)
		ADDZ -edge_t/2
		Li = SQR(LA^2 + LB^2)-ed_t/2
		GOSUB "edge"
		DEL 4
		MULX -1
		ADD ws/2, 0, -ds/2
		ROTY ATN(RB/LA)
		ADDZ -edge_t/2
		Li = SQR(LA^2 + RB^2)-ed_t/2
		GOSUB "edge"
	ENDIF
Endif

	!------ Круглая плита ------!
IF type_base = TYPE_PLATE_CIRCLE THEN
	Li = LA: RA=LA: LB=LA: RB=LA
	type_traverse = TYPE_TRAVERSE_0
	PLACEGROUP "plate_circle"

	IF edge_n = EDGE_N_4 THEN										!Четыре ребра
		ROTX 90
		ADD ws*0.5,0,-ed_t/2
		GOSUB "edge"
		DEL 1
		FOR j=1 To 3
			ROTY 90
			ADD ws*0.5,0,-ed_t/2
			GOSUB "edge"
			DEL 1
		Next j
	ENDIF

	IF edge_n = EDGE_N_6 THEN										!Шесть ребер
		ROTX 90
		ADD ws*0.5,0,-ed_t/2
		GOSUB "edge"
		DEL 1
		FOR j=1 To 5
			ROTY 60
			ADDX ws*0.5
			ADDZ -ed_t/2
			GOSUB "edge"
			DEL 2
		Next j
	ENDIF

	IF edge_n = EDGE_N_8 THEN										!Восемь ребер
		ROTX 90
		ADD ws*0.5,0,-ed_t/2
		GOSUB "edge"
		DEL 1
		FOR j=1 To 7
			ROTY 45
			ADDX ws*0.5
			ADDZ -ed_t/2
			GOSUB "edge"
			DEL 2
		Next j
	ENDIF
ENDIF

goto "3_end"
!================ ОКОНЧАНИЕ ПОСТРОЕНИЯ БАЗЫ КОЛОННЫ ==============
!=================================================================

!====================== Расстановка HOTSPOT: =====================
"set_hotspot":
	if NSP>3 then
		n_form = NSP
		for i=1 to n_form step 3
			x=get(1) :	y=get(1) :  jt=get(1)
			put x,y,z
		next i
	endif
return

!=============== Скрипт координаты подливки под колонны: ===============
"prof_gravy": !"Подливка под базой"
	IF type_base = TYPE_PLATE_CIRCLE Then 
		RA=LA
		LB=LA
		RB=LA
	ENDIF

		put -ws/2-LA-gr_L, -ds/2-LB-gr_L,jt
		put -ws/2-LA-gr_L, ds/2+RB+gr_L,jt
		put ws/2+RA+gr_L, ds/2+RB+gr_L,jt
		put ws/2+RA+gr_L, -ds/2-LB-gr_L,jt
return

!========== Скрипт координаты прямоугольной опорной плиты: =============
"prof_plate_rect": !"Плита базы"
		put -ws/2-LA, -ds/2-LB,jt
		put -ws/2-LA, ds/2+RB,jt
		put ws/2+RA, ds/2+RB,jt
		put ws/2+RA, -ds/2-LB,jt
return

!=============== Скрипт координаты траверсы: ===========================
"prof_traverse": !"Траверса"
		put -ds/2-LB, 0,jt
		put -ds/2-LB, t_tr_h,jt
		put -ds/2, tr_h,jt
		put ds/2, tr_h,jt
		put ds/2+RB, t_tr_h,jt
		put ds/2+RB, 0,jt
return

!=============== Скрипт координаты ребер: ===============================
"prof_edge": !"Ребро"
	IF type_traverse = TYPE_TRAVERSE_0 THEN
		itt = 0
	ELSE
		itt = tr_t: ed_h = tr_h
	ENDIF
	IF type_base = TYPE_PLATE_CIRCLE THEN ed_h = edge_h
		put Li, 0,jt
		put Li, t_ed_h,jt
		put itt+0.02, ed_h,jt
		put itt, ed_h,jt
		put itt, 0.02,jt
		put itt+0.02,0,jt
return

!=============== Группа ребра 90 градусов: ==========================
"edge": !Функция Ребро
	tmater = mater_edge : GOSUB "set_mat" !Назначение материала
	GOSUB "prof_edge"
	n = NSP/3
	if NSP>3 then EXTRUDE n, 0, 0, ed_t, mask, use(NSP)
	z=0
	gosub "set_hotspot"
	if NSP>3 then PLANE NSP/3, use(NSP)
	for i=1 to NSP step 3
		x=get(1) :	y=get(1) :  z=get(1)
		hotspot x, y, z
		hotspot x, y, ed_t
	next i
		!Добавление сварки
	ang_axis_weld = 0 ! Угол поворота сварки
	pos_weld[1][1] = itt+0.02 !Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = Li				!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
		!Добавление сварки
	ADDZ ed_t
	ang_axis_weld = 90 ! Угол поворота сварки
	pos_weld[1][1] = itt+0.02 !Начало, X
	pos_weld[1][2] = 0		 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = Li				!Конец, X
	pos_weld[2][2] = 0				!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
	DEL 1
			!Добавление сварки
	pos_weld[1][1] = itt			!Начало, X
	pos_weld[1][2] = 0.02	 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = itt			!Конец, X
	pos_weld[2][2] = ed_h			!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z

	IF type_traverse = TYPE_TRAVERSE_0 AND type_base <> TYPE_PLATE_CIRCLE Then ! Отсутсвие траверс !
		ang_axis_weld = 270 ! Угол поворота сварки
		gosub "set_weld_hands"  ! Вызов функции СВАРКИ
	ELSE
		ang_axis_weld = 270 ! Угол поворота сварки
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
	ENDIF
		!Добавление сварки
	ADDZ ed_t
	ang_axis_weld = 180 ! Угол поворота сварки
	pos_weld[1][1] = itt			!Начало, X
	pos_weld[1][2] = 0.02	 		!Начало, Y
	pos_weld[1][3] = 0 				!Начало, Z
	pos_weld[2][1] = itt			!Конец, X
	pos_weld[2][2] = ed_h			!Конец, Y
	pos_weld[2][3] = 0				!Конец, Z
	gosub "set_weld_auto" ! Вызов функции СВАРКИ
	DEL 1
return

!========================================================================
!======================= Вызов параметров сварик ========================
"set_weld_auto": ! Функция сварки
	type_weld = 0
	type_weld_ang = 0
	call "Сварка 3д" parameters type_weld=type_weld, type_weld_ang = type_weld_ang, show_weld_line=0, pos_weld = pos_weld, ang_axis = ang_axis_weld, subpos=subpos, tip_konstr=tip_konstr
return

"set_weld_hands": ! Функция сварки
	type_weld = 0
	type_weld_ang = 1
	call "Сварка 3д" parameters type_weld=type_weld, type_weld_ang = type_weld_ang, show_weld_line=0, pos_weld = pos_weld, ang_axis = ang_axis_weld, subpos=subpos, tip_konstr=tip_konstr
return

"set_mat":
	tcol_mat = 0 : tbmat = 0
	call "macro_get_mat" parameters all mater=tmater, RETURNED_PARAMETERS tcol_mat, tbmat
	if tbmat>0 then
		BUILDING_MATERIAL tbmat
		SECT_ATTRS{2} gs_cont_pen, gs_line_type
	else
		SECT_ATTRS gs_fill_type_sec, gs_back_pen, gs_fill_pen, gs_sec_pen, gs_line_type
		fill gs_fill_type_sec
		line_type gs_line_type
	endif
	if tcol_mat>0 then col_mat = tcol_mat
	model solid
	material col_mat
	pen gs_cont_pen
return


"3_end":
