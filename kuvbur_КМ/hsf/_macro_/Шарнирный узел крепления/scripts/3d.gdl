
!==================== Выбор типа сечения уголка ====================
jt = 15
success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
IF gs_detlevel_3D=`Простой` then
	det_lvl_2d = 1
	det_lvl_3d = 1
endif
IF gs_detlevel_3D=`Детальный` THEN
	det_lvl_2d = 2
	det_lvl_3d = 2
endif
IF gs_detlevel_3D=`Откл` THEN
	det_lvl_2d = 2
	det_lvl_3d = 0
endif

GOSUB "set_mat" !Назначение материала

!=================== Поворот узла ======================
ROTX Xrot
rotx 90
add -(-ws*0.5-t_sten/2), -(-ds*0.5+0.02), -h_prof/2

!=================== Построение болтов ======================
if n_bolt>EPS then
	lenB = t_psk+diaB*2.3
	ROTZ 180
	ADDX ws*0.5-wt-wash1_thk
	ADDY ds*0.5-60/1000
	ADDZ eb ! Расстояние от края до болта
		FOR i=1 TO n_bolt
			call "Болт универсальный" PARAMETERS ALL,is_show_2d=0, nPos = t_psk, lenB = lenB, amg2d=90 !Позиция гайки
			ADDZ sb ! Расстояние между болтами
		NEXT i
	DEL n_bolt+4
endif

!=================== Построение уголка ======================
	mask = 1+2+4+16+32
IF type_plate = TYPE_PLATE_YES THEN
	type_form = 1 : gosub "set_form"
		TUBE n_form, 4, mask,
		GET(NSP),
		0, 0, -1, 0,
		0, 0, 0, 0,
		0, 0, h_prof, 0,
		0, 0, 1, 0
	type_form = 2 : gosub "set_form"
		EXTRUDE n_form, 0, 0, h_prof, mask, get(NSP)
ENDIF

IF type_plate = TYPE_PLATE_NO THEN
		type_form = 1 : gosub "set_form"
		TUBE n_form, 4, mask,
		GET(NSP),
		0, 0, -1, 0,
		0, 0, 0, 0,
		0, 0, h_prof, 0,
		0, 0, 1, 0
ENDIF

IF type_plate = TYPE_PLATE_YES_2 THEN
	type_form = 2 : gosub "set_form"
		EXTRUDE n_form, 0, 0, h_prof, mask, get(NSP)
ENDIF

IF type_plate = TYPE_PLATE_NO_2 THEN
	type_form = 1 : gosub "set_form"
		TUBE n_form, 4, mask,
		GET(NSP),
		0, 0, -1, 0,
		0, 0, 0, 0,
		0, 0, h_prof, 0,
		0, 0, 1, 0
	type_form = 1 : gosub "set_form"
		MULX -1
		ADD ws+t_sten, 0, 0
		TUBE n_form, 4, mask,
		GET(NSP),
		0, 0, -1, 0,
		0, 0, 0, 0,
		0, 0, h_prof, 0,
		0, 0, 1, 0
		del 2
ENDIF


!=========================== Построение HOTSPOT  ===============================
	
!	HOTSPOT -ws*0.5-t_sten/2, -ds*0.5+0.02, h_prof/2 ! Точка привязки к профилю
IF type_plate = TYPE_PLATE_NO OR type_plate = TYPE_PLATE_YES OR type_plate = TYPE_PLATE_NO_2 THEN
	! HOTSPOT - Уголка
	HOTSPOT -ws*0.5,-ds*0.5,0
	HOTSPOT -ws*0.5,ds*0.5,0
	HOTSPOT -ws*0.5+wt,ds*0.5,0
	HOTSPOT ws*0.5,-ds*0.5+wt,0
	HOTSPOT ws*0.5,-ds*0.5,0
	HOTSPOT -ws*0.5,-ds*0.5,0
	HOTSPOT -ws*0.5,-ds*0.5,h_prof
	HOTSPOT -ws*0.5,ds*0.5,h_prof
	HOTSPOT -ws*0.5+wt,ds*0.5,h_prof
	HOTSPOT ws*0.5,-ds*0.5+wt,h_prof
	HOTSPOT ws*0.5,-ds*0.5,h_prof
	HOTSPOT -ws*0.5,-ds*0.5,h_prof
ENDIF

IF type_plate = TYPE_PLATE_YES OR type_plate = TYPE_PLATE_YES_2 THEN
	! HOTSPOT - Платсины
	HOTSPOT -ws*0.5-t_sten,-ds*0.5,0
	HOTSPOT -ws*0.5-t_sten,ds*0.5,0
	HOTSPOT -ws*0.5-t_sten-t_plate,-ds*0.5,0
	HOTSPOT -ws*0.5-t_sten-t_plate,ds*0.5,0
	HOTSPOT -ws*0.5-t_sten,-ds*0.5,h_prof
	HOTSPOT -ws*0.5-t_sten,ds*0.5,h_prof
	HOTSPOT -ws*0.5-t_sten-t_plate,-ds*0.5,h_prof
	HOTSPOT -ws*0.5-t_sten-t_plate,ds*0.5,h_prof
ENDIF


!==================== Набор для выбора типа сварки ====================
	dim ttype_weld[]
	dim itype_weld[] : i=0
	i=i+1 : itype_weld[i]=0 : ttype_weld[i] = "Автоматическая"
	i=i+1 : itype_weld[i]=1 : ttype_weld[i] = "Ручная"
	i=i+1 : itype_weld[i]=2 : ttype_weld[i] = "Нет"
	
	dim pos_weld[2][3]

IF weld = WELD_YES THEN
	IF type_plate = TYPE_PLATE_NO OR type_plate = TYPE_PLATE_YES OR type_plate = TYPE_PLATE_NO_2 THEN
		!Добавление сварки по обушку
		ang_axis_weld = 90 ! Угол поворота сварки
		pos_weld[1][1] = -ws*0.5 	!Начало, X
		pos_weld[1][2] = -ds*0.5 	!Начало, Y
		pos_weld[1][3] = 0 			!Начало, Z
		pos_weld[2][1] = -ws*0.5 	!Конец, X
		pos_weld[2][2] = -ds*0.5	!Конец, Y
		pos_weld[2][3] = h_prof		!Конец, Z
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
		!Добавление сварки по перу
		ang_axis_weld = 0 ! Угол поворота сварки
		pos_weld[1][1] = ws*0.5 	!Начало, X
		pos_weld[1][2] = -ds*0.5 	!Начало, Y
		pos_weld[1][3] = 0 			!Начало, Z
		pos_weld[2][1] = ws*0.5 	!Конец, X
		pos_weld[2][2] = -ds*0.5	!Конец, Y
		pos_weld[2][3] = h_prof		!Конец, Z
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
	ENDIF

	IF type_plate = TYPE_PLATE_NO_2 THEN
		!Добавление сварки по перу
		ang_axis_weld = 90 ! Угол поворота сварки
		pos_weld[1][1] = -(1.5*ws)-t_sten 	!Начало, X
		pos_weld[1][2] = -ds*0.5 			!Начало, Y
		pos_weld[1][3] = 0 					!Начало, Z
		pos_weld[2][1] = -(1.5*ws)-t_sten 	!Конец, X
		pos_weld[2][2] = -ds*0.5			!Конец, Y
		pos_weld[2][3] = h_prof				!Конец, Z
		gosub "set_weld_hand" ! Вызов функции СВАРКИ
	ENDIF

	IF type_plate = TYPE_PLATE_YES THEN
		!Добавление сварки по пластине
		ang_axis_weld = 90 ! Угол поворота сварки
		pos_weld[1][1] = -ws*0.5-t_sten-t_plate 	!Начало, X
		pos_weld[1][2] = -ds*0.5 					!Начало, Y
		pos_weld[1][3] = 0 							!Начало, Z
		pos_weld[2][1] = -ws*0.5-t_sten-t_plate 	!Конец, X
		pos_weld[2][2] = -ds*0.5					!Конец, Y
		pos_weld[2][3] = h_prof						!Конец, Z
		gosub "set_weld_hand" ! Вызов функции СВАРКИ
	ENDIF

	IF type_plate = TYPE_PLATE_YES_2 THEN
		!Добавление сварки по пластине
		ang_axis_weld = 90 ! Угол поворота сварки
		pos_weld[1][1] = -ws*0.5-t_sten-t_plate 	!Начало, X
		pos_weld[1][2] = -ds*0.5 					!Начало, Y
		pos_weld[1][3] = 0 							!Начало, Z
		pos_weld[2][1] = -ws*0.5-t_sten-t_plate 	!Конец, X
		pos_weld[2][2] = -ds*0.5					!Конец, Y
		pos_weld[2][3] = h_prof						!Конец, Z
		gosub "set_weld_auto" ! Вызов функции СВАРКИ
		ang_axis_weld = 0 ! Угол поворота сварки
		pos_weld[1][1] = -ws*0.5-t_sten 			!Начало, X
		pos_weld[1][2] = -ds*0.5 					!Начало, Y
		pos_weld[1][3] = 0 							!Начало, Z
		pos_weld[2][1] = -ws*0.5-t_sten 			!Конец, X
		pos_weld[2][2] = -ds*0.5					!Конец, Y
		pos_weld[2][3] = h_prof						!Конец, Z
		gosub "set_weld_auto" ! Вызов функции СВАРКИ

	ENDIF

ENDIF
!======//=====//=====//=====//=====//=====//=====//=====//=====//=====//=====
del 3
goto "m_end"

"set_form":
	n_form = 0
	tstart = NSP
	if type_form = 1 then
		if det_lvl_3d = 2 then
			gosub "prof_5det"
		else
			gosub "prof_5simp"
		endif
	endif
	if type_form = 2 then gosub "prof_plate"
	tend = NSP
	n_form = (tend - tstart)/3
return

"prof_5simp": !"Уголок"
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,ds*0.5,jt
	put -ws*0.5+wt,ds*0.5,jt
	put -ws*0.5+wt,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return

"prof_5det": !"Уголок"
	put 0,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,0,jt !
	put -ws*0.5,ds*0.5,jt
	put -ws*0.5+wt-rr,ds*0.5,jt
	put rr,-90,2000+jt
	put -ws*0.5+wt,ds*0.5-rr,jt
	put -ws*0.5+wt,0,jt
	put -ws*0.5+wt,-ds*0.5+wt+ft,jt
	put ft,90,2000+jt
	put -ws*0.5+wt+ft,-ds*0.5+wt,jt
	put 0,-ds*0.5+wt,jt
	put ws*0.5-rr,-ds*0.5+wt,jt
	put rr,-90,2000+jt
	put ws*0.5,-ds*0.5-rr+wt,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return

!=================== Построение пластины ======================

"prof_plate": !"Пластина"
	put -ws*0.5-t_sten,-ds*0.5,jt
	put -ws*0.5-t_sten,ds*0.5,jt
	put -ws*0.5-t_sten-t_plate,ds*0.5,jt
	put -ws*0.5-t_sten-t_plate,-ds*0.5,jt
return


!======================= Вызов параметров сварик =======================
"set_weld_auto": ! Функция сварки
	type_weld = 0
	type_weld_ang = 0
	call "Сварка 3д" parameters type_weld=type_weld, type_weld_ang = type_weld_ang, show_weld_line=0, pos_weld = pos_weld, ang_axis = ang_axis_weld, subpos=subpos, tip_konstr=tip_konstr
return

"set_weld_hand": ! Функция сварки
	type_weld = 1
	type_weld_ang = 0
	call "Сварка 3д" parameters type_weld=type_weld, type_weld_ang = type_weld_ang, show_weld_line=0, pos_weld = pos_weld, ang_axis = ang_axis_weld, subpos=subpos, tip_konstr=tip_konstr
return


"set_mat":
	tcol_mat = 0 : tbmat = 0
	call "macro_get_mat" parameters all, RETURNED_PARAMETERS tcol_mat, tbmat
	if tbmat>0 then
		BUILDING_MATERIAL tbmat
		SECT_ATTRS{2} gs_cont_pen, gs_line_type
	else
		SECT_ATTRS gs_fill_type_sec, gs_back_pen, gs_fill_pen, gs_sec_pen, gs_line_type
		fill gs_fill_type_sec
		line_type gs_line_type
	endif
	if tcol_mat>0 then col_mat = tcol_mat
	model solid
	material col_mat
	pen gs_cont_pen
return

!==================== Расчет болтов и уголка ====================


"m_end":
