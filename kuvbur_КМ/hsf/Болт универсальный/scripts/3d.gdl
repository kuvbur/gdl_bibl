hotspot 0,0,0,900

gs_detlevel_3D="" : success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
if gs_detlevel_3D = `Простой` then
	show_thread=0
	bChamfer = 0
	res = 5
else
	res = 36
endif
call "Показ_болтов" PARAMETERS ALL ang1=rang, ang2=yRot_r, el_views=el_views RETURNED_PARAMETERS show_type, show_wash_list, show_hole


show = 0
call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
show = show_subpos
if not(show) then end

if GLOB_PREVIEW_MODE=1 or GLOB_PREVIEW_MODE=2 or GLOB_FEEDBACK_MODE=1 then
	res = 9
	bChamfer=0
	show_thread=0
else
	if GLOB_SEO_TOOL_MODE = 1 then
		res = 19
	else
		if res<3 then res=13
	endif
endif
if iscall then goto 3001
show_res=res
SHADOW OFF, OFF
resol show_res
material gs_ifc_mat
pen gs_cont_pen

roty 90    ! Horizontal
HOTSPOT 0, 0,	0.4*lenB, unID, yRot,	4+128: unID=unID+1
HOTSPOT 0,	0,	0,	unID,	yRot,	6: unID=unID+1
HOTSPOT 0,	1,	0,	unID,	yRot,	7: unID=unID+1
HOTSPOT 0.4*lenB*sin(yRot),0,0.4*lenB*cos(yRot),	unID, yRot, 5: unID=unID+1
roty yRot

if show_hole then
	ADDZ strart_hole-0.001
	if type_hole=2 or type_hole=3 then
		if abs(coord_hole[1])<abs(coord_hole[2]) then
			max_r=coord_hole[2]
		else
			max_r=coord_hole[1]
		endif
		HOTSPOT max_r,	0,0, unID,	ang_hole,	4+128: unID=unID+1
		HOTSPOT 0,	0, 0,	unID,	ang_hole,	6: unID=unID+1
		HOTSPOT 0,	0, 1,	unID,	ang_hole,	7: unID=unID+1
		HOTSPOT  max_r*cos(ang_hole)*0.6,	 max_r*sin(ang_hole)*0.6,0,	unID,	ang_hole, 5: unID=unID+1
		rotz ang_hole
		if type_hole=2 then
			HOTSPOT	0,					0,	0,	unID,	coord_hole[1],	1+128: unID=unID+1
			HOTSPOT	coord_hole[1],	0,	0,	unID,	coord_hole[1],	2: unID=unID+1
			HOTSPOT	-1,					0,	0,	unID,	coord_hole[1],	3: unID=unID+1
		endif
		HOTSPOT	0,					0,	0,	unID,	coord_hole[2],	1+128: unID=unID+1
		HOTSPOT	coord_hole[2],	0,	0,	unID,	coord_hole[2],	2: unID=unID+1
		HOTSPOT	-1,					0,	0,	unID,	coord_hole[2],	3: unID=unID+1
		del 1
	endif
	if show_hole_axis and show_type = NOT_SHOW then
		if type_hole=1 then
			lin_ 0,-holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0
			lin_ -holeSizeR-zas_axis,0,0,holeSizeR+zas_axis,0,0
			lin_ 0,-holeSizeR-zas_axis,insideLength+0.001,0,holeSizeR+zas_axis,insideLength+0.001
			lin_ -holeSizeR-zas_axis,0,insideLength+0.001,holeSizeR+zas_axis,0,insideLength+0.001
		endif
		if type_hole=2 then
			rotz ang_hole
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-holeSizeR-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-holeSizeR-zas_axis,0,insideLength+0.001
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[2],holeSizeR+zas_axis,0,coord_hole[2],-holeSizeR-zas_axis,0
			lin_ coord_hole[2],holeSizeR+zas_axis,insideLength+0.001,coord_hole[2],-holeSizeR-zas_axis,insideLength+0.001
			del 1
		endif
		if type_hole=3 then
			rotz ang_hole
			lin_ coord_hole[1],holeSizeR+zas_axis,0,coord_hole[1],-holeSizeR-zas_axis,0
			lin_ coord_hole[1],holeSizeR+zas_axis,insideLength+0.001,coord_hole[1],-holeSizeR-zas_axis,insideLength+0.001
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,0,coord_hole[2]-zas_axis,0,0
			lin_ coord_hole[1]+holeSizeR+zas_axis,0,insideLength+0.001,coord_hole[2]-zas_axis,0,insideLength+0.001
			del 1
		endif
	endif
	del 1
endif

if GLOB_SEO_TOOL_MODE = 1 and show_type<>SHOW_TOP and show_type<>SHOW_SIDE and show_hole and show_res>9 then
	ADDZ strart_hole-0.001
	if type_hole=1 then
		cylind insideLength+0.001, holeSizeR
	endif
	if type_hole=2 or type_hole=3 then
		rotz ang_hole
		PUT 0,holeSizeR,15
		PUT coord_hole[1],holeSizeR,15
		PUT coord_hole[1],0,915
		PUT 0,-180,4015
		PUT coord_hole[1],-holeSizeR,15
		PUT coord_hole[2],-holeSizeR,15
		if type_hole=2 then
			PUT coord_hole[2],0,915
			PUT 0,-180,4015
		else
			PUT coord_hole[2],holeSizeR,15
		endif
		PUT 0,holeSizeR,-1
		prism_ NSP/3,insideLength+0.001,get(NSP)
		del 1
	endif
	del 1
	end
endif

roty -90
call "Условный_болт" PARAMETERS ALL show_type=show_type, insideLength=insideLength, amg2d=amg2d
del 1

if show_type = NOT_SHOW then
	HOTARC holeSizeR, 0, 360, 9010
	addx insideLength
	hotspot 0,0,0,9011
	HOTARC holeSizeR, 0, 360, 9012
	del 1
	end
endif

if show_type<>SHOW_3D then end
gosub 600
3001:

group "Nut"
	hotspot 0,0,0
	hotspot 0,0,nut_thk
	put -nut_diam/2, -nut_diam/2*tan(30)  , 15,
		-nut_diam/2,  nut_diam/2*tan(30)  , 15,
		 0     ,  nut_diam/(2*cos(30)), 15,
		 nut_diam/2,  nut_diam/2*tan(30)  , 15,
		 nut_diam/2, -nut_diam/2*tan(30)  , 15,
		 0     , -nut_diam/(2*cos(30)), 15,
		-nut_diam/2, -nut_diam/2*tan(30)  , -1,
	
		 0     , 0          , 900,    ! smooth hole
		 diaB/2, 0          , 79,
		 diaB/2, 360-stepAng, 4000+79,
		 diaB/2, 0          , -1
	prism_ nsp/3, nut_thk, get(nsp)	
endgroup
group "nChamfer"
	nRad = bolt_diam+0.01
	nDia = nut_diam 
	mulz -1
	gosub 350
	del 1
	body -1
	addz nut_thk
	gosub 350
	del 1
endgroup
group "Head"
	hotspot 0,0,0
	hotspot 0,0,bolt_thk
	put -bolt_diam/2, -bolt_diam/2*tan(30)  , 15,
		-bolt_diam/2,  bolt_diam/2*tan(30)  , 15,
		 0     ,  bolt_diam/(2*cos(30)), 15,
		 bolt_diam/2,  bolt_diam/2*tan(30)  , 15,
		 bolt_diam/2, -bolt_diam/2*tan(30)  , 15,
		 0     , -bolt_diam/(2*cos(30)), 15,
		-bolt_diam/2, -bolt_diam/2*tan(30)  , -1
	prism_ nsp/3, bolt_thk, get(nsp)		
endgroup
group "hChamfer"
	mulz -1
	nRad = bolt_diam+0.01 : nDia = bolt_diam : gosub 350
	del 1
endgroup
group "Wash1"
	hotspot 0,0,0
	hotspot 0,0,wash1_thk
	prism_  3, wash1_thk,
		0    , 0  ,  900+79,
		wash1_diam/2, 360, 4000+79,
		wash1_diam_in/2, 360, 4000+79
endgroup
group "Wash2"
	hotspot 0,0,0
	hotspot 0,0,wash2_thk
	hotspot wash2_a/2,wash2_b/2,0
	hotspot wash2_a/2,wash2_b/2,wash2_thk
	hotspot -wash2_a/2,wash2_b/2,0
	hotspot -wash2_a/2,wash2_b/2,wash2_thk
	hotspot -wash2_a/2,-wash2_b/2,0
	hotspot -wash2_a/2,-wash2_b/2,wash2_thk
	hotspot wash2_a/2,-wash2_b/2,0
	hotspot wash2_a/2,-wash2_b/2,wash2_thk
	if wash2_type="Прямоугольная" then
		prism_  4, wash2_thk, wash2_a/2,wash2_b/2,15, -wash2_a/2,wash2_b/2,15, -wash2_a/2,-wash2_b/2,15, wash2_a/2,-wash2_b/2,15
	endif
	if wash2_type="Круглая" then
		prism_  3, wash2_thk,
			0    , 0  ,  900+79,
			wash2_diam/2, 360, 4000+79,
			wash2_diam_in/2, 360, 4000+79
	endif
endgroup

IF show_bolt gosub 100    ! Bolt          
if not(is_stud) then gosub 300    ! Bolt Head         
if (qty_nut_start + qty_nut_end)>0 then gosub 400    ! Nut      
if wash_thk>EPS then gosub 500    ! Washers 

killgroup "Head"
killgroup "Nut"
killgroup "hChamfer"
killgroup "Wash1"
killgroup "Wash2"
killgroup "nChamfer"
end ! -------------------------------------------


100 : !!! ========== Bolt ==========
	if abs(lenB-hit)> EPS then
		cylind lenB-hit, diaB/2
	endif
	addz lenB-hit
	IF not(show_thread)THEN bolt_tpi=0
	cylind hit, diaB/2 - bolt_tpi 
	del 1
	IF show_thread THEN gosub 200    ! Thread 
return


200 : !!! ========== Thread ========== 
	if req ("GDL_version") >= 1.564 then bAC12 = 1    ! >= AC12
	totAng  = 360*hit/bolt_tpi + 360   ! total angle
	for k = -stepAng to totAng + stepAng + EPS step stepAng    ! phantom points
		put bolt_body/2*cos(k), bolt_body/2*sin(k), bolt_tpi*k/360
	next k
	addz lenB-hit
	cutplane  1, 1, 0, 1    ! start
	del 1
	addz lenB
	cutplane  1, 1, 0, 0    ! end
	del 1
	addz lenB - hit - bolt_tpi/2
	if bAC12 then
		tubea 4, nsp/3, 1+2+16+32 + 128,    ! reduce Polycount
			0  ,  0    ,  0,
			bolt_tpi,  bolt_tpi/2,  0,    
			bolt_tpi, -bolt_tpi/2,  0,  
			0  ,  0    , -1,  
			get(nsp)
	else
		delta = atn((bolt_tpi/2)/bolt_tpi)
		xOff  = EPS*cos(delta)    ! additional points for rendering
		yOff  = EPS*sin(delta)
		tubea 8, nsp/3, 1+2+16+32,
			0       ,  0         ,  0,
			xOff    ,  yOff      ,  1,    ! rendering 
			bolt_tpi-xOff,  bolt_tpi/2-yOff,  1,    ! rendering
			bolt_tpi     ,  bolt_tpi/2     ,  0, 
			bolt_tpi     , -bolt_tpi/2     ,  0, 
			bolt_tpi-xOff, -bolt_tpi/2+yOff,  1,    ! rendering
			xOff    , -yOff      ,  1,    ! rendering 
			0       ,  0         , -1,
			get(nsp)
	endif
	del 1
	cutend
	cutend
return


300 : !!! ========== Bolt Head ==========
	addz -bolt_thk
	if not(bChamfer) then placegroup "Head"
	if bChamfer then placegroup subgroup("Head", "hChamfer")
	del 1
return


350 : !!! ========== Chamfer ==========
	if not(bChamfer) then return
	nutRad = abs(nRad)
	put 0            , nutRad     ,  2,
		nutRad-nDia/2, 0.96*nDia/2,  2,
		nutRad-nDia/2, nutRad     ,  2,
		0            , nutRad     , -1
	roty -90
	addx -(nutRad-nDia/2)
	revolve nsp/3, 360, 1+2+4+8+16+32, get(nsp)	
	del 2
return

400 : !!! ========== Nut ==========	
	if qty_nut_end>0 then
		addz nPos+wash_thk_end
		if not(bChamfer) then placegroup "Nut" 
		if bChamfer then placegroup subgroup("Nut", "nChamfer")
		if qty_nut_end>1 then
			rotz 90
			addz nut_thk
			if not(bChamfer) then placegroup "Nut" 
			if bChamfer then placegroup subgroup("Nut", "nChamfer")
			del 2
		endif
		del 1
	endif
	if qty_nut_start>0 then
		addz POS_2-nut_thk
		if not(bChamfer) then placegroup "Nut" 
		if bChamfer then placegroup subgroup("Nut", "nChamfer")
		if qty_nut_start>1 then
			rotz 90
			addz -nut_thk
			if not(bChamfer) then placegroup "Nut" 
			if bChamfer then placegroup subgroup("Nut", "nChamfer")
			del 2
		endif
		del 1
	endif
return

500 : !!! ========== Washers 1 ==========
	if (qty_wash_end=1) or (qty_wash_end=2) then
		ADDZ nPos + wash2_thk*(qty_wash_end=2)
		placegroup "Wash1"
		DEL 1
	endif
	if (qty_wash_start=1) or (qty_wash_start=2) then
		ADDZ POS_2*(is_stud=1)
		placegroup "Wash1"
		DEL 1
	endif
	if (qty_wash_start=3) or (qty_wash_start=2) then
		addz POS_2*(is_stud=1) + wash1_thk*(qty_wash_start=2)
		if show_wash_list then placegroup "Wash2"
		DEL 1
	endif
	if (qty_wash_end=3) or (qty_wash_end=2) then
		addz nPos
		if show_wash_list then placegroup "Wash2"
		DEL 1
	endif
return

600 : !!! ========== Hotspots ==========
	!!! ----- Length -----
	hotspot 0, 0,  0  , unID, lenB, 1+256 : unID = unID+1    ! base 
	hotspot 0, 0, -1  , unID, lenB, 3     : unID = unID+1    ! ref
	hotspot 0, 0,  lenB, unID, lenB, 2     : unID = unID+1    ! moving
	!!! ----- Nut Position -----
	hotspot 0, 0,  0   ,  unID, nPos, 1 : unID = unID+1    ! base 
	hotspot 0, 0, -1   ,  unID, nPos, 3 : unID = unID+1    ! ref
	hotspot 0, 0,  nPos,  unID, nPos, 2 : unID = unID+1    ! moving
	!!! ----- Диаметр -----
	addy -diaB/2
	hotspot 0,  0, 0, unID, diaB, 1+128 : unID = unID+1    ! base 
	hotspot 0, -1, 0, unID, diaB, 3     : unID = unID+1    ! ref
	hotspot 0,  diaB, 0, unID, diaB, 2       : unID = unID+1    ! moving
	del 1
return