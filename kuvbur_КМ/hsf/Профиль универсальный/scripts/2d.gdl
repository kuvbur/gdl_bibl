
HOtspot2 0,0

ws = ws / 1000
ds = ds / 1000
wt = wt / 1000
ft = ft / 1000
rr = rr / 1000
tr = tr / 1000
dbf = dbf / 1000
if GLOB_CONTEXT=6 then
	gosub "showved"
	end
endif
gosub "set_show_2d"
if not(show) or det_lvl_2d=0 then end

if show_mvo_proj=2 then
	show_mvo_proj = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "show_mvo_proj", show_mvo_proj)
	if show_mvo_proj then
		use_mvo_h = 0 : success = LIBRARYGLOBAL ("Libraryflobals-km", "use_mvo_h", use_mvo_h)
		if use_mvo_h then
			success = LIBRARYGLOBAL ("Libraryflobals-km", "t_proj_down", t_proj_down)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "t_proj_up", t_proj_up)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "h_proj_down", h_proj_down)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "h_proj_up", h_proj_up)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "is_show_lower", is_show_lower)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "is_show_upper", is_show_upper)
		endif
	endif
endif

if show_only_axsis then
	only_axis_line = gs_line_type
else
	if show_mvo_proj<1 then
		chStoryDist = GLOB_CH_STORY_DIST
		IF gs_StorySensitive = 0 THEN chStoryDist = 0
		if chStoryDist > EPS then !"Этажом выше"
			gs_cont_pen = gs_cont_pen_up
			gs_fill_type = gs_fill_type_up
			gs_fill_pen = gs_fill_pen_up
			gs_back_pen = gs_back_pen_up
			gs_line_type = gs_line_type_up
			mark = mark_up
		endif
		if chStoryDist < EPS then
			if abs(chStoryDist) < EPS then !"Собственный этаж"
			else !"Этажом ниже"
				gs_cont_pen = gs_cont_pen_down
				gs_fill_type = gs_fill_type_down
				gs_fill_pen = gs_fill_pen_down
				gs_back_pen = gs_back_pen_down
				gs_line_type = gs_line_type_down
				mark = mark_down
			endif
		endif
	endif
endif

if strstr(error_msg, "Ошибка")>0 then 
	gs_cont_pen = error_pen
	gs_fill_pen = error_pen
	gs_back_pen = error_pen
	gs_fill_type = GLOB_FILL_INDEX_SOLID
endif

pen gs_cont_pen
fill gs_fill_type
line_type gs_line_type
SECT_ATTRS gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen, gs_line_type
IF GLOB_FEEDBACK_MODE=1 THEN
	det_lvl_2d = 1
	show3dhotspot2d = 0
	show_mvo_proj = 1
endif

add2 pos_prof_call_xyz[ix], pos_prof_call_xyz[iy]
rot2 phi_call

if abs(ang + etta_call) mod 90>EPS or iscall or (abs(abs(ang + etta_call) mod 180 - 90)<EPS and abs(ang_axis) mod 90>EPS) then
	!Определяем ширину проекции
	aa_plane = aa*sin(ang_axis)+bb*cos(ang_axis)
	if (abs(ang_axis) mod 90)<EPS then
		if (abs(ang_axis) mod 180)<EPS then
			aa_plane = bb
		endif
		if abs((abs(ang_axis) mod 180) - 90)<EPS then
			aa_plane = aa
		endif
	endif
	!Привязка к контуру
	HOTSPOT2	pos_prof[is][ix], pos_prof[is][iy]-aa_plane,unIDD: unIDD=unIDD+1
	HOTSPOT2	pos_prof[is][ix], pos_prof[is][iy]+aa_plane,unIDD: unIDD=unIDD+1
	HOTSPOT2	pos_prof[ie][ix], pos_prof[ie][iy]-aa_plane,unIDD: unIDD=unIDD+1
	HOTSPOT2	pos_prof[ie][ix], pos_prof[ie][iy]+aa_plane,unIDD: unIDD=unIDD+1
	hotline2 pos_prof[is][ix], pos_prof[is][iy]-aa_plane, pos_prof[ie][ix], pos_prof[ie][iy]-aa_plane,unIDD: unIDD=unIDD+1
	hotline2 pos_prof[is][ix], pos_prof[is][iy]+aa_plane, pos_prof[ie][ix], pos_prof[ie][iy]+aa_plane,unIDD: unIDD=unIDD+1
	!Смещение оси
	!Показ оси 
	if show_axsis or show_only_axsis then gosub "axis2d"
	!Показ проекции или условного прямоугольника
	pen gs_cont_pen
	fill gs_fill_type
	line_type gs_line_type
	SECT_ATTRS gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen, gs_line_type
	if show_only_axsis=0 and show_mvo_proj<1 then
		if show_proj then
			same_pen = (gs_fill_pen = gs_cont_pen)
			pen gs_fill_pen
			fill gs_fill_type
			line_type gs_line_type
			SECT_ATTRS gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen, gs_line_type
			PROJECT2{3} 3, 270, 3+32, 1+4+(2+8)*same_pen, gs_back_pen, 0, 0, 90
			if not(same_pen) then
				pen gs_cont_pen
				PROJECT2{3} 3, 270, 3+32, 2+8, 0, 0, 0, 90
			endif
		else
			POLY2_B 4, 1+2+4,gs_fill_pen, gs_back_pen,
							pos_prof[is][ix]-offset_body_2d_s,pos_prof[is][iy]-aa_plane,1,
							pos_prof[is][ix]-offset_body_2d_s,pos_prof[is][iy]+aa_plane,1,
							pos_prof[ie][ix]-offset_body_2d_e,pos_prof[ie][iy]+aa_plane,1,
							pos_prof[ie][ix]-offset_body_2d_e,pos_prof[ie][iy]-aa_plane,1
		endif
	endif
	if iscall then end
endif
!Редактируем длину
if type_coord=1 then gosub "hotspot2d_coord"
if type_coord=2 then gosub "hotspot2d_rphi"

if show_mvo_proj and show_only_axsis=0 then 
	if is_show_lower then
		same_pen = (gs_fill_pen_down = gs_cont_pen_down)
		pen gs_fill_pen_down
		fill gs_fill_type_down
		line_type gs_line_type_down
		SECT_ATTRS gs_fill_type_down, gs_back_pen_down, gs_fill_pen_down, gs_cont_pen_down, gs_line_type_down
		PROJECT2{3} 3, 270, 3+32, 1+4+(2+8)*same_pen, gs_back_pen_down, 0, 0, 90, PARAMETERS cutplane_enable = 1, n_part_show = -1
		if not(same_pen) then
			pen gs_cont_pen_down
			PROJECT2{3} 3, 270, 3+32, 2+8, 0, 0, 0, 90, PARAMETERS cutplane_enable = 1, n_part_show = -1
		endif
	endif
	same_pen = (gs_fill_pen = gs_cont_pen)
	pen gs_fill_pen
	fill gs_fill_type
	line_type gs_line_type
	SECT_ATTRS gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen, gs_line_type
	PROJECT2{3} 3, 270, 3+32, 1+4+(2+8)*same_pen, gs_back_pen, 0, 0, 90, PARAMETERS cutplane_enable = 1, n_part_show = 0
	if not(same_pen) then
		pen gs_cont_pen
		PROJECT2{3} 3, 270, 3+32, 2+8, 0, 0, 0, 90, PARAMETERS cutplane_enable = 1, n_part_show = 0
	endif
	if is_show_upper then
		same_pen = (gs_fill_pen_up = gs_cont_pen_up)
		pen gs_fill_pen_up
		fill gs_fill_type_up
		line_type gs_line_type_up
		SECT_ATTRS gs_fill_type_up, gs_back_pen_up, gs_fill_pen_up, gs_cont_pen_up, gs_line_type_up
		PROJECT2{3} 3, 270, 3+32, 1+4+(2+8)*same_pen, gs_back_pen_up, 0, 0, 90, PARAMETERS cutplane_enable = 1, n_part_show = 1
		if not(same_pen) then
			pen gs_cont_pen_up
			PROJECT2{3} 3, 270, 3+32, 2+8, 0, 0, 0, 90, PARAMETERS cutplane_enable = 1, n_part_show = 1
		endif
	endif
endif
pen gs_cont_pen
fill gs_fill_type
line_type gs_line_type
SECT_ATTRS gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen, gs_line_type
if show3dhotspot2d and show_only_axsis=0 then PROJECT2{3} 3, 270, 1, 16+32, 0, 0, 0, 90, PARAMETERS cutplane_enable = 0

if show_axsis or show_only_axsis then gosub "axis2d"

!Подвинули и повернули СК на плане
add2 pos_prof[is][ix],	pos_prof[is][iy]
rot2 ang_pl
pen gs_cont_pen
fill gs_fill_type
line_type gs_line_type
SECT_ATTRS gs_fill_type, gs_back_pen, gs_fill_pen, gs_cont_pen, gs_line_type
!Горизонтальный вид
if abs((abs(ang + etta_call) mod 180) - 90)<EPS then
	!Редактирование подрезки в 2д
	add2 0, offset_prof[is][ix]*sin(ang_axis)+offset_prof[is][iy]*cos(ang_axis)
	rot2 -offset_ang_pl
	ismul=1 : if abs(abs(ang_axis)-180)<EPS or abs(abs(ang_axis)-270)<EPS then ismul=-1
	mul2 1,ismul
	if (abs(ang_axis) mod 180)<EPS then
		if type_cutf_s=1 then
			gosub "cut2d_lps"
		else
			put offset_body_2d_s,	bb,1
			put offset_body_2d_s,	0,1
			put offset_body_2d_s,	-bb,1
		endif
		if abs(type_cutf_e-1)<EPS then 
			gosub "cut2d_lpe"
		else
			put l_axis_2d-offset_body_2d_e,	-bb,1
			put l_axis_2d-offset_body_2d_e,	0,1
			put l_axis_2d-offset_body_2d_e,	bb,1
		endif
		aa_plane = bb
	endif
	if abs((abs(ang_axis) mod 180) - 90)<EPS then
		if type_cutf_s=2 then
			gosub "cut2d_vns"
		else
			put offset_body_2d_s,	-aa,1
			put offset_body_2d_s,	0,1
			put offset_body_2d_s,	aa,1
		endif
		if abs(type_cutf_e-2)<EPS then
			gosub "cut2d_vne"
		else
			put l_axis_2d-offset_body_2d_e,	aa,1
			put l_axis_2d-offset_body_2d_e,	0,1
			put l_axis_2d-offset_body_2d_e,	-aa,1
		endif
		aa_plane = aa
	endif
	if abs(ang_axis) mod 90<EPS then gosub "front_view"
	del 1
endif
!Вертикальный вид
if abs(ang + etta_call) mod 180 < EPS then
	gosub "rot2d_axis"
	rot2 ang_axis
	add2 offset_prof[ie][ix], offset_prof[ie][iy]
!	rot2 -offset_ang_pl
	if mirror_axis=1 then mul2 -1,1
	if mirror_axis=2 then mul2 1,-1
	if not(show_mvo_proj) then gosub "top_view"
endif

del 4
end
"hotspot2d_rphi":
	if hotspot_call then
		add2 pos_prof[is][ix],	pos_prof[is][iy]
		HOTSPOT2 l_axis_2d,	0, unID,	ang_pl,	4+128: unID=unID+1
		HOTSPOT2 0,	0,	unID,	ang_pl,	6: unID=unID+1
		HOTSPOT2 l_axis_2d*cos(ang_pl)*0.6,	l_axis_2d*sin(ang_pl)*0.6,	unID,	ang_pl, 5: unID=unID+1
		rot2 ang_pl
	!	HOTSPOT2	0,					pos_prof[is][iy],	unID,	pos_prof[is][ix],	1+128: unID=unID+1
	!	HOTSPOT2	pos_prof[is][ix],	pos_prof[is][iy],	unID,	pos_prof[is][ix],	2, p_name: unID=unID+1
	!	HOTSPOT2	-1,					pos_prof[is][iy],	unID,	pos_prof[is][ix],	3, p_name: unID=unID+1
		del 2
	endif
return

"hotspot2d_coord":
	!-- Редактирование длины в 2д --
	if hotspot_call then
		p_name = paramname_coord[is][ix]
		HOTSPOT2	0,					pos_prof[is][iy],	unID,	pos_prof[is][ix],	1+128, pos_prof[is][ix], p_name: unID=unID+1
		HOTSPOT2	pos_prof[is][ix],	pos_prof[is][iy],	unID,	pos_prof[is][ix],	2, pos_prof[is][ix], p_name: unID=unID+1
		HOTSPOT2	-1,					pos_prof[is][iy],	unID,	pos_prof[is][ix],	3, pos_prof[is][ix], p_name: unID=unID+1
		p_name = paramname_coord[is][iy]
		HOTSPOT2	pos_prof[is][ix],					0,	unID,	pos_prof[is][iy],	1+128, pos_prof[is][iy], p_name: unID=unID+1
		HOTSPOT2	pos_prof[is][ix],	pos_prof[is][iy],	unID,	pos_prof[is][iy],	2, pos_prof[is][iy], p_name: unID=unID+1
		HOTSPOT2	pos_prof[is][ix],					-1,	unID,	pos_prof[is][iy],	3, pos_prof[is][iy], p_name: unID=unID+1
		p_name = paramname_coord[ie][ix]
		HOTSPOT2	0,					pos_prof[ie][iy],	unID,	pos_prof[ie][ix],	1+128, pos_prof[ie][ix], p_name: unID=unID+1
		HOTSPOT2	pos_prof[ie][ix],	pos_prof[ie][iy],	unID,	pos_prof[ie][ix],	2, pos_prof[ie][ix], p_name: unID=unID+1
		HOTSPOT2	-1,					pos_prof[ie][iy],	unID,	pos_prof[ie][ix],	3, pos_prof[ie][ix], p_name: unID=unID+1
		p_name = paramname_coord[ie][iy]
		HOTSPOT2	pos_prof[ie][ix],					0,	unID,	pos_prof[ie][iy],	1+128, pos_prof[ie][iy], p_name: unID=unID+1
		HOTSPOT2	pos_prof[ie][ix],	pos_prof[ie][iy],	unID,	pos_prof[ie][iy],	2, pos_prof[ie][iy], p_name: unID=unID+1
		HOTSPOT2	pos_prof[ie][ix],					-1,	unID,	pos_prof[ie][iy],	3, pos_prof[ie][iy], p_name: unID=unID+1
	endif
return

"axis2d":
	l_axis_2d_t = l_axis_2d
	pen only_axis_pen
	line_type only_axis_line
	if abs(ang) mod 180 < EPS then !Вертикальный вид
		add2 offset_prof_glob[is][ix],	offset_prof_glob[is][iy]
		rot2 ang_axis
		line2 offset_prof_arr[type_axis_pos+1][ix],	bb, offset_prof_arr[type_axis_pos+1][ix], -bb
		line2 aa,	offset_prof_arr[type_axis_pos+1][iy], -aa, offset_prof_arr[type_axis_pos+1][iy]
		hotline2 offset_prof_arr[type_axis_pos+1][ix],	bb, offset_prof_arr[type_axis_pos+1][ix], -bb,unIDD: unIDD=unIDD+1
		hotline2 aa,	offset_prof_arr[type_axis_pos+1][iy], -aa, offset_prof_arr[type_axis_pos+1][iy],unIDD: unIDD=unIDD+1
		hotspot2 offset_prof_arr[type_axis_pos+1][ix],	bb,unIDD: unIDD=unIDD+1
		hotspot2 offset_prof_arr[type_axis_pos+1][ix],	-bb,unIDD: unIDD=unIDD+1
		hotspot2 aa,	offset_prof_arr[type_axis_pos+1][iy],unIDD: unIDD=unIDD+1
		hotspot2 -aa,	offset_prof_arr[type_axis_pos+1][iy],unIDD: unIDD=unIDD+1
		del 2
	else !Любой другой
		if show_only_axsis then
			line2 pos_prof[is][ix],	pos_prof[is][iy], pos_prof[ie][ix],	pos_prof[ie][iy]
			hotline2 pos_prof[is][ix],	pos_prof[is][iy], pos_prof[ie][ix],	pos_prof[ie][iy],unIDD: unIDD=unIDD+1
		else
			HOTSPOT2	offset_prof_glob[type_axis_pos+1][ix],offset_prof_glob[type_axis_pos+1][iy],unIDD: unIDD=unIDD+1
			HOTSPOT2	offset_prof_glob[type_axis_pos+11][ix],offset_prof_glob[type_axis_pos+11][iy],unIDD: unIDD=unIDD+1
			line2 offset_prof_glob[type_axis_pos+1][ix],offset_prof_glob[type_axis_pos+1][iy], offset_prof_glob[type_axis_pos+11][ix],offset_prof_glob[type_axis_pos+11][iy]
			hotline2 offset_prof_glob[type_axis_pos+1][ix],offset_prof_glob[type_axis_pos+1][iy], offset_prof_glob[type_axis_pos+11][ix],offset_prof_glob[type_axis_pos+11][iy],unIDD: unIDD=unIDD+1
		endif
	endif
	l_axis_2d = l_axis_2d_t
return

"rot2d_axis":
	if hotspot_call then
		HOTSPOT2 aa,	0, unID,	ang_axis,	4+128: unID=unID+1
		HOTSPOT2 0,	0,	unID,	ang_axis,	6: unID=unID+1
		HOTSPOT2 aa*cos(ang_axis),	aa*sin(ang_axis),	unID,	ang_axis, 5: unID=unID+1
	endif
return

"cut2d_lps":
	if hotspot_call then
		!-- Редактирование подрезки лево/право --
		if not(mirr_cutf_s) then
			p_name = paramname_cut[is][il]
			HOTSPOT2 start_cut_s,	-bb, unID, pos_cutf[is][il],	4+128, pos_cutf[is][il], p_name: unID=unID+1
			HOTSPOT2 start_cut_s,	0,	unID,	pos_cutf[is][il],	6+512*(ismul>EPS), pos_cutf[is][il], p_name: unID=unID+1
			HOTSPOT2 start_cut_s-bb*tan(pos_cutf[is][il]),	-bb,	unID,	pos_cutf[is][il], 5, pos_cutf[is][il], p_name: unID=unID+1
		else
			HOTSPOT2 start_cut_s-bb*tan(pos_cutf[is][il]),	-bb,unIDR : unIDR=unIDR+1
		endif
		p_name = paramname_cut[is][ip]
		HOTSPOT2 start_cut_s,	bb, unID, pos_cutf[is][ip],	4+128, pos_cutf[is][ip], p_name: unID=unID+1
		HOTSPOT2 start_cut_s,	0,	unID,	pos_cutf[is][ip],	6+512*(ismul>EPS), pos_cutf[is][ip], p_name: unID=unID+1
		HOTSPOT2 start_cut_s+bb*tan(pos_cutf[is][ip]),	bb,	unID,	pos_cutf[is][ip], 5, pos_cutf[is][ip], p_name: unID=unID+1
	endif
	put start_cut_s+bb*tan(pos_cutf[is][ip]),	bb,1
	put start_cut_s,	0,1
	put start_cut_s-bb*tan(pos_cutf[is][il]),	-bb,1
return

"cut2d_lpe":
	add2 l_axis_2d,0
	if hotspot_call then
		if not(mirr_cutf_e) then
			p_name = paramname_cut[ie][il]
			HOTSPOT2 -start_cut_e,	-bb, unID, pos_cutf[ie][il],	4+128, pos_cutf[ie][il], p_name: unID=unID+1
			HOTSPOT2 -start_cut_e,	0,	unID,	pos_cutf[ie][il],	6+512*(ismul<EPS), pos_cutf[ie][il], p_name: unID=unID+1
			HOTSPOT2 -start_cut_e+bb*tan(pos_cutf[ie][il]),	-bb,	unID,	pos_cutf[ie][il], 5, pos_cutf[ie][il], p_name: unID=unID+1
		else
			HOTSPOT2 -start_cut_e+bb*tan(pos_cutf[ie][il]),	-bb,unIDR : unIDR=unIDR+1
		endif
		p_name = paramname_cut[ie][ip]
		HOTSPOT2 -start_cut_e,	bb, unID, pos_cutf[ie][ip],	4+128, pos_cutf[ie][ip], p_name: unID=unID+1
		HOTSPOT2 -start_cut_e,	0,	unID,	pos_cutf[ie][ip],	6+512*(ismul<EPS), pos_cutf[ie][ip], p_name: unID=unID+1
		HOTSPOT2 -start_cut_e-bb*tan(pos_cutf[ie][ip]),	bb,	unID,	pos_cutf[ie][ip], 5, pos_cutf[ie][ip], p_name: unID=unID+1
	endif
	put -start_cut_e+bb*tan(pos_cutf[ie][il])+l_axis_2d,	-bb,1
	put -start_cut_e+l_axis_2d,	0,1
	put -start_cut_e-bb*tan(pos_cutf[ie][ip])+l_axis_2d,	bb,1
	del 1
return

"cut2d_vns":
	if hotspot_call then
		!-- Редактирование подрезки верх/низ --
		if not(mirr_cutf_s) then
			p_name = paramname_cut[is][iv]
			HOTSPOT2 start_cut_s,	-aa, unID, pos_cutf[is][iv],	4+128, pos_cutf[is][iv], p_name: unID=unID+1
			HOTSPOT2 start_cut_s,	0,	unID,	pos_cutf[is][iv],	6+512*(ismul>EPS), pos_cutf[is][iv], p_name: unID=unID+1
			HOTSPOT2 start_cut_s-aa*tan(pos_cutf[is][iv]),	-aa,	unID,	pos_cutf[is][iv], 5, pos_cutf[is][iv], p_name: unID=unID+1
		else
			HOTSPOT2 start_cut_s-aa*tan(pos_cutf[is][iv]),	-aa,unIDR : unIDR=unIDR+1
		endif
		p_name = paramname_cut[is][in]
		HOTSPOT2 start_cut_s,	aa, unID, pos_cutf[is][in],	4+128, pos_cutf[is][in], p_name: unID=unID+1
		HOTSPOT2 start_cut_s,	0,	unID,	pos_cutf[is][in],	6+512*(ismul>EPS), pos_cutf[is][in], p_name: unID=unID+1
		HOTSPOT2 start_cut_s+aa*tan(pos_cutf[is][in]),	aa,	unID,	pos_cutf[is][in], 5, pos_cutf[is][in], p_name: unID=unID+1
	endif
	put start_cut_s-aa*tan(pos_cutf[is][iv]),	-aa,1
	put start_cut_s,	0,1
	put start_cut_s+aa*tan(pos_cutf[is][in]),	aa,1
return

"cut2d_vne":
	add2 l_axis_2d,0
	if hotspot_call then
		if not(mirr_cutf_e) then
			p_name = paramname_cut[ie][iv]
			HOTSPOT2 -start_cut_e,	-aa, unID, pos_cutf[ie][iv],	4+128, pos_cutf[ie][iv], p_name: unID=unID+1
			HOTSPOT2 -start_cut_e,	0,	unID,	pos_cutf[ie][iv],	6+512*(ismul<EPS), pos_cutf[ie][iv], p_name: unID=unID+1
			HOTSPOT2 -start_cut_e+aa*tan(pos_cutf[ie][iv]),	-aa,	unID,	pos_cutf[ie][iv], 5, pos_cutf[ie][iv], p_name: unID=unID+1
		else
			HOTSPOT2 -start_cut_e+aa*tan(pos_cutf[ie][iv]),	-aa,unIDR : unIDR=unIDR+1
		endif
		p_name = paramname_cut[ie][in]
		HOTSPOT2 -start_cut_e,	aa, unID, pos_cutf[ie][in],	4+128, pos_cutf[ie][in], p_name: unID=unID+1
		HOTSPOT2 -start_cut_e,	0,	unID,	pos_cutf[ie][in],	6+512*(ismul<EPS), pos_cutf[ie][in], p_name: unID=unID+1
		HOTSPOT2 -start_cut_e-aa*tan(pos_cutf[ie][in]),	aa,	unID,	pos_cutf[ie][in], 5, pos_cutf[ie][in], p_name: unID=unID+1
	endif
	put -start_cut_e-aa*tan(pos_cutf[ie][in])+l_axis_2d,	aa,1
	put -start_cut_e+l_axis_2d,	0,1
	put -start_cut_e+aa*tan(pos_cutf[ie][iv])+l_axis_2d,	-aa,1
	del 1
return

"showved":
	pen SYMB_SECT_PEN
	MUL2 A_/1000, A_/1000 
	k=5
	if name_prof_i=1 then
		circle2 0,0,k/2
	endif
	if name_prof_i=2 then
		if abs(ws-ds)<EPS then rect2 0,0,k,k
		if abs(ws-ds)>EPS then rect2 0,0,k,k*1.5
	endif
	if name_prof_i=3 or name_prof_i=9 then
		line2 0,0,0,k
		line2 0,0,k*0.5,0
		line2 0,k,k*0.5,k
	endif
	if name_prof_i=4 then
		line2 -k/2,-k/2,k/2,-k/2
		line2 -k/2,k/2,k/2,k/2
		line2 0,-k/2,0,k/2
	endif
	if name_prof_i=5 or name_prof_i=8 then
		if abs(ws-ds)<EPS then km = 1
		if abs(ws-ds)>EPS then km = 1.5
		line2 0,0,0,k*km
		line2 0,0,k,0
	endif
return

"front_view":
	if show_only_axsis = 0 then
		x1=0 : x2=0 : y1=0 : y2=0 : xs=0 :ys=0
		pen gs_cont_pen
		fill gs_fill_type
		line_type gs_line_type
		if not(show_mvo_proj) then POLY2_b nsp/3, 7, gs_fill_pen, gs_back_pen,use(nsp)
		for i=1 to nsp step 3
			x1=get(1)
			y1=get(1)
			s1=get(1)
			if show_2d_hotspot then 
				hotspot2 x1,y1,unIDR : unIDR=unIDR+1
			endif
			if i=1 then
				xs=x1 : ys=y1
			endif
			if i=4 then
				line2 xs,ys,x1,y1
				hotline2 xs,ys,x1,y1,unIDR : unIDR=unIDR+1
			endif
			if i>1 and s1>0 then 
!				line2 x1,y1,x2,y2
				hotline2 x1,y1,x2,y2,unIDR : unIDR=unIDR+1
				x2=x1
				y2=y1
			endif
		next i
		line2 xs,ys,x2,y2
		hotline2 xs,ys,x2,y2,unIDR : unIDR=unIDR+1
	else
!		line2 offset_s,0,l_pos_prof-offset_e,0
!		hotline2 offset_s,0,l_pos_prof-offset_e,0,unIDR : unIDR=unIDR+1
	endif
	if hotspot_call then
		!Отступы начала и конца
		HOTSPOT2	0,	aa_plane/2,	unID,	offset_s,	1+128: unID=unID+1
		HOTSPOT2	offset_s,	aa_plane/2,	unID,	offset_s,	2: unID=unID+1
		HOTSPOT2	offset_s-1,	aa_plane/2,	unID,	offset_s,	3: unID=unID+1
		HOTSPOT2	l_pos_prof,	aa_plane/2,	unID,	offset_e,	1+128: unID=unID+1
		HOTSPOT2	l_pos_prof-offset_e,	aa_plane/2,	unID,	offset_e,	2: unID=unID+1
		HOTSPOT2	l_pos_prof*2,	aa_plane/2,	unID,	offset_e,	3: unID=unID+1
		!Размеры фасонки
		if type_fin_s=3 then
			HOTSPOT2	offset_s,	-aa_plane/2,	unID,	zagf_h_left,	1+128: unID=unID+1
			HOTSPOT2	zagf_h_left+offset_s,	-aa_plane/2,	unID,	zagf_h_left,	2: unID=unID+1
			HOTSPOT2	-1,	-aa_plane/2,	unID,	zagf_h_left,	3: unID=unID+1
		endif
		if type_fin_e=3 then
			HOTSPOT2	l_pos_prof-offset_e,	-aa_plane/2,	unID,	zagf_h_right,	1+256: unID=unID+1
			HOTSPOT2	l_pos_prof-zagf_h_right-offset_e,	-aa_plane/2,	unID,	zagf_h_right,	2: unID=unID+1
			HOTSPOT2	l_pos_prof*2,	-aa_plane/2,	unID,	zagf_h_right,	3: unID=unID+1
		endif
	endif
return

"top_view":
	jt=1
	if is_emp_fill then gosub "emp_fill"
	if name_prof_i=1 then
		gosub "prof2d_1det"
	endif
	if name_prof_i=2 then
		if det_lvl_2d = 2 then
			gosub "prof2d_2det"
		else
			gosub "prof2d_2simp"
		endif
	endif
	if name_prof_i=3 then
		if det_lvl_2d = 2 then
			gosub "prof2d_3det"
		else
			gosub "prof2d_3simp"
		endif
	endif
	if name_prof_i=4 then
		if det_lvl_2d = 2 then
			gosub "prof2d_4det"
		else
			gosub "prof2d_4simp"
		endif
	endif
	if name_prof_i=5 then
		if det_lvl_2d = 2 then
			gosub "prof2d_5det"
		else
			gosub "prof2d_5simp"
		endif
	endif
	if name_prof_i=8 then
		if det_lvl_2d = 2 then
			gosub "prof2d_8det"
		else
			gosub "prof2d_8simp"
		endif
	endif
	if name_prof_i=9 then
		if det_lvl_2d = 2 then
			gosub "prof2d_9det"
		else
			gosub "prof2d_9simp"
		endif
	endif

	if nsp/3>0 and abs(int(nsp/3)-nsp/3)<EPS then
		hotspot2 0, 0,unIDR : unIDR=unIDR+1
		hotspot2 0,-bb,unIDR : unIDR=unIDR+1
		hotspot2 0,bb,unIDR : unIDR=unIDR+1
		pen gs_cont_pen
		fill gs_fill_type
		POLY2_b nsp/3, 1+2+4+8, gs_fill_pen, gs_back_pen,use(nsp)
		if show_2d_hotspot=10 then
			x1=get(1)
			y1=get(1)
			s1=get(1)
			hotspot2 x1,y1,unIDR : unIDR=unIDR+1
			for i=1 to nsp step 3
				x2=get(1)
				y2=get(1)
				s2=get(1)
				if s2<10 and s2>EPS then
					hotspot2 x2,y2,unIDR : unIDR=unIDR+1
					hotline2 x1,y1,x2,y2,unIDR : unIDR=unIDR+1
					x1=x2 : y1=y2 : s1=s2
				endif
			next i
		else
			tt = max(get(nsp))
			hotspot2 aa,0,unIDR : unIDR=unIDR+1
			hotspot2 -aa,0,unIDR : unIDR=unIDR+1
			hotspot2 0,bb,unIDR : unIDR=unIDR+1
			hotspot2 0,-bb,unIDR : unIDR=unIDR+1
			hotspot2 aa,bb,unIDR : unIDR=unIDR+1
			hotspot2 -aa,bb,unIDR : unIDR=unIDR+1
			hotspot2 aa,-bb,unIDR : unIDR=unIDR+1
			hotspot2 -aa,-bb,unIDR : unIDR=unIDR+1
		endif
	endif
	if type_axis_pos>0 then 
		hotspot2 offset_prof_arr[type_axis_pos+1][ix], offset_prof_arr[type_axis_pos+1][iy],unIDR : unIDR=unIDR+1
		hotspot2 offset_prof_arr[type_axis_pos+1][ix],bb,unIDR : unIDR=unIDR+1
		hotspot2 offset_prof_arr[type_axis_pos+1][ix],-bb,unIDR : unIDR=unIDR+1
		hotspot2 aa,offset_prof_arr[type_axis_pos+1][iy],unIDR : unIDR=unIDR+1
		hotspot2 -aa,offset_prof_arr[type_axis_pos+1][iy],unIDR : unIDR=unIDR+1
	else
		hotspot2 offset_prof[is][ix], offset_prof_arr[is][iy],unIDR : unIDR=unIDR+1
		hotspot2 offset_prof[ie][ix], offset_prof_arr[ie][iy],unIDR : unIDR=unIDR+1
	endif
return

"emp_fill":
	if name_prof_i=1 then
		put 0,0,900+jt
		put ds/2-ws,360,4000+jt
	endif
	if name_prof_i=2 then
		if det_lvl_2d = 2 then
			put -ws/2+ft,-ds/2+wt,jt
			put ws/2-ft,-ds/2+wt,jt
			put ws/2-ft,-ds/2+ft,900+jt
			put 0,90,4000+jt
			put ws/2-wt,-ds/2+ft,jt
			put ws/2-wt,ds/2-ft,jt
			put ws/2-ft,ds/2-ft,900+jt
			put 0,90,4000+jt
			put ws/2-ft,ds/2-wt,jt
			put -ws/2+ft,ds/2-wt,jt
			put -ws/2+ft,ds/2-ft,900+jt
			put 0,90,4000+jt
			put -ws/2+wt,ds/2-ft,jt
			put -ws/2+wt,-ds/2+ft,jt
			put -ws/2+ft,-ds/2+ft,900+jt
			put 0,90,4000+jt
			put -ws/2+ft,-ds/2+wt,jt
			put -ws/2+ft,-ds/2+wt,-1
		else
			put wt-ws/2,wt-ds/2,jt
			put ws/2-wt,wt-ds/2,jt
			put ws/2-wt,ds/2-wt,jt
			put wt-ws/2,ds/2-wt,jt
			put wt-ws/2,wt-ds/2,-1
		endif
	endif
	if name_prof_i=3 then return
	if name_prof_i=4 then return
	if name_prof_i=5 then return
	if name_prof_i=8 then return
	if name_prof_i=9 then return
	if nsp/3>0 and abs(int(nsp/3)-nsp/3)<EPS then
		if emp_fill_pen<EPS or emp_fill_pen>254 then emp_fill_pen = 1
		if emp_fill_back<EPS or emp_fill_back>254 then emp_fill_back = 19
		if emp_fill<EPS then emp_fill = GLOB_FILL_INDEX_BACKGROUND
		pen emp_fill_pen
		fill emp_fill
		POLY2_b nsp/3, 2+4+8, emp_fill_pen, emp_fill_back,get(nsp)
	endif
return

"prof2d_1det":
	put 0,0,900+jt
	put ds/2,360,4000+jt
	put ds/2-ws,360,4000+jt
return

"prof2d_2simp":
	put -ws/2,-ds/2,jt
	put ws/2,-ds/2,jt
	put ws/2,ds/2,jt
	put -ws/2,ds/2,jt
	put -ws/2,-ds/2,-1
	if has_hole=1 then
		put wt-ws/2,wt-ds/2,jt
		put ws/2-wt,wt-ds/2,jt
		put ws/2-wt,ds/2-wt,jt
		put wt-ws/2,ds/2-wt,jt
		put wt-ws/2,wt-ds/2,-1
	endif
return
"prof2d_2det":
	put -ws/2+ft,-ds/2,jt
	put ws/2-ft,-ds/2,jt
	put ws/2-ft,-ds/2+ft,900+jt
	put 0,90,4000+jt
	put ws/2,-ds/2+ft,jt
	put ws/2,ds/2-ft,jt
	put ws/2-ft,ds/2-ft,900+jt
	put 0,90,4000+jt
	put ws/2-ft,ds/2,jt
	put -ws/2+ft,ds/2,jt
	put -ws/2+ft,ds/2-ft,900+jt
	put 0,90,4000+jt
	put -ws/2,ds/2-ft,jt
	put -ws/2,-ds/2+ft,jt
	put -ws/2+ft,-ds/2+ft,900+jt
	put 0,90,4000+jt
	put -ws/2+ft,-ds/2,jt
	put -ws/2+ft,-ds/2,-1
	if has_hole=1 then
		put -ws/2+ft,-ds/2+wt,jt
		put ws/2-ft,-ds/2+wt,jt
		put ws/2-ft,-ds/2+ft,900+jt
		put 0,90,4000+jt
		put ws/2-wt,-ds/2+ft,jt
		put ws/2-wt,ds/2-ft,jt
		put ws/2-ft,ds/2-ft,900+jt
		put 0,90,4000+jt
		put ws/2-ft,ds/2-wt,jt
		put -ws/2+ft,ds/2-wt,jt
		put -ws/2+ft,ds/2-ft,900+jt
		put 0,90,4000+jt
		put -ws/2+wt,ds/2-ft,jt
		put -ws/2+wt,-ds/2+ft,jt
		put -ws/2+ft,-ds/2+ft,900+jt
		put 0,90,4000+jt
		put -ws/2+ft,-ds/2+wt,jt
		put -ws/2+ft,-ds/2+wt,-1
	endif
return

"prof2d_3simp":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5-ft,jt
	put wt-ws*0.5,ds*0.5-ft,jt
	put wt-ws*0.5,ft-ds*0.5,jt
	put ws*0.5,ft-ds*0.5,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return
"prof2d_3det":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5-(ft-tr),jt
	put tr,-90,2000+jt
	put ws*0.5-tr,ds*0.5-ft,jt
	put -ws*0.5+wt+rr,ds*0.5-ft,jt
	put rr,90,2000+jt
	put -ws*0.5+wt,ds*0.5-ft-rr,jt
	put -ws*0.5+wt,-ds*0.5+ft+rr,jt
	put rr,90,2000+jt
	put -ws*0.5+wt+rr,-ds*0.5+ft,jt
	put ws*0.5-tr,-ds*0.5+ft,jt
	put tr,-90,2000+jt
	put ws*0.5,-ds*0.5+ft-tr,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return

"prof2d_4simp":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5+ft,jt
	put -wt*0.5,-ds*0.5+ft,jt
	put -wt*0.5,ds*0.5-ft,jt
	put -ws*0.5,ds*0.5-ft,jt
	put -ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5-ft,jt
	put wt*0.5,ds*0.5-ft,jt
	put wt*0.5,-ds*0.5+ft,jt
	put ws*0.5,-ds*0.5+ft,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return
"prof2d_4det":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5+(ft-tr),jt
	put tr,-90,2000+jt
	put -ws*0.5+tr,-ds*0.5+ft,jt
	put -wt*0.5-rr,-ds*0.5+ft,jt
	put rr,90,2000+jt
	put -wt*0.5,-ds*0.5+ft+rr,jt
	put -wt*0.5,ds*0.5-ft-rr,jt
	put rr,90,2000+jt
	put -wt*0.5-rr,ds*0.5-ft,jt
	put -ws*0.5+tr,ds*0.5-ft,jt
	put tr,-90,2000+jt
	put -ws*0.5,ds*0.5-(ft-tr),jt
	put -ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5-(ft-tr),jt
	put tr,-90,2000+jt
	put ws*0.5-tr,ds*0.5-ft,jt
	put wt*0.5+rr,ds*0.5-ft,jt
	put rr,90,2000+jt
	put wt*0.5,ds*0.5-ft-rr,jt
	put wt*0.5,-ds*0.5+ft+rr,jt
	put rr,90,2000+jt
	put wt*0.5+rr,-ds*0.5+ft,jt
	put ws*0.5-tr,-ds*0.5+ft,jt
	put tr,-90,2000+jt
	put ws*0.5,-ds*0.5+(ft-tr),jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return

"prof2d_5simp":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,ds*0.5,jt
	put -ws*0.5+wt,ds*0.5,jt
	put -ws*0.5+wt,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return
"prof2d_5det":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,ds*0.5,jt
	put -ws*0.5+wt-rr,ds*0.5,jt
	put rr,-90,2000+jt
	put -ws*0.5+wt,ds*0.5-rr,jt
	put -ws*0.5+wt,-ds*0.5+wt+ft,jt
	put ft,90,2000+jt
	put -ws*0.5+wt+ft,-ds*0.5+wt,jt
	put ws*0.5-rr,-ds*0.5+wt,jt
	put rr,-90,2000+jt
	put ws*0.5,-ds*0.5-rr+wt,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return

"prof2d_8simp":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,ds*0.5,jt
	put -ws*0.5+wt,ds*0.5,jt
	put -ws*0.5+wt,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return
"prof2d_8det":
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5+wt+ft,-ds*0.5,jt
	put wt+ft,-90,2000+jt
	put -ws*0.5,-ds*0.5+wt+ft,jt
	put -ws*0.5,ds*0.5,jt
	put -ws*0.5+wt,ds*0.5,jt
	put -ws*0.5+wt,-ds*0.5+wt+ft,jt
	put ft,90,2000+jt
	put -ws*0.5+wt+ft,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5,jt
	put ws*0.5,-ds*0.5,-1
return

"prof2d_9simp":
	put -ws*0.5,-ds*0.5,jt
	put -ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5-wt,jt
	put wt-ws*0.5,ds*0.5-wt,jt
	put wt-ws*0.5,wt-ds*0.5,jt
	put ws*0.5,wt-ds*0.5,jt
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5,-ds*0.5,-1
return
"prof2d_9det":
	put ws*0.5,-ds*0.5,jt
	put -ws*0.5+wt+ft,-ds*0.5,jt
	put wt+ft,-90,2000+jt
	put -ws*0.5,-ds*0.5+wt+ft,jt
	put -ws*0.5,ds*0.5-wt-ft,jt
	put wt+ft,-90,2000+jt
	put -ws*0.5+wt+ft,ds*0.5,jt
	put ws*0.5,ds*0.5,jt
	put ws*0.5,ds*0.5-wt,jt
	put -ws*0.5+wt+ft,ds*0.5-wt,jt
	put ft,90,2000+jt
	put -ws*0.5+wt,ds*0.5-wt-ft,jt
	put -ws*0.5+wt,-ds*0.5+wt+ft,jt
	put ft,90,2000+jt
	put -ws*0.5+wt+ft,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5+wt,jt
	put ws*0.5,-ds*0.5,-1
return

"set_show_2d":
	success = LIBRARYGLOBAL ("Libraryflobals-km", "show_connect", show_connect)
	show = 0
	if show_connect and upr then show = 1
	if not(upr) then show = 1
	if show then 
		call "Показ_по_типам" PARAMETERS tip_konstr=tip_konstr RETURNED_PARAMETERS show_type
		show = show_type
	endif
	if show then 
		call "Показ_по_группам" PARAMETERS ALL el_views=el_views RETURNED_PARAMETERS show_el
		show = show_el
	endif
	if show then 
		call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
		show = show_subpos
	endif
	if show then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_izd", show_izd)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_metall", show_metall)
		if type_spec = "Металл" and not(show_metall) then show = 0
		if type_spec = "Изделие" and not(show_izd) then show = 0
	endif
	if show then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "ignore_id", ignore_id)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_ingore", show_ingore)
		show_spec = 0 : IF STRSTR (GLOB_ID, ignore_id, 1) > 0 or type_spec="Не специфицировать" THEN show_spec = 1
		if not(show_ingore) and show_spec then show = 0
	endif
	if show then
		show_if_vert=1 : show_if_gor=1 : show_if_ang=1
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_if_vert", show_if_vert)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_if_gor", show_if_gor)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_if_ang", show_if_ang)
		if el_type_view=1 and not(show_if_gor) and show then show = 0
		if el_type_view=2 and not(show_if_vert) and show then show = 0
		if el_type_view=3 and not(show_if_ang) and show then show = 0
	endif

	if not(type_axis) then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_axsis", show_axsis)
		success = LIBRARYGLOBAL ("Libraryflobals-km", "show_only_axsis", show_only_axsis)
	else
		if type_axis=1 then
			show_axsis = 0
			show_only_axsis = 0
		endif
		if type_axis=2 then
			show_axsis = 1
			show_only_axsis = 0
		endif
		if type_axis=3 then
			show_axsis = 0
			show_only_axsis = 1
		endif
	endif
	if show_axsis then show_line = 0
	if show_only_axsis then show_line = 1
	
	if not(det_lvl) then
		success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
		IF gs_detlevel_3D=`Простой` then
			det_lvl_2d = 1
			det_lvl_3d = 1
		endif
		IF gs_detlevel_3D=`Детальный` THEN
			det_lvl_2d = 2
			det_lvl_3d = 2
		endif
		IF gs_detlevel_3D=`Откл` THEN
			det_lvl_2d = 2
			det_lvl_3d = 0
		endif
	else
		if det_lvl=1 then
			det_lvl_2d = 2
			det_lvl_3d = 2
		endif
		if det_lvl=2 then
			det_lvl_2d = 1
			det_lvl_3d = 1
		endif
		if det_lvl=3 then
			det_lvl_2d = 2
			det_lvl_3d = 0
		endif
		if det_lvl=4 then
			det_lvl_2d = 0
			det_lvl_3d = 2
		endif
	endif
	show_list = 0 : success = LIBRARYGLOBAL ("Libraryflobals-km", "show_list", show_list)
	if strlen(syst_name)>1 then
		success = LIBRARYGLOBAL ("Libraryflobals-mep", "show_op", show_op)
		call "Показ_систем" PARAMETERS All isMep=0, isEqp=1 RETURNED_PARAMETERS show_el
		if show = 1 and show_el < EPS then show = 0
		if show = 1 and show_op < EPS then show = 0
	endif
return
