
if GLOB_SCALE>25 then end
gosub "set_show_3d"
if not(show) or abs(l_pos_weld)<EPS then end
PAPER_TO_MODEL = GLOB_SCALE / 1000
L_strih = weld_L_strih_mm * PAPER_TO_MODEL
Step_strih = weld_Step_strih_mm * PAPER_TO_MODEL

if type_weld = 1 and type_weld_ang = 0 then
!	_l = SQR(L_strih*cos(ang_det)*L_strih*cos(ang_det)+L_strih*sin(ang_det)*L_strih*sin(ang_det))
	Step_strih = Step_strih*sin(ang_det)-Step_strih*cos(ang_det)
endif

Dz = 0.00004
if is_call=0 then gosub "hotspot3d_coord"

add pos_weld[is][ix], pos_weld[is][iy], pos_weld[is][iz]
rotz ang_pl
roty ang
if is_call=0 then gosub "rot3d_axis"
rotz ang_axis
if is_call=0 then 
	if type_weld_ang = 0 then
		gosub "rot3d_det"
	else
		ang_det = 180
	endif
endif

n_strih = int(l_pos_weld/Step_strih)
cz_start = (l_pos_weld-n_strih*Step_strih)/2
if show_weld = 1 and n_strih>2 then
	cz = cz_start
	for n=0 to n_strih-(type_weld = 1)
		gosub "shtih"
		cz = cz + Step_strih
	next n
	if show_weld_line then lin_ Dz,Dz,cz_start, Dz,Dz,cz - Step_strih*(type_weld = 0)
endif

if GLOB_VIEW_TYPE<>2 then
	L_strih_t = L_strih - Dz
	if show_weld = 2 then L_strih = 0.005
	!Угловой
	if type_weld_ang = 0 then PRISM_ 3, l_pos_weld, Dz,Dz,8, L_strih_t,Dz,8, L_strih*cos(ang_det)+Dz,L_strih*sin(ang_det)-Dz,8
	!Стыковой
	if type_weld_ang = 1 then PRISM_ 5, l_pos_weld, Dz,Dz,8,-L_strih_t/2,Dz,8,Dz,L_strih_t/4,8,L_strih_t/2,Dz,8,Dz,Dz,8
endif

if show_weld = 1 and n_strih>2 then
	gosub "shtih_end"
	addz l_pos_weld
	gosub "shtih_end"
	del 1
endif

del 4
shadow on
end

"shtih":
	!Автоматическая
	if type_weld = 0 then
		!Угловой
		if type_weld_ang = 0 then
			lin_ L_strih, Dz, cz, L_strih*cos(ang_det), L_strih*sin(ang_det), cz
		endif
		!Стыковой
		if type_weld_ang = 1 then
			lin_ L_strih/2, Dz, cz, -L_strih/2, Dz, cz
		endif
	endif
	!Ручная
	if type_weld = 1 then
		!Угловой
		if type_weld_ang = 0 then
			lin_ L_strih,Dz,cz+L_strih*sin(ang_det),  L_strih*cos(ang_det),L_strih*sin(ang_det),cz+L_strih*cos(ang_det)
			lin_ L_strih,Dz,cz+L_strih*cos(ang_det),  L_strih*cos(ang_det),L_strih*sin(ang_det),cz+L_strih*sin(ang_det)
		endif
		!Стыковой
		if type_weld_ang = 1 then
			lin_ L_strih/2,Dz,cz+L_strih,-L_strih/2,Dz,cz
			lin_ -L_strih/2,Dz,cz+L_strih,L_strih/2,Dz,cz
		endif
	endif
return

"shtih_end":
	!Угловой
	if type_weld_ang = 0 then
		for kx = L_strih/20 to L_strih step L_strih/20
			lin_ kx, 0, 0, kx*cos(ang_det), kx*sin(ang_det), 0
		next kx
	endif
	lin_ L_strih, 0, 0, L_strih*cos(ang_det), L_strih*sin(ang_det), 0
	lin_ 0, 0, 0, L_strih*sin(ang_det), L_strih*cos(ang_det), 0
	lin_ 0, 0, 0, L_strih*cos(ang_det), L_strih*sin(ang_det), 0
	!Стыковой
return

"hotspot3d_rphi":
	add pos_weld[is][ix], pos_weld[is][iy], pos_weld[is][iz]
	HOTSPOT 0.5*l_axis_2d, 0,	0.5*l_pos_weld*cos(ang), unID, ang_pl,	4+128: unID=unID+1
	HOTSPOT 0,	0,	0.5*l_pos_weld*cos(ang),	unID,	ang_pl,	6: unID=unID+1
	HOTSPOT 0,	0,	1,	unID,	ang_pl,	7: unID=unID+1
	HOTSPOT 0.5*l_axis_2d*cos(ang_pl),	0.5*l_axis_2d*sin(ang_pl),	0.5*l_pos_weld*cos(ang),	unID, ang_pl, 5: unID=unID+1
	rotz ang_pl
	HOTSPOT 0, 0,	0.6*l_pos_weld, unID, ang,	4+128: unID=unID+1
	HOTSPOT 0,	0,	0,	unID,	ang,	6: unID=unID+1
	HOTSPOT 0,	1,	0,	unID,	ang,	7: unID=unID+1
	HOTSPOT 0.6*l_pos_weld*sin(ang),0,	0.6*l_pos_weld*cos(ang),	unID, ang, 5: unID=unID+1
	roty ang
	HOTSPOT	0,	0,	0,	unID,	l_pos_weld,	1+128: unID=unID+1
	HOTSPOT	0,	0,	l_pos_weld,	unID,	l_pos_weld,	2: unID=unID+1
	HOTSPOT	0,	0,	-1,	unID,	l_pos_weld,	3: unID=unID+1
	del 3
	HOTSPOT	pos_weld[is][ix],	pos_weld[is][iy],	pos_weld[is][iz]
	HOTSPOT	pos_weld[ie][ix],	pos_weld[ie][iy],	pos_weld[ie][iz]
return

"hotspot3d_coord":
	!-- Редактирование длины в 3д --
	p_name = "X начало"
	HOTSPOT	0,					pos_weld[is][iy],	pos_weld[is][iz],	unID,	pos_weld[is][ix],	1+128, pos_weld[is][ix], p_name: unID=unID+1
	HOTSPOT	pos_weld[is][ix],	pos_weld[is][iy],	pos_weld[is][iz],	unID,	pos_weld[is][ix],	2, pos_weld[is][ix], p_name: unID=unID+1
	HOTSPOT	-1,					pos_weld[is][iy],	pos_weld[is][iz],	unID,	pos_weld[is][ix],	3, pos_weld[is][ix], p_name: unID=unID+1

	p_name = "Y начало"
	HOTSPOT	pos_weld[is][ix],					0,	pos_weld[is][iz],	unID,	pos_weld[is][iy],	1+128, pos_weld[is][iy], p_name: unID=unID+1
	HOTSPOT	pos_weld[is][ix],	pos_weld[is][iy],	pos_weld[is][iz],	unID,	pos_weld[is][iy],	2, pos_weld[is][iy], p_name: unID=unID+1
	HOTSPOT	pos_weld[is][ix],	-1,					pos_weld[is][iz],	unID,	pos_weld[is][iy],	3, pos_weld[is][iy], p_name: unID=unID+1

	p_name = "Z начало"
	HOTSPOT	pos_weld[is][ix],	pos_weld[is][iy],	0,					unID,	pos_weld[is][iz],	1+128, pos_weld[is][iz], p_name: unID=unID+1
	HOTSPOT	pos_weld[is][ix],	pos_weld[is][iy],	pos_weld[is][iz],	unID,	pos_weld[is][iz],	2, pos_weld[is][iz], p_name: unID=unID+1
	HOTSPOT	pos_weld[is][ix],	pos_weld[is][iy],	-1,					unID,	pos_weld[is][iz],	3, pos_weld[is][iz], p_name: unID=unID+1

	p_name = "X конец"
	HOTSPOT	0,					pos_weld[ie][iy],	pos_weld[ie][iz],	unID,	pos_weld[ie][ix],	1+128, pos_weld[ie][ix], p_name: unID=unID+1
	HOTSPOT	pos_weld[ie][ix],	pos_weld[ie][iy],	pos_weld[ie][iz],	unID,	pos_weld[ie][ix],	2, pos_weld[ie][ix], p_name: unID=unID+1
	HOTSPOT	-1,					pos_weld[ie][iy],	pos_weld[ie][iz],	unID,	pos_weld[ie][ix],	3, pos_weld[ie][ix], p_name: unID=unID+1

	p_name = "Y конец"
	HOTSPOT	pos_weld[ie][ix],					0,	pos_weld[ie][iz],	unID,	pos_weld[ie][iy],	1+128, pos_weld[ie][iy], p_name: unID=unID+1
	HOTSPOT	pos_weld[ie][ix],	pos_weld[ie][iy],	pos_weld[ie][iz],	unID,	pos_weld[ie][iy],	2, pos_weld[ie][iy], p_name: unID=unID+1
	HOTSPOT	pos_weld[ie][ix],	-1,					pos_weld[ie][iz],	unID,	pos_weld[ie][iy],	3, pos_weld[ie][iy], p_name: unID=unID+1

	p_name = "Z конец"
	HOTSPOT	pos_weld[ie][ix],	pos_weld[ie][iy],	0,					unID,	pos_weld[ie][iz],	1+128, pos_weld[ie][iz], p_name: unID=unID+1
	HOTSPOT	pos_weld[ie][ix],	pos_weld[ie][iy],	pos_weld[ie][iz],	unID,	pos_weld[ie][iz],	2, pos_weld[ie][iz], p_name: unID=unID+1
	HOTSPOT	pos_weld[ie][ix],	pos_weld[ie][iy],	-1,					unID,	pos_weld[ie][iz],	3, pos_weld[ie][iz], p_name: unID=unID+1
return

"rot3d_axis":
	addz l_pos_weld/2
	HOTSPOT 0,	0, 0,	unID,	ang_axis,	6: unID=unID+1
	HOTSPOT L_strih/2, 0, 0,	unID,	ang_axis,	4+128: unID=unID+1
	HOTSPOT L_strih*cos(ang_axis)/2, L_strih*sin(ang_axis)/2, 0,	unID,	ang_axis, 5: unID=unID+1
	HOTSPOT 0,	0, 1,	unID,	ang_axis,	7: unID=unID+1
	del 1
return

"rot3d_det":
	addz l_pos_weld/2
	HOTSPOT 0,	0, 0,	unID,	ang_det,	6: unID=unID+1
	HOTSPOT L_strih, 0, 0,	unID,	ang_det,	4+128: unID=unID+1
	HOTSPOT L_strih*cos(ang_det), L_strih*sin(ang_det), 0,	unID,	ang_det, 5: unID=unID+1
	HOTSPOT 0,	0, 1,	unID,	ang_det,	7: unID=unID+1
	del 1
return

"set_show_3d":
	show = 1
	success = LIBRARYGLOBAL ("Libraryflobals-km", "show_only_axsis", show_only_axsis)
	IF show = 1 and show_only_axsis = 1 then show = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "gs_detlevel_3D", gs_detlevel_3D)
	IF gs_detlevel_3D=`Простой` or gs_detlevel_3D=`Откл` and show = 1 then show = 0
	show_weld = 0 : success = LIBRARYGLOBAL ("Libraryflobals-km", "show_weld", show_weld)
	IF show = 1 and show_weld = 0 then show = 0
	if show and strlen(tip_konstr)>1 then 
		call "Показ_по_типам" PARAMETERS tip_konstr=tip_konstr RETURNED_PARAMETERS show_type
		show = show_type
	endif
	if show then 
		call "Показ_по_группам" PARAMETERS ALL el_views=el_views RETURNED_PARAMETERS show_el
		show = show_el
	endif
	if show then 
		call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
		show = show_subpos
	endif
	weld_L_strih_mm = 2 : success = LIBRARYGLOBAL ("Libraryflobals-km", "weld_L_strih_mm", weld_L_strih_mm)
	weld_Step_strih_mm = 2 : success = LIBRARYGLOBAL ("Libraryflobals-km", "weld_Step_strih_mm", weld_Step_strih_mm)
	if show_weld = 1 then 
		DEFINE MATERIAL "Empty" 1,0,0,0,0,0,0.0,1,0,0
		material "Empty"
		pen 1
		SECT_ATTRS "Фон", 1, 1, 1, 1
	else
		tbmatname = ttype_weld[type_weld+1] + " сварка"
		tbmat = IND(BUILDING_MATERIAL, tbmatname)
		if tbmat>0 then
			BUILDING_MATERIAL tbmat
			SECT_ATTRS{2} 1
		else
			dim weld_mat_arr[]
			success = LIBRARYGLOBAL ("Libraryflobals-km", "weld_mat_arr", weld_mat_arr)
			weld_mat = weld_mat_arr[type_weld+1]
			material weld_mat
			pen 1
			SECT_ATTRS "Фон", 1, 1, 1, 1
		endif
	endif
	RESOL 12
	shadow off
return
