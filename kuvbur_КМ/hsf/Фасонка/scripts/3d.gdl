
if strlen(syst_name)>1 then
	if show_el < EPS then end
	IF show_op < EPS THEN END
	CALL "MEP_m_RequestSystemGlobals" PARAMETERS ALL one_ConID=0 returned_parameters gs_cont_pe, sMat, gs_InsMat
endif

call "macro_get_mat" parameters all, RETURNED_PARAMETERS tcol_mat, tbmat
if tcol_mat>1 then col_mat = tcol_mat
if strlen(syst_name)>1 then col_mat = sMat
if gs_shadow=0 then
	shadow off, off
else
	shadow on
endif

if strlen(syst_name)>1 then col_mat = sMat
pen gs_cont_pen
if tbmat>0 then
	BUILDING_MATERIAL tbmat
	SECT_ATTRS{2} gs_cont_pen, gs_line_type
else
	SECT_ATTRS gs_fill_type_sec, gs_back_pen, gs_fill_pen, gs_sec_pen, gs_line_type
	fill gs_fill_type_sec
	line_type gs_line_type
endif
material col_mat


if show_mvo_proj=2 then
	show_mvo_proj = 0
	success = LIBRARYGLOBAL ("Libraryflobals-km", "show_mvo_proj", show_mvo_proj)
	if show_mvo_proj then
		use_mvo_h = 0 : success = LIBRARYGLOBAL ("Libraryflobals-km", "use_mvo_h", use_mvo_h)
		if use_mvo_h then
			success = LIBRARYGLOBAL ("Libraryflobals-km", "t_proj_down", t_proj_down)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "t_proj_up", t_proj_up)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "h_proj_down", h_proj_down)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "h_proj_up", h_proj_up)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "is_show_lower", is_show_lower)
			success = LIBRARYGLOBAL ("Libraryflobals-km", "is_show_upper", is_show_upper)
		endif
	endif
endif

If GLOB_VIEW_TYPE = 2 and not(show_only_axsis) and cutplane_enable and show_mvo_proj THEN
	if t_proj_down="Низ элемента" and t_proj_up="Верх элемента" then
		cutplane_enable = 0
	else
		show_axsis = 0
		show_line = 0
	
		if t_proj_down = "Низ элемента" then h_proj_down = -2
		if t_proj_down = "Низ диапазона этажа" then h_proj_down = GLOB_CUTPLANES_INFO[3] + GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV
		if t_proj_down = "П. сечения этажа" then h_proj_down = GLOB_CUTPLANES_INFO[1] + GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV
	
		if t_proj_up = "Верх элемента" then h_proj_up = 2
		if t_proj_up = "Верх диапазона этажа" then h_proj_up = GLOB_CUTPLANES_INFO[2] + GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV
		if t_proj_up = "П. сечения этажа" then h_proj_up = GLOB_CUTPLANES_INFO[1] + GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV
	
		if n_part_show = -1 then !Ниже сечения
			pen gs_cont_pen_down
			fill gs_fill_type_down
			line_type gs_line_type_down
			SECT_ATTRS gs_fill_type_down, gs_back_pen_down, gs_fill_pen_down, gs_cont_pen_down, gs_line_type_down
			addz h_proj_down
			CUTPLANE 0
			del 1
			cutplane_enable = 1
		endif
		if n_part_show = 0 then !В сечении
			addz h_proj_up
			CUTPLANE 0
			del 1
			if t_proj_down <> "Низ элемента" then 
				addz h_proj_down
				mulz -1
				CUTPLANE 0
				del 2
				cutplane_enable = 2
			endif
		endif
		if n_part_show = 1 then !Выше сечения
			pen gs_cont_pen_up
			fill gs_fill_type_up
			line_type gs_line_type_up
			SECT_ATTRS gs_fill_type_up, gs_back_pen_up, gs_fill_pen_up, gs_cont_pen_up, gs_line_type_up
			addz h_proj_up
			mulz -1
			CUTPLANE 0
			del 2
			cutplane_enable = 1
		endif
	endif
endIf

del top
roty ry
rotx rx
rotz rz

for i = 1 to n_t
	j = i - 1 : if i=1 then j = n_t
	dx = kontur[j][1]-kontur[i][1]
	dy = kontur[j][2]-kontur[i][2]
	gosub "xytorphi3d"
	gosub "change_3d"
	if r-EPS > t_sten*3-EPS then
		if weld_side[i]>EPS then
			calltype_weld = weld_side[i]
			ang_axis_weld = 90 : if calltype_weld = 2 or calltype_weld = 4 then ang_axis_weld = -90
			ang_axis_weld = weld_side_ang[i]
			pos_weld[is][ix]=kontur[i][1] : pos_weld[is][iy]=kontur[i][2] : pos_weld[is][iz]=0
			pos_weld[ie][ix]=kontur[j][1] : pos_weld[ie][iy]=kontur[j][2] : pos_weld[ie][iz]=0
			gosub "set_weld"
		endif
		if weld_side_up[i]>EPS then
			calltype_weld = weld_side_up[i]
			ang_axis_weld = 0 : if calltype_weld = 2 or calltype_weld = 4 then ang_axis_weld = 90
			ang_axis_weld = weld_side_ang_up[i]
			pos_weld[is][ix]=kontur[i][1] : pos_weld[is][iy]=kontur[i][2] : pos_weld[is][iz]=ZZYZX
			pos_weld[ie][ix]=kontur[j][1] : pos_weld[ie][iy]=kontur[j][2] : pos_weld[ie][iz]=ZZYZX
			gosub "set_weld"
		endif
	endif
	put kontur[i][1], kontur[i][2]
	hotline kontur[i][1], kontur[i][2], 0, kontur[i+1][1], kontur[i+1][2], 0, unID : unID=unID+1
	hotline kontur[i][1], kontur[i][2], ZZYZX, kontur[i+1][1], kontur[i+1][2], ZZYZX, unID : unID=unID+1
next i
prism nsp/2,ZZYZX,get(nsp)

end
"set_weld":
	call "Сварка 3д" parameters calltype_weld = calltype_weld, show_weld_line=show_weld_line, pos_weld = pos_weld, ang_axis = ang_axis_weld
	pen gs_cont_pen
	fill gs_fill_type_sec
	line_type gs_line_type
	SECT_ATTRS gs_fill_type_sec, gs_back_pen, gs_fill_pen, gs_sec_pen, gs_line_type
return

"change_3d":
	k = 0.004
	HOTSPOT 0, kontur[i][2],0, unID, kontur[i][1], 1+128: unID=unID+1
	HOTSPOT -1, kontur[i][2],0, unID, kontur[i][1], 3: unID=unID+1
	HOTSPOT kontur[i][1], kontur[i][2],0, unID, kontur[i][1], 2: unID=unID+1
	HOTSPOT kontur[i][1], 0,0, unID, kontur[i][2], 1+128: unID=unID+1
	HOTSPOT kontur[i][1], -1,0, unID, kontur[i][2], 3: unID=unID+1
	HOTSPOT kontur[i][1], kontur[i][2],0, unID, kontur[i][2], 2: unID=unID+1
	HOTSPOT 0, kontur[i][2],ZZYZX, unID, kontur[i][1], 1+128: unID=unID+1
	HOTSPOT -1, kontur[i][2],ZZYZX, unID, kontur[i][1], 3: unID=unID+1
	HOTSPOT kontur[i][1], kontur[i][2],ZZYZX, unID, kontur[i][1], 2: unID=unID+1
	HOTSPOT kontur[i][1], 0,ZZYZX, unID, kontur[i][2], 1+128: unID=unID+1
	HOTSPOT kontur[i][1], kontur[i][2],ZZYZX, unID, kontur[i][2], 2: unID=unID+1
	HOTSPOT kontur[i][1], -1,ZZYZX, unID, kontur[i][2], 3: unID=unID+1

	add kontur[i][1], kontur[i][2], 0
	rotz phi
	addx r/3
	if abs(weld_side[i])>EPS then
		HOTSPOT 0,	0, 0,	unID,	weld_side_ang[i],	6,	weld_side_ang[i],	"Поворот шва снизу "+str("%.0", i): unID=unID+1
		HOTSPOT 0, k, 0,	unID,	weld_side_ang[i],	4+128,	weld_side_ang[i],	"Поворот шва снизу "+str("%.0", i): unID=unID+1
		HOTSPOT 0, k*cos(weld_side_ang[i]), k*sin(weld_side_ang[i]),	unID,	weld_side_ang[i], 5,	weld_side_ang[i],	"Поворот шва снизу "+str("%.0", i): unID=unID+1
		HOTSPOT 1,	0, 0,	unID,	weld_side_ang[i],	7,	weld_side_ang[i],	"Поворот шва снизу "+str("%.0", i): unID=unID+1
	endif
	rotx weld_side_ang[i]
	HOTSPOT 0, 0,0, unID, weld_side_coord[i][1], 1+128, weld_side[i], "Тип шва снизу "+str("%.0", i): unID=unID+1
	HOTSPOT 0, -1,0, unID, weld_side_coord[i][1], 3, weld_side[i], "Тип шва снизу "+str("%.0", i): unID=unID+1
	HOTSPOT 0, weld_side_coord[i][1],0, unID, weld_side_coord[i][1], 2, weld_side[i], "Тип шва снизу "+str("%.0", i): unID=unID+1
	del 2
	addz ZZYZX
	addx 2*r/3
	if abs(weld_side_up[i])>EPS then
		HOTSPOT 0,	0, 0,	unID,	weld_side_ang_up[i],	6,	weld_side_ang_up[i],	"Поворот шва сверху "+str("%.0", i): : unID=unID+1
		HOTSPOT 0, k, 0,	unID,	weld_side_ang_up[i],	4+128,	weld_side_ang_up[i],	"Поворот шва сверху "+str("%.0", i): : unID=unID+1
		HOTSPOT 0, k*cos(weld_side_ang_up[i]), k*sin(weld_side_ang_up[i]),	unID,	weld_side_ang_up[i], 5,	weld_side_ang_up[i],	"Поворот шва сверху "+str("%.0", i): unID=unID+1
		HOTSPOT 1,	0, 0,	unID,	weld_side_ang_up[i],	7,	weld_side_ang_up[i],	"Поворот шва сверху "+str("%.0", i): unID=unID+1
	endif
	rotx weld_side_ang_up[i]
	HOTSPOT 0, 0,0, unID, weld_side_coord[i][2], 1+128, weld_side_up[i], "Тип шва сверху "+str("%.0", i): unID=unID+1
	HOTSPOT 0, -1,0, unID, weld_side_coord[i][2], 3, weld_side_up[i], "Тип шва сверху "+str("%.0", i): unID=unID+1
	HOTSPOT 0, weld_side_coord[i][2],0, unID, weld_side_coord[i][2], 2, weld_side_up[i], "Тип шва сверху "+str("%.0", i): unID=unID+1
	del 5
return

"xytorphi3d":
	r = SQR(dx*dx+dy*dy)
	if dx>0 and dy>=0 then phi = atn(dy/dx)
	if dx>0 and dy<0 then phi = atn(dy/dx)+360
	if dx<0 then phi = atn(dy/dx)+180
	if dx=0 and dy>0 then phi = 90
	if dx=0 and dy<0 then phi = 270
	if dx=0 and dy=0 then phi = 0
return
