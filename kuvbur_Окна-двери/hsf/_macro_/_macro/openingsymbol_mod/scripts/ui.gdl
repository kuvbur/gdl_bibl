
_pageStartY = 0
_pageHeight = 266

if _symbol.hasTwoFills then
	_st2Fill		= `2. Штриховка`
	_st2FillType	= `2. Тип Штриховки`
	_st2FillPen		= `2. Перо Штриховки`
	_st2FillBgPen	= `2. Перо Фона Штриховки`
else
	_st2Fill		= `Штриховка`
	_st2FillType	= `Тип Штриховки`
	_st2FillPen		= `Перо Штриховки`
	_st2FillBgPen	= `Перо Фона Штриховки`
endif

! show as single page (unless GLOB_DIAGNOSTICS_MODE)
if GLOB_DIAGNOSTICS_MODE then gosub "diagnosticPage"

_yCurr = _pageStartY

if iSymbolType # SYMBOL_X then
	ui_outfield `Ориентация`, 0, _yCurr, 200, 13

	dim _picOrientation[]
	gosub "getRelevantIcons"	! returns _picOrientation, _sliderDelta

	_yCurr = _yCurr + 18
	ui_infield{3} iOrientation, 60, _yCurr, vardim1(_picOrientation) * 45, 31,
					4, 1, 80, 20,												! 1: types_hl(1)
					45, 31, 45, 31,
					_picOrientation, sOrientation, aOrientation


	_x1		= 305
	_yCurr	= _pageStartY - 3

	if _symbol.hasTwoFills then
		
		_yCurr	= _yCurr + 18
		ui_infield{3} drawFill1, _x1, _yCurr, 444 - _x1, 19,
						7, 0, 0, 0,
						0, 0, 0, 0,
						0, `1. Штриховка`, 0,
						0, `1. Штриховка`, 1

	endif

	_yCurr	= _yCurr + 18
	ui_infield{3} drawFill2, _x1, _yCurr, 444 - _x1, 19,
					7, 0, 0, 0,
					0, 0, 0, 0,
					0, _st2Fill, 0,
					0, _st2Fill, 1


	_yCurr = _pageStartY + 64

	if _symbol.hasBreakPoint then

		ui_slider{2} percBreakpoint, 55, _yCurr + 7, _x1 - 115, 20, 0, 0
		ui_infield{3} percBreakpoint, _x1, _yCurr + 9, 60, 13
		ui_outfield "%", _x1 + 60 + 2, _yCurr + 9, 13, 13

		! id of relevant icons
		if _symbol.type = SYMBOL_BREAK then
			_picID = 100
		else
			_picID = 150
		endif
		_picID = _picID + _sliderDelta + iOrientation

		ui_pict _picID,			! left, 101-194
				5, _yCurr, 45, 31, 1
		ui_pict _picID + 100,	! right, 201-294
				_x1 - 10 - 45, _yCurr, 45, 31, 1

		_yCurr	= _yCurr + 40

	endif

endif

FIELDID = 1
ui_listfield FIELDID, 0, _yCurr, 444, _pageHeight - _yCurr

_itemID = 1
ITEM_MAIN	= 0
ITEM_SUB	= 1

_picCont		= "ui_list_detailLevel2D_hl(1)"
_picContLine	= "ui_list_contLineType_hl(1)"
_picContPen		= "ui_list_contourPen_hl(1)"
_picFillType	= "ui_list_fillType_hl(1)"
_picFillPen		= "ui_list_fillPen_hl(1)"
_piFillBgPen	= "ui_list_fillBgPen_hl(1)"

file_dependence "ui_list_detailLevel2D_hl(1)"
file_dependence "ui_list_contLineType_hl(1)"
file_dependence "ui_list_contourPen_hl(1)"
file_dependence "ui_list_fillType_hl(1)"
file_dependence "ui_list_fillPen_hl(1)"
file_dependence "ui_list_fillBgPen_hl(1)"


ui_listitem{2}			_itemID, FIELDID, 	shrinkOffset,			ITEM_SUB,	0,				`Смещение`				: _itemID = _itemID + 1

if iSymbolType = SYMBOL_X then
	ui_listitem{2}		_itemID, FIELDID, 	drawFill2,				ITEM_MAIN,	0,				`Чертить Штриховку`				: _itemID = _itemID + 1
endif

ui_listitem{2} 			_itemID, FIELDID, 	title_VisibleAttrs,		ITEM_MAIN,	0,				`Видимый`				: _itemID = _itemID + 1
	if _symbol.enableHideContour then
		ui_listitem{2}	_itemID, FIELDID, 	drawContourVisible,		ITEM_SUB,	_picCont,		`Контуры`
	endif
																														  _itemID = _itemID + 1

	! hide line attribs when contour is not shown
	if _symbol.enableHideContour & not(drawContourVisible) then
		_itemID = _itemID + 2
	else
		ui_listitem{2}	_itemID, FIELDID, 	lineVisible,			ITEM_SUB,	_picContLine,	`Тип Линии`				: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	penVisible,				ITEM_SUB,	_picContPen,	`Перо`					: _itemID = _itemID + 1
	endif
	if _symbol.hasTwoFills & drawFill1 then
		ui_listitem{2}	_itemID, FIELDID, 	fill1Visible,			ITEM_SUB,	_picFillType,	`1. Тип Штриховки`			: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill1PenVisible,		ITEM_SUB,	_picFillPen,	`1. Перо Штриховки`			: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill1BgpenVisible,		ITEM_SUB,	_piFillBgPen,	`1. Перо Фона Штриховки`: _itemID = _itemID + 1
	else
		_itemID = _itemID + 3
	endif
	if drawFill2 then
		ui_listitem{2}	_itemID, FIELDID, 	fill2Visible,			ITEM_SUB,	_picFillType,	_st2FillType			: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill2PenVisible,		ITEM_SUB,	_picFillPen,	_st2FillPen				: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill2BgpenVisible,		ITEM_SUB,	_piFillBgPen,	_st2FillBgPen			: _itemID = _itemID + 1
	else
		_itemID = _itemID + 3
	endif

ui_listitem{2} 			_itemID, FIELDID, 	title_HiddenAttrs,		ITEM_MAIN,	0,				`Скрытая/Невидимая Часть`		: _itemID = _itemID + 1
	if _symbol.type # SYMBOL_X then
		if _symbol.type = SYMBOL_DIAG	then _picFlipOrient = 10
		if _symbol.type = SYMBOL_BREAK	then _picFlipOrient = 12
		if _symbol.type = SYMBOL_SHADOW	then _picFlipOrient = 14
		_picFlipOrient = _picFlipOrient + _symbol.hasTwoFills
		ui_listitem{2}	_itemID, FIELDID, 	flipHiddenOrientation,	ITEM_SUB,	_picFlipOrient,	`Развернуть`
	endif
																														  _itemID = _itemID + 1
	if _symbol.enableHideContour then
		ui_listitem{2}	_itemID, FIELDID, 	drawContourHidden,		ITEM_SUB,	_picCont,		`Контуры`
	endif
																														  _itemID = _itemID + 1

	! hide line attribs when contour is not shown
	if _symbol.enableHideContour & not(drawContourHidden) then
		_itemID = _itemID + 2
	else
		ui_listitem{2}	_itemID, FIELDID, 	lineHidden,				ITEM_SUB,	_picContLine,	`Тип Линии`				: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	penHidden,				ITEM_SUB,	_picContPen,	`Перо`					: _itemID = _itemID + 1
	endif
	if _symbol.hasTwoFills & drawFill1 then
		ui_listitem{2}	_itemID, FIELDID, 	fill1Hidden,			ITEM_SUB,	_picFillType,	`1. Тип Штриховки`			: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill1PenHidden,			ITEM_SUB,	_picFillPen,	`1. Перо Штриховки`			: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill1BgpenHidden,		ITEM_SUB,	_piFillBgPen,	`1. Перо Фона Штриховки`: _itemID = _itemID + 1
	else
		_itemID = _itemID + 3
	endif
	if drawFill2 then
		ui_listitem{2}	_itemID, FIELDID, 	fill2Hidden,			ITEM_SUB,	_picFillType,	_st2FillType			: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill2PenHidden,			ITEM_SUB,	_picFillPen,	_st2FillPen				: _itemID = _itemID + 1
		ui_listitem{2}	_itemID, FIELDID, 	fill2BgpenHidden,		ITEM_SUB,	_piFillBgPen,	_st2FillBgPen			: _itemID = _itemID + 1
	else
		_itemID = _itemID + 3
	endif


! =============================================================================
end		! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! =============================================================================


"getRelevantIcons":

	! icon group
	dim _symbolBase[]
		_symbolBase[SYMBOL_X]			= 0
		_symbolBase[SYMBOL_DIAG]		= 20
		_symbolBase[SYMBOL_BREAK]		= 40
		_symbolBase[SYMBOL_SHADOW]		= 60


	! icon row relevant to fill setting
	if _symbol.hasTwoFills then
		if not(drawFill1)	& not(drawFill2)	then _fillDelta = 0
		if drawFill1		& not(drawFill2)	then _fillDelta = 4
		if not(drawFill1)	& drawFill2			then _fillDelta = 8
		if drawFill1		& drawFill2			then _fillDelta = 16
	else
		if not(drawFill2)						then _fillDelta = 0
		if drawFill2							then _fillDelta = 12
	endif

	dim _picOrientation[]
	for i = 1 to vardim1(picOrientation)
		_picOrientation[i] = _symbolBase[_symbol.type] + _fillDelta + picOrientation[i]
	next i

	_sliderDelta = _fillDelta / 4 * 10

return


"diagnosticPage":

	PAGE_ROOT		= -1
	PAGE_DIAGNOSTIC	= 1
	PAGE_SETTINGS	= 2
	
	UI_PAGE PAGE_DIAGNOSTIC, PAGE_ROOT, `Диагностические Параметры`

	_yCurr = _pageStartY
	ui_infield{3} bShowDiagnostics, 0, _yCurr, 444, 13,
					7, 0, 0, 0,
					0, 0, 0, 0,
					0, `Показ диагностической информации`, 0,
					0, `Показ диагностической информации`, 1

	_yCurr = _yCurr + 20

	! show otherwise single page as tabpage
	UI_PAGE PAGE_SETTINGS, PAGE_ROOT, `Параметры Символа Отверстия`

return
