isPlan = (GLOB_VIEW_TYPE=2)
isPrew = (GLOB_PREVIEW_MODE<>0)
isEdit = GLOB_FEEDBACK_MODE
gosub "CW Transformation"
mul2 tranWidth, _yDistort
add2 -tranX, 0
fill gs_fill_type
pen gs_cont_pen
globMVO = GLOB_MVO_CWPANEL_DETLEVEL

if GLOB_FEEDBACK_MODE then globMVO = DETLEVEL3D_SCHEMATIC

if globMVO = DETLEVEL3D_SCHEMATIC then
	line2 leftSide_X, 0, rightSide_X, 0
	end
endif
tGLOB_STRUCTURE_DISPLAY = force_structure_display
if force_structure_display=-1 then tGLOB_STRUCTURE_DISPLAY = GLOB_STRUCTURE_DISPLAY

myProfileIdx = 0 : use_profile = 0
if globMVO = DETLEVEL3D_DETAILED then
	use_profile = use_profile_det
	if use_profile then myProfileIdx = myProfileIdx_det
endif

if globMVO = DETLEVEL3D_SIMPLE then
	if use_profile_det and not(use_profile_simp) then
		myProfileIdx = myProfileIdx_det
		use_profile = use_profile_simp
	endif
	if use_profile_simp then
		myProfileIdx = myProfileIdx_simp
		use_profile = use_profile_simp
	endif
endif
if GLOB_PREVIEW_MODE<>0 or GLOB_FEEDBACK_MODE then use_profile = 0

if myProfileIdx>EPS then
	nComponents = 0
	dim _componentTypes[]
	n = REQUEST ("Profile_components", myProfileIdx, _nComponents, _componentTypes)
	n = REQUEST ("Profile_default_boundingbox", myProfileIdx, xmin, ymin, xmax, ymax)
	if n then
		dim _bShowPart[]
		_needRequestDetails = 0
		for _i = 1 to _nComponents
			_bShowPart[_i] = 1    
			  if tGLOB_STRUCTURE_DISPLAY = 1 AND _componentTypes[_i] <> COMPTYPE_CORE then _bShowPart[_i] = 0
			  if tGLOB_STRUCTURE_DISPLAY = 2 AND _componentTypes[_i] = COMPTYPE_FINISH then _bShowPart[_i] = 0
			  if _bShowPart[_i] then _needRequestDetails = 1
		next _i
		if _needRequestDetails then
			dim _profileGeometryRawData[]
			n = REQUEST ("Profile_default_geometry", myProfileIdx, _profileGeometryRawData)	 
			dim _idxStartPoints[]
			dim _idxEndPoints[]
			_idxStartPoints[1] = _nComponents + 1
			_numPropertybyNodes = 5
			for _i = 2 to _nComponents
				_numCompNodes = _profileGeometryRawData[_i - 1]
				_idxStartPoints[_i] = _idxStartPoints[_i - 1] + _numCompNodes * _numPropertybyNodes
				_idxEndPoints[_i-1] = _idxStartPoints[_i] - 1
			next _i
			_idxEndPoints[_nComponents] = vardim1(_profileGeometryRawData)
		endif
	endif
	th = ymax-ymin
endif

if bOverridepen<>"Ручная настройка" then
	bSucceed = request{2} ("Building_Material_info", panelBMat, "gs_bmat_cutfill_properties", gs_fill_type, gs_fill_pen, gs_back_pen)
endif

if not(use_profile) or myProfileIdx<EPS then
	if nCorners < 3 then end
	add2 0,-ymax
	poly2_b 	5, 1+2+32,
				gs_fill_pen, gs_back_pen,
				leftClamp_X,	0, 					1,
				rightClamp_X,	0, 					1,
				rightClamp_X,	th,		1,
				leftClamp_X,	th,		1,
				leftClamp_X,	0, 					-1
	end
endif

if use_profile and myProfileIdx>EPS then
	for _iComp = 1 to _nComponents
		if _bShowPart[_iComp] then
			if bOverridepen = "Сложный профиль" then
				_bmat = 1
				n = REQUEST{4} ("Profile_component_info", myProfileIdx, _iComp, "gs_profile_outlinepen", gs_cont_pen)
				n = REQUEST{4} ("Profile_component_info", myProfileIdx, _iComp, "gs_profile_bmat", _bmat)
				n = REQUEST{2} ("Building_Material_info", _bmat, "gs_bmat_cutfill_properties", gs_fill_type, gs_fill_pen, gs_back_pen)
			endif
			fill gs_fill_type
			pen gs_fill_pen
			PROJECT2{3} 4, 270, 3+32+512,4+8,gs_back_pen, PARAMETERS inx_component=_iComp
			pen gs_cont_pen
			PROJECT2{3} 4, 270, 3+32+512,1+2,gs_back_pen, PARAMETERS inx_component=_iComp
		endif
	next _iComp
	del 1
endif

end
"CW Transformation":
	leftClamp_X  = AC_PanelCoords[1][1]
	rightClamp_X = AC_PanelCoords[1][1]
	for i = 1 to nCorners
		if AC_PanelCoords[i][1] < leftClamp_X  then leftClamp_X  = AC_PanelCoords[i][1]
		if AC_PanelCoords[i][1] > rightClamp_X then rightClamp_X = AC_PanelCoords[i][1]
	next i
	if abs (a) < EPS then end
	if abs (rightClamp_X - leftClamp_X) > EPS then
		tranWidth = a / (rightClamp_X - leftClamp_X)
	else
		tranWidth = 1
	endif
	if not(ac_originIsFrameCenter) then
		tranX = leftClamp_X
	else
		tranX = 0
	endif
	_yDistort = 1
	if 	abs(CWPANEL_VERTICAL_DIRECTION) > EPS then
		_yDistort = 1 / sin(CWPANEL_VERTICAL_DIRECTION)		! Distort Panel Thickness in case of slanted CW
	endif
return

"showved_2d":
	line2 0,0,1,1
	line2 1,0,0,1
	if nCorners < 3 then end
	max_y = 0 : min_y = 0
	max_x = 0 : min_x = 0
	for i = 1 to vardim1(AC_FrameAxisCoords)
		put AC_FrameAxisCoords[i][1], AC_FrameAxisCoords[i][2], 1
		max_x = max(max_x, AC_FrameAxisCoords[i][1])
		min_x = min(min_x, AC_FrameAxisCoords[i][1])
		max_y = max(max_y, AC_FrameAxisCoords[i][2])
		min_y = min(min_y, AC_FrameAxisCoords[i][2])
	next i
	POLY2 nsp/3, 1+2+4, get (nsp)
	
	l = force_with
	b = max_y
	if is_gor then
		b = force_with
		l = max_x
	endif
return
