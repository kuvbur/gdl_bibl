
category_nam = ""
nam = ""
numbe = ""
cod = ""
ttxt = ""
if not(show_spec) then
	n_rea = 0
	n_rea = REQUEST ("Zone_relations", "", category_nam, cod, nam, numbe)
	if (n_zon <> numbe or name_zone <> nam) and not(mun_zone) then gs_cont_pen = p_err
	if (n_zon = "" or name_zone = "") and not(mun_zone) then gs_cont_pen = p_err
	if n_sloi = -2 then gs_cont_pen = 75
endif
otobr = 0
if annot[1] = 0 then 
	otobr = 1
else
	for i=1 to vardim1(annot)
		if annot[i] = GLOB_SCALE then otobr = 1
	next i
endif
unID=1
show_pol = 0 : show_pot = 0 : show_type_pot = 0 : show_tip_otm = 0
if tip_el = "Пол" then
	success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_pol", show_pol)
	success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_tip_otm", show_tip_otm)
	if otobr and not(show_pol) then otobr = 0
else
	success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_pot", show_pot)
	success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_type_pot", show_type_pot)
	success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_h_pot", show_h_pot)
	if otobr and not(show_pot) and not(show_type_pot) then otobr = 0
endif
success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_num", show_num)
success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_fill", show_fill)
if fillr=0 then show_fill=0
if is_show_fill=0 then show_fill=0
if show_fill then
	gs_fill_type = fillr
	gs_fill_pen = fill_pen
	gs_back_pen = fill_bac
endif
tp = "!"
if show_num then 
	if vartype(n_zon) = 2 then tp = tip_pol + "/" + n_zon
else
	tp = tip_pol
endif
LINE_TYPE SYMB_LINETYPE
success = LIBRARYGLOBAL ("Libraryflobals-as", "Shrift", Shrift)
success = LIBRARYGLOBAL ("Libraryflobals-as", "Shrift_razm_tip", Shrift_razm)
success = LIBRARYGLOBAL ("Libraryflobals-as", "Shrift_razm_razm", Shrift_razm)

DEFINE STYLE "MARK" Shrift, Shrift_razm, 2, Shrift_tip
DEFINE STYLE{2} "MARK_" Shrift, Shrift_razm, Shrift_tip
STYLE "MARK"
sss=request("Height_of_style", "MARK", height, descent, leading)

Shrift_razm = Shrift_razm*GLOB_SCALE / 1000
height = height*GLOB_SCALE / 1000
descent = descent*GLOB_SCALE / 1000
leading = leading*GLOB_SCALE / 1000
dx = height/2-descent-leading-Shrift_razm/2

if show_tip_otm then
	pen 6
	hotspot2 ch_x, ch_y
	text2 ch_x, ch_y, "Тип "+tip_pol + " t=" + str("%.0mm", summ_h)
	text2 ch_x, ch_y-height, "Отм. верха " + str("%.3m", SYMB_POS_Z)
	text2 ch_x, ch_y-height*2, "Отм. низа " + str("%.3m", SYMB_POS_Z-summ_h)
endif

if otobr then
	pen gs_cont_pen
	if tip_el = "Потолок" then
		ch = INITADDONSCOPE ("PolyOperations", "", "")
		PREPAREFUNCTION ch, "CreateContainer", "from_cont", ""
		PREPAREFUNCTION ch, "CreateContainer", "to_cont", ""
		PREPAREFUNCTION ch, "SetSourceContainer",		"from_cont", ""
		PREPAREFUNCTION ch, "SetDestinationContainer",	"to_cont", ""
		dim emp2[][]
		dim emp1[]
		dim vertArray[][]
		dim contourArray[]
		dim resPolyIDArray[] : resPolyIDArray[1] = ""
		dim contArr[] : dim resVertices[]
		LINE_TYPE connt
		if k_otv>0 then
			for j=1 to k_otv
				vertArray = emp2
				contourArray = emp1
				hole_poly = "po"+str("%.0", j)
				nVertices = 0
				nContours = 0
				if j = 1 then n_perv = 1
				if j>1 then n_perv=k_otr_otv[k_otv-1]+1
				for i=1 to k_otr_otv[j]-1
					nVertices = nVertices + 1
					vertArray[nVertices][1] = otr_otv[n_perv+i][1]
					vertArray[nVertices][2] = otr_otv[n_perv+i][2]
					vertArray[nVertices][3] = 0
				next i
				nVertices = nVertices + 1
				vertArray[nVertices][1] = otr_otv[n_perv+k_otr_otv[j]-1][3]
				vertArray[nVertices][2] = otr_otv[n_perv+k_otr_otv[j]-1][4]
				vertArray[nVertices][3] = 0
				nVertices = nVertices + 1
				vertArray[nVertices][1] = otr_otv[n_perv+1][1]
				vertArray[nVertices][2] = otr_otv[n_perv+1][2]
				vertArray[nVertices][3] = 0
				nContours = nContours+1
				contourArray[nContours] = nVertices
				PREPAREFUNCTION ch, "Store", hole_poly, nVertices, nContours, vertArray, contourArray
				PREPAREFUNCTION  ch, "OffsetParams", "", 1, -offset_l
				numPoly = CALLFUNCTION (ch, "ResizeContour", hole_poly, resPolyIDArray)
				polygonID = resPolyIDArray[1]
				gosub 100	! Get one polygon contour data (without contours)
				if nsp > 3 then
					poly2_ nsp/3, 1+4, get(nsp)
				else
					pen 20
					text2 0,0,"!!ERROR!!"
				endif
			next j
		endif
		nVertices = 0
		nContours = 0
		!Наружный контур
		base_poly = "poly1"
		for i=1 to k_otr_pl
			nVertices = nVertices + 1
			vertArray[nVertices][1] = otr_pl[i][1]
			vertArray[nVertices][2] = otr_pl[i][2]
			vertArray[nVertices][3] = 0
		next i
		nVertices = nVertices + 1
		vertArray[nVertices][1] = otr_pl[k_otr_pl][3]
		vertArray[nVertices][2] = otr_pl[k_otr_pl][4]
		vertArray[nVertices][3] = 0
		nContours = nContours + 1
		contourArray[nContours] = nVertices
		PREPAREFUNCTION ch, "Store", base_poly, nVertices, nContours, vertArray, contourArray
		PREPAREFUNCTION  ch, "OffsetParams", "", 1, offset_l
		numPoly = CALLFUNCTION (ch, "ResizeContour", base_poly, resPolyIDArray)
		polygonID = resPolyIDArray[1]
		gosub 100	! Get one polygon contour data (without contours)
		DRAWINDEX 10
		if nsp > 3 then
			poly2_ nsp/3, 1+4, use(nsp)
			for i = 1 to nsp step 3
				hotspot2 get(2)
				t_=get(1)
			next i
		else
			pen 20
			text2 0,0,"!!ERROR!!"
		endif
		CLOSEADDONSCOPE (ch)
		aaz = SYMB_POS_Z + ac_ref_height  * (ac_ref_height>0) - h_otn
		if abs(aaz) < EPS then az = "0.000" else az = str(abs(aaz),6,3)
		if aaz < 0 then znak1 = "-"
		if aaz > 0 then znak1 = "+"
		if abs(aaz) < EPS then znak1 = ""

		if show_type_pot then 
			ttxt = znak1 + az + " Тип " + tp
		else
			if show_num then 
				ttxt = tp + " (" + znak1 + az + ")" 
			else
				ttxt = znak1 + az
			endif
		endif
		if show_h_pot and abs(h_otn)>EPS then
			ttxt = ttxt + " Hподв=" + str("%.0mm", h_otn) + "мм."
			if germ then ttxt = ttxt + " ГЕРМЕТИЧЕН"
		endif
		HOTSPOT2 0, 0, unID: unID=unID+1
		HOTSPOT2 0, pos_y1, unID, pos_x1, 1+128: unID=unID+1
		HOTSPOT2 -1, pos_y1, unID, pos_x1, 3: unID=unID+1
		HOTSPOT2 pos_x1, pos_y1, unID, pos_x1, 2: unID=unID+1
		HOTSPOT2 pos_x1, 0, unID, pos_y1, 1+128: unID=unID+1
		HOTSPOT2 pos_x1, -1, unID, pos_y1, 3: unID=unID+1
		HOTSPOT2 pos_x1, pos_y1, unID, pos_y1, 2: unID=unID+1

		HOTSPOT2 0, pos_y2, unID, pos_x2, 1+128: unID=unID+1
		HOTSPOT2 -1, pos_y2, unID, pos_x2, 3: unID=unID+1
		HOTSPOT2 pos_x2, pos_y2, unID, pos_x2, 2: unID=unID+1
		HOTSPOT2 pos_x2, 0, unID, pos_y2, 1+128: unID=unID+1
		HOTSPOT2 pos_x2, -1, unID, pos_y2, 3: unID=unID+1
		HOTSPOT2 pos_x2, pos_y2, unID, pos_y2, 2: unID=unID+1
		pos_x = pos_x1 - pos_x2
		pos_y = pos_y1 - pos_y2
		!Поворот
		angl = 0
		if pos_x > 0 and pos_y > 0 then
			angl = atn(pos_y/pos_x)
		endif
		if pos_x < 0 and pos_y > 0 then
			angl = 270 - atn(pos_x/pos_y)
		endif
		if pos_x < 0 and pos_y < 0 then
			angl = 90 - atn(pos_x/pos_y)
		endif
		if pos_x > 0 and pos_y < 0 then
			angl = 270 - atn(pos_x/pos_y)
		endif
		if abs(pos_x)<EPS and pos_y < 0 then
			angl = 90
		endif
		if abs(pos_x)<EPS and pos_y > 0 then
			angl = 90
		endif
		if pos_x < 0 and abs(pos_y)<EPS then
			angl = 0
		endif
		if pos_x > 0 and abs(pos_y)<EPS then
			angl = 0
		endif

		HOTSPOT2 0, pos_ytxt, unID, pos_xtxt, 1+128: unID=unID+1
		HOTSPOT2 -1, pos_ytxt, unID, pos_xtxt, 3: unID=unID+1
		HOTSPOT2 pos_xtxt, pos_ytxt, unID, pos_xtxt, 2: unID=unID+1
		HOTSPOT2 pos_xtxt, 0, unID, pos_ytxt, 1+128: unID=unID+1
		HOTSPOT2 pos_xtxt, -1, unID, pos_ytxt, 3: unID=unID+1
		HOTSPOT2 pos_xtxt, pos_ytxt, unID, pos_ytxt, 2: unID=unID+1

		Ll = sqr((pos_x2-pos_x1)*(pos_x2-pos_x1)+(pos_y2-pos_y1)*(pos_y2-pos_y1))
		paragraph "mySamplePar"	2,	! name alignment
					0,			! firstline_indent
					0,			! left_indent
					0,			! right_indent
					1	! line_spacing
			pen 1
			style "MARK_"
			ttxt 
		endparagraph
		TEXTBLOCK "type" 0, 5, 0, 1, 1, 1, "mySamplePar"
		width_ = 0
		height_ = 0
		n = REQUEST ("TEXTBLOCK_INFO", "type", width_, height_)
		width_ = width_ * GLOB_SCALE / 1000
		height_ = height_ * GLOB_SCALE / 1000
		LINE2 pos_x1, pos_y1, pos_x2, pos_y2
		HOTLINE2 pos_x1, pos_y1, pos_x2, pos_y2
		add2 pos_xtxt, pos_ytxt
		rot2 angl
		DRAWINDEX 50
		RICHTEXT2 0, dx, "type"
		DRAWINDEX 20
		put -width_/2, -height_/2,1
		put width_/2, -height_/2,1
		put width_/2, height_/2,1
		put -width_/2, height_/2,1
		pen 19
		set fill GLOB_FILL_INDEX_BACKGROUND
		POLY2_B NSP/3,2+4,19,19,get(NSP)
		del 2
	endif
else
	hotspot2 0,0
	circle2 0,0,0.005
endif
del top
if nsp>0 then n = max(get(nsp))

for i=1 to k_otr_pl
	put otr_pl[i][1],otr_pl[i][2],1+32
next i
put otr_pl[1][1],otr_pl[1][2],-1
if k_otv>=1 then
	for i = 1 to k_otv
		if i = 1 then n_perv = 1
		if i > 1 then n_perv=n_perv + k_otr_otv[i-1]
		for j=1 to k_otr_otv[i]-1
			put otr_otv[n_perv+j][1],otr_otv[n_perv+j][2], 1+16
			put otr_otv[n_perv+j][3],otr_otv[n_perv+j][4], 1+16
		next j
		put otr_otv[n_perv+1][1],otr_otv[n_perv+1][2],-1
	next i
endif
if nsp > 3 then
	pen gs_cont_pen
	DRAWINDEX 40
	set fill gs_fill_type
	LINE_TYPE SYMB_LINETYPE
	st = 1 : if show_fill then st = 1+2+4
	POLY2_B NSP/3,st,gs_fill_pen,gs_back_pen,get(NSP)
else
	pen 20
endif
end

100:
! ==============================================================================
! Get GDL Polygon
! ------------------------------------------------------------------------------
! Input variables:
!	ch:				Channel ID
!	polygonID:		Polygon ID
!
! Returned variables:
!	GDL stack:			GDL polygon
! ==============================================================================
	resVertices = emp1 : contArr = emp1
	numVertices = CALLFUNCTION (ch, "GetVertices", polygonID, resVertices)
	numVertices = numVertices / 3
	numContours = CALLFUNCTION (ch, "GetContourEnds", polygonID, contArr)
	for contIndex = 1 to numContours
		if contIndex = 1 then
			begIdx = 0
		else
			begIdx = contArr[contIndex] - 1
		endif
		if contIndex = numContours then
			endIdx = numVertices - 1
		else
			endIdx = contArr[contIndex + 1] - 2
		endif

		bClosed = 0
		bStartWithLast = 0

		index = endIdx * 3
		lastVertX = resVertices[index + 1]
		lastVertY = resVertices[index + 2]
		lastVertA = resVertices[index + 3]

		if abs(lastVertA) > EPS then
			put lastVertX, lastVertY, 1
			bStartWithLast = 1
		endif

		for vertIndex = begIdx to endIdx
			index = vertIndex * 3
			actVertX = resVertices[index + 1]
			actVertY = resVertices[index + 2]
			actVertA = resVertices[index + 3]

			if vertIndex = begIdx then
				if bStartWithLast then
					ac2gdl_spx = lastVertX
					ac2gdl_spy = lastVertY
				else
					ac2gdl_spx = actVertX
					ac2gdl_spy = actVertY
				endif
			endif

			if abs(lastVertA) > EPS then
				ac2gdl_mx = (actVertX + lastVertX) / 2
				ac2gdl_my = (actVertY + lastVertY) / 2
				ac2gdl_ratio = 1 / 2 / tan (lastVertA / 2)

				ac2gdl_cx = ac2gdl_mx - ac2gdl_ratio * (actVertY - lastVertY)	! Arc center
				ac2gdl_cy = ac2gdl_my + ac2gdl_ratio * (actVertX - lastVertX)

				put ac2gdl_cx,	ac2gdl_cy,	900,
					0,			lastVertA,	4001
			endif

			if vertIndex > begIdx & abs(ac2gdl_spx - actVertX) < EPS & abs(ac2gdl_spy - actVertY) < EPS then
				put actVertX, actVertY, -1
				bClosed = 1
			else
				if vertIndex < endIdx | not(bStartWithLast) then
					put actVertX, actVertY, 1
				endif
			endif

			lastVertX = actVertX
			lastVertY = actVertY
			lastVertA = actVertA

		next vertIndex

		if not(bClosed) then	! Close the polygon if it has holes
			put ac2gdl_spx, ac2gdl_spy, -1
		endif

	next contIndex
return

