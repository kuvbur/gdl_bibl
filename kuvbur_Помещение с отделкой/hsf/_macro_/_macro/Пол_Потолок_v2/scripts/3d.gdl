
del top

if n_sloi <= 0 then goto 30

material mat_pol

success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_nes", show_nes)
success = LIBRARYGLOBAL ("Libraryflobals-pokr", "show_otd", show_otd)

group "pol"
	rotz -SYMB_ROTANGLE
	sgnh = -1
	if tip_el = "Потолок" then
		sgnh = 1 
	else
		if auto_height then sgnh = 1
	endif
	for i=1 to k_otr_pl
		put otr_pl[i][1],otr_pl[i][2]
		hotspot otr_pl[i][1],otr_pl[i][2],0,i
		hotspot otr_pl[i][1],otr_pl[i][2],sgnh*h_pirog
		if tip_el = "Потолок" and h_otn<>0 and strstr(main_name, "плит")=0 then hotspot otr_pl[i][1],otr_pl[i][2],h_otn
		hotline otr_pl[i][1],otr_pl[i][2],0,otr_pl[i][3],otr_pl[i][4],0,unID:unID=unID+1
		hotline otr_pl[i][1],otr_pl[i][2],sgnh*h_pirog,otr_pl[i][3],otr_pl[i][4],-h_pirog,unID:unID=unID+1		
	next i
	if tip_el = "Потолок" then
		for i=1 to n_sloi
			if (pokaz[i] and show_nes) or (pokaz[i]=0 and show_otd) then
				LINE_PROPERTY 2
				pen gs_cont_pen
				SECT_ATTRS strih[i],gs_back_pen,gs_fill_pen,gs_fill_pen
				if i<n_sloi then PRISM NSP/2,tolsh[i],USE(NSP)
				if i=n_sloi then PRISM NSP/2,tolsh[i],GET(NSP)
			endif
			addz tolsh[i]
		next i
	else
		if not(auto_height) then addz -h_pirog
		for i=n_sloi to 1 step -1
			if (pokaz[i] and show_nes) or (pokaz[i]=0 and show_otd) then
				LINE_PROPERTY 2
				pen gs_cont_pen
				SECT_ATTRS strih[i],gs_back_pen,gs_fill_pen,gs_fill_pen
				if i>1 then PRISM NSP/2,tolsh[i],USE(NSP)
				if i=1 then PRISM NSP/2,tolsh[i],GET(NSP)
			endif
			addz tolsh[i]
		next i
	endif
endgroup

del top

group "operator"
	addz -h_pirog*2
	rotz -SYMB_ROTANGLE
	if k_otv>=1 then
		for i = 1 to k_otv
			if i = 1 then n_perv = 1
			if i > 1 then n_perv=k_otr_otv[k_otv-1]+1
			for j=1 to k_otr_otv[i]-1
				put otr_otv[n_perv+j][1],otr_otv[n_perv+j][2]
				put otr_otv[n_perv+j][3],otr_otv[n_perv+j][4]
				hotspot otr_otv[n_perv+j][1],otr_otv[n_perv+j][2],0,i
				hotspot otr_otv[n_perv+j][1],otr_otv[n_perv+j][2],h_pirog
				hotline otr_otv[n_perv+j][1],otr_otv[n_perv+j][2],0,otr_otv[n_perv+j][3],otr_otv[n_perv+j][4],0,unID:unID=unID+1
				hotline otr_otv[n_perv+j][1],otr_otv[n_perv+j][2],0,otr_otv[n_perv+j][3],otr_otv[n_perv+j][4],h_pirog,unID:unID=unID+1
			next j
			PRISM NSP/2,h_pirog*5+2,GET(NSP)
		next i
	endif
endgroup

result_1=SUBGROUP("pol","operator")
if tip_el = "Потолок" and strstr(main_name, "плит")=0 then 
	HOTSPOT ch_x, ch_y, ac_ref_height, unID, h_otn, 1: unID=unID+1
	HOTSPOT ch_x, ch_y, ac_ref_height-h_otn, unID, h_otn, 2: unID=unID+1
	HOTSPOT ch_x, ch_y, ac_ref_height*2, unID, h_otn, 3: unID=unID+1
	addz ac_ref_height-h_otn
endif
PLACEGROUP result_1
KILLGROUP "pol"
KILLGROUP "operator"
KILLGROUP result_1

30:

