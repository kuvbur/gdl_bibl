
EPS = 0.0001
dim tov_norm[]
dim iov_norm[] : i=0
i=i+1 : iov_norm[i]=1 : tov_norm[i] = "Нет"
i=i+1 : iov_norm[i]=2 : tov_norm[i] = "Вручную"
i=i+1 : iov_norm[i]=3 : tov_norm[i] = "По кратности"
i=i+1 : iov_norm[i]=4 : tov_norm[i] = "По теплопоступлению"
i=i+1 : iov_norm[i]=5 : tov_norm[i] = "По влаговыделению"
i=i+1 : iov_norm[i]=6 : tov_norm[i] = "Душевая"
i=i+1 : iov_norm[i]=7 : tov_norm[i] = "Санузел"
i=i+1 : iov_norm[i]=8 : tov_norm[i] = "Кабинет"
i=i+1 : iov_norm[i]=9 : tov_norm[i] = "Люди"
i=i+1 : iov_norm[i]=10 : tov_norm[i] = "Койки"
dim tov_norm_in[]
dim iov_norm_in[]
tov_norm_in = tov_norm
iov_norm_in = iov_norm
i=i+1 : iov_norm_in[i]=99 : tov_norm_in[i] = "Как в вытяжке"

dim ui_page_name[] : i=0
i=i+1 : ui_page_name[i]="Отделка"
i=i+1 : ui_page_name[i]="Характеристики ОВ"
i=i+1 : ui_page_name[i]="Характеристики ПБ"

dim type_add[] : i=0
i=i+1 : type_add[i]="кв.м."
i=i+1 : type_add[i]="%"
k_eff = 1

SHT_ID_CIRCLE=1
SHT_NAME=2
SHT_AREA=3
SHT_NAME_AREA_ID=4
SHT_OTD_1=5
SHT_OV=6
SHT_ID_AREA=7
SHT_ID_PPL=8
SHT_CODE=9
SHT_PB=10
SHT_OTD_TYPE=11
SHT_BTY=12
SHT_PROPERTY=13
SHT_MAX = 13

_stPropertyValue = ""
dict propInputIds
dict propOutputVals
dict text_area
unID=100
PAR_MAX = 13

if vardim1(pos_xy)<>SHT_MAX+1 or vardim2(pos_xy)<>(PAR_MAX+1)*2 then 
	dim emp_arr[][]
	for i=1 to SHT_MAX+1
		for j=1 to (PAR_MAX+1)*2
			emp_arr[i][j]=0.01*j
			emp_arr[i][j]=0.01*j
		next j
	next i
	PARAMETERS pos_xy = emp_arr
endif
if idtonumb then ROOM_NUMBER = GLOB_ID

max_n = 15
if tip_pol<>"Не задан" or tip_pot<>"Не задан" then
	n_sloi_r=0
	dim tolsh_r_arr[15]
	dim tolsh_min_r_arr[15]
	dim strih_r_arr[15]
	dim pokaz_arr[15]
	mat_r=0
	germ_r = 0
	for i = 1 to max_n
		tolsh_r_arr[i] = 0
		tolsh_min_r_arr[i] = 0
		strih_r_arr[i] = 0
		pokaz_arr[i] = 0
	next i
endif
if tip_pol<>"Не задан" then
	germ_r = 2
	CALL "macro_read_tip" PARAMETERS tip_pol=tip_pol, RETURNED_PARAMETERS n_sloi_r, tolsh_r_arr, tolsh_min_r_arr, strih_r_arr, pokaz_arr, mat_r, germ_r, fill_r, fill_pen_r, fill_bac_r
	if n_sloi_r > 0 then 
		isPol = 1
		germ_pol = germ_r
		summ_h = 0
		for i=1 to n_sloi_r
			if pokaz_arr[i]<EPS then summ_h = summ_h + tolsh_r_arr[i]
		next i
		tolsh_pol = summ_h
		PARAMETERS tolsh_pol = tolsh_pol
	endif
else
	isPol = 0
	tolsh_pol = 0
	PARAMETERS tolsh_pol = tolsh_pol
endif

if tip_pot<>"Не задан" then
	germ_r = 2
	CALL "macro_read_tip" PARAMETERS tip_pol=tip_pot, RETURNED_PARAMETERS n_sloi_r, tolsh_r_arr, tolsh_min_r_arr, strih_r_arr, pokaz_arr, mat_r, germ_r, fill_r, fill_pen_r, fill_bac_r
	if n_sloi_r > 0 then 
		isPot = 1
		germ_pot = germ_r
	endif
else
	isPot = 0
endif

S = ROOM_AREA
if abs(hroom_pot)>EPS and germ_pot=1 then
	H = hroom_pot
else
	H = ROOM_HEIGHT
endif

if type_v = "S*H" then
	V = S * H
endif

if type_v = "С учётом подрезки" then
	V = ROOM_VOLUME
endif
V = V + dV

success = LIBRARYGLOBAL ("Libraryflobals-as", "ishow_zone_type", ishow_zone_type)
success = LIBRARYGLOBAL ("Libraryflobals-as", "ishow_zone_num", ishow_zone_num)
if not(auto_mat) then
	read_msg = "Ручная настройка"
	use_flag = 0
endif
is_cfg_mview = 0
if auto_mat then
	success = LIBRARYGLOBAL ("Libraryflobals-otd", "is_cfg_mview", is_cfg_mview)
	if success and is_cfg_mview then
		dim category[]
		success = LIBRARYGLOBAL ("Libraryflobals-otd", "used_category", category)
		inx=0
		for i = 1 to vardim1(category)
			if strstr(category[i], tip_otd)>0 and tip_otd<>"" and tip_otd<>" " and tip_otd<>"-" then 
				inx = i
				i = vardim1(category)
			endif
		next i
		if inx=0 then
			read_msg = "Отсутвуют правила для типа -" + tip_otd
			inx = -1
		else
		endif
		use_flag = 1
	else
		inx=-1
	endif

	if inx>0 then
		dim finish_mat_pot[]
		dim finish_mat_wall[]
		dim finish_mat_pan[]
		dim finish_h_pan[]
		dim finish_mat_col[]
		success = LIBRARYGLOBAL ("Libraryflobals-otd", "finish_mat_pot", finish_mat_pot)
		success = LIBRARYGLOBAL ("Libraryflobals-otd", "finish_mat_wall", finish_mat_wall)
		success = LIBRARYGLOBAL ("Libraryflobals-otd", "finish_mat_pan", finish_mat_pan)
		success = LIBRARYGLOBAL ("Libraryflobals-otd", "finish_mat_col", finish_mat_col)
		success = LIBRARYGLOBAL ("Libraryflobals-otd", "finish_h_pan", finish_h_pan)
		VOTS = finish_mat_pot[inx]
		VOTW = finish_mat_wall[inx]
		VOTP = finish_mat_pan[inx]
		hpan = finish_h_pan[inx]
		VOTC = finish_mat_col[inx]
		read_msg = "Правила загружены"
		use_flag = 1
		parameters use_flag = use_flag
		parameters VOTS = VOTS
		parameters VOTW = VOTW
		parameters VOTP = VOTP
		parameters hpan = hpan
		parameters VOTC = VOTC
	else
		if use_flag then
			read_msg = "Вид не используется"
		else
			read_msg = "Ошибка, правила не загружены"
		endif
	endif
	lock 'VOTS', 'VOTW', 'VOTP', 'hpan', 'VOTC', 'use_flag'
endif
parameters read_msg = read_msg
