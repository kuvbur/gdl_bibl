
if idtonumb then
	ROOM_NUMBER = GLOB_ID
	parameters ROOM_NUMBER = ROOM_NUMBER
endif

PARAMETERS glob_x = SYMB_POS_X
PARAMETERS glob_y = SYMB_POS_Y
PARAMETERS glob_z = SYMB_POS_Z

if use_somestuff then
	LOCK "perim_without_wall", "column_perim", "type_add_pot", "add_s_pot", "type_add_pol", "add_s_pol"
	HIDEPARAMETER "perim_without_wall", "column_perim", "type_add_pot", "add_s_pot", "type_add_pol", "add_s_pol"
endif

dim name_arr[] : i=0
i=i+1 : name_arr[i] = "Тамбур"
i=i+1 : name_arr[i] = "Лестничная клетка"
i=i+1 : name_arr[i] = "Коридор"

i=i+1 : name_arr[i] = "Помещение хранения уборочного инвентаря"
i=i+1 : name_arr[i] = "Кладовая"

i=i+1 : name_arr[i] = "Электрощитовая"
i=i+1 : name_arr[i] = "Операторская"
i=i+1 : name_arr[i] = "Кабинет"

i=i+1 : name_arr[i] = "Кладовая грязной спецодежды"
i=i+1 : name_arr[i] = "Кладовая чистой спецодежды"
i=i+1 : name_arr[i] = "Раздаточная спецодежды"

i=i+1 : name_arr[i] = "Мужской санузел"
i=i+1 : name_arr[i] = "Женский санузел"
i=i+1 : name_arr[i] = "Санузел с тамбуром"
i=i+1 : name_arr[i] = "Санузел"

i=i+1 : name_arr[i] = "Преддушевая"
i=i+1 : name_arr[i] = "Мужская душевая"
i=i+1 : name_arr[i] = "Женская душевая"

i=i+1 : name_arr[i] = "Мужская гардеробная спецодежды"
i=i+1 : name_arr[i] = "Мужская гардеробная уличной и домашней одежды"
i=i+1 : name_arr[i] = "Женская гардеробная спецодежды"
i=i+1 : name_arr[i] = "Женская гардеробная уличной и домашней одежды"

i=i+1 : name_arr[i] = "Цех"
i=i+1 : name_arr[i] = "Склад"
i=i+1 : name_arr[i] = "Блок реагентов"
i=i+1 : name_arr[i] = "Производственное помещение"

values 'ROOM_NAME' CUSTOM, name_arr

values "type_v" "S*H", "С учётом подрезки"

if auto_mat and is_cfg_mview then values 'tip_otd' category, CUSTOM

values 'plst' "системной переменной", "по периметру СТЕН", "по периметру ЗОНЫ", "по наружной поверхности","вручную"
values 'plpt' "системной переменной", "вручную"

if GLOB_MODPAR_NAME = "vin" then
	vin_arr[ishow_zone_num] = vin
	PARAMETERS vin_arr = vin_arr
endif

values{2} "ov_norm_Out" iov_norm, tov_norm
values{2} "ov_norm_In" iov_norm_in, tov_norm_in

dim temp_system_out[]: i=0
i=i+1 : temp_system_out[i] = ov_System_In
i=i+1 : temp_system_out[i] = ov_System_Out
i=i+1 : temp_system_out[i] = ov_norm_In_dop
i=i+1 : temp_system_out[i] = ov_norm_Out_dop
if STRLEN(ov_System_In_summ)>0 and ov_System_In_summ<>" " then
	i=i+1 : temp_system_out[i] = ov_System_In_summ + "(лето)"
endif
if STRLEN(ov_System_Out_summ)>0 and ov_System_Out_summ<>" " then
	i=i+1 : temp_system_out[i] = ov_System_Out_summ + "(лето)"
endif

t_ov_System = ""
for i=1 to vardim1(temp_system_out)
	if STRLEN(temp_system_out[i])>0 and temp_system_out[i]<>" " then 
		if STRLEN(t_ov_System)>0 and t_ov_System<>" " then
			t_ov_System = t_ov_System + ", " + temp_system_out[i]
		else
			t_ov_System = temp_system_out[i]
		endif
	endif
next i
PARAMETERS ov_System = t_ov_System

values "ov_Temp_wint" range[5,40]
values "ov_Temp_summ" range[5,40]

PARAMETERS ov_Temp_summ_txt = "+" + STR("%.0",ov_Temp_summ)
PARAMETERS ov_Temp_wint_txt = "+" + STR("%.0",ov_Temp_wint)

if GLOB_MODPAR_NAME = "ov_norm_Out" then
	PARAMETERS ov_norm_In = 99
endif

ov_norm_Out_txt = "-" : ov_norm_In_txt = "-"
ov_norm_Out_t = ov_norm_Out : if ov_norm_Out = 99 then ov_norm_Out_t = ov_norm_In

if ov_norm_Out = 3 then !"По кратности"
	values 'ov_ed_krat_out' range [1,100]
	ov_norm_Out_txt = STR("%.0", ov_ed_krat_out)+ " крат"
	ov_Out = (ov_ed_krat_out * V)
else
	LOCK "ov_ed_krat_out" 
	HIDEPARAMETER "ov_ed_krat_out"
endif

if ov_norm_In = 3 then !"По кратности"
	values 'ov_ed_krat_in' range [1,100]
	ov_norm_In_txt = STR("%.0", ov_ed_krat_in)+ " крат"
	ov_In = (ov_ed_krat_in * V)
else
	LOCK "ov_ed_krat_in"
	HIDEPARAMETER "ov_ed_krat_in"
endif

if ov_norm_Out = 4 then !"По теплопоступлению"
	LOCK "ov_tout_sum", "ov_tin_sum_wint", "ov_tin_sum_summ","ov_pt_summ", "ov_nt_wint", "ov_nt_summ", "ov_pt_wint"
	ov_norm_Out_txt = "По расчет на тепл."
else
	HIDEPARAMETER "ov_tout_slab", "ov_tout_windor", "ov_tout_wall",
	"ov_tout_roof", "ov_tout_inf", "ov_tin_light", "ov_tin_eq",
	"ov_tin_sun", "ov_tout_sum", "ov_tin_sum_wint", "ov_tin_sum_summ",
	"ov_pt_wint", "ov_pt_summ", "ov_nt_wint", "ov_nt_summ"
endif

if ov_norm_Out = 5 then !"По влаговыделению"
	ov_norm_Out_txt = "По расчет по влаг."
else

endif

if ov_norm_Out = 6 then !"Душевая"
	values "ov_ed_d" range[1,100]
	ov_Out = ov_ed_d * 75
	ov_norm_Out_txt = "75м3/прибор"
	parameters ov_norm_in = 1
else
	parameters ov_ed_d = 0
	HIDEPARAMETER "ov_ed_d"
endif

if ov_norm_Out = 7 then !"Санузел"
	values "ov_ed_t" range[1,100]
	parameters ov_norm_in = 1
	ov_Out = (ov_ed_p * 25) + (ov_ed_t * 50)
	ov_norm_Out_txt = "50м3/прибор"
else
	parameters ov_ed_p = 0, ov_ed_t = 0
	HIDEPARAMETER "ov_ed_p", "ov_ed_t"
endif

if ov_norm_Out = 8 then !"Кабинет"
	ov_Out = round_int(S/3)
	parameters ov_norm_in = 1
	ov_norm_Out_txt = "3м3/м2"
endif

if ov_norm_Out = 9 then !"Люди"
	values "ov_ed_pos" range[1,100]
	ov_Out = ov_ed_pos * 60
	ov_norm_Out_txt = "60м3/Человек"
	parameters ov_norm_in = 1
else
	parameters ov_ed_pos = 0
	HIDEPARAMETER "ov_ed_pos"
endif

if ov_norm_Out = 10 then !"Койки"
	values "ov_ed_bunk" range[1,100]
	ov_Out = ov_ed_bunk * 80
	ov_norm_Out_txt = "80м3/Койки"
	parameters ov_norm_in = 1
else
	parameters ov_ed_bunk = 0
	HIDEPARAMETER "ov_ed_bunk"
endif

!if ov_norm_In <> 2 then LOCK "ov_In"
!if ov_norm_Out <> 2 then LOCK "ov_Out"
if ov_norm_In = 1 then 
	ov_In = 0
	ov_norm_In_txt = "-"
endif
if ov_norm_Out = 1 then 
	ov_Out = 0
	ov_norm_Out_txt = "-"
endif
if ov_norm_In = 99 then 
	ov_In = ov_Out
	ov_norm_In_txt = ov_norm_Out_txt
endif
if ov_In>EPS then
	ov_In = int(ov_In/10)*10+5*((ov_In/10-int(ov_In/10))>EPS)
else
	ov_In = 0
endif
if ov_Out>EPS then
	ov_Out = int(ov_Out/10)*10+5*((ov_Out/10-int(ov_Out/10))>EPS)
else
	ov_Out = 0
endif

if not(ov_Inmun) then parameters ov_In = ov_In
if not(ov_Outmun) then parameters ov_Out = ov_Out

ov_In_txt = "-" : if ov_In>1 then ov_In_txt = STR("%.0",ov_In)
ov_Out_txt = "-" : if ov_Out>1 then ov_Out_txt = STR("%.0",ov_Out)
parameters ov_In_txt = ov_In_txt, ov_Out_txt = ov_Out_txt

parameters S = S
parameters H = H
parameters V = V

if plpt = "системной переменной" then parameters pl_pt = ROOM_AREA
parameters perim = ROOM_NET_PERIMETER
parameters perim_st = ROOM_WALLS_PRM
parameters plst_sys = ROOM_WALLS_SURF
parameters plst_perim = ROOM_WALLS_PRM * H
parameters plst_perimz = ROOM_NET_PERIMETER * H
parameters plst_bound = ROOM_BOUNDARY_SURF
parameters plst_hole = ROOM_DOORS_SURF + ROOM_WINDS_SURF
parameters plst_ph = ROOM_WALLS_PRM * H - (ROOM_DOORS_SURF + ROOM_WINDS_SURF)
parameters plst_phz = ROOM_NET_PERIMETER * H - (ROOM_DOORS_SURF + ROOM_WINDS_SURF)
parameters plst_phbound = ROOM_BOUNDARY_SURF - (ROOM_DOORS_SURF + ROOM_WINDS_SURF)
parameters span = (perim_st - ROOM_DOORS_WID) * hpan
parameters plt_sys = ROOM_AREA
parameters plt_bound1 = ROOM_TOP_SURFACE
parameters plt_bound2 = ROOM_ROOF_TOP_SURF + ROOM_SLAB_TOP_SURF + ROOM_BEAM_TOP_SURF

if plst <> "вручную" then
	if plst = "системной переменной" then s_st = plst_sys
	if plst = "по периметру СТЕН" then s_st = plst_ph
	if plst = "по периметру ЗОНЫ" then s_st = plst_phz
	if plst = "по наружной поверхности" then s_st = plst_phbound
	parameters ssten = s_st - span
endif
parameters sstentot = ssten + span

if pb_mun then
	values 'pb_norm' 6, CUSTOM
	if pb_norm>0 then 
		pb_user = round_int(ROOM_AREA/pb_norm)
		if pb_user<1 then pb_user=1
		parameters pb_user = pb_user
		LOCK "pb_user"
	endif
else
	LOCK "pb_norm"
	HIDEPARAMETER "pb_norm"
endif
dim used_tip[]
success = LIBRARYGLOBAL ("Libraryflobals-pokr", "used_tip", used_tip)
values 'tip_pol' "Не задан",used_tip, CUSTOM
values 'tip_pot' "Не задан",used_tip, CUSTOM
parameters isPol=isPol, isPot=isPot
if isPot then parameters germ_pot = germ_pot
if hroom_pot<EPS then LOCK "VOTW2", "germ_pot"
if hpan<EPS then LOCK "VOTP"


zone_param = "-"
if column_perim>EPS then
	if zone_param = "-" then zone_param = ""
	zone_param = zone_param + "@Cp=" + str("%.0mm",column_perim)
endif

area_pol = 0
if tip_pol<>"Не задан" then
	area_pol = S
	values 'type_add_pol' type_add
	if abs(add_s_pol)>EPS then
		if zone_param = "-" then zone_param = ""
		add_s = add_s_pol : if type_add_pol="%" then add_s = S*add_s_pol/100
		zone_param = zone_param + "@AL=" + str("%.0",add_s*1000)
		area_pol = area_pol + add_s
	endif
else
	LOCK "type_add_pol", "add_s_pol", "floor_by_slab"
	HIDEPARAMETER "type_add_pol", "add_s_pol", "floor_by_slab"
endif
parameters area_pol = area_pol

area_pot = 0
if tip_pot<>"Не задан" then
	area_pot = pl_pt
	values 'type_add_pot' type_add
	if abs(add_s_pot)>EPS then
		if zone_param = "-" then zone_param = ""
		add_s = add_s_pot : if type_add_pot="%" then add_s = S*add_s_pot/100
		zone_param = zone_param + "@AT=" + str("%.0",add_s*1000)
		area_pot = area_pot + add_s
	endif
else
	LOCK "type_add_pot", "add_s_pot", "ceil_by_slab"
	HIDEPARAMETER "type_add_pot", "add_s_pot", "ceil_by_slab"
endif


if offPot then
	area_pot = 0
	parameters tip_pot="Не задан"
	if zone_param = "-" then zone_param = ""
	zone_param = zone_param + "@OfT=1"
	LOCK "VOTS", "tip_pot", "germ_pot", "hroom_pot", "ceil_by_slab"
	parameters germ_pot = 0, hroom_pot = 0
	HIDEPARAMETER "VOTS", "tip_pot", "germ_pot", "hroom_pot", "ceil_by_slab"
endif

parameters area_pot = area_pot

parameters zone_param = zone_param
if STRLEN(tip_otd)<EPS or tip_otd="" or tip_otd=" " then parameters tip_otd="-"


parameters version = "1"
parameters help_url = "https://github.com/kuvbur/gdl_bibl/wiki/%D0%9F%D0%BE%D0%BC%D0%B5%D1%89%D0%B5%D0%BD%D0%B8%D0%B5"
