
! enable2dDetLevels - possible values
!	2	- enable scale sansitive
!	4	- enable 1:50, detailed
!	8	- enable 1:100, middle
!	16	- enable 1:200, simple
!	32	- enable symbolic

if enable2dDetLevels < EPS then enable2dDetLevels = 2 + 4 + 8 + 16 + 32

dim detLevel2dValueList[]
j = 1
for i = 1 to vardim1(stDetail2D)
	if bittest(enable2dDetLevels, i) then
		if i = 1 then put 0			! Scale sensitive
		if i = 2 then put 3			! 1:50 - Detailed
		if i = 3 then put 2			! 1:100 - Middle
		if i = 4 then put 1			! 1:200 - Simple
		if i = 5 then put 4			! Symbolic

		detLevel2dValueList[j] = stDetail2D[i]
		j = j + 1
	endif
next i

values "gs_detlevel_2D" detLevel2dValueList
values "gs_detlevel_2D_m" get(NSP)

if GLOB_MODPAR_NAME = "gs_detlevel_2D" then
	gs_detlevel_2D_m = 0											! Scale sensitive
	if gs_detlevel_2D = stDetail2D[2] then gs_detlevel_2D_m = 3		! 1:50 - Detailed
	if gs_detlevel_2D = stDetail2D[3] then gs_detlevel_2D_m = 2		! 1:100 - Middle
	if gs_detlevel_2D = stDetail2D[4] then gs_detlevel_2D_m = 1		! 1:200 - Simple
	if gs_detlevel_2D = stDetail2D[5] then gs_detlevel_2D_m = 4		! Symbolic
	parameters gs_detlevel_2D_m = gs_detlevel_2D_m
else
	gs_detlevel_2D = stDetail2D[1]									! Scale sensitive
	if gs_detlevel_2D_m = 1 then gs_detlevel_2D = stDetail2D[4]		! 1:200 - Simple
	if gs_detlevel_2D_m = 2 then gs_detlevel_2D = stDetail2D[3]		! 1:100 - Middle
	if gs_detlevel_2D_m = 3 then gs_detlevel_2D = stDetail2D[2]		! 1:50 - Detailed
	if gs_detlevel_2D_m = 4 then gs_detlevel_2D = stDetail2D[5]		! Symbolic
	parameters gs_detlevel_2D = gs_detlevel_2D
endif
