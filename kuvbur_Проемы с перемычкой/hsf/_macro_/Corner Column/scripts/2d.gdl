
! ==============================================================================
! This macro draws the corner columns
! ------------------------------------------------------------------------------
! Input parameters:
! ==============================================================================

if lod2D_CornerColumn = 0 then end

fill gs_fillFr
penCC_bg = gs_penFr_bg
penCC_fg = gs_penFr_fg

line_property 0	! LINE_PROPERTY_GENERIC


if WIDO_REVEAL_SIDE then
	add2 0,-WIDO_FRAME_THICKNESS
else
	mul2 1,-1
endif

signRevealSide = not(WIDO_REVEAL_SIDE) - WIDO_REVEAL_SIDE

! Draw Right Column ------------------------------------------------------------

if bRightCornerFunction & idxBoardEndRevealRight >= idxR then
	add2 rightRevealPnts[idxR][1] + signRevealSide * SymbolMid_shiftDy / tanAngle,
		 rightRevealPnts[idxR][2] - signRevealSide * SymbolMid_shiftDy
	if bMirrorRight then mul2 1,-1

	bMirror	= bMirrorRight
	px1		= px1R
	px2		= px2R
	px3		= px3R
	pxFrame	= pxFrameR
	unID	= 10300
	gosub 100

	del bMirrorRight + 1
endif


! Draw Left Column -------------------------------------------------------------

if bLeftCornerFunction & idxBoardEndRevealLeft >= idxL then
	add2 leftRevealPnts[idxL][1],leftRevealPnts[idxL][2]
	if bMirrorLeft then mul2 1,-1
	add2 offsLeftX,offsLeftY
	mul2 -1,1

	bMirror	= bMirrorLeft
	px1		= px1L
	px2		= px2L
	px3		= px3L
	pxFrame	= pxFrameL
	unID	= 10310
	gosub 100

	del bMirrorLeft + 3
endif

del 1

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ==============================================================================


100:
	if lod2D_CornerColumn = 1 then
		if bMirror then
			line2 px2 - pxFrame,WIDO_FRAME_THICKNESS, px3,WIDO_FRAME_THICKNESS
		else
			line2 px2 - pxFrame,0, px1,0
		endif

		if abs(SymbolMid_shiftDy) < EPS then
			hotspot2 px1,			0, unID
			hotspot2 px2 - pxFrame,	0, unID + 1
			hotspot2 px2 - pxFrame,	0, unID + 2
			hotspot2 px1,			0, unID + 3
		endif
	endif

	if lod2D_CornerColumn = 2 then
		line2 px2 - pxFrame,0, px1,0
		line2 px2 - pxFrame,WIDO_FRAME_THICKNESS, px3,WIDO_FRAME_THICKNESS

		hotspot2 px1,			0,  		          unID
		hotspot2 px2 - pxFrame,	0,  		          unID + 1
		hotspot2 px2 - pxFrame,	WIDO_FRAME_THICKNESS, unID + 2
		hotspot2 px3,			WIDO_FRAME_THICKNESS, unID + 3
	endif

	if lod2D_CornerColumn = 3 then
		poly2_b 4, 1+2+4+16+32, penCC_fg, penCC_bg,
			px1,			-gs_cc_pr,				32 + 1,
			px2 - pxFrame,	-gs_cc_pr,				32 + 1,
			px2 - pxFrame,	-gs_cc_pr + gs_cc_thk,	32 + 1,
			px3,			-gs_cc_pr + gs_cc_thk,	32 + gs_cc_endline

		hotspot2 px1,			-gs_cc_pr,             unID
		hotspot2 px2 - pxFrame,	-gs_cc_pr,             unID + 1
		hotspot2 px2 - pxFrame,	-gs_cc_pr + gs_cc_thk, unID + 2
		hotspot2 px3,			-gs_cc_pr + gs_cc_thk, unID + 3
	endif
return




