
! Contact person: <NJ>

! ==============================================================================
! This macro draws the corner columns
! ------------------------------------------------------------------------------
! Input Parameters:
! ==============================================================================


pen gs_frame_pen
material matCornerColumn
sect_fill gs_fillFr, gs_penFr_bg, gs_penFr_fg, gs_frame_pen_cont



if WIDO_REVEAL_SIDE then
	addz WIDO_FRAME_THICKNESS
else
	mulz -1
endif

rotx -90

topAngle = 0
if openingArchHeight > EPS then
	topAngle = atn(openingArchHeight / ac_wallhole_width)
endif

! Draw Right Column ------------------------------------------------------------

if bRightCornerFunction & vardim1(rightRevealPnts) >= idxR then
	add rightRevealPnts[idxR][1],rightRevealPnts[idxR][2],0
	if bMirrorRight then muly -1

	px1		= px1R
	px2		= px2R
	px3		= px3R
	pxFrame	= pxFrameR
	trapezoidHeightMod = 0
	bCutDone = 0

	if openingArchHeight > EPS then		! only trapeze shape!
		add px2 - pxFrame, 0, openingHeight - openingArchHeight
		rotz 90

		cutplane topAngle

		del 2
		bCutDone = 1
	endif

	gosub 100

	if bCutDone then cutend

	del bMirrorRight + 1
endif


! Draw Left Column -------------------------------------------------------------

if bLeftCornerFunction & vardim1(leftRevealPnts) >= idxL then
	add leftRevealPnts[idxL][1],leftRevealPnts[idxL][2],0
	if bMirrorLeft then muly -1
	add offsLeftX, offsLeftY, 0
	mulx -1

	px1		= px1L
	px2		= px2L
	px3		= px3L
	pxFrame	= pxFrameL
	trapezoidHeightMod = 0
	bCutDone = 0

	if openingArchHeight > EPS then		! only trapeze shape!
		trapezoidHeightMod = max(px1, px3) - (px2 - pxFrame) * tan(topAngle)
		add px2 - pxFrame, 0, openingHeight
		rotz 90
		cutplane -topAngle
		del 2
		bCutDone = 1
	endif

	gosub 100

	if bCutDone then cutend

	del bMirrorLeft + 3
endif

del 2

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ==============================================================================


100:
	body -1

	bPlanarIn	= (abs(WIDO_FRAME_THICKNESS - (gs_cc_thk - gs_cc_pr)) < EPS & abs(px3 - (px2 - pxFrame)) > EPS)
	bPlanarOut	= (abs(gs_cc_pr) < EPS & abs(px1 - (px2 - pxFrame)) > EPS)

	if bPlanarIn | bPlanarOut then
		! Workaround to avoid line elimination in case when the corner column
		! and the window frame are in the same plane.

		add px1, -gs_cc_pr, 0
		rotz -atn(gs_cc_thk / (px1 - px3))
		rotx 90
		cutplane 180
		del 3

		add px2 - pxFrame, -gs_cc_pr, 0
		block max(px1, px3) - (px2 - pxFrame), gs_cc_thk, openingHeight + trapezoidHeightMod
		del 1

		cutend
	else
		prism_ 4, openingHeight + trapezoidHeightMod,
			px1,			-gs_cc_pr,				15,
			px2 - pxFrame,	-gs_cc_pr,				15,
			px2 - pxFrame,	-gs_cc_pr + gs_cc_thk,	15,
			px3,			-gs_cc_pr + gs_cc_thk,	15
	endif

	base
	vert 0, 0, 0
	vert 0, 0, 1
	vert 0, 1, 0
	vert 1, 0, 0
	coor 5, -1, -2, -3, -4
	body 1

	! Cut space from upper and bottom reveal
	if abs(openingArchHeight) < EPS then
		wallniche 4,1,2,
			0,0,1, openingHeight,
			px1,			-gs_cc_pr,				31,
			px2 - pxFrame,	-gs_cc_pr,				31,
			px2 - pxFrame,	-gs_cc_pr + gs_cc_thk,	31,
			px3,			-gs_cc_pr + gs_cc_thk,	31
	endif
return
