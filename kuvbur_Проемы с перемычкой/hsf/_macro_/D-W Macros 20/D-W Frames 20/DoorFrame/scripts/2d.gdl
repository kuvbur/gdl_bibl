
gs_fgs_fillFr	= gs_fillFr * gs_bFills
if gs_frame_pen_cont <= 0 then
	pen gs_pen_2D
else
	pen gs_frame_pen_cont
endif

drawindex 40

outsideFrameThk	= gs_frame_thk
if gs_frame_wthk then
	if gs_sframe then
		insideFrameThk	= gs_frame_thk
	else
		insideFrameThk	= gs_frame_thkin
	endif
else
	insideFrameThk	= gs_frame_thk
endif


! =============================================================================
! 2D symbol following scale
! =============================================================================

lod2D						= DET2D_FS_OFF
bShowRebateOnFixGlass		= 0
bShowFrameContoursOnBottom	= 0

if iLoD2D = SYMBD_S_1LINE then						lod2D	= DET2D_FS_SIMPLE			! Simple --> INT 200
if iLoD2D = SYMBD_M_1LINELEAF_2LINE then			lod2D	= DET2D_FS_MIDDLE			! Middle --> INT 100
if iLoD2D = SYMBD_M_1LINELEAF_1LINE then			lod2D	= DET2D_FS_MIDDLE			! Middle --> USA Middle
if iLoD2D = SYMBD_M_2LINELEAFCL_GLASS then			lod2D	= DET2D_FS_MIDDLE			! Middle --> NLD 100
if iLoD2D = SYMBD_D_2LINELEAFOP_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> INT 50
if iLoD2D = SYMBD_D_1LINELEAF_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> SWE 50
if iLoD2D = SYMBD_D_2LINELEAFCL_REBATEONGLASS then	lod2D	= DET2D_FS_DETAILED			! Detailed --> NLD 50
if iLoD2D = SYMBD_D_2LINELEAFOP_SASH then			lod2D	= DET2D_FS_DETAILED			! Detailed --> USA Detailed
if iLoD2D = SYMBD_D_2LEAFOPGLASS_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> DAN 20
if iLoD2D = SYMBD_S_2LINE then						lod2D	= DET2D_FS_SIMPLE_CHI		! Chinese Simple
if iLoD2D = SYMBD_D_2LINELEAFOP_2LINE then			lod2D 	= DET2D_FS_DETAILED_CHI		! Chinese Detailed

if iLoD2D = SYMBD_M_2LINELEAFCL_GLASS then												! Middle --> NLD 100
	bShowFrameContoursOnBottom		= 0
endif
if	iLoD2D = SYMBD_D_2LINELEAFCL_REBATEONGLASS then										! Detailed --> NLD 50
	bShowRebateOnFixGlass			= 1
	bShowFrameContoursOnBottom		= 0
endif


bNoRebate_05 = (gs_optype_m_05 = SASH_FIXED * (not(bShowRebateOnFixGlass) | iMullionStyle_05 = MULLION_SOLID_PANEL)	|\
				gs_optype_m_05 = SASH_HOR_PIVOT				|\
				gs_optype_m_05 = SASH_VER_PIVOT				|\
				gs_optype_m_05 = SASH_VER_PIVOT_M			|\
				gs_optype_m_05 = SASH_TILT_SLIDE			|\
				gs_optype_m_05 = SASH_LEFT_SLIDING			|\
				gs_optype_m_05 = SASH_RIGHT_SLIDING			|\
				gs_optype_m_05 = SASH_UP_SLIDING			|\
				gs_optype_m_05 = SASH_DOWN_SLIDING			|\
				gs_optype_m_05 = SASH_VER_PIVOT_M			|\
				gs_optype_m_05 = SASH_FIXED_SLIDE			|\
				gs_optype_m_05 = SASH_SLIDING_HOR			|\
				gs_optype_m_05 = SASH_SLIDING_HOR_M			|\
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR	|\
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR_M	|\
				gs_optype_m_05 = SASH_SLIDING_VER			|\
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_VER)

bNoRebate_09 = (gs_optype_m_09 = SASH_FIXED * (not(bShowRebateOnFixGlass) | iMullionStyle_09 = MULLION_SOLID_PANEL)	|\
				gs_optype_m_09 = SASH_HOR_PIVOT				|\
				gs_optype_m_09 = SASH_VER_PIVOT				|\
				gs_optype_m_09 = SASH_VER_PIVOT_M			|\
				gs_optype_m_09 = SASH_TILT_SLIDE			|\
				gs_optype_m_09 = SASH_LEFT_SLIDING			|\
				gs_optype_m_09 = SASH_RIGHT_SLIDING			|\
				gs_optype_m_09 = SASH_UP_SLIDING			|\
				gs_optype_m_09 = SASH_DOWN_SLIDING			|\
				gs_optype_m_09 = SASH_VER_PIVOT_M			|\
				gs_optype_m_09 = SASH_FIXED_SLIDE			|\
				gs_optype_m_09 = SASH_SLIDING_HOR			|\
				gs_optype_m_09 = SASH_SLIDING_HOR_M			|\
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR	|\
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR_M	|\
				gs_optype_m_09 = SASH_SLIDING_VER			|\
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_VER)

symb_2d = 0
if lod2D = DET2D_FS_SIMPLE | lod2D = DET2D_FS_SIMPLE_CHI then symb_2d = 200
if lod2D = DET2D_FS_MIDDLE | lod2D = DET2D_FS_DETAILED_CHI then symb_2d = 100
if lod2D = DET2D_FS_DETAILED then symb_2d = 50

bFeedBack = (GLOB_CONTEXT > 20 & GLOB_CONTEXT < 40)
if bFeedBack then symb_2d = 200

bListView = (GLOB_CONTEXT = 6 | GLOB_CONTEXT = 46)

if bListView then
	gs_frame_left_endline  = 1
	gs_frame_right_endline = 1
endif

if symb_2d = 200 then
	posLeafy		= posLeafy - gs_pocket_frame * gs_pocket_frame_thk
	widthWallCut	= widthWallCut + 2 * gs_pocket_frame * gs_pocket_frame_thk
endif


if gs_pocket & not(gs_pocket_door_frame) then goto "PlacingPockets"


! =============================================================================
! Coordinates according to size types
! =============================================================================

unitleft = -ac_unit_width / 2
unitright = ac_unit_width / 2

leafleft	= unitleft  + gs_sl_w1 - gs_rebate_width
leafright	= unitright - gs_sl_w2 + gs_rebate_width
egressleft	= unitleft  + gs_sl_w1 + gs_ds_w + gs_sliding_fixpanel_width_left
egressright	= unitright - gs_sl_w2 - gs_ds_w - gs_sliding_fixpanel_width_right

frameleftout	= unitleft  - gs_frame_ovhg_left_out
framerightout	= unitright + gs_frame_ovhg_right_out
frameleftin		= unitleft  - gs_frame_ovhg_left_in
framerightin	= unitright + gs_frame_ovhg_right_in
frameDiff		= (gs_frw1 - gs_frw2) / 2
egressDiff		= (gs_sliding_fixpanel_width_left - gs_sliding_fixpanel_width_right)/2

if gs_pivotdoor then
	egressleft = -ac_unit_width / 2 + gs_frw1 - frameDiff
	egressright = ac_unit_width / 2 - gs_frw2 - gs_pivotOffset - gs_leaf_thk/2 - frameDiff
else
	if	gs_leaf_optype_m = LEAF_1L_POCKET	|\
		gs_leaf_optype_m = LEAF_1L_POCKET_M	|\
		gs_leaf_optype_m = LEAF_2L_POCKET	then
			egressleft = -ac_egress_width / 2 + ((gs_sl_w1 - gs_frw1) / 2)*gs_sidelight_left - ((gs_sl_w2 - gs_frw2) / 2)*gs_sidelight_right + frameDiff
			egressright = ac_egress_width / 2 + ((gs_sl_w1 - gs_frw1) / 2)*gs_sidelight_left - ((gs_sl_w2 - gs_frw2) / 2)*gs_sidelight_right + frameDiff
	endif
endif

egressLeftHotspot = egressleft
egressRightHotspot = egressright

if	gs_usaframe & ( gs_leaf_optype_m = LEAF_1L_POCKET	|\
					gs_leaf_optype_m = LEAF_1L_POCKET_M	|\
					gs_leaf_optype_m = LEAF_2L_POCKET)	then

	egressleft = -ac_egress_width / 2 + frameDiff + egressDiff - gs_sliding_fixpanel_width_left
	egressright = ac_egress_width / 2 + frameDiff + egressDiff + gs_sliding_fixpanel_width_right
endif

if symb_2d=100 then
	egressright	= egressright + gs_ds_w
	egressleft	= egressleft  - gs_ds_w
endif


! =============================================================================
! Hotspots on Unit Size
! =============================================================================

if bShowUnitHotspots then
	unID = 100
	hotspot2 unitleft + gs_frame_oversize,	 comy * (symb_2d <> 200),					unID
	hotspot2 unitright - gs_frame_oversize,	 comy * (symb_2d <> 200),					unID + 1
	hotspot2 unitleft + gs_frame_oversize,	(-gs_frame_thk - comy) * (symb_2d <> 200),	unID + 2
	hotspot2 unitright - gs_frame_oversize,	(-gs_frame_thk - comy) * (symb_2d <> 200),	unID + 3

	if gs_frame_oversize > EPS then
		hotspot2 unitleft,	(comy - frameRebateOffset) * (symb_2d <> 200),					unID + 4
		hotspot2 unitright,	(comy - frameRebateOffset) * (symb_2d <> 200),					unID + 5
		hotspot2 unitleft,	(-gs_frame_thk - comy + frameRebateOffset) * (symb_2d <> 200),	unID + 6
		hotspot2 unitright,	(-gs_frame_thk - comy + frameRebateOffset) * (symb_2d <> 200),	unID + 7
	endif
endif

if not(gs_frame_wthk) then
	gs_frame_thkin2 = gs_frame_thk
else
	if gs_frame_style_m = FRAME_STYLE_METAL | gs_frame_style_m = FRAME_STYLE_BLOCK then
		gs_frame_thkin2 = gs_frame_thk
	else
		gs_frame_thkin2 = gs_frame_thkin
	endif
endif


! =============================================================================
! Hotspots on Egress Size
! =============================================================================

if symb_2d = 200 then
	egressthk1 = 0
	egressthk2 = 0
	egressthk3 = 0
else
	if symb_2d=100 then
		egressthk1=0
		egressthk2=-gs_frame_thk
		egressthk3=-gs_frame_thkin2
	else
		egressthk1 = - gs_leaf_thk * not(gs_usaframe) - (gs_ds_pr * (gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE | gs_frame_style_m = FRAME_STYLE_METAL) + gs_leaf_thk * (gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE)) * (gs_usaframe) + comy
		egressthk2 = - gs_leaf_thk * not(gs_usaframe) - (gs_ds_pr * (gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE | gs_frame_style_m = FRAME_STYLE_METAL) + gs_leaf_thk * (gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE)) * (gs_usaframe) - gs_ds_thk + comy
		egressthk3 = - gs_leaf_thk * not(gs_usaframe) - (gs_ds_pr * (gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE | gs_frame_style_m = FRAME_STYLE_METAL) + gs_leaf_thk * (gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE)) * (gs_usaframe) - gs_ds_thk + comy
	endif
endif

if bShowEgressHotspots then
	unID = 200
	if gs_frame_style_m = FRAME_STYLE_BLOCK then
		hotspot2 egressLeftHotspot,  0, unID
		hotspot2 egressRightHotspot, 0, unID + 1
	else
		if gs_frame_style_m = FRAME_STYLE_DOUBLE_EGRESS_METAL then
			hotspot2 egressLeftHotspot  + frameDiff, egressthk1, unID
			hotspot2 egressRightHotspot + frameDiff + gs_rebate_width*2, egressthk1, unID + 1
		else
			hotspot2 egressLeftHotspot,  egressthk1, unID
			hotspot2 egressRightHotspot, egressthk1, unID + 1
		endif
	endif

	if gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE | gs_frame_style_m = FRAME_STYLE_METAL then
		hotspot2 egressLeftHotspot,  egressthk3, unID + 2
		hotspot2 egressRightHotspot, egressthk3, unID + 3
	else
		if gs_frame_style_m = FRAME_STYLE_DOUBLE_EGRESS_METAL then
			hotspot2 egressLeftHotspot + frameDiff - gs_rebate_width*2, -gs_frame_thk - comy, unID + 2
			hotspot2 egressRightHotspot + frameDiff, -gs_frame_thk - comy, unID + 3
		else
			if gs_frame_style_m <> FRAME_STYLE_SIMPLE_REBATE | not(gs_sidelight_left) then
				hotspot2 egressLeftHotspot,  -gs_frame_thkin2 * (symb_2d <> 200), unID + 2
			endif
			if gs_frame_style_m <> FRAME_STYLE_SIMPLE_REBATE | not(gs_sidelight_right) then
				hotspot2 egressRightHotspot, -gs_frame_thkin2 * (symb_2d <> 200), unID + 3
			endif
		endif
	endif
endif


! =============================================================================
! Hotspots on Leaf Size
! =============================================================================

if symb_2d = 200 then
	leafthk1 = 0
else
	if symb_2d=100 then
		leafthk1=-gs_frame_thkin2
	else
		leafthk1=-gs_leaf_thk
	endif
endif
leafleft1	= leafleft
leafright1	= leafright

if bShowLeafHotspots then
	unID = 300
	if	gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE		|\
		gs_frame_style_m = FRAME_STYLE_EXTRUSION			|\
		gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION	then

		hotspot2 leafleft1,  0,        unID
		hotspot2 leafright1, 0,        unID + 1
		if lod2D = DET2D_FS_MIDDLE then
			hotspot2 leafleft1,  0,        unID + 2
			hotspot2 leafright1, 0,        unID + 3
		else
			hotspot2 leafleft1,  leafthk1, unID + 2
			hotspot2 leafright1, leafthk1, unID + 3
		endif
	else
		if gs_frame_style_m = FRAME_STYLE_DOUBLE_EGRESS_METAL then
			hotspot2 leafleft + frameDiff,		-posLeafy * (symb_2d = 50) + comy * (symb_2d = 100),   unID
			hotspot2 leafright + frameDiff,		-posLeafy * (symb_2d = 50) + comy * (symb_2d = 100),           unID + 1
			hotspot2 leafleft + frameDiff,		(-gs_frame_thk + posLeafy) * (symb_2d = 50) + (-gs_frame_thk - comy) * (symb_2d = 100), unID + 2
			hotspot2 leafright + frameDiff,		(-gs_frame_thk + posLeafy) * (symb_2d = 50) + (-gs_frame_thk - comy) * (symb_2d = 100), unID + 3
		else
			hotspot2 leafleft,   comy * (symb_2d <> 200),                   unID
			hotspot2 leafright,  comy * (symb_2d <> 200),                   unID + 1
			hotspot2 leafleft,  (-gs_frame_thkin2 - comy) * (symb_2d <> 200), unID + 2
			hotspot2 leafright, (-gs_frame_thkin2 - comy) * (symb_2d <> 200), unID + 3
		endif
	endif
endif


! =============================================================================
! Hotspot Editing of SideLights in 2D
! =============================================================================

if lod2D = DET2D_FS_MIDDLE | lod2D = DET2D_FS_DETAILED |\
	(bShowEgressHotspots & (gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE)) then

	if gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE then
		_iSideLightLeftMovingHsID	= 202
		_iSideLightRightMovingHsID	= 203
	else
		_iSideLightLeftMovingHsID	= 1322
		_iSideLightRightMovingHsID	= 1342
	endif

	add2 0, -gs_frame_thkin2 * (gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE)
		if gs_sidelight_left then
			hotspot2 -A/2 + gs_frame_oversize, comy, 1320, gs_sidelight_width_left, 1 + 128
			hotspot2 -A/2 + gs_frame_oversize-1, comy, 1321, gs_sidelight_width_left, 3
			hotspot2 -A/2 + gs_frame_oversize+gs_sidelight_width_left, comy, _iSideLightLeftMovingHsID, gs_sidelight_width_left, 2
		endif
		if gs_sidelight_right then
			hotspot2 A/2 - gs_frame_oversize, comy, 1340, gs_sidelight_width_right, 1 + 128
			hotspot2 A/2 - gs_frame_oversize+1, comy, 1341, gs_sidelight_width_right, 3
			hotspot2 A/2 - gs_frame_oversize-gs_sidelight_width_right, comy, _iSideLightRightMovingHsID, gs_sidelight_width_right, 2
		endif
	del 1
endif

_frame_left_endline		= gs_frame_left_endline | (gs_tolerance_left  > 0)
_frame_right_endline	= gs_frame_right_endline | (gs_tolerance_right > 0)


! =============================================================================
! 1:100
! =============================================================================

if symb_2d = 100 then

	!*********************** Simple or 1:100

	if gs_frame_style_m <> FRAME_STYLE_METAL & gs_frame_style_m <> FRAME_STYLE_DOUBLE_EGRESS_METAL then		! Commercial Door Frame
		if gs_pocket_door_frame then
			_pocket_frame_thk = gs_pocket_frame_thk * gs_pocket_frame
			if (gs_frame_ovhg_left_out > EPS) | (gs_frame_ovhg_left_in > EPS) then
				if bDoubleDoor then
					_proportion = ((gs_frame_ovhg_left_in - gs_frame_ovhg_left_out)/outsideFrameThk)
					if not(_pocket_frame_thk) then
						line2 unitleft, -posLeafy, frameleftout - posLeafy*_proportion, -posLeafy
						line2 unitleft, -posLeafy - widthWallCut, frameleftout - (posLeafy + widthWallCut)*_proportion, -posLeafy - widthWallCut
					endif

					if gs_frame_left_endline then
						line2 frameleftout, 0, frameleftout - (posLeafy - _pocket_frame_thk)*_proportion, -posLeafy + _pocket_frame_thk
						line2 frameleftout - (posLeafy + widthWallCut + _pocket_frame_thk)*_proportion, -posLeafy - widthWallCut - _pocket_frame_thk, frameleftin, -outsideFrameThk
					endif
				else
					if gs_frame_left_endline then
						line2 frameleftout, 0, frameleftin, -outsideFrameThk
					endif
				endif

				if (gs_frame_ovhg_left_out > EPS) then
					line2 unitleft, 0, frameleftout, 0
				endif

				if (gs_frame_ovhg_left_in > EPS) then
					line2 unitleft, -outsideFrameThk, frameleftin, -outsideFrameThk
				endif
			endif
			if (gs_frame_ovhg_right_out > EPS) | (gs_frame_ovhg_right_in > EPS) then
				if (gs_frame_ovhg_right_out > EPS) then
					line2 unitright, 0, framerightout, 0
				endif

				if (gs_frame_ovhg_right_in > EPS) then
					line2 unitright, -outsideFrameThk, framerightin, -outsideFrameThk
				endif

				_proportion = ((gs_frame_ovhg_right_in - gs_frame_ovhg_right_out)/outsideFrameThk)
				if not(_pocket_frame_thk) then
					line2 unitright, -posLeafy, framerightout + posLeafy*_proportion, -posLeafy
					line2 unitright, -posLeafy - widthWallCut, framerightout + (posLeafy + widthWallCut)*_proportion, -posLeafy - widthWallCut
				endif

				if gs_frame_right_endline then
					line2 framerightout, 0, framerightout + (posLeafy - _pocket_frame_thk)*_proportion, -posLeafy + _pocket_frame_thk
					line2 framerightout + (posLeafy + widthWallCut + _pocket_frame_thk)*_proportion, -posLeafy - widthWallCut - _pocket_frame_thk, framerightin, -outsideFrameThk
				endif
			endif
		else
			if (gs_frame_ovhg_left_out > EPS) | (gs_frame_ovhg_left_in > EPS) then
				if (gs_frame_ovhg_left_out > EPS) then
					line2 unitleft + gs_frame_oversize,  0, unitleft + gs_frame_oversize - gs_frame_ovhg_left_out,  0
				endif
				if (gs_frame_ovhg_left_in > EPS) then
					line2 unitleft + gs_frame_oversize,  -outsideFrameThk, unitleft + gs_frame_oversize - gs_frame_ovhg_left_in,  -outsideFrameThk
				endif
				if gs_frame_left_endline then
					line2 unitleft + gs_frame_oversize - gs_frame_ovhg_left_out,  0, unitleft + gs_frame_oversize - gs_frame_ovhg_left_in,  -outsideFrameThk
				endif
			endif
			if (gs_frame_ovhg_right_out > EPS) | (gs_frame_ovhg_right_in > EPS) then
				if (gs_frame_ovhg_right_out > EPS) then
					line2 unitright - gs_frame_oversize, 0, unitright - gs_frame_oversize + gs_frame_ovhg_right_out, 0
				endif
				if (gs_frame_ovhg_right_in > EPS) then
					line2 unitright - gs_frame_oversize, -outsideFrameThk, unitright - gs_frame_oversize + gs_frame_ovhg_right_in, -outsideFrameThk
				endif
				if gs_frame_right_endline then
					line2 unitright - gs_frame_oversize + gs_frame_ovhg_right_out, 0, unitright - gs_frame_oversize + gs_frame_ovhg_right_in, -outsideFrameThk
				endif
			endif
		endif

		fill gs_fillFr

		if gs_pocket_door_frame & bDoubleDoor then	! Double Pocket Doors Frame Left
			if posLeafy > EPS then
				put unitleft,	 0,			1+32,
					egressleft,		 0,			1+32,
					egressleft,		-posLeafy,	1+32,
					unitleft,	-posLeafy,	_frame_left_endline * not(gs_pocket_frame) + 32
				if gs_pocket_frame then
					 put unitleft,	-posLeafy + gs_pocket_frame_thk,	_frame_left_endline + 32
				endif
				poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
			endif

			if posLeafy < gs_frame_thk - widthWallCut - eps then
				put unitleft,	-posLeafy - widthWallCut,	1+32,
					egressleft,		-posLeafy - widthWallCut,			1+32,
					egressleft,		-gs_frame_thk,	1+32,
					unitleft,	-gs_frame_thk,				_frame_left_endline+32
				if gs_pocket_frame then
					 put unitleft,	-posLeafy - widthWallCut - gs_pocket_frame_thk,	0
				endif
				poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
			endif
		else ! Other Doors & Single Pocket Doors Frame Left
			poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
				-A/2 + gs_frame_oversize,	 0,					1+32,
				-A/2 + gs_frw1,										 0,					1+32,
				-A/2 + gs_frw1,										-outsideFrameThk,	1+32,
				-A/2 + gs_frame_oversize,	-outsideFrameThk,	_frame_left_endline+32
		endif

		if gs_sidelight_left then
			poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			   -A/2 - gs_frw_Sl_left + gs_sl_w1,	0,					1+32,
			   -A/2 + gs_sl_w1,						0,					1+32,
			   -A/2 + gs_sl_w1,						-insideFrameThk,	1+32,
			   -A/2 - gs_frw_Sl_left + gs_sl_w1,	-insideFrameThk,	1+32
		endif

		if gs_pocket_door_frame then	! Pocket Doors Frame Right
			if posLeafy > EPS then
				put	unitright,	 	 0,			1+32,
					 egressright,	 0,			1+32,
					 egressright,	-posLeafy,	1+32,
					unitright,		-posLeafy,	_frame_right_endline * not(gs_pocket_frame) + 32
				if gs_pocket_frame then
					 put unitright,	-posLeafy + gs_pocket_frame_thk,	_frame_right_endline + 32
				endif
				poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
			endif

			if posLeafy < gs_frame_thk - widthWallCut - eps then
				put	unitright,	 	-posLeafy - widthWallCut,	1+32,
					 egressright,		-posLeafy - widthWallCut,			1+32,
					 egressright,		-gs_frame_thk,	1+32,
					unitright,		-gs_frame_thk,				_frame_right_endline+32
				if gs_pocket_frame then
					 put unitright,	-posLeafy - widthWallCut - gs_pocket_frame_thk,	0
				endif
				poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
			endif
		else	! Others Doors Frame Right
			poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
				A/2 - gs_frame_oversize,	 0,					1+32,
				A/2 - gs_frw2,										 0,					1+32,
				A/2 - gs_frw2,										-outsideFrameThk,	1+32,
				A/2 - gs_frame_oversize,	-outsideFrameThk,	_frame_right_endline+32
		endif

		if gs_sidelight_right then
			poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			   A/2 + gs_frw_Sl_right - gs_sl_w2,	 0,					1+32,
			   A/2 - gs_sl_w2,						 0,					1+32,
			   A/2 - gs_sl_w2,						-insideFrameThk,	1+32,
			   A/2 + gs_frw_Sl_right - gs_sl_w2,	-insideFrameThk,	1+32
		endif
	endif

	if gs_frame_style_m = FRAME_STYLE_METAL then		! Commercial Door Frame
		fill gs_fillFr

		jp = 1/2"	! Jamb projection for drywall partition, or edge return for masonry wall, (Typical)
		gs_fcr=1
		gs_fcl=1

		if gs_frame_wthk = 0 then
			poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
				-A/2 - gs_frame_ovhg_left_out,	 0,				1,
				-A/2 + gs_frw1,					 0,				1,
				-A/2 + gs_frw1,					-gs_frame_thk,	1,
				-A/2 - gs_frame_ovhg_left_in,	-gs_frame_thk,	_frame_left_endline

			poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
				A/2 + gs_frame_ovhg_right_out,	 0,				1,
				A/2 - gs_frw2,					 0,				1,
				A/2 - gs_frw2,					-gs_frame_thk,	1,
				A/2 + gs_frame_ovhg_right_in,	-gs_frame_thk,	_frame_right_endline

			if gs_sidelight_left then
				poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					leafleft,					 0,				1+32,
					leafleft - gs_frw_Sl_left,	 0,				1+32,
					leafleft - gs_frw_Sl_left,	-gs_frame_thk,	1+32,
					leafleft,					-gs_frame_thk,	1+32
			endif

			if gs_sidelight_right then
				poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2 - gs_sl_w2 + gs_frw_Sl_right,	 0,				1+32,
					A/2 - gs_sl_w2,						 0,				1+32,
					A/2 - gs_sl_w2,						-gs_frame_thk,	1+32,
					A/2 - gs_sl_w2 + gs_frw_Sl_right,	-gs_frame_thk,	1+32
			endif
		else
			poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
				-A/2,								 jp,				1+32,
				-A/2 + gs_frw1,						 jp,				1+32,
				-A/2 + gs_frw1,						-gs_frame_thk - jp,	1+32,
				-A/2,								-gs_frame_thk - jp,	1+32,
				-A/2,								-gs_frame_thk, 		bListView+32,
				-A/2 - gs_fcr * gs_tolerance_left, 	-gs_frame_thk, 		_frame_left_endline+32,
				-A/2 - gs_fcr * gs_tolerance_left, 	 0, 				bListView+32,
				-A/2,								 0, 				1+32

			if gs_sidelight_left then
				poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					leafleft,					 jp,				1+32,
					leafleft - gs_frw_Sl_left,	 jp,				1+32,
					leafleft - gs_frw_Sl_left,	-gs_frame_thk-jp,	1+32,
					leafleft,					-gs_frame_thk-jp,	1+32
			endif

			if gs_sidelight_right then
				poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2 - gs_sl_w2 + gs_frw_Sl_right,	 jp,				1+32,
					A/2 - gs_sl_w2,						 jp,				1+32,
					A/2 - gs_sl_w2,						-gs_frame_thk - jp,	1+32,
					A/2 - gs_sl_w2 + gs_frw_Sl_right,	-gs_frame_thk - jp,	1+32
			endif

			poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
				A/2 - gs_frw2,												 jp,				1+32,
				A/2 - gs_frw2 + gs_frw2,									 jp,				1+32,
				A/2 - gs_frw2 + gs_frw2,									 0,					bListView+32,
				A/2 - gs_frw2 + gs_fcr * (gs_frw2 + gs_tolerance_right),	 0,					_frame_right_endline+32,
				A/2 - gs_frw2 + gs_fcr * (gs_frw2 + gs_tolerance_right),	-gs_frame_thk,		bListView+32,
				A/2 - gs_frw2 + gs_frw2,									-gs_frame_thk,		1+32,
				A/2 - gs_frw2 + gs_frw2,									-gs_frame_thk - jp,	1+32,
				A/2 - gs_frw2,												-gs_frame_thk - jp,	1+32
		endif
	endif

	if gs_frame_style_m = FRAME_STYLE_DOUBLE_EGRESS_METAL then		! Double Egress Metal Frame

		fill gs_fillFr


		add2 frameDiff, 0

		poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			leafleft,				gs_unit_extra_thk,				1+32,
			leafleft + gs_rebate_width - gs_frw1,	gs_unit_extra_thk,				1+32,
			leafleft + gs_rebate_width - gs_frw1, 	0,								bListView+32,
			leafleft + gs_rebate_width - gs_frw1 - gs_tolerance_left,	0, 			_frame_left_endline+32,
			leafleft + gs_rebate_width - gs_frw1 - gs_tolerance_left,	-gs_frame_thk,	bListView+32,
			leafleft + gs_rebate_width - gs_frw1,	-gs_frame_thk,  						1+32,
			leafleft + gs_rebate_width - gs_frw1,	-gs_frame_thk-gs_unit_extra_thk,		1+32,
			leafleft,				-gs_frame_thk - gs_unit_extra_thk,		1+32

		poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			leafright,								gs_unit_extra_thk,				1+32,
			leafright - gs_rebate_width + gs_frw2,	gs_unit_extra_thk,				1+32,
			leafright - gs_rebate_width + gs_frw2, 	0,								bListView+32,
			leafright - gs_rebate_width + gs_frw2 + gs_tolerance_right,	0, 				_frame_right_endline+32,
			leafright - gs_rebate_width + gs_frw2 + gs_tolerance_right,	-gs_frame_thk,	bListView+32,
			leafright - gs_rebate_width + gs_frw2,	-gs_frame_thk,  						1+32,
			leafright - gs_rebate_width + gs_frw2,	-gs_frame_thk - gs_unit_extra_thk,		1+32,
			leafright,								-gs_frame_thk-gs_unit_extra_thk,		1+32

		del 1


	endif

	gosub 2500

endif


! =============================================================================
! 1:50
! =============================================================================

if symb_2d = 50 then

	!*********************** Detailed or 1:50

	if gs_frame_style_m = FRAME_STYLE_EXTRUSION | gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION then
		gs_frame_ovhg_left_in	= 0
		gs_frame_ovhg_left_out	= 0
		gs_frame_ovhg_right_in	= 0
		gs_frame_ovhg_right_out	= 0
		gs_tolerance_left		= 0
		gs_tolerance_right		= 0
	endif

	if gs_pocket_door_frame then
		_pocket_frame_thk = gs_pocket_frame_thk * gs_pocket_frame
		if (gs_frame_ovhg_left_out > EPS) | (gs_frame_ovhg_left_in > EPS) then
			if bDoubleDoor then
				_proportion = ((gs_frame_ovhg_left_in - gs_frame_ovhg_left_out)/outsideFrameThk)
				if not(_pocket_frame_thk) then
					line2 unitleft, -posLeafy, frameleftout - posLeafy*_proportion, -posLeafy
					line2 unitleft, -posLeafy - widthWallCut, frameleftout - (posLeafy + widthWallCut)*_proportion, -posLeafy - widthWallCut
				endif

				if gs_frame_left_endline then
					line2 frameleftout, 0, frameleftout - (posLeafy - _pocket_frame_thk)*_proportion, -posLeafy + _pocket_frame_thk
					line2 frameleftout - (posLeafy + widthWallCut + _pocket_frame_thk)*_proportion, -posLeafy - widthWallCut - _pocket_frame_thk, frameleftin, -outsideFrameThk
				endif
			else
				if gs_frame_left_endline then
					line2 frameleftout, 0, frameleftin, -outsideFrameThk
				endif
			endif

			if (gs_frame_ovhg_left_out > EPS) then
				line2 unitleft, 0, frameleftout, 0
			endif

			if (gs_frame_ovhg_left_in > EPS) then
				line2 unitleft, -outsideFrameThk, frameleftin, -outsideFrameThk
			endif
		endif
		if (gs_frame_ovhg_right_out > EPS) | (gs_frame_ovhg_right_in > EPS) then
			if (gs_frame_ovhg_right_out > EPS) then
				line2 unitright, 0, framerightout, 0
			endif

			if (gs_frame_ovhg_right_in > EPS) then
				line2 unitright, -outsideFrameThk, framerightin, -outsideFrameThk
			endif

			_proportion = ((gs_frame_ovhg_right_in - gs_frame_ovhg_right_out)/outsideFrameThk)
			if not(_pocket_frame_thk) then
				line2 unitright, -posLeafy, framerightout + posLeafy*_proportion, -posLeafy
				line2 unitright, -posLeafy - widthWallCut, framerightout + (posLeafy + widthWallCut)*_proportion, -posLeafy - widthWallCut
			endif

			if gs_frame_right_endline then
				line2 framerightout, 0, framerightout + (posLeafy - _pocket_frame_thk)*_proportion, -posLeafy + _pocket_frame_thk
				line2 framerightout + (posLeafy + widthWallCut + _pocket_frame_thk)*_proportion, -posLeafy - widthWallCut - _pocket_frame_thk, framerightin, -outsideFrameThk
			endif
		endif
	else
		if (gs_frame_ovhg_left_out > EPS) | (gs_frame_ovhg_left_in > EPS) then
			if (gs_frame_ovhg_left_out > EPS) then
				line2 unitleft,  0, unitleft - gs_frame_ovhg_left_out,  0
			endif
			if (gs_frame_ovhg_left_in > EPS) then
				line2 unitleft,  -outsideFrameThk, unitleft - gs_frame_ovhg_left_in,  -outsideFrameThk
			endif
			if gs_frame_left_endline then
				line2 unitleft - gs_frame_ovhg_left_out,  0, unitleft - gs_frame_ovhg_left_in,  -outsideFrameThk
			endif
		endif
		if (gs_frame_ovhg_right_out > EPS) | (gs_frame_ovhg_right_in > EPS) then
			if (gs_frame_ovhg_right_out > EPS) then
				line2 unitright, 0, unitright + gs_frame_ovhg_right_out, 0
			endif
			if (gs_frame_ovhg_right_in > EPS) then
				line2 unitright, -outsideFrameThk, unitright + gs_frame_ovhg_right_in, -outsideFrameThk
			endif
			if gs_frame_right_endline then
				line2 unitright + gs_frame_ovhg_right_out, 0, unitright + gs_frame_ovhg_right_in, -outsideFrameThk
			endif
		endif
	endif

	leafleft2	= egressLeft
	leafright2	= egressRight + (gs_pivotOffset + gs_leaf_thk/2) * gs_pivotdoor

	fill gs_fillFr

	if gs_frame_style_m <> FRAME_STYLE_METAL & gs_frame_style_m <> FRAME_STYLE_DOUBLE_EGRESS_METAL then	! Not Commercial & Metal Frame
		if gs_sidelight_left then

			put -A/2, -outsideFrameThk,	_frame_left_endline+32
			if gs_frame_style_m = FRAME_STYLE_EXTRUSION | gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION then
				put -A/2,						-outsideFrameThk + frameRebateOffset,	1+32,
					-A/2 + frameRebateWidth,	-outsideFrameThk + frameRebateOffset,	1+32,
					-A/2 + frameRebateWidth,	-frameRebateOffset,						1+32,
					-A/2,						-frameRebateOffset,						_frame_left_endline+32
			endif

			if gs_sidelight_rebate_width > 0 & gs_sidelight_rebate_depth > 0 & not(bNoRebate_09) then
				if gs_opening_dir_sl_m <> 1 then
					poly2_b 5+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						-A/2,	 									0,			1+32,
						-A/2 + gs_frw1,								0,			1+32,
						-A/2 + gs_frw1,								-insideFrameThk+gs_sidelight_rebate_depth,			1+32,
						-A/2 + gs_frw1-gs_sidelight_rebate_width,	-insideFrameThk+gs_sidelight_rebate_depth,			1+32,
						-A/2 + gs_frw1-gs_sidelight_rebate_width,	-outsideFrameThk,							1+32,
						get(nsp)
				else
					poly2_b 5+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						-A/2,							 			0,							1+32,
						-A/2 + gs_frw1-gs_sidelight_rebate_width,	0,							1+32,
						-A/2 + gs_frw1-gs_sidelight_rebate_width,	-gs_sidelight_rebate_depth,	1+32,
						-A/2 + gs_frw1,								-gs_sidelight_rebate_depth,	1+32,
						-A/2 + gs_frw1,								-outsideFrameThk,			1+32,
						get(nsp)
				endif
			else
				poly2_b 3+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					-A/2,				 0,					1+32,
					-A/2 + gs_frw1,		 0,					1+32,
					-A/2 + gs_frw1,		-outsideFrameThk,	1+32,
					get(nsp)
			endif

			if gs_sidelight_rebate_width > 0 & gs_sidelight_rebate_depth > 0 & not(bNoRebate_09) then
				if gs_opening_dir_sl_m <> 1 then
					put	-A/2 + gs_sl_w1 - gs_frw_Sl_left+gs_sidelight_rebate_width,	-insideFrameThk, 1+32,
						-A/2 + gs_sl_w1 - gs_frw_Sl_left+gs_sidelight_rebate_width, -insideFrameThk+gs_sidelight_rebate_depth, 1+32,
						-A/2 + gs_sl_w1 - gs_frw_Sl_left, -insideFrameThk+gs_sidelight_rebate_depth, 1+32,
						-A/2 + gs_sl_w1 - gs_frw_Sl_left, 0, 1+32
				else
					put	-A/2 + gs_sl_w1 - gs_frw_Sl_left,	-insideFrameThk, 1+32,
						-A/2 + gs_sl_w1 - gs_frw_Sl_left, -gs_sidelight_rebate_depth, 1+32,
						-A/2 + gs_sl_w1 - gs_frw_Sl_left+gs_sidelight_rebate_width, -gs_sidelight_rebate_depth, 1+32,
						-A/2 + gs_sl_w1 - gs_frw_Sl_left+gs_sidelight_rebate_width, 0, 1+32
				endif
			else
				put	-A/2 + gs_sl_w1 - gs_frw_Sl_left,	-insideFrameThk, 1+32,
					-A/2 + gs_sl_w1 - gs_frw_Sl_left, 0, 1+32
			endif

			if	gs_frame_style_m = FRAME_STYLE_BLOCK			|\
				gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE	then
				poly2_b 2+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					get(nsp),
					-A/2 + gs_sl_w1,	0,1+32,
					-A/2 + gs_sl_w1,	-insideFrameThk, 1+32
			endif

			if gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE then
				add2 0, (gs_leaf_thk-gs_ds_pr)*(abs(gs_ds_pr)>eps)
				poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					-A/2 + gs_sl_w1,			-gs_leaf_thk,			1+32,
					-A/2 + gs_sl_w1 + gs_ds_w,	-gs_leaf_thk,			1+32,
					-A/2 + gs_sl_w1 + gs_ds_w,	-gs_leaf_thk-gs_ds_thk,	1+32,
					-A/2 + gs_sl_w1,			-gs_leaf_thk-gs_ds_thk,	1+32
				del 1
			endif

			if	gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE		|\
				gs_frame_style_m = FRAME_STYLE_EXTRUSION			|\
				gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION	then
				poly2_b 4+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					get(nsp),
					-A/2 + gs_sl_w1 - gs_rebate_width,	 0,				1+32,
					-A/2 + gs_sl_w1 - gs_rebate_width,	-gs_leaf_thk,	1+32,
					-A/2 + gs_sl_w1,					-gs_leaf_thk,	1+32,
					-A/2 + gs_sl_w1,					-insideFrameThk,1+32
			endif

		else

			if gs_pocket_door_frame & bDoubleDoor then  ! Double Pocket Doors Frame Left
				if posLeafy > EPS then
					put	unitleft,	 0,			1+32,
						leafleft2,	 0,			1+32,
						leafleft2,	-posLeafy,	1+32,
						unitleft,	-posLeafy,	_frame_left_endline * not(gs_pocket_frame) + 32
					if gs_pocket_frame then
						 put unitleft,	-posLeafy + gs_pocket_frame_thk,	_frame_left_endline + 32
					endif
					poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
				endif

				if posLeafy < gs_frame_thk - widthWallCut - eps then
					put	unitleft,	-posLeafy - widthWallCut,	1+32,
						leafleft2,	-posLeafy - widthWallCut,	1+32,
						leafleft2,	-gs_frame_thk,				1+32,
						unitleft,	-gs_frame_thk,				_frame_left_endline+32
					if gs_pocket_frame then
						 put unitleft,	-posLeafy - widthWallCut - gs_pocket_frame_thk,	0
					endif
					poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
				endif

			else ! Other Doors & Single Pocket Doors Frame Left

				if gs_frame_style_m = FRAME_STYLE_BLOCK | gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE then
					poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						-A/2 + gs_sl_w1 - gs_frw1,	-outsideFrameThk,_frame_left_endline+32,
						-A/2 + gs_sl_w1 - gs_frw1,	0,				1+32,
						-A/2 + gs_sl_w1,			0,				1+32,
						-A/2 + gs_sl_w1,			-outsideFrameThk,1+32
				endif

				if gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE then
					add2 0, (gs_leaf_thk-gs_ds_pr)*(abs(gs_ds_pr)>eps)
					poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						-A/2 + gs_sl_w1,			-gs_leaf_thk,			1+32,
						-A/2 + gs_sl_w1 + gs_ds_w,	-gs_leaf_thk,			1+32,
						-A/2 + gs_sl_w1 + gs_ds_w,	-gs_leaf_thk-gs_ds_thk,	1+32,
						-A/2 + gs_sl_w1,			-gs_leaf_thk-gs_ds_thk,	1+32
					del 1
				endif

				if gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE then
					poly2_b 6, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						-A/2 + gs_sl_w1 - gs_frw1,			-outsideFrameThk,_frame_left_endline+32,
						-A/2 + gs_sl_w1 - gs_frw1,			 0,				1+32,
						-A/2 + gs_sl_w1 - gs_rebate_width,	 0,				1+32,
						-A/2 + gs_sl_w1 - gs_rebate_width,	-gs_leaf_thk,	1+32,
						-A/2 + gs_sl_w1,					-gs_leaf_thk,	1+32,
						-A/2 + gs_sl_w1,					-outsideFrameThk,1+32
				endif

				if gs_frame_style_m = FRAME_STYLE_EXTRUSION | gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION then
					poly2_b 10, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						-A/2 + gs_sl_w1 - gs_frw1,						-outsideFrameThk,					1+32,
						-A/2 + gs_sl_w1 - gs_frw1,						-outsideFrameThk+frameRebateOffset,	1+32,
						-A/2 + gs_sl_w1 - gs_frw1 + frameRebateWidth,	-outsideFrameThk+frameRebateOffset,	1+32,
						-A/2 + gs_sl_w1 - gs_frw1 + frameRebateWidth,	-frameRebateOffset,					1+32,
						-A/2 + gs_sl_w1 - gs_frw1,						-frameRebateOffset,					1+32,
						-A/2 + gs_sl_w1 - gs_frw1,	 					 0,									1+32,
						-A/2 + gs_sl_w1 - gs_rebate_width,				 0,									1+32,
						-A/2 + gs_sl_w1 - gs_rebate_width,				-gs_leaf_thk,						1+32,
						-A/2 + gs_sl_w1,								-gs_leaf_thk,						1+32,
						-A/2 + gs_sl_w1,								-outsideFrameThk,					1+32
				endif
			endif
		endif

		if gs_sidelight_right then
			put A/2, -outsideFrameThk, _frame_right_endline+32
			if gs_frame_style_m = FRAME_STYLE_EXTRUSION | gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION then
				put A/2,					-outsideFrameThk + frameRebateOffset,	1+32,
					A/2 - frameRebateWidth,	-outsideFrameThk + frameRebateOffset,	1+32,
					A/2 - frameRebateWidth,	-frameRebateOffset,						1+32,
					A/2,					-frameRebateOffset,						_frame_right_endline+32
			endif

			if gs_sidelight_rebate_width > 0 & gs_sidelight_rebate_depth > 0 & not(bNoRebate_05) then
				if gs_opening_dir_sl_m <> 1 then
					poly2_b 5+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						A/2,	 									0,			1+32,
						A/2 - gs_frw2,								0,			1+32,
						A/2 - gs_frw2,								-insideFrameThk+gs_sidelight_rebate_depth,			1+32,
						A/2 - gs_frw2+gs_sidelight_rebate_width,	-insideFrameThk+gs_sidelight_rebate_depth,			1+32,
						A/2 - gs_frw2+gs_sidelight_rebate_width,	-outsideFrameThk,							1+32,
						get(nsp)
				else
					poly2_b 5+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						A/2,							 			0,			1+32,
						A/2 - gs_frw2+gs_sidelight_rebate_width,	0,			1+32,
						A/2 - gs_frw2+gs_sidelight_rebate_width,	-gs_sidelight_rebate_depth,			1+32,
						A/2 - gs_frw2,								-gs_sidelight_rebate_depth,			1+32,
						A/2 - gs_frw2,								-outsideFrameThk,							1+32,
						get(nsp)
				endif
			else
				poly2_b 3+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2,			 0,			1+32,
					A/2 - gs_frw2,	 0,			1+32,
					A/2 - gs_frw2,	-outsideFrameThk,	1+32,
					get(nsp)
			endif

			if gs_sidelight_rebate_width > 0 & gs_sidelight_rebate_depth > 0 & not(bNoRebate_05) then
				if gs_opening_dir_sl_m <> 1 then
					put	A/2 - gs_sl_w2 + gs_frw_Sl_right-gs_sidelight_rebate_width,	-insideFrameThk, 1+32,
						A/2 - gs_sl_w2 + gs_frw_Sl_right-gs_sidelight_rebate_width, -insideFrameThk+gs_sidelight_rebate_depth, 1+32,
						A/2 - gs_sl_w2 + gs_frw_Sl_right, -insideFrameThk+gs_sidelight_rebate_depth, 1+32,
						A/2 - gs_sl_w2 + gs_frw_Sl_right, 0, 1+32
				else
					put	A/2 - gs_sl_w2 + gs_frw_Sl_right,							-insideFrameThk,			1+32,
						A/2 - gs_sl_w2 + gs_frw_Sl_right,							-gs_sidelight_rebate_depth,	1+32,
						A/2 - gs_sl_w2 + gs_frw_Sl_right-gs_sidelight_rebate_width, -gs_sidelight_rebate_depth,	1+32,
						A/2 - gs_sl_w2 + gs_frw_Sl_right-gs_sidelight_rebate_width,	 0,							1+32
				endif
			else
				put	A/2 - gs_sl_w2 + gs_frw_Sl_right,	-insideFrameThk, 1+32,
					A/2 - gs_sl_w2 + gs_frw_Sl_right, 0, 1+32
			endif

			if	gs_frame_style_m = FRAME_STYLE_BLOCK			|\
				gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE	then
				poly2_b 2+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					get(nsp),
					A/2 - gs_sl_w2,	0,				1+32,
					A/2 - gs_sl_w2,	-insideFrameThk,1+32
			endif

			if gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE then
				add2 0, (gs_leaf_thk-gs_ds_pr)*(abs(gs_ds_pr)>eps)
				poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2 - gs_sl_w2,				-gs_leaf_thk,			1+32,
					A/2 - gs_sl_w2 - gs_ds_w,	-gs_leaf_thk,			1+32,
					A/2 - gs_sl_w2 - gs_ds_w,	-gs_leaf_thk-gs_ds_thk,	1+32,
					A/2 - gs_sl_w2,				-gs_leaf_thk-gs_ds_thk,	1+32
				del 1
			endif

			if	gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE		|\
				gs_frame_style_m = FRAME_STYLE_EXTRUSION			|\
				gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION	then
				poly2_b 4+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					get(nsp),
					A/2 - gs_sl_w2 + gs_rebate_width,	 0,				1+32,
					A/2 - gs_sl_w2 + gs_rebate_width,	-gs_leaf_thk,	1+32,
					A/2 - gs_sl_w2,						-gs_leaf_thk,	1+32,
					A/2 - gs_sl_w2,						-insideFrameThk,1+32
			endif

		else

			if gs_pocket_door_frame then	! Pocket Doors Frame Right
				if posLeafy > EPS then
					put	 unitright,		 0,			1+32,
						 leafright2,	 0,			1+32,
						 leafright2,	-posLeafy,	1+32,
						 unitright,		-posLeafy,	_frame_right_endline * not(gs_pocket_frame) + 32
					if gs_pocket_frame then
						 put unitright,	-posLeafy + gs_pocket_frame_thk,	_frame_right_endline + 32
					endif
					poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
				endif

				if posLeafy < gs_frame_thk - widthWallCut - eps then
					put	 unitright,		-posLeafy - widthWallCut,			1+32,
						 leafright2,	-posLeafy - widthWallCut,			1+32,
						 leafright2,	-gs_frame_thk,	1+32,
						 unitright,		-gs_frame_thk,	_frame_right_endline + 32
					if gs_pocket_frame then
						 put unitright,	-posLeafy - widthWallCut - gs_pocket_frame_thk,	0
					endif
					poly2_b nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg, get(nsp)
				endif

			else	! Others Doors Frame Right

				if gs_frame_style_m = FRAME_STYLE_BLOCK | gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE then
					poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						A/2 - gs_sl_w2 + gs_frw2,	-outsideFrameThk,_frame_right_endline+32,
						A/2 - gs_sl_w2 + gs_frw2,	0,				1+32,
						A/2 - gs_sl_w2,				0,				1+32,
						A/2 - gs_sl_w2,				-outsideFrameThk,1+32
				endif

				if gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE then
					add2 0, (gs_leaf_thk-gs_ds_pr)*(abs(gs_ds_pr)>eps)
					poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						A/2 - gs_sl_w2,				-gs_leaf_thk,			1+32,
						A/2 - gs_sl_w2 - gs_ds_w,	-gs_leaf_thk,			1+32,
						A/2 - gs_sl_w2 - gs_ds_w,	-gs_leaf_thk-gs_ds_thk,	1+32,
						A/2 - gs_sl_w2,				-gs_leaf_thk-gs_ds_thk,	1+32
					del 1
				endif

				if gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE then
					poly2_b 6, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						A/2 - gs_sl_w2 + gs_frw2,			-outsideFrameThk,_frame_right_endline+32,
						A/2 - gs_sl_w2 + gs_frw2,			 0,				1+32,
						A/2 - gs_sl_w2 + gs_rebate_width,	 0,				1+32,
						A/2 - gs_sl_w2 + gs_rebate_width,	-gs_leaf_thk,	1+32,
						A/2 - gs_sl_w2,						-gs_leaf_thk,	1+32,
						A/2 - gs_sl_w2,						-outsideFrameThk,1+32
				endif

				if gs_frame_style_m = FRAME_STYLE_EXTRUSION | gs_frame_style_m = FRAME_STYLE_WRAPPED_EXTRUSION then
					poly2_b 10, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
						A/2 - gs_sl_w2 + gs_frw2,						-outsideFrameThk,						1+32,
						A/2 - gs_sl_w2 + gs_frw2,						-outsideFrameThk + frameRebateOffset,	1+32,
						A/2 - gs_sl_w2 + gs_frw2 - frameRebateWidth,	-outsideFrameThk + frameRebateOffset,	1+32,
						A/2 - gs_sl_w2 + gs_frw2 - frameRebateWidth,	-frameRebateOffset,						1+32,
						A/2 - gs_sl_w2 + gs_frw2,	 					-frameRebateOffset,						1+32,
						A/2 - gs_sl_w2 + gs_frw2,						 0,										1+32,
						A/2 - gs_sl_w2 + gs_rebate_width,				 0,										1+32,
						A/2 - gs_sl_w2 + gs_rebate_width,				-gs_leaf_thk,							1+32,
						A/2 - gs_sl_w2,									-gs_leaf_thk,							1+32,
						A/2 - gs_sl_w2,									-outsideFrameThk,						1+32
				endif
			endif
		endif
	endif

	if gs_frame_style_m = FRAME_STYLE_METAL then		! Commercial Door Frame
		fill gs_fillFr

		jp = 1/2"	! Jamb projection for drywall partition, or edge return for masonry wall, (Typical)
		gs_fcr=_frame_right_endline
		gs_fcl=_frame_left_endline

		if not(gs_frame_wthk) then		! Frame Surrounds Wall

			frameleftout	= unitleft  - gs_frame_ovhg_left_out
			framerightout	= unitright + gs_frame_ovhg_right_out
			frameleftin		= unitleft  - gs_frame_ovhg_left_in
			framerightin	= unitright + gs_frame_ovhg_right_in

			min_frameleft	= max(frameleftout, frameleftin)
			min_frameright	= min(framerightout, framerightin)

			! Terminal Frame Left
			if gs_fitframe_slantedreveal then		! Fit Frame to Slanted Wall
				if frameleftout > frameleftin then
					put min_frameleft, 0,                  1+32,
						min_frameleft - tan(gs_reveal_left_angle) * jp, -jp,                gs_fcr+32,
						min_frameleft+jp*(gs_fcl=1),-jp,                gs_fcr+32,
						min_frameleft+jp*(gs_fcl=1),-gs_frame_thk+jp,            gs_fcr+32,
						min_frameleft - tan(gs_reveal_left_angle) * (gs_frame_thk-jp),-gs_frame_thk+jp,            1+32,
						min_frameleft - tan(gs_reveal_left_angle) * gs_frame_thk,-gs_frame_thk,               1+32
				else
					put min_frameleft - tan(gs_reveal_left_angle) * gs_frame_thk, 0,                  1+32,
						min_frameleft - tan(gs_reveal_left_angle) * (gs_frame_thk-jp), -jp,                gs_fcr+32,
						min_frameleft+jp*(gs_fcl=1),-jp,                gs_fcr+32,
						min_frameleft+jp*(gs_fcl=1),-gs_frame_thk+jp,            gs_fcr+32,
						min_frameleft - tan(gs_reveal_left_angle) * jp,-gs_frame_thk+jp,            1+32,
						min_frameleft,-gs_frame_thk,               1+32
				endif
			else
				put	min_frameleft, 0,                  1+32,
					min_frameleft, -jp,                gs_fcr+32,
					min_frameleft+jp*(gs_fcl=1),-jp,                gs_fcr+32,
					min_frameleft+jp*(gs_fcl=1),-gs_frame_thk+jp,            gs_fcr+32,
					min_frameleft,-gs_frame_thk+jp,            1+32,
					min_frameleft,-gs_frame_thk,               1+32
			endif


			if gs_sidelight_left then
				poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					leafleft, 										0,         1+32,
					leafleft-gs_frw_Sl_left, 0,                  1+32,
					leafleft-gs_frw_Sl_left,-gs_frame_thk,               1+32,
					leafleft,-gs_frame_thk,                 1+32,
					leafleft,-gs_ds_pr-gs_ds_thk,             1+32,
					leafleft+gs_ds_w,-gs_ds_pr-gs_ds_thk,         1+32,
					leafleft+gs_ds_w,-gs_ds_pr,             1+32,
					leafleft,-gs_ds_pr,                 1+32

				! Terminal Sidelight Frame Left
				poly2_b 2+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					unitleft+gs_frw1, 0,                    1+32,
					get(nsp),
					unitleft+gs_frw1,-gs_frame_thk,                 1+32
			else

				! Terminal Door Frame Left
				poly2_b 6+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					unitleft+gs_frw1, 0,                    1+32,
					get(nsp),
					unitleft+gs_frw1,-gs_frame_thk,                 1+32,
					unitleft+gs_frw1,-gs_ds_pr-gs_ds_thk,             1+32,
					unitleft+gs_frw1+gs_ds_w,-gs_ds_pr-gs_ds_thk,         1+32,
					unitleft+gs_frw1+gs_ds_w,-gs_ds_pr,             1+32,
					unitleft+gs_frw1,-gs_ds_pr,                 1+32
			endif

			! Terminal Frame Right
			if gs_fitframe_slantedreveal then
				if framerightout < framerightin then
					put min_frameright, 0,                  1+32,
						min_frameright + tan(gs_reveal_right_angle) * jp, -jp,                gs_fcr+32,
						min_frameright-jp*(gs_fcr=1),-jp,                gs_fcr+32,
						min_frameright-jp*(gs_fcr=1),-gs_frame_thk+jp,            gs_fcr+32,
						min_frameright + tan(gs_reveal_right_angle) * (gs_frame_thk-jp),-gs_frame_thk+jp,            1+32,
						min_frameright + tan(gs_reveal_right_angle) * gs_frame_thk,-gs_frame_thk,               1+32
				else
					put min_frameright + tan(gs_reveal_right_angle) * gs_frame_thk, 0,                  1+32,
						min_frameright + tan(gs_reveal_right_angle) * (gs_frame_thk-jp), -jp,                gs_fcr+32,
						min_frameright-jp*(gs_fcr=1),-jp,                gs_fcr+32,
						min_frameright-jp*(gs_fcr=1),-gs_frame_thk+jp,            gs_fcr+32,
						min_frameright + tan(gs_reveal_right_angle) * jp,-gs_frame_thk+jp,            1+32,
						min_frameright,-gs_frame_thk,               1+32
				endif
			else
				put min_frameright, 0,                  1+32,
					min_frameright, -jp,                gs_fcr+32,
					min_frameright-jp*(gs_fcr=1),-jp,                gs_fcr+32,
					min_frameright-jp*(gs_fcr=1),-gs_frame_thk+jp,            gs_fcr+32,
					min_frameright,-gs_frame_thk+jp,            1+32,
					min_frameright,-gs_frame_thk,               1+32
			endif


			if gs_sidelight_right then
				poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2-gs_sl_w2+gs_frw_Sl_right, 					0,         1+32,
					A/2-gs_sl_w2, 0,                  1+32,
					A/2-gs_sl_w2,-gs_ds_pr,                 1+32,
					A/2-gs_sl_w2-gs_ds_w,-gs_ds_pr,             1+32,
					A/2-gs_sl_w2-gs_ds_w,-gs_ds_pr-gs_ds_thk,         1+32,
					A/2-gs_sl_w2,-gs_ds_pr-gs_ds_thk,             1+32,
					A/2-gs_sl_w2,-gs_frame_thk,               1+32,
					A/2-gs_sl_w2+gs_frw_Sl_right,-gs_frame_thk,                 1+32

				! Terminal Sidelight Frame Left
				poly2_b 2+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					unitright-gs_frw2, 0,                    1+32,
					get(nsp),
					unitright-gs_frw2,-gs_frame_thk,                 1+32

			else

				! Terminal Door Frame Left
				poly2_b 6+nsp/3, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					unitright-gs_frw2, 0,                    1+32,
					get(nsp),
					unitright-gs_frw2,-gs_frame_thk,                 1+32,
					unitright-gs_frw2,-gs_ds_pr-gs_ds_thk,             1+32,
					unitright-gs_frw2-gs_ds_w,-gs_ds_pr-gs_ds_thk,         1+32,
					unitright-gs_frw2-gs_ds_w,-gs_ds_pr,             1+32,
					unitright-gs_frw2,-gs_ds_pr,                 1+32
			endif
		else
			if gs_sidelight_left then
				poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					-A/2, jp,1+32,
					-A/2+gs_frw1, 						jp,			1+32,
					-A/2+gs_frw1, 						-gs_frame_thk-jp,1+32,
					-A/2, 								-gs_frame_thk-jp,1+32,
					-A/2, 								-gs_frame_thk, 	32,
					-A/2 - gs_fcr * gs_tolerance_left, 	-gs_frame_thk, 	1+32,
					-A/2 - gs_fcr * gs_tolerance_left, 	0, 			1+32,
					-A/2, 								0, 			1+32

				poly2_b 9, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					leafleft, 										jp,         1+32,
					leafleft-gs_frw_Sl_left, 	jp,                  1+32,
					leafleft-gs_frw_Sl_left,	-gs_frame_thk-jp,               1+32,
					leafleft,					-gs_frame_thk-jp,                 1+32,
					leafleft,					-gs_ds_pr-gs_ds_thk+jp,             1+32,
					leafleft+gs_ds_w,			-gs_ds_pr-gs_ds_thk+jp,         1+32,
					leafleft+gs_ds_w,			-gs_ds_pr+jp,             1+32,
					leafleft,					-gs_ds_pr+jp,                 1+32,
					leafleft, jp,                    -1
			else

				poly2_b 12, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					-A/2,						jp,							1+32,
					-A/2 + gs_frw1,				jp,							1+32,
					-A/2 + gs_frw1,				-gs_ds_pr + jp,				1+32,
					-A/2 + gs_frw1 + gs_ds_w,	-gs_ds_pr + jp,				1+32,
					-A/2 + gs_frw1 + gs_ds_w,	-gs_ds_pr - gs_ds_thk + jp,	1+32,
					-A/2 + gs_frw1,				-gs_ds_pr - gs_ds_thk + jp,	1+32,
					-A/2 + gs_frw1,				-gs_frame_thk - jp,			1+32,
					-A/2,						-gs_frame_thk - jp,			1+32,
					-A/2,						-gs_frame_thk,				bListView+32,
					-A/2 - gs_fcr * gs_tolerance_left,	-gs_frame_thk,		_frame_left_endline+32,
					-A/2 - gs_fcr * gs_tolerance_left,	 0,					bListView+32,
					-A/2,								 0,					1+32
			endif

			if gs_sidelight_right then
				poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2-gs_sl_w2+gs_frw_Sl_right, 	jp,						1+32,
					A/2-gs_sl_w2,					jp,						1+32,
					A/2-gs_sl_w2,					-gs_ds_pr+jp,			1+32,
					A/2-gs_sl_w2-gs_ds_w,			-gs_ds_pr+jp,			1+32,
					A/2-gs_sl_w2-gs_ds_w,			-gs_ds_pr-gs_ds_thk+jp,	1+32,
					A/2-gs_sl_w2,					-gs_ds_pr-gs_ds_thk+jp,	1+32,
					A/2-gs_sl_w2,					-gs_frame_thk-jp,			1+32,
					A/2-gs_sl_w2+gs_frw_Sl_right,	-gs_frame_thk-jp,			1+32

				poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2-gs_frw2, 										jp,         1+32,
					A/2-gs_frw2+gs_frw2, jp,                  1+32,
					A/2-gs_frw2+gs_frw2, 								0,          0+32,
					A/2-gs_frw2+gs_fcr*(gs_frw2+gs_tolerance_right), 	0, 			0+32,
					A/2-gs_frw2+gs_fcr*(gs_frw2+gs_tolerance_right), -gs_frame_thk,	0+32,
					A/2-gs_frw2+gs_frw2,							   -gs_frame_thk,  	1+32,
					A/2-gs_frw2+gs_frw2,-gs_frame_thk-jp,               1+32,
					A/2-gs_frw2,-gs_frame_thk-jp,                 1+32
			else
				poly2_b 12, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
					A/2-gs_frw2, 										jp,         1+32,
					A/2-gs_frw2+gs_frw2, jp,                  1+32,
					A/2-gs_frw2+gs_frw2, 								0,          bListView+32,
					A/2-gs_frw2+gs_fcr*(gs_frw2+gs_tolerance_right), 	0, 			_frame_right_endline+32,
					A/2-gs_frw2+gs_fcr*(gs_frw2+gs_tolerance_right), -gs_frame_thk,	bListView+32,
					A/2-gs_frw2+gs_frw2,							   -gs_frame_thk,  	1+32,
					A/2-gs_frw2+gs_frw2,-gs_frame_thk-jp,               1+32,
					A/2-gs_frw2,-gs_frame_thk-jp,                 1+32,
					A/2-gs_frw2,-gs_ds_pr-gs_ds_thk+jp,             1+32,
					A/2-gs_frw2-gs_ds_w,-gs_ds_pr-gs_ds_thk+jp,         1+32,
					A/2-gs_frw2-gs_ds_w,-gs_ds_pr+jp,             1+32,
					A/2-gs_frw2,-gs_ds_pr+jp,                 1+32
			endif
		endif
	endif


	if gs_frame_style_m = FRAME_STYLE_DOUBLE_EGRESS_METAL then		! Double Egress Metal Frame

		fill gs_fillFr

		add2 frameDiff, 0

		poly2_b 12, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			leafleft + gs_rebate_width,				gs_unit_extra_thk,				1+32,
			leafleft + gs_rebate_width - gs_frw1,	gs_unit_extra_thk,				1+32,
			leafleft + gs_rebate_width - gs_frw1, 	0,								bListView+32,
			leafleft + gs_rebate_width - gs_frw1 - gs_tolerance_left,	0, 			_frame_left_endline+32,
			leafleft + gs_rebate_width - gs_frw1 - gs_tolerance_left,	-gs_frame_thk,	bListView+32,
			leafleft + gs_rebate_width - gs_frw1,	-gs_frame_thk,  						1+32,
			leafleft + gs_rebate_width - gs_frw1,	-gs_frame_thk-gs_unit_extra_thk,		1+32,
			leafleft - gs_rebate_width,				-gs_frame_thk-gs_unit_extra_thk,		1+32,
			leafleft - gs_rebate_width,				-gs_frame_thk + posLeafy,					1+32,
			leafleft,								-gs_frame_thk + posLeafy,					1+32,
			leafleft,								-gs_frame_thk + posLeafy+ gs_leaf_thk,	1+32,
			leafleft + gs_rebate_width,				-gs_frame_thk + posLeafy+ gs_leaf_thk,	1+32

		poly2_b 12, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			leafright + gs_rebate_width,			gs_unit_extra_thk,				1+32,
			leafright - gs_rebate_width + gs_frw2,	gs_unit_extra_thk,				1+32,
			leafright - gs_rebate_width + gs_frw2, 	0,								bListView+32,
			leafright - gs_rebate_width + gs_frw2 + gs_tolerance_right,	0, 			_frame_right_endline+32,
			leafright - gs_rebate_width + gs_frw2 + gs_tolerance_right,	-gs_frame_thk,	bListView+32,
			leafright - gs_rebate_width + gs_frw2,	-gs_frame_thk,  						1+32,
			leafright - gs_rebate_width + gs_frw2,	-gs_frame_thk-gs_unit_extra_thk,		1+32,
			leafright - gs_rebate_width,			-gs_frame_thk-gs_unit_extra_thk,		1+32,
			leafright - gs_rebate_width,			-gs_frame_thk + posLeafy,					1+32,
			leafright,								-gs_frame_thk + posLeafy,					1+32,
			leafright,								-gs_frame_thk + posLeafy + gs_leaf_thk,	1+32,
			leafright + gs_rebate_width,			-gs_frame_thk + posLeafy + gs_leaf_thk,	1+32

		del 1


	endif




	gosub 2500

endif


! =============================================================================
! Pockets
! =============================================================================
"PlacingPockets":

if gs_pocket then

	unID = 3400

	add2 (gs_tolerance_right - gs_tolerance_left)/2, 0

	if not(gs_pocket_door_frame) then
		gs_tolerance_left	= 0
		gs_tolerance_right	= 0
	endif

	if bDoubleDoor then
		if gs_usaframe then
			PocketWidth = ac_leaf_width/2-0.005
			if gs_pocket_door_frame then PocketWidth = ac_leaf_width/2 - gs_frame_width_right-0.005
		else
			if bPocketWidthOverride then
				PocketWidth = gs_pocket_width/2 - (ac_egress_width/2 + gs_frame_width_right) * gs_pocket_door_frame - ac_wallhole_width/2 * not(gs_pocket_door_frame)
			else
				PocketWidth = ac_wallhole_width/2
				if gs_pocket_door_frame then PocketWidth = ac_egress_width/2 - gs_frame_width_right
			endif
		endif
	else
		if gs_usaframe then
			PocketWidth = ac_leaf_width-0.005
			if gs_pocket_door_frame then PocketWidth = ac_leaf_width - gs_frame_width_right-0.005
		else
			if bPocketWidthOverride then
				PocketWidth = gs_pocket_width - (A + gs_tolerance_left) * gs_pocket_door_frame - ac_wallhole_width * not(gs_pocket_door_frame)
			else
				PocketWidth = ac_wallhole_width
				if gs_pocket_door_frame then PocketWidth = ac_egress_width - gs_frame_width_right
			endif
		endif
	endif
	PocketWidth = PocketWidth - (gs_tolerance_right - (gs_pocket_frame * gs_pocket_frame_thk) + gs_right_oversize) * (lod2D = DET2D_FS_SIMPLE)
	add2 - (gs_tolerance_right) * (lod2D <> DET2D_FS_SIMPLE) + gs_right_oversize * (lod2D = DET2D_FS_SIMPLE), 0

	_ActReveal_width = gs_reveal_right
	gosub "Pocket"
	del 1

	if bDoubleDoor then
		if gs_usaframe then
			PocketWidth = ac_leaf_width/2-0.005
			if gs_pocket_door_frame then PocketWidth = ac_leaf_width/2 - gs_frame_width_left-0.005
		else
			if bPocketWidthOverride then
				PocketWidth = gs_pocket_width/2 - (ac_egress_width/2 + gs_frame_width_left) * gs_pocket_door_frame - ac_wallhole_width/2 * not(gs_pocket_door_frame)
			else
				PocketWidth = ac_wallhole_width/2
				if gs_pocket_door_frame then PocketWidth = ac_egress_width/2 - gs_frame_width_left
			endif
		endif
		PocketWidth = PocketWidth - (gs_tolerance_left - (gs_pocket_frame * gs_pocket_frame_thk) + gs_left_oversize) * (lod2D = DET2D_FS_SIMPLE)

		mul2 -1,1
		add2 - (gs_tolerance_left) * (lod2D <> DET2D_FS_SIMPLE) + gs_left_oversize * (lod2D = DET2D_FS_SIMPLE), 0
		_ActReveal_width = gs_reveal_left
		gosub "Pocket"
		del 2
	endif

	del 1
endif




end



! =============================================================================
! Middle Frame (USA)
! =============================================================================

2500:

fill gs_fillFr

if gs_mf then

	if gs_frame_style_m = FRAME_STYLE_BLOCK or gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE or (symb_2d = 100 and gs_frame_style_m = FRAME_STYLE_SIMPLE_REBATE) then
		add2 A/2-gs_mf_pos, 0
		poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			-gs_mf_w/2,	 0,			1+32,
			-gs_mf_w/2,	-gs_frame_thk,	1+32,
			 gs_mf_w/2,	-gs_frame_thk,	1+32,
			 gs_mf_w/2,	 0,			1+32
		del 1
	else
		add2 A/2-gs_mf_pos, 0
		poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			-gs_mf_w/2+gs_rebate_width,	 0,				1+32,
			-gs_mf_w/2+gs_rebate_width,	-gs_leaf_thk,	1+32,
			-gs_mf_w/2,					-gs_leaf_thk,	1+32,
			-gs_mf_w/2,					-gs_frame_thk,		1+32,
			 gs_mf_w/2,					-gs_frame_thk,		1+32,
			 gs_mf_w/2,					-gs_leaf_thk,	1+32,
			 gs_mf_w/2-gs_rebate_width,	-gs_leaf_thk,	1+32,
			 gs_mf_w/2-gs_rebate_width,	 0,				1+32
		del 1
	endif

	if gs_frame_style_m = FRAME_STYLE_DOUBLE_REBATE and symb_2d = 50 then
		add2 0, (gs_leaf_thk-gs_ds_pr)*(abs(gs_ds_pr)>eps)

		poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			A/2-gs_mf_pos-gs_mf_w/2-gs_ds_w,	-gs_leaf_thk,			1+32,
			A/2-gs_mf_pos-gs_mf_w/2-gs_ds_w,	-gs_leaf_thk-gs_ds_thk,	1+32,
			A/2-gs_mf_pos-gs_mf_w/2,			-gs_leaf_thk-gs_ds_thk,	1+32,
			A/2-gs_mf_pos-gs_mf_w/2,			-gs_leaf_thk,			1+32

		poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
			A/2-gs_mf_pos+gs_mf_w/2+gs_ds_w,	-gs_leaf_thk,			1+32,
			A/2-gs_mf_pos+gs_mf_w/2+gs_ds_w,	-gs_leaf_thk-gs_ds_thk,	1+32,
			A/2-gs_mf_pos+gs_mf_w/2,			-gs_leaf_thk-gs_ds_thk,	1+32,
			A/2-gs_mf_pos+gs_mf_w/2,			-gs_leaf_thk,			1+32

		del 1
	endif
endif

return


! =============================================================================
! Pocket for Pocket Doors
! =============================================================================

"Pocket":


add2 ac_wallhole_width/2, 0

if gs_pocket_frame & symb_2d <= 100 then
	fill gs_fgs_fillFr
	pen gs_frame_pen_cont

	hotspot2 0,											-posLeafy + gs_pocket_frame_thk, 				unID: unID = unID+1
	hotspot2 0,											-posLeafy - widthWallCut - gs_pocket_frame_thk, unID: unID = unID+1
	hotspot2 PocketWidth + 0.005 + gs_pocket_frame_thk,	-posLeafy - widthWallCut - gs_pocket_frame_thk, unID: unID = unID+1
	hotspot2 PocketWidth + 0.005 + gs_pocket_frame_thk,	-posLeafy + gs_pocket_frame_thk, 				unID: unID = unID+1

	if bShowUnitHotspots & gs_pocket & (GLOB_CONTEXT = 4 | GLOB_CONTEXT = 6) then
		hotspot2 -ac_unit_width,								-posLeafy - widthWallCut/2,	unID: unID = unID+1
		hotspot2 PocketWidth + 0.005 + gs_pocket_frame_thk,		-posLeafy - widthWallCut/2,	unID: unID = unID+1
	endif

	poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
		0,											-posLeafy + gs_pocket_frame_thk,				1+32,
		0,											-posLeafy,										1+32,
		PocketWidth + 0.005,						-posLeafy,										1+32,
		PocketWidth + 0.005,						-posLeafy - widthWallCut,						1+32,
		0,											-posLeafy - widthWallCut,						1+32,
		0,											-posLeafy - widthWallCut - gs_pocket_frame_thk,	1+32,
		PocketWidth + 0.005 + gs_pocket_frame_thk,	-posLeafy - widthWallCut - gs_pocket_frame_thk,	1+32,
		PocketWidth + 0.005 + gs_pocket_frame_thk,	-posLeafy + gs_pocket_frame_thk,				1+32

	if gs_usaframe then
		pen WALL_SECT_PEN
	else
		pen gs_frame_pen_cont
	endif
	if gs_bFills then fill gs_wallhole_fill
	wallhole2 4, 2, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
		0, 0, 0,
		0,											-posLeafy + gs_pocket_frame_thk,				1,
		0,											-posLeafy - widthWallCut - gs_pocket_frame_thk,	1,
		PocketWidth + 0.005 + gs_pocket_frame_thk,	-posLeafy - widthWallCut - gs_pocket_frame_thk,	1,
		PocketWidth + 0.005 + gs_pocket_frame_thk,	-posLeafy + gs_pocket_frame_thk,				1

else
	if (gs_usaframe & GLOB_CONTEXT = 6) then
		hotspot2 0,						-posLeafy, 					unID: unID = unID+1
		hotspot2 0,						-posLeafy - widthWallCut,	unID: unID = unID+1
		hotspot2 PocketWidth + 0.005,	-posLeafy - widthWallCut,	unID: unID = unID+1
		hotspot2 PocketWidth + 0.005,	-posLeafy, 					unID: unID = unID+1

		if bShowUnitHotspots & gs_pocket & (GLOB_CONTEXT = 4 | GLOB_CONTEXT = 6) & not(bDoubleDoor) then
			hotspot2 -ac_wallhole_width + gs_tolerance_left,-posLeafy - widthWallCut/2,	unID: unID = unID+1
			hotspot2 PocketWidth + 0.005,					-posLeafy - widthWallCut/2,	unID: unID = unID+1
		endif
		pen gs_frame_pen_cont

		line2	0, 					-posLeafy,					PocketWidth + 0.005, 	-posLeafy
		line2	PocketWidth + 0.005,-posLeafy,					PocketWidth + 0.005,	-posLeafy - widthWallCut
		line2	0, 					-posLeafy - widthWallCut,	PocketWidth + 0.005, 	-posLeafy - widthWallCut
	endif

	pen WALL_SECT_PEN

	if gs_bFills then fill gs_wallhole_fill
	wallhole2 5, 2, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
		0, 0, 0,
		0, 						-posLeafy, 1,
		0, 						-posLeafy - widthWallCut, 1,
		PocketWidth + 0.005,	-posLeafy - widthWallCut, 1,
		PocketWidth + 0.005,	-posLeafy, 1,
		0,						-posLeafy, -1

	wallline2 -_ActReveal_width,	-posLeafy, PocketWidth + 0.005,	-posLeafy
	wallline2 PocketWidth + 0.005,	-posLeafy, PocketWidth + 0.005,	-posLeafy - widthWallCut
	wallline2 PocketWidth + 0.005,	-posLeafy - widthWallCut,	0,	-posLeafy - widthWallCut
endif

del 1

return


! =============================================================================
! Frame Symbol at Bottom
! =============================================================================

"frame symbol at bottom":

	fill gs_fillFr

	bottomSymbolPosLeftX	= -A/2 + gs_sl_w1
	bottomSymbolPosRightX	= A/2 - gs_sl_w2

	line2	bottomSymbolPosLeftX - gs_rebate_width * (gs_frame_style_m = 3) * (symb_2d = 50), 0,
			bottomSymbolPosRightX + gs_rebate_width * (gs_frame_style_m = 3) * (symb_2d = 50), 0
	line2 bottomSymbolPosLeftX, -outsideFrameThk, bottomSymbolPosRightX, -outsideFrameThk

return





