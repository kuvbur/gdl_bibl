

!bFixedPanelLeft	= (gs_optype_m_09 = SASH_FIXED)
!bFixedPanelRight	= (gs_optype_m_05 = SASH_FIXED)

PanelNumLeft = 1
if	gs_optype_m_09 = SASH_DOUBLE_SIDE_HUNG | \
	gs_optype_m_09 = SASH_SLIDING_HOR | \
	gs_optype_m_09 = SASH_SLIDING_HOR_M | \
	gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR | \
	gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR_M then PanelNumLeft = 2
PanelNumRight = 1
if	gs_optype_m_05 = SASH_DOUBLE_SIDE_HUNG | \
	gs_optype_m_05 = SASH_SLIDING_HOR | \
	gs_optype_m_05 = SASH_SLIDING_HOR_M | \
	gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR | \
	gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR_M then PanelNumRight = 2

bSlidingLeft = 	gs_optype_m_09 = SASH_SLIDING_HOR | \
				gs_optype_m_09 = SASH_SLIDING_HOR_M | \
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR | \
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR_M
bSlidingRight = gs_optype_m_05 = SASH_SLIDING_HOR | \
				gs_optype_m_05 = SASH_SLIDING_HOR_M | \
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR | \
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR_M

bMirroredLeft	=	gs_optype_m_09 = SASH_SLIDING_HOR_M | \
					gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR_M
bMirroredRight	=	gs_optype_m_05 = SASH_SLIDING_HOR_M | \
					gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR_M

gs_fillLeaf	= gs_fillLeaf * gs_bFills
gs_FillSdl = gs_FillSdl * gs_bFills


toleranceDiff = (gs_tolerance_right - gs_tolerance_left) / 2

bHotspotEdit2D=1

if gs_frame_wthk=0 then
	temp_frame_thk=-gs_frame_thk
else
	temp_frame_thk=-gs_frame_thkin
endif

pen gs_leaf_pen_2D

drawindex 40

lod2D					= DET2D_FS_OFF
bShowRebate				= 0
bShowRebateOnFixGlass	= 0
rebateWidth				= 0

if iLoD2D = SYMBD_S_1LINE then						lod2D	= DET2D_FS_SIMPLE			! Simple --> INT 200
if iLoD2D = SYMBD_S_1LINECL then					lod2D	= DET2D_FS_SIMPLE			! Simple --> NLD 200
if iLoD2D = SYMBD_M_1LINELEAF_2LINE then			lod2D	= DET2D_FS_MIDDLE			! Middle --> INT 100
if iLoD2D = SYMBD_M_2LINELEAFCL_GLASS then			lod2D	= DET2D_FS_DETAILED			! Middle --> NLD 100
if iLoD2D = SYMBD_D_2LINELEAFOP_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> INT 50
if iLoD2D = SYMBD_D_1LINELEAF_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> SWE 50
if iLoD2D = SYMBD_D_2LINELEAFCL_REBATEONGLASS then	lod2D	= DET2D_FS_DETAILED			! Detailed --> NLD 50
if iLoD2D = SYMBD_D_2LEAFOPGLASS_GLASS then			lod2D	= DET2D_FS_DETAILED			! Detailed --> DAN 20
if iLoD2D = SYMBD_S_2LINE then						lod2D	= DET2D_FS_SIMPLE_CHI		! Chinese Simple
if iLoD2D = SYMBD_D_2LINELEAFOP_2LINE then			lod2D 	= DET2D_FS_DETAILED_CHI		! Chinese Detailed

if iLoD2D = SYMBD_D_2LINELEAFOP_GLASS then												! Detailed --> INT 50
	bShowRebate	= 1
endif
if iLoD2D = SYMBD_D_2LINELEAFCL_REBATEONGLASS then										! Detailed --> NLD 50
	bShowRebate				= 1
	bShowRebateOnFixGlass	= 1
endif

if iTypeDemolitionFill = 2 then				! RENOVATION_FILL_DEMOLISHED
	line_type gs_Demolition_linetype
else
	line_type 1
endif

! ==============================================================================
! Left Sidelight
! ==============================================================================

if gs_sidelight_left then

	if lod2D = DET2D_FS_SIMPLE then		! 1:200
		line2 -ac_unit_width/2-gs_frame_ovhg_left_out, 0, -ac_unit_width/2+gs_sl_w1 - gs_frame_width_left_2,0
	endif

	if lod2D = DET2D_FS_MIDDLE then		! 1:100
		fill gs_FillSdl
		pen gs_frame_pen_cont

		poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
			-ac_unit_width/2+gs_frw1, 0,1,
			-ac_unit_width/2+gs_sl_w1-gs_frame_width_left_2, 0, 1,
			-ac_unit_width/2+gs_sl_w1-gs_frame_width_left_2, temp_frame_thk, 1,
			-ac_unit_width/2+gs_frw1, temp_frame_thk, 1
	endif

	if lod2D = DET2D_FS_DETAILED then		! 1:50

		rebateWidth = gs_sidelight_rebate_width * (bShowRebate | bShowRebateOnFixGlass)

		if bOpeningDirectionSL then
			add2 0,temp_frame_thk
			mul2 1,-1
		endif

		reb_mod_y = 0
		GlassWidth_VGrid2 = 0
		if gs_optype_m_09 = SASH_FIXED then
			if iMullionStyle_09 <> MULLION_SOLID_PANEL then
				reb_mod_y = gs_sl_thk - (bShowRebateOnFixGlass * gs_sidelight_rebate_depth)
				if iMullionStyle_09 <> MULLION_DIVIDED_FRAME then
					line_property 2
					line2 -ac_unit_width/2+gs_frw1, -gs_sidelight_rebate_depth, -ac_unit_width/2+gs_sl_w1-gs_frame_width_left_2, -gs_sidelight_rebate_depth
					line_property 0
				else
					rebateWidth = 0
				endif
			endif
		else
			if	gs_optype_m_09 = SASH_HOR_PIVOT | \
				gs_optype_m_09 = SASH_VER_PIVOT | \
				gs_optype_m_09 = SASH_VER_PIVOT_M | \
				gs_optype_m_09 = SASH_SLIDING_HOR | \
				gs_optype_m_09 = SASH_SLIDING_HOR_M | \
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR | \
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_HOR_M | \
				gs_optype_m_09 = SASH_SLIDING_VER | \
				gs_optype_m_09 = SASH_DOUBLE_SLIDING_VER then
				rebateWidth = 0

				! The Opening Sash is always inside by Single Hung and Double Hung Sash Type
				if gs_optype_m_09 = SASH_SLIDING_VER | gs_optype_m_09 = SASH_DOUBLE_SLIDING_VER then
					if not(bOpeningDirectionSL) exor WIDO_REVEAL_SIDE then reb_mod_y = gs_sl_thk
				endif
			else
				reb_mod_y = gs_sl_thk - gs_sidelight_rebate_depth
			endif
		endif

		! Change sash width size by mullion style
		if (iMullionStyle_09 = MULLION_FRENCH_PANEL) then
			_sashFrameWidthLeft		= sashFrameWidth[1][9]
			_sashFrameWidthRight	= sashFrameWidth[2][9]
		else
			_sashFrameWidthLeft		= gs_sl_fw
			_sashFrameWidthRight	= gs_sl_fw
		endif

		LeftTotalWidth	= gs_sl_w1 - gs_frw1 - gs_frame_width_left_2 + 2 * rebateWidth
		PanelWidth		= LeftTotalWidth
		if PanelNumLeft = 2 then
			if bSlidingLeft then
				PanelWidth		= LeftTotalWidth/2 + (min(_sashFrameWidthLeft, _sashFrameWidthRight))/2
			else
				PanelWidth		= LeftTotalWidth/2
			endif
		endif
		if vgn_09 > 0 then GlassWidth_VGrid2 = (LeftTotalWidth - (vgn_09 - 1) * gs_sidelight_div_width) / (vgn_09)

		add2 -ac_unit_width/2 + gs_frw1 - rebateWidth + LeftTotalWidth/2, 0
		if bMirroredLeft then mul2 -1, 1

		if gs_frame_thkin > gs_sl_thk then

			fill gs_FillSdl
			pen gs_frame_pen_cont

			if bSlidingLeft then
				poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
					 LeftTotalWidth/2 - PanelWidth,	0,			1,
					-LeftTotalWidth/2,				0,			0,
					-LeftTotalWidth/2,				-gs_sl_thk, 0,
					 LeftTotalWidth/2 - PanelWidth,	-gs_sl_thk, 0

				add2 0, -gs_sl_thk
				poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
					-LeftTotalWidth/2 + PanelWidth,	0, 												0,
					 LeftTotalWidth/2,				0, 												0,
					 LeftTotalWidth/2,				-1 * min(gs_sl_thk, -temp_frame_thk-gs_sl_thk), -temp_frame_thk <= 2*gs_sl_thk,
					-LeftTotalWidth/2 + PanelWidth,	-1 * min(gs_sl_thk, -temp_frame_thk-gs_sl_thk), 0

				if -temp_frame_thk > 2*gs_sl_thk then
					add2 0, -gs_sl_thk
					poly2_b 5, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
						 LeftTotalWidth/2,				0,				0,
						-LeftTotalWidth/2 + PanelWidth,	0,				0,
						-LeftTotalWidth/2,				0,				0,
						-LeftTotalWidth/2,	temp_frame_thk+2*gs_sl_thk, 1,
						 LeftTotalWidth/2,	temp_frame_thk+2*gs_sl_thk, 0
					del 1
				endif
				del 1
			else
				if iMullionStyle_09 <> MULLION_DIVIDED_FRAME then
					poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
						 LeftTotalWidth/2 - rebateWidth,	temp_frame_thk,			1,
						-LeftTotalWidth/2 + rebateWidth,	temp_frame_thk,			1,
						-LeftTotalWidth/2 + rebateWidth,	-gs_sl_thk+reb_mod_y,	1,
						 LeftTotalWidth/2 - rebateWidth,	-gs_sl_thk+reb_mod_y,	1

					if bShowRebateOnFixGlass & (gs_optype_m_09 = SASH_FIXED) & (iMullionStyle_09 <> MULLION_SOLID_PANEL) & (abs(-gs_sl_thk+reb_mod_y) > EPS) then
						poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
							 LeftTotalWidth/2,	0,			1,
							-LeftTotalWidth/2,	0,			1,
							-LeftTotalWidth/2,	-gs_sl_thk+reb_mod_y,	1,
							 LeftTotalWidth/2,	-gs_sl_thk+reb_mod_y,	1
					endif

				else
					_glassWidthOv		= gs_sidelight_rebate_width * (bShowRebateOnFixGlass)

					put	LeftTotalWidth/2, temp_frame_thk, 1
					put	LeftTotalWidth/2, -gs_sl_thk+reb_mod_y,	1

					if vgn_09 > 1 then
						for i = 0 to vgn_09-2
							put	LeftTotalWidth/2 - GlassWidth_VGrid2 - i * (GlassWidth_VGrid2 + gs_sidelight_div_width),						-gs_sl_thk + reb_mod_y,				1,
								LeftTotalWidth/2 - GlassWidth_VGrid2 - i * (GlassWidth_VGrid2 + gs_sidelight_div_width), 						-gs_sidelight_div_thk,	1,
								LeftTotalWidth/2 - GlassWidth_VGrid2 - i * (GlassWidth_VGrid2 + gs_sidelight_div_width) - gs_sidelight_div_width,	-gs_sidelight_div_thk,	1,
								LeftTotalWidth/2 - GlassWidth_VGrid2 - i * (GlassWidth_VGrid2 + gs_sidelight_div_width) - gs_sidelight_div_width,	-gs_sl_thk + reb_mod_y,				1
						next i

						if bShowRebateOnFixGlass & (gs_optype_m_09 = SASH_FIXED) & (iMullionStyle_09 <> MULLION_SOLID_PANEL) & (abs(-gs_sl_thk+reb_mod_y) > EPS) then
							add2 -LeftTotalWidth / 2 + GlassWidth_VGrid2 / 2, 0
								for i = 0 to vgn_09-1
									poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
										 GlassWidth_VGrid2/2 + _glassWidthOv,	0,						1,
										-GlassWidth_VGrid2/2 - _glassWidthOv,	0,						1,
										-GlassWidth_VGrid2/2 - _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1,
										 GlassWidth_VGrid2/2 + _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1

									add2 GlassWidth_VGrid2 + gs_sidelight_div_width, 0
								next i
								del vgn_09
							del 1
						endif
					else
						if bShowRebateOnFixGlass & (gs_optype_m_09 = SASH_FIXED) & (iMullionStyle_09 <> MULLION_SOLID_PANEL) & (abs(-gs_sl_thk+reb_mod_y) > EPS) then
							poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
								 LeftTotalWidth/2 + _glassWidthOv,	0,						1,
								-LeftTotalWidth/2 - _glassWidthOv,	0,						1,
								-LeftTotalWidth/2 - _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1,
								 LeftTotalWidth/2 + _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1
						endif
					endif

					put	-LeftTotalWidth/2, -gs_sl_thk+reb_mod_y,	1
					put	-LeftTotalWidth/2, temp_frame_thk, 1

					poly2_b nsp/3, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
					get(nsp)
				endif
				line2	 LeftTotalWidth/2 - rebateWidth, temp_frame_thk,
						-LeftTotalWidth/2 + rebateWidth, temp_frame_thk
			endif
		endif

		if gs_optype_m_09 <> SASH_FIXED then

			add2 0, reb_mod_y

			! The Opening Sash is always inside by Single Hung and Double Hung Sash Type
			if gs_optype_m_09 = SASH_SLIDING_VER | gs_optype_m_09 = SASH_DOUBLE_SLIDING_VER then
				 if not(bOpeningDirectionSL) exor WIDO_REVEAL_SIDE then
					add2 0,-2 * gs_sl_thk
				endif
			endif

			for i = 1 to PanelNumLeft
				if i = 2 then
					_sashFrameWidthLeft_temp	= _sashFrameWidthLeft
					_sashFrameWidthRight_temp	= _sashFrameWidthRight
					_sashFrameWidthLeft		= _sashFrameWidthRight_temp
					_sashFrameWidthRight	= _sashFrameWidthLeft_temp
				endif

				add2 LeftTotalWidth/2, 0

				if iMullionStyle_09 = MULLION_SOLID_PANEL then
					fill gs_fillLeaf
					pen gs_leaf_pen_2D
					poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
						-PanelWidth, 	0,			1,
						 0,				0,			1,
						 0,				-gs_sl_thk, 1,
						-PanelWidth, 	-gs_sl_thk, 1
				else
					fill gs_FillSdl
					pen gs_leaf_pen_2D

					poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
						-PanelWidth + _sashFrameWidthLeft,	 0,			1,
						-_sashFrameWidthRight,				 0,			1,
						-_sashFrameWidthRight, 				-gs_sl_thk, 1,
						-PanelWidth + _sashFrameWidthLeft,	-gs_sl_thk, 1

					fill gs_fillLeaf

					poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
						-_sashFrameWidthRight, 	 0,			1,
						 0,			 			 0,			1,
						 0,						-gs_sl_thk, 1,
						-_sashFrameWidthRight, 	-gs_sl_thk, 1

					poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
						-PanelWidth + _sashFrameWidthLeft,	 0,			1,
						-PanelWidth,						 0,			1,
						-PanelWidth,						-gs_sl_thk, 1,
						-PanelWidth + _sashFrameWidthLeft,	-gs_sl_thk, 1

					line_property 2
					line2	-_sashFrameWidthRight,	-gs_sl_thk/2, -PanelWidth + _sashFrameWidthLeft,	-gs_sl_thk/2
					line_property 0
				endif

				del 1

				if i = 2 then
					_sashFrameWidthLeft		= _sashFrameWidthLeft_temp
					_sashFrameWidthRight	= _sashFrameWidthRight_temp
				endif

				add2 0, -bSlidingLeft * gs_sl_thk
				mul2 -1, 1
			next i
			del PanelNumLeft*2
			if gs_optype_m_09 = SASH_SLIDING_VER | gs_optype_m_09 = SASH_DOUBLE_SLIDING_VER then
				if not(bOpeningDirectionSL) exor WIDO_REVEAL_SIDE then
					del 1
				endif
			endif
			del 1
		else
			if iMullionStyle_09 = MULLION_DIVIDED_FRAME then
				_glassWidthOv		= gs_sidelight_rebate_width * (bShowRebateOnFixGlass)

				add2 -LeftTotalWidth/2, 0
				for i = 1 to vgn_09
					line2 0, -gs_sidelight_rebate_depth, GlassWidth_VGrid2, -gs_sidelight_rebate_depth
					add2 GlassWidth_VGrid2 + gs_sidelight_div_width, 0
				next i
				del vgn_09

				pen gs_frame_pen_cont
				fill gs_fillFr

				for i = 1 to vgn_09-1
					if bShowRebateOnFixGlass then
						poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
							GlassWidth_VGrid2, 											-gs_sl_thk + reb_mod_y,	1,
							GlassWidth_VGrid2 + _glassWidthOv, 							-gs_sl_thk + reb_mod_y,	1,
							GlassWidth_VGrid2 + _glassWidthOv,							0,						1,
							GlassWidth_VGrid2 + gs_sidelight_div_width - _glassWidthOv,	0,						1,
							GlassWidth_VGrid2 + gs_sidelight_div_width - _glassWidthOv,	-gs_sl_thk + reb_mod_y,	1,
							GlassWidth_VGrid2 + gs_sidelight_div_width,					-gs_sl_thk + reb_mod_y,	1,
							GlassWidth_VGrid2 + gs_sidelight_div_width,					-gs_sidelight_div_thk,	1,
							GlassWidth_VGrid2, 											-gs_sidelight_div_thk,	1
					else
						poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
							GlassWidth_VGrid2, 							0,						1,
							GlassWidth_VGrid2 + gs_sidelight_div_width,	0,						1,
							GlassWidth_VGrid2 + gs_sidelight_div_width,	-gs_sidelight_div_thk,	1,
							GlassWidth_VGrid2, 							-gs_sidelight_div_thk,	1
					endif
					add2 GlassWidth_VGrid2 + gs_sidelight_div_width, 0
				next i
				del vgn_09-1
				del 1
			endif

			if iMullionStyle_09 = MULLION_SOLID_PANEL then
				fill gs_fillLeaf
				pen gs_leaf_pen_2D

				add2 -(gs_frw1 - gs_frame_width_left_2)/2,0
				poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
					-gs_sl_w1/2+gs_frw1,				0,			1,
					 gs_sl_w1/2-gs_frame_width_left_2,	0,			1,
					 gs_sl_w1/2-gs_frame_width_left_2,	-gs_sl_thk, 1,
					-gs_sl_w1/2+gs_frw1, 				-gs_sl_thk, 1
				del 1
			endif
		endif

		if bMirroredLeft then del 1
		del 1

		if bOpeningDirectionSL then del 2

	endif

	! --- Chinese -----------------------------------------------------------------
	if lod2D = DET2D_FS_SIMPLE_CHI | lod2D = DET2D_FS_DETAILED_CHI then

		fill gs_FillSdl
		pen gs_leaf_pen_2D

		if lod2D = DET2D_FS_SIMPLE_CHI then
			poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
				-ac_unit_width/2, 0,1,
				-ac_unit_width/2 + gs_sl_w1,0, 1,
				-ac_unit_width/2 + gs_sl_w1,temp_frame_thk, 1,
				-ac_unit_width/2, temp_frame_thk, 1
		else															!!!detailed CHI
			poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
				-ac_unit_width/2 + gs_frw1, 0,1,
				-ac_unit_width/2 + gs_sl_w1 - gs_frame_width_left_2, 0, 1,
				-ac_unit_width/2 + gs_sl_w1 - gs_frame_width_left_2, temp_frame_thk, 1,
				-ac_unit_width/2 + gs_frw1, temp_frame_thk, 1
		endif
	endif

	! --- Opening line in 2D-------------------------------------------------------
	__rightPos		= -ac_unit_width/2 + gs_sl_w1 - gs_frame_width_left_2 + rebateWidth
	__leftPos		= -ac_unit_width/2 + gs_frame_width_left - rebateWidth
	__gs_optype_m	= gs_optype_m_09
	__gs_open_2D	= gs_open_2D_sl_left
	__actSL			= 1		! Left
	unID = sh_UnID

	gosub "SidelightsOpLines"
endif


pen gs_leaf_pen_2D

! ==============================================================================
! Right Sidelight
! ==============================================================================

if gs_sidelight_right then

	if lod2D = DET2D_FS_SIMPLE then		! 1:200
		line2  ac_unit_width/2 + gs_frame_ovhg_right_out, 0,  ac_unit_width/2-gs_sl_w2 + gs_frame_width_right_2,0
	endif

	if lod2D = DET2D_FS_MIDDLE then		! 1:100
		fill gs_FillSdl
		pen gs_frame_pen_cont

		poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
			ac_unit_width/2-gs_frw2, 0,1,
			ac_unit_width/2-gs_sl_w2+gs_frame_width_right_2, 0, 1,
			ac_unit_width/2-gs_sl_w2+gs_frame_width_right_2, temp_frame_thk, 1,
			ac_unit_width/2-gs_frw2, temp_frame_thk, 1
	endif

	if lod2D = DET2D_FS_DETAILED then		! 1:50

		rebateWidth = gs_sidelight_rebate_width * (bShowRebate | bShowRebateOnFixGlass)

		if bOpeningDirectionSL then
			add2 0,temp_frame_thk
			mul2 1,-1
		endif

		reb_mod_y = 0
		GlassWidth_VGrid2 = 0
		if gs_optype_m_05 = SASH_FIXED then
			if iMullionStyle_05 <> MULLION_SOLID_PANEL then
				reb_mod_y = gs_sl_thk - (bShowRebateOnFixGlass * gs_sidelight_rebate_depth)
				if iMullionStyle_05 <> MULLION_DIVIDED_FRAME then
					line_property 2
					line2 ac_unit_width/2-gs_frw2, -gs_sidelight_rebate_depth, ac_unit_width/2-gs_sl_w2+gs_frame_width_right_2, -gs_sidelight_rebate_depth
					line_property 0
				else
					rebateWidth = 0
				endif
			endif
		else
			if	gs_optype_m_05 = SASH_HOR_PIVOT | \
				gs_optype_m_05 = SASH_VER_PIVOT | \
				gs_optype_m_05 = SASH_VER_PIVOT_M | \
				gs_optype_m_05 = SASH_SLIDING_HOR | \
				gs_optype_m_05 = SASH_SLIDING_HOR_M | \
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR | \
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_HOR_M | \
				gs_optype_m_05 = SASH_SLIDING_VER | \
				gs_optype_m_05 = SASH_DOUBLE_SLIDING_VER then
				rebateWidth = 0

				! The Opening Sash is always inside by Single Hung and Double Hung Sash Type
				if gs_optype_m_05 = SASH_SLIDING_VER | gs_optype_m_05 = SASH_DOUBLE_SLIDING_VER then
					if not(bOpeningDirectionSL) exor WIDO_REVEAL_SIDE then reb_mod_y = gs_sl_thk
				endif
			else
				reb_mod_y = gs_sl_thk - gs_sidelight_rebate_depth
			endif
		endif

		! Change sash width size by mullion style
		if (iMullionStyle_05 = MULLION_FRENCH_PANEL) then
			_sashFrameWidthLeft		= sashFrameWidth[1][5]
			_sashFrameWidthRight	= sashFrameWidth[2][5]
		else
			_sashFrameWidthLeft		= gs_sl_fw
			_sashFrameWidthRight	= gs_sl_fw
		endif

		RightTotalWidth	= gs_sl_w2 - gs_frw2 - gs_frame_width_right_2 + 2 * rebateWidth
		PanelWidth		= RightTotalWidth
		if PanelNumRight = 2 then
			if bSlidingRight then
				PanelWidth		= RightTotalWidth/2 + (min(_sashFrameWidthLeft, _sashFrameWidthRight))/2
			else
				PanelWidth		= RightTotalWidth/2
			endif
		endif
		if vgn_05 > 0 then GlassWidth_VGrid2 = (RightTotalWidth - (vgn_05 - 1) * gs_sidelight_div_width) / (vgn_05)

		add2 ac_unit_width/2 - gs_frw2 + rebateWidth - RightTotalWidth/2, 0
		if not(bMirroredRight) then mul2 -1, 1

		if gs_frame_thkin > gs_sl_thk then

			fill gs_FillSdl
			pen gs_frame_pen_cont

			if bSlidingRight then
				poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
					-RightTotalWidth/2 + PanelWidth,0, 			1,
					 RightTotalWidth/2,				0, 			0,
					 RightTotalWidth/2,				-gs_sl_thk, 0,
					-RightTotalWidth/2 + PanelWidth,-gs_sl_thk, 0

				add2 0, -gs_sl_thk
				poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
					 RightTotalWidth/2 - PanelWidth,0, 												0,
					-RightTotalWidth/2,				0, 												0,
					-RightTotalWidth/2,				-1 * min(gs_sl_thk, -temp_frame_thk-gs_sl_thk), -temp_frame_thk <= 2*gs_sl_thk,
					 RightTotalWidth/2 - PanelWidth,-1 * min(gs_sl_thk, -temp_frame_thk-gs_sl_thk), 0

				if -temp_frame_thk > 2*gs_sl_thk then
					add2 0, -gs_sl_thk
					poly2_b 5, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
						-RightTotalWidth/2,				0,							0,
						 RightTotalWidth/2 - PanelWidth,0,							0,
						 RightTotalWidth/2,				0,							0,
						 RightTotalWidth/2,				temp_frame_thk+2*gs_sl_thk, 1,
						-RightTotalWidth/2,				temp_frame_thk+2*gs_sl_thk, 0
					del 1
				endif
				del 1
			else
				if iMullionStyle_05 <> MULLION_DIVIDED_FRAME then
					poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
						-RightTotalWidth/2 + rebateWidth,	temp_frame_thk,			1,
						 RightTotalWidth/2 - rebateWidth,	temp_frame_thk,			1,
						 RightTotalWidth/2 - rebateWidth,	-gs_sl_thk+reb_mod_y,	1,
						-RightTotalWidth/2 + rebateWidth,	-gs_sl_thk+reb_mod_y,	1

					if bShowRebateOnFixGlass & (gs_optype_m_05 = SASH_FIXED) & (iMullionStyle_05 <> MULLION_SOLID_PANEL) & (abs(-gs_sl_thk+reb_mod_y) > EPS) then
						poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
							 RightTotalWidth/2,	0,			1,
							-RightTotalWidth/2,	0,			1,
							-RightTotalWidth/2,	-gs_sl_thk+reb_mod_y,	1,
							 RightTotalWidth/2,	-gs_sl_thk+reb_mod_y,	1
					endif
				else
					_glassWidthOv		= gs_sidelight_rebate_width * (bShowRebateOnFixGlass)

					put		-RightTotalWidth/2, temp_frame_thk, 1
					put		-RightTotalWidth/2, -gs_sl_thk+reb_mod_y,	1

					if vgn_05 > 1 then
						for i = 0 to vgn_05-2
							put	-RightTotalWidth/2 + GlassWidth_VGrid2 + i * (GlassWidth_VGrid2 + gs_sidelight_div_width),						-gs_sl_thk + reb_mod_y,	1,
								-RightTotalWidth/2 + GlassWidth_VGrid2 + i * (GlassWidth_VGrid2 + gs_sidelight_div_width), 						-gs_sidelight_div_thk,	1,
								-RightTotalWidth/2 + GlassWidth_VGrid2 + i * (GlassWidth_VGrid2 + gs_sidelight_div_width) + gs_sidelight_div_width,	-gs_sidelight_div_thk,	1,
								-RightTotalWidth/2 + GlassWidth_VGrid2 + i * (GlassWidth_VGrid2 + gs_sidelight_div_width) + gs_sidelight_div_width,	-gs_sl_thk + reb_mod_y,	1
						next i

						if bShowRebateOnFixGlass & (gs_optype_m_05 = SASH_FIXED) & (iMullionStyle_05 <> MULLION_SOLID_PANEL) & (abs(-gs_sl_thk+reb_mod_y) > EPS) then
							add2 -RightTotalWidth / 2 + GlassWidth_VGrid2 / 2, 0
								for i = 0 to vgn_05-1
									poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
										 GlassWidth_VGrid2/2 + _glassWidthOv,	0,						1,
										-GlassWidth_VGrid2/2 - _glassWidthOv,	0,						1,
										-GlassWidth_VGrid2/2 - _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1,
										 GlassWidth_VGrid2/2 + _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1

									add2 GlassWidth_VGrid2 + gs_sidelight_div_width, 0
								next i
								del vgn_05
							del 1
						endif
					else
						if bShowRebateOnFixGlass & (gs_optype_m_05 = SASH_FIXED) & (iMullionStyle_05 <> MULLION_SOLID_PANEL) & (abs(-gs_sl_thk+reb_mod_y) > EPS) then
							poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
								 RightTotalWidth/2 + _glassWidthOv,	0,						1,
								-RightTotalWidth/2 - _glassWidthOv,	0,						1,
								-RightTotalWidth/2 - _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1,
								 RightTotalWidth/2 + _glassWidthOv,	-gs_sl_thk+reb_mod_y,	1
						endif
					endif

					put		RightTotalWidth/2, -gs_sl_thk+reb_mod_y,	1
					put		RightTotalWidth/2, temp_frame_thk, 1

					poly2_b nsp/3, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
					get(nsp)
				endif
				line2	-RightTotalWidth/2 + rebateWidth, temp_frame_thk,
						 RightTotalWidth/2 - rebateWidth,	temp_frame_thk
			endif
		endif

		if gs_optype_m_05 <> SASH_FIXED then

			add2 0, reb_mod_y

			! The Opening Sash is always inside by Single Hung and Double Hung Sash Type
			if gs_optype_m_05 = SASH_SLIDING_VER | gs_optype_m_05 = SASH_DOUBLE_SLIDING_VER then
				 if not(bOpeningDirectionSL) exor WIDO_REVEAL_SIDE then
					add2 0,-2*gs_sl_thk
				endif
			endif

			for i = 1 to PanelNumRight
				if i = 2 then
					_sashFrameWidthLeft_temp	= _sashFrameWidthLeft
					_sashFrameWidthRight_temp	= _sashFrameWidthRight
					_sashFrameWidthLeft		= _sashFrameWidthRight_temp
					_sashFrameWidthRight	= _sashFrameWidthLeft_temp
				endif

				add2 -RightTotalWidth/2, 0

				if iMullionStyle_05 = MULLION_SOLID_PANEL then
					fill gs_fillLeaf
					pen gs_leaf_pen_2D
					poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
						PanelWidth, 	0,			1,
						0,				0,			1,
						0,				-gs_sl_thk, 1,
						PanelWidth, 	-gs_sl_thk, 1
				else
					fill gs_FillSdl
					pen gs_leaf_pen_2D

					poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
						PanelWidth - _sashFrameWidthLeft,	0,			1,
						_sashFrameWidthRight,				0,			0,
						_sashFrameWidthRight, 				-gs_sl_thk, 1,
						PanelWidth - _sashFrameWidthLeft,	-gs_sl_thk, 0

					fill gs_fillLeaf

					poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
						_sashFrameWidthRight, 	0,			1,
						0,						0,			1,
						0,						-gs_sl_thk, 1,
						_sashFrameWidthRight, 	-gs_sl_thk, 1

					poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
						PanelWidth - _sashFrameWidthLeft,	0,			1,
						PanelWidth,							0, 			1,
						PanelWidth,							-gs_sl_thk, 1,
						PanelWidth - _sashFrameWidthLeft,	-gs_sl_thk, 1

					line_property 2
					line2	_sashFrameWidthRight,	-gs_sl_thk/2, PanelWidth - _sashFrameWidthLeft,	-gs_sl_thk/2
					line_property 0
				endif

				del 1

				if i = 2 then
					_sashFrameWidthLeft		= _sashFrameWidthLeft_temp
					_sashFrameWidthRight	= _sashFrameWidthRight_temp
				endif

				add2 0, -bSlidingRight * gs_sl_thk
				mul2 -1, 1
			next i
			del PanelNumRight*2
			if gs_optype_m_05 = SASH_SLIDING_VER | gs_optype_m_05 = SASH_DOUBLE_SLIDING_VER then
				if not(bOpeningDirectionSL) exor WIDO_REVEAL_SIDE then
					del 1
				endif
			endif
			del 1
		else
			if iMullionStyle_05 = MULLION_DIVIDED_FRAME then
				_glassWidthOv		= gs_sidelight_rebate_width * (bShowRebateOnFixGlass)

				add2 RightTotalWidth/2, 0
				for i = 1 to vgn_05
					line2 0, -gs_sidelight_rebate_depth, -GlassWidth_VGrid2, -gs_sidelight_rebate_depth
					add2 -GlassWidth_VGrid2 - gs_sidelight_div_width, 0
				next i
				del vgn_05

				pen gs_frame_pen_cont
				fill gs_fillFr

				for i = 1 to vgn_05-1
					if bShowRebateOnFixGlass then
						poly2_b 8, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
							-GlassWidth_VGrid2, 											-gs_sl_thk + reb_mod_y,	1,
							-GlassWidth_VGrid2 - _glassWidthOv, 							-gs_sl_thk + reb_mod_y,	1,
							-GlassWidth_VGrid2 - _glassWidthOv,								0,						1,
							-GlassWidth_VGrid2 - gs_sidelight_div_width + _glassWidthOv,	0,						1,
							-GlassWidth_VGrid2 - gs_sidelight_div_width + _glassWidthOv,	-gs_sl_thk + reb_mod_y,	1,
							-GlassWidth_VGrid2 - gs_sidelight_div_width,					-gs_sl_thk + reb_mod_y,	1,
							-GlassWidth_VGrid2 - gs_sidelight_div_width,					-gs_sidelight_div_thk,	1,
							-GlassWidth_VGrid2, 											-gs_sidelight_div_thk,	1
					else
						poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
							-GlassWidth_VGrid2, 							0,						1,
							-GlassWidth_VGrid2 - gs_sidelight_div_width,	0,						1,
							-GlassWidth_VGrid2 - gs_sidelight_div_width,	-gs_sidelight_div_thk,	1,
							-GlassWidth_VGrid2, 							-gs_sidelight_div_thk,	1
					endif

					add2 -GlassWidth_VGrid2 - gs_sidelight_div_width, 0
				next i
				del vgn_05-1
				del 1
			endif

			if iMullionStyle_05 = MULLION_SOLID_PANEL then
				fill gs_fillLeaf
				pen gs_leaf_pen_2D

				add2 -(gs_frw2-gs_frame_width_right_2)/2,0
				poly2_b 4, 1+2+4+32, gs_penLeaf_fg, gs_penLeaf_bg,
					-gs_sl_w2/2+gs_frw2,				0,			1,
					 gs_sl_w2/2-gs_frame_width_right_2,	0,			1,
					 gs_sl_w2/2-gs_frame_width_right_2,	-gs_sl_thk, 1,
					-gs_sl_w2/2+gs_frw2, 				-gs_sl_thk, 1
				del 1
			endif
		endif

		if not(bMirroredRight) then del 1
		del 1

		if bOpeningDirectionSL then del 2

	endif

	! --- Chinese -----------------------------------------------------------------
	if lod2D = DET2D_FS_SIMPLE_CHI | lod2D = DET2D_FS_DETAILED_CHI then

		fill gs_FillSdl
		pen gs_leaf_pen_2D

		if lod2D = DET2D_FS_SIMPLE_CHI then
			poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
				ac_unit_width/2, 0,1,
				ac_unit_width/2 - gs_sl_w2,0, 1,
				ac_unit_width/2 - gs_sl_w2,temp_frame_thk, 1,
				ac_unit_width/2, temp_frame_thk, 1
		else															!!!detailed CHI
			poly2_b 4, 1+2+4+64, gs_penSdl_fg, gs_penSdl_bg,
				ac_unit_width/2 - gs_frw2, 0,1,
				ac_unit_width/2 - gs_sl_w2 + gs_frame_width_right_2, 0, 1,
				ac_unit_width/2 - gs_sl_w2 + gs_frame_width_right_2, temp_frame_thk, 1,
				ac_unit_width/2 - gs_frw2, temp_frame_thk, 1
		endif
	endif


	! --- Opening line in 2D-------------------------------------------------------
	__rightPos		= ac_unit_width/2 - gs_frame_width_right + rebateWidth
	__leftPos		= ac_unit_width/2 - gs_sl_w2 + gs_frame_width_right_2 - rebateWidth
	__gs_optype_m	= gs_optype_m_05
	__gs_open_2D	= gs_open_2D_sl_right
	__actSL			= 2		! Right
	unID = sh_UnID + 50

	gosub "SidelightsOpLines"
endif


! ------------------------------------------------------------------------------
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ------------------------------------------------------------------------------

"SidelightsOpLines":

	pen gs_winOpLinePen

	__sash_thk_oh = 0
	if lod2D = DET2D_FS_DETAILED then
		__sash_thk_oh = gs_sidelight_rebate_depth - gs_sl_thk
	endif

	__gs_frame_thk = 0
	if lod2D <> DET2D_FS_SIMPLE then
		__gs_frame_thk = gs_frame_thk
	endif

	if not(bOpeningDirectionSL) then
		add2 0,-__gs_frame_thk
		mul2 1,-1
	endif

	rev_A = not(bOpeningDirectionSL) * 512
	rev_B = bOpeningDirectionSL * 512

	if	__gs_optype_m = SASH_SIDE_HUNG |\
		__gs_optype_m = SASH_SIDE_HUNG_M |\
		__gs_optype_m = SASH_TILT_TURN |\
		__gs_optype_m = SASH_TILT_TURN_M then

		if	__gs_optype_m = SASH_SIDE_HUNG |\
			__gs_optype_m = SASH_TILT_TURN then

			if abs(__gs_open_2D) > EPS & abs(__gs_open_2D - 360) > EPS then
				line2	__rightPos, -__gs_frame_thk+__sash_thk_oh,
							__rightPos-(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh

				line_type gs_winSwingLineType

				if gs_iWinSwingType = 1 then		! Curved
					arc2	__rightPos, -__gs_frame_thk+__sash_thk_oh, ABS(__leftPos-__rightPos), 180, 180+__gs_open_2D
				else
					line2	__leftPos, -__gs_frame_thk+__sash_thk_oh,
							__rightPos-(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh
				endif
			endif

			! Hotspots
			add2 0, -__gs_frame_thk + __sash_thk_oh

			if __actSL = 1 then		! Left sidelight
				hotspot2 __rightPos,	0, unID,		gs_open_2D_sl_left, 6 + rev_A	! Center
				hotspot2 __leftPos,		0, unID + 1,	gs_open_2D_sl_left, 4+128		! Base
				hotspot2 __rightPos-(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_left,5						! Moving
			else
				hotspot2 __rightPos,	0, unID,		gs_open_2D_sl_right, 6 + rev_A	! Center
				hotspot2 __leftPos,		0, unID + 1,	gs_open_2D_sl_right, 4+128		! Base
				hotspot2 __rightPos-(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_right,5						! Moving
			endif

			unID = unID + 3
			del 1

		else
			if abs(__gs_open_2D) > EPS & abs(__gs_open_2D - 360) > EPS then
				line2	__leftPos, -__gs_frame_thk+__sash_thk_oh,
							__leftPos+(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh

				line_type gs_winSwingLineType

				if gs_iWinSwingType = 1 then		! Curved
					arc2	__leftPos, -__gs_frame_thk+__sash_thk_oh, abs(__leftPos-__rightPos), -__gs_open_2D, 0
				else
					line2	__rightPos, -__gs_frame_thk+__sash_thk_oh,
							__leftPos+(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh
				endif
			endif

			! Hotspots
			add2 0, -__gs_frame_thk + __sash_thk_oh

			if __actSL = 1 then		! Left sidelight
				hotspot2 __leftPos,		0, unID,		gs_open_2D_sl_left, 6 + rev_B	! Center
				hotspot2 __rightPos,	0, unID + 1,	gs_open_2D_sl_left, 4+128		! Base
				hotspot2 __leftPos+(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_left,5						! Moving
			else
				hotspot2 __leftPos,		0, unID,		gs_open_2D_sl_right, 6 + rev_B	! Center
				hotspot2 __rightPos,	0, unID + 1,	gs_open_2D_sl_right, 4+128		! Base
				hotspot2 __leftPos+(abs(__leftPos-__rightPos))*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos))*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_right,5						! Moving
			endif

			unID = unID + 3
			del 1
		endif
	endif

	if	__gs_optype_m = SASH_VER_PIVOT |\
		__gs_optype_m = SASH_VER_PIVOT_M then

		add2 (__leftPos + __rightPos)/2,0

		if __gs_optype_m = SASH_VER_PIVOT then
			if abs(__gs_open_2D) > EPS & abs(__gs_open_2D - 360) > EPS then
				line2	-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						 (abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk,
						 (abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk

				line_type gs_winSwingLineType

				if gs_iWinSwingType = 1 then		! Curved
					if __gs_open_2D > ASN(gs_sash_thk / __rightPos) * bFullSashOpeningIn2D then
						arc2	0, -__gs_frame_thk, abs(__leftPos-__rightPos)/2, -__gs_open_2D + ASN(gs_sash_thk / __rightPos) * bFullSashOpeningIn2D, 0
					endif
					arc2	0, -__gs_frame_thk, abs(__leftPos-__rightPos)/2, 180-__gs_open_2D, 180
				else							! Straight
					line2	-(abs(__leftPos-__rightPos)/2), -__gs_frame_thk,
							-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							 (abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk
					line2	abs(__leftPos-__rightPos)/2, -__gs_frame_thk,
							 (abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk
				endif
			endif

			! Hotspots
			add2 0, -__gs_frame_thk

			if __actSL = 1 then		! Left sidelight
				hotspot2 0,				0, unID,		gs_open_2D_sl_left, 6 + rev_B	! Center
				hotspot2 abs(__leftPos-__rightPos)/2,	0, unID + 1,	gs_open_2D_sl_left, 4+128		! Base
				hotspot2 (abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_left,5						! Moving
			else
				hotspot2 0,				0, unID,		gs_open_2D_sl_right, 6 + rev_B	! Center
				hotspot2 abs(__leftPos-__rightPos)/2,	0, unID + 1,	gs_open_2D_sl_right, 4+128		! Base
				hotspot2 (abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_right,5						! Moving
			endif

			unID = unID + 3
			del 1
		else
			if abs(__gs_open_2D) > EPS & abs(__gs_open_2D - 360) > EPS then
				line2	 (abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						 (abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk,
						-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk

				line_type gs_winSwingLineType

				if gs_iWinSwingType = 1 then		! Curved
					if __gs_open_2D > ASN(gs_sash_thk / __rightPos) * bFullSashOpeningIn2D then
						arc2	0, -__gs_frame_thk, abs(__leftPos-__rightPos)/2, 180, 180+__gs_open_2D - ASN(gs_sash_thk / __rightPos) * bFullSashOpeningIn2D
					endif
					arc2	0, -__gs_frame_thk, abs(__leftPos-__rightPos)/2, 0, __gs_open_2D
				else							! Straight
					line2	(abs(__leftPos-__rightPos)/2), -__gs_frame_thk,
							(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							 (abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk
					line2	-abs(__leftPos-__rightPos)/2, -__gs_frame_thk,
							-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk
				endif
			endif

			! Hotspots
			add2 0, -__gs_frame_thk

			if __actSL = 1 then		! Left sidelight
				hotspot2 0,				0, unID,		gs_open_2D_sl_left, 6 + rev_A	! Center
				hotspot2 -(abs(__leftPos-__rightPos)/2),		0, unID + 1,	gs_open_2D_sl_left, 4+128		! Base
				hotspot2 -(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_left,5						! Moving
			else
				hotspot2 0,				0, unID,		gs_open_2D_sl_right, 6 + rev_A	! Center
				hotspot2 -(abs(__leftPos-__rightPos)/2),		0, unID + 1,	gs_open_2D_sl_right, 4+128		! Base
				hotspot2 -(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
							-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D),
							unID + 2, gs_open_2D_sl_right,5						! Moving
			endif

			unID = unID + 3
			del 1
		endif

		del 1
	endif

	if	__gs_optype_m = SASH_DOUBLE_SIDE_HUNG | \
		__gs_optype_m = SASH_DOUBLE_TILT_TURN | \
		__gs_optype_m = SASH_DOUBLE_TILT_TURN_M then

		if abs(__gs_open_2D) > EPS & abs(__gs_open_2D - 360) > EPS then
			line2	__leftPos, -__gs_frame_thk+__sash_thk_oh,
						__leftPos+(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh
			line2	__rightPos, -__gs_frame_thk+__sash_thk_oh,
						__rightPos-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh

			line_type gs_winSwingLineType

			if gs_iWinSwingType = 1 then		! Curved
				arc2	__rightPos, -__gs_frame_thk+__sash_thk_oh, ABS(__leftPos-__rightPos)/2, 180, 180+__gs_open_2D
				arc2	__leftPos, -__gs_frame_thk+__sash_thk_oh, ABS(__leftPos-__rightPos)/2, -__gs_open_2D, 0

			else
				line2	__leftPos-(__leftPos-__rightPos)/2, -__gs_frame_thk+__sash_thk_oh,
						__rightPos-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh
				line2	__leftPos-(__leftPos-__rightPos)/2, -__gs_frame_thk+__sash_thk_oh,
						__leftPos+(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D)-__gs_frame_thk + __sash_thk_oh
			endif
		endif

		! Hotspots
		add2 0, -__gs_frame_thk + __sash_thk_oh

		if __actSL = 1 then		! Left sidelight
			hotspot2 __rightPos,	0, unID,		gs_open_2D_sl_left, 6 + rev_A	! Center
			hotspot2 __leftPos,		0, unID + 1,	gs_open_2D_sl_left, 4+128		! Base
			hotspot2 __rightPos-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D),
						unID + 2, gs_open_2D_sl_left,5						! Moving
		else
			hotspot2 __rightPos,	0, unID,		gs_open_2D_sl_right, 6 + rev_A	! Center
			hotspot2 __leftPos,		0, unID + 1,	gs_open_2D_sl_right, 4+128		! Base
			hotspot2 __rightPos-(abs(__leftPos-__rightPos)/2)*cos(__gs_open_2D),
						-(abs(__leftPos-__rightPos)/2)*sin(__gs_open_2D),
						unID + 2, gs_open_2D_sl_right,5						! Moving
		endif
	endif

	if not(bOpeningDirectionSL) then del 2

	if iTypeDemolitionFill = 2 then				! RENOVATION_FILL_DEMOLISHED
		line_type gs_Demolition_linetype
	else
		line_type 1
	endif
return
