
if GLOB_CONTEXT = 6 then	! Window Schedule - 3D view should NOT display sash in open position
	gs_open_3D = 0
	gs_open = 0
	gs_open3D = 0
endif

bMiddleLine = (bMiddleLine & abs(gs_open_3d) < EPS)

pen gs_leaf_pen

! --- Chinese specific ---

glas_mask5 = 5

if gs_detlevel_3D_m = 5 then
	gs_leaf_thk = chi_sash_thk
	gs_glass_thk = chi_sash_thk
	glas_mask5 = 13
	gs_grid_thk = chi_sash_thk
	gz = 0

endif



if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 or gs_detlevel_3D_m=1 then
!! for Simple Model and in 3D Windoow
	res=4
else
	res=res
endif

if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then gs_detlevel_3D_m=1

resol res

if gs_fillLeaf < 0.5 then gs_fillLeaf = SYMB_FILL
if gs_penLeaf_bg < 0.5 then gs_penLeaf_bg = SYMB_FBGD_PEN
if gs_penLeaf_fg < 0.5 then gs_penLeaf_fg = SYMB_FILL_PEN

sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

!*********************************** inside/outside materials - opening direction
pos_def = 1
if WIDO_REVEAL_SIDE then pos_def = 0


if pos_def=1 then

	!*** default placement

	topmat=gs_leaf_matin
	botmat=gs_leaf_mat1
	sidemat=gs_leaf_matin
	sidematedge=gs_leaf_matin

else

	!*** flip

	topmat=gs_leaf_mat1
	botmat=gs_leaf_matin
	sidemat=gs_leaf_mat1
	sidematedge=gs_leaf_matin

endif

if gs_leaf_mat1 = gs_leaf_matin then
	!*** same materials inside / outside
	nn=1
	framethick=gs_leaf_thk
	mmask=15
else
	!*** different materials inside / outside
	nn=2
	framethick=gs_leaf_thk/2
	mmask=14
endif


!! Chinese sash swing in 3D section !!!=============================

if gs_detlevel_3D_m = 5 then	!!chinese

	if gar_chi_cont = 1 then			!!!Type 1

		D = int(b/(b*0.1))

		for k = 1 to d
			lin_ -a/2,0,0,a/2,0,0
			addy b*0.1
		next k
		del d


		lin_ -a/2,0,0,0,b/1.5,0
		mulx -1
		lin_ -a/2,0,0,0,b/1.5,0
		del 1
		lin_ -a/2,b/1.5,0,0,b,0
		mulx -1
		lin_ -a/2,b/1.5,0,0,b,0
		del 1

		lin_ -a*0.5,b,0,-a*0.5,b*0.8,-b*0.3
		lin_ a*0.5,b,0,a*0.5,b*0.8,-b*0.3
		lin_ -a*0.5,b*0.8,-b*0.3, -a*0.5,b*0.5,b*0.3
		lin_ a*0.5,b*0.8,-b*0.3, a*0.5,b*0.5,b*0.3

	endif


	if gar_chi_cont = 2 then			!!!Type 2

		D = int(b/(b*0.1))

		for k = 1 to d
			lin_ -a/2,0,0,a/2,0,0
			addy b*0.1
		next k
		del d

		lin_ 0,b/3,0,0,b/1.5,0

	endif


	gs_detlevel_3D_m=1				!!!Simple
endif


unID = 1

if gar_chi_cont = 3 then
	if GLOB_CONTEXT=5 then
		mulz -1
	endif
endif

if gs_mov_typ=1 or gs_mov_typ=2 then

	! Flip Corrections because of panel orientation
	mulz -1
	nFlipTrans = 1

	if gs_mov_typ = 1 then
		addz -gs_frame_thk + gs_leaf_thk
		nFlipTrans = nFlipTrans + 1
	endif

	!!!!!!!!!!! HOTSPOT EDITING !!!!!!!!!!!

	if gs_full_edit & iDoorPanelType <> 0 & GLOB_CONTEXT <> 4 & GLOB_CONTEXT <> 6 then
		hotspot 0,	0,			0,	unID,	gs_open3D,	1+128	: unID = unID + 1
		hotspot 0,	-1,			0,	unID,	gs_open3D,	3		: unID = unID + 1
		hotspot 0,	gs_open3D,	0,	unID,	gs_open3D,	2		: unID = unID + 1
	endif

	addy gs_open3D
	nFlipTrans = nFlipTrans + 1

	if gs_mov_typ = 1 then
		rotx acs((b-gs_open3D)/b)
		nFlipTrans = nFlipTrans + 1
	endif

	addx -a/2
	nFlipTrans = nFlipTrans + 1

	if iDoorPanelType<>0 & bOplines3d then
		addz -0.001

		if gs_mov_typ=1 then
			iOpeningType		= 8				! SASH_TOP_HUNG
		else
			iOpeningType		= 14			! SASH_UP_SLIDING
		endif

		call "OpeningLines" parameters a = a,
					b					= b,
					frameThickness		= gs_leaf_thk+0.002,
					iOpeningType		= iOpeningType,
					opLineType3dIn		= opLineType3dIn,
					opLineType3dOut		= opLineType3dOut,
					gs_opLinePen		= gs_opLinePen,
					gs_opline_style_m	= gs_opline_style_m

		del 1

	endif

	if iDoorPanelType=0 then
		del nFlipTrans
		nFlipTrans = 0
		end
	else
		del 1
		nFlipTrans = nFlipTrans - 1

		! Flip Corrections for Handle - always on the outside
		if not(WIDO_REVEAL_SIDE) then
			mulz -1
			addz -gs_leaf_thk
			nFlipTrans = nFlipTrans + 2
		endif

		if bINTHandles then
			add 0,gs_han_height,0
			mulz -1
			call "DoorHandle" parameters A=A,
				bDifferentHandles		= 0,
				bSecondHandle			= 0,
				gs_handle_type_m		= gs_handle_typ_m,
				gs_cust_handle			= gs_cust_handle,
				gs_leaf_thk				= gs_leaf_thk,
				gs_handle_mat			= gs_handle_mat,
				gs_handle_ac_mat		= gs_handle_ac_mat,
				gs_han_pos				= 0,
				gs_han_height			= gs_han_height,
				horizontalBarLength		= 0.3,
				horizontalBarPos		= -0.3/2,
				verticalBarLength		= 0.3,
				verticalBarHeight		= gs_han_height - 0.3/2
			del 2
		else
			reqBHandles = 0
			reqBHandlesTemp = 0
			success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBDoorHandles", reqBHandlesTemp)
			if success > 0 then
				reqBHandles = reqBHandlesTemp
			endif
			if not(reqBHandles) then
				add 0,gs_han_height,0
				mulx -1
				mulz -1
				if gs_handle_typ_m=1 then
					call "da_handletyp" parameters gs_handle_typ_m=10, res=res, gs_shadow=gs_shadow,
											gs_handle_mat=gs_handle_mat, gs_handle_ac_mat=gs_handle_ac_mat,
											gs_detlevel_3D_m=gs_detlevel_3D_m
				endif

				if gs_handle_typ_m=2 then
					addx -0.15
					call "da_handletyp" parameters gs_handle_typ_m=14, res=res, gs_shadow=gs_shadow,
											gs_handle_mat=gs_handle_mat, gs_handle_ac_mat=gs_handle_ac_mat,
											gs_detlevel_3D_m=gs_detlevel_3D_m
					del 1
				endif

				if gs_handle_typ_m=3 then
					call "da_handletyp" parameters gs_handle_typ_m=15, res=res, gs_shadow=gs_shadow,
											gs_handle_mat=gs_handle_mat, gs_handle_ac_mat=gs_handle_ac_mat,
											gs_detlevel_3D_m=gs_detlevel_3D_m
				endif

				if gs_handle_typ_m = 99 then
					if gs_cust_handle<>"" then
						call "DW_Hardware_Collection" parameters gs_ptype=gs_cust_handle
					endif
				endif
				del 3
			endif
		endif


		! =============================================================================
		! Kick Board
		! =============================================================================
		if bEnableKickBoard then
			addz gs_leaf_thk / 2

			gosub "KickBoard"

			del 1
		endif


		! Flip Corrections for Handle - always on the outside
		if not(WIDO_REVEAL_SIDE) then
			del 2
			nFlipTrans = nFlipTrans - 2
		endif

		if gs_detlevel_3D_m=1 then
			gosub 130
			end
		endif
	endif

	! Flip Corrections for the Panel Material - always on the outside
	if not(WIDO_REVEAL_SIDE) then
		mulz -1
		addz -gs_leaf_thk
		nFlipTrans = nFlipTrans + 2
	endif

	if iDoorPanelType <> 0 then
		if _bSimplePanel then
			gosub 130
		else
			gosub iDoorPanelType * 10
		endif
	endif

	del nFlipTrans
	end

endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

if gs_mov_typ=3 then

	addx -a/2

	!!!!!!!!!!! HOTSPOT EDITING !!!!!!!!!!!

	if gs_full_edit & iDoorPanelType <> 0 & GLOB_CONTEXT <> 4 & GLOB_CONTEXT <> 6 then
		hotspot a,					gs_han_height,	0,					unID,	gs_open_3D,	4+128	: unID = unID + 1
		hotspot a*cos(gs_open_3D),	gs_han_height,	-a*sin(gs_open_3D),	unID,	gs_open_3D,	5		: unID = unID + 1
		hotspot 0,					gs_han_height,	0,					unID,	gs_open_3D,	6		: unID = unID + 1
		hotspot 0,				0,				0,					unID,	gs_open_3D,	7		: unID = unID + 1
	endif

	roty gs_open_3D
	if iDoorPanelType<>0 & bOplines3d then
		add a, 0, -0.001
		mulx -1

		call "OpeningLines" parameters a = a,
					b					= b,
					frameThickness		= gs_leaf_thk+0.002,
					iOpeningType		= 3,				! SASH_SIDE_HUNG
					opLineType3dIn		= opLineType3dIn,
					opLineType3dOut		= opLineType3dOut,
					gs_opLinePen		= gs_opLinePen,
					gs_opline_style_m	= gs_opline_style_m

		del 2
	endif

	if bINTHandles then
		addx a/2

		add a/2-gs_han_pos,gs_han_height,0
		mul -1, 1, -1
		call "DoorHandle" parameters A=A,
			bDifferentHandles		= 0,
			bSecondHandle			= 1,
			gs_handle_type_m		= gs_handle_typ_m,
			gs_cust_handle			= gs_cust_handle,
			gs_leaf_thk				= gs_leaf_thk,
			gs_handle_mat			= gs_handle_mat,
			gs_handle_ac_mat		= gs_handle_ac_mat,
			gs_han_pos				= gs_han_pos,
			gs_han_height			= gs_han_height
		del 2
	else
		reqBHandles = 0
		reqBHandlesTemp = 0
		success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBDoorHandles", reqBHandlesTemp)
		if success > 0 then
			reqBHandles = reqBHandlesTemp
		endif
		if iDoorPanelType=0 then
			del 2
			end
		else
			addx a/2
			add a/2-gs_han_pos,gs_han_height,gs_leaf_thk
			mulx -1
			if not(reqBHandles) then
				for t=1 to 2
					if gs_handle_typ_m = 99 then
						if gs_cust_handle<>"" then
							call "DW_Hardware_Collection" parameters gs_ptype=gs_cust_handle
						endif
					else
						call "da_handletyp" parameters gs_handle_typ_m=gs_handle_typ_m, res=res,
												gs_shadow=gs_shadow, gs_handle_mat=gs_handle_mat,
												gs_handle_ac_mat=gs_handle_ac_mat,
												gs_detlevel_3D_m=gs_detlevel_3D_m
					endif
					mulz -1
					addz gs_leaf_thk
				next t
				del 4
			endif
			del 2
		endif
	endif

	! =============================================================================
	! Kick Board
	! =============================================================================
	if bEnableKickBoard then
		addz gs_leaf_thk / 2

		if WIDO_REVEAL_SIDE then mulz -1

		gosub "KickBoard"

		if WIDO_REVEAL_SIDE then del 1

		del 1
	endif

	if gs_detlevel_3D_m=1 then
		gosub 130
		end
	endif

	if WIDO_REVEAL_SIDE then
		mulz -1
		addz -gs_leaf_thk
	endif
	if iDoorPanelType<>0 then gosub iDoorPanelType*10
	if WIDO_REVEAL_SIDE then
		del 2
	endif
	del 3
	end
endif



10:
!!!!!******************** Garage 1 !!!!!

a=a+2*(0.08)
b=b+0.08

pen gs_leaf_pen
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
n=int((b-0.07)/0.1)
dx=(b-0.07)/n

base

for i=1 to n

	prism 4,gs_leaf_thk/3,
		-a/2+0.08,0,
		-a/2+0.08,dx-0.01,
		a/2-0.08,dx-0.01,
		a/2-0.08,0

	addy dx
next i
del n

gosub 600

addz gs_leaf_thk/3
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

base

cprism_ gs_leaf_matin, gs_leaf_mat1, gs_leaf_matin,
	5,gs_leaf_thk*2/3,
	-a/2+0.08,0,15,
	-a/2+0.08,b-0.08,15,
	a/2-0.08,b-0.08,15,
	a/2-0.08,0,15,
	-a/2+0.08,0,15

gosub 500

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.08, gs_leaf_thk
	for i=1 to n
		lin_ a/2-0.08, 0, 0, a/2-0.08, dx-0.01, 0
		if i < n then lin_ a/2-0.08, dx-0.01, gs_leaf_thk/3, a/2-0.08, dx, gs_leaf_thk/3
		addy dx
	next i
	del n
endif

RETURN

20:
!!!!!******************** Garage 2 !!!!!

a=a+2*(0.08)
b=b+0.08

pen gs_leaf_pen
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
n=int((a-0.16)/0.1)
dx=(a-0.14)/n

base

for i=1 to n

	prism 4,gs_leaf_thk/3,
		-a/2+0.08,0,
		-a/2+0.08,b-0.08,
		-a/2+0.06+dx,b-0.08,
		-a/2+0.06+dx,0
	addx dx

next i

del n

gosub 500

addz gs_leaf_thk/3
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

base

cprism_ gs_leaf_matin, gs_leaf_mat1, gs_leaf_matin,
	5,gs_leaf_thk*2/3,
	-a/2+0.08,0,15,
	-a/2+0.08,b-0.08,15,
	a/2-0.08,b-0.08,15,
	a/2-0.08,0,15,
	-a/2+0.08,0,15

gosub 600

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.08, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.08, gs_leaf_thk
endif

RETURN

30:
!!!!!******************** Garage 3 !!!!!

a=a+2*(0.08)
b=b+0.08

pen gs_leaf_pen
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
dx=(a-0.66)/4
dy=(b-0.58)/2.5


if gs_SashText_typ_num = 1 then

	base

	prism_ 8,gs_leaf_thk/3,
		-a/2+0.08,0,11,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.18, b-0.18-dy/2, 10,
		-a/2+0.18, b-0.18-dy/2-0.1, 15,
		-a/2+0.18, b-0.18-dy/2-0.1-dy, 10,
		-a/2+0.18, b-0.18-dy/2-0.1-dy-0.1, 15,
		-a/2+0.18, 0.2, 10

	prism_ 8,gs_leaf_thk/3,
		a/2-0.08,0,11,
		a/2-0.08, b-0.08, 10,
		a/2-0.18, b-0.18, 15,
		a/2-0.18, b-0.18-dy/2, 10,
		a/2-0.18, b-0.18-dy/2-0.1, 15,
		a/2-0.18, b-0.18-dy/2-0.1-dy, 10,
		a/2-0.18, b-0.18-dy/2-0.1-dy-0.1, 15,
		a/2-0.18, 0.2, 10

	gosub 500

	prism_ 10,gs_leaf_thk/3,
		-a/2+0.08, 0, 10,
		-a/2+0.18, 0.2, 15,
		-a/2+0.08+dx+0.1, 0.2, 10,
		-a/2+0.08+dx+0.2, 0.2, 15,
		-a/2+0.08+2*dx+0.2, 0.2, 10,
		-a/2+0.08+2*dx+0.3, 0.2, 15,
		-a/2+0.08+3*dx+0.3, 0.2, 10,
		-a/2+0.08+3*dx+0.4, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0, 11

	prism_ 10,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.08+dx+0.1, b-0.18, 10,
		-a/2+0.08+dx+0.2, b-0.18, 15,
		-a/2+0.08+2*dx+0.2, b-0.18, 10,
		-a/2+0.08+2*dx+0.3, b-0.18, 15,
		-a/2+0.08+3*dx+0.3, b-0.18, 10,
		-a/2+0.08+3*dx+0.4, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.08, 11

	gosub 600

endif


if gs_SashText_typ_num = 2 then

	base

	prism_ 8,gs_leaf_thk/3,
		-a/2+0.08, 0.2, 9,
		-a/2+0.08, b-0.18, 8,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.18, b-0.18-dy/2, 10,
		-a/2+0.18, b-0.18-dy/2-0.1, 15,
		-a/2+0.18, b-0.18-dy/2-0.1-dy, 10,
		-a/2+0.18, b-0.18-dy/2-0.1-dy-0.1, 15,
		-a/2+0.18, 0.2, 10

	prism_ 8,gs_leaf_thk/3,
		a/2-0.08, 0.2, 9,
		a/2-0.08, b-0.18, 8,
		a/2-0.18, b-0.18, 15,
		a/2-0.18, b-0.18-dy/2, 10,
		a/2-0.18, b-0.18-dy/2-0.1, 15,
		a/2-0.18, b-0.18-dy/2-0.1-dy, 10,
		a/2-0.18, b-0.18-dy/2-0.1-dy-0.1, 15,
		a/2-0.18, 0.2, 10

	gosub 500

	prism_ 12,gs_leaf_thk/3,
		-a/2+0.08, 0, 11,
		-a/2+0.08, 0.2, 8,
		-a/2+0.18, 0.2, 15,
		-a/2+0.08+dx+0.1, 0.2, 10,
		-a/2+0.08+dx+0.2, 0.2, 15,
		-a/2+0.08+2*dx+0.2, 0.2, 10,
		-a/2+0.08+2*dx+0.3, 0.2, 15,
		-a/2+0.08+3*dx+0.3, 0.2, 10,
		-a/2+0.08+3*dx+0.4, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0.2, 9,
		a/2-0.08, 0, 11

	prism_ 12,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 11,
		-a/2+0.08, b-0.18, 8,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.08+dx+0.1, b-0.18, 10,
		-a/2+0.08+dx+0.2, b-0.18, 15,
		-a/2+0.08+2*dx+0.2, b-0.18, 10,
		-a/2+0.08+2*dx+0.3, b-0.18, 15,
		-a/2+0.08+3*dx+0.3, b-0.18, 10,
		-a/2+0.08+3*dx+0.4, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.18, 9,
		a/2-0.08, b-0.08, 11

	gosub 600

endif



!************* Both in Type1 and Type2

base
addx -a/2+0.08+dx+0.1

for kji=1 to 3

	prism_ 12,gs_leaf_thk/3,
		0, 0.2, 15,
		0, b-0.18-dy/2-0.1-dy-0.1, 10,
		0, b-0.18-dy/2-0.1-dy, 15,
		0, b-0.18-dy/2-0.1, 10,
		0, b-0.18-dy/2, 15,
		0, b-0.18, 10,

		0.1, b-0.18, 15,
		0.1, b-0.18-dy/2, 10,
		0.1, b-0.18-dy/2-0.1, 15,
		0.1, b-0.18-dy/2-0.1-dy, 10,
		0.1, b-0.18-dy/2-0.1-dy-0.1, 15,
		0.1, 0.2, 10

		addx dx+0.1

next kji

del 4

gosub 500

ky=0.2+dy
for hjk=1 to 2
	kx= -a/2+0.08+0.1
	for ju= 1 to 4
		prism_ 4,gs_leaf_thk/3,
			kx, ky, 10,
			kx, ky+0.1, 15,
			kx+dx, ky+0.1, 10,
			kx+dx, ky, 15
	kx=kx+dx+0.1
	next ju
ky=ky+dy+0.1
next hjk

gosub 600




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
kx=-a/2+0.18
ky=0.2
dx=(a-0.66)/4
dy=(b-0.58)/2.5
for u=1 to 2
for q=1 to 4
	put kx+0.05,ky+0.05,15
	put kx+dx-0.05,ky+0.05,15
	put kx+dx-0.05,ky+dy-0.05,15
	put kx+0.05,ky+dy-0.05,15
	put kx+0.05,ky+0.05,-1
	kx=kx+dx+0.1
next q
kx=-a/2+0.18
ky=ky+dy+0.1
next u

for u=1 to 4
	put kx+0.05,ky+0.05,15
	put kx+dx-0.05,ky+0.05,15
	put kx+dx-0.05,ky+dy/2-0.05,15
	put kx+0.05,ky+dy/2-0.05,15
	put kx+0.05,ky+0.05,-1
	kx=kx+dx+0.1
next u

	base
for i=1 to 12
prism_ 5,gs_leaf_thk/3,
	get(14),get(1)
next i

gosub 500

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

addz gs_leaf_thk/3

base

cprism_ gs_leaf_matin, gs_leaf_mat1, gs_leaf_matin,
	5,gs_leaf_thk*2/3,
	-a/2+0.08,0,14,
	-a/2+0.08,b-0.08,14,
	a/2-0.08,b-0.08,14,
	a/2-0.08,0,14,
	-a/2+0.08,0,14

gosub 500

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.08, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.08, gs_leaf_thk
endif

RETURN


40:

!!!!!******************** Garage 4 !!!!!


a=a+2*(0.08)
b=b+0.08

pen gs_leaf_pen
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
kx=-a/2+0.18
ky=0.2
dx=(a-0.66)/4
dy=(b-0.38)


if gs_SashText_typ_num = 1 then

	base

	prism_ 4,gs_leaf_thk/3,
		-a/2+0.08,0,11,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.18, 0.2, 10

	prism_ 4,gs_leaf_thk/3,
		a/2-0.08,0,11,
		a/2-0.08, b-0.08, 10,
		a/2-0.18, b-0.18, 15,
		a/2-0.18, 0.2, 10

	gosub 500

	prism_ 10,gs_leaf_thk/3,
		-a/2+0.08, 0, 14,
		-a/2+0.18, 0.2, 15,
		-a/2+0.18+dx, 0.2, 10,
		-a/2+0.18+dx+0.1, 0.2, 15,
		-a/2+0.18+2*dx+0.1, 0.2, 10,
		-a/2+0.18+2*dx+0.2, 0.2, 15,
		-a/2+0.18+3*dx+0.2, 0.2, 10,
		-a/2+0.18+3*dx+0.3, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0, 11

	prism_ 10,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 14,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.18+dx, b-0.18, 10,
		-a/2+0.18+dx+0.1, b-0.18, 15,
		-a/2+0.18+2*dx+0.1, b-0.18, 10,
		-a/2+0.18+2*dx+0.2, b-0.18, 15,
		-a/2+0.18+3*dx+0.2, b-0.18, 10,
		-a/2+0.18+3*dx+0.3, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.08, 11

	gosub 600

endif


if gs_SashText_typ_num = 2 then

	base

	prism_ 4,gs_leaf_thk/3,
		-a/2+0.08, 0.2, 9,
		-a/2+0.08, b-0.18, 8,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.18, 0.2, 10

	prism_ 4,gs_leaf_thk/3,
		a/2-0.08, 0.2, 9,
		a/2-0.08, b-0.18, 8,
		a/2-0.18, b-0.18, 15,
		a/2-0.18, 0.2, 10

	gosub 500

	prism_ 12,gs_leaf_thk/3,
		-a/2+0.08, 0, 11,
		-a/2+0.08, 0.2, 8,
		-a/2+0.18, 0.2, 15,
		-a/2+0.18+dx, 0.2, 10,
		-a/2+0.18+dx+0.1, 0.2, 15,
		-a/2+0.18+2*dx+0.1, 0.2, 10,
		-a/2+0.18+2*dx+0.2, 0.2, 15,
		-a/2+0.18+3*dx+0.2, 0.2, 10,
		-a/2+0.18+3*dx+0.3, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0.2, 9,
		a/2-0.08, 0, 11

	prism_ 12,gs_leaf_thk/3,
			-a/2+0.08,b-0.08,11,
		-a/2+0.08, b-0.18, 8,
		-a/2+0.18, b-0.18, 15,
		-a/2+0.18+dx, b-0.18, 10,
		-a/2+0.18+dx+0.1, b-0.18, 15,
		-a/2+0.18+2*dx+0.1, b-0.18, 10,
		-a/2+0.18+2*dx+0.2, b-0.18, 15,
		-a/2+0.18+3*dx+0.2, b-0.18, 10,
		-a/2+0.18+3*dx+0.3, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.18, 9,
		a/2-0.08, b-0.08, 11

	gosub 600

endif


!********** Both in Type1 and Type2

base

addx -a/2+0.08+dx+0.1

for kji=1 to 3

	prism_ 4,gs_leaf_thk/3,
		0, 0.2, 15,
		0, b-0.18, 10,
		0.1, b-0.18, 15,
		0.1, 0.2, 10

		addx dx+0.1
next kji

del 4

gosub 500

addz gs_leaf_thk/3
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

base



cprism_ gs_leaf_matin, gs_leaf_mat1, gs_leaf_matin,
	5,gs_leaf_thk*2/3,
	-a/2+0.08,0,14,
	-a/2+0.08,b-0.08,14,
	a/2-0.08,b-0.08,14,
	a/2-0.08,0,14,
	-a/2+0.08,0,14

gosub 500

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.08, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.08, gs_leaf_thk
endif

RETURN

50:
!!!!!******************** Garage 5 !!!!!

b=b+0.08


pen gs_leaf_pen
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
a=a+2*(0.08)

kx=-a/2+0.2
ky=b-0.23-2*dy		!!*dx*0.8

for kj=1 to panenum-1
	put kx+dx, b-0.2, 10,
		kx+dx+0.03, b-0.2, 15
	kx=kx+dx+0.03
next kj
kx=-a/2+0.2
for kj=1 to panenum-1
	put kx+dx, b-0.2-2*dy-0.03, 10,
		kx+dx+0.03, b-0.2-2*dy-0.03, 15
	kx=kx+dx+0.03
next kj
kx=-a/2+0.2


if gs_SashText_typ_num = 1 then

	base

	prism_ 7,gs_leaf_thk/3,
		-a/2+0.08, 0, 11,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.2, b-0.2, 15,
		-a/2+0.2, b-0.2-dy, 10,
		-a/2+0.2, b-0.2-dy-0.03, 15,
		-a/2+0.2, b-0.2-2*dy-0.03, 10,
		-a/2+0.2, 0,11

	prism_ 7,gs_leaf_thk/3,
		a/2-0.08, 0, 11,
		a/2-0.08, b-0.08, 10,
		a/2-0.2, b-0.2, 15,
		a/2-0.2, b-0.2-dy, 10,
		a/2-0.2, b-0.2-dy-0.03, 15,
		a/2-0.2, b-0.2-2*dy-0.03, 10,
		a/2-0.2, 0,11

	gosub 500

	prism_ 4+nsp/3/2,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 14,
		-a/2+0.2, b-0.2, 15,
		get(nsp/2),
		a/2-0.2, b-0.2, 10,
		a/2-0.08, b-0.08, 11

	gosub 600

endif


if gs_SashText_typ_num = 2 then

	base

	prism_ 7,gs_leaf_thk/3,
		-a/2+0.08, 0, 11,
		-a/2+0.08, b-0.2, 8,
		-a/2+0.2, b-0.2, 15,
		-a/2+0.2, b-0.2-dy, 10,
		-a/2+0.2, b-0.2-dy-0.03, 15,
		-a/2+0.2, b-0.2-2*dy-0.03, 10,
		-a/2+0.2, 0,11

	prism_ 7,gs_leaf_thk/3,
		a/2-0.08, 0, 11,
		a/2-0.08, b-0.2, 8,
		a/2-0.2, b-0.2, 15,
		a/2-0.2, b-0.2-dy, 10,
		a/2-0.2, b-0.2-dy-0.03, 15,
		a/2-0.2, b-0.2-2*dy-0.03, 10,
		a/2-0.2, 0,11

	gosub 500

	prism_ 6+nsp/3/2,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 11,
		-a/2+0.08, b-0.2, 8,
		-a/2+0.2, b-0.2, 15,
		get(nsp/2),
		a/2-0.2, b-0.2, 10,
		a/2-0.08, b-0.2, 9,
		a/2-0.08, b-0.08, 11

	gosub 600

endif



!*********** Both in Type1 and Type2

base

prism_ 4+nsp/3,gs_leaf_thk/3,
	-a/2+0.2, b-0.2-2*dy-0.03-0.1, 14,
	-a/2+0.2, b-0.2-2*dy-0.03, 15,
	get(nsp),
	a/2-0.2, b-0.2-2*dy-0.03, 10,
	a/2-0.2, b-0.2-2*dy-0.03-0.1, 15


for kj=1 to panenum-1
	put kx+dx, b-0.2-dy, 10,
		kx+dx+0.03, b-0.2-dy, 15
	kx=kx+dx+0.03
next kj

kx=a/2-0.2

for kj=1 to panenum-1
	put kx-dx, b-0.2-dy-0.03, 10,
		kx-dx-0.03, b-0.2-dy-0.03, 15
	kx=kx-dx-0.03
next kj

kx=-a/2+0.2

base

prism_ 4+nsp/3,gs_leaf_thk-0.002,
	kx, b-0.2-dy, 15,
	get(nsp/2),
	a/2-0.2, b-0.2-dy, 10,
	a/2-0.2, b-0.2-dy-0.03, 15,
	get(nsp),
	kx, b-0.2-dy-0.03, 10

gosub 600

kx=-a/2+0.2

for ijk=1 to panenum-1

	prism_ 4,gs_leaf_thk-0.002,
		kx+dx, b-0.2-2*dy-0.03, 15,
		kx+dx, b-0.2-dy-0.03, 10,
		kx+dx+0.03, b-0.2-dy-0.03, 15,
		kx+dx+0.03, b-0.2-2*dy-0.03, 10

	prism_ 4,gs_leaf_thk-0.002,
		kx+dx, b-0.2-dy, 15,
		kx+dx, b-0.2, 10,
		kx+dx+0.03, b-0.2, 15,
		kx+dx+0.03, b-0.2-dy, 10

	kx=kx+dx+0.03

next ijk

kx=-a/2+0.2

gosub 500

for i=1 to 2

  for u=1 to panenum	!6

	put kx,ky,15
	put kx+dx,ky,15
	put kx+dx,ky+dy,15
	put kx,ky+dy,15
	put kx,ky,-1
	kx=kx+dx+0.03

  next u

kx=-a/2+0.2
ky=ky+dy+0.03

next i


body -1
lin_ -a/2+0.2,0,0.02,a/2-0.2,0,0.02
n=int((a-0.39)/0.1)
dx1=(a-0.39)/n
kx=-a/2+0.2

base

for q=1 to n

	material gs_leaf_mat2
	sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

	prism 4,gs_leaf_thk/3,
		kx,0,
		kx+dx1-0.01,0,
		kx+dx1-0.01,b-0.33-2*dy,	!!*dx*0.8,
		kx,b-0.33-2*dy		!!*dx*0.8

		kx=kx+dx1
	body -1

next q

gosub 500


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
addz gs_leaf_thk/3
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

base

material gs_leaf_matin

prism_ 5+5,gs_leaf_thk*2/3,
	-a/2+0.08,0,14,
	-a/2+0.08,b-0.08,14,
	a/2-0.08,b-0.08,14,
	a/2-0.08,0,14,
	-a/2+0.08,0,-1,

	-a/2+0.2, b-0.2, 15,
	a/2-0.2, b-0.2, 15,
	a/2-0.2, b-0.2-2*dy-0.03, 15,
	-a/2+0.2, b-0.2-2*dy-0.03, 15,
	-a/2+0.2, b-0.2, -1


!!	use (nsp-1),-1

gosub 500

body -1
pen gs_glass_pen
material gs_glass_mat

for qu=1 to nsp/15

	prism_ 5,0.01,
		get (14),get(1)

	body -1

next qu

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.08, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.08, gs_leaf_thk
endif

RETURN


60:

!!!!!******************** Garage 6 !!!!!

a=a+2*(0.08)
b=b+0.08

pen gs_leaf_pen
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

if gs_SashText_typ_num = 1 then
	base
		prism_ 4, gs_leaf_thk/3,
	-a/2+0.08,0,11,
			-a/2+0.08, b-0.08, 10,
			-a/2+0.18, b-0.18, 15,
			-a/2+0.18, 0, 11
		prism_ 4, gs_leaf_thk/3,
	a/2-0.08,0,11,
			a/2-0.08, b-0.08, 10,
	a/2-0.18,b-0.18,15,
			a/2-0.18, 0, 11
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
		prism_ 4, gs_leaf_thk/3,
			-a/2+0.08, b-0.08, 10,
			-a/2+0.18, b-0.18, 15,
			a/2-0.18, b-0.18, 10,
			a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif
if gs_SashText_typ_num = 2 then
	base
		prism_ 4, gs_leaf_thk/3,
			-a/2+0.08, 0, 11,
			-a/2+0.08, b-0.18, 8,
	-a/2+0.18,b-0.18,15,
	-a/2+0.18,0,11
		prism_ 4, gs_leaf_thk/3,
			a/2-0.08, 0, 11,
			a/2-0.08, b-0.18, 8,
			a/2-0.18, b-0.18, 15,
			a/2-0.18, 0, 11
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
		prism_ 6, gs_leaf_thk/3,
			-a/2+0.08, b-0.08, 11,
			-a/2+0.08, b-0.18, 8,
			-a/2+0.18, b-0.18, 15,
			a/2-0.18, b-0.18, 10,
			a/2-0.08, b-0.18, 9,
			a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif



n=int((a-0.36)/0.1)
dx=(a-0.36)/n
kx=-a/2+0.18

	base
for i=1 to n
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
prism 4,gs_leaf_thk/3,
	kx,0,
	kx,b-0.18,
	kx+dx-0.01,b-0.18,
	kx+dx-0.01,0
	kx=kx+dx
next i
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1


lin_ -a/2+0.18,0,0.02,a/2-0.18,0,0.02

addz gs_leaf_thk/3
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

cprism_ gs_leaf_matin, gs_leaf_mat1, gs_leaf_matin,
	4,gs_leaf_thk*2/3,
	-a/2+0.08,0,14,
	-a/2+0.08,b-0.08,14,
	a/2-0.08,b-0.08,14,
	a/2-0.08,0,14

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.07, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.07, gs_leaf_thk
endif

RETURN

70:
!!!!!******************** Garage 7 !!!!!

a=a+2*(0.08)
b=b+0.08


!!kx=-a/2+0.18
!!dx=(a-0.42)/3
!!ky=b-0.18-2*dx*0.8
!!dy=dx*0.8

panenum=ceil((a-0.36+0.03)/0.28)

pen gs_leaf_pen
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
kx=-a/2+0.18
dx=(a-0.36-((panenum-1)*0.03))/panenum	!6
dy=0.208				!!!0.157
ky=b-0.23-2*dy		!!*dx*0.8


for kj=1 to panenum-1
	put kx+dx, b-0.2, 10,
		kx+dx+0.03, b-0.2, 15
	kx=kx+dx+0.03
next kj
kx=-a/2+0.18
for kj=1 to panenum-1
	put kx+dx, b-0.2-2*dy-0.03, 10,
		kx+dx+0.03, b-0.2-2*dy-0.03, 15
	kx=kx+dx+0.03
next kj
kx=-a/2+0.18

if gs_SashText_typ_num = 1 then
	base
	prism_ 7,gs_leaf_thk/3,
		-a/2+0.08, 0, 11,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.18, b-0.2, 15,
		-a/2+0.18, b-0.2-dy, 10,
		-a/2+0.18, b-0.2-dy-0.03, 15,
		-a/2+0.18, b-0.2-2*dy-0.03, 10,
		-a/2+0.18, 0,11
	prism_ 7,gs_leaf_thk/3,
		a/2-0.08, 0, 11,
		a/2-0.08, b-0.08, 10,
		a/2-0.18, b-0.2, 15,
		a/2-0.18, b-0.2-dy, 10,
		a/2-0.18, b-0.2-dy-0.03, 15,
		a/2-0.18, b-0.2-2*dy-0.03, 10,
		a/2-0.18, 0,11
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
	prism_ 4+nsp/3/2,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.18, b-0.2, 15,
		get(nsp/2),
		a/2-0.18, b-0.2, 10,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif
if gs_SashText_typ_num = 2 then
	base
	prism_ 7,gs_leaf_thk/3,
		-a/2+0.08, 0, 11,
		-a/2+0.08, b-0.2, 8,
		-a/2+0.18, b-0.2, 15,
		-a/2+0.18, b-0.2-dy, 10,
		-a/2+0.18, b-0.2-dy-0.03, 15,
		-a/2+0.18, b-0.2-2*dy-0.03, 10,
		-a/2+0.18, 0,11
	prism_ 7,gs_leaf_thk/3,
		a/2-0.08, 0, 11,
		a/2-0.08, b-0.2, 8,
		a/2-0.18, b-0.2, 15,
		a/2-0.18, b-0.2-dy, 10,
		a/2-0.18, b-0.2-dy-0.03, 15,
		a/2-0.18, b-0.2-2*dy-0.03, 10,
		a/2-0.18, 0,11
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
	prism_ 6+nsp/3/2,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 11,
		-a/2+0.08, b-0.2, 8,
		-a/2+0.18, b-0.2, 15,
		get(nsp/2),
		a/2-0.18, b-0.2, 10,
		a/2-0.08, b-0.2, 9,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif



!!!Both in Type1 and Type2
	base
	prism_ 4+nsp/3,gs_leaf_thk/3,
		-a/2+0.18, b-0.2-2*dy-0.03-0.1, 14,
		-a/2+0.18, b-0.2-2*dy-0.03, 15,
		get(nsp),
		a/2-0.18, b-0.2-2*dy-0.03, 10,
		a/2-0.18, b-0.2-2*dy-0.03-0.1, 15


for kj=1 to panenum-1
	put kx+dx, b-0.2-dy, 10,
		kx+dx+0.03, b-0.2-dy, 15
	kx=kx+dx+0.03
next kj
kx=a/2-0.18
for kj=1 to panenum-1
	put kx-dx, b-0.2-dy-0.03, 10,
		kx-dx-0.03, b-0.2-dy-0.03, 15
	kx=kx-dx-0.03
next kj
kx=-a/2+0.18

	base
	prism_ 4+nsp/3,gs_leaf_thk-0.002,
		kx, b-0.2-dy, 15,
		get(nsp/2),
		a/2-0.18, b-0.2-dy, 10,
		a/2-0.18, b-0.2-dy-0.03, 15,
		get(nsp),
		kx, b-0.2-dy-0.03, 10

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
	kx=-a/2+0.18
	for ijk=1 to panenum-1
		prism_ 4+nsp/3,gs_leaf_thk-0.002,
			kx+dx, b-0.2-2*dy-0.03, 15,
			kx+dx, b-0.2-dy-0.03, 10,
			kx+dx+0.03, b-0.2-dy-0.03, 15,
			kx+dx+0.03, b-0.2-2*dy-0.03, 10
		prism_ 4+nsp/3,gs_leaf_thk-0.002,
			kx+dx, b-0.2-dy, 15,
			kx+dx, b-0.2, 10,
			kx+dx+0.03, b-0.2, 15,
			kx+dx+0.03, b-0.2-dy, 10
		kx=kx+dx+0.03
	next ijk
	kx=-a/2+0.18

	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

body -1



for q=1 to 2
  for u=1 to panenum
	put kx,ky,15
	put kx+dx,ky,15
	put kx+dx,ky+dy,15
	put kx,ky+dy,15
	put kx,ky,-1
	kx=kx+dx+0.03
  next u
kx=-a/2+0.18
ky=ky+dy+0.03
next q


lin_ -a/2+0.18,0,0.02,a/2-0.18,0,0.02

n=int((a-0.36)/0.1)
dx1=(a-0.36)/n
kx=-a/2+0.18
for i=1 to n
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
prism 4,gs_leaf_thk/3,
	kx,0,
	kx,b-0.33-2*dy,
	kx+dx1-0.01,b-0.33-2*dy,
	kx+dx1-0.01,0
	kx=kx+dx1
body -1
next i

addz gs_leaf_thk/3
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
prism_ 10,gs_leaf_thk*2/3,
	-a/2+0.08,0,14,
	-a/2+0.08,b-0.08,14,
	a/2-0.08,b-0.08,14,
	a/2-0.08,0,14,
	-a/2+0.08,0,-1,

	-a/2+0.18, b-0.2, 15,
	a/2-0.18, b-0.2, 15,
	a/2-0.18, b-0.2-2*dy-0.03, 15,
	-a/2+0.18, b-0.2-2*dy-0.03, 15,
	-a/2+0.18, b-0.2, -1

!!	use (nsp-1),-1


body -1
pen gs_glass_pen
material gs_glass_mat
for i=1 to panenum*2
prism_ 5,0.01,
	get (14),get(1)
body -1
next i

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.07, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.07, gs_leaf_thk
endif

RETURN

80:
!!!!!******************** Garage 8 !!!!!

a=a+2*(0.08)
b=b+0.08

pen gs_leaf_pen
material gs_leaf_mat2
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
kx=-a/2+0.18
ky=0.2
dx=(a-0.46)/2
dy=(b-0.58)/2.5


if gs_SashText_typ_num = 1 then
	base
	prism_ 8,gs_leaf_thk/3,
		-a/2+0.08, 0, 10,
		-a/2+0.18, 0.2, 15,
		-a/2+0.18, 0.2+dy, 10,
		-a/2+0.18, 0.2+dy+0.1, 15,
		-a/2+0.18, 0.2+2*dy+0.1, 10,
		-a/2+0.18, 0.2+2*dy+0.2, 15,
		-a/2+0.18, b-0.18, 10,
		-a/2+0.08, b-0.08, 11
	prism_ 8,gs_leaf_thk/3,
		a/2-0.08, 0, 10,
		a/2-0.18, 0.2, 15,
		a/2-0.18, 0.2+dy, 10,
		a/2-0.18, 0.2+dy+0.1, 15,
		a/2-0.18, 0.2+2*dy+0.1, 10,
		a/2-0.18, 0.2+2*dy+0.2, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1


	base
	prism_ 6,gs_leaf_thk/3,
		-a/2+0.08, 0, 10,
		-a/2+0.18, 0.2, 15,
		-0.05, 0.2, 10,
		0.05, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0, 11
	prism_ 6,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.18, b-0.18, 15,
		-0.05, b-0.18, 10,
		0.05, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif

if gs_SashText_typ_num = 2 then
	base
	prism_ 8,gs_leaf_thk/3,
		-a/2+0.08, 0.2, 8,
		-a/2+0.18, 0.2, 15,
		-a/2+0.18, 0.2+dy, 10,
		-a/2+0.18, 0.2+dy+0.1, 15,
		-a/2+0.18, 0.2+2*dy+0.1, 10,
		-a/2+0.18, 0.2+2*dy+0.2, 15,
		-a/2+0.18, b-0.18, 10,
		-a/2+0.08, b-0.18, 9
	prism_ 8,gs_leaf_thk/3,
		a/2-0.08, 0.2, 8,
		a/2-0.18, 0.2, 15,
		a/2-0.18, 0.2+dy, 10,
		a/2-0.18, 0.2+dy+0.1, 15,
		a/2-0.18, 0.2+2*dy+0.1, 10,
		a/2-0.18, 0.2+2*dy+0.2, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.18, 9
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1


	base
	prism_ 8,gs_leaf_thk/3,
	-a/2+0.08,0,11,
		-a/2+0.08, 0.2, 8,
		-a/2+0.18, 0.2, 15,
		-0.05, 0.2, 10,
		0.05, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0.2, 9,
		a/2-0.08, 0, 11
	prism_ 8,gs_leaf_thk/3,
	-a/2+0.08,b-0.08,11,
		-a/2+0.08, b-0.18, 8,
		-a/2+0.18, b-0.18, 15,
		-0.05, b-0.18, 10,
		0.05, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.18, 9,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif



!!!Both in type1 and type2
	base
	prism_ 12,gs_leaf_thk/3,
		-0.05, 0.2, 15,
		-0.05, 0.2+dy, 10,
		-0.05, 0.2+dy+0.1, 15,
		-0.05, 0.2+2*dy+0.1, 10,
		-0.05, 0.2+2*dy+0.2, 15,
		-0.05, b-0.18, 10,

		0.05, b-0.18, 15,
		0.05, 0.2+2*dy+0.2, 10,
		0.05, 0.2+2*dy+0.1, 15,
		0.05, 0.2+dy+0.1, 10,
		0.05, 0.2+dy, 15,
		0.05, 0.2, 10
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
ky=0.2
for vti=1 to 2
	kx=-a/2+0.18
	for hti=1 to 2
		prism_ 4,gs_leaf_thk/3,
			kx, ky+dy, 15,
			kx+dx, ky+dy, 10,
			kx+dx, ky+dy+0.1, 15,
			kx, ky+dy+0.1, 10
	kx=kx+dx+0.1
	next hti
ky=ky+dy+0.1
next vti

	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1





!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
kx=-a/2+0.18
ky=0.2
dx=(a-0.46)/2
dy=(b-0.58)/2.5
for u=1 to 2
for q=1 to 2
	put kx+0.05,ky+0.05,15
	put kx+dx-0.05,ky+0.05,15
	put kx+dx-0.05,ky+dy-0.05,15
	put kx+0.05,ky+dy-0.05,15
	put kx+0.05,ky+0.05,-1
	kx=kx+dx+0.1
next q
kx=-a/2+0.18
ky=ky+dy+0.1
next u

for u=1 to 2
	put kx+0.05,ky+0.05,15
	put kx+dx-0.05,ky+0.05,15
	put kx+dx-0.05,ky+dy/2-0.05,15
	put kx+0.05,ky+dy/2-0.05,15
	put kx+0.05,ky+0.05,-1
	kx=kx+dx+0.1
next u

	base
for i=1 to 6
prism_ 5,gs_leaf_thk/3,
	get(14),get(1)
next i
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

addz gs_leaf_thk/3
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
	base
prism_ 5,gs_leaf_thk*2/3,
	-a/2+0.08,0,14,
	-a/2+0.08,b-0.08,14,
	a/2-0.08,b-0.08,14,
	a/2-0.08,0,14,
	-a/2+0.08,0,14
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.07, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.07, gs_leaf_thk
endif

RETURN

90:
!!!!!******************** Garage 9 !!!!!

a=a+2*(0.08)
b=b+0.08

pen gs_leaf_pen
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
kx=-a/2+0.18
ky=0.2
dx=(a-0.46)/2
dy=(b-0.38)


if gs_SashText_typ_num = 1 then

	base
	prism_ 4,gs_leaf_thk/3,
		-a/2+0.08, 0, 10,
		-a/2+0.18, 0.2, 15,
		-a/2+0.18, b-0.18, 10,
		-a/2+0.08, b-0.08, 11
	prism_ 4,gs_leaf_thk/3,
		a/2-0.08, 0, 10,
		a/2-0.18, 0.2, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
	prism_ 6,gs_leaf_thk/3,
		-a/2+0.08, 0, 10,
		-a/2+0.18, 0.2, 15,
		-0.05, 0.2, 10,
		0.05, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0, 11
	prism_ 6,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 10,
		-a/2+0.18, b-0.18, 15,
		-0.05, b-0.18, 10,
		0.05, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif
if gs_SashText_typ_num = 2 then

	base
	prism_ 4,gs_leaf_thk/3,
		-a/2+0.08, 0.2, 8,
		-a/2+0.18, 0.2, 15,
		-a/2+0.18, b-0.18, 10,
		-a/2+0.08, b-0.18, 9
	prism_ 4,gs_leaf_thk/3,
		a/2-0.08, 0.2, 8,
		a/2-0.18, 0.2, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.18, 9
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

	base
	prism_ 8,gs_leaf_thk/3,
		-a/2+0.08, 0, 9,
		-a/2+0.08, 0.2, 8,
		-a/2+0.18, 0.2, 15,
		-0.05, 0.2, 10,
		0.05, 0.2, 15,
		a/2-0.18, 0.2, 10,
		a/2-0.08, 0.2, 9,
		a/2-0.08, 0, 11
	prism_ 8,gs_leaf_thk/3,
		-a/2+0.08, b-0.08, 9,
		-a/2+0.08, b-0.18, 8,
		-a/2+0.18, b-0.18, 15,
		-0.05, b-0.18, 10,
		0.05, b-0.18, 15,
		a/2-0.18, b-0.18, 10,
		a/2-0.08, b-0.18, 9,
		a/2-0.08, b-0.08, 11
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
endif


!!!Both in type1 and type2

	base
	prism_ 4,gs_leaf_thk/3,
		-0.05, 0.2, 10,
		0.05, 0.2, 15,
		0.05, b-0.18, 10,
		-0.05, b-0.18, 15
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
addz gs_leaf_thk/3
	base
prism_ 5,gs_leaf_thk*2/3,
	-a/2+0.08,0,14,
	-a/2+0.08,b-0.08,14,
	a/2-0.08,b-0.08,14,
	a/2-0.08,0,14,
	-a/2+0.08,0,14
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1

del 1

if bMiddleLine then
	lin_ a/2-0.08, 0, 0, a/2-0.08, b-0.08, 0
	lin_ a/2-0.08, 0, gs_leaf_thk, a/2-0.08, b-0.08, gs_leaf_thk
endif

RETURN


100:
	!!!!!******************** Garage 10 !!!!!

	pen gs_leaf_pen
	material gs_leaf_mat1
	sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

	add -a/2,0,gs_leaf_thk
	roty 180
	addx  -a

	base

	cprism_ gs_leaf_mat1, gs_leaf_matin,  gs_leaf_matin,
		5,gs_leaf_thk*3/5,
		0,0,15,
		0,b,15,
		a,b,15,
		a,0,15,
		0,0,-1


	gosub 500


	addz gs_leaf_thk*3/5
	addx 0.05

	base

	for i = 1 to gs_ver_pnum
		addy (i-1) * (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum + 0.05*i

		brick A - 0.1, (B - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum, gs_leaf_thk*2/5

		del 1
	next i

	del 5

	gosub 600

	if bMiddleLine then
		lin_ a/2, 0, gs_leaf_thk*2/5, a/2, b, gs_leaf_thk*2/5
		lin_ a/2, 0, gs_leaf_thk, a/2, b, gs_leaf_thk
	endif

RETURN


110:
!!!!!******************** Garage 11 !!!!!

	pen gs_leaf_pen
	material gs_leaf_mat1
	sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

	add -a/2,0,gs_leaf_thk
	roty 180
	addx  -a

	base

	cprism_ gs_leaf_mat1, gs_leaf_matin, gs_leaf_matin,
		5,gs_leaf_thk*3/5,
		0,0,15,
		0,b,15,
		a,b,15,
		a,0,15,
		0,0,-1

	gosub 500

	addz gs_leaf_thk*3/5
	addx 0.05

	base

	for i = 1 to gs_ver_pnum
		addy (i-1) * (b - 0.05*(gs_ver_pnum + 1))/gs_ver_pnum + 0.05*i

		brick a - 0.1, (b - 0.05*(gs_ver_pnum + 1))/gs_ver_pnum, gs_leaf_thk/5

		del 1
	next i

	gosub 600

	addz gs_leaf_thk/5

	if gs_ver_pnum > 1 then
		addy (gs_ver_pnum-2)*(b-0.05*(gs_ver_pnum+1))/gs_ver_pnum+(gs_ver_pnum-2)*0.05
	endif

	addy 0.1

	base

	for i = 1 to gs_hor_pnum
		addx (i-1) * (a - 0.1 - 0.05*(gs_hor_pnum + 1)) / gs_hor_pnum + 0.05*i

		brick (a - 0.1 - 0.05*(gs_hor_pnum + 1)) / gs_hor_pnum, (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum - 0.1, gs_leaf_thk/5

		del 1
	next i

	gosub 500

	del 1
	if gs_ver_pnum > 1 then del 1
	del 6

	if bMiddleLine then
		lin_ 0, 0, 0, 0, b, 0
		lin_ 0, 0, gs_leaf_thk*3/5, 0, b, gs_leaf_thk*3/5
	endif

RETURN


120:
!!!!!******************** Garage 12 !!!!!

	pen gs_leaf_pen
	material gs_leaf_mat1
	sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

	add -a/2,0,gs_leaf_thk

	roty 180
	addx  -a

	base

	cprism_ gs_leaf_mat1, gs_leaf_matin, gs_leaf_matin,
		5,gs_leaf_thk*3/5,
		0,0,15,
		0,b,15,
		a,b,15,
		a,0,15,
		0,0,-1

	gosub 500

	addz gs_leaf_thk*3/5
	addx 0.05

	base

	for i = 1 to gs_ver_pnum
		addy (i-1)*(b-0.05*(gs_ver_pnum+1))/gs_ver_pnum+0.05*i

		brick a - 0.1, (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum, gs_leaf_thk/5

		gosub 600

		addy 0.05
		addz gs_leaf_thk/5

		base

		for j=1 to gs_hor_pnum
			addx (j-1) * (a - 0.1 - 0.05*(gs_hor_pnum + 1)) / gs_hor_pnum + 0.05*j

			brick (a - 0.1 - 0.05*(gs_hor_pnum + 1)) / gs_hor_pnum, (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum - 0.1, gs_leaf_thk/5

			gosub 500

			del 1
		next j

		del 3
	next i

	del 5

	if bMiddleLine then
		lin_ a/2, 0, gs_leaf_thk*2/5, a/2, b, gs_leaf_thk*2/5
		lin_ a/2, 0, gs_leaf_thk, a/2, b, gs_leaf_thk
	endif

RETURN


130:
!!!!!******************** Simple 3D !!!!!

pen gs_leaf_pen
material gs_leaf_mat1
sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

add -a/2,0,0
	base
brick a,b,gs_leaf_thk
	vert 0, 0, 0
	vert 0, 1, 0
	vert 1, 0, 0
	vert 0, 0, 1
	coor 2, -1, -2, -3, -4
	body 1
del 1

if bMiddleLine then
	lin_ a/2, 0, 0, a/2, b, 0
	lin_ a/2, 0, gs_leaf_thk, a/2, b, gs_leaf_thk
endif

RETURN


140:
!!!!!******************** Garage 14

	pen gs_leaf_pen
	material gs_leaf_mat1
	sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

	add -a/2,0,gs_leaf_thk
	roty 180
	addx  -a

	base

	if gs_ver_pnum > 1 then
		addy (gs_ver_pnum - 2) * (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum + (gs_ver_pnum - 2)*0.05
	endif

	addx 0.05
	addy 0.1

	for i = 1 to gs_hor_pnum
		addx (i-1) * (a - 0.1 - 0.05*(gs_hor_pnum + 1)) / gs_hor_pnum + 0.05*i

		cutpolya 5, 1, 0,
			0, 0, 15,
			(a-0.1-0.05*(gs_hor_pnum+1))/gs_hor_pnum, 0, 15,
			(a-0.1-0.05*(gs_hor_pnum+1))/gs_hor_pnum, (b-0.05*(gs_ver_pnum+1))/gs_ver_pnum-0.1, 15,
			0, (b-0.05*(gs_ver_pnum+1))/gs_ver_pnum-0.1, 15,
			0, 0, -1

		del 1
	next i

	del 2
	if gs_ver_pnum > 1 then del 1

	cprism_ gs_leaf_mat1, gs_leaf_matin, gs_leaf_matin,
		5,gs_leaf_thk*3/5,
		0,0,15,
		0,b,15,
		a,b,15,
		a,0,15,
		0,0,-1

	gosub 500

	addz gs_leaf_thk*3/5
	addx 0.05

	base

	for i = 1 to gs_ver_pnum
		addy (i-1) * (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum + 0.05*i

		brick a - 0.1, (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum, gs_leaf_thk/5

		del 1
	next i

	gosub 600

	for i = 1 to gs_hor_pnum
		cutend
	next i

	addz -gs_leaf_thk/2+0.01/2

	if gs_ver_pnum > 1 then
		addy (gs_ver_pnum - 2) * (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum + (gs_ver_pnum - 2)*0.05
	endif

	addy 0.1

	base

	pen gs_glass_pen
	material gs_glass_mat

	for i = 1 to gs_hor_pnum
		addx (i-1) * (a - 0.1 - 0.05*(gs_hor_pnum + 1)) / gs_hor_pnum + 0.05*i

		brick (a - 0.1 - 0.05*(gs_hor_pnum + 1)) / gs_hor_pnum, (b - 0.05*(gs_ver_pnum + 1)) / gs_ver_pnum - 0.1, 0.01

		del 1
	next i

	gosub 500

	del 1
	if gs_ver_pnum > 1 then del 1
	del 3

	if bMiddleLine then
		lin_ 0, 0, 0, 0, b, 0
		lin_ 0, 0, gs_leaf_thk*3/5, 0, b, gs_leaf_thk*3/5
	endif

	del 3

RETURN





310:

!******************************* Custom panel

aaa=a
bbb=b

if gs_cust_panel<>"" then
	addx -aaa/2
	call "Door_Panel_Collection" parameters a=aaa, b=bbb, zzyzx=gs_leaf_thk,
			gs_ptype=gs_cust_panel
	del 1
endif

if bMiddleLine then
	lin_ a/2, 0, 0, a/2, b, 0
	lin_ a/2, 0, gs_leaf_thk, a/2, b, gs_leaf_thk
endif

return


end

500:

!**************** texture coordination horizontal

vert 0, 0, 0
vert 0, 1, 0
vert 1, 0, 0
vert 0, 0, 1
coor 2, -1, -2, -3, -4
body 1

base

return

end



600:

!**************** texture coordination vertical

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1
coor 2, -1, -2, -3, -4
body 1

base

return

end



900:

if ii=1 then
	sidemat=botmat
	if gs_leaf_mat1 = gs_leaf_matin then mmask=15 else mmask=11
	if gs_leaf_mat1 = gs_leaf_matin then mmask2=13 else mmask2=9
else
	sidemat=topmat
	mmask=14
	mmask2=12
endif

return

end


"KickBoard":
	_kickBoardThickness	= 0.002
	_kickBoardWidth		= a - 0.02

	addz -gs_leaf_thk/2 - _kickBoardThickness

	if bKickBoardOut then		! Outside
		material matKickBoardOut

		prism_ 5, _kickBoardThickness,
			-_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	0.01,				-1
	endif

	addz gs_leaf_thk + _kickBoardThickness

	if bKickBoardIn then		! Inside
		material matKickBoardIn

		prism_ 5, _kickBoardThickness,
			-_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	0.01,				-1
	endif

	del 2
return

