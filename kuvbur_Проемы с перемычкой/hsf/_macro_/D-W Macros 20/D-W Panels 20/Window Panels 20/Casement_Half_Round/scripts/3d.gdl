

! ==============================================================================
! Simple Detail Level
! ==============================================================================

if	gs_optype_m = SASH_FIXED |\
	gs_optype_m = SASH_HOR_PIVOT |\
	gs_optype_m = SASH_VER_PIVOT |\
	gs_optype_m = SASH_VER_PIVOT_M |\
	gs_optype_m = SASH_SLIDING_HOR |\
	gs_optype_m = SASH_SLIDING_HOR_M |\
	gs_optype_m = SASH_DOUBLE_SLIDING_HOR |\
	gs_optype_m = SASH_DOUBLE_SLIDING_HOR_M |\
	gs_optype_m = SASH_SLIDING_VER |\
	gs_optype_m = SASH_DOUBLE_SLIDING_VER then

	if lod3D = 1 | lod3D = 5 | gs_optype_m = SASH_FIXED  then
		if iMullionStyle = MULLION_SOLID_PANEL then
			addz -gs_frame_thk
		else
			addy gs_rebate_width
		endif
		A = A - 2*gs_rebate_width
		B = B - 2*gs_rebate_width
	else
		if	gs_optype_m = SASH_HOR_PIVOT |\
			gs_optype_m = SASH_VER_PIVOT |\
			gs_optype_m = SASH_VER_PIVOT_M then
			addz -gs_sash_thk
		else
			addz -gs_frame_thk
		endif
	endif
else
	if iMullionStyle = MULLION_SOLID_PANEL & (lod3D = 1 | lod3D = 5) then
		addz -gs_frame_thk
	else
		if lod3D <> 1 & lod3D <> 5 then
			addz -gs_rebate_depth
		endif
	endif
endif

bShowFrame = 1
if lod3D = 1 | lod3D = 5 then

	bShowFrame = 0

	vgn = 1
	hgn = 1
	vgn2 = 1
	hgn2 = 1

	if iMullionStyle = MULLION_CUSTOM then
		iMullionStyle = MULLION_NONE
	endif

	gs_full_edit = 0
endif

if GLOB_CONTEXT = 6 then	! Window Schedule - 3D view should NOT display sash in open position
	openingAngle = 0
	openingAngle2 = 0
endif

resol res

if not(bOpeningDirectionInside) then
	refPointSign = -1
else
	refPointSign = 1
endif


!.......................................................................
if gs_optype_m = SASH_BOTTOM_HUNG then
	if gs_full_edit then
		muly	-1
		rotz 	-90
		rotx	-90

		if bEnableSEOpening then
			add 0,gs_rebate_depth - gs_sash_thk, 0
			if CasementID = 1 then
				hotspot b,											-gs_rebate_depth,													0, unID, openingAngle_01, 4+128 : unID=unID+1
				hotspot 0,											0,											refPointSign, 			 unID, openingAngle_01, 7 : unID=unID+1
				hotspot B*sin(90+openingAngle_01)+gs_rebate_depth*cos(90+openingAngle_01),B*cos(90+openingAngle_01)-gs_rebate_depth*sin(90+openingAngle_01),0, unID, openingAngle_01, 5 : unID=unID+1
				hotspot 0, 											0, 																	0, unID, openingAngle_01, 6 : unID=unID+1
			endif
			if CasementID = 2 then
				hotspot b,											-gs_rebate_depth,													0, unID, openingAngle_02, 4+128 : unID=unID+1
				hotspot 0,											0,											refPointSign, 			 unID, openingAngle_02, 7 : unID=unID+1
				hotspot B*sin(90+openingAngle_02)+gs_rebate_depth*cos(90+openingAngle_02),B*cos(90+openingAngle_02)-gs_rebate_depth*sin(90+openingAngle_02),0, unID, openingAngle_02, 5 : unID=unID+1
				hotspot 0, 											0, 																	0, unID, openingAngle_02, 6 : unID=unID+1
			endif
			del 1
		endif
		del 2

		if openingAngle>90 then openingAngle=0

		if openingAngle<>0 then
			addz gs_rebate_depth
			rotx -openingAngle
			addz -gs_rebate_depth
		endif

		muly	-1
	endif
endif


! ==============================================================================
! Opening Line
! ==============================================================================

if bOplines3d & (gs_optype_m <> SASH_FIXED) then

	oplineGap = 0

	iOpeningType	= gs_optype_m

	oplineWidth = a - 2 * gs_sash_width * (lod3D = 1 | lod3D = 5)

	if lod3D = 5 then	! Chinese
		opLineDist = gs_frame_thk + 2 * oplineGap
	else
		if	(iMullionStyle = MULLION_DIVIDED	|\
			 iMullionStyle = MULLION_CUSTOM		|\
			 iMullionStyle = MULLION_SOLID_PANEL	|\
			 lod3D <> 1) then

			opLineDist = gs_sash_thk + 2 * oplineGap
		else
			opLineDist = gs_glass_thk + 2 * oplineGap
		endif
	endif
	if gs_optype_m = SASH_SIDE_HUNG | gs_optype_m = SASH_SIDE_HUNG_M | gs_optype_m = SASH_TOP_HUNG | gs_optype_m = SASH_TILT_TURN | gs_optype_m = SASH_TILT_TURN_M  then
		oplineHeight = h - gs_sash_width * (lod3D = 1 | lod3D = 5)
	else
		oplineHeight  = B - 2 * gs_sash_width * (lod3D = 1 | lod3D = 5)
	endif

	if gs_optype_m = SASH_BOTTOM_HUNG & gs_opline_style_m = 2 then
		oplineHeight	= (a/2 - gs_sash_width * (lod3D = 1 | lod3D = 5)) * sin(45) - (gs_sash_width) * (lod3D = 1 | lod3D = 5) + gs_rebate_width
		oplineWidth		= (a - 2 * gs_sash_width * (lod3D = 1 | lod3D = 5)) * cos(45)
	endif

	if (lod3D = 1 | lod3D = 5) then	! Simple, Chinese
		if iMullionStyle = MULLION_SOLID_PANEL then
			addz gs_frame_thk/2
		else
			addz -gs_frame_thk/2
		endif
	else
		addz gs_sash_thk/2
	endif

	add -oplineWidth/2, gs_sash_width * (lod3D = 1 | lod3D = 5), - opLineDist/2
	call "OpeningLines" parameters a = oplineWidth,
				b					= oplineHeight,
				frameThickness		= opLineDist,
				iOpeningType		= iOpeningType,
				opLineType3dIn		= opLineType3dIn,
				opLineType3dOut		= opLineType3dOut,
				gs_opLinePen		= gs_opLinePen,
				gs_opline_style_m	= gs_opline_style_m
	del 2
endif


! =============================================================================
! 3D Window Model
! =============================================================================

sect_fill gs_fillSash, gs_penSash_bg, gs_penSash_fg, gs_frame_pen_cont


! =============================================================================
! Frame Model
! =============================================================================

var_mater		= gs_frame_mat	! Sash front material
var_mater2		= gs_frame_mat2	! Sash back material
var_mater_side	= gs_frame_mat	! Sash side material

if gs_frame_mat = gs_frame_mat2 then
	in_out_cikl = 1

	out_thk = 0
	part_thk = gs_sash_thk

	in_out_mask = 15
	in_out_mask2 = 13 + 2 * (gs_SashText_typ_num = 1)
	in_out_mask3 = 13 + 2 * (gs_SashText_typ_num = 1)
else												! two bodies
	in_out_cikl = 2

	if not(bOpeningDirectionInside) then
		tempMat = var_mater
		var_mater = var_mater2
		var_mater2 = tempMat

		var_mater_side	= gs_frame_mat2
	else
		var_mater_side	= gs_frame_mat
	endif

	if	gs_optype_m = SASH_SLIDING_HOR |\
		gs_optype_m = SASH_SLIDING_HOR_M |\
		gs_optype_m = SASH_DOUBLE_SLIDING_HOR |\
		gs_optype_m = SASH_DOUBLE_SLIDING_HOR_M |\
		gs_optype_m = SASH_SLIDING_VER |\
		gs_optype_m = SASH_DOUBLE_SLIDING_VER then

		if bSecondSash exor not(bOpeningDirectionInside) then
			var_mater_side	= gs_frame_mat2
		else
			var_mater_side	= gs_frame_mat
		endif
	endif

	if	gs_optype_m = SASH_SLIDING_HOR_M |\
		gs_optype_m = SASH_DOUBLE_SLIDING_HOR_M then

		if bSecondSash exor not(bOpeningDirectionInside) then
			var_mater_side	= gs_frame_mat
		else
			var_mater_side	= gs_frame_mat2
		endif
	endif

	out_thk = gs_sash_thk/2 - gs_glass_thk/2
	part_thk = out_thk

	in_out_mask = 11
	in_out_mask2 = 9
	in_out_mask3 = 9 + 2 * (gs_SashText_typ_num = 1)
endif

if iMullionStyle <> MULLION_CUSTOM & iMullionStyle <> MULLION_SOLID_PANEL then

	pen gs_frame_pen
	material var_mater

	if gs_optype_m <> SASH_FIXED then

		if bShowFrame then
			for ijk = 1 to in_out_cikl
				material var_mater2

				base
				sprism_{2} var_mater2,var_mater2,var_mater2,
					9,
					0,0, 0,1, 0, 0,
					0,0, 0,1, part_thk, 0,
					 A/2,0,							in_out_mask,		var_mater_side,
					 A/2,h + gs_rebate_width,		in_out_mask+64,		var_mater_side,
					 0, h + gs_rebate_width,		964+in_out_mask,	var_mater_side,
					 0,A/2 + gs_rebate_width,		3064 + in_out_mask,	var_mater_side,
					-A/2,h + gs_rebate_width,		3064+in_out_mask,	var_mater_side,
					-A/2,0,									10,			var_mater2,
					-A/2 + gs_sash_width, gs_sash_width,	79,			var_mater2,
					 0,B - gs_sash_width + gs_rebate_width,	3079,		var_mater2,
					 A/2 - gs_sash_width, gs_sash_width,	1010,		var_mater2
				vert 0, 0, 0
				vert 0, 1, 0
				vert 1, 0, 0
				vert 0, 0, 1
				coor 5, -1, -2, -3, -4
				body 1

				sprism_{2} var_mater2,var_mater2,var_mater2,
					4,
					0,0, 0,1, 0, 0,
					0,0, 0,1, part_thk,	0,
					 A/2,					0,				10,			var_mater2,
					 A/2 - gs_sash_width,	gs_sash_width,	15,			var_mater2,
					-A/2 + gs_sash_width,	gs_sash_width,	10,			var_mater_side,
					-A/2,					0,				in_out_mask,var_mater_side
				body -1

				addz part_thk
				part_thk = gs_sash_thk - out_thk

				tempMat = var_mater
				var_mater = var_mater2
				var_mater2 = tempMat

				in_out_mask = 14
				in_out_mask2 = 12
				in_out_mask3 = 12
			next ijk

			del in_out_cikl
		endif

	else
		pen gs_glass_pen
		material gs_glass_mat

		if lod3D = 5 then	! Chinese
			addz -gs_frame_thk
			gl_chi_thk = gs_frame_thk
			gl_chi_mask = 13
		else
			if lod3D = 1 then	! Simple
				addz -gs_frame_thk/2 - gs_glass_thk/2
			else
				addz -gs_rebate_depth - gs_glass_thk/2
			endif
			gl_chi_thk = gs_glass_thk
			gl_chi_mask = 5
		endif

		prism_ 6,gl_chi_thk,
			-a/2 + h,	h,	gl_chi_mask,
			 a/2 - h,	h,	gl_chi_mask,
			 0,			h,	900,
			 0,			90,	4000+gl_chi_mask,
			 0,			90,	4000+gl_chi_mask,
			-a/2 + h,	h,	-1
		body -1
		del 1
	endif


! =============================================================================
! Glazing
! =============================================================================

	if gs_optype_m <> SASH_FIXED then

		pen gs_glass_pen
		material gs_glass_mat

		if lod3D = 5 then	! chinese
			addz -gs_frame_thk
			gl_chi_thk = gs_frame_thk
			gl_chi_mask = 13
		else
			if lod3D = 1 then	! Simple
				addz -gs_frame_thk/2 - gs_glass_thk/2
			else
				addz gs_sash_thk/2 - gs_glass_thk/2
			endif
			gl_chi_thk = gs_glass_thk
			gl_chi_mask = 5
		endif

		prism_ 5,gl_chi_thk,
			-A/2 + gs_sash_width,	gs_sash_width,		gl_chi_mask,
			 A/2 - gs_sash_width,	gs_sash_width,		gl_chi_mask,
			 0,						h + gs_rebate_width,900,
			 0,						B - gs_sash_width,	3000 + gl_chi_mask,
			-A/2 + gs_sash_width,	gs_sash_width,		1000 + gl_chi_mask
		body -1

		del 1
	endif

	matGrillSide	= gs_grill_mat
	matGrillTop		= gs_grill_mat2
	matGrillBottom	= gs_grill_mat2

	if (gs_grill_mat = gs_grill_mat2) |\
		iMullionPosition <> MULLION_THROUGHOUT |\
		(iMullionPosition = MULLION_THROUGHOUT & gs_glass_thk >= gth) then

		in_out_cikl_gr	= 1

		part_thk_grill	= gth

		if iMullionPosition = MULLION_INSIDE then
			matGrillSide	= gs_grill_mat2
		else
			matGrillSide	= gs_grill_mat
		endif
		if not(bOpeningDirectionInside) then
			matGrillTop		= gs_grill_mat
			matGrillBottom	= gs_grill_mat2
		else
			matGrillTop		= gs_grill_mat2
			matGrillBottom	= gs_grill_mat
		endif
	else
		in_out_cikl_gr	= 2

		out_thk_grill	= gth/2 - gs_glass_thk/2
		part_thk_grill	= out_thk_grill

		if not(bOpeningDirectionInside) then
			matGrillSide	= gs_grill_mat2
			matGrillTop		= gs_grill_mat
			matGrillBottom	= gs_grill_mat2
		else
			matGrillSide	= gs_grill_mat2
			matGrillTop		= gs_grill_mat2
			matGrillBottom	= gs_grill_mat
		endif
		if iMullionPosition = MULLION_INSIDE exor not(bOpeningDirectionInside) then
			matGrillSide	= gs_grill_mat2
		else
			matGrillSide	= gs_grill_mat
		endif
	endif

	if iMullionStyle = MULLION_NONE then end


! =============================================================================
! Grids
! =============================================================================

	if lod3D <> 1 & lod3D <> 5 then

		if iMullionPosition = MULLION_THROUGHOUT then
			gz = (gs_optype_m <> SASH_FIXED) * gs_sash_thk/2 - gth/2
		else
			if (iMullionPosition = MULLION_INSIDE) exor not(bOpeningDirectionInside) then
				gz = (gs_optype_m <> SASH_FIXED) * gs_sash_thk/2 + gs_glass_thk/2
			else
				gz = (gs_optype_m <> SASH_FIXED) * gs_sash_thk/2 - gs_glass_thk/2 - gth
			endif
		endif
		gz = gz - (gs_optype_m = SASH_FIXED) * gs_rebate_depth

		if gs_optype_m <> SASH_FIXED then
			fix_sh_grmod = gs_sash_width
		else
			fix_sh_grmod = h
		endif

		! Glass pane width between grids
		hgnn = ((a - 2*fix_sh_grmod + gw)/(vgn + 1)) - gw

		pen gs_frame_pen
		material gs_grill_mat

		material matGrillSide
		cutpolya 9, 2, 0,
			-a/2 + fix_sh_grmod,		fix_sh_grmod,			79,
			-a/2 + fix_sh_grmod - 0.5,	fix_sh_grmod,			79,
			-a/2 + fix_sh_grmod - 0.5,	b + 0.5,				79,
			 a/2 - fix_sh_grmod + 0.5,	b + 0.5,				79,
			 a/2 - fix_sh_grmod + 0.5,	fix_sh_grmod,			79,
			 a/2 - fix_sh_grmod,		fix_sh_grmod,			79,
			 0,							h + (gs_optype_m <> SASH_FIXED) * gs_rebate_width,	900,
			 0,							B + fix_sh_grmod,		3079,
			-a/2 + fix_sh_grmod,		fix_sh_grmod,			1079


		for ijk= 1 to in_out_cikl_gr

			if ijk = 1 then
				currentGrillThickness = part_thk_grill
			else
				currentGrillThickness = gth - out_thk_grill
			endif

! -----------------------------------------------------------------------------
! H-V GRID
! -----------------------------------------------------------------------------

			if iMullionStyle = MULLION_HVGRID then

				! Vertical grids
				if vgn > 0 then
					for i=1 to vgn
						addx hgnn + gw

						add  -a/2 - gw/2 + fix_sh_grmod, 0, gz
						if gw > EPS then
							sprism_ matGrillTop,matGrillBottom,matGrillSide,
								5,
								0,0, 0,1, currentGrillThickness, 0,
								-gw/2, fix_sh_grmod,		15,
								 gw/2, fix_sh_grmod,		15,
								 gw/2, b - fix_sh_grmod,	15,
								-gw/2, b - fix_sh_grmod,	15,
								-gw/2, fix_sh_grmod,		-1
						else
							lin_ 0, fix_sh_grmod, 0,
								 0, b - fix_sh_grmod, 0
						endif
						del 1
					next i
					del vgn
				endif

				! Horizontal grids
				if hgn > 0 then
					for i=1 to vgn+1
						for j=1 to hgn
							addy (b - 2*fix_sh_grmod + gw)/(hgn + 1)

							add -a/2 + fix_sh_grmod + hgnn/2, fix_sh_grmod - gw/2, gz
							if gw > EPS then
								sprism_ matGrillTop,matGrillBottom,matGrillSide,
									5,
									0,0, 0,1, currentGrillThickness, 0,
									-hgnn/2,-gw/2,15,
									 hgnn/2,-gw/2,15,
									 hgnn/2, gw/2,15,
									-hgnn/2, gw/2,15,
									-hgnn/2,-gw/2,-1
							else
								lin_ -hgnn/2,0,0, hgnn/2,0,0
							endif
							del 1
						next j
						del hgn

						addx hgnn + gw
					next i
					del vgn+1
					body -1
				endif
			endif

! -----------------------------------------------------------------------------
! Sunburst Grids
! -----------------------------------------------------------------------------

			if	iMullionStyle = MULLION_SUNBURST_1 |\
				iMullionStyle = MULLION_SUNBURST_2 |\
				iMullionStyle = MULLION_SUNBURST_3 |\
				iMullionStyle = MULLION_SUNBURST_4 |\
				iMullionStyle = MULLION_SUNBURST_5 |\
				iMullionStyle = MULLION_SUNBURST_6 then

				if gw < EPS then gw = EPS

				if iMullionStyle = MULLION_SUNBURST_1 then
					addy h
					gsp=((a - 2*fix_sh_grmod)-(2*gw))/3
					cutpolya 7,2,0,
						 gsp/2+gw,0,15,
						 0,1,800,
						-gsp/2-gw,0,1015,
						-gsp/2-gw,0,15,
						-gsp/2-gw,0,15,
						 gsp/2+gw,0,15,
						 gsp/2+gw,0,-1
					del 1
				endif

				if gs_optype_m <> SASH_FIXED then addy gs_sash_width - gs_rebate_width

				addz gz

				!5 radial grids
				if iMullionStyle = MULLION_SUNBURST_4 then
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						-gw/2,h,15,
						 gw/2,h,15,
						 gw/2,b,15,
						-gw/2,b,15,
						-gw/2,h,15
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						gw/2,h,15,
						gw/2+gw/sin(60),h,15,
						gw/2+gw/sin(60)+b*cos(60),h+b*sin(60),15,
						gw/2+gw/sin(60)+b*cos(60)-gw*cos(60),h+b*sin(60)+gw*sin(60),15,
						gw/2,h,-1
					addx gw/sin(60)
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						gw/2,h,15,
						gw/2+gw/sin(30),h,15,
						gw/2+gw/sin(30)+b*cos(30),h+b*sin(30),15,
						gw/2+gw/sin(30)+b*cos(30)-gw*cos(30),h+b*sin(30)+gw*sin(30),15,
						gw/2,h,-1
					del 1
					mulx -1
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						gw/2,h,15,
						gw/2+gw/sin(60),h,15,
						gw/2+gw/sin(60)+b*cos(60),h+b*sin(60),15,
						gw/2+gw/sin(60)+b*cos(60)-gw*cos(60),h+b*sin(60)+gw*sin(60),15,
						gw/2,h,-1
					addx gw/sin(60)
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						gw/2,h,15,
						gw/2+gw/sin(30),h,15,
						gw/2+gw/sin(30)+b*cos(30),h+b*sin(30),15,
						gw/2+gw/sin(30)+b*cos(30)-gw*cos(30),h+b*sin(30)+gw*sin(30),15,
						gw/2,h,-1
					del 2
				endif

				!3 radial grids
				if iMullionStyle = MULLION_SUNBURST_1 or iMullionStyle = MULLION_SUNBURST_2 or iMullionStyle = MULLION_SUNBURST_3 or iMullionStyle = MULLION_SUNBURST_5 then
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						-gw/2,h,15,
						 gw/2,h,15,
						 gw/2,b,15,
						-gw/2,b,15,
						-gw/2,h,15
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						gw/2,h,15,
						gw/2+gw/sin(45),h,15,
						gw/2+gw/sin(45)+b*cos(45),h+b*sin(45),15,
						gw/2+gw/sin(45)+b*cos(45)-gw*cos(45),h+b*sin(45)+gw*sin(45),15,
						gw/2,h,-1
					mulx -1
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						5,
						0,0, 0,1, currentGrillThickness, 0,
						gw/2,h,15,
						gw/2+gw/sin(45),h,15,
						gw/2+gw/sin(45)+b*cos(45),h+b*sin(45),15,
						gw/2+gw/sin(45)+b*cos(45)-gw*cos(45),h+b*sin(45)+gw*sin(45),15,
						gw/2,h,-1
					del 1
				endif

				del 1

				if gs_optype_m <> SASH_FIXED then del 1


				if iMullionStyle = MULLION_SUNBURST_1 then
					cutend
				endif

				!! CURVED GRIDS !!

				if gs_optype_m <> SASH_FIXED then
					addy gs_sash_width - gs_rebate_width
					rotx 90
					cutplane
					del 2
				endif

				addz gz

				if iMullionStyle = MULLION_SUNBURST_1 then
					addy h
					gsp=((a - 2*fix_sh_grmod)-(2*gw))/3
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						9,
						0,0, 0,1, currentGrillThickness, 0,
						gsp/2,0,15,
						gsp/2+gw,0,79,
						0,1,800,
						-gsp/2-gw,0,1079,
						-gsp/2-gw,0,15,
						-gsp/2,0,79,
						0,1,800,
						gsp/2,0,1079,
						gsp/2,0,-1
					del 1
				endif


				if iMullionStyle = MULLION_SUNBURST_2 then
					if gs_optype_m <> SASH_FIXED then addy gs_sash_width - gs_rebate_width
					cutpolya 5,1,0,
						-gw/2,h,15,
						 gw/2,h,15,
						 gw/2,b,15,
						-gw/2,b,15,
						-gw/2,h,15
					cutpolya 5,1,0,
						gw/2,h,15,
						gw/2+gw/sin(45),h,15,
						gw/2+gw/sin(45)+b*cos(45),h+b*sin(45),15,
						gw/2+gw/sin(45)+b*cos(45)-gw*cos(45),h+b*sin(45)+gw*sin(45),15,
						gw/2,h,-1
					mulx -1
					cutpolya 5,1,0,
						gw/2,h,15,
						gw/2+gw/sin(45),h,15,
						gw/2+gw/sin(45)+b*cos(45),h+b*sin(45),15,
						gw/2+gw/sin(45)+b*cos(45)-gw*cos(45),h+b*sin(45)+gw*sin(45),15,
						gw/2,h,-1
					del 1
					if gs_optype_m <> SASH_FIXED then del 1


					gsp=((a - 2*fix_sh_grmod)-(3*gw))/4

					addy h
					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						9,
						0,0, 0,1, currentGrillThickness, 0,
						gsp+gw/2,0,15,
						gsp+gw/2+gw,0,79,
						0,1,800,
						-gsp-gw/2-gw,0,1079,
						-gsp-gw/2-gw,0,15,
						-gsp-gw/2,0,79,
						0,1,800,
						gsp+gw/2,0,1079,
						gsp+gw,0,-1
					del 1

					cutend
					cutend
					cutend
				endif


				if iMullionStyle = MULLION_SUNBURST_3 then
					if gs_optype_m <> SASH_FIXED then addy gs_sash_width - gs_rebate_width
					cutpolya 5,1,0,
						-gw/2,h,15,
						 gw/2,h,15,
						 gw/2,b,15,
						-gw/2,b,15,
						-gw/2,h,15
					cutpolya 5,1,0,
						gw/2,h,15,
						gw/2+gw/sin(45),h,15,
						gw/2+gw/sin(45)+b*cos(45),h+b*sin(45),15,
						gw/2+gw/sin(45)+b*cos(45)-gw*cos(45),h+b*sin(45)+gw*sin(45),15,
						gw/2,h,-1
					mulx -1
					cutpolya 5,1,0,
						gw/2,h,15,
						gw/2+gw/sin(45),h,15,
						gw/2+gw/sin(45)+b*cos(45),h+b*sin(45),15,
						gw/2+gw/sin(45)+b*cos(45)-gw*cos(45),h+b*sin(45)+gw*sin(45),15,
						gw/2,h,-1
					del 1
					if gs_optype_m <> SASH_FIXED then del 1

					gsp=((a - 2*fix_sh_grmod)-(4*gw))/5

					addy h

					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						9,
						0,0, 0,1, currentGrillThickness, 0,
						gsp/2,0,15,
						gsp/2+gw,0,79,
						0,1,800,
						-gsp/2-gw,0,1079,
						-gsp/2-gw,0,15,
						-gsp/2,0,79,
						0,1,800,
						gsp/2,0,1079,
						gsp/2,0,-1

					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						9,
						0,0, 0,1, currentGrillThickness, 0,
						gsp/2+gw+gsp,0,15,
						gsp/2+gw+gsp+gw,0,79,
						0,1,800,
						-gsp/2-gw-gsp-gw,0,1079,
						-gsp/2-gw-gsp-gw,0,15,
						-gsp/2-gw-gsp,0,79,
						0,1,800,
						gsp/2+gw+gsp,0,1079,
						-gsp/2-gw-gsp,0,-1
					del 1

					cutend
					cutend
					cutend
				endif


				if iMullionStyle = MULLION_SUNBURST_4 then
					if gs_optype_m <> SASH_FIXED then addy gs_sash_width - gs_rebate_width
					cutpolya 5,1,0,
						-gw/2,h,15,
						 gw/2,h,15,
						 gw/2,b,15,
						-gw/2,b,15,
						-gw/2,h,15

					cutpolya 5,1,0,
						 gw/2,h,15,
						 gw/2+gw/sin(60),h,15,
						 gw/2+gw/sin(60)+b*cos(60),h+b*sin(60),15,
						 gw/2+gw/sin(60)+b*cos(60)-gw*cos(60),h+b*sin(60)+gw*sin(60),15,
						 gw/2,h,-1

					addx gw/sin(60)
					cutpolya 5,1,0,
						 gw/2,h,15,
						 gw/2+gw/sin(30),h,15,
						 gw/2+gw/sin(30)+b*cos(30),h+b*sin(30),15,
						 gw/2+gw/sin(30)+b*cos(30)-gw*cos(30),h+b*sin(30)+gw*sin(30),15,
						 gw/2,h,-1
					del 1

					mulx -1

					cutpolya 5,1,0,
						 gw/2,h,15,
						 gw/2+gw/sin(60),h,15,
						 gw/2+gw/sin(60)+b*cos(60),h+b*sin(60),15,
						 gw/2+gw/sin(60)+b*cos(60)-gw*cos(60),h+b*sin(60)+gw*sin(60),15,
						 gw/2,h,-1

					addx gw/sin(60)
					cutpolya 5,1,0,
						 gw/2,h,15,
						 gw/2+gw/sin(30),h,15,
						 gw/2+gw/sin(30)+b*cos(30),h+b*sin(30),15,
						 gw/2+gw/sin(30)+b*cos(30)-gw*cos(30),h+b*sin(30)+gw*sin(30),15,
						 gw/2,h,-1
					del 1

					del 1
					if gs_optype_m <> SASH_FIXED then del 1

					gsp=((a - 2*fix_sh_grmod)-(5*gw))/6

					addy h

					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						9,
						0,0, 0,1, currentGrillThickness, 0,
						gsp+gw/2,0,15,
						gsp+gw/2+gw,0,79,
						0,1,800,
						-gsp-gw/2-gw,0,1079,
						-gsp-gw/2-gw,0,15,
						-gsp-gw/2,0,79,
						0,1,800,
						gsp+gw/2,0,1079,
						gsp+gw,0,-1

					sprism_ matGrillTop,matGrillBottom,matGrillSide,
						9,
						0,0, 0,1, currentGrillThickness, 0,
						gsp+gw/2+gw+gsp,0,15,
						gsp+gw/2+gw+gw+gsp,0,79,
						0,1,800,
						-gsp-gw/2-gw-gw-gsp,0,1079,
						-gsp-gw/2-gw-gw-gsp,0,15,
						-gsp-gw/2-gw-gsp,0,79,
						0,1,800,
						gsp+gw/2+gw+gsp,0,1079,
						gsp+gw+gw+gsp,0,-1
					del 1


					cutend
					cutend
					cutend
					cutend
					cutend
				endif

				del 1

				if gs_optype_m <> SASH_FIXED then cutend
			endif

! -----------------------------------------------------------------------------

			addz currentGrillThickness

			matGrillSide = matGrillTop

		next ijk
		del in_out_cikl_gr

		cutend

	endif
endif

! =============================================================================
! Solid Panel
! =============================================================================
if iMullionStyle = MULLION_SOLID_PANEL then

	if gs_optype_m = SASH_FIXED then
		_rebate_w = gs_rebate_width
		_rebate_w2 = gs_rebate_width
	else
		if lod3D = 1 | lod3D = 5 then
			_rebate_w = gs_sash_width
			_rebate_w2 = gs_sash_width
		else
			_rebate_w = 0
			_rebate_w2 = gs_rebate_width
		endif
	endif
	if gs_optype_m <> SASH_FIXED & (lod3D = 1 | lod3D = 5) then
		A = A - 2* gs_sash_width
	endif

	sprism_{2} gs_panel_mat2, gs_panel_mat,gs_panel_mat,
		6,
		0,0, 0,1, 0, 0,
		0,0, 0,1, gs_sash_thk, 0,
		 A/2,	_rebate_w,					in_out_mask,		gs_panel_mat2,
		 A/2,	h + _rebate_w2,				in_out_mask+64,		gs_panel_mat2,
		 0,		h + gs_rebate_width,		964+in_out_mask,	gs_panel_mat2,
		 0,		A/2 + gs_rebate_width,		3064 + in_out_mask,	gs_panel_mat2,
		-A/2,	h + _rebate_w2,				3064+in_out_mask,	gs_panel_mat2,
		-A/2,	_rebate_w,					in_out_mask,		gs_panel_mat2
endif


! =============================================================================
! Custom Panel
! =============================================================================

if iMullionStyle = MULLION_CUSTOM then
	if gs_cust_panel <> "" then

		if gs_optype_m <> SASH_FIXED then
			fix_sh_grmod = 0
		else
			fix_sh_grmod = -h
		endif

		cutpolya 10, 1, 0,
			 A/2 + 1,	h, 31,
			 A/2+fix_sh_grmod,		h,	95,
			 A/2+fix_sh_grmod,		h + (gs_optype_m <> SASH_FIXED) * gs_rebate_width,	95,
			 0,			h + (gs_optype_m <> SASH_FIXED) * gs_rebate_width, 995,
			 0, 		B-fix_sh_grmod, 3095,
			-A/2-fix_sh_grmod, 		h + (gs_optype_m <> SASH_FIXED) * gs_rebate_width, 3095,
			-A/2-fix_sh_grmod, 		h, 95,
			-A/2 - 1,	h, 31,
			-A/2 - 1,	B + 1, 31,
			 A/2 + 1,	B + 1, 31

		if gs_optype_m = SASH_FIXED then
			addz -gs_rebate_depth - gs_glass_thk/2
			panelThk = gs_glass_thk
		else
			panelThk = gs_sash_thk
		endif

		add -A/2-fix_sh_grmod, -fix_sh_grmod, 0
		call "Window_Panel_Collection" parameters a=a+2*fix_sh_grmod, b=b+2*fix_sh_grmod, zzyzx=panelThk, gs_ptype=gs_cust_panel
		del 1

		if gs_optype_m = SASH_FIXED then del 1

		cutend
	endif
endif

