

! ==============================================================================
! Simple Detail Level
! ==============================================================================

if	gs_optype_m = SASH_FIXED |\
	gs_optype_m = SASH_HOR_PIVOT |\
	gs_optype_m = SASH_VER_PIVOT |\
	gs_optype_m = SASH_VER_PIVOT_M |\
	gs_optype_m = SASH_SLIDING_HOR |\
	gs_optype_m = SASH_SLIDING_HOR_M |\
	gs_optype_m = SASH_DOUBLE_SLIDING_HOR |\
	gs_optype_m = SASH_DOUBLE_SLIDING_HOR_M |\
	gs_optype_m = SASH_SLIDING_VER |\
	gs_optype_m = SASH_DOUBLE_SLIDING_VER then

	if lod3D = 1 | lod3D = 5 | gs_optype_m = SASH_FIXED then
		addy gs_rebate_width
		A = A - 2*gs_rebate_width
		B = B - 2*gs_rebate_width
		if iMullionStyle = MULLION_SOLID_PANEL then
			addz -gs_frame_thk
		endif
	else
		if	gs_optype_m = SASH_HOR_PIVOT |\
			gs_optype_m = SASH_VER_PIVOT |\
			gs_optype_m = SASH_VER_PIVOT_M then
			addz -gs_sash_thk
		else
			addz -gs_frame_thk
		endif
	endif
else
	if lod3D <> 1 & lod3D <> 5 then
		addz -gs_rebate_depth
	endif
endif

if lod3D = 1 | lod3D = 5 then
	vgn = 0
	hgn = 0

	if iMullionStyle = MULLION_CUSTOM then
		iMullionStyle = MULLION_NONE
	endif

	gs_full_edit = 0
endif


gs_sash_width = gs_sash_width * (gs_optype_m <> SASH_FIXED)

if abs(oval_unID)<EPS then
	unID = 0
else
	unID = oval_unID
endif

if GLOB_CONTEXT = 6 then	! Window Schedule - 3D view should NOT display sash in open position
	openingAngle_01 = 0
endif

resol res


! =============================================================================
! Open the Window
! =============================================================================

if gs_full_edit then
	if gs_optype_m = SASH_VER_PIVOT then	!fenti-tengely
		addy b/2
		rotx	-90
		addy gs_sash_thk/2

		if bEnableSEOpening then
			hotspot gs_sash_thk*sin(135)+a/2*sin(90), gs_sash_thk*cos(135)+a/2*cos(90), (0), unID, openingAngle_01, 4+128 : unID=unID+1
			hotspot gs_sash_thk*sin(90)+a/2*sin(0), gs_sash_thk*cos(90)+a/2*cos(0), 0, unID, openingAngle_01, 7 : unID=unID+1
			hotspot gs_sash_thk*sin(180+openingAngle_01)+a/2*sin(90+openingAngle_01), gs_sash_thk*cos(180+openingAngle_01)+a/2*cos(90+openingAngle_01), (0), unID, openingAngle_01, 5 : unID=unID+1
			hotspot 0, -gs_sash_thk, (0), unID, openingAngle_01, 6 : unID=unID+1
		endif

		del 2

		if abs(openingAngle_01)>EPS then
			addz gs_sash_thk/2
			roty -openingAngle_01
			addz -(gs_sash_thk/2)
		endif
		addy -b/2
	endif
endif


! =============================================================================
! Handling Inner-outer Sash
! =============================================================================

if gs_frame_mat = gs_frame_mat2 then				! One body, in case of similar materials
	in_out_cikl = 1

	gs_frame_mat2 = gs_frame_mat

	out_thk = 0
	part_thk = gs_sash_thk

	in_out_mask = 79
else												! two bodies
	in_out_cikl = 2

	! Inner-outer Material Control
	if bOpeningDirectionInside then
		temp_frame_mat	= gs_frame_mat
		gs_frame_mat	= gs_frame_mat2
		gs_frame_mat2	= temp_frame_mat
	endif

	out_thk = gs_sash_thk/2
	part_thk = out_thk

	in_out_mask = 75
endif
var_mater = gs_frame_mat
var_mater2 = gs_frame_mat2


! =============================================================================
! 3D Window Model
! =============================================================================

sect_fill gs_fillSash, gs_penSash_bg, gs_pensash_fg, gs_frame_pen_cont


! =============================================================================
! Frame
! =============================================================================

pen gs_frame_pen
material gs_frame_mat

if ((iMullionStyle <> MULLION_CUSTOM & iMullionStyle <> MULLION_SOLID_PANEL) | abs(gs_glass_thk) < EPS) & (lod3D = 2) & (gs_optype_m <> SASH_FIXED) then

	alpha_rotation = 180 / res
	if (res mod 2) = 0 then
		alpha_rotation = 0
	endif

	innerRadius	= A/2 - gs_sash_width

	addy B/2
	for ijk = 1 to in_out_cikl
		material var_mater

		__mat = var_mater
		gosub "geomerty_of_frame"

		sprism_{2} var_mater2,var_mater,var_mater2,
			nsp/4,
			0,0, 0,1, 0, 0,
			0,0, 0,1, part_thk, 0,
			get(nsp)

		body -1


		addz part_thk
		part_thk= gs_sash_thk-out_thk

		var_mater = gs_frame_mat2
		var_mater2 = gs_frame_mat2

		in_out_mask = 78

	next ijk
	del in_out_cikl
	del 1
endif


! =============================================================================
! Grill
! =============================================================================

if iMullionStyle <> MULLION_CUSTOM & iMullionStyle <> MULLION_SOLID_PANEL then

	pen gs_sash_pen

	if iMullionStyle <> MULLION_NONE then

		!!! ________________Handling Inner-outer Grill_____________________
		if gs_grill_mat = gs_grill_mat2 then				! One body, in case of similar materials
			in_out_cikl_gr = 1

			var_mater_grill = gs_grill_mat
			var_mater2_grill = gs_grill_mat

			part_thk_grill = gth
			out_thk_grill = 0
		else
			!!! Inner-outer Material Control-----------
			temp_grill_mat = gs_grill_mat
			temp_grill_mat2 = gs_grill_mat2
			if not(bOpeningDirectionInside) then
				gs_grill_mat=temp_grill_mat
				gs_grill_mat2=temp_grill_mat2
			else
				gs_grill_mat=temp_grill_mat2
				gs_grill_mat2=temp_grill_mat
			endif
			!!!----------------------------------------

			var_mater_grill = gs_grill_mat
			var_mater2_grill = gs_grill_mat2

			if iMullionPosition = MULLION_INSIDE or iMullionPosition = MULLION_OUTSIDE or (iMullionPosition = MULLION_THROUGHOUT and gs_glass_thk>=gth ) then		!!Inside, Outside position
				in_out_cikl_gr = 1
				out_thk_grill = gth
				part_thk_grill = out_thk_grill
			else									!!Throughout position
				in_out_cikl_gr = 2
				out_thk_grill = gth/2 - gs_glass_thk/2
				part_thk_grill = out_thk_grill
			endif
		endif

		if iMullionPosition = MULLION_INSIDE then
			side_mat = var_mater2_grill
		else
			side_mat = var_mater_grill
		endif

		! Glass pane width between grids
		hgnn=((a-2*gs_sash_width+gw)/(vgn+1))-gw

		if iMullionPosition = MULLION_THROUGHOUT then
			gz = (gs_optype_m <> SASH_FIXED) * gs_sash_thk/2 - gth/2
		else
			if (iMullionPosition = MULLION_INSIDE) exor not(bOpeningDirectionInside) then
				gz = (gs_optype_m <> SASH_FIXED) * gs_sash_thk/2 + gs_glass_thk/2
			else
				gz = (gs_optype_m <> SASH_FIXED) * gs_sash_thk/2 - gs_glass_thk/2 - gth
			endif
		endif
		gz = gz - (gs_optype_m = SASH_FIXED) * gs_rebate_depth

		pen gs_sash_pen
		material gs_grill_mat


		!! Model Solid

		!Cutplanes at curve

		beta=360/res
		dev=(a/2-gs_sash_width)-(a/2-gs_sash_width)*cos(beta/2)

		addy b/2
		muly (b-2*gs_sash_width)/(a-2*gs_sash_width)
		rotz beta/2
		for i=1 to res step 1
			addx a/2-gs_sash_width-dev
			cutplane 0,1,1,0
			del 1
			rotz beta
		next i
		del res+3


		!VERTICAL GRIDS

		for ijk = 1 to in_out_cikl_gr
			add(a/2-gs_sash_width)+gw/2, 0, gz

			for i=1 to vgn
				addx -hgnn-gw

				if gw > EPS then
					sprism_ var_mater2_grill,var_mater_grill,side_mat,
						5,
						0,0, 0,1, part_thk_grill, 0,
						-gw/2,gs_sash_width,15,
						 gw/2,gs_sash_width,15,
						 gw/2,b-gs_sash_width,15,
						-gw/2,b-gs_sash_width,15,
						-gw/2,gs_sash_width,-1
				else
					lin_ 0, gs_sash_width, 0, 0, b-gs_sash_width, 0
				endif
			next i
			del 1+vgn	!vgn

			!HORIZONTAL GRIDS

			for i=1 to vgn+1
				for j=1 to hgn
					addy (b-2*gs_sash_width+gw)/(hgn+1)

					add -(a-2*gs_sash_width)/2+hgnn/2, gs_sash_width-gw/2, gz
					if gw > EPS then
						sprism_ var_mater2_grill,var_mater_grill,side_mat,
							5,
							0,0, 0,1, part_thk_grill, 0,
							-hgnn/2,-gw/2,15,
							 hgnn/2,-gw/2,15,
							 hgnn/2, gw/2,15,
							-hgnn/2, gw/2,15,
							-hgnn/2,-gw/2,-1
					else
						lin_ -hgnn/2, 0, 0, hgnn/2, 0, 0
					endif
					del 1
				next j
				del hgn

				addx (a-2*gs_sash_width+gw)/(vgn+1)
			next i
			del vgn+1
			body -1

			addz part_thk_grill

			part_thk_grill = gth - out_thk_grill

			var_mater_grill = gs_grill_mat2
			var_mater2_grill = gs_grill_mat2
			side_mat = gs_grill_mat2

		next ijk
		del in_out_cikl_gr

		for i=1 to res step 1
			cutend
		next i
	endif


! =============================================================================
! Glazing
! =============================================================================

	if abs(gs_glass_thk) > EPS then
		pen gs_glass_pen
		material gs_glass_mat

		if gs_optype_m <> SASH_FIXED then
			if lod3D = 5 then	! Chinese
				addz -gs_frame_thk
				gl_chi_thk = gs_frame_thk
				gl_chi_mask = 13
			else
				if lod3D = 1 then	! Simple
					addz -gs_frame_thk/2 - gs_glass_thk/2
				else
					addz gs_sash_thk/2-gs_glass_thk/2
				endif
				gl_chi_thk = gs_glass_thk
				gl_chi_mask = 5
			endif
		else
			if lod3D = 5 then	! Chinese
				addz -gs_frame_thk
				gl_chi_thk = gs_frame_thk
				gl_chi_mask = 13
			else
				if lod3D = 1 then	! Simple
					addz -gs_frame_thk/2 - gs_glass_thk/2
				else
					addz -gs_rebate_depth - gs_glass_thk/2
				endif
				gl_chi_thk = gs_glass_thk
				gl_chi_mask = 5
			endif
		endif

		addy b/2
		muly (b-2*gs_sash_width)/(a-2*gs_sash_width)
		prism_ 2,gl_chi_thk,
			0,0,900+gl_chi_mask,
			a/2-gs_sash_width,360,4000+gl_chi_mask
		body -1
		del 2

		del 1
	endif
endif


! ==============================================================================
! Opening Line
! ==============================================================================

if bOplines3d & (gs_optype_m <> SASH_FIXED) then
	oplineGap = 0

	if lod3D = 5 then
		opLineDist = gs_frame_thk + 2 * oplineGap
		frameWidth = gs_sash_width
	else
		if lod3D = 1 then
			opLineDist = gs_glass_thk + 2 * oplineGap
			frameWidth = gs_sash_width
		else
			opLineDist = gs_sash_thk + 2 * oplineGap
			frameWidth = 0
		endif
	endif

	if (lod3D = 1 | lod3D = 5) then	! Simple, Chinese
		addz -gs_frame_thk/2
	else
		addz gs_sash_thk/2
	endif

	add -a/2 + frameWidth, frameWidth, - opLineDist/2
	call "OpeningLines" parameters a = a - 2 * frameWidth,
				b					= b - 2 * frameWidth,
				frameThickness		= opLineDist,
				iOpeningType		= gs_optype_m,
				opLineType3dIn		= opLineType3dIn,
				opLineType3dOut		= opLineType3dOut,
				gs_opLinePen		= gs_opLinePen,
				gs_opline_style_m	= gs_opline_style_m
	del 2
endif

! =============================================================================
! Solid Panel
! =============================================================================

if iMullionStyle = MULLION_SOLID_PANEL then

	__mat = gs_panel_mat2
	gosub "geomerty_of_frame"

	addy b/2

	sprism_{2} gs_panel_mat2, gs_panel_mat,gs_panel_mat,
		nsp/4,
		0,0, 0,1, 0, 0,
		0,0, 0,1, gs_sash_thk, 0,
		get(nsp)

	del 1
endif

! =============================================================================
! Custom Panel
! =============================================================================

if iMullionStyle = MULLION_CUSTOM & gs_cust_panel <> "" then
	beta=360/res
	dev=(a/2)-(a/2)*cos(beta/2)

	addy b/2
	muly b/a
	rotz beta/2
	for i=1 to res step 1
		addx a/2-dev
		cutplane 0,1,1,0
		del 1
		rotz beta
	next i
	del res+3

	if gs_optype_m = SASH_FIXED then
		addz -gs_rebate_depth - gs_glass_thk/2
		panelThk = gs_glass_thk
	else
		panelThk = gs_sash_thk
	endif

	addx -A/2
	call "Window_Panel_Collection" parameters a=a, b=b, zzyzx=panelThk, gs_ptype=gs_cust_panel
	del 1

	if gs_optype_m = SASH_FIXED then del 1

	for i=1 to res step 1
		cutend
	next i
endif


if gs_full_edit and gs_optype_m = SASH_VER_PIVOT then
	if abs(openingAngle_01)<EPS then del 2 else del 5
endif


oval_unID = unID

end oval_unID


end
! =============================================================================
"geomerty_of_frame":
! =============================================================================
	for jj = 1 to res
		put A/2 * sin (alpha_rotation + (jj-1) * (360 / res)), A/2 * B/A * cos (alpha_rotation + (jj-1) * (360 / res)), in_out_mask, __mat
	next jj
	put A/2 * sin (alpha_rotation), A/2 * B/A * cos (alpha_rotation), -1, __mat
	for jj = 1 to res
		put innerRadius * sin (alpha_rotation + (jj-1) * (360 / res)), A/2 * B/A * cos (alpha_rotation + (jj-1) * (360 / res)) - \
			(gs_sash_width) * cos (alpha_rotation + (jj-1) * (360 / res)), in_out_mask, __mat
	next jj
	put innerRadius * sin (alpha_rotation), A/2 * B/A * cos (alpha_rotation) - \
			(gs_sash_width) * cos (alpha_rotation), -1, __mat

return
