

dim st_uiDoorSettModAttr[5]
	st_uiDoorSettModAttr[1] = `Покрытия Коробки и Полотна`
	st_uiDoorSettModAttr[2] = `Покрытия Ручки`
	st_uiDoorSettModAttr[3] = `Покрытие Защитной Пластины`
	st_uiDoorSettModAttr[4] = `Перья Коробки и Створок в 3D`
	st_uiDoorSettModAttr[5] = `Приточный Клапан`


bSingleDoor = 0
if	gs_doorcode = "Gar1" |\
	gs_doorcode = "Gar2" |\
	gs_doorcode = "Gar4" |\
	gs_highspeedgar = 1 then bSingleDoor = 1

if	gs_doorcode = "Gar1" |\
	gs_doorcode = "Gar2" |\
	gs_doorcode = "Gar3" |\
	gs_doorcode = "Gar4" then 

	titleFrame = `Коробка и Полотно`
else
	titleFrame = `Коробка Двери`
endif

titlePanel = `Полотно Двери`


! =============================================================================
! Tabpage Structure Definition
! =============================================================================

! call all available page fix IDs
call "ui_tabIDlistHierarchy_m" parameters  iUITreeStandardType = 10,		!INT_STANDARD_DOOR_UI
		returned_parameters iUiID_Root,
							iUiID_Shape,			iParID_Shape,
							iUiID_NominalSizes, 	iParID_NominalSizes,
							iUiID_DoorSettOpening,	iParID_DoorSettOpening,
							iUiID_FrameSettings,	iParID_FrameSettings,
							iUiID_VentProfile,		iParID_VentProfile,
							iUiID_FrameSettingsGar,	iParID_FrameSettingsGar,
							iUiID_VentProfileGar,	iParID_VentProfileGar,
							iUiID_LeafSettingsGar,	iParID_LeafSettingsGar,
							iUiID_LeafSettingsGar2,	iParID_LeafSettingsGar2,
							iUiID_LeafTypeHandle,	iParID_LeafTypeHandle,
                            iUiID_LeafSymbol_CHI,	iParID_LeafSymbol_CHI,
							iUiID_SidePanel,		iParID_SidePanel,
							iUiID_HandleAndKickBoard,	iParID_HandleAndKickBoard,
							iUiID_LeafTypeHandle2,	iParID_LeafTypeHandle2,
							iUiID_PassDoorHandle,	iParID_PassDoorHandle,
							iUiID_LeafSashDimensions,	iParID_LeafSashDimensions,
							iUiID_SideSashes,		iParID_SideSashes,
							iUiID_OpeningType,		iParID_OpeningType,
							iUiID_OpLinesDoor,		iParID_OpLinesDoor,
							iUiID_OpLinesSideSashes,	iParID_OpLinesSideSashes,
							iUiID_OpeningGar,		iParID_OpeningGar,
							iUiID_DoorSettOpModAttr,	iParID_DoorSettOpModAttr,
							
							! Wall Opening group
							iUiID_WallOpening,		iParID_WallOpening,
							iUiID_Reveal,			iParID_Reveal,
							iUiID_Closure,			iParID_Closure,
							iUiID_MasonryArch,		iParID_MasonryArch,
							iUiID_Tolerances,		iParID_Tolerances,
							iUiID_WallOpModelAttr,	iParID_WallOpModelAttr,
							
							! Fixtures and Fittings group
							iUiID_FixtureFitting,	iParID_FixtureFitting,
							iUiID_Threshold,		iParID_Threshold,
							iUiID_ThresholdSill,	iParID_ThresholdSill,
							iUiID_Sill,				iParID_Sill,
							iUiID_Board,			iParID_Board,
							iUiID_CasingOut,		iParID_CasingOut,
							iUiID_CasingIn,			iParID_CasingIn,
							iUiID_Shutter,			iParID_Shutter,
							iUiID_FixFittingModAttr, iParID_FixFittingModAttr,
				
							iUiID_FloorPlanSection,	iParID_FloorPlanSection,
							iUiID_MinSpace,			iParID_MinSpace,
							iUiID_Descriptions,		iParID_Descriptions


dim uiUsedPageIDs[][2]
dim uiUsedPageNames[][2]

idxPage = 1

! --------------------------------------------------------------------------------
! Independent pages
! --------------------------------------------------------------------------------
uiUsedPageIDs[idxPage][1]	= iUiID_NominalSizes		: uiUsedPageIDs[idxPage][2]		= iParID_NominalSizes
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

! --------------------------------------------------------------------------------
! Door Settings and Opening group
! --------------------------------------------------------------------------------

uiUsedPageIDs[idxPage][1]	= iUiID_DoorSettOpening			: uiUsedPageIDs[idxPage][2]		= iParID_DoorSettOpening
uiUsedPageNames[idxPage][1]	= `Параметры Двери и Открывания`	: uiUsedPageNames[idxPage][2]	= "uiTab_DoorSettings_flat(1)"
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_FrameSettingsGar	: uiUsedPageIDs[idxPage][2]		= iParID_FrameSettingsGar
uiUsedPageNames[idxPage][1]	= titleFrame				: uiUsedPageNames[idxPage][2]	= "uiTab_DoorFrameStyle_flat(1)"
idxPage = idxPage + 1

if gs_doorcode <> "HSRD" then
	uiUsedPageIDs[idxPage][1]	= iUiID_LeafSettingsGar	: uiUsedPageIDs[idxPage][2]	= iParID_LeafSettingsGar
	uiUsedPageNames[idxPage][1]	= titlePanel			: uiUsedPageNames[idxPage][2]	= "uiTab_DoorLeaf_flat(1)"
	idxPage = idxPage + 1
endif

! Display tabpage for Chinese Leaf Symbol parameters if any of them are enabled
if bEnableShowHandle or bEnableShowGlassPanel or bEnableShowAirtightPanel then
	uiUsedPageIDs[idxPage][1]	= iUiID_LeafSettingsGar2			: uiUsedPageIDs[idxPage][2]		= iParID_LeafSettingsGar2
	uiUsedPageNames[idxPage][1]	= `Параметры 2D-символа Полотна Двери `	: uiUsedPageNames[idxPage][2]	= "uiTab_DoorLeaf_flat(1)"
	idxPage = idxPage + 1
endif

if bEnableVentilationProfile then
	uiUsedPageIDs[idxPage][1]	= iUiID_VentProfileGar		: uiUsedPageIDs[idxPage][2]		= iParID_VentProfileGar
	uiUsedPageNames[idxPage][1]	= `Приточная Вентиляция`	: uiUsedPageNames[idxPage][2]	= "uiTab_OpeningVentilation_flat(1)"
	idxPage = idxPage + 1
endif

if not(gs_highspeedgar) then
	_parIDGarage	= iUiID_LeafSettingsGar

	uiUsedPageIDs[idxPage][1]	= iUiID_HandleAndKickBoard	: uiUsedPageIDs[idxPage][2]		= _parIDGarage
	uiUsedPageNames[idxPage][1]	= `Ручка и Защитная Пластина`	: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontHandle_flat(1)"
	idxPage = idxPage + 1
endif

if gs_sectgar then
	uiUsedPageIDs[idxPage][1]	= iUiID_PassDoorHandle		: uiUsedPageIDs[idxPage][2]		= iParID_PassDoorHandle
	uiUsedPageNames[idxPage][1]	= `Проходная Ручка`		: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontHandle_flat(1)"
	idxPage = idxPage + 1
endif

! override regular iParID_OpLinesDoor with iUiID_DoorSettOpening, different hierarchy applied in garage doors
iParID_OpLinesDoor = iUiID_DoorSettOpening
uiUsedPageIDs[idxPage][1]	= iUiID_OpLinesDoor			: uiUsedPageIDs[idxPage][2]		= iParID_OpLinesDoor
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_DoorSettOpModAttr	: uiUsedPageIDs[idxPage][2]	= iParID_DoorSettOpModAttr
uiUsedPageNames[idxPage][1]	= `Реквизиты Модели`		: uiUsedPageNames[idxPage][2]	= "uiTab_3DRepr_flat(1)"
idxPage = idxPage + 1

! --------------------------------------------------------------------------------
! Wall Opening group
! --------------------------------------------------------------------------------
uiUsedPageIDs[idxPage][1]	= iUiID_WallOpening			: uiUsedPageIDs[idxPage][2]		= iParID_WallOpening
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

if not(gs_highspeedgar) & not(gs_sectgar) then
	uiUsedPageIDs[idxPage][1]	= iUiID_Reveal				: uiUsedPageIDs[idxPage][2]		= iParID_Reveal
	uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_Closure				: uiUsedPageIDs[idxPage][2]		= iParID_Closure
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_MasonryArch		: uiUsedPageIDs[idxPage][2]		= iParID_MasonryArch
uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_WallOpModelAttr		: uiUsedPageIDs[idxPage][2]		= iParID_WallOpModelAttr
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

if gs_garage & not (gs_sectgar) then
	! --------------------------------------------------------------------------------
	! Fixtures and Fittings group
	! --------------------------------------------------------------------------------
	uiUsedPageIDs[idxPage][1]	= iUiID_FixtureFitting		: uiUsedPageIDs[idxPage][2]		= iParID_FixtureFitting
	uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1


	uiUsedPageIDs[idxPage][1]	= iUiID_CasingOut		: uiUsedPageIDs[idxPage][2]		= iParID_CasingOut
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1

	uiUsedPageIDs[idxPage][1]	= iUiID_CasingIn		: uiUsedPageIDs[idxPage][2]		= iParID_CasingIn
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1

	if bEnableSill then
		uiUsedPageIDs[idxPage][1]	= iUiID_Sill			: uiUsedPageIDs[idxPage][2]		= iParID_Sill
		uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
		idxPage = idxPage + 1
	endif

	uiUsedPageIDs[idxPage][1]	= iUiID_FixFittingModAttr	: uiUsedPageIDs[idxPage][2]		= iParID_FixFittingModAttr
	uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

! --------------------------------------------------------------------------------
! Additional independent pages
! --------------------------------------------------------------------------------

uiUsedPageIDs[idxPage][1]	= iUiID_FloorPlanSection	: uiUsedPageIDs[idxPage][2]		= iParID_FloorPlanSection
uiUsedPageNames[idxPage][1]	= `План Этажа и Разрез`	: uiUsedPageNames[idxPage][2]	= "uiTab_2DRepr_flat(1)"
idxPage = idxPage + 1

if gs_leaf_optype_m > 0 then
	uiUsedPageIDs[idxPage][1]	= iUiID_MinSpace		: uiUsedPageIDs[idxPage][2]		= iParID_MinSpace
	uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_Descriptions	: uiUsedPageIDs[idxPage][2]		= iParID_Descriptions
uiUsedPageNames[idxPage][1] = `Характеристики`		: uiUsedPageNames[idxPage][2]	= "uiTab_Descriptions_flat(1)"
idxPage = idxPage + 1



! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_DoorSettings_flat(1)"
file_dependence "uiTab_DoorFrameStyle_flat(1)"
file_dependence "uiTab_DoorLeaf_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"
file_dependence "uiTab_StorefrontHandle_flat(1)"
file_dependence "uiTab_OpeningVentilation_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"


! =============================================================================
! Display actual tabpages
! =============================================================================
ui_dialog `Параметры Гаражных Ворот`
ui_current_page gs_ui_current_page

bDoorPanelPage1 = 0
bDoorPanelPage2 = 0


gs_hantype = 1
gs_ui_dtype = 0

if gs_garage = 1 then
	gs_ui_dtype = 10
	if gs_doorcode = "Gar3" then
		gs_hantype = 6
	else
		gs_hantype = 5
	endif
endif

if gs_sectgar = 1 then
	gs_ui_dtype = 12
	gs_hantype = 6
endif

if gs_highspeedgar = 1 then
	if gs_doorcode = "HSD" then		gs_ui_dtype = 14
	if gs_doorcode = "HSRD" then	gs_ui_dtype = 15
endif

for iTabs = 1 to vardim1(uiUsedPageIDs)
	! --------------------------------------------------------------------------------
	! Door Settings and Opening group
	! --------------------------------------------------------------------------------
	if uiUsedPageIDs[iTabs][1]= iUiID_DoorSettOpening then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_DoorSettOpening then
			gosub "pageCallDoorSettOpening"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_FrameSettingsGar then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_FrameSettingsGar then
			gosub "pageCallDoorFrame"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_LeafSettingsGar then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_LeafSettingsGar then
			bDoorPanelPage1 = 1
			bDoorPanelPage2 = 0
			gosub "pageCallDoorPanelSettings"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_LeafSettingsGar2 then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_LeafSettingsGar2 then
			bDoorPanelPage1 = 0
			bDoorPanelPage2 = 1
			gosub "pageCallDoorPanelSettings"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_HandleAndKickBoard then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_HandleAndKickBoard then
			gosub "pageCallHandleAndKickBoard"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_PassDoorHandle then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_PassDoorHandle then
			gosub "pageCallPassDoorHandle"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_VentProfileGar then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_VentProfileGar then
			gosub "pageCallVentilationProfile"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_DoorSettOpModAttr then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_DoorSettOpModAttr then
			listFieldDoorOpSettAttrID = iUiID_DoorSettOpModAttr
			gosub "pageCallDoorModAttributes"
		endif
	endif
! --------------------------------------------------------------------------------
! Additional independent pages
! --------------------------------------------------------------------------------
	if uiUsedPageIDs[iTabs][1]= iUiID_FloorPlanSection then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_FloorPlanSection then
			gosub "pageCallFloorPlanAndSect"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_Descriptions then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Descriptions then
			gosub "pageCallListing"
		endif
	endif	
next iTabs


! --------------------------------------------------------------------------------
! GS Door Functions
! --------------------------------------------------------------------------------
call "GS Door Functions" parameters all	iWindowShape = iWindowShape,
					bOpeningDirection			= WIDO_REVEAL_SIDE,
					iDoorType 					= iDoorType,
					bEnableParapetInset			= bEnableParapetInset,
					bEnableMasonryArch			= bEnableMasonryArch,
					bEnableSill					= bEnableSill,
					bEnableBoard				= bEnableBoard,
					bEnableFaschen				= bEnableFaschen,
					bEnableThreshold			= bEnableThreshold,
					bEnableCasing				= bEnableCasing,
					bEnableShutter				= bEnableShutter,
					bEnableCustomCornerLeft		= bEnableCustomCornerLeft,
					bEnableCustomCornerRight	= bEnableCustomCornerRight,
					bEnableGanging				= bEnableGanging,
					bEnableVentilationProfile	= bEnableVentilationProfile,
					gs_pivotdoor            	= 0,
					gs_sectgar					= gs_sectgar,
					gs_highspeedgar				= gs_highspeedgar,
					b2ColumnsOnOpLineUI			= b2ColumnsOnOpLineUI,
					bHideOplineStyle			= bHideOplineStyle,
					bLockOplineStyle			= bLockOplineStyle,
					bDisable2DOverride			= bDisable2DOverride,
					bDisableSwingType			= bDisableSwingType,
					bDisableSwingLineType 		= bDisableSwingLineType,
					iOpeningParamType			= iOpeningParamType,
					bDoorEmptyPanel				= bDoorEmptyPanel,
					uiUsedPageIDs				= uiUsedPageIDs,
					uiUsedPageNames				= uiUsedPageNames



! =============================================================================
end
! =============================================================================


! --------------------------------------------------------------------------------
! Door Settings and Opening
! --------------------------------------------------------------------------------
"pageCallDoorSettOpening":
	ui_outfield		`Тип ворот`,				225,	180,		60,	20
	ui_infield		"gs_doorcode_",		225+70,	180,	60,	20
	call "ui_DoorSettOpening_m" parameters		gs_doorcode				= gs_doorcode,
												iUiID_OpeningType 		= iUiID_OpeningType,
												iUiID_LeafTypeHandle 	= iUiID_LeafTypeHandle,
												iUiID_FrameSettings		= iUiID_FrameSettings,
												iUiID_VentProfile		= iUiID_VentProfile,
												iUiID_VentProfileGar	= iUiID_VentProfileGar,
												iUiID_LeafSettingsGar	= iUiID_LeafSettingsGar,
												iUiID_HandleAndKickBoard	= iUiID_HandleAndKickBoard,
												iDoorPanelType			= iDoorPanelType,
												stHandleTypes 			= stHandleTypes,
												gs_handle_type_m		= gs_handle_type_m, 
												gs_rttl					= gs_rttl,
												gs_section_num			= gs_section_num,
												gs_passdoor				= gs_passdoor,
												gs_bGlazedDoor			= gs_bGlazedDoor,
												bEnableVentilationProfile = bEnableVentilationProfile,
												stVentilationProfileType = stVentilationProfileType,
												bVentilationProfile		= bVentilationProfile,
												iVentilationProfileType = iVentilationProfileType,
												iDWDet2D				= iDWDet2D,
												iDWDet3D				= iDWDet3D
return


! --------------------------------------------------------------------------------
! Door Model Attributes
! --------------------------------------------------------------------------------
"pageCallDoorModAttributes":
	_bShowKickBoard = bEnableKickBoard & (bKickBoardOut | bKickBoardIn)

	call "ui_DoorSettOpeningModAttr_m" parameters all listFieldDoorOpSettAttrID	= listFieldDoorOpSettAttrID,
													gs_sectgar		= gs_sectgar,
													gs_garage		= gs_garage,
													gs_highspeedgar = gs_highspeedgar,
													iDoorPanelType = iDoorPanelType,
													bShowKickBoard	= _bShowKickBoard,
													gs_bGlazedDoor = gs_bGlazedDoor,
													st_uiDoorSettModAttr = st_uiDoorSettModAttr
return


! --------------------------------------------------------------------------------
! Door Panel
! --------------------------------------------------------------------------------
"pageCallDoorPanelSettings":
	call "ui_door_panel" parameters iDoorPanelType	        = iDoorPanelType,
								gs_passdoor					= gs_passdoor,
								gs_grill_typ_m		        = gs_grill_typ_m,
								gs_ui_dtype			        = gs_ui_dtype,
								gs_metdoor			        = 0,
								gs_entrancedoor		        = 0,
								gs_shutShape		        = 0,
								gs_sectgar			        = gs_sectgar,
								gs_hantype			        = gs_hantype,
								gs_sidelight_n_m	        = gs_sidelight_n_m,
								gs_transom_n_m		        = gs_transom_n_m,
								gs_bGlazedDoor		        = gs_bGlazedDoor,
								gs_door_typ_num		        = gs_door_typ_num,
								gs_handle_type_m	        = gs_handle_type_m,
								gs_cust_handle		        = gs_cust_handle,
								bINTHandles			        = bINTHandles,
								bShowHandleAccMaterial      = 1,
								bShowAirtightPanel			= bShowAirtightPanel,
								bDoorPanelPage1		        = bDoorPanelPage1,
								bDoorPanelPage2		        = bDoorPanelPage2,
                                bEnableShowHandle			= bEnableShowHandle,
								bEnableShowGlassPanel		= bEnableShowGlassPanel,
								bEnableShowAirtightPanel	= bEnableShowAirtightPanel

return


! --------------------------------------------------------------------------------
! Handle and Kick Board
! --------------------------------------------------------------------------------
"pageCallHandleAndKickBoard":

	pageStartY = 4

	_hanType = gs_handle_type_m

	if iDoorPanelType = 0 then
		_hanType = 0	! Force to lock and hide handle parameters
	endif

	call "DoorHandle" parameters A=A,
		pageStartY				= pageStartY,
		bDifferentHandles		= 0,
		bSecondHandle			= 1,
		bIndependentHandlePage	= 1,
		bINTHandles				= 1,
		bPassDoor				= gs_sectgar,
		gs_handle_type_m		= _hanType

	! Full Vertical Separator Line
	ui_separator	222,	2,		222,	264

	gosub "pageDrawKickBoardBlock"

return


! --------------------------------------------------------------------------------
! Kick Board Block
! --------------------------------------------------------------------------------
"pageDrawKickBoardBlock":

	y1 = pageStartY
	dy = 24

	fullWidth	= 212
	halfWidth	= 105

	inFldHgt	= 19
	outFldHgt	= 15

	xPos01	= 230
	xPos02	= xPos01
	xPos03	= xPos02 + halfWidth + 2

	ui_style 0, 1
	ui_outfield		`Защитная Пластина`,			xPos01,	y1,		fullWidth,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_infield{3}	"bKickBoardOut",	xPos01,	y1-4,	halfWidth,	inFldHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1,	`Снаружи`, 0,
		1,	`Снаружи`, 1

	y1 = y1 + dy

	ui_infield{3}	"bKickBoardIn",	xPos01,	y1-4,	halfWidth,	inFldHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1,	`Внутри`, 0,
		1,	`Внутри`, 1

	y1 = y1 + dy

	ui_outfield		`Высота`,				xPos02,	y1,		halfWidth,	outFldHgt,	4 * not(bKickBoardOut | bKickBoardIn)
	ui_infield		"kickBoardHeight",		xPos03,	y1-4,	halfWidth,	inFldHgt

return

! --------------------------------------------------------------------------------
! Pass Door Handle
! --------------------------------------------------------------------------------
"pageCallPassDoorHandle":

	pageStartY = 6
	y0 = pageStartY 
	dy = 23
	x1 = 2
	x2 = 150
	x3 = 305

	pict_x	= 444 - 2 - x3
	pict_y	= 56
	cell_x	= 55
	cell_y	= 48
	image_x	= 55
	image_y	= 48

	heightOutf	= 15
	heightInf	= 19
	
	stHandleTypesNone	= `Нет`
	stHandleTypesCustom	= `Специальный`

	flagNoPass = 4 * not(gs_passdoor)

	ui_style 0, 1
	ui_outfield `Проходная Ручка`, x1, y0, x2 - x1 - 2, heightOutf

	! --- Outside
	ui_outfield `Снаружи`, x2, y0, x3 - x2 - 2, heightOutf, flagNoPass
	ui_style 0,0

	ui_infield{3} "gs_handle_typ_out_m", x3, y0, pict_x, pict_y,
		2, 1, 27, 9,		! Handle_Types.png
		cell_x,cell_y,image_x,image_y,
		27,stHandleTypesNone, 0,
		1,stHandleTypes[ 1], 1,
		2,stHandleTypes[ 2], 2,
		3,stHandleTypes[ 3], 3,
		4,stHandleTypes[ 4], 4,
		5,stHandleTypes[ 5], 5,
		6,stHandleTypes[ 6], 6,
		7,stHandleTypes[ 7], 7,
		8,stHandleTypes[ 8], 8,
		9,stHandleTypes[ 9], 9,
		10,stHandleTypes[10], 10,
		11,stHandleTypes[11], 11,
		12,stHandleTypes[12], 12,
		13,stHandleTypes[13], 13,
		14,stHandleTypes[14], 14,
		15,stHandleTypes[15], 15,
		16,stHandleTypes[16], 16,
		17,stHandleTypes[17], 17,
		18,stHandleTypes[18], 18,
		19,stHandleTypes[19], 19,
		20,stHandleTypes[20], 20,
		21,stHandleTypes[21], 21,
		22,stHandleTypes[22], 22,
		23,stHandleTypes[23], 23,
		24,stHandleTypes[24], 24,
		25,stHandleTypes[25], 25,
		26,stHandleTypesCustom, 99

	flagOut = 4 * ((gs_handle_typ_out_m <> 99) | not(gs_passdoor))

	y0 = y0 + pict_y + 8
	ui_outfield `Имя Спец. Ручки`, x2, y0, x3 - x2, heightOutf, flagOut			! Custom Handle Name
	ui_infield "gs_cust_handle_out", x3, y0-3, 444 - 2 - x3, heightInf

	y0 = y0 + dy - 2

	ui_separator x2, y0, 444 - 2, y0

	y0 = y0 + 5

	! --- Inside
	ui_style 0, 1
	ui_outfield `Внутри`, x2, y0, x3 - x2 - 2, heightOutf, flagNoPass
	ui_style 0,0

	ui_infield{3} "gs_handle_typ_in_m", x3, y0, pict_x, pict_y,
		2, 1, 27, 9,		! Handle_Types.png
		cell_x,cell_y,image_x,image_y,
		27,stHandleTypesNone, 0,
		1,stHandleTypes[ 1], 1,
		2,stHandleTypes[ 2], 2,
		3,stHandleTypes[ 3], 3,
		4,stHandleTypes[ 4], 4,
		5,stHandleTypes[ 5], 5,
		6,stHandleTypes[ 6], 6,
		7,stHandleTypes[ 7], 7,
		8,stHandleTypes[ 8], 8,
		9,stHandleTypes[ 9], 9,
		10,stHandleTypes[10], 10,
		11,stHandleTypes[11], 11,
		12,stHandleTypes[12], 12,
		13,stHandleTypes[13], 13,
		14,stHandleTypes[14], 14,
		15,stHandleTypes[15], 15,
		16,stHandleTypes[16], 16,
		17,stHandleTypes[17], 17,
		18,stHandleTypes[18], 18,
		19,stHandleTypes[19], 19,
		20,stHandleTypes[20], 20,
		21,stHandleTypes[21], 21,
		22,stHandleTypes[22], 22,
		23,stHandleTypes[23], 23,
		24,stHandleTypes[24], 24,
		25,stHandleTypes[25], 25,
		26,stHandleTypesCustom, 99

	flagIn = 4 * ((gs_handle_typ_in_m <> 99) | not(gs_passdoor))

	y0 = y0 + pict_y + 8
	ui_outfield `Имя Спец. Ручки`, x2, y0, x3 - x2, heightOutf, flagIn			! Custom Handle Name
	ui_infield "gs_cust_handle_in", x3, y0-3, 444 - 2 - x3, heightInf

	y0 = y0 + dy - 2

	ui_separator x2, y0, 444 - 2, y0

	y0 = y0 + 8

	flagHeightPos = 4 * ((gs_handle_typ_in_m = 0 & gs_handle_typ_out_m = 0) | not(gs_passdoor))

	! --- Height, Position
	ui_outfield `Высота`, x2, y0, x3 - x2 - 2, heightOutf, flagHeightPos
	ui_infield "gs_han_height", x3, y0-3, 444 - 2 - x3 - 40, heightInf

	y0 = y0 + dy

	ui_outfield `Расположение`, x2, y0, x3 - x2 - 2, heightOutf, flagHeightPos
	ui_infield "gs_han_pos", x3, y0-3, 444 - 2 - x3 - 40, heightInf
return

! --------------------------------------------------------------------------------
! Ventilation Profile
! --------------------------------------------------------------------------------
"pageCallVentilationProfile":
	call "Ventilation_profile" parameters	isWindow						= 0,
											iWindowType						= 0,
											bVentilationProfile				= bVentilationProfile,
											iVentilationProfileType			= iVentilationProfileType
return


! --------------------------------------------------------------------------------
! Door Frame
! --------------------------------------------------------------------------------
"pageCallDoorFrame":
	call "ui_door_frame" parameters all gs_stDoorFrame			= gs_stDoorFrame,
										iDoorPanelType			= iDoorPanelType,
										gs_grill_typ_m			= gs_grill_typ_m,
										gs_ui_dtype				= gs_ui_dtype,
										gs_metdoor				= 0,
										gs_sectgar				= gs_sectgar,
										gs_shutShape			= 0,
										gs_hantype				= gs_hantype,
										gs_sidelight_n_m		= gs_sidelight_n_m,
										gs_transom_n_m			= gs_transom_n_m,
										gs_door_typ_num			= gs_door_typ_num
return



! --------------------------------------------------------------------------------
! Floor Plan and Section
! --------------------------------------------------------------------------------
"pageCallFloorPlanAndSect":
	call "ui_FloorPlanSection_m" parameters isWindow	= 0,
						iWindowShape					= iWindowShape,
						iWallContourLine				= iWallContourLine,
						gs_bFills						= gs_bFills,
						gs_bShowDemolitionPar			= gs_bShowDemolitionPar,
						bShowDemolitionLineType			= 1,
						iTypeDemolitionFill				= iTypeDemolitionFill,
						iRevealType						= gs_reveal_type_int - 1,
						gs_frame_style_m 				= gs_frame_style_m,
						bDoorWithSidelightSupport		= 0,
						gs_stack_top					= gs_stack_top,
						gs_stack_bottom					= 0,
						iWindowType						= 0,
!						gs_bLeftSLight					= gs_bLeftSLight,		! only for windows
!						gs_bSLight						= gs_bSLight,
!						gs_UTrans						= gs_UTrans,
!						gs_UTrans_Div					= gs_UTrans_Div,
!						gs_LTrans						= gs_LTrans,
!						gs_LTrans_Div					= gs_LTrans_Div,
!						iUTrans							= iUTrans,
						gs_sash_uniform					= 0,
!						gs_panes_number					= gs_panes_number,		! only for multi-sash windows
!						CasementIDArray					= CasementIDArray,
!						iMullionStyle_00				= iMullionStyle_00,
!						iMullionStyle_01				= iMullionStyle_01,
!						iMullionStyle_02				= iMullionStyle_02,
!						iMullionStyle_03				= iMullionStyle_03,
!						iMullionStyle_04				= iMullionStyle_04,
!						iMullionStyle_05				= iMullionStyle_05,
!						iMullionStyle_06				= iMullionStyle_06,
!						iMullionStyle_07				= iMullionStyle_07,
!						iMullionStyle_08				= iMullionStyle_08,
!						iMullionStyle_09				= iMullionStyle_09,
!						iMullionStyle_10				= iMullionStyle_10,
!						iMullionStyle_11				= iMullionStyle_11,
!						gs_optype_m_01					= gs_optype_m_01,		! only for windows
						gs_optype_m_02					= 1,
!						gs_optype_m_03					= gs_optype_m_03,		! only for windows
!						gs_optype_m_04					= gs_optype_m_04,
						gs_optype_m_05					= 1,
						gs_optype_m_06					= 1,
!						gs_optype_m_07					= gs_optype_m_07,
!						gs_optype_m_08					= gs_optype_m_08,
						gs_optype_m_09					= 1,
						gs_optype_m_10					= 1,
!						gs_optype_m_11					= gs_optype_m_11,
						bOverride_MVO_2D				= bOverride_MVO_2D,
						bOverride_MVO_3D				= bOverride_MVO_3D,
						iClosureType					= gs_closure_type_int - 1,
						gs_bJoint						= gs_bJoint,
						bEnableParapetInset				= bEnableParapetInset,
						gs_parapet_wall_inset			= 0,
						bEnableMasonryArch				= bEnableMasonryArch,
						bMasonryArch					= bMasonryArch,
						bEnableThreshold				= bEnableThreshold,
						bThreshold						= bThreshold,
						bEnableCasing					= bEnableCasing,
						gs_trim_inside					= gs_trim_inside,
						gs_trim_outside					= gs_trim_outside,
						bEnableSill						= bEnableSill,
						gs_sill_outside					= gs_sill_outside,
						iSillType						= iSillType,
						gs_sill_outside_sl_left			= 0,
						gs_sidelight_left				= 0,
						gs_sidelight_parapet_hgt_left	= 0,
						gs_sill_outside_sl_right		= 0,
						gs_sidelight_right				= 0,
						gs_sidelight_parapet_hgt_right	= 0,
						gs_sill_ins_below				= 0,
						gs_sill_ins_use_wall_skin		= 0,
						bEnableBoard					= bEnableBoard,
						gs_board_inside					= 0,
						bEnableVentilationProfile		= bEnableVentilationProfile,
						bVentilationProfile				= bVentilationProfile,
						iVentilationProfileType			= iVentilationProfileType,
!						isVentilationProfileInSash_01	= isVentilationProfileInSash_01,
!						isVentilationProfileInSash_02	= isVentilationProfileInSash_02,
!						isVentilationProfileInSash_03	= isVentilationProfileInSash_03,
!						isVentilationProfileInSash_04	= isVentilationProfileInSash_04,
!						isVentilationProfileInSash_05	= isVentilationProfileInSash_05,
!						isVentilationProfileInSash_06	= isVentilationProfileInSash_06,
!						isVentilationProfileInSash_07	= isVentilationProfileInSash_07,
!						isVentilationProfileInSash_08	= isVentilationProfileInSash_08,
!						isVentilationProfileInSash_09	= isVentilationProfileInSash_09,
!						isVentilationProfileInSash_10	= isVentilationProfileInSash_10,
!						isVentilationProfileInSash_11	= isVentilationProfileInSash_11,
						bEnableShutter					= bEnableShutter,
						bSunshade						= bSunshade,
						gs_sunShade_iType				= gs_sunShade_iType,
						listFieldID		 				= iUiID_FloorPlanSection,
						isEmptyOpening					= 0
return



! --------------------------------------------------------------------------------
! Descriptions
! --------------------------------------------------------------------------------
"pageCallListing":
	call "ui_Description_m" parameters all	iDWObjectType				= 2, 	! DOOR_OBJECT
											gs_show_UKListParams		= gs_show_UKListParams,
											listFieldID 				= iUiID_Descriptions,
											gs_bOpeningToZone			= gs_bOpeningToZone,
											gs_list_cost				= gs_list_cost,
											gs_list_manufacturer		= gs_list_manufacturer,
											gs_list_note				= gs_list_note,
											gs_list_location			= gs_list_location,
											gs_list_location_as			= gs_list_location_as,
											gs_list_accessories			= gs_list_accessories,
											gs_list_glazing				= gs_list_glazing,
											gs_list_headdet				= gs_list_headdet,
											gs_list_jambdet				= gs_list_jambdet,
											gs_list_silldet				= gs_list_silldet,
											gs_list_hwsetno				= gs_list_hwsetno,
											gs_list_hwkeyside			= gs_list_hwkeyside,
											gs_list_optype				= gs_list_optype,
											gs_list_finish				= gs_list_finish,
											gs_list_elevation			= gs_list_elevation,
											gs_list_hings				= gs_list_hings,
											gs_list_lockset				= gs_list_lockset,
											gs_list_glassarea_tmp		= gs_list_glassarea_tmp,
											gs_list_glassarea_override	= gs_list_glassarea_override,
											gs_list_glassarea			= gs_list_glassarea,
											gs_list_glassperimeter		= gs_list_glassperimeter,
											gs_list_heattransfer		= gs_list_heattransfer,
											gs_list_firerating			= gs_list_firerating,
											gs_list_acousticrating		= gs_list_acousticrating,
											FM_Type						= FM_Type,
											iFMType						= iFMType,
											FM_InventoryNumber			= FM_InventoryNumber,
											FM_SerialNumber				= FM_SerialNumber,
											FM_ProductionYear			= FM_ProductionYear,
											FM_ObjectWeight				= FM_ObjectWeight,
											FM_ObjectWeightUnit			= FM_ObjectWeightUnit,
											gs_list_orientationDef_m	= gs_list_orientationDef_m,
											gs_list_NBSClause			= gs_list_NBSClause,
											gs_list_doorcloser			= gs_list_doorcloser,
											gs_list_visionpanel_type	= gs_list_visionpanel_type,
											gs_list_elevationtype		= gs_list_elevationtype,
											gs_list_frametype			= gs_list_frametype,
											gs_list_holddevice			= gs_list_holddevice,
											gs_list_firesignage			= gs_list_firesignage,
											gs_list_comment				= gs_list_comment,
											ifc_optypestr				= ifc_optypestr,
											ifc_optype					= ifc_optype
return


