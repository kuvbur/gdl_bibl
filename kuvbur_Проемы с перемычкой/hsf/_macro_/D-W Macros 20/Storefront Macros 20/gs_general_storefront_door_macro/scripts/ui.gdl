

if iDoorType = 2 then
	stHandleTab = `Дверные Ручки и Защитная Пластина 1`
else
	stHandleTab = `Дверные Ручки и Защитная Пластина`
endif

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

! call all available page fix IDs
call "ui_tabIDlistHierarchy_m" parameters iUITreeStandardType = 2,			! STOREFRONT_UI
					returned_parameters iUiID_Root,
										iUiID_NominalSizes,		iParID_NominalSizes,
										iUiID_ShapePanelDiv,	iParID_ShapePanelDiv,
										iUiID_GlassMullions,	iParID_GlassMullions,
										iUiID_SFFrameStyle,		iParID_SFFrameStyle,
										iUiID_VentProfile,		iParID_VentProfile,
										iUiID_Doorleaf,			iParID_Doorleaf,
										iUiID_Handle1,			iParID_Handle1,
										iUiID_Handle2,			iParID_Handle2,
										iUiID_OpeningLines,		iParID_OpeningLines,
										iUiID_DoorModelAttrib,	iParID_DoorModelAttrib,
										iUiID_WallOpening,		iParID_WallOpening,
										iUiID_Reveal,			iParID_Reveal,
										iUiID_Closure,			iParID_Closure,
										iUiID_FloorPlanSection,	iParID_FloorPlanSection,
										iUiID_MinSpace,			iParID_MinSpace,
										iUiID_Descriptions,		iParID_Descriptions



dim uiUsedPageIDs[][2]
dim uiUsedPageNames[][2]

! override regular iParID_SFFrameStyle and iParID_VentProfile with iUiID_Root, different hierarchy applied in this door
if iStorefrontType = SFTYPE_DOOR_ONLY then 
	iParID_SFFrameStyle = iUiID_Root
	iParID_VentProfile = iUiID_Root
endif

idxPage = 1

uiUsedPageIDs[idxPage][1]	= iUiID_NominalSizes		: uiUsedPageIDs[idxPage][2]		= iParID_NominalSizes
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

if iStorefrontType <> SFTYPE_DOOR_ONLY then
	uiUsedPageIDs[idxPage][1]	= iUiID_ShapePanelDiv			: uiUsedPageIDs[idxPage][2]		= iParID_ShapePanelDiv
	uiUsedPageNames[idxPage][1]	= `Форма и Рисунок Переплета`	: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontPanels_flat(1)"
	idxPage = idxPage + 1

	uiUsedPageIDs[idxPage][1]	= iUiID_GlassMullions	: uiUsedPageIDs[idxPage][2]		= iParID_GlassMullions
	uiUsedPageNames[idxPage][1]	= `Остекление и Раскладка`	: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontMullion_flat(1)"
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_SFFrameStyle				: uiUsedPageIDs[idxPage][2]		= iParID_SFFrameStyle
uiUsedPageNames[idxPage][1]	= `Стиль и Размеры Коробки`		: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontFrame_flat(1)"
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_Doorleaf			: uiUsedPageIDs[idxPage][2]		= iParID_Doorleaf
uiUsedPageNames[idxPage][1]	= `Полотно Двери и Открывание`	: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontLeafType_flat(1)"
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_Handle1				: uiUsedPageIDs[idxPage][2]		= iParID_Handle1
uiUsedPageNames[idxPage][1]	= stHandleTab				: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontHandle_flat(1)"
idxPage = idxPage + 1

if iDoorType = 2 then
	uiUsedPageIDs[idxPage][1]	= iUiID_Handle2						: uiUsedPageIDs[idxPage][2]		= iParID_Handle2
	uiUsedPageNames[idxPage][1]	= `Дверные Ручки и Защитная Пластина 2`	: uiUsedPageNames[idxPage][2]	= "uiTab_StorefrontHandle_flat(1)"
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_DoorModelAttrib 	: uiUsedPageIDs[idxPage][2]		= iParID_DoorModelAttrib 
uiUsedPageNames[idxPage][1]	= `Реквизиты Модели`		: uiUsedPageNames[idxPage][2]	= "uiTab_3DRepr_flat(1)"
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_OpeningLines		: uiUsedPageIDs[idxPage][2]		= iParID_OpeningLines
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_WallOpening			: uiUsedPageIDs[idxPage][2]		= iParID_WallOpening	
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_Reveal				: uiUsedPageIDs[idxPage][2]		= iParID_Reveal
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_Closure				: uiUsedPageIDs[idxPage][2]		= iParID_Closure
uiUsedPageNames[idxPage][1]	= ""						: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1					

uiUsedPageIDs[idxPage][1]	= iUiID_FloorPlanSection	: uiUsedPageIDs[idxPage][2]		= iParID_FloorPlanSection
uiUsedPageNames[idxPage][1]	= `План Этажа и Разрез`	: uiUsedPageNames[idxPage][2]	= "uiTab_2DRepr_flat(1)"
idxPage = idxPage + 1					

if bEnableVentilationProfile then
	uiUsedPageIDs[idxPage][1]	= iUiID_VentProfile		: uiUsedPageIDs[idxPage][2]		= iParID_VentProfile
	uiUsedPageNames[idxPage][1]	= `Приточная Вентиляция`	: uiUsedPageNames[idxPage][2]	= "uiTab_OpeningVentilation_flat(1)"
	idxPage = idxPage + 1
endif

uiUsedPageIDs[idxPage][1]	= iUiID_MinSpace		: uiUsedPageIDs[idxPage][2]		= iParID_MinSpace
uiUsedPageNames[idxPage][1]	= ""					: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1

uiUsedPageIDs[idxPage][1]	= iUiID_Descriptions	: uiUsedPageIDs[idxPage][2]		= iParID_Descriptions
uiUsedPageNames[idxPage][1] = ""					: uiUsedPageNames[idxPage][2]	= ""
idxPage = idxPage + 1


! =============================================================================
! Tabpages
! =============================================================================

ui_current_page gs_ui_current_page

ui_dialog `Параметры Витрины `

bGlassInLeaf = 0

if	iDoorPanelType = PANEL_STYLE2 |\
	iDoorPanelType = PANEL_STYLE3 | iDoorPanelType = PANEL_STYLE4 |\
	iDoorPanelType = PANEL_STYLE5 | iDoorPanelType = PANEL_STYLE6 |\
	iDoorPanelType = PANEL_STYLE7 | iDoorPanelType = PANEL_STYLE8 then

		bGlassInLeaf = 1
endif




bPanelSettingsPage	= 0
bPanelSettingsPage2	= 0


for iTabs = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[iTabs][1]= iUiID_ShapePanelDiv then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_ShapePanelDiv then
			gosub "pageCallPanels"
		endif
	endif	
	if uiUsedPageIDs[iTabs][1]= iUiID_GlassMullions then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_GlassMullions then
			gosub "pageCallGlassMullions"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_SFFrameStyle then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_SFFrameStyle then
			gosub "pageCallFrame"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_Doorleaf then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Doorleaf then
			gosub "pageCallDoorLeaves"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_Handle1 then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Handle1 then
			bPanelSettingsPage	= 1
			bPanelSettingsPage2	= 0
			gosub "pageCallHandleAndKickBoard"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_Handle2 then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_Handle2 then
			bPanelSettingsPage	= 0
			bPanelSettingsPage2	= 1
			gosub "pageCallHandleAndKickBoard"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_DoorModelAttrib then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_DoorModelAttrib then
			gosub "pageCallSurfaces"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_VentProfile then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_VentProfile then
			gosub "pageCallVentProfile"
		endif
	endif
	if uiUsedPageIDs[iTabs][1]= iUiID_FloorPlanSection then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], uiUsedPageNames[iTabs][1], uiUsedPageNames[iTabs][2]
		if gs_ui_current_page = iUiID_FloorPlanSection then
			gosub "pageCallFloorPlanAndSect"
		endif
	endif
next iTabs



call "GS Storefront Door Functions",
	parameters all	bEnableSwingLineTurnOff		= bEnableSwingLineTurnOff,
					bEnableSecondaryOpLineTypes = bEnableSecondaryOpLineTypes,
					gs_bSameLineTypeForLeaves	= gs_bSameLineTypeForLeaves,
					bEnableVentilationProfile	= bEnableVentilationProfile,
					bOpeningDirection			= WIDO_REVEAL_SIDE,
					bSidelights					= bSidelights,
					gs_leaf_optype_m			= gs_leaf_optype_m,
					bDoorEmptyPanel				= bDoorEmptyPanel,
					uiUsedPageIDs				= uiUsedPageIDs,
					uiUsedPageNames				= uiUsedPageNames



file_dependence "uiTab_StorefrontPanels_flat(1)"
file_dependence "uiTab_StorefrontMullion_flat(1)"
file_dependence "uiTab_StorefrontFrame_flat(1)"
file_dependence "uiTab_StorefrontLeafType_flat(1)"
file_dependence "uiTab_StorefrontHandle_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_OpeningVentilation_flat(1)"

end


! =============================================================================
! Handles
! =============================================================================
"pageCallHandleAndKickBoard":
	pageStartY = 4
	call "DoorHandle" parameters	bStorefrontDoorHandle			= 1,
									bINTHandles						= 1,
									pageStartY						= pageStartY,
									gs_handle_type_m				= gs_handle_type_m,
									gs_handle_type_out_m			= gs_handle_type_out_m,
									gs_handle_type2_m				= gs_handle_type2_m,
									gs_handle_type2_out_m			= gs_handle_type2_out_m,
									bPanelSettingsPage				= bPanelSettingsPage,
									bPanelSettingsPage2				= bPanelSettingsPage2

	y0	= pageStartY + 194
	dy	= 24

	outFldWdt	= 120
	inFldWdt	= 90
	outFldHgt	= 15
	inFldHgt	= 19

	xMiddle		= 222

	xPos1	= 2
	xPos2	= xMiddle + 8
	xPos3	= xPos2 + outFldWdt + 2

	boolWidth	= xMiddle - xPos1

	y1 = y0

	! =========================================================================
	ui_separator xPos1,	y1-4,		442,	y1-4

	if bPanelSettingsPage then
		ui_style 0, 1
		ui_outfield		`Защитная Пластина`,		xPos1,	y1,		boolWidth,	outFldHgt

		y1 = y1 + dy

		ui_style 0, 0
		ui_infield{3}	"bKickBoardOut",	xPos1,	y1-4,	boolWidth,	inFldHgt,
			7, "", 1, 1,
			0, 0, 0, 0,
			1,	`Снаружи`, 0,
			1,	`Снаружи`, 1

		y1 = y1 + dy

		ui_infield{3}	"bKickBoardIn",		xPos1,	y1-4,	boolWidth,	inFldHgt,
			7, "", 1, 1,
			0, 0, 0, 0,
			1,	`Внутри`, 0,
			1,	`Внутри`, 1

		y1	= y0 + dy

		ui_outfield		`Высота`,			xPos2,	y1,		outFldWdt,	outFldHgt, 4 * not(bKickBoardOut | bKickBoardIn)
		ui_infield		"kickBoardHeight",	xPos3,	y1-4,	inFldWdt,	inFldHgt
	endif

	if bPanelSettingsPage2 then
		ui_style 0, 1
		ui_outfield		`Защитная Пластина`,		xPos1,	y1,		boolWidth,	outFldHgt

		y1 = y1 + dy

		ui_style 0, 0
		ui_infield{3}	"bKickBoardOut2",	xPos1,	y1-4,	boolWidth,	inFldHgt,
			7, "", 1, 1,
			0, 0, 0, 0,
			1,	`Снаружи`, 0,
			1,	`Снаружи`, 1

		y1 = y1 + dy

		ui_infield{3}	"bKickBoardIn2",	xPos1,	y1-4,	boolWidth,	inFldHgt,
			7, "", 1, 1,
			0, 0, 0, 0,
			1,	`Внутри`, 0,
			1,	`Внутри`, 1

		y1	= y0 + dy

		ui_outfield		`Высота`,			xPos2,	y1,		outFldWdt,	outFldHgt, 4 * not(bKickBoardOut2 | bKickBoardIn2)
		ui_infield		"kickBoardHeight2",	xPos3,	y1-4,	inFldWdt,	inFldHgt
	endif

return


! =============================================================================
! Panels
! =============================================================================

"pageCallPanels":

	call "ui_StoreFront_Panels_m"	parameters	iStorefrontType			= iStorefrontType,
											iDoorType				= iDoorType,
											stDivisionMethod		= stDivisionMethod,
											iDivisionMethod			= iDivisionMethod,
											bMiddleRail				= bMiddleRail,
											gs_door_transom			= gs_door_transom,
											gs_sidelight_left		= gs_sidelight_left,
											gs_sidelight_right		= gs_sidelight_right
return


! =============================================================================
! Glass and Mullions
! =============================================================================
"pageCallGlassMullions":

	call "ui_StoreFront_Glass_m" parameters	iStorefrontType			= iStorefrontType,
											titleGlassMullions		= titleGlassMullions,
											iFrameStyleMullion		= iFrameStyleMullion,
											gs_opening_dir_sl_m		= gs_opening_dir_sl_m,
											gs_md					= gs_md,
											gs_grid_w				= gs_grid_w,
											gs_grid_thk				= gs_grid_thk,
											gridWidthVert			= gridWidthVert,
											gridThkVert				= gridThkVert,
											gs_glass_thk			= gs_glass_thk,
											gs_gsmd					= gs_gsmd,
											gs_gsb					= gs_gsb,
											buttGalzedJoint			= buttGalzedJoint,
											iFrameStyleLeft			= iFrameStyleLeft,
											iFrameStyleRight		= iFrameStyleRight,
											gs_wthk					= gs_wthk


return


! =============================================================================
! Frame
! =============================================================================

"pageCallFrame":

	call "ui_StoreFront_Frame_m" parameters	iStorefrontType		= iStorefrontType,
											iFrameStyleLeft			= iFrameStyleLeft,
											iFrameStyleRight		= iFrameStyleRight,
											gs_wthk					= gs_wthk,
											iEndFrameLeft			= iEndFrameLeft,
											leftCornerAngle			= leftCornerAngle,
											iEndFrameRight			= iEndFrameRight,
											rightCornerAngle		= rightCornerAngle,
											gs_frame_thk			= gs_frame_thk,
											gs_frame_width			= gs_frame_width


return


! =============================================================================
! Door Leaves
! =============================================================================
"pageCallDoorLeaves":

	call "ui_StoreFront_DoorLeaves_m" parameters	iStoreFrontType			= iStoreFrontType,
													iDoorType				= iDoorType,
													iDoorPanelType			= iDoorPanelType,
													gs_ds					= gs_ds,
													gs_ds_thk				= gs_ds_thk,
													gs_ds_w					= gs_ds_w,
													gs_sidelight_left		= gs_sidelight_left,
													gs_sidelight_right		= gs_sidelight_right,
													leafFrameWidth			= leafFrameWidth,
													gs_leaf_thk				= gs_leaf_thk,
													bottomRailWidth			= bottomRailWidth,
													bMiddleRail				= bMiddleRail,
													midRailWidth			= midRailWidth,
													midRailElevation		= midRailElevation,
													gs_rttl					= gs_rttl,
													gs_cust_panel			= gs_cust_panel
return

! =============================================================================
! Material Tabpage
! =============================================================================
"pageCallSurfaces":

	itemID = 1

	listID = iUiID_DoorModelAttrib

	ui_listfield listID, 0, 0, 444, 265 

	ui_listitem itemID, listID, "iDWDet3D",			0,	"ui_list_detailLevel3D_flat(1)"	: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_SashText_typ",	0,	""							: itemID = itemID + 1

	ui_listitem itemID, listID, "", 				0, "", `Перья в 3D`						: itemID = itemID + 1	! 3D Pens
	ui_listitem itemID, listID, "gs_frame_pen",		1,	"ui_list_pencolor_flat(1)", `Коробка`		: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_leaf_pen",		1,	"ui_list_pencolor_flat(1)", `Полотно`		: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_glass_pen",		1,	"ui_list_pencolor_flat(1)", `Остекление`		: itemID = itemID + 1


	ui_listitem itemID, listID, "", 				0, "", `Покрытия`				: itemID = itemID + 1	! Surfaces
	ui_listitem itemID, listID, "bLinkSurfacesFrameSash",		1,		"ui_list_surface_flat(1)"	: itemID = itemID + 1

	ui_listitem itemID, listID, "gs_frame_mat",			1,	"ui_list_surface_flat(1)", `Коробка`		: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_leaf_mat",			1,	"ui_list_surface_flat(1)", `Полотно`		: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_leafMidrail_mat",	1,	"ui_list_surface_flat(1)", `Средник Полотна`	: itemID = itemID + 1

	if gs_sidelight_left | gs_sidelight_right then
		ui_listitem itemID, listID, "gs_midrail_mat",		1,	"ui_list_surface_flat(1)", `Средник`	: itemID = itemID + 1
	endif

	if gs_sidelight_left | gs_sidelight_right | gs_door_transom then
		ui_listitem itemID, listID, "gs_sash_mat",			1,	"ui_list_surface_flat(1)", `Горизонтальная Раскладка`		: itemID = itemID + 1
		ui_listitem itemID, listID, "gs_vertMull_mat",		1,	"ui_list_surface_flat(1)", `Вертикальная Раскладка`		: itemID = itemID + 1
	endif

	ui_listitem itemID, listID, "gs_glass_mat",			1,	"ui_list_surface_flat(1)", `Остекление`		: itemID = itemID + 1

	ui_listitem itemID, listID, "gs_handle_mat",		1,	"ui_list_surface_flat(1)", `Ручка`		: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_lock_mat",			1,	"ui_list_surface_flat(1)", `Замок`		: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_handle_ac_mat",		1,	"ui_list_surface_flat(1)", `Аксессуар`	: itemID = itemID + 1
	ui_listitem itemID, listID, "gs_ventProfile_mat",	1,	"ui_list_surface_flat(1)", `Приточный Клапан`	: itemID = itemID + 1

	ui_listitem itemID, listID, "matKickBoardOut",		1,	"ui_list_surface_flat(1)", `Защитная Пластина Снаружи`	: itemID = itemID + 1
	ui_listitem itemID, listID, "matKickBoardIn",		1,	"ui_list_surface_flat(1)", `Защитная Пластина Внутри`	: itemID = itemID + 1

return

! =============================================================================
! Natural Ventilation
! =============================================================================

"pageCallVentProfile":
		call "Ventilation_profile" parameters	isWindow						= 0,
												iWindowType						= 0,
												bVentilationProfile				= bVentilationProfile,
												iVentilationProfileType			= iVentilationProfileType
return

! =============================================================================
! Floor Plan and Section
! =============================================================================

"pageCallFloorPlanAndSect":
	call "ui_FloorPlanSection_m" parameters isWindow	= 0,
						iWindowShape					= 0,		! SHAPE_RECTANGULAR
						iWallContourLine				= iWallContourLine,
						gs_bFills						= gs_bFills,
						gs_bShowDemolitionPar			= gs_bShowDemolitionPar,
						bShowDemolitionLineType			= 1,
						iTypeDemolitionFill				= iTypeDemolitionFill,
						iRevealType						= gs_reveal_type_int - 1,
!						gs_frame_style_m 				= gs_frame_style_m,		! only for doors
						bDoorWithSidelightSupport		= 0,
						bStorefrontClassic				= 1,
						gs_stack_top					= gs_stack_top,
						gs_stack_bottom					= gs_stack_bottom,
						iWindowType						= 0,
!						gs_bLeftSLight					= gs_bLeftSLight,		! only for windows
!						gs_bSLight						= gs_bSLight,
!						gs_UTrans						= gs_UTrans,
!						gs_UTrans_Div					= gs_UTrans_Div,
!						gs_LTrans						= gs_LTrans,
!						gs_LTrans_Div					= gs_LTrans_Div,
!						iUTrans							= iUTrans,
						gs_sash_uniform					= 0,
!						gs_panes_number					= gs_panes_number,		! only for multi-sash windows
!						CasementIDArray					= CasementIDArray,
!						iMullionStyle_00				= iMullionStyle_00,
!						iMullionStyle_01				= iMullionStyle_01,
!						iMullionStyle_02				= iMullionStyle_02,
!						iMullionStyle_03				= iMullionStyle_03,
!						iMullionStyle_04				= iMullionStyle_04,
!						iMullionStyle_05				= iMullionStyle_05,
!						iMullionStyle_06				= iMullionStyle_06,
!						iMullionStyle_07				= iMullionStyle_07,
!						iMullionStyle_08				= iMullionStyle_08,
!						iMullionStyle_09				= iMullionStyle_09,
!						iMullionStyle_10				= iMullionStyle_10,
!						iMullionStyle_11				= iMullionStyle_11,
!						gs_optype_m_01					= gs_optype_m_01,
						gs_optype_m_02					= gs_optype_m_02,
!						gs_optype_m_03					= gs_optype_m_03,
!						gs_optype_m_04					= gs_optype_m_04,
						gs_optype_m_05					= gs_optype_m_05,
						gs_optype_m_06					= gs_optype_m_06,
!						gs_optype_m_07					= gs_optype_m_07,
!						gs_optype_m_08					= gs_optype_m_08,
						gs_optype_m_09					= gs_optype_m_09,
						gs_optype_m_10					= gs_optype_m_10,
!						gs_optype_m_11					= gs_optype_m_11,
						bOverride_MVO_2D				= bOverride_MVO_2D,
						bOverride_MVO_3D				= bOverride_MVO_3D,
						iClosureType					= gs_closure_type_int - 1,
						gs_bJoint						= gs_bJoint,
						bEnableParapetInset				= bEnableParapetInset,
						gs_parapet_wall_inset			= 0,
						bEnableMasonryArch				= bEnableMasonryArch,
						bMasonryArch					= 0,
						bEnableThreshold				= bEnableThreshold,
						bThreshold						= 0,
						bEnableCasing					= bEnableCasing,
						gs_trim_inside					= 0,
						gs_trim_outside					= 0,
						bEnableSill						= bEnableSill,
						gs_sill_outside					= 0,
!						iSillType						= iSillType,
!						gs_sill_outside_sl_left			= gs_sill_outside_sl_left,		! only for doors
!						gs_sidelight_left				= gs_sidelight_left,
!						gs_sidelight_parapet_hgt_left	= gs_sidelight_parapet_hgt_left,
!						gs_sill_outside_sl_right		= gs_sill_outside_sl_right,
!						gs_sidelight_right				= gs_sidelight_right,
!						gs_sidelight_parapet_hgt_right	= gs_sidelight_parapet_hgt_right,
!						gs_sill_ins_below				= gs_sill_ins_below,
!						gs_sill_ins_use_wall_skin		= gs_sill_ins_use_wall_skin,
						bEnableBoard					= bEnableBoard,
						gs_board_inside					= 0,
						bEnableVentilationProfile		= bEnableVentilationProfile,
						bVentilationProfile				= bVentilationProfile,
						iVentilationProfileType			= iVentilationProfileType,
!						isVentilationProfileInSash_01	= isVentilationProfileInSash_01,
!						isVentilationProfileInSash_02	= isVentilationProfileInSash_02,
!						isVentilationProfileInSash_03	= isVentilationProfileInSash_03,
!						isVentilationProfileInSash_04	= isVentilationProfileInSash_04,
!						isVentilationProfileInSash_05	= isVentilationProfileInSash_05,
!						isVentilationProfileInSash_06	= isVentilationProfileInSash_06,
!						isVentilationProfileInSash_07	= isVentilationProfileInSash_07,
!						isVentilationProfileInSash_08	= isVentilationProfileInSash_08,
!						isVentilationProfileInSash_09	= isVentilationProfileInSash_09,
!						isVentilationProfileInSash_10	= isVentilationProfileInSash_10,
!						isVentilationProfileInSash_11	= isVentilationProfileInSash_11,
						bEnableShutter					= bEnableShutter,
						bSunshade						= 0,
!						gs_sunShade_iType				= gs_sunShade_iType,
						listFieldID		 				= iUiID_FloorPlanSection,
						isEmptyOpening					= 0
return


