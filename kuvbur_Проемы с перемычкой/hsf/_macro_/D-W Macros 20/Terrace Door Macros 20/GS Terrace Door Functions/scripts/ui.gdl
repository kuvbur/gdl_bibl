


! =============================================================================
! Tabpage Structure Definition
! =============================================================================

! call all available page fix IDs
call "ui_tabIDListHierarchy_m" parameters iUITreeStandardType = 1,		!TERRACEDOOR_UI
					returned_parameters iUiID_Root,
										iUiID_General,		iParID_General,
										iUiID_ShapeOpening,	iParID_ShapeOpening,
										iUiID_OpLines,		iParID_OpLines,
										iUiID_WindowFrame,	iParID_WindowFrame,
										iUiID_SashLeaf,		iParID_SashLeaf,
										iUiID_Grid,			iParID_Grid,
										iUiID_SillBoard,	iParID_SillBoard,
										iUiID_VenetianBlind, iParID_VenetianBlind,
										iUiID_Shutter,		iParID_Shutter,
										iUiID_Closure,		iParID_Closure,
										iUiID_ModelAttrib,	iParID_ModelAttrib,
										iUiID_FloorPlanSection, iParID_FloorPlanSection,
										iUiID_Descriptions,	iParID_Descriptions


file_dependence "uiTab_DoorGeneral_flat(1)"
file_dependence "uiTab_OpeningClosure_flat(1)"
file_dependence "uiTab_DoorOpeningLines_flat(1)"


! =============================================================================
! Tabpages
! =============================================================================

ui_current_page gs_ui_current_page

ui_dialog `Параметры Балконной Двери`
stSubTabpageToolTip 	= `Выбор Вкладки: Общие Параметры, Реквизиты`


for iTabs = 1 to vardim1(uiUsedPageIDs)
	if uiUsedPageIDs[iTabs][1] = iUiID_General then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Общие Параметры`, "uiTab_DoorGeneral_flat(1)"
		if gs_ui_current_page = iUiID_General then
			gosub "pageGeneralSettings"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_Closure then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Замыкание Слоев`, "uiTab_OpeningClosure_flat(1)"
		if gs_ui_current_page = iUiID_Closure then
			gosub "pageCallCavityClosure"
		endif
	endif
	if uiUsedPageIDs[iTabs][1] = iUiID_OpLines then
		ui_page	uiUsedPageIDs[iTabs][1], uiUsedPageIDs[iTabs][2], `Линии Открывания`, "uiTab_DoorOpeningLines_flat(1)"
		if gs_ui_current_page = iUiID_OpLines then
			gosub "pageCallOpeningLines"
		endif
	endif
next iTabs

end


! =============================================================================
! Cavity Closure Settings Tabpage
! =============================================================================

"pageCallCavityClosure":
	call "terrace_CavityClosure" parameters gs_macro_version = 12,
			gs_useWallFinishSkin		= gs_useWallFinishSkin,
			iClosureMethod				= iClosureMethod,
			gs_bJoint					= gs_bJoint,
			gs_stCavityClosure			= gs_stCavityClosure
return


! =============================================================================
! Opening Lines Settings Tabpage
! =============================================================================

"pageCallOpeningLines":
	call "OpeningOptions" parameters	bWindow						= 1,
										iLineShapeCategory			= iLineShapeCategory,
										iLineShapeSwingEnable		= iLineShapeSwingEnable,
										gs_leaf_optype_m			= gs_leaf_optype_m,
										bOverride_MVO_2D			= bOverride_MVO_2D,
										or_iSwingType				= or_iSwingType,
										or_iSlidingType				= or_iSlidingType,
										bOverride_MVO_3D			= bOverride_MVO_3D,
										or_bOplines3d				= or_bOplines3d,
										or_iOplineStyle				= or_iOplineStyle,
										b2ColumnsOnOplineUI			= b2ColumnsOnOplineUI,
										bHideOplineStyle			= bHideOplineStyle,
										bLockOplineStyle			= bLockOplineStyle,
										bDisable2DOverride			= bDisable2DOverride,
										bDisable3DOverride			= bDisable3DOverride,
										bDisableSwingType			= bDisableSwingType,
										iOpeningParamType			= iOpeningParamType,
										bEnableSecondaryOpLineTypes = 0,
										pageStartY					= 3

return


! =============================================================================
! Built-In General Settings Tabpage
! =============================================================================
"pageGeneralSettings":
	!*** Separator positions

	sep1x = 292
	
	dy = 21
	
		pageStartY = 8
		sepy  = 95 + pageStartY

		ui_separator sep1x,pageStartY - 7, sep1x,226 + pageStartY
		ui_separator 0,sepy, sep1x-2,sepy

		x1 = 2
		y = pageStartY
		cx1 = 70
		cx = 55

		ui_style 0,1

		ui_outfield gs_stGeneralSettings[27], x1, y+3, x1+cx1, 20	! Width
		ui_infield "A", x1+cx1+5, y, cx,dy

		ui_style 0,0

		ui_outfield gs_stGeneralSettings[33], x1+cx1+5+cx+15, y+3, x1+cx1, 20	! Unit Width
		ui_infield "gs_frame_sizex", x1+cx1+5+cx+15+cx1+5, y, cx,dy
		y = y + dy

		ui_style 0,1

		ui_outfield gs_stGeneralSettings[28], x1, y+3, x1+cx1, 20	! Height
		ui_infield "B", x1+cx1+5, y, cx,dy

		ui_style 0,0

		ui_outfield gs_stGeneralSettings[34], x1+cx1+5+cx+15, y+3, x1+cx1, 20	! Unit Height
		ui_infield "gs_frame_sizey", x1+cx1+5+cx+15+cx1+5, y, cx,dy
		y = y + dy

		ui_outfield gs_stGeneralSettings[32], x1, y+5, cx1, 20		! Tolerance
		ui_infield "gs_tolerance", x1+cx1+5, y, cx,dy
		y = y + dy + 5

		ui_style 0,1

		ui_infield{3} "gs_lich", x1, y, sep1x-x1-2, 19,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"", gs_stGeneralSettings[35], 0,
			"", gs_stGeneralSettings[35], 1		! Nominal Size = Jamb Inner Size

		ui_style 0,0


		!******************** symbol picture, sill/board

		p_x = 5
		t_x = 160
		p_y = 135 + pageStartY

		cx = 105

		x1 = 0
		x2 = x1 + 20
		x3 = 150
		x4 = x3 + 20

		ui_style 0,1

		ui_infield{3} "gs_feba_a", x1, p_y-27, x3-x1-1, 19,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"", gs_stGeneralSettings[37], 0,
			"", gs_stGeneralSettings[37], 1		! Window Sill

		ui_infield{3} "feba_a2", x3, p_y-27, sep1x-x3-1, 19,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"", gs_stGeneralSettings[38], 0,
			"", gs_stGeneralSettings[38], 1		! Door Sill

		ui_infield{3} "gs_fi", x1, p_y+50+10, x3-x1-1, 19,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"", gs_stGeneralSettings[39], 0,
			"", gs_stGeneralSettings[39], 1		! Window Board

		ui_infield{3} "feba_i2", x3, p_y+50+10, sep1x-x3-2, 19,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"", gs_stGeneralSettings[40], 0,
			"", gs_stGeneralSettings[40], 1		! Door Board

		if gs_feba_a+feba_a2+gs_fi+feba_i2=4 then
			ui_pict 23, p_x, p_y, 268, 50, 1	! balc1_s11_b11.png
		else
			if gs_feba_a+feba_a2+gs_fi+feba_i2=0 then
				ui_pict 8, p_x, p_y, 268, 50, 1		! balc1_s00_b00.png
			else
				if gs_feba_a+feba_a2=0 then
					if gs_fi=1 and feba_i2=0 then ui_pict 10, p_x, p_y, 268, 50, 1	! balc1_s00_b10.png
					if gs_fi=0 and feba_i2=1 then ui_pict  9, p_x, p_y, 268, 50, 1	! balc1_s00_b01.png
					if gs_fi=1 and feba_i2=1 then ui_pict 11, p_x, p_y, 268, 50, 1	! balc1_s00_b11.png
				endif
				if gs_fi+feba_i2=0 then
					if gs_feba_a=1 and feba_a2=0 then ui_pict 16, p_x, p_y, 268, 50, 1	! balc1_s10_b00.png
					if gs_feba_a=0 and feba_a2=1 then ui_pict 12, p_x, p_y, 268, 50, 1	! balc1_s01_b00.png
					if gs_feba_a=1 and feba_a2=1 then ui_pict 20, p_x, p_y, 268, 50, 1	! balc1_s11_b00.png
				endif
				if gs_feba_a+feba_a2=2 then
					if gs_fi=1 and feba_i2=0 then ui_pict 22, p_x, p_y, 268, 50, 1	! balc1_s11_b10.png
					if gs_fi=0 and feba_i2=1 then ui_pict 21, p_x, p_y, 268, 50, 1	! balc1_s11_b01.png
				endif
				if gs_fi+feba_i2=2 then
					if gs_feba_a=1 and feba_a2=0 then ui_pict 19, p_x, p_y, 268, 50, 1	! balc1_s10_b11.png
					if gs_feba_a=0 and feba_a2=1 then ui_pict 15, p_x, p_y, 268, 50, 1	! balc1_s01_b11.png
				endif
			if gs_feba_a=1 and feba_a2=0 and gs_fi=1 and feba_i2=0 then \
				ui_pict 18, p_x, p_y, 268, 50, 1	! balc1_s10_b10.png
			if gs_feba_a=1 and feba_a2=0 and gs_fi=0 and feba_i2=1 then \
				ui_pict 17, p_x, p_y, 268, 50, 1	! balc1_s10_b01.png
			if gs_feba_a=0 and feba_a2=1 and gs_fi=1 and feba_i2=0 then \
				ui_pict 14, p_x, p_y, 268, 50, 1	! balc1_s01_b10
			if gs_feba_a=0 and feba_a2=1 and gs_fi=0 and feba_i2=1 then \
				ui_pict 13, p_x, p_y, 268, 50, 1	! balc1_s01_b01.png
			endif
		endif


		!********************** cavity closure

		y = pageStartY
		px = 30
		py = 20
		cx = px + 10
		cy = py + 4

		ui_style 0,1

		ui_outfield gs_stCavityClosure[10], sep1x+10, y, 140, 20	! Cavity Closure
		y = y + dy + 10

		ui_style 0, 0

		ui_infield{3} "iClosureMethod", sep1x+15, y, cx, cy,
			2, 50,	! ui_cavityclosure_type.png
			3, 1,
			cx, cy, px, py,
			1,	gs_stCavityClosure[1],	CLOSURE_METHOD_NONE,
			2,	gs_stCavityClosure[2],	CLOSURE_METHOD_TRADITIONAL,
			3,	gs_stCavityClosure[3],	CLOSURE_METHOD_PREFABRICATED

		!********************** detail level

		cx = 120
		ui_style 0, 1

		y = sepy - 15

		ui_outfield gs_stGeneralSettings[18], sep1x+10, y, cx, 15	! 2D Detail Level
		y = y + dy
		ui_style 0, 0
		ui_infield{3} "iDWDet2D", sep1x+30, y, cx, 19,
				8, "",
				0, 0,
				0, 0, 0, 0,	
				"",	stDetail2D[1], DETLEVEL_DW2D_MVO,		
				"",	stDetail2D[2], DETLEVEL_DW2D_SCSENS,
				"",	stDetail2D[3], DETLEVEL_DW2D_50,
				"",	stDetail2D[4], DETLEVEL_DW2D_100,
				"",	stDetail2D[5], DETLEVEL_DW2D_200,
				"",	stDetail2D[6], DETLEVEL_DW2D_OFF,
				"",	stDetail2D[7], DETLEVEL_DW2D_CHISMPL,
				"",	stDetail2D[8], DETLEVEL_DW2D_CHIDET

		y = y + dy + 5

		ui_style 0, 1
		ui_outfield gs_stGeneralSettings[19], sep1x+10, y, cx, 15	! 3D Detail Level
		y = y + dy
		ui_style 0, 0
		ui_infield{3} "iDWDet3D", sep1x+30, y, cx, 19,
				8, "",
				0, 0,
				0, 0, 0, 0,	
				"",	stDetail[1],	DETLEVEL_DW3D_SCALESENS,
				"",	stDetail[2],	DETLEVEL_DW3D_DETAILED,
				"",	stDetail[3],	DETLEVEL_DW3D_SIMPLE,
				"",	stDetail[4],	DETLEVEL_DW3D_OFF,
				"",	stDetail[5],	DETLEVEL_DW3D_CHI_SIMPLE,
				"",	stDetail[6],	DETLEVEL_DW3D_CHI_DETAILED

		y = y + dy + 5

		ui_style 0, 1
		ui_outfield gs_stGeneralSettings[36], sep1x+10, y, cx, 15	! Section Detail Level
		y = y + dy
		ui_style 0, 0
		ui_infield{3} "iDWDetSE", sep1x+30, y, cx, 19,
				8, "",
				0, 0,
				0, 0, 0, 0,	
				"", `Зависит от Масштаба`, DETLEV_SE_SCALESENSITIVE,
				"", `Детальный`, DETLEV_SE_DETAILED,
				"", `Середина`, DETLEV_SE_MIDDLE,
				"", `Простой`, DETLEV_SE_SIMPLE,
				"", `Откл.`, DETLEV_SE_OFF
		y = y + dy + 5

		ui_style 0, 1
	
return


