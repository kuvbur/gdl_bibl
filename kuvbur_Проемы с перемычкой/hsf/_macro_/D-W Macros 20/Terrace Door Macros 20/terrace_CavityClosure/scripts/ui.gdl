

pageStartY = 5

bDefaultElem = (GLOB_INTGUID = "" or GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")
bDefaultElem = 1	! GLOB_INTGUID always 0 !

showTurnPlaster		= 1
showCavityClosure	= 1

if not(bDefaultElem) then
	if WALL_SKINS_NUMBER < 2 then showTurnPlaster	= 0
	if WALL_SKINS_NUMBER = 0 then showCavityClosure	= 0
endif



sep1x = 222
sep2y = 45 + pageStartY
sep3y = 127 + pageStartY

ui_separator sep1x,pageStartY - 4, sep1x,229 + pageStartY
ui_separator 0,sep2y, sep1x-2,sep2y
ui_separator 0,sep3y, sep1x-2,sep3y

! Use Wall Finish Component ---------------------------------------------------

x0 = 0
x1 = 20
x2 = sep1x - 2
y = pageStartY

iFlag = 4 * not(showTurnPlaster | showCavityClosure)

ui_style 0, 0
cx = 18
ui_outfield gs_stCavityClosure[32], x1,y+3, x2-x1-1,18, iFlag		! Use Wall Finish Component
ui_infield "gs_useWallFinishSkin", x0,y, cx,18


! Plaster Thickness Limit -----------------------------------------------------

x1 = 0
x2 = 215
y = 18 + pageStartY

iFlag = 4 * (not(showTurnPlaster | showCavityClosure) | gs_useWallFinishSkin)

cx = 80
ui_style 0, 0
ui_outfield gs_stCavityClosure[31], x0,y,x2-x1-cx-5,27, iFlag		! Plaster Thickness Limit
ui_infield "gs_maxPlasterThk", x2-cx,y+3, cx,18 ui_tooltip `Считать Слой Штукатуркой, если он Тоньше или Равен`


! Turn Plaster ----------------------------------------------------------------

x1 = 0
x2 = 10
x3 = 115
x4 = 162
x5 = 182
x6 = sep1x - 2
y = sep2y + 2
dy = 20

iFlag = 4 * not(showTurnPlaster)

ui_style 0,1
ui_outfield gs_stCavityClosure[26], x1,y+2, x6-x1-1,18				! Turn Plaster

y = y + 20
ui_style 0, 0
ui_outfield gs_stCavityClosure[30], x1,y+2, sep1x-x1-1,18, iFlag	! Number of Skins to Turn
y = y + 18


ui_outfield gs_stCavityClosure[27], x2,y+3, x3-x2-1,18, iFlag		! Outside Face
cx = 40
ui_infield "gs_bOverIn",			x3,y, cx,22
cx = 18
ui_infield "gs_bAutoTurnIn",	x4,y+2, cx,18
ui_outfield gs_stCavityClosure[29], x5,y+3, x6-x5-1,18, iFlag		! Auto Turn
y = y + dy

ui_outfield gs_stCavityClosure[28], x2,y+3, x3-x2-1,18, iFlag		! Inside Face
cx = 40
ui_infield "gs_bOverOut",			x3,y, cx,22
cx = 18
ui_infield "gs_bAutoTurnOut",	x4,y+2, cx,18
ui_outfield gs_stCavityClosure[29], x5,y+3, x6-x5-1,18, iFlag		! Auto Turn
y = y + dy


! Cavity Closure --------------------------------------------------------------

y = sep3y + 2
dy = 20

x0 = 0
x1 = 10
x2 = 215

ui_style 0,1
ui_outfield gs_stCavityClosure[10], x0,y+2,sep1x-x0-1,15		! Cavity Closure
y = y + dy

px = 30
py = 20
cx = px + 10
cy = py + 4

iFlag = 4 * not(showCavityClosure)

ui_style 0, 0
ui_outfield gs_stCavityClosure[11], x1,y+3,x2-x1-cx-18-1,18, iFlag	! Closure Method
ui_infield{3} "iClosureMethod", x2-cx-18,y-7, cx, cy,
	2, 1,		! ui_cavityclosure_type.png
	3, 1,
	cx, cy, px, py,
	1,	gs_stCavityClosure[1],	CLOSURE_METHOD_NONE,
	2,	gs_stCavityClosure[2],	CLOSURE_METHOD_TRADITIONAL,
	3,	gs_stCavityClosure[3],	CLOSURE_METHOD_PREFABRICATED
y = y + dy


iFlag = 4 * not(iClosureMethod <> CLOSURE_METHOD_NONE & showCavityClosure)

cx = 100
ui_style 0, 0
ui_outfield gs_stCavityClosure[12], x1,y+3, x2-x1-cx-1,18, iFlag		! Turn Skin on Side
ui_infield{3}	"iCavityClosureSide", x2-cx, y, cx, 18,
		8, "", 3, 1,
		0, 0, 0, 0,
		1, gs_stCavityClosure[6], CAVITY_SIDE_BOTH,
		2, gs_stCavityClosure[7], CAVITY_SIDE_LEFT,
		3, gs_stCavityClosure[8], CAVITY_SIDE_RIGHT
y = y + dy

if iClosureMethod = CLOSURE_METHOD_NONE | iClosureMethod = CLOSURE_METHOD_TRADITIONAL then

	iFlag = 4 * not(iClosureMethod = CLOSURE_METHOD_TRADITIONAL & showCavityClosure)

	cx = 50
	ui_outfield gs_stCavityClosure[14], x1,y+2, x2-x1-cx-1,18, iFlag		! Number of Skins to Turn
	ui_infield "gs_nTurn", x2-cx,y, cx,18
	y = y + dy

	cx = 100
	ui_outfield gs_stCavityClosure[15], x1,y+2, x2-x1-cx-1,18, iFlag		! Number from
	ui_infield{3}	"iCavityClosureTurnDir", x2-cx, y, cx, 18,
			8, "", 2, 1,
			0, 0, 0, 0,
			1, gs_stCavityClosure[4], CAVITY_TURN_REFERENCE,
			2, gs_stCavityClosure[5], CAVITY_TURN_OPPOSITE

	y = y + dy
endif


! Insulation Strip and Plug ---------------------------------------------------

x0 = 233
x1 = x0 + 30
x2 = 444
y = pageStartY
dy = 25

if iClosureMethod = CLOSURE_METHOD_NONE | iClosureMethod = CLOSURE_METHOD_TRADITIONAL then

	iFlag = 4 * not(iClosureMethod = CLOSURE_METHOD_TRADITIONAL & showCavityClosure)

	ui_style 0,1
	cx = 18
	ui_outfield gs_stCavityClosure[17], x1,y+3, x2-x1-1,18, iFlag		! Insulation Strip
	ui_infield "gs_bJoint", x0,y, cx,18
	y = y + dy

	bFlag = not(gs_bJoint & iClosureMethod = CLOSURE_METHOD_TRADITIONAL & showCavityClosure)
	iFlag = 4 * bFlag

	ui_style 0, 0
	cx = 60
	ui_outfield gs_stCavityClosure[18], x1,y+3, x2-x1-cx-1,18, iFlag		! Thickness
	ui_infield "gs_thkJoint", x2-cx,y, cx,18
	y = y + dy
	y = y + 10

endif

if iClosureMethod = CLOSURE_METHOD_PREFABRICATED then

	bFlag = not(showCavityClosure)
	iFlag = 4 * bFlag

	ui_style 0,1
	ui_outfield gs_stCavityClosure[24], x0,y+3, x2-x0-1,18, iFlag		! Profile
	y = y + dy

	ui_style 0, 0
	cx = 60
	ui_outfield gs_stCavityClosure[25], x1,y+3, x2-x1-cx-1,18, iFlag		! Width
	ui_infield "gs_widthPlug", x2-cx,y, cx,18
	y = y + dy
	y = y + 10

endif



