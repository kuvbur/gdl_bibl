
if lod2D = 0 then end

! Wall Contour Line
WALL_CONTOUR_OFF		= 1
WALL_CONTOUR_INSIDE		= 2
WALL_CONTOUR_OUTSIDE	= 3
WALL_CONTOUR_BOTHSIDES	= 4


if (abs(SYMB_ROTANGLE)<EPS and SYMB_MIRRORED) or (abs(SYMB_ROTANGLE-180)<EPS and not(SYMB_MIRRORED)) then
	jamb1=WIDO_RIGHT_JAMB
	jamb2=WIDO_LEFT_JAMB
else
	jamb2=WIDO_RIGHT_JAMB
	jamb1=WIDO_LEFT_JAMB
endif


pen gs_pen_2D


a0=a
a_original=a

if symm then bf1=a * (nWindows / (nWindows + 1))


!**********************************
!************************** Fenster

if lod2D = DET2D_FS_SIMPLE_ONE then		goto 1100
if lod2D = DET2D_FS_SIMPLE_TWO | lod2D = DET2D_FS_MIDDLE then		goto 1050
if lod2D = DET2D_FS_DETAILED then		goto 1020



!********************* scale 1:500

1500:

end

!********************* scale 1:200<

1200:

end



!********************* scale 1:200

1100:


gosub 2000


end


!********************* scale 1:100


1050:


gosub 2000



end




!********************* scale 1:20/50


1020:


gosub 2000



end


2000:


!********************************** wall edge lines
!**************************************************

if WIDO_REVEAL_SIDE then
	add2 0, -gs_frame_thk
	mul2 1, -1
endif


pen gs_pen_brust


if dars_2d=0 then
	feba_i=0
	feba_i2=0
endif


!*********** outside *********
!*****************************

!*** line under window

line_type gs_wcont_line_out

if gs_wcont_line_m = WALL_CONTOUR_OUTSIDE | gs_wcont_line_m = WALL_CONTOUR_BOTHSIDES then
	if gs_feba_a then
		line2 -a/2+jamb1+thkSkinAtSillLeft, WIDO_SILL, -a/2+bf1-stb_m/2, WIDO_SILL

		if gs_fbl<WIDO_SILL then line2 -a/2+bf1-stb_m/2, WIDO_SILL, -a/2+bf1-stb_m/2, gs_fbl
		if lod2D = DET2D_FS_SIMPLE_ONE  then line2 -a/2+bf1-stb_m/2, WIDO_SILL, -a/2+bf1-stb_m/2, 0
	endif
endif

!*** line under door

line_type gs_wcont_line_out_d

if gs_wcont_line_d_m = WALL_CONTOUR_OUTSIDE | gs_wcont_line_d_m = WALL_CONTOUR_BOTHSIDES then
	if feba_a2=1 then line2 a/2-jamb2, WIDO_SILL, -a/2+bf1-stb_m/2, WIDO_SILL
endif



!*********** inside **********
!*****************************


!*** line under window

line_type gs_wcont_line_in

if gs_wcont_line_m = WALL_CONTOUR_INSIDE or gs_wcont_line_m = WALL_CONTOUR_BOTHSIDES then
	if feba_i=1 then
		line2 -a/2+thkSkinAtBoardLeft, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp, -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp
	endif
endif

if gs_siv<WALL_THICKNESS-WIDO_SILL-gs_frame_thk then\
	line2 -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp, -a/2+bf1-stb_m/2, -gs_frame_thk-gs_siv

!*** line under door

line_type gs_wcont_line_in_d

if gs_wcont_line_d_m = WALL_CONTOUR_INSIDE or gs_wcont_line_d_m = WALL_CONTOUR_BOTHSIDES then
	if feba_i2=1 then line2 a/2, WIDO_SILL-WALL_THICKNESS, -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS
endif





!************** lines of wall edges when no window board/sill


!***** line between window and door

line_type gs_wcont_line_out

if lod2D = DET2D_FS_SIMPLE_ONE then

	if dars_2D=0 and GLOB_ELEVATION>0.001 then
		line2 -a/2+bf1-stb_m/2, WIDO_SILL, -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS
	else
		line2 -a/2+bf1-stb_m/2, 0, -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp
	endif

else

	line_type gs_wcont_line_out

	if gs_feba_a+feba_a2=0 then
		if lod2D <> DET2D_FS_DETAILED or bMainFieldFix or not(WIDO_REVEAL_SIDE) then
			if gs_opening_dir_m = 2 and not(bMainFieldFix) then
				if WIDO_SILL> gs_frame_thk-gs_vt then line2 -a/2+bf1-stb_m/2, WIDO_SILL, -a/2+bf1-stb_m/2, gs_sash_thk-gs_vt
			else
				line2 -a/2+bf1-stb_m/2, WIDO_SILL, -a/2+bf1-stb_m/2, 0
			endif
		else
			line2 -a/2+bf1-stb_m/2, WIDO_SILL, -a/2+bf1-stb_m/2, gs_sash_thk-gs_vt
		endif
	endif

	line_type gs_wcont_line_in

	if feba_i+feba_i2=0 then

		if dars_2D=0 and GLOB_ELEVATION>0.001 then
			if lod2D <> DET2D_FS_DETAILED or bMainFieldFix or WIDO_REVEAL_SIDE then
				line2 -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS, -a/2+bf1-stb_m/2, -gs_frame_thk
			else
				line2 -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS, -a/2+bf1-stb_m/2, -gs_vt-gs_sash_thk
			endif
		else
			if lod2D <> DET2D_FS_DETAILED or bMainFieldFix or WIDO_REVEAL_SIDE or gs_opening_dir_m = 2 then
				line2 -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp, -a/2+bf1-stb_m/2, -gs_frame_thk
			else
				line2 -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp, -a/2+bf1-stb_m/2, -gs_vt-gs_sash_thk
			endif
		endif
	endif

endif


!**************************** outside

!*** line at window side

line_type gs_wcont_line_out

if gs_wcont_line_m = WALL_CONTOUR_OUTSIDE | gs_wcont_line_m = WALL_CONTOUR_BOTHSIDES then
	if not(gs_feba_a) then
		if gs_opening_dir_m = 2 and not(bMainFieldFix) then
			if WIDO_SILL> gs_frame_thk-gs_vt then
				line2 -a/2+jamb1, WIDO_SILL, -a/2+bf1-stb_m/2, WIDO_SILL
			endif
		else
			line2 -a/2+jamb1, WIDO_SILL, -a/2+bf1-stb_m/2, WIDO_SILL
		endif
	endif
endif




!*** line at door side

line_type gs_wcont_line_out_d

if gs_wcont_line_d_m = WALL_CONTOUR_OUTSIDE | gs_wcont_line_d_m = WALL_CONTOUR_BOTHSIDES then
	if feba_a2=0 then line2 a/2-jamb2, WIDO_SILL, -a/2+bf1-stb_m, WIDO_SILL
endif



!***************************** inside

!*** line at window side

line_type gs_wcont_line_in

if gs_wcont_line_m = WALL_CONTOUR_INSIDE | gs_wcont_line_m = WALL_CONTOUR_BOTHSIDES then
	if feba_i=0 then
		line2 -a/2, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp, -a/2+bf1-stb_m/2, WIDO_SILL-WALL_THICKNESS+tiefe_hkorp
	endif
endif


!*** line at door side

line_type gs_wcont_line_in_d

if gs_wcont_line_d_m = WALL_CONTOUR_INSIDE | gs_wcont_line_d_m = WALL_CONTOUR_BOTHSIDES then
	if feba_i2=0 then line2 a/2, WIDO_SILL-WALL_THICKNESS, -a/2+bf1-stb_m, WIDO_SILL-WALL_THICKNESS
endif

line_type SYMB_LINETYPE
pen gs_pen_2D

if lod2D > DET2D_FS_SIMPLE_ONE then

	!**************************** 1:50, 1:20

	fill gs_wallhole_fill


	!************************* outside sills

	if gs_feba_a then
		if gs_fbl > eps then
			fill gs_wallhole_fill
			put		-a/2+bf1-stb_m/2,				gs_fbl,			0,
					-a/2+jamb1-suba_l,				gs_fbl,			0,
					-a/2+jamb1-suba_l,				WIDO_SILL,		0,
					-a/2+jamb1+thkSkinAtSillLeft,	WIDO_SILL,		0,
					-a/2+jamb1+thkSkinAtSillLeft,	0,				0,
					-a/2+bf1-stb_m/2,				0,				0

			if GLOB_CONTEXT = 5 then
				poly2_b nsp/3, 1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					get(nsp)
			else
				wallhole2 nsp/3,1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					0, 0, 0,
					get(nsp)
			endif
		endif

		pen penSkinAtSill
		line_type ltSkinAtSill
		line_property 2
		wallline2 -a/2+jamb1-suba_l,	WIDO_SILL,
				  -a/2+jamb1,			WIDO_SILL

		pen gs_pen_2D
		line_type 1
		line_property 0

		line2 -a/2+jamb1-suba_l, gs_fbl, -a/2+bf1-stb_m/2, gs_fbl
		line2 -a/2+jamb1-suba_l, gs_fbl, -a/2+jamb1-suba_l, WIDO_SILL
		if lod2D <> DET2D_FS_DETAILED or bMainFieldFix or WIDO_REVEAL_SIDE then
			line2 -a/2+bf1-stb_m/2, gs_fbl, -a/2+bf1-stb_m/2, 0
		else
			line2 -a/2+bf1-stb_m/2, gs_fbl, -a/2+bf1-stb_m/2, gs_vt
		endif
	endif
	if (not(gs_feba_a) | not(gs_fbl > EPS)) & WIDO_SILL > EPS then
		fill 0
		wallhole2 4,2 + 64, 0, -1,
			0,0,0,
			-a/2+bf1-stb_m/2,				WIDO_SILL+0.1,	0,
			-a/2+jamb1+thkSkinAtSillLeft,	WIDO_SILL+0.1,	0,
			-a/2+jamb1+thkSkinAtSillLeft,	0,				0,
			-a/2+bf1-stb_m/2,				0,				0
	endif

	if feba_a2=1 then
		fill gs_wallhole_fill
		put		-a/2+bf1-stb_m/2,				gs_fbl,	0,
				 a/2-jamb2+suba_r,				gs_fbl,	0,
				 a/2-jamb2+suba_r,				WIDO_SILL,		0,
				 a/2-jamb2-thkSkinAtSillLeft,	WIDO_SILL,		0,
				 a/2-jamb2-thkSkinAtSillLeft,	0,	0,
				-a/2+bf1-stb_m/2,				0,	0

		if gs_fbl > eps then
			if GLOB_CONTEXT = 5 then
				poly2_b nsp/3, 1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					get(nsp)
			else
				wallhole2 nsp/3,1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					0,0,0,
					get(nsp)
			endif
		endif

		pen penSkinAtSill
		line_type ltSkinAtSill
		line_property 2
		wallline2 a/2-jamb2+suba_r,	WIDO_SILL,
				  a/2-jamb2,		WIDO_SILL

		pen gs_pen_2D
		line_type 1
		line_property 0

		line2  a/2-jamb2+suba_r, gs_fbl, -a/2+bf1-stb_m/2, gs_fbl
		line2  a/2-jamb2+suba_r, gs_fbl, a/2-jamb2+suba_r, WIDO_SILL
		if not(gs_feba_a) then
			line2 -a/2+bf1-stb_m/2, gs_fbl, -a/2+bf1-stb_m/2, 0
		endif
	endif
	if not(feba_a2) & WIDO_SILL > EPS then
		fill 0
		wallhole2 4,2 + 64, 0, -1,
			0,0,0,
			-a/2+bf1-stb_m/2,				WIDO_SILL+0.1,	0,
			 a/2-jamb2-thkSkinAtSillLeft,	WIDO_SILL+0.1,	0,
			 a/2-jamb2-thkSkinAtSillLeft,	0,				0,
			-a/2+bf1-stb_m/2,				0,				0
	endif

	!************************* inside boards

	if feba_i+dars_2D=2 then
		if abs (-gs_frame_thk-siv) > eps then
			fill gs_wallhole_fill
			put	-a/2-gs_subi_l,				-gs_frame_thk-siv,	0,
				-a/2+bf1-stb_m/2,			-gs_frame_thk-siv,	0,
				-a/2+bf1-stb_m/2,			0,					0,
				-a/2+thkSkinAtBoardLeft,	0,					0,
				-a/2+thkSkinAtBoardLeft,	WIDO_SILL - WALL_THICKNESS,	0,
				-a/2-gs_subi_l,				WIDO_SILL - WALL_THICKNESS,	0

			if GLOB_CONTEXT = 5 then
				poly2_b nsp/3, 1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					get(nsp)
			else
				wallhole2 nsp/3,1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					0,0,0,
					get(nsp)
			endif
		endif

		pen penSkinAtBoard
		line_type ltSkinAtBoard
		line_property 2
		wallline2 -a/2,				WIDO_SILL - WALL_THICKNESS,
				  -a/2-gs_subi_l,	WIDO_SILL - WALL_THICKNESS

		pen gs_pen_2D
		line_type 1
		line_property 0

		line2 -a/2-gs_subi_l, -gs_frame_thk-siv, -a/2+bf1-stb_m/2, -gs_frame_thk-siv
		line2 -a/2-gs_subi_l, WIDO_SILL-WALL_THICKNESS, -a/2-gs_subi_l, -gs_frame_thk-siv

		if lod2D <> DET2D_FS_DETAILED or bMainFieldFix or not(WIDO_REVEAL_SIDE) then
			line2 -a/2+bf1-stb_m/2, -gs_frame_thk, -a/2+bf1-stb_m/2, -gs_frame_thk-siv
		else
			line2 -a/2+bf1-stb_m/2, -gs_vt-gs_sash_thk, -a/2+bf1-stb_m/2, -gs_frame_thk-siv
		endif
	endif
	if feba_i+dars_2D <> 2 | not(abs(-gs_frame_thk-siv) > EPS) & (WALL_THICKNESS - WIDO_SILL) > EPS then
		fill 0
		wallhole2 4,2 + 64, 0, -1,
			0,0,0,
			-a/2+bf1-stb_m/2,			WIDO_SILL-WALL_THICKNESS-0.1,	0,
			-a/2+thkSkinAtBoardLeft,	WIDO_SILL-WALL_THICKNESS-0.1,	0,
			-a/2+thkSkinAtBoardLeft,	0,								0,
			-a/2+bf1-stb_m/2,			0,								0
	endif

	if feba_i2+dars_2D=2 then
		if abs (-gs_frame_thk-siv) > eps then
			fill gs_wallhole_fill
			put	-a/2+bf1-stb_m/2,			-gs_frame_thk-siv,			0,
				 a/2+gs_subi_r,				-gs_frame_thk-siv,			0,
				 a/2+gs_subi_r,				WIDO_SILL-WALL_THICKNESS,	0,
				 a/2-thkSkinAtBoardRight,	WIDO_SILL-WALL_THICKNESS,	0,
				 a/2-thkSkinAtBoardRight,	0,	0,
				-a/2+bf1-stb_m/2,			0,	0

			if GLOB_CONTEXT = 5 then
				poly2_b nsp/3, 1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					get(nsp)
			else
				wallhole2 nsp/3,1+2+4+64, gs_wallhole_pen_fg, gs_wallhole_pen_bg,
					0,0,0,
					get(nsp)
			endif
		endif
		pen penSkinAtBoard
		line_type ltSkinAtBoard
		line_property 2
		wallline2 a/2,				WIDO_SILL - WALL_THICKNESS,
				  a/2+gs_subi_r,	WIDO_SILL - WALL_THICKNESS

		pen gs_pen_2D
		line_type 1
		line_property 0

		line2 a/2+gs_subi_r, -gs_frame_thk-siv, -a/2+bf1-stb_m/2, -gs_frame_thk-siv
		line2 a/2+gs_subi_r, WIDO_SILL-WALL_THICKNESS, a/2+gs_subi_r, -gs_frame_thk-siv

		if lod2D <> DET2D_FS_DETAILED or bMainFieldFix or WIDO_REVEAL_SIDE then
			line2 -a/2+bf1-stb_m/2, -gs_frame_thk, -a/2+bf1-stb_m/2, -gs_frame_thk-siv
		else
			line2 -a/2+bf1-stb_m/2, -gs_vt-gs_sash_thk, -a/2+bf1-stb_m/2, -gs_frame_thk-siv
		endif
	endif
	if (feba_i2+dars_2D <> 2 | not(abs(-gs_frame_thk-siv) > EPS)) & (WALL_THICKNESS - WIDO_SILL) > EPS then
		fill 0
		wallhole2 4,2 + 64, 0, -1,
			0,0,0,
			-a/2+bf1-stb_m/2,			WIDO_SILL-WALL_THICKNESS-0.1,	0,
			 a/2-thkSkinAtBoardRight,	WIDO_SILL-WALL_THICKNESS-0.1,	0,
			 a/2-thkSkinAtBoardRight,	0,								0,
			-a/2+bf1-stb_m/2,			0,								0
	endif

endif



if WIDO_REVEAL_SIDE then
	del 2
endif

return



end
