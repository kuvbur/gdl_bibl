
unID = officeCab01_unID

if gs_detlevel_3D_m = 0 then end

if NOT(gs_shadow) then shadow off

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
	if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then
		iResol = 3
	else
		iResol = 8
	endif
else
	iResol = gs_resol
endif

resol iResol

PEN gs_cont_pen

! -----------------------------------------------------------------------------
! Cabinet parameter transform for cabindex 2
! -----------------------------------------------------------------------------

if iCabIndex = 2 then
	drawer_num			= drawer_num_2
	drawerThk			= drawerThk2
	gs_drawerHeights	= gs_drawerHeights_2
	gs_knobHorPos		= gs_knobHorPos_2
endif


! -----------------------------------------------------------------------------
! Cabinet body
! -----------------------------------------------------------------------------

cabinetHeight = zzyzx - baseHeight - topThk

MATERIAL gs_cabinet_mat
ADD 0, drawerThk, baseHeight
BLOCK a, b - drawerThk, cabinetHeight
DEL 1

GOSUB 1000


! -----------------------------------------------------------------------------
! Top
! -----------------------------------------------------------------------------

IF bTopCover THEN
	MATERIAL count_mat
	ADDZ zzyzx - topThk
	BLOCK a, b, topThk
	DEL 1
ENDIF

GOSUB 2000


! -----------------------------------------------------------------------------
! Base
! -----------------------------------------------------------------------------

if ((gs_baseType_m > 0) & bBaseNoneType) | (bBase & not(bBaseNoneType)) then

	if bBaseNoneType then
		_baseWidth = 0.02
	else
		_baseWidth = baseWidth
	endif

	MATERIAL gs_base_mat

	IF gs_baseType_m = 1 THEN		! castor
		FOR k = 1 TO 2
			ADD 0.02, 0.05, gs_baseHeight/2
			ROTX 90
			ROTY 90
			RESOL 1.5 * iResol
			cylind _baseWidth, gs_baseHeight / 2
			ADDZ a - 0.04 - _baseWidth
			cylind _baseWidth, gs_baseHeight / 2
			DEL 4
			ADDY b - 0.1
		NEXT k
		DEL 2
	ELSE
		ADDY drawerThk
		BLOCK a, b - drawerThk, gs_baseHeight
		DEL 1
	ENDIF
ENDIF


! -----------------------------------------------------------------------------
! Drawers
! -----------------------------------------------------------------------------

ADDZ zzyzx - topThk
FOR m = 1 TO drawer_num
	MATERIAL gs_door_mat

	bottomDiff = 0
	IF ABS(baseHeight) < EPS AND m = drawer_num THEN bottomDiff = 0.005

	ADDZ -gs_drawerHeights[m] + bottomDiff

	if gs_detlevel_3D_m = 1 | (gs_detlevel_3D_m = 2 & gs_bevel = 0) then
		PRISM_ 4, gs_drawerHeights[m] - 0.005 - bottomDiff,
			0, 0, 15,
			a, 0, 15,
			a, drawerThk, 15,
			0, drawerThk, 15
	else
		addy drawerThk
		rotx 90
		fprism_ gs_door_mat, gs_door_mat, gs_door_mat, gs_door_mat,
			5, drawerThk, 45, 0.01,
				0, 0, 15,
				a, 0, 15,
				a, gs_drawerHeights[m] - 0.005 - bottomDiff, 15,
				0, gs_drawerHeights[m] - 0.005 - bottomDiff, 15,
				0, 0, -1
		del 2
	endif

	gosub 2000

	if ((gs_knob_type_m <> KNOB_STYLE_NONE) & bKnobNoneType) | (bKnob & not(bKnobNoneType)) then
		! --- Knob hotspots ---
		if _act_gs_editableKnobs then
			add a/2 + gs_knobHorPos, 0, (gs_drawerHeights[m] - 0.005)/2 + gs_knobVerPos_m * (gs_drawerHeights[m] - 0.005)/4
			if iCabIndex = 1 then

				HOTSPOT 0, 0, gs_knobPosArray[m][2], unID, gs_knobPosArray[m][1], 1+128 : unID = unID + 1
				HOTSPOT gs_knobPosArray[m][1], 0, gs_knobPosArray[m][2], unID, gs_knobPosArray[m][1], 2 : unID = unID + 1
				HOTSPOT -1, 0, gs_knobPosArray[m][2], unID, gs_knobPosArray[m][1], 3 : unID = unID + 1

				HOTSPOT gs_knobPosArray[m][1], 0, 0, unID, gs_knobPosArray[m][2], 1+128 : unID = unID + 1
				HOTSPOT gs_knobPosArray[m][1], 0, gs_knobPosArray[m][2], unID, gs_knobPosArray[m][2], 2 : unID = unID + 1
				HOTSPOT gs_knobPosArray[m][1], 0, -1, unID, gs_knobPosArray[m][2], 3 : unID = unID + 1

			else

				HOTSPOT 0, 0, gs_knobPosArray_2[m][2], unID, gs_knobPosArray_2[m][1], 1+128 : unID = unID + 1
				HOTSPOT gs_knobPosArray_2[m][1], 0, gs_knobPosArray_2[m][2], unID, gs_knobPosArray_2[m][1], 2 : unID = unID + 1
				HOTSPOT -1, 0, gs_knobPosArray_2[m][2], unID, gs_knobPosArray_2[m][1], 3 : unID = unID + 1

				HOTSPOT gs_knobPosArray_2[m][1], 0, 0, unID, gs_knobPosArray_2[m][2], 1+128 : unID = unID + 1
				HOTSPOT gs_knobPosArray_2[m][1], 0, gs_knobPosArray_2[m][2], unID, gs_knobPosArray_2[m][2], 2 : unID = unID + 1
				HOTSPOT gs_knobPosArray_2[m][1], 0, -1, unID, gs_knobPosArray_2[m][2], 3 : unID = unID + 1

			endif
			del 1
		endif

		! --- Knob ---

		if bEditableDrawerKnobSize then
			_drawerKnobSize = drawerKnobSize
		else
			_drawerKnobSize = 0.1
		endif

		if bEditableKnobNum & (iKnobNum = 2) then ! --- When there are 2 knobs
			knobXDiff = 0
			knobZDiff = 0
		else
			if iCabIndex = 1 then
				knobXDiff = gs_knobPosArray[m][1]
				knobZDiff = gs_knobPosArray[m][2]
			else
				knobXDiff = gs_knobPosArray_2[m][1]
				knobZDiff = gs_knobPosArray_2[m][2]
			endif
		endif
		ADD  a/2 + gs_knobHorPos + knobXDiff, 0, (gs_drawerHeights[m] - 0.005)/2 + gs_knobVerPos_m * (gs_drawerHeights[m] - 0.005)/4 + knobZDiff

		if gs_knob_type_m = KNOB_STYLE_4 	or gs_knob_type_m = KNOB_STYLE_5 \
											or gs_knob_type_m = KNOB_STYLE_6 \
											or gs_knob_type_m = KNOB_STYLE_7 \
											or gs_knob_type_m = KNOB_STYLE_8 then
			ROTY 90
		endif

		if gs_knob_type_m = KNOB_STYLE_4 	or gs_knob_type_m = KNOB_STYLE_5 \
											or gs_knob_type_m = KNOB_STYLE_6 \
											or gs_knob_type_m = KNOB_STYLE_7 \
											or gs_knob_type_m = KNOB_STYLE_8 then
			addz _drawerKnobSize / 2
		endif

		if gs_knob_type_m = KNOB_STYLE_8 then
			roty 180
			addz _drawerKnobSize
		endif

		if bEditableKnobNum then
			if iKnobNum = 2 then
				if gs_knob_type_m = KNOB_STYLE_8 then
					addz a / 2 - _drawerKnobSize / 2
				endif
				if gs_knob_type_m = KNOB_STYLE_4 	or gs_knob_type_m = KNOB_STYLE_5 \
													or gs_knob_type_m = KNOB_STYLE_6 \
													or gs_knob_type_m = KNOB_STYLE_7 then
					addz - a / 2 + _drawerKnobSize / 2
				endif
				if gs_knob_type_m = KNOB_STYLE_3 or gs_knob_type_m = KNOB_STYLE_2 or gs_knob_type_m = KNOB_STYLE_1 then
					addx - a / 2 + 0.01
				endif
			endif
		endif

		CALL "fa_knobtyp" PARAMETERS	knob_size			= _drawerKnobSize,
										gs_detlevel_3D_m	= gs_detlevel_3D_m,
										gs_knob_type_m		= gs_knob_type_m,
										gs_resol			= iResol,
										gs_shadow			= gs_shadow,
										gs_cont_pen			= gs_cont_pen,
										gs_knob_mat			= gs_knob_mat
		if bEditableKnobNum then
			if iKnobNum = 2 then
				del 1
				if gs_knob_type_m = KNOB_STYLE_8 then
					addz -a / 2 + _drawerKnobSize / 2 + gs_knobHorPos * 2
				endif
				if gs_knob_type_m = KNOB_STYLE_4 	or gs_knob_type_m = KNOB_STYLE_5 \
													or gs_knob_type_m = KNOB_STYLE_6 \
													or gs_knob_type_m = KNOB_STYLE_7 then
					addz a / 2 - gs_knobHorPos * 2 - _drawerKnobSize / 2
				endif
				if gs_knob_type_m < 4 then
					addx a / 2 - gs_knobHorPos * 2 - 0.01
				endif
	
				CALL "fa_knobtyp" PARAMETERS	knob_size			= _drawerKnobSize,
												gs_detlevel_3D_m	= gs_detlevel_3D_m,
												gs_knob_type_m		= gs_knob_type_m,
												gs_resol			= iResol,
												gs_shadow			= gs_shadow,
												gs_cont_pen			= gs_cont_pen,
												gs_knob_mat			= gs_knob_mat
			endif
	
			if iKnobNum = 2 then
				del 1
			endif
		endif

		if gs_knob_type_m = KNOB_STYLE_4 	or gs_knob_type_m = KNOB_STYLE_5 \
											or gs_knob_type_m = KNOB_STYLE_6 \
											or gs_knob_type_m = KNOB_STYLE_7 \
											or gs_knob_type_m = KNOB_STYLE_8 then
			del 2
		endif
		if gs_knob_type_m = KNOB_STYLE_8 then
			del 2
		endif

		del 1
	endif
NEXT m

DEL drawer_num + 1


if NOT(bPartOfLayout) then
	call "Resize_A_B_ZZYZX" parameters all
endif


! ======================================================================
officeCab01_unID = unID
end officeCab01_unID
! ======================================================================



1000:

roty 90
vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1
del 1

coor 2, -1, -2, -3, -4

base

body -1

return



2000:

vert 0, 0, 0
vert 1, 0, 0
vert 0, 1, 0
vert 0, 0, 1

coor 2, -1, -2, -3, -4

base

body -1

return


