
! =============================================================================
! Tabpage Structure Definition
! =============================================================================
TABID_Root 					=-1
_idxTab = 1
TABID_StyleAndDimensions		= _idxTab  : _idxTab = _idxTab + 1
TABID_Base						= _idxTab  : _idxTab = _idxTab + 1
TABID_Knob						= _idxTab  : _idxTab = _idxTab + 1
TABID_2DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_3DRepresentation			= _idxTab  : _idxTab = _idxTab + 1
TABID_MinimalSpace				= _idxTab  : _idxTab = _idxTab + 1
TABID_Descriptions				= _idxTab  : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================
ui_current_page gs_ui_current_page

if bOnlyDrawerUI then
	gosub "drawerPage"
else
	ui_dialog `Параметры Офисной Тумбы`

	ui_page TABID_StyleAndDimensions, TABID_Root, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
		gosub "StyleAndDimensionpage"
	
	ui_page TABID_Base, TABID_StyleAndDimensions, `Основание`, "uiTab_Leg_flat(1)"
		gosub "Basepage"
	
	ui_page TABID_Knob, TABID_StyleAndDimensions, `Ручка`, "uiTab_Knob_flat(1)"
		gosub "Knobpage"
	
	ui_page TABID_2DRepresentation, TABID_Root, `2D-отображение`, "uiTab_2DRepr_flat(1)"
		gosub "2DRepresentationpage"
	
	ui_page TABID_3DRepresentation, TABID_Root, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
		gosub "3DRepresentationpage"
	
	ui_page TABID_MinimalSpace, TABID_Root, `Минимальное Пространство`, "uiTab_MinSpace_flat(1)"
		gosub "minimalSpacePage"
	
	ui_page TABID_Descriptions, TABID_Root, `Характеристики`, "uiTab_Descriptions_flat(1)"
		gosub "Descriptionspage"
endif

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Leg_flat(1)"
file_dependence "uiTab_Knob_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_MinSpace_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ==============================================================================
end
! ==============================================================================


! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"StyleAndDimensionpage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	_outfHeight = 15
	_yOffset = 4
	xPict = 230
	yPict = 48
	PictWidth = 109 
	PictHeight = 135
	x1 = 0
	x2 = 80
	x3 = 124
	x4 = 160
	x5 = xPict - 3
	x6 = xPict + PictWidth + 3

	dy = 24
	dy2 = 40

	_yCurr = pageStartY + _yOffset
	ui_style 0, 1
	ui_outfield `Стиль`, x1, _yCurr, 444, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + 16
	ui_infield{3} "gs_drawerScheme_m", x2, _yCurr, 135, 96,
		2, 9, 8, 2,
		90, 90, 120, 90,
		1, stDrawerSchemes[1], 1,
		2, stDrawerSchemes[2], 2,
		3, stDrawerSchemes[3], 3,
		4, stDrawerSchemes[4], 4,
		5, stDrawerSchemes[5], 5

	_yCurr = pageStartY + dy * 5
	ui_infield{3} "drawer_num", x4, _yCurr, 55, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", `1`, 1,
				"", `2`, 2,
				"", `3`, 3,
				"", `4`, 4,
				"", `5`, 5,
				"", `6`, 6 ui_tooltip `Количество Ящиков`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Количество Ящиков`, x1, _yCurr, 158, _outfHeight
	
	_yCurr = _yCurr + dy
	ui_outfield `Толщина Спереди`, x1, _yCurr, 120, _outfHeight

	_yCurr = _yCurr - _yOffset
	ui_infield "drawerThk", x3, _yCurr, 90, _infHeight

	_yCurr = _yCurr + dy

	ui_infield{3} "gs_bevel", x1, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", `Фаска Торца`, 0,
					"", `Фаска Торца`, 1

	if gs_drawerScheme_m = 1 or gs_drawerScheme_m = 5 then
		_actPict = drawer_num + 9
		ui_pict _actPict, xPict, yPict, PictWidth, PictHeight, 1
	else
		if gs_drawerScheme_m = 2 then
			ui_pict 16, xPict, yPict, PictWidth, PictHeight, 1
		endif
		if gs_drawerScheme_m = 3 then
			ui_pict 17, xPict, yPict, PictWidth, PictHeight, 1
		endif
		if gs_drawerScheme_m = 4 then
			ui_pict 18, xPict, yPict, PictWidth, PictHeight, 1
		endif
	endif

dim stDrawerNum[6]
	stDrawerNum[1] = "gs_drawerH_1"
	stDrawerNum[2] = "gs_drawerH_2"
	stDrawerNum[3] = "gs_drawerH_3"
	stDrawerNum[4] = "gs_drawerH_4"
	stDrawerNum[5] = "gs_drawerH_5"
	stDrawerNum[6] = "gs_drawerH_6"

	_yCurr = yPict - dy
	ui_outfield `Высота Ящиков`, x5, _yCurr, 210, _outfHeight

	_yCurr = yPict
	if gs_drawerScheme_m = 1 or gs_drawerScheme_m = 5 then
		for k = 1 to drawer_num
			ui_infield{4} stDrawerNum[k], x6, _yCurr + (PictHeight / drawer_num)/2 - _infHeight / 2, 90, _infHeight
			dy = PictHeight / drawer_num
			_yCurr = _yCurr + dy
		next k
	else
		if gs_drawerScheme_m = 2 or gs_drawerScheme_m = 4 then
			ui_infield "gs_topDrawerH", x6, _yCurr + 3, 90, _infHeight
		endif
		if gs_drawerScheme_m = 3 or gs_drawerScheme_m = 4 then
			_yCurr = yPict + PictHeight - _infHeight - 3
			ui_infield "gs_bottomDrawerH", x6, _yCurr, 90, _infHeight
		endif
	endif
return

! ------------------------------------------------------------------------------
"Basepage":
! ------------------------------------------------------------------------------
	call "ui_cabinetBase" parameters all	iBaseClass = 3,	! BASECLASS_BASE
											iItemClass = 2	! ITEMCLASS_UNDERDESK_DRAWER
return

! ------------------------------------------------------------------------------
"Knobpage":
! ------------------------------------------------------------------------------
	call "ui_cabinetKnob" parameters all	iKnobClass = 3,	! KNOBCLASS_DRAWER
											iItemClass = 4	! ITEMCLASS_UNDERDESK_DRAWER
return

! ------------------------------------------------------------------------------
"2DRepresentationpage":
! ------------------------------------------------------------------------------
	dim stSymbolTypes[6]
		stSymbolTypes[1] = `Стиль 1`
		stSymbolTypes[2] = `Стиль 2`
		stSymbolTypes[3] = `Стиль 3`
		stSymbolTypes[4] = ``
		stSymbolTypes[5] = ``
		stSymbolTypes[6] = `Стиль 4`
	
	call "ui_object_2DRepresentation" parameters	iSymbolClass		= 13,
													stSymbolTypes 		= stSymbolTypes,
													iSymbolStyle 		= iSymbolStyle,
													bOpeningAngle		= 0,
													bShowDrawer2D 		= 1,
													drawer2d			= drawer2d,
													iFillListID			= TABID_2DRepresentation
return

! ------------------------------------------------------------------------------
"3DRepresentationpage":
! ------------------------------------------------------------------------------
	dim surfacesArray[] 
 	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "gs_cabinet_mat"
		SurfItemID = SurfItemID + 1
		surfacesArray[SurfItemID]	= "count_mat"
		SurfItemID = SurfItemID + 1
		surfacesArray[SurfItemID]	= "gs_door_mat"
		SurfItemID = SurfItemID + 1
		if bKnob then
			surfacesArray[SurfItemID]	= "gs_knob_mat"
			SurfItemID = SurfItemID + 1
		endif
		if bBase then
			surfacesArray[SurfItemID]	= "gs_base_mat"
			SurfItemID = SurfItemID + 1
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3D_m,
													gs_resol			= gs_resol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3DRepresentation
return

! ------------------------------------------------------------------------------
"minimalSpacePage":
! ------------------------------------------------------------------------------
	pageStartY = 0
	_infHeight = 19
	xPict = 142
	yPict = 61
	PictWidth = 141 
	PictHeight = 144
	x1 = xPict + 29
	x2 = xPict + PictWidth + 3

	ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1
	
	_yCurr = yPict - _infHeight - 3
	ui_infield "A", x1, _yCurr, 90, _infHeight ui_tooltip `Размер 1`

	_yCurr = yPict + 23
	ui_infield "B", x2, _yCurr, 90, _infHeight ui_tooltip `Размер 2`

	_yCurr = _yCurr + 65
	ui_infield "MSFront", x2, _yCurr, 90, _infHeight ui_tooltip `Минимальное Пространство Спереди`
return

! ------------------------------------------------------------------------------
"Descriptionspage":
! ------------------------------------------------------------------------------
	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	dim stOnormNames[]
		stOnormNames[1] = "gs_onorm_dimensions"
		stOnormNames[2] = "gs_onorm_manufacturer"
		stOnormNames[3] = "gs_onorm_baseheight_value"
		stOnormNames[4] = "gs_onorm_baseheight"
		stOnormNames[5] = "gs_onorm_assemblydate"
		stOnormNames[6] = "gs_onorm_customtext1"
		stOnormNames[7] = "gs_onorm_customtext2"
		stOnormNames[8] = "gs_onorm_aks"
		stOnormNames[9] = "gs_onorm_product"
		stOnormNames[10] = "gs_onorm_leistungbuch"
		stOnormNames[11] = "gs_onorm_description"
		stOnormNames[12] = "gs_onorm_number"
		stOnormNames[13] = "gs_onorm_id"
		stOnormNames[14] = "gs_onorm_admission"
		stOnormNames[15] = "gs_onorm_modification"
		stOnormNames[16] = "gs_onorm_tozonenumber"

	if LibraryLangCode = "AUT" or LibraryLangCode = "CHE" or LibraryLangCode = "GER" then
		_bShowOnormItems = 1
	else
		_bShowOnormItems = 0
	endif

	call "ui_object_listing" parameters all listfieldID 	= TABID_Descriptions,
										listFieldPosY 		= 0,
										listFieldHeight 	= 265,
										stDescHeader 		= "",
										stValueHeader 		= "",
										bShowOnormItems 	= _bShowOnormItems,
										stParamNames 		= stParamNames,
										stOnormNames 		= stOnormNames
return


! ------------------------------------------------------------------------------
"drawerPage":
! ------------------------------------------------------------------------------
	textElevInfield		= 4
	textElevBoolean		= 2
	textElevMaterial	= 4

	infieldHeight = 19
	outfieldHeight = 15

	pageStartY = 5

	xCenter	= 222
	yCenter	= 120

	xLeftStr1	= 230 - 220
	Strlength1	= 110
	xLeftStr2	= 345 - 220
	Strlength2	= 90
	yLeftStr1	= pageStartY
	yCurrent	= yLeftStr1

	ui_outfield `Количество Ящиков`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
	yCurrent = yCurrent - textElevInfield
	if iCabIndex = 1 then
		ui_infield "drawer_num", xLeftStr2, yCurrent, Strlength2, infieldHeight
	else
		ui_infield "drawer_num_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
	endif
	yCurrent = yCurrent + 25

	ui_outfield `Схема`, xLeftStr1, yCurrent, Strlength1, 16
	if iCabIndex = 1 then
		ui_infield{3} "gs_drawerScheme_m", xLeftStr2, yCurrent, Strlength2, 110,
			2, 2, 5, 1,		!ui_cab01_scheme.png
			80, 90, 80, 73,
			1, `Схема 1`, 1,
			2, `Схема 2`, 2,
			3, `Схема 3`, 3,
			4, `Схема 4`, 4,
			5, `Спец.`, 5
	else
		ui_infield{3} "gs_drawerScheme_m_2", xLeftStr2, yCurrent, Strlength2, 110,
			2, 2, 5, 1,		!ui_cab01_scheme.png
			80, 90, 80, 73,
			1, `Схема 1`, 1,
			2, `Схема 2`, 2,
			3, `Схема 3`, 3,
			4, `Схема 4`, 4,
			5, `Спец.`, 5
	endif

	yCurrent = pageStartY + 140
	ui_infield{3} "gs_bevel", xLeftStr1, yCurrent, Strlength1, 20,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Фаска Торца`, 0,
		1, `Фаска Торца`, 1
	yCurrent = yCurrent + 25

	ui_outfield `Толщина Спереди`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
	yCurrent = yCurrent - textElevInfield
	if iCabIndex = 1 then
		ui_infield "drawerThk", xLeftStr2, yCurrent, Strlength2, infieldHeight
	else
		ui_infield "drawerThk2", xLeftStr2, yCurrent, Strlength2, infieldHeight
	endif
	yCurrent = yCurrent + 25

	if bOnlyDrawerUI then
		ui_outfield `Ширина Корпуса`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
		yCurrent = yCurrent - textElevInfield
		if iCabIndex = 1 then
			ui_infield "gs_officeCab_width", xLeftStr2, yCurrent, Strlength2, infieldHeight
		else
			ui_infield "gs_officeCab_width_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
		endif
	endif

	ui_separator xCenter, pageStartY - 3, xCenter, 265

	xLeftStr1	= 230
	Strlength1	= 110
	xLeftStr2	= 345
	Strlength2	= 90
	yLeftStr1	= pageStartY
	yCurrent	= yLeftStr1

	ui_outfield `Высота Ящиков`, xLeftStr1, yCurrent, Strlength1, 16
	yCurrent = yCurrent + 25 - textElevInfield

	if (gs_drawerScheme_m = 2 and iCabIndex = 1) or (gs_drawerScheme_m_2 = 2 and iCabIndex = 2) then
		ui_outfield `Верхний Ящик`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
		yCurrent = yCurrent - textElevInfield
		if iCabIndex = 1 then
			ui_infield "gs_topDrawerH", xLeftStr2, yCurrent, Strlength2, infieldHeight
		else
			ui_infield "gs_topDrawerH_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
		endif
	endif

	if (gs_drawerScheme_m = 3 and iCabIndex = 1) or (gs_drawerScheme_m_2 = 3 and iCabIndex = 2) then
		ui_outfield `Нижний Ящик`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
		yCurrent = yCurrent - textElevInfield
		if iCabIndex = 1 then
			ui_infield "gs_bottomDrawerH", xLeftStr2, yCurrent, Strlength2, infieldHeight
		else
			ui_infield "gs_bottomDrawerH_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
		endif
	endif

	if (gs_drawerScheme_m = 4 and iCabIndex = 1) or (gs_drawerScheme_m_2 = 4 and iCabIndex = 2) then
		ui_outfield `Верхний Ящик`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
		yCurrent = yCurrent - textElevInfield
		if iCabIndex = 1 then
			ui_infield "gs_topDrawerH", xLeftStr2, yCurrent, Strlength2, infieldHeight
		else
			ui_infield "gs_topDrawerH_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
		endif
		yCurrent = yCurrent + 25

		ui_outfield `Нижний Ящик`, xLeftStr1, yCurrent, Strlength1, outfieldHeight
		yCurrent = yCurrent - textElevInfield
		if iCabIndex = 1 then
			ui_infield "gs_bottomDrawerH", xLeftStr2, yCurrent, Strlength2, infieldHeight
		else
			ui_infield "gs_bottomDrawerH_2", xLeftStr2, yCurrent, Strlength2, infieldHeight
		endif
	endif

	if (gs_drawerScheme_m = 5 and iCabIndex = 1) or (gs_drawerScheme_m_2 = 5 and iCabIndex = 2) then
		if (drawer_num = 1 and iCabIndex = 1) or (drawer_num_2 = 1 and iCabIndex = 2) then
			ui_pict 3, xLeftStr1, yCurrent, 109, 135, 1			!drawer1
			y0 = 58
		endif
		if (drawer_num = 2 and iCabIndex = 1) or (drawer_num_2 = 2 and iCabIndex = 2) then
			ui_pict 4, xLeftStr1, yCurrent, 109, 135, 1			!drawer2
			y0 = 30
			dy = 58
		endif
		if (drawer_num = 3 and iCabIndex = 1) or (drawer_num_2 = 3 and iCabIndex = 2) then
			ui_pict 5, xLeftStr1, yCurrent, 109, 135, 1			!drawer3
			y0 = 15
			dy = 45
		endif
		if (drawer_num = 4 and iCabIndex = 1) or (drawer_num_2 = 4 and iCabIndex = 2) then
			ui_pict 6, xLeftStr1, yCurrent, 109, 135, 1			!drawer4
			y0 = 10
			dy = 34
		endif
		if (drawer_num = 5 and iCabIndex = 1) or (drawer_num_2 = 5 and iCabIndex = 2) then
			ui_pict 7, xLeftStr1, yCurrent, 109, 135, 1			!drawer5
			y0 = 5
			dy = 27
		endif
		if (drawer_num = 6 and iCabIndex = 1) or (drawer_num_2 = 6 and iCabIndex = 2) then
			ui_pict 8, xLeftStr1, yCurrent, 109, 135, 1			!drawer6
			y0 = 3
			dy = 22
		endif

		yCurrent = yCurrent + y0

		if iCabIndex = 1 then
			ui_infield "gs_drawerH_1", xLeftStr2, yCurrent, Strlength2, 20

			if drawer_num > 1 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_2", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num > 2 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_3", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num > 3 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_4", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num > 4 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_5", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num > 5 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_6", xLeftStr2, yCurrent, Strlength2, 20
			endif
		else
			ui_infield "gs_drawerH_1_2", xLeftStr2, yCurrent, Strlength2, 20

			if drawer_num_2 > 1 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_2_2", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num_2 > 2 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_3_2", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num_2 > 3 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_4_2", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num_2 > 4 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_5_2", xLeftStr2, yCurrent, Strlength2, 20
			endif
			if drawer_num_2 > 5 then
				yCurrent = yCurrent + dy
				ui_infield "gs_drawerH_6_2", xLeftStr2, yCurrent, Strlength2, 20
			endif
		endif
	endif
return


