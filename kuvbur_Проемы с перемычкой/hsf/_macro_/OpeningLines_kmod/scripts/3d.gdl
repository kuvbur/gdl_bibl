
!opening lines macro
! ==============================================================================
!
!  a: width
!
!  b: height
!
!  frameThickness: sash frame thickness
!
!  iOpeningType: opening type -
!		SASH_FIXED					= 1		! Fixed Glass
!		SASH_FIXED_SCREW			= 2		! Fixed Sash
!		SASH_SIDE_HUNG				= 3		! Side Hung
!		SASH_SIDE_HUNG_M			= 4		! Side Hung Mirrored
!		SASH_TILT_TURN				= 5		! Tilt and Turn
!		SASH_TILT_TURN_M			= 6		! Tilt and Turn Mirrored
!		SASH_BOTTOM_HUNG			= 7		! Bottom Hung
!		SASH_TOP_HUNG				= 8		! Top Hung
!		SASH_HOR_PIVOT				= 9		! Horizontal Pivot Hung
!		SASH_VER_PIVOT				= 10	! Vertical Pivotted
!		SASH_TILT_SLIDE				= 11	! Tilt and Slide
!		SASH_LEFT_SLIDING			= 12	! Left Sliding
!		SASH_RIGHT_SLIDING			= 13	! Right Sliding
!		SASH_UP_SLIDING				= 14	! Sliding Up
!		SASH_DOWN_SLIDING			= 15	! Sliding Down
!		SASH_VER_PIVOT_M			= 16	! Vertical Pivotted Mirrored
!		SASH_FIXED_SLIDE			= 17	! Fixed Sliding Sash
!		SASH_DOUBLE_SIDE_HUNG		= 18	! Double Sash
!		SASH_DOUBLE_TILT_TURN		= 19	! Double Tilt and Turn
!		SASH_DOUBLE_TILT_TURN_M		= 20	! Double Tilt and Turn Mirrored
!		SASH_SLIDING_HOR			= 21	! Sliding
!		SASH_SLIDING_HOR_M			= 22	! Sliding Mirrored
!		SASH_DOUBLE_SLIDING_HOR		= 23	! Double Sliding
!		SASH_DOUBLE_SLIDING_HOR_M	= 24	! Double Sliding Mirrored
!		SASH_SLIDING_VER			= 25	! Single Hung
!		SASH_DOUBLE_SLIDING_VER		= 26	! Double Hung
!
!  oplinetype - 0-solid line, 1-dashed line, 2-dotted line, 3-dash & dot line, 4-no line
!
!  sideHeightMod: Side height modification for trapezoid windows (0 = Default, Half Height)
!  topWidthMod:   Top width modification for trapezoid windows (0 = Default)
!
! ==============================================================================


if A < EPS | B < EPS then end
if (opLineType3dIn = NO_LINE & opLineType3dOut = NO_LINE) then end		! Opening line not needed on fix windows.

!if iOpeningType = SASH_FIXED | iOpeningType = SASH_FIXED_SCREW | (opLineType3dIn = NO_LINE & opLineType3dOut = NO_LINE) then end		! Opening line not needed on fix windows.

if not(bSkyLight) and GLOB_CONTEXT = 2 then end		! no opening lines in projected 2d view

pen gs_opLinePen

v1 = 0.08     ! 1 dash length
v2 = 0.05     ! 1 space length

bPivotWindow = ((iOpeningType = SASH_HOR_PIVOT) | (iOpeningType = SASH_VER_PIVOT) | (iOpeningType = SASH_VER_PIVOT_M))
bSlidingWindow = ((iOpeningType = SASH_LEFT_SLIDING) | (iOpeningType = SASH_RIGHT_SLIDING) | (iOpeningType = SASH_TILT_SLIDE) | \
					(iOpeningType = SASH_UP_SLIDING) | (iOpeningType = SASH_DOWN_SLIDING))
bMirrored = (iOpeningType = SASH_SIDE_HUNG_M | iOpeningType = SASH_TILT_TURN_M | iOpeningType = SASH_LEFT_SLIDING | iOpeningType = SASH_TILT_SLIDE)

aa = a
bb = b
iFirstLineType = 0
iSecondLineType = 0
delnum = 0

if iOpeningType = SASH_FIXED or iOpeningType = SASH_FIXED_SCREW then
	rcr = v1/4
	addx 3*rcr
	addy 3*rcr
	CIRCLE rcr
	addz -frameThickness
	CIRCLE rcr
	del 3
!	lin_ a/2-v2, b/2, 0, a/2+v2, b/2, 0
!	lin_ a/2, b/2-v2, 0, a/2, b/2+v2, 0
!	lin_ a/2-v2, b/2, -frameThickness, a/2+v2, b/2, -frameThickness
!	lin_ a/2, b/2-v2, -frameThickness, a/2, b/2+v2, -frameThickness
endif

if iOpeningType <> SASH_VER_PIVOT and iOpeningType <> SASH_VER_PIVOT_M and iOpeningType <> SASH_HOR_PIVOT then
	if bMirrored exor (gs_opline_style_m = 2) then
		addx aa
		mulx -1
		delnum = delnum + 2
	endif
	if (gs_opline_style_m = 2) then
		addy bb
		muly -1
		delnum = delnum + 2
	endif
endif

if iOpeningType = SASH_SIDE_HUNG | iOpeningType = SASH_SIDE_HUNG_M | iOpeningType = SASH_TILT_TURN | iOpeningType = SASH_TILT_TURN_M then
	iFirstLineType = opLineType3dIn
	iSecondLineType = opLineType3dOut
	gosub "drawLines"
endif

if iOpeningType = SASH_TOP_HUNG then
	addx aa
	rotz 90
	aa = b
	bb = a
	iFirstLineType = opLineType3dIn
	iSecondLineType = opLineType3dOut
	gosub "drawLines"
	del 2
endif

if iOpeningType = SASH_BOTTOM_HUNG | iOpeningType = SASH_TILT_TURN | iOpeningType = SASH_TILT_TURN_M | iOpeningType = SASH_TILT_SLIDE then
	addy bb
	rotz -90
	aa = b
	bb = a
	iFirstLineType = opLineType3dIn
	iSecondLineType = opLineType3dOut
	gosub "drawLines"
	del 2
endif

if bSlidingWindow then
	aa = a
	bb = b
	if iOpeningType = SASH_UP_SLIDING | iOpeningType = SASH_DOWN_SLIDING then
		addx a/2
		addy b/2
		rotz 90 * (iOpeningType = SASH_UP_SLIDING) - 90 * (iOpeningType = SASH_DOWN_SLIDING)
		addx -b/2
		addy -a/2
		aa = b
		bb = a
	endif
	gosub "drawArrow"
	if iOpeningType = SASH_UP_SLIDING | iOpeningType = SASH_DOWN_SLIDING then del 5
endif

if iOpeningType = SASH_HOR_PIVOT then
	aa = b / 2
	bb = a
	addy b
	rotz -90
	iFirstLineType = opLineType3dIn
	iSecondLineType = opLineType3dOut
	gosub "drawLines"
	mulx -1
	addx -2*aa
	iFirstLineType = opLineType3dOut
	iSecondLineType = opLineType3dIn
	gosub "drawLines"
	del 4
endif

if iOpeningType = SASH_VER_PIVOT then
	aa = a/2
	bb = b
	iFirstLineType = opLineType3dOut
	iSecondLineType = opLineType3dIn
	gosub "drawLines"
	mulx -1
	addx -2*aa
	iFirstLineType = opLineType3dIn
	iSecondLineType = opLineType3dOut
	gosub "drawLines"
	del 2
endif

if iOpeningType = SASH_VER_PIVOT_M then
	aa = a/2
	bb = b
	iFirstLineType = opLineType3dIn
	iSecondLineType = opLineType3dOut
	gosub "drawLines"
	mulx -1
	addx -2*aa
	iFirstLineType = opLineType3dOut
	iSecondLineType = opLineType3dIn
	gosub "drawLines"
	del 2
endif



del delnum

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end !
! ==============================================================================


! ==============================================================================
! Opening lines triangle
! ==============================================================================
"drawLines":

	for ijk = 1 to 2
		if ijk = 1 then
			iLineType = iFirstLineType
		else
			iLineType = iSecondLineType
		endif

		x1 = aa - topWidthMod : y1 = 0 : x2 = 0 : y2 = bb/2 + sideHeightMod

		gosub "DrawSingleLine"

		x1 = aa : y1 = bb : x2 = 0 : y2 = bb/2 + sideHeightMod

		gosub "DrawSingleLine"

		addz frameThickness
	next ijk
	del 2
return


! ==============================================================================
! Arrow for sliding door
! ==============================================================================
"drawArrow":

	arrowHead = 0.07
	arrowLength = max (2*arrowHead, min(aa/2, 0.4))
	add aa/2, bb/2, 0

	for ijk = 1 to 2
		if ijk = 1 then
			iLineType = opLineType3dIn
		else
			iLineType = opLineType3dOut
		endif

		x1 = -arrowLength/2 : y1 = 0 : x2 = arrowLength/2 : y2 = 0

		gosub "DrawSingleLine"

		if not (bArrowWithoutEnd) then
			x1 = -arrowLength/2 : y1 = 0 : x2 = -arrowLength/2 : y2 = -arrowHead

			gosub "DrawSingleLine"
		endif

		addx arrowLength/2
		rotz 60

		x1 = 0 : y1 = 0 : x2 = 0 : y2 = arrowHead
		gosub "DrawSingleLine"

		del 1
		rotz 120

		x1 = 0 : y1 = 0 : x2 = 0 : y2 = arrowHead
		gosub "DrawSingleLine"

		del 2
		addz frameThickness
	next ijk

	del 3
return

! ==============================================================================
"DrawSingleLine":
! ==============================================================================
! x1, y1, x2, y2 - end point coordinates
! iLineType - line type
! ==============================================================================

	if iLineType = NO_LINE then return

	if iLineType = SOLID_LINE then
		lin_ x1, y1, 0, x2, y2, 0
	else
		if iLineType = DASHED_LINE then
			v1 = 0.08     ! 1 dash length
			s1 = 0.05     ! 1 space length
			sectLength = v1 + s1
		else
			if iLineType = DOTTED_LINE then
				v1 = 0.002    ! 1 dash length
				s1 = 0.03     ! 1 space length
				sectLength = v1 + s1
			else	!dash and dot
				v1 = 0.08
				v2 = 0.002
				s1 = 0.03
				sectLength = v1 + v2 + s1 * 2
			endif
		endif

		lineLength = sqr((x2 - x1)^2 + (y2 - y1)^2)
		nSegment = int((lineLength - v1) / sectLength)

		if abs(x1 - x2) < EPS then
			if y1 < y2 then
				lineAngle = 270
			else
				lineAngle = 90
			endif
		else
			if abs(y1 - y2) < EPS then
				if x1 < x2 then
					lineAngle = 180
				else
					lineAngle = 0
				endif
			else
				lineAngle = atn((y2-y1) / (x2-x1))
			endif
		endif

		if lineLength < v1 * 2 then
			lin_ x1, y1, 0, x2, y2, 0
		else
			add x1, y1, 0
			rotz 180+lineAngle

			actX = 0
			for i = 1 to nSegment
				lin_ actX, 0, 0, actX + v1, 0, 0
				if iLineType = DASHDOT_LINE then
					lin_ actX + v1 + s1, 0, 0, actX + v1 + s1 + v2, 0, 0
				endif
				actX = actX + sectLength
			next i

			restLength = lineLength - sectLength * nSegment - v1

			lin_ actX, 0, 0, actX + min(v1, restLength), 0, 0

			if iLineType = DASHDOT_LINE and v1 + s1 < restLength then
				lin_ actX + v1 + s1, 0, 0, actX + min(v1 + s1 + v2, restLength), 0, 0
			endif

			lin_ lineLength - v1, 0, 0, lineLength, 0, 0

			del 2
		endif

	endif
return

