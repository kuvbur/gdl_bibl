
! Contact person: <NJ>

! ==============================================================================
! This macro draws the Threshold
! ------------------------------------------------------------------------------
! Input parameters:
! Functions --------------------------------------------------------------------
!	gs_IsCurved:			Does the door follow the curvature of the wall? (0 / 1)
!	iWindowShape:			Shape of the door (integer)
!								0 = rectangular (default)
!								1 = arched
! Door Frame -------------------------------------------------------------------
!	gs_frame_width:			Width of the door frame (length)
! Casing -----------------------------------------------------------------------
!	gs_trim_width_in:		Width of inside Trim. (length)
!	gs_trim_width_out:		Width of outside Trim. (length)
! Threshold --------------------------------------------------------------------
!	iThresholdType:		Identifies the type of the Threshold (integer)
! Ganging ----------------------------------------------------------------------
!	gs_stack_left:			Align to door/window on left side (0 / 1)
!	gs_stack_right:			Align to door/window on right side (0 / 1)
!	--- String Tables ---
!	stThresholdTypes:	String Table (text; array[])
!
! Related Global Variables:
!	GLOB_SCRIPT_TYPE
!	GLOB_CONTEXT
!	WALL_THICKNESS
!	WALL_INCL
!	WIDO_FRAME_THICKNESS
!	WIDO_SILL
!	WIDO_LEFT_JAMB
!	WIDO_RIGHT_JAMB
!	WIDO_ORIG_DIST
!	WIDO_REVEAL_SIDE
!	SYMB_MIRRORED
!	SYMB_ROTANGLE
! ==============================================================================


! =============================================================================
!	Door Threshold
! =============================================================================

dim TresholdTypeValueMask[]
dim iTresholdTypeValueMask[]

i=1
if bittest( gs_TresholdTypeEnable, 0) then 				! Normal treshold(1)
	TresholdTypeValueMask[i] = stThresholdTypes[2]
	iTresholdTypeValueMask[i] = 2
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 1) then 				! Extended treshold(2)
	TresholdTypeValueMask[i] = stThresholdTypes[3]
	iTresholdTypeValueMask[i] = 3
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 2) then 				! Extended both sides treshold(4)
	TresholdTypeValueMask[i] = stThresholdTypes[4]
	iTresholdTypeValueMask[i] = 4
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 3) then 				! Threshold beneath doorpanel(8)
	TresholdTypeValueMask[i] = stThresholdTypes[5]
	iTresholdTypeValueMask[i] = 5
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 4) then 				! Profiled treshold(16)
	TresholdTypeValueMask[i] = stThresholdTypes[6]
	iTresholdTypeValueMask[i] = 6
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 5) then 				! NOR Flat Terskel(32)
	TresholdTypeValueMask[i] = stThresholdTypes[7]
	iTresholdTypeValueMask[i] = 7
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 6) then 				! NOR Anslagterskel(64)
	TresholdTypeValueMask[i] = stThresholdTypes[8]
	iTresholdTypeValueMask[i] = 8
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 7) then 				! NOR Anslagterskel(128)
	TresholdTypeValueMask[i] = stThresholdTypes[9]
	iTresholdTypeValueMask[i] = 9
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 8) then 				! NOR Anslagterskel(256)
	TresholdTypeValueMask[i] = stThresholdTypes[10]
	iTresholdTypeValueMask[i] = 10
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 9) then 				! NOR Anslagterskel(512)
	TresholdTypeValueMask[i] = stThresholdTypes[11]
	iTresholdTypeValueMask[i] = 11
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 10) then 				! NOR Anslagterskel(1024)
	TresholdTypeValueMask[i] = stThresholdTypes[12]
	iTresholdTypeValueMask[i] = 12
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 11) then 				! NOR Anslagterskel(2048)
	TresholdTypeValueMask[i] = stThresholdTypes[13]
	iTresholdTypeValueMask[i] = 13
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 12) then 				! NOR Anslagterskel(4096)
	TresholdTypeValueMask[i] = stThresholdTypes[14]
	iTresholdTypeValueMask[i] = 14
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 13) then 				! NED Simple Threshold(8192)
	TresholdTypeValueMask[i] = stThresholdTypes[15]
	iTresholdTypeValueMask[i] = 15
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 14) then 				! Sloping Extended (Terrace) (16384)
	TresholdTypeValueMask[i] = stThresholdTypes[16]
	iTresholdTypeValueMask[i] = 16
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 15) then 				! Metal (Terrace) (32768)
	TresholdTypeValueMask[i] = stThresholdTypes[17]
	iTresholdTypeValueMask[i] = 17
	i = i + 1
endif
if bittest( gs_TresholdTypeEnable, 16) then 				! SWE Extended treshold (65536)
	TresholdTypeValueMask[i] = stThresholdTypes[18]
	iTresholdTypeValueMask[i] = 18
	i = i + 1
endif


! =============================================================================
!	Threshold Defaults
! =============================================================================

if bThreshold & (GLOB_MODPAR_NAME = "bThreshold" | GLOB_MODPAR_NAME = "iThresholdType") then
	if iThresholdType = TRESHOLD_SLOPINGEXT then
		gs_treshold_thk		= gs_frame_width
		gs_treshold_angle	= 10
		gs_treshold_width	= 0.095
		gs_treshold_nosing	= 0.01
		gs_treshold_ext_hgt	= 0.0226571
		parameters	gs_treshold_thk		= gs_treshold_thk,
					gs_treshold_angle	= gs_treshold_angle,
					gs_treshold_width	= gs_treshold_width,
					gs_treshold_nosing	= gs_treshold_nosing,
					gs_treshold_ext_hgt	= gs_treshold_ext_hgt
	endif
	if iThresholdType = TRESHOLD_METAL then
		gs_treshold_thk		= 0.025
		gs_treshold_width	= 0.105
		gs_treshold_ext_hgt	= 0.01
		parameters	gs_treshold_thk		= gs_treshold_thk,
					gs_treshold_width	= gs_treshold_width,
					gs_treshold_ext_hgt	= gs_treshold_ext_hgt
	endif
	if bittest( gs_TresholdTypeEnable, 14) | bittest( gs_TresholdTypeEnable, 15) then
		if iThresholdType <> TRESHOLD_SLOPINGEXT & iThresholdType <> TRESHOLD_METAL then
			gs_treshold_thk		= 0.025
			gs_treshold_nosing	= 0.025
			parameters	gs_treshold_thk		= gs_treshold_thk,
						gs_treshold_nosing	= gs_treshold_nosing
		endif
	endif
endif


if iThresholdType = TRESHOLD_SLOPINGEXT | iThresholdType = TRESHOLD_METAL then
	if GLOB_MODPAR_NAME = "gs_treshold_angle" then
		gs_treshold_ext_hgt = gs_treshold_thk - gs_rebate_width - (tan(gs_treshold_angle) * (gs_treshold_width - gs_frame_thkin + gs_rebate_depth))
		parameters gs_treshold_ext_hgt	= gs_treshold_ext_hgt
	else
		if ABS(gs_treshold_width - gs_frame_thkin + gs_rebate_depth) > EPS then
			gs_treshold_angle = atn((gs_treshold_thk - gs_rebate_width - gs_treshold_ext_hgt) / (gs_treshold_width - gs_frame_thkin + gs_rebate_depth))
		else
			gs_treshold_angle = 0
		endif
		parameters gs_treshold_angle = gs_treshold_angle
	endif

	if ABS(gs_treshold_width - gs_frame_thkin + gs_rebate_depth) > EPS then
		values "gs_treshold_angle" range [0, atn((gs_treshold_thk - gs_rebate_width) / (gs_treshold_width - gs_frame_thkin + gs_rebate_depth))]
	else
		gs_treshold_angle = 0
		parameters gs_treshold_angle = gs_treshold_angle
	endif

	values "gs_treshold_ext_hgt" range [0, gs_treshold_thk - gs_rebate_width]
endif


values{2} "iThresholdType" iTresholdTypeValueMask, TresholdTypeValueMask

if iThresholdType = TRESHOLD_ANSLAG | iThresholdType = TRESHOLD_BRANN | \
 iThresholdType = TRESHOLD_BADEROMS | iThresholdType = TRESHOLD_AKKUSTIKK then
	values "gs_treshold_thk"		range (0,gs_treshold_width-gs_leaf_thk]
else
	values "gs_treshold_thk"		range (0,]
endif

if	iThresholdType = TRESHOLD_NORMAL		| \
	iThresholdType = TRESHOLD_EXTENDED		| \
	iThresholdType = TRESHOLD_BOTHEXTENDED	| \
	iThresholdType = TRESHOLD_BENEATHPANEL	| \
	iThresholdType = TRESHOLD_PROFILED		| \
	iThresholdType = TRESHOLD_EXTENDED_SWE	then

		values "gs_treshold_width"		range (0, gs_fthk]
else														! NOR Threshold types
		values "gs_treshold_width"		range (0,]
endif

values "gs_treshold_nosing"		range (0,]
values "gs_treshold_nosing_2"	range (0,]

for i=1 to 255
	put i
next i
values "gs_treshold_pen_sectcont" get(nsp)

if not(bThreshold) then
	hideparameter	"iThresholdType",
					"gs_treshold_thk",
					"gs_treshold_width",
					"gs_treshold_angle",
					"gs_treshold_ext_hgt",
					"gs_treshold_nosing",
					"gs_treshold_nosing_2",
					"gs_treshold_mat",
					"gs_treshold_metal_mat",
					"gs_treshold_pen_sectcont",
					"gs_treshold_fill",
					"gs_treshold_pen_fg",
					"gs_treshold_pen_bg"

	lock	"iThresholdType",
			"gs_treshold_thk",
			"gs_treshold_width",
			"gs_treshold_angle",
			"gs_treshold_ext_hgt",
			"gs_treshold_nosing",
			"gs_treshold_nosing_2",
			"gs_treshold_mat",
			"gs_treshold_metal_mat",
			"gs_treshold_pen_sectcont",
			"gs_treshold_fill",
			"gs_treshold_pen_fg",
			"gs_treshold_pen_bg"

	if not(gs_threshold_nor) then
		hideparameter	"gs_treshold_ovhg",
						"gs_treshold_ovhg_2"

		lock	"gs_treshold_ovhg",
				"gs_treshold_ovhg_2"
	endif
else
	if iThresholdType = TRESHOLD_NORMAL then
		hideparameter "gs_treshold_nosing", "gs_treshold_nosing_2", "gs_treshold_ovhg", "gs_treshold_ovhg_2"
		lock "gs_treshold_nosing", "gs_treshold_nosing_2", "gs_treshold_ovhg", "gs_treshold_ovhg_2"
	endif
	if iThresholdType = TRESHOLD_EXTENDED then
		hideparameter "gs_treshold_nosing", "gs_treshold_ovhg"
		lock "gs_treshold_nosing", "gs_treshold_ovhg"
	endif
	if	iThresholdType = TRESHOLD_FLAT		|\
		iThresholdType = TRESHOLD_ANSLAG		|\
		iThresholdType = TRESHOLD_GUMMI		|\
		iThresholdType = TRESHOLD_HEVE		|\
		iThresholdType = TRESHOLD_HC			|\
		iThresholdType = TRESHOLD_BRANN		|\
		iThresholdType = TRESHOLD_BADEROMS	|\
		iThresholdType = TRESHOLD_AKKUSTIKK	|\
		iThresholdType = TRESHOLD_SIMPLE		then	! NOR Threshold

		hideparameter "gs_treshold_nosing", "gs_treshold_nosing_2", "gs_treshold_ovhg", "gs_treshold_ovhg_2"
		lock "gs_treshold_nosing", "gs_treshold_nosing_2", "gs_treshold_ovhg", "gs_treshold_ovhg_2"
	endif
	if iThresholdType = TRESHOLD_SIMPLE then
		parameters gs_treshold_width = gs_fthk
		lock "gs_treshold_width"
		hideparameter "gs_treshold_width"
	endif
	if iThresholdType <> TRESHOLD_SLOPINGEXT & iThresholdType <> TRESHOLD_METAL then
		lock "gs_treshold_angle", "gs_treshold_ext_hgt", "gs_treshold_metal_mat"
		hideparameter "gs_treshold_angle", "gs_treshold_ext_hgt", "gs_treshold_metal_mat"
	endif
	if iThresholdType = TRESHOLD_EXTENDED_SWE then
		hideparameter "gs_treshold_nosing_2", "gs_treshold_ovhg_2"
		lock "gs_treshold_nosing_2", "gs_treshold_ovhg_2"
	endif
endif


! =============================================================================
! IFC Parameters
! =============================================================================

if bEnableThreshold then
	ifc_ThresholdDepth		= 0
	ifc_ThresholdThickness	= 0
	ifc_ThresholdOffset		= 0

	if bThreshold then
		if iThresholdType = TRESHOLD_NORMAL then
			ifc_ThresholdDepth = gs_fthk - gs_leaf_thk
		endif
		if iThresholdType = TRESHOLD_EXTENDED then
			if WIDO_REVEAL_SIDE then
				ifc_ThresholdDepth = gs_fthk - gs_leaf_thk + gs_wido_sill + gs_treshold_nosing_2
			else
				ifc_ThresholdDepth = WALL_THICKNESS - gs_leaf_thk - gs_wido_sill + gs_treshold_nosing_2
			endif
		endif
		if iThresholdType = TRESHOLD_EXTENDED_SWE then
			if not(WIDO_REVEAL_SIDE) then
				ifc_ThresholdDepth = gs_fthk - gs_leaf_thk + gs_wido_sill + gs_treshold_nosing
			else
				ifc_ThresholdDepth = WALL_THICKNESS - gs_leaf_thk - gs_wido_sill + gs_treshold_nosing
			endif
		endif
		if iThresholdType = TRESHOLD_BOTHEXTENDED then
			ifc_ThresholdDepth = WALL_THICKNESS + gs_treshold_nosing + gs_treshold_nosing_2
		endif
		if	iThresholdType = TRESHOLD_FLAT		|\
			iThresholdType = TRESHOLD_ANSLAG		|\
			iThresholdType = TRESHOLD_GUMMI		|\
			iThresholdType = TRESHOLD_BRANN		|\
			iThresholdType = TRESHOLD_BADEROMS	|\
			iThresholdType = TRESHOLD_AKKUSTIKK	|\
			iThresholdType = TRESHOLD_SLOPINGEXT |\
			iThresholdType = TRESHOLD_METAL		then

			ifc_ThresholdDepth = gs_treshold_width
		endif
		if iThresholdType = TRESHOLD_SIMPLE then
			ifc_ThresholdDepth = gs_fthk
		endif

		if	iThresholdType = TRESHOLD_HEVE	|\
			iThresholdType = TRESHOLD_HC		then

			ifc_ThresholdThickness	= 0
		else
			if	iThresholdType = TRESHOLD_ANSLAG		|\
				iThresholdType = TRESHOLD_BRANN		|\
				iThresholdType = TRESHOLD_BADEROMS	|\
				iThresholdType = TRESHOLD_AKKUSTIKK	then

				ifc_ThresholdThickness	= gs_treshold_thk + nor_threshold_offset
			else
				ifc_ThresholdThickness	= gs_treshold_thk
			endif
		endif

		if iThresholdType = TRESHOLD_NORMAL then
			if WIDO_REVEAL_SIDE then
				ifc_ThresholdOffset	= gs_fthk
			else
				ifc_ThresholdOffset	= gs_leaf_thk
			endif
		endif
		if iThresholdType = TRESHOLD_EXTENDED then
			if WIDO_REVEAL_SIDE then
				ifc_ThresholdOffset	= gs_fthk + gs_wido_sill + gs_treshold_nosing_2
			else
				ifc_ThresholdOffset	= gs_leaf_thk
			endif
		endif
		if iThresholdType = TRESHOLD_EXTENDED_SWE then
			if not(WIDO_REVEAL_SIDE) then
				ifc_ThresholdOffset	= gs_fthk + gs_wido_sill + gs_treshold_nosing
			else
				ifc_ThresholdOffset	= gs_leaf_thk
			endif
		endif
		if iThresholdType = TRESHOLD_BOTHEXTENDED then
			if WIDO_REVEAL_SIDE then
				ifc_ThresholdOffset	= gs_fthk + gs_wido_sill + gs_treshold_nosing_2
			else
				ifc_ThresholdOffset	= -gs_wido_sill -  gs_treshold_nosing
			endif
		endif
		if	iThresholdType = TRESHOLD_FLAT		|\
			iThresholdType = TRESHOLD_ANSLAG		|\
			iThresholdType = TRESHOLD_GUMMI		|\
			iThresholdType = TRESHOLD_BRANN		|\
			iThresholdType = TRESHOLD_BADEROMS	|\
			iThresholdType = TRESHOLD_AKKUSTIKK	then

			if WIDO_REVEAL_SIDE then
				ifc_ThresholdOffset	= gs_treshold_width
			else
				ifc_ThresholdOffset	= 0
			endif
		endif
		if iThresholdType = TRESHOLD_SIMPLE then
			if WIDO_REVEAL_SIDE then
				ifc_ThresholdOffset	= gs_fthk
			else
				ifc_ThresholdOffset	= 0
			endif
		endif
		if	iThresholdType = TRESHOLD_SLOPINGEXT |\
			iThresholdType = TRESHOLD_METAL		then
	
			if WIDO_REVEAL_SIDE then
				ifc_ThresholdOffset	= gs_fthk
			else
				ifc_ThresholdOffset	= gs_treshold_width - gs_fthk
			endif
		endif
	endif

	parameters	ifc_ThresholdDepth		= ifc_ThresholdDepth,
				ifc_ThresholdThickness	= ifc_ThresholdThickness,
				ifc_ThresholdOffset		= ifc_ThresholdOffset
endif
