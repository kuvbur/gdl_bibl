
pageStartY = 0

outFldWdt	= 120
inFldWdt	= 90
inFldHalf	= inFldWdt/2 - 1
picBoxWdt	= 222

xPosStart	= 0
xPos0		= xPosStart + outFldWdt + 2
xPos1		= xPosStart + picBoxWdt + 8
xPos2		= xPos1 + outFldWdt + 2
xPos3		= xPos2 + inFldHalf + 2

fullWidth	= 442 - xPos1

outFldHgt	= 15
inFldHgt	= 19
picBoxHgt	= 266

y1	= pageStartY + 4
dy	= 21

ui_infield{3} "iDoorPanelType", xPosStart, y1-4, picBoxWdt, picBoxHgt,
	1, 1, 76, 19,													! DoorPanelTypes.png
	67, 110, 40, 80,
	1, stDoorTypes[ 1], PANEL_STYLE1,
	38, stDoorTypes[35], PANEL_STYLE2,
	40, stDoorTypes[36], PANEL_STYLE3,
	41, stDoorTypes[37], PANEL_STYLE4,
	42, stDoorTypes[38], PANEL_STYLE5,
	76, stDoorTypes[75], PANEL_STYLE6,
	43, stDoorTypes[39], PANEL_STYLE7,
	44, stDoorTypes[40], PANEL_STYLE8,
	50, stDoorTypes[49], PANEL_CUSTOM,
	51, stDoorTypes[50], PANEL_EMPTY


leafFrameFlag = 4*(iDoorPanelType <> PANEL_STYLE7 & iDoorPanelType <> PANEL_STYLE8)

ui_style 0,0
ui_infield{3}	"gs_ds",		xPos1,		y1-4,	fullWidth,	inFldHgt,
	7, "", 2, 1,
	0, 0, 0, 0,
	1, `Ограничитель Двери`, 0,
	2, `Ограничитель Двери`, 1

y1 = y1 + dy

ui_outfield `Толщина`,	xPos1,		y1,		outFldWdt,	outFldHgt, 4*not(gs_ds)
ui_infield "gs_ds_thk",		xPos2,		y1-4,	inFldWdt,	inFldHgt

y1 = y1 + dy

ui_outfield `Ширина`,		xPos1,		y1,		outFldWdt,	outFldHgt, 4*not(gs_ds)
ui_infield "gs_ds_w",		xPos2,		y1-4,	inFldWdt,	inFldHgt

y1 = y1 + dy

ui_separator xPos1-3, y1-4, 444, y1-4

y1 = y1 + 5

if iDoorPanelType = PANEL_CUSTOM then
	gosub "customPanelUI"
else
	if iDoorType = 2 then
		ui_outfield `Соотношение Панелей`,	xPos1,		y1,		outFldWdt,	outFldHgt
		ui_infield "gs_rttl",				xPos2,		y1-4,	inFldWdt,	inFldHgt

		y1 = y1 + dy

		ui_separator xPos1-3, y1-4, 444, y1-4

		y1 = y1 + 5
	endif

	if iDoorPanelType <> PANEL_EMPTY then
		ui_outfield `Толщина Полотна`,	xPos1,		y1,		outFldWdt,	outFldHgt 
		ui_infield "gs_leaf_thk",		xPos2,		y1-4,	inFldWdt,	inFldHgt

		y1 = y1 + dy

		if iDoorPanelType = PANEL_STYLE7 | iDoorPanelType = PANEL_STYLE8 then
			ui_outfield `Ширина Обвязки Полотна`,	xPos1,		y1,		outFldWdt,	outFldHgt, leafFrameFlag
			ui_infield "leafFrameWidth",	xPos2,		y1-4,	inFldWdt,	inFldHgt

			y1 = y1 + dy
		endif
	endif

	if iDoorPanelType = PANEL_STYLE7 | iDoorPanelType = PANEL_STYLE8 |\
		gs_sidelight_left | gs_sidelight_right then

		ui_outfield `Ширина Нижней Обвязки`,	xPos1,		y1,		outFldWdt,	outFldHgt
		ui_infield "bottomRailWidth",		xPos2,		y1-4,	inFldWdt,	inFldHgt
	endif

	y1 = 5 + pageStartY + 7*dy + 5 + 5*(iDoorType = 2)
	
	if iDoorPanelType = PANEL_STYLE7 | gs_sidelight_left | gs_sidelight_right then
		if iDoorPanelType = PANEL_STYLE7 then
			stMiddleRail = `Средник`
		else
			stMiddleRail = `Средник (Боковой Панели)`
		endif

		ui_infield{3}	"bMiddleRail",	xPos1,		y1-4,	fullWidth,	inFldHgt,
			7, "", 2, 1,
			0, 0, 0, 0,
			1, stMiddleRail, 0,
			2, stMiddleRail, 1

		y1 = y1 + dy

		ui_outfield `Ширина Средника`,		xPos1,		y1,		outFldWdt,	outFldHgt, 4*not(bMiddleRail)
		ui_infield "midRailWidth",				xPos2,		y1-4,	inFldWdt,	inFldHgt

		y1 = y1 + dy

		ui_outfield `Высота Средника`,	xPos1,		y1,		outFldWdt,	outFldHgt, 4*not(bMiddleRail)
		ui_infield "midRailElevation",			xPos2,		y1-4,	inFldWdt,	inFldHgt

		y1 = y1 + dy
	endif
endif

!===================================================================================
end
!===================================================================================

"customPanelUI":
	ui_style 0, 1

	y1 = y1 - 4

	ui_outfield `Имя Специального Полотна`,	xPos1,		y1,		fullWidth,		outFldHgt

	ui_style 0, 0

	y1 = y1 + dy

	ui_infield "gs_cust_panel",				xPos1+20,	y1-4,	fullWidth-20,	inFldHgt

	y1 = y1 + outFldHgt + 2

	call "ui_CreateCustomPanelInfo_m" parameters	bWindow		= 0,
													xPosition	= xPos1,
													yPosition	= y1,
													blockWidth	= fullWidth,
													blockHeight	= 265 - y1

return

