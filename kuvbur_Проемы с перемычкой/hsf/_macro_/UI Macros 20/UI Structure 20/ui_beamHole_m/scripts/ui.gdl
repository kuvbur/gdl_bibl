


HOLE_RECTANGULAR = 1
HOLE_CIRCULAR 	 = 2

uiHole		= `Отверстие`
uiHoleNum	= `Количество Отверстий`

x1 = 0
x2 = 171
x3 = 230
x4 = x2 + 100
x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
x6 = 400
x7 = 408
xEnd = 444

outfieldHeight		= 15
infieldHeight 		= 19

dy 					= 27
yEnd 				= 266
pageStartY			= 0
yCurrent 			= pageStartY + 1

ui_style 0, 0

ui_infield{3} "Shape", 	 x1,   yCurrent, 	200, infieldHeight,
			7, "",
			0, 0,
			0, 0, 0, 0,
			"", uiHole, 0,
			"", uiHole, 1

yCurrent = yCurrent + dy
ui_outfield	uiHoleNum,x1,    yCurrent+4, 	x2 - x1 - 2,	outfieldHeight, 4*not(Shape)
ui_infield "hnum", 	 x2,   yCurrent, 	x3 - x2- 60, infieldHeight

yCurrent = yCurrent + dy
ui_outfield	`Форма`, x1,    yCurrent+4, 	x2 - x1 - 2,	outfieldHeight, 4*not(Shape)
ui_infield{3} "gs_shap_m", 	 x2,   yCurrent-1, 	2*43, 21,
			4, 1, !"HoleChanger"
			2, 1,
			78, 17, 39, 17,
			1, "", 1,
			2, "", 2

yCurrent = yCurrent + dy
ui_outfield	`Контуры на Плане`, x1,    yCurrent+4, 	x2 - x1 - 2,	outfieldHeight, 4*not(Shape)
ui_infield{3} "iHoleContour2D", 	 x2,   yCurrent-1, 	2*43, 21,
			4, 2,  !"HolePlanSymbol"
			2, 1,
			78, 17, 39, 17,
			1, "", 1,
			2, "", 2

yCurrent = yCurrent + dy

ui_separator 0, yCurrent, xEnd, yCurrent

uiPictX = x2 + 10
uiPictY = yCurrent + 50
uiPictWdt = 57
uiPictHgt = 41
uiPictMargin = 5
uiPictInfWdt = 90

uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
uiPicLeftEND = uiPicLeftStart - uiPictMargin

uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3

uiPicBelow = uiPictY + uiPictHgt + uiPictMargin

uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin

if gs_shap_m = HOLE_RECTANGULAR then
	UI_PICT 3, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"HoleSquare"
else
	UI_PICT 4, uiPictX, uiPictY, uiPictWdt , uiPictHgt, 1 !"HoleCircle"
endif

if gs_shap_m = HOLE_RECTANGULAR then
	ui_infield "rectw", x2, uiPicTop, uiPictInfWdt, infieldHeight UI_TOOLTIP `Ширина Прямоугольника`
endif

ui_infield "pos", x2, uiPicBelow, uiPictInfWdt, infieldHeight UI_TOOLTIP `Расположение`

if gs_shap_m = HOLE_RECTANGULAR then
	ui_infield "recth", uiPicRightStart, uiPictY+12, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Прямоугольника`
else
	ui_infield "cdia", uiPicRightStart, uiPictY+12, uiPictInfWdt, infieldHeight UI_TOOLTIP `Диаметр Окружности`
endif

