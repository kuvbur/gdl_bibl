
dim stOrnamentType[]
	stOrnamentType[1] = `Тосканский`
	stOrnamentType[2] = `Дорический`
	stOrnamentType[3] = `Ионический`

dim stPlacementType[]
	stPlacementType[1] = `Отдельностоящая`
	stPlacementType[2] = `Угловая`
	stPlacementType[3] = `Полуколонна`
	stPlacementType[4] = `Частично Срезанная`

!iColumnType
COLUMN_DOR_1 	= 1
COLUMN_DOR_2 	= 2
COLUMN_PILLAR 	= 3
COLUMN_ROUND 	= 4

COLUMN_ARCH 	= 5
COLUMN_CONIC 	= 6
COLUMN_FACETED 	= 7

! const values for parameter: gs_detlevel_3D_m
DLEV3D_OFF      = 0
DLEV3D_SIMPLE   = 1
DLEV3D_DETAILED = 2

! const values for parameter: iPlacementType
PLACEMENT_FREESTAND  = 1
PLACEMENT_CORNER     = 2
PLACEMENT_HALFONWALL = 3
PLACEMENT_PARTCUT    = 4


! const values for parameter: iOrnamentType
ORNAMTYPE_TUSCAN = 1
ORNAMTYPE_DORIC  = 2
ORNAMTYPE_IONIC  = 3

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT = -1
_idxTab = 1
if 	iColumnType = COLUMN_ARCH |\
	iColumnType = COLUMN_CONIC |\
	iColumnType = COLUMN_FACETED then
	TABID_DIMENSIONS	 	= _idxTab : _idxTab = _idxTab + 1
endif
TABID_REPRES 			= _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS 		= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Колонны`
ui_current_page gs_ui_current_page


if 	iColumnType = COLUMN_ARCH | iColumnType = COLUMN_FACETED then
	dimensionTabTitle = `Стиль и Размеры`
	dimensionTabPict = "uiTab_Dimension_flat(1)"
else
	dimensionTabTitle = `Размеры`
	dimensionTabPict = "uiTab_Dimension_flat(1)"
endif

if 	iColumnType = COLUMN_ARCH |\
	iColumnType = COLUMN_CONIC |\
	iColumnType = COLUMN_FACETED then
	ui_page TABID_DIMENSIONS, TABID_ROOT, dimensionTabTitle, dimensionTabPict 
	gosub "dimensionsPage"
endif

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
gosub "descriptionPage"

n = REQUEST ("Working_length_unit", "", workingLengthUnit)

!--------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"


! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------
"dimensionsPage":
	x1 = 0
	x2 = 120
	x3 = 210
	x4 = x2 + 100
	x5 = x2 + 200	!200 (Style Guide => fillTypeInfLenght)
	x6 = 360
	x7 = 390
	xEnd = 444
	
	outfieldHeight		= 15
	infieldHeight 		= 19
	
	dy 					= 24
	yEnd 				= 266
	pageStartY			= 0
	yCurrent 			= pageStartY + 1
	
	ui_style 0, 0


	if iColumnType = COLUMN_ARCH then
		ui_outfield	`Тип Ордера` ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
		ui_infield{3} "iOrnamentType", 	 x2,   yCurrent, 	x3 - x2, infieldHeight,
					8, "", 3, 1,
					75, 15, 75, 15,
					1, stOrnamentType[1], ORNAMTYPE_TUSCAN,
					2, stOrnamentType[2], ORNAMTYPE_DORIC,
					3, stOrnamentType[3], ORNAMTYPE_IONIC
	
			ui_outfield	`Количество Канелюр` ,x4,    yCurrent+4, 	x7 - x4-2,	outfieldHeight	
			ui_infield "cannum", 	 x7,   yCurrent, 	xEnd - x7, infieldHeight
		
		yCurrent = yCurrent + dy

		ui_outfield	`Размещение` ,x1,    yCurrent+4, 	x2 - 2,	outfieldHeight	
		ui_infield{3} "iPlacementType", 	 x2,   yCurrent, 	x3 - x2, infieldHeight+25,
					2, 1, 4, 1, 
					75, 39, 75, 39,
					1, stPlacementType[1], PLACEMENT_FREESTAND,
					2, stPlacementType[2], PLACEMENT_CORNER,
					3, stPlacementType[3], PLACEMENT_HALFONWALL,
					4, stPlacementType[4], PLACEMENT_PARTCUT

			ui_infield{3} "canelure",	x4,   yCurrent, 	xEnd-x4, infieldHeight,
					7,"",0,0,
					0,0,0,0,
					"",`Канелюры`, 0,
					"",`Канелюры`, 1

		yCurrent = yCurrent + dy

			ui_infield{3} "entasis",	x4,   yCurrent, 	xEnd-x4, infieldHeight,
					7,"",0,0,
					0,0,0,0,
					"",`Энтазис`, 0,
					"",`Энтазис`, 1


		yCurrent = yCurrent + dy
	
		ui_separator 0, yCurrent, xEnd, yCurrent
	
		uiPictX = 170
		uiPictY = 85
		uiPictWdt = 71
		uiPictHgt = 173
		uiPictMargin = 5
		uiPictInfWdt = 60
		infieldHeight 	= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin
	
		ui_pict 2, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
		ui_infield "coldt", uiPicLeftStart, uiPictY+2, uiPictInfWdt, infieldHeight UI_TOOLTIP `Диаметр Вверху`
		ui_infield "coldB", uiPicLeftStart, uiPictY+110, uiPictInfWdt, infieldHeight UI_TOOLTIP `Диаметр Внизу`
		
		ui_infield{3} "coc",	uiPicRightStart,   uiPictY-5, 	150, infieldHeight,
				7,"",0,0,
				0,0,0,0,
				"",`Капитель`, 0,
				"",`Капитель`, 1
	
		ui_infield{3} "boc",	uiPicRightStart,   uiPictY+118, 	150, infieldHeight,
				7,"",0,0,
				0,0,0,0,
				"",`Основание`, 0,
				"",`Основание`, 1

		ui_infield{3} "poc",	uiPicRightStart,   uiPictY+140, 	150, infieldHeight,
				7,"",0,0,
				0,0,0,0,
				"",`Пьедестал`, 0,
				"",`Пьедестал`, 1

	endif

	if iColumnType = COLUMN_CONIC then
	
		uiPictX = 170
		uiPictY = 10
		uiPictWdt = 71
		uiPictHgt = 242
		uiPictMargin = 5
		uiPictInfWdt = 60
		infieldHeight 	= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin
	
		ui_pict 3, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
		ui_infield "td", uiPicLeftStart, uiPictY-4, uiPictInfWdt, infieldHeight UI_TOOLTIP `Верхний Диаметр`

		ui_infield "tl", uiPicRightStart, uiPictY+20, uiPictInfWdt, infieldHeight UI_TOOLTIP `Длина Конуса`
	
	endif

	if iColumnType = COLUMN_FACETED then
		x3 = 155

		ui_outfield	`Количество Канелюр`, x1,    yCurrent+4, 	x3-x1-2,	outfieldHeight	
		ui_infield "nf", 	 x3,   yCurrent, 	x4 - x3-10, infieldHeight
	
		yCurrent = yCurrent + dy
	
		ui_separator 0, yCurrent, xEnd, yCurrent
	
		uiPictX = 178
		uiPictY = 35
		uiPictWdt = 71
		uiPictHgt = 222
		uiPictMargin = 5
		uiPictInfWdt = 60
		infieldHeight 	= 19
	
		uiPicLeftStart = uiPictX - uiPictInfWdt - uiPictMargin
		uiPicLeftEND = uiPicLeftStart - uiPictMargin
	
		uiPicTop = uiPictY - uiPictMargin - infieldHeight + 3
	
		uiPicBelow = uiPictY + uiPictHgt + uiPictMargin
	
		uiPicRightStart = uiPictX + uiPictWdt  + uiPictMargin
	
		ui_pict 4, uiPictX , uiPictY , uiPictWdt , uiPictHgt , 1
		ui_infield "hc", uiPicLeftStart, uiPictY-1, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Капители`
		ui_infield "hb", uiPicLeftStart, uiPictY+205, uiPictInfWdt, infieldHeight UI_TOOLTIP `Высота Основания`
		
		ui_infield{3} "capital",	uiPicRightStart,   uiPictY-1, 	150, infieldHeight,
				7,"",0,0,
				0,0,0,0,
				"",`Капитель`, 0,
				"",`Капитель`, 1
	
		ui_infield{3} "gs_base",	uiPicRightStart,   uiPictY+205, 	150, infieldHeight,
				7,"",0,0,
				0,0,0,0,
				"",`Основание`, 0,
				"",`Основание`, 1

	endif


return

"AppearancePage":
	if iColumnType = COLUMN_DOR_1 | iColumnType = COLUMN_DOR_2 | iColumnType = COLUMN_FACETED then
		dim st2DParamNames[][]
			st2DParamNames[1][1] = "gs_line_type" 			: st2DParamNames[1][2] = "ui_list_contLineType_flat(1)"
			st2DParamNames[2][1] = "capital_line_type" 		: st2DParamNames[2][2] = "ui_list_contLineType_flat(1)"
	
			st2DParamNames[3][1] = "gs_cont_pen" 			: st2DParamNames[3][2] = "ui_list_contourPen_flat(1)"
			st2DParamNames[4][1] = "gs_fill_type" 			: st2DParamNames[4][2] = "ui_list_fillType_flat(1)"
			st2DParamNames[5][1] = "gs_fill_pen" 			: st2DParamNames[5][2] = "ui_list_fillPen_flat(1)"
			st2DParamNames[6][1] = "gs_back_pen" 			: st2DParamNames[6][2] = "ui_list_fillBgPen_flat(1)"
	
			st2DParamNames[7][1] = "capital_cont_pen" 		: st2DParamNames[7][2] = "ui_list_contourPen_flat(1)"
			st2DParamNames[8][1] = "capital_fill_type" 		: st2DParamNames[8][2] = "ui_list_fillType_flat(1)"
			st2DParamNames[9][1] = "capital_fill_pen" 		: st2DParamNames[9][2] = "ui_list_fillPen_flat(1)"
			st2DParamNames[10][1] = "capital_back_pen" 		: st2DParamNames[10][2] = "ui_list_fillBgPen_flat(1)"
	
		dim st3DParamNames[][]
			st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
			st3DParamNames[2][1] = "gs_resol"			: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
			st3DParamNames[3][1] = "gs_shadow"			: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"

		dim stSurfaceParamNames[][]
			stSurfaceParamNames[1][1] = "gs_capital_mat"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	
			stSurfaceParamNames[2][1] = "gs_column_mat"			: stSurfaceParamNames[2][2] = "ui_list_surface_flat(1)"


		if iColumnType = COLUMN_FACETED | iColumnType = COLUMN_DOR_2 then
			stSurfaceParamNames[3][1] = "gs_base_mat"		: stSurfaceParamNames[3][2] = "ui_list_surface_flat(1)"
		endif
	else
		dim st2DParamNames[][]
			st2DParamNames[1][1] = "gs_line_type" 		: st2DParamNames[1][2] = "ui_list_contLineType_flat(1)"
			st2DParamNames[2][1] = "gs_cont_pen" 		: st2DParamNames[2][2] = "ui_list_contourPen_flat(1)"
			st2DParamNames[3][1] = "gs_fill_type" 		: st2DParamNames[3][2] = "ui_list_fillType_flat(1)"
			st2DParamNames[4][1] = "gs_fill_pen" 		: st2DParamNames[4][2] = "ui_list_fillPen_flat(1)"
			st2DParamNames[5][1] = "gs_back_pen" 		: st2DParamNames[5][2] = "ui_list_fillBgPen_flat(1)"	

		if iColumnType = COLUMN_ARCH then
			st2DParamNames[6][1] = "dca" 			: st2DParamNames[6][2] = ""	
		endif
		
		dim st3DParamNames[][]
		if iColumnType = COLUMN_ARCH | iColumnType = COLUMN_CONIC then
			st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
			st3DParamNames[2][1] = "gs_resol"			: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
			st3DParamNames[3][1] = "gs_shadow"			: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"
		else
			st3DParamNames[1][1] = "gs_detlevel_3D_m"	: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
			st3DParamNames[2][1] = "gs_shadow"			: st3DParamNames[2][2] = "ui_list_shadow_flat(1)"
		endif

		dim stSurfaceParamNames[][]
		if iColumnType <> COLUMN_ARCH & iColumnType <> COLUMN_FACETED then
			stSurfaceParamNames[1][1] = "gs_column_mat"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	
		endif

		if iColumnType = COLUMN_ARCH then
			stSurfaceParamNames[1][1] = "mat"		: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	
		endif

		if iColumnType = COLUMN_CONIC then
			stSurfaceParamNames[2][1] = "mat2"		: stSurfaceParamNames[2][2] = "ui_list_surface_flat(1)"	
		endif

	endif
	
	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

return

"descriptionPage":

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										stParamNames = stParamNames
return
