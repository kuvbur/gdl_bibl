
! =============================================================================
! Localization
! =============================================================================

! iMoldingObject
MOLDING_STRAIGHT = 1
MOLDING_CURVED_POS = 2
MOLDING_CURVED_NEG = 3

! iClosingSide
CLOSING_SIDE1 = 1
CLOSING_SIDE2 = 2
CLOSING_SIDE_BOTH = 3

dim idxUIControl[]
	for i = 1 to vardim1(stMoldingStyle)
		idxUIControl[i] = i
	next i

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 			= -1
_idxTab 			= 1
TABID_STYLE 		= _idxTab : _idxTab = _idxTab + 1
TABID_GEOMETRY 		= _idxTab : _idxTab = _idxTab + 1
TABID_REPRES		= _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS 	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Профиля`
ui_current_page gs_ui_current_page

ui_page TABID_STYLE, TABID_ROOT , `Стиль`, "uiTab_Style_flat(1)"
	gosub "stylePage"

ui_page TABID_GEOMETRY, TABID_ROOT , `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "geometryPage"

ui_page TABID_REPRES, TABID_ROOT, `Отображение и Покрытия`, "uiTab_2DRepr_flat(1)"
	gosub "AppearancePage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Описание`, "uiTab_Descriptions_flat(1)"
	gosub "descriptionPage"

! --------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Style_flat(1)"
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! =============================================================================
end
! =============================================================================

! --------------------------------------------------------------------------------
! Subroutines
! --------------------------------------------------------------------------------

! --------------------------------------------------------------------------------
"stylePage":
! --------------------------------------------------------------------------------

	x1 					= 0
	xEnd 				= 444
	yEnd 				= 265
	pageStartY			= 0
	yCurrent 			= pageStartY

	if vardim1(stMoldingStyle) > 0 then
		ui_infield{4} "iMoldingStyle", x1, yCurrent, xEnd - x1, yEnd - yCurrent,
					1,"ui_moldingtypes(14)",
					18, 5,
					105, 125, 90, 90,
					idxUIControl, stMoldingStyle, idxUIControl
	endif

return

! --------------------------------------------------------------------------------
"geometryPage":
! --------------------------------------------------------------------------------
	x1 			= 0
	x2 			= 170
	_infHeight 	= 19
	_yCurr 		= pageStartY
	_dy 		= 24

	if iMoldingObject = 1 then
		ui_infield{3} "bClosingEnds", x1, _yCurr, x2 - 2, _infHeight,
						7, "",
						0, 0,
						0, 0, 0, 0,
						"", `Профилированный Торец`, 0,
						"", `Профилированный Торец`, 1

		ui_infield{3} "iClosingSide", x2, _yCurr, 97, _infHeight,
					8, "",
					0, 0,
					0, 0, 0, 0,
					"", stClosingSide[1], 1,
					"", stClosingSide[2], 2,
					"", stClosingSide[3], 3
		_yCurr = _yCurr + _dy
		ui_separator x1, _yCurr, xEnd, _yCurr
	endif

	xPict = 9
	yPict = 116
	PictWidth = 40 
	PictHeight = 40

	if iMoldingStyle < 7  then
		ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1	! ui_Moldings_Profile1.png
	else
		if iMoldingStyle > 6 and iMoldingStyle < 13 then
			ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1 ! ui_Moldings_Profile2.png
		else
			ui_pict 3, xPict, yPict, PictWidth, PictHeight, 1	! ui_Moldings_Profile3.png
		endif
	endif

	_yCurr = yPict - _infHeight - 3
	ui_infield "profileWidth", xPict - 3, _yCurr, 90, _infHeight ui_tooltip `Ширина Профиля`

	_yCurr = yPict + 16
	ui_infield "profileHeight", xPict + PictWidth + 3, _yCurr, 90, _infHeight ui_tooltip `Высота Профиля`


	xPict = 228
	PictWidth = 148 

	if iMoldingObject = 1 then	! Straight

		_yCurr = yPict + 16
		if not(bClosingEnds) then
			ui_pict 4, xPict, yPict, PictWidth, PictHeight, 1	! ui_StraightMolding_Dim1.png
			ui_infield "alpha", xPict + PictWidth + 3, _yCurr, 60, _infHeight ui_tooltip `Угол Подрезки Справа`
			ui_infield "beta", xPict - 63, _yCurr, 60, _infHeight ui_tooltip `Угол Подрезки Слева`
		else
			if iClosingSide = CLOSING_SIDE1 then
				ui_pict 5, xPict, yPict, PictWidth, PictHeight, 1	! ui_StraightMolding_Dim2.png
				ui_infield "alpha", xPict + PictWidth + 3, _yCurr, 60, _infHeight ui_tooltip `Угол Подрезки Справа`
			else
				if iClosingSide = CLOSING_SIDE2 then
					ui_pict 6, xPict, yPict, PictWidth, PictHeight, 1	! ui_StraightMolding_Dim3.png
					ui_infield "beta", xPict - 63, _yCurr, 60, _infHeight ui_tooltip `Угол Подрезки Слева`
				else
					ui_pict 7, xPict, yPict, PictWidth, PictHeight, 1	! ui_StraightMolding_Dim4.png
				endif
			endif
		endif

		_yCurr = yPict - _infHeight - 3
		ui_infield "A", xPict + 29, _yCurr, 90, _infHeight ui_tooltip `Размер 1`
	else
		yPict = 79
		PictHeight = 115

		if iMoldingObject = 3 then 	! Curved 01
			ui_pict 8, xPict, yPict, PictWidth, PictHeight, 1	! ui_Curved01Molding_Dim.png
		else						! Curved 02
			ui_pict 9, xPict, yPict, PictWidth, PictHeight, 1	! ui_Curved02Molding_Dim.png
		endif

		_yCurr = yPict - _infHeight - 3
		ui_infield "beta", xPict + 96, _yCurr, 60, _infHeight ui_tooltip `Угол Подрезки Слева`

		_yCurr = yPict + 86
		ui_infield "malpha", xPict - 63, _yCurr, 60, _infHeight ui_tooltip `Угол Профиля`
		ui_infield "alpha", xPict + PictWidth + 3, _yCurr, 60, _infHeight ui_tooltip `Угол Подрезки Справа`

		_yCurr = yPict + PictHeight + 3
		ui_infield "rr", xPict + 28, _yCurr, 90, _infHeight ui_tooltip `Радиус Профиля`
	endif
return

! ------------------------------------------------------------------------------
"AppearancePage":
! ------------------------------------------------------------------------------

	dim st2DParamNames[5][2]
		st2DParamNames[1][1] = "gs_line_type" 	: st2DParamNames[1][2] = "ui_list_contLineType_flat(1)"
		st2DParamNames[2][1] = "gs_cont_pen" 	: st2DParamNames[2][2] = "ui_list_contourPen_flat(1)"
		st2DParamNames[3][1] = "gs_fill_type" 	: st2DParamNames[3][2] = "ui_list_fillType_flat(1)"
		st2DParamNames[4][1] = "gs_fill_pen" 	: st2DParamNames[4][2] = "ui_list_fillPen_flat(1)"
		st2DParamNames[5][1] = "gs_back_pen" 	: st2DParamNames[5][2] = "ui_list_fillBgPen_flat(1)"

	dim st3DParamNames[3][2]
		st3DParamNames[1][1] = "gs_detlevel_3D_m"		: st3DParamNames[1][2] = "ui_list_detailLevel3D_flat(1)"
		st3DParamNames[2][1] = "gs_resol"				: st3DParamNames[2][2] = "ui_list_resol_flat(1)"
		st3DParamNames[3][1] = "gs_shadow"				: st3DParamNames[3][2] = "ui_list_shadow_flat(1)"

	dim stSurfaceParamNames[1][2]
		stSurfaceParamNames[1][1] = "gs_molding_mat"	: stSurfaceParamNames[1][2] = "ui_list_surface_flat(1)"	


	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"

	call "ui_structure_Representation" parameters all bDraw2DGroupList = 1,
										listfieldID = TABID_REPRES,
										listFieldPosY = 0,
										listFieldHeight = 265,
										stDescHeader = "",
										stValueHeader = "",
										st2DParamNames = st2DParamNames,
										st3DParamNames = st3DParamNames,
										stSurfaceParamNames = stSurfaceParamNames

	! --------------------------------------------------------------------------------
	! image list for archive
	! --------------------------------------------------------------------------------
	file_dependence "ui_list_contLineType_flat(1)"
	file_dependence "ui_list_contourPen_flat(1)"
	file_dependence "ui_list_fillType_flat(1)"
	file_dependence "ui_list_fillPen_flat(1)"
	file_dependence "ui_list_fillBgPen_flat(1)"
	file_dependence "ui_list_detailLevel3D_flat(1)"
	file_dependence "ui_list_resol_flat(1)"
	file_dependence "ui_list_shadow_flat(1)"
	file_dependence "ui_list_surface_flat(1)"
return

! ------------------------------------------------------------------------------
"descriptionPage":
! ------------------------------------------------------------------------------

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID = TABID_DESCRIPTIONS,
									listFieldPosY = 0,
									listFieldHeight = 265,
									stDescHeader = "",
									stValueHeader = "",
									stParamNames = stParamNames

return
