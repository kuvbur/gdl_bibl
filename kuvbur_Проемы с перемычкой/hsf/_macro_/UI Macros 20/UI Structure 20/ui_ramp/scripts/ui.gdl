

!iRampType
RAMP_CURVED = 1
RAMP_STRAIGHT = 2

! =============================================================================
! Tabpage Structure Definition
! =============================================================================

TABID_ROOT 			= - 1
_idxTab 			= 1
TABID_STRUC_DIM 	= _idxTab : _idxTab = _idxTab + 1
TABID_PROFILE 		= _idxTab : _idxTab = _idxTab + 1
TABID_2D	 		= _idxTab : _idxTab = _idxTab + 1
TABID_BREAKWALK	 	= _idxTab : _idxTab = _idxTab + 1
TABID_STARTEND 		= _idxTab : _idxTab = _idxTab + 1
TABID_TEXT	 		= _idxTab : _idxTab = _idxTab + 1
TABID_3D	 		= _idxTab : _idxTab = _idxTab + 1
TABID_DESCRIPTIONS	= _idxTab : _idxTab = _idxTab + 1

! =============================================================================
! Display actual tabpages
! =============================================================================

ui_dialog `Параметры Пандуса`
ui_current_page gs_ui_current_page

ui_page TABID_STRUC_DIM, TABID_ROOT, `Стиль и Размеры`, "uiTab_Dimension_flat(1)"
	gosub "StyleDimPage"

ui_page TABID_PROFILE, TABID_ROOT, `Профиль Пандуса`, "uiTab_RampSect_flat(1)"
	gosub "ProfilePage"

ui_page TABID_2D, TABID_ROOT, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DPage"

	ui_page TABID_BREAKWALK, TABID_2D, `Линия Разрыва и Основная Линия`, "uiTab_StairWalkLBreakM_flat(1)"
		gosub "BreakWalkingPage"

	ui_page TABID_STARTEND, TABID_2D, `Начало и Окончание Основной Линии`, "uiTab_StairStartEndSymb_flat(1)"
		gosub "StartEndPage"

	ui_page TABID_TEXT, TABID_2D, `Текст`, "uiTab_Text_flat(1)"
		gosub "TextPage"

ui_page TABID_3D, TABID_ROOT, `3D-отображение и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DPage"

ui_page TABID_DESCRIPTIONS, TABID_ROOT, `Характеристики`, "uiTab_Descriptions_flat(1)"
	gosub "descriptionPage"


!--------------------------------------------------------------------------------
! Image links for archive
! --------------------------------------------------------------------------------
file_dependence "uiTab_Dimension_flat(1)"
file_dependence "uiTab_RampSect_flat(1)"
file_dependence "uiTab_2DRepr_flat(1)"
file_dependence "uiTab_StairWalkLBreakM_flat(1)"
file_dependence "uiTab_StairStartEndSymb_flat(1)"
file_dependence "uiTab_Text_flat(1)"
file_dependence "uiTab_3DRepr_flat(1)"
file_dependence "uiTab_Descriptions_flat(1)"

! ======================================================================
end
! ======================================================================

"StyleDimPage":

	pageStartY 	= 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_outfFlag 	= 0
	_dy			= 24
	_outfFlag	= 0

	x1 = 0
	x2 = 170
	x3 = 277
	x4 = x3 + 57
	x5 = 354
	x6 = x5 + 42
	x7 = x6 + 7

	_yCurr = pageStartY
	ui_outfield `Способ Расчета`, x1, _yCurr + _yOffset, x2 - 2, _outfHeight

	ui_infield{3} "iCalculationMethod", x2, _yCurr, 96, _infHeight,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"", `Соотношение`, 		1,
		"", `Проценты`, 	2,
		"", `Размеры`, 	3 ui_tooltip `Способ Расчета`

	if iCalculationMethod = 2 then	! Percentage
		ui_infield "percentage", x3, _yCurr, 55, _infHeight  ui_tooltip `Проценты`
	else
		_outfFlag = 4
		dimensionForm = "%~"
		ui_outfield str(dimensionForm, percentage), x3, _yCurr + _yOffset, 55, _outfHeight, 1 + _outfFlag
	endif

	ui_outfield `%`, x4, _yCurr + _yOffset, 18, _outfHeight, _outfFlag
	_outfFlag = 0

	if iCalculationMethod = 1 then	! Ratio
		ui_infield "ratioLength", x5, _yCurr, 40, _infHeight  ui_tooltip `Соотношение Длины`
		ui_infield "ratioHeight", x7, _yCurr, 40, _infHeight  ui_tooltip `Соотношение Высоты`
	else
		_outfFlag = 4
		dimensionForm = "%~"
		ui_outfield str(dimensionForm, ratioLength), x5, _yCurr + _yOffset, 40, _outfHeight, 1 + _outfFlag
		ui_outfield str(dimensionForm, ratioHeight), x7, _yCurr + _yOffset, 40, _outfHeight,_outfFlag
	endif

	ui_outfield ":", x6, _yCurr + _yOffset, 5, _outfHeight, 2 + _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr + _dy

	ui_infield{3} "bAscent", x1, _yCurr, 266, _infHeight,
		7, "",
		2, 0,
		0, 0, 0, 0,
		"", `Переход`, 0,
		"", `Переход`, 1

	if iCalculationMethod = 2 then	! Percentage
		ui_infield "ascentPercentage", x3, _yCurr, 55, _infHeight  ui_tooltip `Проценты Перехода`
	else
		_outfFlag = 4
		dimensionForm = "%~"
		ui_outfield str(dimensionForm, ascentPercentage), x3, _yCurr + _yOffset, 55, _outfHeight, 1 + _outfFlag
	endif

	ui_outfield `%`, x4, _yCurr + _yOffset, 18, _outfHeight, _outfFlag
	_outfFlag = 0

	if iCalculationMethod = 1 then	! Ratio
		ui_infield "ratioAscentLength", x5, _yCurr, 40, _infHeight  ui_tooltip `Соотношение Длины Перехода`
		ui_infield "ratioAscentHeight", x7, _yCurr, 40, _infHeight  ui_tooltip `Соотношение Высоты Перехода`
	else
		_outfFlag = 4
		dimensionForm = "%~"
		ui_outfield str(dimensionForm, ratioAscentLength), x5, _yCurr + _yOffset, 40, _outfHeight, 1 + _outfFlag
		ui_outfield str(dimensionForm, ratioAscentHeight), x7, _yCurr + _yOffset, 40, _outfHeight, _outfFlag
	endif

	ui_outfield ":", x6, _yCurr + _yOffset, 5, _outfHeight, 2 + _outfFlag
	_outfFlag = 0

	_yCurr = _yCurr + _dy

	ui_separator x1, _yCurr, 444, _yCurr

	xPict 		= 93
	yPict 		= 52
	PictWidth 	= 258 
	PictHeight 	= 58
	x1 = xPict - 93
	x2 = xPict + PictWidth + 3

	if iRampType = RAMP_STRAIGHT then
		if bAscent then
			ui_pict 2, xPict, yPict, PictWidth, PictHeight, 1	! RampStraight_StyleAndDimensions2.png
		else
			ui_pict 1, xPict, yPict, PictWidth, PictHeight, 1	! RampStraight_StyleAndDimensions1.png
		endif
	else
		if bAscent then
			ui_pict 6, xPict, yPict, PictWidth, PictHeight, 1	! RampCurved_StyleAndDimensions2.png
		else
			ui_pict 5, xPict, yPict, PictWidth, PictHeight, 1	! RampCurved_StyleAndDimensions1.png
		endif
	endif

	qq= REQUEST ("Working_length_unit", "", dimensionForm)

	_yCurr = yPict + 2
	if bAscent then
		ui_infield "heightAscent", x1, _yCurr, 90, _infHeight  ui_tooltip `Высота Перехода`

		_yCurr = _yCurr + _dy
		_Height = zzyzx - heightAscent*2

		ui_outfield str(dimensionForm, _Height), x1, _yCurr + _yOffset, 90, _outfHeight, 1
	endif

	_yCurr = yPict + 15
	ui_infield "zzyzx", x2, _yCurr, 90, _infHeight  ui_tooltip `Высота`

	xPict 		= 93
	yPict 		= 135
	PictWidth 	= 258 
	x1 = xPict - 93
	x2 = xPict + PictWidth + 3

	if iRampType = RAMP_STRAIGHT then

		PictHeight 	= 87
		_yCurr = yPict - _infHeight - 3
		ui_infield "A", x1 + (PictWidth + 90)/2, _yCurr, 90, _infHeight ui_tooltip `Размер 1`

		_yCurr = yPict + 33
		ui_infield "B", x2, _yCurr, 90, _infHeight ui_tooltip `Размер 2`
		if bAscent then
			ui_pict 4, xPict, yPict, PictWidth, PictHeight, 1	! RampStraight_StyleAndDimensions4.png

			_yCurr = yPict + PictHeight + 3
			ui_infield "lengthAscent", xPict - 15, _yCurr, 90, _infHeight ui_tooltip `Длина Перехода`

			ui_outfield str(dimensionForm, lengthAscent), xPict + PictWidth - 75, _yCurr + _yOffset, 90, _outfHeight, 2 ui_tooltip `Длина Перехода`

			_Length = A - lengthAscent*2
			ui_outfield str(dimensionForm, _Length), x1 + (PictWidth + 90)/2, _yCurr + _yOffset, 90, _outfHeight, 2
		else
			ui_pict 3, xPict, yPict, PictWidth, PictHeight, 1	! RampStraight_StyleAndDimensions3.png
		endif
	else
		PictHeight 	= 130
		_yCurr = yPict - _infHeight - 3
		ui_infield "alph", xPict + 95, _yCurr, 90, _infHeight ui_tooltip `Угол`

		ui_infield "widthUsable", xPict - 7, _yCurr, 90, _infHeight ui_tooltip `Используемая Ширина`

		_yCurr = yPict + 5
		ui_infield "inRad", x1, _yCurr, 90, _infHeight ui_tooltip `Малый Радиус`

		_yCurr = _yCurr + _dy
		ui_infield "outRad", x1, _yCurr, 90, _infHeight  ui_tooltip `Большой Радиус`

		_yCurr = yPict - _infHeight - 3
		ui_outfield `Длина по Центру`, x2 - 30, _yCurr + _yOffset, 120, _outfHeight

		_yCurr = _yCurr + _dy - _yOffset

		if iCalculationMethod <> 3 then _outfFlag = 4
		ui_outfield `Общая`, x2, _yCurr + _yOffset, 90, _outfHeight, _outfFlag
		_outfFlag = 0

		_yCurr = _yCurr + _dy
		ui_infield "lengthFull", x2, _yCurr, 90, _infHeight ui_tooltip `Полная Длина`

		if bAscent then
			_yCurr = _yCurr + _dy - _yOffset
			ui_outfield `Верх / Низ`, x2, _yCurr + _yOffset, 90, _outfHeight
		
			_yCurr = _yCurr + _dy
			ui_infield "lengthAscent", x2, _yCurr, 90, _infHeight ui_tooltip `Длина Перехода`
		
			_yCurr = _yCurr + _dy - _yOffset
			ui_outfield `Середина`, x2, _yCurr + _yOffset, 90, _outfHeight
		
			_yCurr = _yCurr + _dy - _yOffset
			_Length = lengthFull - lengthAscent*2
			ui_outfield str(dimensionForm, _Length), x2, _yCurr + _yOffset, 90, _outfHeight

			ui_pict 8, xPict, yPict, PictWidth, PictHeight, 1	! RampCurved_StyleAndDimensions4.png
		else
			ui_pict 7, xPict, yPict, PictWidth, PictHeight, 1	! RampCurved_StyleAndDimensions3.png
		endif

	endif
return

"ProfilePage":

	pageStartY 	= 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_dy			= 24

	x1 = 0
	x2 = 231

	qq= REQUEST ("Working_length_unit", "", dimensionForm)
	
	_yCurr = pageStartY

	ui_infield{3} "bDiffSides", x1,	_yCurr, 444, _infHeight,
		7, "", 0, 0,
		0, 0, 0, 0,
		"",`Разные Стороны`, 0,
		"",`Разные Стороны`, 1

	_yCurr = _yCurr + _dy
	if not (bDiffSides) then
		ui_infield{3} "bSideFlange1", x1, _yCurr, 444, _infHeight,
				7, "", 0, 0,
				0, 0, 0, 0,
				"",`Боковая Кромка 1`, 0,
				"",`Боковая Кромка 1`, 1

		_yCurr = _yCurr + _dy

		ui_infield{3} "bSideFlange2",	x1,   _yCurr, 	444, _infHeight,
				7, "", 0, 0,
				0, 0, 0, 0,
				"",`Боковая Кромка 2`, 0,
				"",`Боковая Кромка 2`, 1

		_yCurr = _yCurr + _dy
	else
		if iRampType = RAMP_CURVED then
			ui_infield{3} "bSideFlange1_in",	x1,   _yCurr, 	x2 - 2, _infHeight,
					7, "", 0, 0,
					0, 0, 0, 0,
					"",`Боковая Кромка 1 Слева`, 0,
					"",`Боковая Кромка 1 Слева`, 1

				ui_infield{3} "bSideFlange1_out",	x2,   _yCurr, 	444 - x2, _infHeight,
						7, "", 0, 0,
						0, 0, 0, 0,
						"",`Боковая Кромка 1 Справа`, 0,
						"",`Боковая Кромка 1 Справа`, 1

			_yCurr = _yCurr + _dy

			ui_infield{3} "bSideFlange2_in",	x1,   _yCurr, 	x2 - 2, _infHeight,
					7, "", 0, 0,
					0, 0, 0, 0,
					"",`Боковая Кромка 2 Слева`, 0,
					"",`Боковая Кромка 2 Слева`, 1

				ui_infield{3} "bSideFlange2_out",	x2,   _yCurr, 	444 - x2, _infHeight,
						7, "", 0, 0,
						0, 0, 0, 0,
						"",`Боковая Кромка 2 Справа`, 0,
						"",`Боковая Кромка 2 Справа`, 1
		else
			ui_infield{3} "bSideFlange1_left",	x1,   _yCurr, 	x2 - 2, _infHeight,
					7, "", 0, 0,
					0, 0, 0, 0,
					"",`Боковая Кромка 1 Слева`, 0,
					"",`Боковая Кромка 1 Слева`, 1

				ui_infield{3} "bSideFlange1_right",	x2,   _yCurr, 	444 - x2, _infHeight,
						7, "", 0, 0,
						0, 0, 0, 0,
						"",`Боковая Кромка 1 Справа`, 0,
						"",`Боковая Кромка 1 Справа`, 1

			_yCurr = _yCurr + _dy

			ui_infield{3} "bSideFlange2_left",	x1,   _yCurr, 	x2 - 2, _infHeight,
					7, "", 0, 0,
					0, 0, 0, 0,
					"",`Боковая Кромка 2 Слева`, 0,
					"",`Боковая Кромка 2 Слева`, 1

				ui_infield{3} "bSideFlange2_right",	x2,   _yCurr, 	444 - x2, _infHeight,
						7, "", 0, 0,
						0, 0, 0, 0,
						"",`Боковая Кромка 2 Справа`, 0,
						"",`Боковая Кромка 2 Справа`, 1
		endif

		_yCurr = _yCurr + _dy
	endif

	ui_separator 0, _yCurr, 444, _yCurr

	PictWidth 	= 122 
	PictHeight 	= 95
	xPictR 		= 100 + PictWidth
	xPictL 		= 100
	yPict 		= 120
	x1 = xPictL - 93
	x2 = xPictR + PictWidth + 3

	_yCurr = yPict + 58
	ui_infield "thicknessRamp", x2, _yCurr, 90, _infHeight ui_tooltip `Толщина`

	_yCurr = yPict + PictHeight + 3
	if iRampType = RAMP_CURVED then
		ui_infield "widthFull", xPictR + 8, _yCurr, 90, _infHeight ui_tooltip `Ширина Внутри`
	else
		ui_infield "widthUsable", xPictR + 8, _yCurr, 90, _infHeight ui_tooltip `Ширина Внутри`
	endif

	if not (bDiffSides) then
		if iRampType = RAMP_CURVED then
			if bSideFlange1 or bSideFlange2 then ui_infield "widthUsable", xPictL + 24, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		else
			if bSideFlange1 or bSideFlange2 then ui_infield "B", xPictL + 24, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		endif

		if bSideFlange1 and bSideFlange2 then
			ui_pict 9, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L1.png
			ui_pict 13, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R1.png
			
			_yCurr = yPict - _infHeight - 3
			ui_infield "widthSideFlange2", xPictL - 62, _yCurr, 90, _infHeight ui_tooltip `Ширина`
			ui_outfield str(dimensionForm, widthSideFlange2), xPictR + PictWidth - 27, _yCurr + _yOffset, 90, _outfHeight

			ui_infield "widthSideFlange1", xPictL + 31, _yCurr, 90, _infHeight ui_tooltip `Ширина`
			ui_outfield str(dimensionForm, widthSideFlange1), xPictR + PictWidth - 120, _yCurr + _yOffset, 90, _outfHeight, 1

			_yCurr = yPict + 10
			ui_infield "heightSideFlange2", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
			ui_outfield str(dimensionForm, heightSideFlange2), x2, _yCurr + _yOffset, 90, _outfHeight

			_yCurr = _yCurr + _dy
			ui_infield "heightSideFlange1", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
			ui_outfield str(dimensionForm, heightSideFlange1), x2, _yCurr + _yOffset, 90, _outfHeight
		endif

		if bSideFlange1 and not (bSideFlange2) then
			ui_pict 10, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L2.png
			ui_pict 14, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R2.png

			_yCurr = yPict - _infHeight - 3
			ui_infield "widthSideFlange1", xPictL + 24+7, _yCurr, 90, _infHeight ui_tooltip `Ширина`
			ui_outfield str(dimensionForm, widthSideFlange1), xPictR + PictWidth - 120, _yCurr + _yOffset, 90, _outfHeight, 1

			_yCurr = yPict + 10 + _dy
			ui_infield "heightSideFlange1", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
			ui_outfield str(dimensionForm, heightSideFlange1), x2, _yCurr + _yOffset, 90, _outfHeight
		endif
		if not (bSideFlange1) and bSideFlange2 then
			ui_pict 10, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L2.png
			ui_pict 14, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R2.png

			_yCurr = yPict - _infHeight - 3
			ui_infield "widthSideFlange2", xPictL + 24+7, _yCurr, 90, _infHeight ui_tooltip `Ширина`
			ui_outfield str(dimensionForm, widthSideFlange2), xPictR + PictWidth - 120, _yCurr + _yOffset, 90, _outfHeight, 1

			_yCurr = yPict + 10 + _dy
			ui_infield "heightSideFlange2", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
			ui_outfield str(dimensionForm, heightSideFlange2), x2, _yCurr + _yOffset, 90, _outfHeight
		endif
		if not (bSideFlange1) and not (bSideFlange2) then
			ui_pict 12, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L4.png
			ui_pict 16, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R4.png
		endif
	else
		if 	not (bSideFlange1_in) and not (bSideFlange1_out) and not (bSideFlange2_in) and not (bSideFlange2_out) |\
			not (bSideFlange1_left) and not (bSideFlange1_right) and not (bSideFlange2_left) and not (bSideFlange2_right) then

			ui_pict 12, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L4.png
			ui_pict 16, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R4.png

		else
			if iRampType = RAMP_CURVED then
				if 	bSideFlange1_in or bSideFlange2_in |\
					bSideFlange1_out or bSideFlange2_out then
					ui_infield "widthUsable", xPictL + 24, _yCurr, 90, _infHeight ui_tooltip `Ширина`
				endif

				if bSideFlange1_in and bSideFlange2_in then
					ui_pict 9, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L1.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_in", xPictL - 62, _yCurr, 90, _infHeight ui_tooltip `Ширина`
					ui_infield "widthSideFlange1_in", xPictL + 31, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10
					ui_infield "heightSideFlange2_in", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
		
					_yCurr = _yCurr + _dy
					ui_infield "heightSideFlange1_in", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if bSideFlange1_in and not (bSideFlange2_in) then
					ui_pict 10, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange1_in", xPictL + 31, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange1_in", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_in) and bSideFlange2_in then
					ui_pict 10, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_in", xPictL + 31, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange2_in", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_in) and not (bSideFlange2_in) then
					if bSideFlange1_out or bSideFlange2_out then
						ui_pict 11, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L3.png
					else
						ui_pict 12, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L4.png
					endif
				endif
			
				if bSideFlange1_out and bSideFlange2_out then
					ui_pict 13, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R1.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_out", xPictR + PictWidth - 27, _yCurr, 90, _infHeight ui_tooltip `Ширина`
					ui_infield "widthSideFlange1_out", xPictR + PictWidth - 120, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10
					ui_infield "heightSideFlange2_out", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`
		
					_yCurr = _yCurr + _dy
					ui_infield "heightSideFlange1_out", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`

				endif
				if bSideFlange1_out and not (bSideFlange2_out) then
					ui_pict 14, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange1_out", xPictR + PictWidth - 120, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange1_out", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_out) and bSideFlange2_out then
					ui_pict 14, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_out", xPictR + PictWidth - 120, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange2_out", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_out) and not (bSideFlange2_out) then
					if bSideFlange1_in or bSideFlange2_in then
						ui_pict 15, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R3.png
					else
						ui_pict 16, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R4.png
					endif
				endif
			else
				if 	bSideFlange1_left or bSideFlange2_left |\
 					bSideFlange1_right or bSideFlange2_right then 
					ui_infield "B", xPictL + 24, _yCurr, 90, _infHeight ui_tooltip `Ширина`
				endif

				if bSideFlange1_left and bSideFlange2_left then
					ui_pict 9, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L1.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_left", xPictL - 62, _yCurr, 90, _infHeight ui_tooltip `Ширина`
					ui_infield "widthSideFlange1_left", xPictL + 31, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10
					ui_infield "heightSideFlange2_left", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
		
					_yCurr = _yCurr + _dy
					ui_infield "heightSideFlange1_left", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if bSideFlange1_left and not (bSideFlange2_left) then
					ui_pict 10, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange1_left", xPictL + 31, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange1_left", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_left) and bSideFlange2_left then
					ui_pict 10, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_left", xPictL + 31, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange2_left", x1, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_left) and not (bSideFlange2_left) then
					if bSideFlange1_right or bSideFlange2_right then
						ui_pict 11, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L3.png
					else
						ui_pict 12, xPictL, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_L4.png
					endif
				endif
			
				if bSideFlange1_right and bSideFlange2_right then
					ui_pict 13, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R1.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_right", xPictR + PictWidth - 27, _yCurr, 90, _infHeight ui_tooltip `Ширина`
					ui_infield "widthSideFlange1_right", xPictR + PictWidth - 120, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10
					ui_infield "heightSideFlange2_right", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`
		
					_yCurr = _yCurr + _dy
					ui_infield "heightSideFlange1_right", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`

				endif
				if bSideFlange1_right and not (bSideFlange2_right) then
					ui_pict 14, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange1_right", xPictR + PictWidth - 120, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange1_right", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_right) and bSideFlange2_right then
					ui_pict 14, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R2.png

					_yCurr = yPict - _infHeight - 3
					ui_infield "widthSideFlange2_right", xPictR + PictWidth - 120, _yCurr, 90, _infHeight ui_tooltip `Ширина`
		
					_yCurr = yPict + 10 + _dy
					ui_infield "heightSideFlange2_right", x2, _yCurr, 90, _infHeight ui_tooltip `Высота`
				endif
				if not (bSideFlange1_right) and not (bSideFlange2_right) then
					if bSideFlange1_left or bSideFlange2_left then
						ui_pict 15, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R3.png
					else
						ui_pict 16, xPictR, yPict, PictWidth, PictHeight, 1	! Ramps_RampProfile_R4.png
					endif
				endif
			endif
		endif
	endif

return

"2DPage":
	pageStartY 	= 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_outfFlag 	= 0
	_dy			= 24
	_widthInf2 = 37

	x1 = 0
	x2 = 87
	x3 = 170
	x4 = 180
	x5 = x1 + 231
	x6 = 444-90
	x7 = x4 + 231

	_yCurr = pageStartY
	ui_style 0, 1
	ui_outfield `2D-отображение`, x1, _yCurr + _yOffset, x3 - 2, _outfHeight
	ui_style 0, 0

	ui_infield{3} "iFloorPlanDisplayType", x3, _yCurr, 444 - x3, _infHeight,
		8, "",
		0, 0,
		0, 0, 0, 0,
		"", `Сечение и Низ`,				1,
		"", `Сечение и Верх`, 	2,
		"", `Символический`, 				3,
		"", `Только Контуры`, 			4,
		"", `Вся Проекция`, 			5 ui_tooltip `Показ на Плане Этажа`

	_yCurr = _yCurr + _dy

	ui_separator x1, _yCurr, 444, _yCurr

	! --- Right Side ---

	_yCurr = pageStartY + _dy + 3

	ui_style 0, 1
	ui_outfield `Текст`, x5, _yCurr + _yOffset + 3, 210 - _widthInf2, _outfHeight
	ui_style 0, 0

	ui_pict_button ui_function, "", "uiFunctionText_flat(1)", 444 - _widthInf2, _yCurr, _widthInf2, 22, -TABID_TEXT ! Set GLOB_UI_BUTTON_ID
	_yCurr = _yCurr + _dy

	ui_separator 225, _yCurr, 444, _yCurr
	_yCurr = _yCurr + 5


	if iFloorPlanDisplayType = 3 then
		ui_style 0, 1

		ui_infield{3} "bShowBreakline", x5, _yCurr, 444 - x5, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Линия Разрыва`, 0,
			"", `Линия Разрыва`, 1

		_yCurr = _yCurr + _dy

		ui_infield{3} "bWalkingLine", x5, _yCurr, 444 - x5 - _widthInf2 - 2, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Линия Всхода`, 0,
			"", `Линия Всхода`, 1

		ui_pict_button ui_function, "", "uiFunctionStairWalklineBreakMark_flat(1)", 444 - _widthInf2, _yCurr - 2, _widthInf2, 22, -TABID_BREAKWALK ! Set GLOB_UI_BUTTON_ID

		_yCurr = _yCurr + _dy
		ui_separator 225, _yCurr, 444, _yCurr

		_yCurr = _yCurr + 5

		ui_infield{3} "bStartSymbol", x5, _yCurr, 444 - x5, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Символ в Начале`, 0,
			"", `Символ в Начале`, 1

		_yCurr = _yCurr + _dy

		ui_infield{3} "bEndSymbol", x5, _yCurr, 444 - x5 - _widthInf2 - 2, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Символ в Конце`, 0,
			"", `Символ в Конце`, 1
		ui_style 0, 0
		ui_pict_button ui_function, "", "uiFunctionStairStartEndSymbol_flat(1)", 444 - _widthInf2, _yCurr - 2, _widthInf2, 22, -TABID_STARTEND ! Set GLOB_UI_BUTTON_ID

	else
		ui_style 0, 1
		ui_outfield `Стрелка`, x5, _yCurr + _yOffset, 444 - x5, _outfHeight
		ui_style 0, 0

		_yCurr = _yCurr + _dy
		ui_outfield `Перо`, x5, _yCurr + _yOffset, 444 - x5 - 35, _outfHeight
		ui_infield "penArrow", x7, _yCurr, 33, _infHeight ui_tooltip `Перо Стрелки` 

		_yCurr = _yCurr + _dy
		ui_outfield `Размер`, x5, _yCurr + _yOffset, 444 - 231 - 92, _outfHeight
		ui_infield "arrowSize", x6, _yCurr, 90, _infHeight ui_tooltip `Размер Стрелки`
	endif

	! --- Left Side ---

	_yCurr = pageStartY + _dy + 6

	ui_style 0, 1
	ui_outfield `Контур`, x1, _yCurr + _yOffset, 212, _outfHeight
	ui_style 0, 0

	_yCurr = _yCurr + _dy

	if iFloorPlanDisplayType = 3 then	! Symbolic
		ui_outfield `Линия Внизу`, x1, _yCurr + 10, x2 - 2, _outfHeight
		ui_infield "lineCont", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Контура`
		ui_infield "gs_cont_pen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Контура`

		_yCurr = _yCurr + _dy + 13

		ui_outfield `Линия Вверху`, x1, _yCurr + 10, x2 - 2, _outfHeight
		ui_infield "lineOverhead", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Штриховой Линии`
		ui_infield "overheadPen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Проекции`

		if iRampType = RAMP_CURVED then
			_yCurr = _yCurr + _dy + 13
	
			ui_outfield `Штриховая Линия`, x1, _yCurr + 10, x2 - 2, _outfHeight
			ui_infield "lineDashed", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Штриховой Линии`
		endif
	else
		ui_outfield `Линия`, x1, _yCurr + 10, x2 - 2, _outfHeight
		
		if iFloorPlanDisplayType = 1 or iFloorPlanDisplayType = 2 then	! Projected, Projected with Overhead
			ui_infield "lineCont", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Контура`
			ui_infield "gs_cont_pen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Контура`

			_yCurr = _yCurr + _dy + 13
	
			ui_outfield `Линия Проекции`, x1, _yCurr + 10, x2 - 2, _outfHeight
			ui_infield "lineOverhead", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Проекции`
			ui_infield "overheadPen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Проекции`

		else	! Outlines Only, Overhead All
			if iFloorPlanDisplayType = 5 then	! Overhead All
				ui_infield "lineOverhead", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Проекции`
				ui_infield "overheadPen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Проекции`
			else
				ui_infield "lineCont", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Контура`
				ui_infield "gs_cont_pen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Контура`
			endif
		endif

		if iRampType = RAMP_CURVED then
			_yCurr = _yCurr + _dy + 13
	
			ui_outfield `Штриховая Линия`, x1, _yCurr + 10, x2 - 2, _outfHeight
			ui_infield "lineDashed", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Штриховой Линии`
		endif

	endif

	_yCurr = _yCurr + _dy + 13
	ui_separator x1, _yCurr, 220, _yCurr

	_yCurr = _yCurr + 5
	ui_infield "gs_fill_type", x2, _yCurr, 126, _infHeight + 6 ui_tooltip `Тип Штриховки`

	ui_style 0, 1
	ui_infield{3} "bFill", x1, _yCurr + 3, x2 - 2, _infHeight,
		7, "",
		2, 0,
		0, 0, 0, 0,
		"", `Штриховка`, 0,
		"", `Штриховка`, 1
	ui_style 0, 0

	_yCurr = _yCurr + _dy + 6
	if iFloorPlanDisplayType = 5 or not(bFill) then _outfFlag = 4
	ui_outfield `Перо`, x1, _yCurr + _yOffset, x4 - 2, _outfHeight, _outfFlag
	ui_infield "gs_fill_pen", x4, _yCurr, 33, _infHeight ui_tooltip `Перо Штриховки`

	_yCurr = _yCurr + _dy

	ui_outfield `Перо Фона`, x1, _yCurr + _yOffset, x4 - 2, _outfHeight, _outfFlag
	ui_infield "gs_back_pen", x4, _yCurr, 33, _infHeight ui_tooltip `Перо Фона Штриховки`

	_outfFlag = 0

	_yCurr = _yCurr + _dy + 3

	ui_separator 222, _yCurr, 222, pageStartY + _dy + 5

return

"BreakWalkingPage":
	if iFloorPlanDisplayType = 3 then	! Symbolic
		pageStartY 	= 0
		_infHeight 	= 19
		_outfHeight = 15
		_yOffset 	= 4
		_outfFlag 	= 0
		_dy			= 24
	
		x1 = 0
		x2 = 87
		x3 = 170
		x4 = 180
		x5 = x1 + 231
		x6 = x2 + 231
		x7 = x4 + 231

		! --- Right Side ---
	
		_yCurr = pageStartY
	
		ui_style 0, 1
		ui_infield{3} "bWalkingLine", x5, _yCurr, 444 - x5, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Линия Всхода`, 0,
			"", `Линия Всхода`, 1
		ui_style 0, 0

		_yCurr = _yCurr + _dy

		if iRampType = RAMP_STRAIGHT then
			ui_infield{3} "iWalkingLineType", x5 + 78, _yCurr, 135, 48,
				2, 17, 2, 1,	! Ramps_WalkingLine.png
				80, 40, 80, 40,
				1, ``,	1,
				2, ``,	2 ui_tooltip `Тип Линии Всхода`
	
			_yCurr = _yCurr + _dy + 29
		endif

		ui_infield{3} "bReverse", x5, _yCurr, 444 - x5, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Обратное Направление Символа`, 0,
			"", `Обратное Направление Символа`, 1

		_yCurr = _yCurr + _dy

		ui_infield{3} "bStop", x5, _yCurr, 444 - x5, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Не Продлевать за Линию Разрыва`, 0,
			"", `Не Продлевать за Линию Разрыва`, 1

		_yCurr = _yCurr + _dy

		if not(bWalkingLine) then _outfFlag = 4

		ui_outfield `Линия Внизу`, x5, _yCurr + 10, x2 - 2, _outfHeight, _outfFlag
		ui_infield "ltWalkingBelow", x6, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Основной Линии Ниже Разрыва`
		ui_infield "penWalkingBelow", x7, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Основной Линии Ниже Разрыва`

		_yCurr = _yCurr + _dy + 13

		if not(bShowBreakline) then _outfFlag = 4

		ui_outfield `Линия Вверху`, x5, _yCurr + 10, x2 - 2, _outfHeight, _outfFlag
		ui_infield "ltWalkingAbove", x6, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Основной Линии Выше Разрыва`
		ui_infield "penWalkingAbove", x7, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Основной Линии Выше Разрыва`

		_outfFlag = 0
	
		! --- Left Side ---
		_yCurr = pageStartY
	
		ui_style 0, 1
		ui_infield{3} "bShowBreakline", x1, _yCurr, 212, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Линия Разрыва`, 0,
			"", `Линия Разрыва`, 1
		ui_style 0, 0
	
		_yCurr = _yCurr + _dy
		
		if not(bShowBreakline) then _outfFlag = 4
	
		ui_outfield `Линия Внизу`, x1, _yCurr + 10, x2 - 2, _outfHeight, _outfFlag
		ui_infield "ltBreakLineBelow", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Разрыва Ниже Разрыва`
		ui_infield "penBreakLineBelow", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Линии Разрыва Ниже Разрыва`
	
		_yCurr = _yCurr + _dy + 13
	
		ui_outfield `Линия Вверху`, x1, _yCurr + 10, x2 - 2, _outfHeight, _outfFlag
		ui_infield "ltBreakLineAbove", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Разрыва Выше Разрыва`
		ui_infield "penBreakLineAbove", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Линии Разрыва Выше Разрыва`
	
		_outfFlag = 0

		_yCurr = _yCurr + _dy + 13
		ui_separator x1, _yCurr, 212, _yCurr
	
		_yCurr = _yCurr + 5
	
		ui_style 0, 1
		ui_outfield `Контур`, x1, _yCurr + _yOffset, 212, _outfHeight
		ui_style 0, 0
	
		_yCurr = _yCurr + _dy
	
		ui_outfield `Линия Внизу`, x1, _yCurr + 10, x2 - 2, _outfHeight
		ui_infield "lineCont", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Линии Контура`
		ui_infield "gs_cont_pen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Контура`
	
		_yCurr = _yCurr + _dy + 13
	
		if not(bShowBreakline) then _outfFlag = 4

		ui_outfield `Линия Вверху`, x1, _yCurr + 10, x2 - 2, _outfHeight, _outfFlag
		ui_infield "lineDashed", x2, _yCurr, 90, _infHeight + 13 ui_tooltip `Тип Штриховой Линии`
		ui_infield "overheadPen", x4, _yCurr + 13, 33, _infHeight ui_tooltip `Перо Проекции`

		_outfFlag = 0
		_yCurr = _yCurr + _dy + 13
 
		ui_separator 222, 0, 222, _yCurr
	else
		ui_pict "ui_InfoIcon_flat(1)", 5, 11, 14, 14, 1

		ui_outfield `Для настройки специального Разрыва и Основной Линии выберите Символическое 2D-отображение на вкладке 2D-отображения.`, 24, 5, 420, 27
	endif

return
	
"StartEndPage":
	if iFloorPlanDisplayType = 3 then	! Symbolic
		pageStartY 	= 0
		_infHeight 	= 19
		_outfHeight = 15
		_yOffset 	= 4
		_outfFlag 	= 0
		_dy			= 24
	
		x1 = 0
		x2 = 78
		x3 = 122
		x4 = 179
		x5 = x1 + 231
		x6 = x2 + 231
		x7 = x3 + 231
		x8 = x4 + 231

		! --- Right Side ---
	
		_yCurr = pageStartY
	
		ui_style 0, 1
		ui_infield{3} "bStartSymbol", x1, _yCurr, 212, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Символ в Начале`, 0,
			"", `Символ в Начале`, 1
		ui_style 0, 0

		_yCurr = _yCurr + _dy

		ui_infield{3} "iStartSymbol", x2, _yCurr, 135, 48,
			2, 18, 3, 3,	! ui_RampStartSymb.png
			80, 40, 80, 40,
			1, ``,	1,
			2, ``,	2,
			3, ``,	3 ui_tooltip `Тип Начала Основной Линии`

		_yCurr = _yCurr + _dy + 29

		ui_infield{3} "bShowAboveBreak", x1, _yCurr, 212, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Показать Выше Линии Разрыва`, 0,
			"", `Показать Выше Линии Разрыва`, 1

		_yCurr = _yCurr + _dy

		if not(bStartSymbol) or not(bShowAboveBreak) or bStop or iStartSymbol = 3 then _outfFlag = 4

		ui_outfield `Перо Штриховки Над Разрывом`, x1, _yCurr + _yOffset, x4 - 2, _outfHeight, _outfFlag
		ui_infield "penStartSymbolAbove", x4, _yCurr, 33, _infHeight ui_tooltip `Перо Начала Основной Линии Выше Разрыва`
		_outfFlag = 0
		_yCurr = _yCurr + _dy

		if not(bStartSymbol) or iStartSymbol = 3 then _outfFlag = 4

		ui_outfield `Перо Штриховки Под Разрывом`, x1, _yCurr + _yOffset, x4 - 2, _outfHeight, _outfFlag
		ui_infield "penStartSymbolBelow", x4, _yCurr, 33, _infHeight ui_tooltip `Перо Начала Основной Линии Ниже Разрыва`
		_outfFlag = 0

		if not(bStartSymbol) then _outfFlag = 4

		_yCurr = _yCurr + _dy
		ui_outfield `Ширина`, x1, _yCurr + _yOffset, x3 - 2, _outfHeight, _outfFlag
		ui_infield "widthStartSymbol", x3, _yCurr, 90, _infHeight ui_tooltip `Ширина Начала Основной Линии`

		_yCurr = _yCurr + _dy
	
		if iStartSymbol <> 3 then _outfFlag = 4

		ui_outfield `Высота`, x1, _yCurr + _yOffset, x3 - 2, _outfHeight, _outfFlag
		ui_infield "heightStartSymbol", x3, _yCurr, 90, _infHeight ui_tooltip `Высота Начала Основной Линии`

		_outfFlag = 0

		! --- Left Side ---
	
		_yCurr = pageStartY
	
		ui_style 0, 1
		ui_infield{3} "bEndSymbol", x5, _yCurr, 212, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Символ в Конце`, 0,
			"", `Символ в Конце`, 1
		ui_style 0, 0

		_yCurr = _yCurr + _dy

		ui_infield{3} "iEndSymbol", x6, _yCurr, 135, 48,
			2, 19, 6, 3,	! ui_RampEndSymb.png
			80, 40, 80, 40,
			1, ``,	1,
			2, ``,	2,
			3, ``,	3,
			4, ``,	4,
			5, ``,	5,
			6, ``,	6 ui_tooltip `Тип Конца Основной Линии`

		_yCurr = _yCurr + _dy * 2 + 29

		if not(bEndSymbol) or bStop  or iEndSymbol = 1 or iEndSymbol = 2 then _outfFlag = 4

		ui_outfield `Перо Штриховки Над Разрывом`, x5, _yCurr + _yOffset, x4 - 2, _outfHeight, _outfFlag
		ui_infield "penEndSymbolAbove", x8, _yCurr, 33, _infHeight ui_tooltip `Перо Конца Основной Линии Выше Разрыва`
	
		_outfFlag = 0
		if not(bEndSymbol) or iEndSymbol = 1 or iEndSymbol = 2 then _outfFlag = 4

		_yCurr = _yCurr + _dy
	
		ui_outfield `Перо Штриховки Под Разрывом`, x5, _yCurr + _yOffset, x4 - 2, _outfHeight, _outfFlag
		ui_infield "penEndSymbolBelow", x8, _yCurr, 33, _infHeight ui_tooltip `Перо Конца Основной Линии Ниже Разрыва`

		_yCurr = _yCurr + _dy

		_outfFlag = 0
		if iEndSymbol = 1 or iEndSymbol = 3 or iEndSymbol = 5 then _outfFlag = 4

		ui_outfield `Ширина`, x5, _yCurr + _yOffset, x3 - 2, _outfHeight, _outfFlag
		ui_infield "arrowWidth", x7, _yCurr, 90, _infHeight ui_tooltip `Ширина Конца Основной Линии`

		_outfFlag = 0
		if not(bEndSymbol) then _outfFlag = 4

		_yCurr = _yCurr + _dy
	
		ui_outfield `Высота`, x5, _yCurr + _yOffset, x3 - 2, _outfHeight, _outfFlag
		ui_infield "arrowHeight", x7, _yCurr, 90, _infHeight ui_tooltip `Высота Конца Основной Линии`

		_yCurr = _yCurr + _dy

		ui_separator 222, pageStartY, 222, _yCurr
	else
		ui_pict "ui_InfoIcon_flat(1)", 5, 11, 14, 14, 1

		ui_outfield `Для настройки специального Разрыва и Основной Линии выберите Символическое 2D-отображение на вкладке 2D-отображения.`, 24, 5, 420, 27
	endif
return

"TextPage":
	pageStartY 	= 0
	_infHeight 	= 19
	_outfHeight = 15
	_yOffset 	= 4
	_outfFlag 	= 0
	_dy			= 24

	x1 = 0
	x2 = 14
	x3 = 170
	x4 = 273
	
	_yCurr = pageStartY

	if iFloorPlanDisplayType = 3 then	! Symbolic

		ui_style 0, 1
		ui_infield{3} "bShowText1", x1, _yCurr, x3 - 2, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Текст 1`, 0,
			"", `Текст 1`, 1
		ui_style 0, 0
	
		ui_infield{3} "iTextType1", x3, _yCurr, 200, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Проценты`,		1,
			"", `Соотношение`,			2,
			"", `Длина Сегмента`, 	3,
			"", `Полная Длина`, 		4,
			"", `Специальный`, 			0 ui_tooltip `Тип Текста 1`
	
		_yCurr = _yCurr + _dy
	
		if not(bShowText1) then _outfFlag = 4
		ui_outfield `Префикс`, x2, _yCurr + _yOffset, x3 - x2 - 2, _outfHeight, _outfFlag
		_outfFlag = 0

		ui_infield "TextPrefix1", x3, _yCurr, 97, _infHeight ui_tooltip `Префикс 1`
	
		if iTextType1 = 0 then	! Custom
			ui_infield "CustomText", x4, _yCurr, 97, _infHeight ui_tooltip `Специальный Текст`
		endif
	
		_yCurr = _yCurr + _dy
	
		ui_style 0, 1
		ui_infield{3} "bShowText2", x1, _yCurr, x3 - 2, _infHeight,
			7, "",
			2, 0,
			0, 0, 0, 0,
			"", `Текст 2`, 0,
			"", `Текст 2`, 1
		ui_style 0, 0
	
		ui_infield{3} "iTextType2", x3, _yCurr, 200, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Проценты`,		1,
			"", `Соотношение`,			2,
			"", `Длина Сегмента`, 	3,
			"", `Полная Длина`, 		4,
			"", `Специальный`, 			0 ui_tooltip `Тип Текста 2`
	
		_yCurr = _yCurr + _dy

		if not(bShowText2) then _outfFlag = 4
		ui_outfield `Префикс`, x2, _yCurr + _yOffset, x3 - x2 - 2, _outfHeight, _outfFlag
		_outfFlag = 0

		ui_infield "TextPrefix2", x3, _yCurr, 97, _infHeight ui_tooltip `Префикс 2`
	
		if iTextType2 = 0 then	! Custom
			ui_infield "CustomText", x4, _yCurr, 97, _infHeight ui_tooltip `Специальный Текст`
		endif

		if not(bShowText1) and not(bShowText2) then _outfFlag = 4

		_yCurr = _yCurr + _dy
	else
		if iFloorPlanDisplayType = 5 then	! Overhead All
			_outfFlag = 4

			ui_pict "ui_InfoIcon_flat(1)", 0, pageStartY , 14, 14, 1	
			ui_outfield `Для настройки специального Текста выберите Символическое 2D-отображение на вкладке 2D-отображения.`, 16, pageStartY , 444 - 16, _outfHeight * 2
		else
		
			ui_style 0, 1
			ui_outfield `Текст`, x1, _yCurr + _yOffset, x3 - 2, _outfHeight, _outfFlag
			ui_style 0, 0
	
			ui_outfield `Проценты`, x3, _yCurr + _yOffset, 200, _outfHeight, _outfFlag
	
			ui_pict "ui_InfoIcon_flat(1)", 5, 238 , 14, 14, 1	
			ui_outfield `Для настройки специального Текста выберите Символическое 2D-отображение на вкладке 2D-отображения.`, 24, 233, 420, 27

			_yCurr = _yCurr + _dy
		endif
	endif

	if iFloorPlanDisplayType <> 5 then
		ui_outfield `Тип Шрифта`, x1, _yCurr + _yOffset, x3 - 2, _outfHeight, _outfFlag
		ui_infield "fontType", x3, _yCurr, 200, _infHeight ui_tooltip `Тип Шрифта`
	
		_yCurr = _yCurr + _dy	
		ui_outfield `Размер Шрифта`, x1, _yCurr + _yOffset, x3 - 2,	_outfHeight, _outfFlag
		ui_infield "tx_size", x3, _yCurr, x5-x3, _infHeight ui_tooltip `Размер Шрифта (мм)`
	
		_yCurr = _yCurr + _dy
		ui_outfield `Цвет Пера`, x1, _yCurr + _yOffset, x3 - 2,	_outfHeight, _outfFlag
		ui_infield "penText", x3, _yCurr, 33, _infHeight ui_tooltip `Перо Текста` 
	
		_yCurr = _yCurr + _dy	
		ui_outfield `Ориентация в Символе`, x1, _yCurr + _yOffset, x3 - 2,	_outfHeight, _outfFlag
		ui_infield{3} "typeTextRotation_m", x3,	_yCurr,	200, _infHeight,
			8, "",
			0, 0, 0, 0, 0, 0,
			0, `Читаемый`,			2,
			0, `Выровнять по Символу`,	0 ui_tooltip `Ориентация Текста в Символе`
	endif		
	_outfFlag = 0
return

"3DPage":
	_outfFlag = 0

	dim surfacesArray[] 
 	SurfItemID = 1   
		surfacesArray[SurfItemID]	= "matRamp"
		SurfItemID = SurfItemID + 1

		if not (bDiffSides) then
			if bSideFlange1 then
				surfacesArray[SurfItemID]	= "matSideFlange1"
				SurfItemID = SurfItemID + 1
			endif
		else	
			if iRampType = RAMP_CURVED then
				if bSideFlange1_in then
					surfacesArray[SurfItemID]	= "matSideFlange1_in"
					SurfItemID = SurfItemID + 1
				endif

				if bSideFlange1_out then
					surfacesArray[SurfItemID]	= "matSideFlange1_out"
					SurfItemID = SurfItemID + 1
				endif
			else
				if bSideFlange1_left then
					surfacesArray[SurfItemID]	= "matSideFlange1_left"
					SurfItemID = SurfItemID + 1
				endif
				if bSideFlange1_right then
					surfacesArray[SurfItemID]	= "matSideFlange1_right"
					SurfItemID = SurfItemID + 1
				endif
			endif
		endif

		if not (bDiffSides) then
			if bSideFlange2 then
				surfacesArray[SurfItemID]	= "matSideFlange2"
				SurfItemID = SurfItemID + 1
			endif
		else
			if iRampType = RAMP_CURVED then
				if bSideFlange2_in then
					surfacesArray[SurfItemID]	= "matSideFlange2_in"
					SurfItemID = SurfItemID + 1
				endif
				if bSideFlange2_out then
					surfacesArray[SurfItemID]	= "matSideFlange2_out"
					SurfItemID = SurfItemID + 1
				endif
			else
				if bSideFlange2_left then
					surfacesArray[SurfItemID]	= "matSideFlange2_left"
					SurfItemID = SurfItemID + 1
				endif
				if bSideFlange2_right then
					surfacesArray[SurfItemID]	= "matSideFlange2_right"
					SurfItemID = SurfItemID + 1
				endif
			endif
		endif

	dim childFlagArray[]
	for k = 1 to vardim1(surfacesArray)
		childFlagArray[k]	= 0
	next k

	if iRampType = RAMP_CURVED then
		bShowResol = 1
	else
		bShowResol = 0
	endif
	
	call "ui_object_3DRepresentation" parameters 	bShowAnimation		= 0,
													surfacesArray		= surfacesArray,
													childFlagArray		= childFlagArray,
													gs_detlevel_3D_m	= gs_detlevel_3d_m,
													bShowResol			= bShowResol,
													gs_shadow			= gs_shadow,
													iSurfaceListID		= TABID_3D

return

"descriptionPage":

	dim stParamNames[16]
		stParamNames[1] = "gs_list_cost"
		stParamNames[2] = "gs_list_manufacturer"
		stParamNames[3] = "gs_list_note"
		stParamNames[4] = "gs_list_location"
		stParamNames[5] = "gs_list_accessories"
		stParamNames[6] = "FM_Type"
		stParamNames[7] = "FM_InventoryNumber"
		stParamNames[8] = "FM_SerialNumber"
		stParamNames[9] = "FM_ProductionYear"
		stParamNames[10] = "FM_ObjectWeight"
		stParamNames[11] = "FM_ObjectWeightUnit"
		stParamNames[12] = "gs_list_custom1"
		stParamNames[13] = "gs_list_custom2"
		stParamNames[14] = "gs_list_custom3"
		stParamNames[15] = "gs_list_custom4"
		stParamNames[16] = "gs_list_custom5"

	call "ui_object_listing" parameters all listfieldID 	= TABID_DESCRIPTIONS,
											listFieldPosY 	= 0,
											listFieldHeight = 265,
											stDescHeader 	= "",
											stValueHeader 	= "",
											stParamNames 	= stParamNames
return
