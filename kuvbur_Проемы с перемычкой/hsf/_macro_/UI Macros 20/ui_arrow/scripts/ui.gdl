
if auto_type and pixel<8 then
	type = 2
	if dir then
		x=x-1
	else
		y=y-1
	endif
	num_pos = sec_pos
	pixel = pixel+2
endif

bArrowsLookingIn = (type=2 or type=4)

! --- Line and Arrow -----------------------------------------------------------

if bArrowsLookingIn then
	endOffset = 0
else
	endOffset = 3
endif

if dir then			! horizontal
	ui_pict 20, x+endOffset,y,pixel-2*endOffset,1	! black dot
	if bArrowsLookingIn then
		ui_pict 20, x-5,       y, 2,1	! black dot
		ui_pict 20, x+pixel+3, y, 2,1	! black dot
		startArrowIcon = 13		! right
		endArrowIcon = 12		! left
	else
		startArrowIcon = 12		! left
		endArrowIcon = 13		! right
	endif
	ui_pict startArrowIcon, x-3+endOffset,     y-2, 3, 5, 1
	ui_pict endArrowIcon,   x+pixel-endOffset, y-2, 3, 5, 1
else				! vertical
	ui_pict 20, x,y+endOffset,1,pixel-2*endOffset	! black dot
	if bArrowsLookingIn then
		ui_pict 20, x,y-2,       1,2	! black dot
		ui_pict 20, x,y+pixel+3, 1,2	! black dot
		startArrowIcon = 11		! down
		endArrowIcon = 14		! up
	else
		startArrowIcon = 14		! up
		endArrowIcon = 11		! down
	endif
	ui_pict startArrowIcon, x-2, y-3+endOffset,     5, 3, 1
	ui_pict endArrowIcon,   x-2, y+pixel-endOffset, 5, 3, 1
endif

! --- Number -------------------------------------------------------------------

if num_pos < 1 or num < 1 or num > 9 then end

if not(dir) then	! vertical
	xPos = x-10 + ((num_pos-1) mod 3) * 7
	yPos = y-12 + int((num_pos-1) / 3) * (pixel/2 + 7)
	if bArrowsLookingIn then
		if num_pos = 2 then yPos = yPos-2
		if num_pos = 8 then yPos = yPos+2
	endif
else				! horizontal
	xPos = x-10 + ((num_pos-1) mod 3) * (pixel/2 + 7)
	yPos = y-12 + int((num_pos-1) / 3) * 7
	if bArrowsLookingIn then
		if num_pos = 4 then xPos = xPos-2
		if num_pos = 6 then xPos = xPos+2
	endif
endif

ui_pict num, xPos, yPos, 7, 9, 1
