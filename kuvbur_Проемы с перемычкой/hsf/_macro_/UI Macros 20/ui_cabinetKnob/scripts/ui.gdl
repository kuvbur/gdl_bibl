
ITEMCLASS_OTHER			 	= 0	! Cupboard, Wall Cabinet, Wardrobe, Wardrobe Modular
ITEMCLASS_OFFICE_CABINET 	= 1
ITEMCLASS_WARDROBE_VARIABLE = 2
ItemClass_Credenza 			= 3
ITEMCLASS_UNDERDESK_DRAWER 	= 4
ITEMCLASS_PLAN_DRAWER 		= 5
ITEMCLASS_OVERHEAD_CABINET 	= 6
ITEMCLASS_NIGHT_TABLE	 	= 7

KNOBCLASS_DOOR_DRAWER 	= 1
KNOBCLASS_DOOR 			= 2
KNOBCLASS_DRAWER 		= 3

_infHeight = 19
_outfHeight = 15
_yOffset = 4
x1 = 0
x2 = 76
x3 = 123
dy = 24
_yCurr = pageStartY
_outfFlag1 = 0
_outfFlag2 = 0
_outfFlag1Size = 0
_outfFlag2Size = 0
_outfFlag3Size = 0

if iKnobClass = KNOBCLASS_DOOR_DRAWER then
	_actMatKnobDoor = "gs_knob_mat"
	_actKnobPlacing = "gs_knob_p"
	
	_stPlacing1 = "Vertical"
	_stPlacing2 = "Horizontal"
	
	if iItemClass = ITEMCLASS_OFFICE_CABINET then
		_actbKnob1 			= "bKnob_up"
		_actbKnob2 			= "bKnob"
		_actKnobPosXDoor 	= "knob_pl_x"
		_actKnobPosYDoor 	= "knob_pl_y"
		_actKnobPosXDrawer	= "xDrawerKnobPos"
		_actKnobPosYDrawer 	= "zDrawerKnobPos"
		_actKnobNumDrawer 	= "nDrawerKnob"
		_actKnobTypeDoor 	= "gs_knob_type_m_up"
		_actKnobTypeDrawer 	= "gs_knob_type_m"
		_actKnobSizeDoor 	= "knob_size"
		_actKnobSizeDrawer 	= "drawerKnobSize"
		_actMatKnobDrawer 	= "matDrawerKnob"
		
		if iCabinetScheme = 1 then	! Drawer and Cupboard
			_stKnob1 = `Ручки Дверей`
			_stKnob2 = `Ручки Ящиков`
		else						! Drawer or Cupboard
			_stKnob1 = `Ручка`
			_stKnob2 = `Ручка`
		endif

		if iCabinetScheme = 5 then
			_outfFlag1 = 4
			_outfFlag2 = 4
		endif
		if not(bKnob_up) then _outfFlag1 = 4
		if not(bKnob) then _outfFlag2 = 4
		if gs_knob_type_m_up < 4 or not(bKnob_up) then _outfFlag1Size = 4
		if gs_knob_type_m < 4 or not(bKnob) then _outfFlag2Size = 4
				if gs_knob_type_m_up < 4 or not(bKnob_up) then
			_outfFlag3Size = 4
		endif

		if iCabinetScheme = 1 or iCabinetScheme = 5 then
			gosub "DoorandDrawer"
		endif
		if iCabinetScheme = 2 or iCabinetScheme = 3 then
			gosub "Drawer"
		endif
		if iCabinetScheme = 4 then
			gosub "Door"
		endif

	endif
	if iItemClass = ITEMCLASS_WARDROBE_VARIABLE then
	
		_actbKnob1 = "bKnobAbove"
		_actbKnob2 = "bKnob"
		if iFrontAbove = 1 then		! Drawer
			_actKnobPosXDoor = "xDrawerKnobPosAbove"
			_actKnobPosYDoor = "zDrawerKnobPosAbove"

			if iFrontUnder <> 1 then
				_stKnob1 = `Ручки Ящиков`
			else
				_stKnob1 = `Ручки Верхних Ящиков`
			endif
		else						! Door
			_actKnobPosXDoor = "xKnobPozAbove"
			_actKnobPosYDoor = "yKnobPozAbove"

			if iFrontUnder <> 3 then
				_stKnob1 = `Ручки Дверей`
			else
				_stKnob1 = `Ручки Верхних Дверей`
			endif
		endif

		if iFrontUnder = 1 then 	! Drawer
			_actKnobPosXDrawer = "xDrawerKnobPos"
			_actKnobPosYDrawer = "zDrawerKnobPos"

			if iFrontAbove <> 1 then
				_stKnob2 = `Ручки Ящиков`
			else
				_stKnob2 = `Ручки Нижних Ящиков`
			endif
		else						! Door
			_actKnobPosXDrawer = "knob_pl_x"
			_actKnobPosYDrawer = "knob_pl_y"

			if iFrontAbove <> 3 then
				_stKnob2 = `Ручки Дверей`
			else
				_stKnob2 = `Ручки Нижних Дверей`
			endif
		endif
		_actKnobNumDoor 	= "nKnobDrawersAbove"
		_actKnobNumDrawer 	= "nKnobDrawers"
		_actKnobNumDoor2 	= "nKnobDrawers"
		_actKnobNumDrawer2 	= "nKnobDrawersAbove"
		_actKnobTypeDrawer 	= "gs_knob_type_m"
		_actKnobTypeDoor 	= "iKnobTypeAbove"
		_actKnobSizeDoor 	= "knobSizeAbove"
		_actKnobSizeDrawer 	= "knob_size"
		_actMatKnobDrawer 	= "gs_knob_mat"
		_actMatKnobDoor 	= "matKnobAbove"

		if not(bKnobAbove) then _outfFlag1 = 4
		if not(bKnob) then _outfFlag2 = 4
		if iKnobTypeAbove < 4 or not(bKnobAbove) then _outfFlag1Size = 4
		if gs_knob_type_m < 4 or not(bKnob) then _outfFlag2Size = 4

		if iFrontUnder = 3 and iFrontAbove = 3 then
			if gs_knob_type_m < 4 and iKnobTypeAbove < 4 or not(bKnob) or not(bKnobAbove) then
				_outfFlag3Size = 4
			endif
		endif
		if iFrontUnder = 3 and iFrontAbove <> 3 then
			if gs_knob_type_m < 4 or not(bKnob) then
				_outfFlag3Size = 4
			endif
		endif
		if iFrontAbove = 3 and iFrontUnder <> 3 then
			if iKnobTypeAbove  < 4 or not(bKnobAbove) then
				_outfFlag3Size = 4
			endif
		endif

		if (iFrontAbove = 1 or iFrontAbove = 3) and	(iFrontUnder = 0 or iFrontUnder = 2) then gosub "Door"
		if (iFrontUnder = 1 or iFrontUnder = 3) and	(iFrontAbove = 0 or iFrontAbove = 2) then gosub "Drawer"
		if (iFrontAbove = 1 or iFrontAbove = 3) and (iFrontUnder = 1 or iFrontUnder = 3) then

		gosub "DoorandDrawer"
		endif
		if (iFrontAbove <> 1 and iFrontAbove <> 3) and (iFrontUnder <> 1 and iFrontUnder <> 3) then 
			_outfFlag1 = 4
			_outfFlag2 = 4
			_outfFlag1Size = 4
			_outfFlag2Size = 4
			gosub "DoorandDrawer"
		endif
	endif
	if iItemClass = ItemClass_Credenza then
	
		_actbKnob1 			= "bDoorKnob"
		_actbKnob2 			= "bDrawerKnob"
		_actKnobPosXDoor 	= "knob_pl_x_up"
		_actKnobPosYDoor 	= "knob_pl_y_up"
		_actKnobPosXDrawer 	= "xDrawerKnobPos"
		_actKnobPosYDrawer 	= "zDrawerKnobPos"
		_actKnobTypeDoor 	= "gs_knob_type_m"
		_actKnobTypeDrawer 	= "iDrawerKnobType"
		_actKnobSizeDoor 	= "knob_size"
		_actKnobSizeDrawer 	= "drawerKnobSize"
		_actMatKnobDrawer 	= "matDrawerKnob"
		_actKnobNumDoor2 	= "iNumDrawerKnob"

		if iStyle = 1 then				! Cabinet Style = with Doors
			_stKnob1 = `Ручки Дверей`
			_stKnob2 = `Ручки Ящиков`
		else							! Cabinet Style = Multi Drawer
			_actKnobNumDrawer2 = "iNumDoorKnob"
			_stKnob1 = `Ручки Нижних Ящиков`
			_stKnob2 = `Ручки Верхних Ящиков`
		endif

		if not(bDoorKnob) then _outfFlag1 = 4
		if not(bDrawerKnob) then _outfFlag2 = 4
		if gs_knob_type_m < 4 or not(bDoorKnob) then _outfFlag1Size = 4
		if iDrawerKnobType < 4 or not(bDrawerKnob) then _outfFlag2Size = 4
		if gs_knob_type_m < 4 or not(bDoorKnob) then
			_outfFlag3Size = 4
		endif

		gosub "DoorandDrawer"
	endif
else	! iKnobClass = KNOBCLASS_DOOR and iKnobClass = KNOBCLASS_DRAWER

	if iKnobClass = KNOBCLASS_DOOR then
		if not(bKnob) then 
			_outfFlag1 = 4
			_outfFlag2 = 4
		endif
		if gs_knob_type_m < 4 or not(bKnob) then
			_outfFlag1Size = 4
			_outfFlag2Size = 4
		endif

		_actMatKnobDoor 	= "gs_knob_mat"
		_actbKnob1 			= "bKnob"
		_actKnobTypeDoor 	= "gs_knob_type_m"
		_actKnobPosXDoor 	= "knob_pl_x"
		_actKnobPosYDoor 	= "knob_pl_y"
		_actKnobSizeDoor 	= "knob_size"

		if iItemClass = ITEMCLASS_OVERHEAD_CABINET then
			_actKnobPlacing = "gs_knob_p_m"

			_stPlacing1 = 0
			_stPlacing2 = 1
		else	! Cupboard, Wall Cabinet, Wardrobe, Wardrobe Variable
			_actKnobPlacing = "gs_knob_p"

			_stPlacing1 = "Vertical"
			_stPlacing2 = "Horizontal"
		endif		
		_stKnob1 = `Ручка`

		if gs_knob_type_m < 4 or not(bKnob) then
			_outfFlag3Size = 4
		endif
		
		gosub "Door"
	else	! iKnobClass = KNOBCLASS_DRAWER
		if not(bKnob) or not(bDrawer) then 
			_outfFlag1 = 4
			_outfFlag2 = 4
		endif
		if gs_knob_type_m < 4 or not(bKnob) or not(bDrawer)then
			_outfFlag1Size = 4
			_outfFlag2Size = 4
		endif

		_actMatKnobDrawer = "gs_knob_mat"
		_actbKnob2 = "bKnob"
		_actKnobTypeDrawer = "gs_knob_type_m"
		if iItemClass = ITEMCLASS_UNDERDESK_DRAWER then
			_actKnobPosXDrawer = "gs_knobHorPos"
			_actKnobPosYDrawer = "gs_knobVerPos"
		else	! Plan Drawer, 
			_actKnobPosXDrawer = "knob_pl_x"
			_actKnobPosYDrawer = "knob_pl_y"
		endif
		if iItemClass = ITEMCLASS_PLAN_DRAWER or iItemClass = ITEMCLASS_NIGHT_TABLE then
			_actKnobNumDrawer 	= "nDrawerKnob"
			_actKnobSizeDrawer 	= "knob_size"
		else	! Underdesk Drawer
			_actKnobNumDrawer 	= "iKnobNum"
			_actKnobSizeDrawer 	= "drawerKnobSize"
		endif
		
		_stKnob2 = `Ручка`
		
		gosub "Drawer"
	endif
endif
end

"Door":
	x0 = 0
	x1 = 0 + 230
	x2 = 76
	x3 = 123 + 230

	_yCurr = _yCurr + _yOffset
	ui_style 0, 1
	ui_infield{4} _actbKnob1, x0, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", _stKnob1, 0,
					"", _stKnob1, 1
	ui_style 0, 0
	
	_yCurr = _yCurr + _infHeight + 2
	ui_infield{4} _actKnobTypeDoor, x2, _yCurr, 137, 90,
		2, 1, 12, 3,
		120, 90, 120, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,	4,
		5, ``,	5,
		6, ``,	6,
		7, ``,	7,
		8, ``,	8 ui_tooltip `Стиль Ручки`
	
	ui_infield{4} _actKnobPosXDoor, x3, _yCurr, 90, _infHeight

	_yCurr = _yCurr + _yOffset
	ui_outfield `По Горизонтали`, x1, _yCurr, 120, _outfHeight, _outfFlag1

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{4} _actKnobPosYDoor, x3, _yCurr, 90, _infHeight ui_tooltip `Привязка по Вертик.`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Привязка по Вертик.`, x1, _yCurr, 120, _outfHeight, _outfFlag1

	_yCurr = _yCurr + dy - _yOffset
	ui_infield _actKnobSizeDoor, x3, _yCurr, 90, _infHeight ui_tooltip `Размер`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Размер`, x1, _yCurr, 120, _outfHeight, _outfFlag1Size

	
	if iKnobClass = KNOBCLASS_DOOR_DRAWER and iFrontAbove = 1 then
		_yCurr = _yCurr + dy - _yOffset
		ui_infield{4} _actKnobNumDrawer2, x3 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Ручек на Ящик`
	
		_yCurr = _yCurr + _yOffset
		ui_outfield `Количество Ручек на Ящик`, x1, _yCurr, 155, _outfHeight, _outfFlag1
	else
		_yCurr = _yCurr + dy - _yOffset
		ui_infield{4} _actKnobPlacing, x3, _yCurr, 90, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", `Вертикальный`, _stPlacing1,
				"", `Горизонтальный`, _stPlacing2 ui_tooltip `Расположение`
	
		_yCurr = _yCurr + _yOffset
		ui_outfield `Расположение`, x1, _yCurr, 120, _outfHeight, _outfFlag3Size
	endif	

	_yCurr = _yCurr + 38
	ui_infield{4} _actMatKnobDoor, x1, _yCurr, 213, 25 ui_tooltip `Ручка`
return

"Drawer":
	x0 = 0
	x1 = 0 + 230
	x2 = 76
	x3 = 123 + 230

	_yCurr = pageStartY  + _yOffset	
	ui_style 0, 1
	ui_infield{4} _actbKnob2, x0, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", _stKnob2, 0,
					"", _stKnob2, 1
	ui_style 0, 0

	_yCurr = _yCurr + _infHeight + 7	
	ui_infield{4} _actKnobTypeDrawer, x2, _yCurr, 137, 90,
		2, 1, 12, 3,
		120, 90, 120, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,	4,
		5, ``,	5,
		6, ``,	6,
		7, ``,	7,
		8, ``,	8 ui_tooltip `Стиль Ручки`

	if iItemClass <> ITEMCLASS_NIGHT_TABLE then
		if iKnobClass = KNOBCLASS_DOOR_DRAWER and iFrontUnder = 3 then
			ui_infield{4} _actKnobPlacing, x3, _yCurr, 90, _infHeight,
					8, "",
					0, 0,
					0, 0, 0, 0,
					"", `Вертикальный`, _stPlacing1,
					"", `Горизонтальный`, _stPlacing2 ui_tooltip `Расположение`
		
			_yCurr = _yCurr + _yOffset
			ui_outfield `Расположение`, x1, _yCurr, 120, _outfHeight, _outfFlag3Size
		else
			ui_infield{4} _actKnobNumDrawer, x3 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Ручек на Ящик`
		
			_yCurr = _yCurr + _yOffset
			ui_outfield `Количество Ручек на Ящик`, x1, _yCurr, 155, _outfHeight, _outfFlag2
		endif

		_yCurr = _yCurr + dy - _yOffset
		ui_infield{4} _actKnobPosXDrawer, x3, _yCurr, 90, _infHeight ui_tooltip `По Горизонтали`
	
		_yCurr = _yCurr + _yOffset
		ui_outfield `По Горизонтали`, x1, _yCurr, 120, _outfHeight, _outfFlag2
	
		_yCurr = _yCurr + dy - _yOffset
		ui_infield{4} _actKnobPosYDrawer, x3, _yCurr, 90, _infHeight ui_tooltip `Привязка по Вертик.`
	
		_yCurr = _yCurr + _yOffset
		ui_outfield `Привязка по Вертик.`, x1, _yCurr, 120, _outfHeight, _outfFlag2
		_yCurr = _yCurr + dy - _yOffset
	else
		_yCurr = _yCurr - _yOffset
	endif

	ui_infield _actKnobSizeDrawer, x3, _yCurr, 90, _infHeight ui_tooltip `Размер`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Размер`, x1, _yCurr, 120, _outfHeight, _outfFlag2Size

	if iItemClass = ITEMCLASS_NIGHT_TABLE then
		_yCurr = _yCurr + 90
	else
		_yCurr = _yCurr + 38
	endif
	ui_infield _actMatKnobDrawer, x1, _yCurr, 213, 25 ui_tooltip `Ручка`
return

"DoorandDrawer":
	_yCurr = _yCurr + _yOffset
	ui_style 0, 1
	ui_infield{4} _actbKnob1, x1, _yCurr, 210, _infHeight,
		7, "",
		2, 0,
		0, 0, 0, 0,
		"", _stKnob1, 0,
		"", _stKnob1, 1
	ui_style 0, 0

	_yCurr = _yCurr + _infHeight + 1
	ui_infield{4} _actKnobTypeDoor, x2, _yCurr, 137, 90,
		2, 1, 12, 3,
		120, 90, 120, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,	4,
		5, ``,	5,
		6, ``,	6,
		7, ``,	7,
		8, ``,	8 ui_tooltip `Стиль Ручки`
	
	_yCurr = _yCurr + dy * 4 + 5	
	ui_infield{4} _actKnobPosXDoor, x3, _yCurr, 90, _infHeight ui_tooltip `По Горизонтали`

	_yCurr = _yCurr + _yOffset
	ui_outfield `По Горизонтали`, x1, _yCurr, 120, _outfHeight, _outfFlag1

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{4} _actKnobPosYDoor, x3, _yCurr, 90, _infHeight

	_yCurr = _yCurr + _yOffset
	ui_outfield `Привязка по Вертик.`, x1, _yCurr, 120, _outfHeight, _outfFlag1

	_yCurr = _yCurr + dy - _yOffset
	ui_infield _actKnobSizeDoor, x3, _yCurr, 90, _infHeight ui_tooltip `Размер`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Размер`, x1, _yCurr, 120, _outfHeight, _outfFlag1Size

	if iItemClass = ITEMCLASS_CREDENZA or iItemClass = ITEMCLASS_WARDROBE_VARIABLE then
		if (iItemClass = ITEMCLASS_CREDENZA and iStyle = 2) or (iItemClass = ITEMCLASS_WARDROBE_VARIABLE and iFrontAbove = 1) then		!Drawer
			_yCurr = _yCurr + dy - _yOffset
			ui_infield{4} _actKnobNumDrawer2, x3 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Ручек на Ящик`
	
			_yCurr = _yCurr + _yOffset
			ui_outfield `Количество Ручек на Ящик`, x1, _yCurr, 155, _outfHeight, _outfFlag1
		else	! Door
			_yCurr = _yCurr + dy - _yOffset
			ui_infield{4} _actKnobPlacing, x3, _yCurr, 90, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", `Вертикальный`, _stPlacing1,
				"", `Горизонтальный`, _stPlacing2 ui_tooltip `Расположение`
			_yCurr = _yCurr + _yOffset
			ui_outfield `Расположение`, x1, _yCurr, 120, _outfHeight, _outfFlag3Size
		endif
	else	! ITEMCLASS_OFFICE_CABINET
		_yCurr = _yCurr + dy - _yOffset
		ui_infield{4} _actKnobPlacing, x3, _yCurr, 90, _infHeight,
			8, "",
			0, 0,
			0, 0, 0, 0,
			"", `Вертикальный`, _stPlacing1,
			"", `Горизонтальный`, _stPlacing2 ui_tooltip `Расположение`
			
		_yCurr = _yCurr + _yOffset
		ui_outfield `Расположение`, x1, _yCurr, 120, _outfHeight, _outfFlag1Size
	endif
	
	_yCurr = _yCurr + 38
	ui_infield{4} _actMatKnobDoor, x1, _yCurr, 213, 25 ui_tooltip `Ручка`

	ui_separator 222, 0, 222, 266

	x1 = 0 + 230
	x2 = 76 + 230
	x3 = 123 + 230
	_yCurr = pageStartY  + _yOffset

	ui_style 0, 1
	ui_infield{4} _actbKnob2, x1, _yCurr, 210, _infHeight,
					7, "",
					2, 0,
					0, 0, 0, 0,
					"", _stKnob2, 0,
					"", _stKnob2, 1
	ui_style 0, 0
	
	_yCurr = _yCurr + _infHeight + 1
	ui_infield{4} _actKnobTypeDrawer, x2, _yCurr, 137, 90,
		2, 1, 12, 3,
		120, 90, 120, 90,
		1, ``,	1,
		2, ``,	2,
		3, ``, 	3,
		4, ``,	4,
		5, ``,	5,
		6, ``,	6,
		7, ``,	7,
		8, ``,	8 ui_tooltip `Стиль Ручки`
		
	_yCurr = _yCurr + dy * 4 + 5
	ui_infield{4} _actKnobPosXDrawer, x3, _yCurr, 90, _infHeight ui_tooltip `По Горизонтали`

	_yCurr = _yCurr + _yOffset
	ui_outfield `По Горизонтали`, x1, _yCurr, 120, _outfHeight, _outfFlag2

	_yCurr = _yCurr + dy - _yOffset
	ui_infield{4} _actKnobPosYDrawer, x3, _yCurr, 90, _infHeight ui_tooltip `Привязка по Вертик.`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Привязка по Вертик.`, x1, _yCurr, 120, _outfHeight, _outfFlag2

	_yCurr = _yCurr + dy - _yOffset
	ui_infield _actKnobSizeDrawer, x3, _yCurr, 90, _infHeight ui_tooltip `Размер`

	_yCurr = _yCurr + _yOffset
	ui_outfield `Размер`, x1, _yCurr, 120, _outfHeight, _outfFlag2Size

	_yCurr = _yCurr + dy - _yOffset
	if iItemClass = ITEMCLASS_WARDROBE_VARIABLE or iItemClass = ITEMCLASS_CREDENZA then
		if iFrontUnder = 3 then	! Door
			_yCurr = _yCurr + _yOffset
			if iFrontUnder = 3 and iFrontAbove = 3 then
				if gs_knob_type_m > 3 or  iKnobTypeAbove > 3 then
					if gs_knob_p = `Горизонтальный` then		
						ui_outfield `Горизонтальный`,	x3, _yCurr, 90, _outfHeight, 1 + _outfFlag2Size
					else
						ui_outfield `Вертикальный`,	x3, _yCurr, 90, _outfHeight, 1 + _outfFlag2Size
					endif
				endif
			endif
			ui_outfield `Расположение`, x1, _yCurr, 120, _outfHeight, _outfFlag3Size
		else	! Drawer
			ui_infield{4} _actKnobNumDoor2, x3 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Ручек на Ящик`

			_yCurr = _yCurr + _yOffset
			ui_outfield `Количество Ручек на Ящик`, x1, _yCurr, 155, _outfHeight, _outfFlag2
		endif
	else
		ui_infield{4} _actKnobNumDrawer, x3 + 36, _yCurr, 54, _infHeight ui_tooltip `Количество Ручек на Ящик`
	
		_yCurr = _yCurr + _yOffset
		ui_outfield `Количество Ручек на Ящик`, x1, _yCurr, 155, _outfHeight, _outfFlag2
	endif

	_yCurr = _yCurr + 38
	ui_infield _actMatKnobDrawer, x1, _yCurr, 213, 25 ui_tooltip `Ручка`
return
