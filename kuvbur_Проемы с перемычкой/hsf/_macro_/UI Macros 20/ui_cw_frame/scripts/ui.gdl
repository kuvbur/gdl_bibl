
! =============================================================================
! Tabpage System
! =============================================================================

if vardim1(gs_idUI) = 1 & gs_idUI[1] = 0 & gs_stUI[1] = "" & gs_icoUI[1] = "" then
	gs_idUI[1] = 1
	gs_idUI[2] = 2
	gs_idUI[3] = 3
	gs_idUI[4] = 4
	gs_idUI[5] = 5
	gs_idUI[6] = 6
	gs_idUI[7] = 7
	gs_idUI[8] = 8
endif

dim tabIDs[]
dim tabNames[]
dim tabIcons[]
dim customPageIdx[10]

actIdx = 1
j = 1

idTab_GeometrySettings = 0
idTab_CuttingPlanes = 0
idTab_Attributes = 0
idTab_ListSettings = 0

for i=1 to vardim1(gs_idUI)

	if gs_idUI[i] = 0 then
		tabIDs[actIdx] = actIdx
		tabNames[actIdx] = gs_stUI[i]
		tabIcons[actIdx] = gs_icoUI[i]
		customPageIdx[j] = actIdx
		j = j + 1

		actIdx = actIdx + 1
	else
		if gs_idUI[i] = 100 then		! Built-in Geometry Settings
			tabIDs[actIdx] = actIdx
			tabNames[actIdx] = stTabpageTitles[1]			! "Geometry"
			tabIcons[actIdx] = "uiTab_CW_modularFrame_flat(1)"
			idTab_GeometrySettings = actIdx

			actIdx = actIdx + 1
		endif
		if gs_idUI[i] = 200 then		! Built-in Cutting Planes Settings
			tabIDs[actIdx] = actIdx
			tabNames[actIdx] = stTabpageTitles[2]			! "Cutting Planes"
			tabIcons[actIdx] = "uiTab_CW_cuttingPlanes_flat(1)"
			idTab_CuttingPlanes = actIdx

			actIdx = actIdx + 1
		endif
		if gs_idUI[i] = 300 then		! Built-in Attributes Tabpage
			tabIDs[actIdx] = actIdx
			tabNames[actIdx] = stTabpageTitles[3]			! "Attributes"
			tabIcons[actIdx] = "uiTab_Attributes_flat(1)"
			idTab_Attributes = actIdx

			actIdx = actIdx + 1
		endif
		if gs_idUI[i] = 400 then		! Built-in Listing Settings
			tabIDs[actIdx] = actIdx
			tabNames[actIdx] = stTabpageTitles[4]			! "Parameters for Listing"
			tabIcons[actIdx] = "uiTab_Description_flat(1)"
			idTab_ListSettings = actIdx

			actIdx = actIdx + 1
		endif
	endif
next i

file_dependence "uiTab_CW_modularFrame_flat(1)"
file_dependence "uiTab_CW_cuttingPlanes_flat(1)"
file_dependence "uiTab_Attributes_flat(1)"
file_dependence "uiTab_Description_flat(1)"

for i=actIdx to 20
	tabIDs[i]	= 0
	tabIcons[i]	= ""
	tabNames[i]	= ""
next i

ui_current_page gs_ui_current_page


! =============================================================================
! Tabpage Selector Control
! =============================================================================

ui_page gs_ui_current_page	! Place it on every page

px = 30
py = 20
cx = px
cy = py

ui_style 0,1
ui_infield{3} "gs_ui_current_page", 0,0, 295, cy+5,
	2, "",
	0, 0,
	cx, cy, px, py,
	tabIcons[1],	tabNames[1],	tabIDs[1],
	tabIcons[2],	tabNames[2],	tabIDs[2],
	tabIcons[3],	tabNames[3],	tabIDs[3],
	tabIcons[4],	tabNames[4],	tabIDs[4],
	tabIcons[5],	tabNames[5],	tabIDs[5],
	tabIcons[6],	tabNames[6],	tabIDs[6],
	tabIcons[7],	tabNames[7],	tabIDs[7],
	tabIcons[8],	tabNames[8],	tabIDs[8],
	tabIcons[9],	tabNames[9],	tabIDs[9],
	tabIcons[10],	tabNames[10],	tabIDs[10],
	tabIcons[11],	tabNames[11],	tabIDs[11],
	tabIcons[12],	tabNames[12],	tabIDs[12],
	tabIcons[13],	tabNames[13],	tabIDs[13],
	tabIcons[14],	tabNames[14],	tabIDs[14],
	tabIcons[15],	tabNames[15],	tabIDs[15],
	tabIcons[16],	tabNames[16],	tabIDs[16],
	tabIcons[17],	tabNames[17],	tabIDs[17],
	tabIcons[18],	tabNames[18],	tabIDs[18],
	tabIcons[19],	tabNames[19],	tabIDs[19],
	tabIcons[20],	tabNames[20],	tabIDs[20] ui_tooltip `Выбор Вкладки`

ui_button ui_prev, "<<", 305,2, 32,21, gs_ui_current_page - 1	ui_tooltip `Предыдущая Вкладка`  ! Previous
ui_button ui_next, ">>", 337,2, 32,21, gs_ui_current_page + 1	ui_tooltip `Следующая Вкладка`      ! Next

ui_separator 0,28, 444,28


! =============================================================================
! Tabpages for CW Elements
! =============================================================================

! -----------------------------------------------------------------------------
! Geometry Settings Tabpage
! -----------------------------------------------------------------------------

if idTab_GeometrySettings > 0 then
	ui_page idTab_GeometrySettings

	y = 40
	x1 = 10
	x2 = 232
	dy = 24

	ui_separator 222, y, 222, 265


	ui_style 0, 1
	ui_outfield `Тип Рамы`, x1, y, 205, 18
	ui_outfield `Тип Насадки`, x2, y, 205, 18

	y = y + dy

	ui_infield{3} "gs_frameType_txt", x1+40, y-5, 165, 72,
				2, 3, 6, 2,			! "CW Frame Types"
				70, 70, 70, 70,
				1,`Тип 1`,`Тип 1`,					!!Type 1
				2,`Тип 2`,`Тип 2`,					!!Type 2
				3,`Тип 3`,`Тип 3`,					!!Type 3
				5,`Специальная Рама`,`Специальная Рама`		!!Custom Frame


	ui_infield{3} "gs_capType_txt", x2+40, y-5, 165, 72,
				2, 4, 6, 2,			! "CW Cap Types"
				70, 70, 70, 70,
				1,`Тип 1`,`Тип 1`,			!!Type 1
				2,`Тип 2`,`Тип 2`,			!!Type 2
				3,`Тип 3`,`Тип 3`,			!!Type 3
				5,`Специальная Насадка`,`Специальная Насадка`		!!Custom Cap

	y = y + 80

	if gs_frameType <> 0 then
		ui_outfield `Имя Спец. Рамы`, x1, y, 150, 18,4
	else
		ui_outfield `Имя Спец. Рамы`, x1, y, 150, 18
	endif

	if gs_capType <> 0 then
		ui_outfield `Имя Спец. Насадки`, x2, y, 150, 18,4
	else
		ui_outfield `Имя Спец. Насадки`, x2, y, 150, 18
	endif


	y = y + dy

	ui_infield "gs_CustomFrameName", x1+40, y-5, 165, 18
	ui_infield "gs_CustomCapName", x2+40, y-5, 165, 18

	ui_style 0, 0

	y = y + dy

	ui_outfield `Внутренний Уплотнитель`, x1, y, 150, 18
	ui_outfield `Наружный Уплотнитель`, x2, y, 150, 18

	y = y + dy

	ui_infield "gs_gasketIN", x1+40, y-5, 165, 18
	ui_infield "gs_gasketOUT", x2+40, y-5, 165, 18

endif

! -----------------------------------------------------------------------------
! Cutting Planes Tabpage
! -----------------------------------------------------------------------------

if idTab_CuttingPlanes > 0 then
	ui_page idTab_CuttingPlanes

	y0 = 35
	x1 = 10
	x2 = 230
	xpic = 290
	dy = 26

	y = y0 + 10

	ui_style 0,1

	ui_infield{3} "gs_Cutting", x1, y, 150, 18,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, `Секущие Плоскости`, 0,
				1, `Секущие Плоскости`, 1

	y = y + dy*2

	ui_style 0, 0

	ui_outfield `Направление Верхнего Сечения`, x1, y, 180, 18

	y = y + dy

	ui_infield{3} "iCutDirTop", x1 + 50, y-5, 152, 18,
		8, "", 0, 0, 0, 0, 0, 0,
		"", stCutModeColValues[1], CUTMODE_ALONG_X,
		"", stCutModeColValues[2], CUTMODE_ALONG_Y

	y = y + dy

	ui_outfield `Направление Нижнего Сечения`, x1, y, 180, 18

	y = y + dy

	ui_infield{3} "iCutDirBottom", x1 + 50, y-5, 152, 18,
		8, "", 0, 0, 0, 0, 0, 0,
		"", stCutModeColValues[1], CUTMODE_ALONG_X,
		"", stCutModeColValues[2], CUTMODE_ALONG_Y

	ui_separator 220, y0, 220, 265

	ui_pict 1, xpic, y0+19, 80, 97, 1	! "ui_frame_top"
	ui_pict 2, xpic, y0+116, 80, 97, 1	! "ui_frame_bottom"

	ui_infield "gs_cutang1", x2+125, y0, 80, 18 ui_tooltip `Верхний Правый Угол Среза`
	ui_infield "gs_cutang2", x2-5, y0, 80, 18 ui_tooltip `Верхний Левый Угол Среза`

	ui_infield "gs_cutang3", x2+125, y0+215, 80, 18 ui_tooltip `Нижний Правый Угол Среза`
	ui_infield "gs_cutang4", x2-5, y0+215, 80, 18 ui_tooltip `Нижний Левый Угол Среза`

endif

! -----------------------------------------------------------------------------
! 2D Representation
! -----------------------------------------------------------------------------

if idTab_Attributes > 0 then
	ui_page idTab_Attributes

	x1 = 10
	x2 = 232
	y0 = 40
	dy = 28

	y = y0

	ui_style 0, 1
	ui_outfield `3D-отображение`, x1, y, 150, 18

	y = y + dy

	ui_style 0, 0
	ui_outfield `3D-детализация`, x1, y, 150, 18

	y = y + dy
	UI_INFIELD{3}	"gs_detlevel_3D_m", x1+50, y-5, 150, 18,
			8, "", 3, 1,
			0, 0, 0, 0,
			1, `Детальный` , 2,
			2, `Откл.`, 0
	y = y + dy

	ui_infield{3} "gs_shadow", x1, y, 150, 18,
				7, "",
				2,
				1, 20, 20, 20, 20,
				0, `Отбрасывание Теней`, 0,
				1, `Отбрасывание Теней`, 1

	y = y + dy

	ui_outfield `Аппроксимация`, x1, y, 135, 18
	ui_infield "gs_resol", x1+140, y-7, 60, 22

	y = y + dy

	ui_outfield `Покрытие`, x1, y, 150, 18
	y = y + dy
	ui_infield "gs_material", x1+50, y-7, 150, 22

	ui_separator 222, y0, 222, 265
	y = y0

	ui_style 0, 1
	ui_outfield `Сечение/Контур`, x2, y, 150, 18

	y = y + dy

	ui_style 0, 0

	ui_outfield `Перо`, x2, y, 135, 18
	ui_infield "gs_cont_pen", x2+150, y-2, 60, 22

	y = y + dy

	ui_outfield `Тип Штриховки Сечения`, x2, y, 150, 20

	y = y + dy

	ui_infield "gs_fill_type", x2+50, y-7, 150, 22

	y = y + dy

	ui_outfield `Перо Штриховки Сечения`, x2, y, 135, 20
	ui_infield "gs_fill_pen", x2+150, y-1, 50, 22

	y = y + dy

	ui_outfield `Перо Фона Штриховки Сечения`, x2, y, 135, 20
	ui_infield "gs_back_pen", x2+150, y-1, 50, 22
endif


! -----------------------------------------------------------------------------
! List Settings Tabpage
! -----------------------------------------------------------------------------

if idTab_ListSettings > 0 then
	ui_page idTab_ListSettings

	dx = 222

	ui_separator dx, 40, dx, 266

! --- 1. column ---

	dy = 45

	ui_outfield `Цена`, 10, dy, 120, 20
	ui_infield "gs_list_cost", 131, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Производитель`, 10, dy, 120, 20
	ui_infield "gs_list_manufacturer", 131, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Расположение`, 10, dy, 120, 20
	ui_infield "gs_list_location", 131, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Аксессуары`, 10, dy, 120, 20
	ui_infield "gs_list_accessories", 131, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Отделка`, 10, dy, 120, 20
	ui_infield "gs_list_finish", 131, dy-5, 80, 20
	dy = dy + 18

	ui_separator 7, dy, dx-3, dy
	dy = dy + 9

! --- 2. column ---

	dy = 45
	dx = dx + 10

	ui_outfield `Специальные Данные 1`, dx, dy, 120, 20
	ui_infield "gs_list_custom1", dx+121, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Специальные Данные 2`, dx, dy, 120, 20
	ui_infield "gs_list_custom2", dx+121, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Специальные Данные 3`, dx, dy, 120, 20
	ui_infield "gs_list_custom3", dx+121, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Специальные Данные 4`, dx, dy, 120, 20
	ui_infield "gs_list_custom4", dx+121, dy-5, 80, 20
	dy = dy + 21

	ui_outfield `Специальные Данные 5`, dx, dy, 120, 20
	ui_infield "gs_list_custom5", dx+121, dy-5, 80, 20
	dy = dy + 21
endif

