
! ==============================================================================
! Initialization
! ==============================================================================

x1	= 2
x2	= 184
x3	= 226
x4	= 408
cx1	= 181
cx2	= 33
dy	= 26

ySep = pageStartY + 152

outFldHgt	= 15
penInfHgt	= 19
strInfHgt	= 19
matInfHgt	= 25
filInfHgt	= 23


! ==============================================================================
! 2D Representation
! ==============================================================================
	gosub "Block2D"

! ==============================================================================
! 3D Representation
! ==============================================================================
	gosub "Block3D"

! ==============================================================================
! Separators
! ==============================================================================
	ui_separator 221,	1,		221,	ySep-1

	ui_separator 0,		ySep,	444,	ySep

! ==============================================================================
! Surfaces
! ==============================================================================
	gosub "BlockSurfaces"


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
"Block2D":
! ------------------------------------------------------------------------------
	y1	= pageStartY

	ui_style 0, 1
	ui_outfield `2D-отображение`,	x1,		y1,		x2+cx2-x1,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Перо Контура`,			x1,		y1,		cx1,	outFldHgt
	ui_infield "gs_cont_pen",			x2,		y1-4,	cx2,	penInfHgt	ui_tooltip `Перо Контура в 2D и в 3D`

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Тип Штриховки`,			x1,		y1,		cx1-77,	outFldHgt
	ui_infield "gs_fill_type",			x2-77,	y1-6,	cx2+77,	filInfHgt

	y1 = y1 + dy

	ui_outfield `Перо Штриховки`,				x1,		y1,		cx1,	outFldHgt
	ui_infield "gs_fill_pen",			x2,		y1-4,	cx2,	penInfHgt

	y1 = y1 + dy

	ui_outfield `Перо Фона`,		x1,		y1,		cx1,	outFldHgt
	ui_infield "gs_back_pen",			x2,		y1-4,	cx2,	penInfHgt

return


! ==============================================================================
"Block3D":
! ------------------------------------------------------------------------------
	y1	= pageStartY

	ui_style 0, 1
	ui_outfield `3D-отображение`,	x3,		y1,		x4+cx2-x3,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0
	ui_outfield `Детализация`,			x3,		y1,		cx1-62,	outFldHgt
	ui_infield{3} "gs_detlevel_3D_m",	x4-62, 	y1-4,	cx2+62,	strInfHgt,
		8, "", 1, 1,
		0, 0, 0, 0,
		1, stDetailLevel[1],	0,
		1, stDetailLevel[2],	1,
		1, stDetailLevel[3],	2

	y1 = y1 + dy

	ui_outfield `Аппроксимация`,			x3,		y1,		cx1-17,	outFldHgt, 4 * (gs_detlevel_3D_m <> 2)
	ui_infield "gs_resol",				x4-17,	y1-4,	50,		strInfHgt

	y1 = y1 + dy

	ui_infield{3} "gs_shadow",			x3, 	y1-4,	x4+cx2-x3,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Отбрасывание Теней`,	0,
		1, `Отбрасывание Теней`,	1

	y1 = y1 + dy

	ui_infield{3} "bShowIllumination3D",	x3, 	y1-4,	cx1,	strInfHgt,
		7, "", 1, 1,
		0, 0, 0, 0,
		1, `Показ Освещения`,	0,
		1, `Показ Освещения`,	1

	ui_infield "penIllumination",		x4,		y1-4,	cx2,	strInfHgt		ui_tooltip `Перо Светового Конуса в 3D`

	y1 = y1 + dy

	if bEnableModelType then
		ui_infield{3} "iModelType",			x4-62, 	y1-4,	cx2+62,	strInfHgt,
			8, "", 1, 1,
			0, 0, 0, 0,
			1, `Каркас`,	1,
			1, `Оттененная`,	2		ui_tooltip `Тип 3D-модели Освещения`
	endif

return


! ==============================================================================
"BlockSurfaces":
! ------------------------------------------------------------------------------
	x1	= 2
	x2	= 160
	cx1	= 150
	cx2	= 180

	y1 = pageStartY + 157

	ui_style 0, 1
	ui_outfield `Покрытия`,	x1,		y1,		440,	outFldHgt

	y1 = y1 + dy

	ui_style 0, 0

	for iii = 1 to vardim1(surfaceBlock)

		if surfaceBlock[iii][1] # "" & surfaceBlock[iii][2] # "" then
			ui_outfield	surfaceBlock[iii][1],	x1,		y1,		cx1,		outFldHgt,	flSurfaceBlk[iii]
			ui_infield	surfaceBlock[iii][2],	x2,		y1-6,	cx2,		matInfHgt

			y1 = y1 + dy
		endif
	next iii

return

