

! iTableTopType
TTOP_L_SHAPED		= 1
TTOP_STRAIGHT		= 2
TTOP_ARCHED_45		= 3
TTOP_ARCHED_60		= 4
TTOP_ARCHED_90		= 5
TTOP_ARCHED_135		= 6
TTOP_ARCHED_180		= 7
TTOP_ARCHED_360		= 8
TTOP_ARCHED_C_TOP	= 9
TTOP_ARCHED_P_TOP	= 10
TTOP_RECT			= 11

! iInnerEdge
STRAIGHT			= 1
ROUNDED				= 2


dim stDetail[3]
	stDetail[1]=`Детальный`
	stDetail[2]=`Простой`
	stDetail[3]=`Откл.`

if bShowLegResol then
	stResol	= `Аппроксимация Столешницы`
else
	stResol	= `Аппроксимация`
endif

x1 = 0
x2 = 170
x3 = 262
x4 = 227
x5 = 390
x6 = 425
x7 = 444

_infHeight 	= 19
_dy 		= 24
_yEnd 		= 266
_yCurr 		= pageStartY + 1

ui_style 0,0
if bShowDetlevel3D then
	ui_outfield	`3D-детализация`, x1,    _yCurr, 	x2 - x1 - 1,	15
	ui_infield{3} "gs_detlevel_3D_m", x2, _yCurr - 4, x3 - x2, _infHeight,
				8, "",
				0, 0,
				0, 0, 0, 0,
				"", stDetail[1], 2,
				"", stDetail[2], 1,
				"", stDetail[3], 0
_yCurr = _yCurr + _dy
endif

_iFlagResol = 0
if bShowLegResol then
	if  iTableTopType <> TTOP_L_SHAPED | iInnerEdge <> ROUNDED | gs_detlevel_3D_m <> 2 then
		_iFlagResol = 4
	endif
else
	if gs_detlevel_3D_m <> 2 then
		!Simple, Off
		_iFlagResol = 4
	endif
endif

if bShowResol then
	ui_outfield	stResol,	x1,    _yCurr, 		x2 - x1 - 1,	15, _iFlagResol
	ui_infield	"gs_resol",	x2,    _yCurr - 4,	x7 - x5 - 2,	_infHeight
	_yCurr = _yCurr + _dy
endif

if bShowLegResol then
	_iFlagResol = 0
	if gs_detlevel_3D_m <> 2 then _iFlagResol = 4 	!Simple, Off
	ui_outfield	`Аппроксимация`,	x1,    _yCurr , 	x2 - x1 - 1,	15, _iFlagResol
	ui_infield	"gs_leg_resol",	x2,    _yCurr - 4, x7 - x5 - 2,	_infHeight 
	_yCurr = _yCurr + _dy
endif

_yCurr = _yCurr - 4
ui_infield{3} "gs_shadow", x1, _yCurr, 442, _infHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `Отбрасывание Теней`, 0,
				"", `Отбрасывание Теней`, 1

_yCurr = _yCurr + _dy 

if bShowHoleParam then
	ui_infield{3} "hole", x1, _yCurr, 442, _infHeight,
				7, "",
				0, 0,
				0, 0, 0, 0,
				"", `Отверстия в 3D`, 0,
				"", `Отверстия в 3D`, 1

	_yCurr = _yCurr + _dy 
endif

if bShowAppBox then
	ui_infield{3} "Appl3D", x1, _yCurr, 442, _infHeight,
					7, "",
					0, 0,
					0, 0, 0, 0,
					"", `Показ Корпуса Прибора в 3D`, 0,
					"", `Показ Корпуса Прибора в 3D`, 1
	
	_yCurr = _yCurr + _dy 
endif

if bShowAnimation then
	ui_separator x1, _yCurr, x7, _yCurr 
	_yCurr = _yCurr + 6

	ui_style 0,1
	ui_outfield	`Анимация`,	x1,    _yCurr, 	442,	15
	_yCurr = _yCurr + _dy 
	ui_style 0,0
	
	ui_outfield	`Кадров в Секунду`,	x1,    _yCurr, 	x2 - x1 - 1,	15
	ui_infield	"nFramesPerSecond",		x2,    _yCurr - 4, x6 - x5 - 2, 		_infHeight 

	_yCurr = _yCurr + _dy
	ui_outfield	`Скорость Вращения (1/мин)`,	x1,    _yCurr, 	x2 - x1 - 1,	15
	ui_infield	"rotorSpeed",			x2,    _yCurr - 4, x6 - x5 - 2, 		_infHeight 
	_yCurr = _yCurr + _dy - 4
endif

if bEdgeVisibility then !Only Cooktop Built-in
	ui_outfield `Показ Торцов`, x1, _yCurr + 4, x2 - x1 - 1, 15
	ui_infield{3} "side_vis_m", x2, _yCurr, 95, 32,
		2,"ui_CabSideVis(13)", 4, 1,
		34,34,30,30,
		1, `Нет`, 1,
		2, `Слева`, 2,
		3, `Справа`, 3,
		4, `Обе`, 4
	_yCurr = _yCurr + 37
endif

if vardim1(surfacesArray) > 0 then
	_itemID = 1
	ui_listfield iSurfaceListID, 0, _yCurr, 444, _yEnd - _yCurr, 1, "", ""
	ui_listitem _itemID, iSurfaceListID, "", 0, "", `Покрытия` : _itemID = _itemID + 1

	for _idxItem = 1 to vardim1(surfacesArray)
		if surfacesArray[_idxItem] = "bUniformSurfaces" then
			ui_listitem _itemID, iSurfaceListID, surfacesArray[_idxItem], childFlagArray[_idxItem], "" : _itemID = _itemID + 1
		else
			ui_listitem _itemID, iSurfaceListID, surfacesArray[_idxItem], childFlagArray[_idxItem], "ui_list_surface_flat(1)" : _itemID = _itemID + 1
			file_dependence "ui_list_surface_flat(1)"
		endif
	next _idxItem
endif

