
dim surfacesArray[]
i = 1
	surfacesArray[i]	= "bUniformSurfaces"	: i = i + 1
	surfacesArray[i]	= "matTower"			: i = i + 1
	surfacesArray[i]	= "matBlade"			: i = i + 1
	surfacesArray[i]	= "matNacelle"			: i = i + 1
if iTurbinType # 2 then
	surfacesArray[i]	= "matTail"				: i = i + 1
endif
	
dim childFlagArray[5]    
	childFlagArray[1]	= 0
	childFlagArray[2]	= 0
	childFlagArray[3]	= 0
	childFlagArray[4]	= 0
	childFlagArray[5]	= 0

dim stTooltip[5]
	stTooltip[1]	= `Угол Ротора`
	stTooltip[2]	= `Диаметр Вышки Вверху`
	stTooltip[3]	= `Диаметр Вышки Внизу`
	stTooltip[4]	= `Высота Хаба`
	stTooltip[5]	= `Диаметр Ротора`

if iTurbinType = TURBIN_HOUSEHOLD then
	UI_dialog `Ветротурбина Бытовая`
else
	UI_dialog `Ветротурбина Промышленная`
endif

ui_current_page gs_ui_current_page

iPageIDRoot 				=-1
iPageIDDimensions			= 1
iPageID2DRepresentation		= 2
iPageID3DRepresentation		= 3


! ==============================================================================
!UI_PAGES
! ==============================================================================

UI_PAGE iPageIDDimensions, iPageIDRoot, `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "DimensionsPage"

UI_PAGE iPageID2DRepresentation, iPageIDRoot, `2D-отображение`, "uiTab_2DRepr_flat(1)"
	gosub "2DPage"

UI_PAGE iPageID3DRepresentation, iPageIDRoot, `3D-отображение, Анимация и Покрытия`, "uiTab_3DRepr_flat(1)"
	gosub "3DPage"

! ==============================================================================
end
! ==============================================================================

! ==============================================================================
!Subroutines
! ==============================================================================

! ------------------------------------------------------------------------------
"DimensionsPage":
! ------------------------------------------------------------------------------
	pageStartX = 0
	pageStartY = 0
	
	ui_outfield `Количество Лопастей`, pageStartX,			pageStartY + 4, 221, 15
	ui_infield{3} "nBlade", 		pageStartX + 17, 	pageStartY + 20, 70, 19,
			8, "", 2, 1,
			0, 0, 0, 0,
			1, `3`, 3,
			2, `2`, 2

	ui_infield "rotorAngle", pageStartX + 222, pageStartY, 90, 19 ui_tooltip stTooltip[1]
	
	ui_infield "towerUpperDia", pageStartX, pageStartY + 116, 90, 19 ui_tooltip stTooltip[2]
	
	ui_infield "towerLowerDia", pageStartX, pageStartY + 245, 90, 19 ui_tooltip stTooltip[3]
	
	ui_infield "hubHeight", pageStartX + 288, pageStartY + 220, 90, 19 ui_tooltip stTooltip[4]
	
	ui_infield "rotorDiameter", pageStartX + 288, pageStartY + 116, 90, 19 ui_tooltip stTooltip[5]
	
	if iTurbinType = TURBIN_HOUSEHOLD then
		ui_pict 1, 91, 20, 196, 245, 1
	endif
	if iTurbinType = TURBIN_INDUSTRIAL then
		ui_pict 2, 91, 20, 196, 245, 1
	endif
return

! ------------------------------------------------------------------------------
"2DPage":
! ------------------------------------------------------------------------------

call "ui_object_2DRepresentation" parameters	gs_detlevel_2D_m= gs_detlevel_2D_m,
												bShowDashedLine = 1,
												ltDashed 		= ltDashed,
												gs_cont_pen 	= gs_cont_pen,
												gs_fill_type 	= gs_fill_type,
												gs_fill_pen		= gs_fill_pen,
												gs_back_pen 	= gs_back_pen,
												iFillListID		= iPageID2DRepresentation
return

! ------------------------------------------------------------------------------
"3DPage":
! ------------------------------------------------------------------------------

call "ui_object_3DRepresentation" parameters 	gs_detlevel_3D		= gs_detlevel_3D,
												gs_detlevel_3D_m	= gs_detlevel_3D_m,
												gs_resol			= gs_resol,
												gs_shadow 			= gs_shadow,
												bShowAnimation		= bShowAnimation,
												nFramesPerSecond	= nFramesPerSecond,
												surfacesArray		= surfacesArray,
												childFlagArray		= childFlagArray,
												iSurfaceListID		= iPageID3DRepresentation,
												rotorSpeed			= rotorSpeed,
												matTower			= matTower,
												matBlade			= matBlade,
												matNacelle			= matNacelle,
												matTail				= matTail

return
