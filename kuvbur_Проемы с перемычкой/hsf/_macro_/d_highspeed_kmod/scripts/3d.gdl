
if lod3D = 5 then		! CHI
	gs_leaf_thk = gs_frame_thk/2
	gs_open3D	= 0
	gapFromFloor	= 0
endif


if bShowUnitHotspots | bShowAddHotspotsForIS then
	addz WALL_THICKNESS
	hotspot -ac_unit_width/2, 0, 0, 100
	hotspot  ac_unit_width/2, 0, 0, 101
	hotspot -ac_unit_width/2, 0, gs_frame_thk, 102
	hotspot  ac_unit_width/2, 0, gs_frame_thk, 103

	hotspot -ac_unit_width/2, ac_unit_height, 0, 104
	hotspot  ac_unit_width/2, ac_unit_height, 0, 105
	hotspot -ac_unit_width/2, ac_unit_height, gs_frame_thk, 106
	hotspot  ac_unit_width/2, ac_unit_height, gs_frame_thk, 107
	del 1
endif


if bShowLeafHotspots | bShowEgressHotspots | bShowAddHotspotsForIS then
	if bShowLeafHotspots then
		hsStartID = 300
		hsW = ac_leaf_width
		hsH = ac_leaf_height
	else
		hsStartID = 200
		hsW = ac_egress_width
		hsH = ac_egress_height
	endif

	addz WALL_THICKNESS
	hotspot -hsW/2, hsH, gs_frame_thk - 0.0775, hsStartID + 10
	hotspot hsW/2, hsH, gs_frame_thk - 0.0775, hsStartID + 11
	hotspot -hsW/2, hsH, gs_frame_thk, hsStartID + 12
	hotspot hsW/2, hsH, gs_frame_thk, hsStartID + 13

	hotspot -hsW/2, 0, gs_frame_thk - 0.0775, hsStartID
	hotspot hsW/2, 0, gs_frame_thk - 0.0775, hsStartID + 1
	hotspot -hsW/2, 0, gs_frame_thk, hsStartID + 2
	hotspot hsW/2, 0, gs_frame_thk, hsStartID + 3
	del 1
endif

if lod3D <> 5 then addz WALL_THICKNESS-WIDO_SILL


rotx -90
if iHighSpeedDoorType = HIGHSPEED_SIMPLE then

	if gs_full_edit then
		unID = 400
		hotspot 0, -CFrameShortThick-0.01, b/2, unID, gs_open3D, 1+128 : unID=unID+1
		hotspot gs_open3D, -CFrameShortThick-0.01, b/2, unID, gs_open3D, 2 : unID=unID+1
		hotspot -1, -CFrameShortThick-0.01, b/2, unID, gs_open3D, 3 : unID=unID+1
	endif
	amod = a/2-gs_open3D


	! --------- Opening Lines

	if bOplines3d then
		if lod3D<>5 then
			add -a/4+gs_open3D, -CFrameShortThick-0.01-gs_leaf_thk+0.001, 0
		else
			add -a/4+gs_open3D, -gs_leaf_thk-0.001, 0
		endif
		rotx 90
		mulz -1
		call "OpeningLines" parameters a = a,
					b					= b,
					frameThickness		= gs_leaf_thk+0.002,
					iOpeningType		= 13,			! SASH_RIGHT_SLIDING
					opLineType3dIn		= opLineType3dIn,
					opLineType3dOut		= opLineType3dOut,
					gs_opLinePen		= gs_opLinePen,
					gs_opline_style_m	= gs_opline_style_m
		del 3
		if lod3D<>5 then
			add -a*3/4-gs_open3D, -CFrameShortThick-0.01-gs_leaf_thk+0.001, 0
		else
			add -a*3/4-gs_open3D, -gs_leaf_thk-0.001, 0
		endif
		rotx 90
		mulz -1
		call "OpeningLines" parameters a = a,
					b					= b,
					frameThickness		= gs_leaf_thk+0.002,
					iOpeningType		= 12,			! SASH_LEFT_SLIDING
					opLineType3dIn		= opLineType3dIn,
					opLineType3dOut		= opLineType3dOut,
					gs_opLinePen		= gs_opLinePen,
					gs_opline_style_m	= gs_opline_style_m
		del 3
	endif


	! --------- Frame

	pen gs_frame_pen
	material gs_frame_mat
	sect_fill gs_fillFr, gs_penFr_bg, gs_penFr_fg, gs_frame_pen_cont

	if lod3D = 1 then 			! --------- SIMPLE 3D
		for i = 1 to -1 step -2
			mulx i
			addx a/2
			prism_ 7, b,
				0,						0,									15,
				gs_frame_width_out,		0,									15,
				gs_frame_width_out,		-gs_frame_thk,						15,
				-diffFrameWidths,		-gs_frame_thk,						15,
				-diffFrameWidths,		-gs_frame_thk+frameAddPartThick,	15,
				0,						-gs_frame_thk+frameAddPartThick,	15,
				0,						0,									-1
			del 2
		next i

		add -a/2-(gs_frame_width_out+offsetUpperBoxLength), 0, b
		rotx 90
		roty 90
		prism_ 5, a+2*(gs_frame_width_out+offsetUpperBoxLength),
			0,				upperBoxHeight,		15,
			-upperBoxThick,	upperBoxHeight,		15,
			-upperBoxThick,	0,					15,
			0,				0,					15,
			0,				upperBoxHeight,		-1
		del 3
	endif

	if lod3D = 2 then 			! --------- DETAILED 3D
		for i = 1 to -1 step -2
			mulx i
			addx a/2
			prism_ 13, b,
				0,													-CFrameShortThick,					15,
				0,		 											0,									15,
				gs_frame_width_out,									0,									15,
				gs_frame_width_out,									-gs_frame_thk,						15,
				gs_frame_width_out-CFrameShortWidth,				-gs_frame_thk,						15,
				gs_frame_width_out-CFrameShortWidth,				-gs_frame_thk+CFrameShorterThick,	15,
				gs_frame_width_out-CFrameShortWidth+CFrameWallThk,	-gs_frame_thk+CFrameShorterThick,	15,
				gs_frame_width_out-CFrameShortWidth+CFrameWallThk,	-gs_frame_thk+CFrameWallThk,		15,
				gs_frame_width_out-CFrameWallThk,					-gs_frame_thk+CFrameWallThk,		15,
				gs_frame_width_out-CFrameWallThk,					-CFrameWallThk,						15,
				CFrameWallThk,										-CFrameWallThk,						15,
				CFrameWallThk,										-CFrameShortThick,					15,
				0,													-CFrameShortThick,					-1

			add -diffFrameWidths, -gs_frame_thk+frameAddPartThick, gapFromFloor
			prism_ 6, b-gapFromFloor,
				0,									0,										15,
				frameAddPartWidth+inclinedPartSize,	0,										15,
				frameAddPartWidth,					-(frameAddPartThick-inclinedPartSize),	15,
				frameAddPartWidth,					-frameAddPartThick,						15,
				0,									-frameAddPartThick,						15,
				0,									0,										-1
			del 3
		next i

		add -a/2-(gs_frame_width_out+offsetUpperBoxLength), 0, b
		rotx 90
		roty 90
		prism_ 7, a+2*(gs_frame_width_out+offsetUpperBoxLength),
			0,										upperBoxHeight,							15,
			-upperBoxThick+upperBoxInclPartSize,	upperBoxHeight,							15,
			-upperBoxThick,							upperBoxHeight-upperBoxInclPartSize,	15,
			-upperBoxThick,							upperBoxInclPartSize,					15,
			-upperBoxThick+upperBoxInclPartSize,	0,										15,
			0,										0,										15,
			0,										upperBoxHeight,							-1
		del 3
	endif


	! --------- Leaf

	pen gs_leaf_pen
	sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

	if lod3D <> 5 then
		add 0, -CFrameShortThick-0.01, gapFromFloor
	else
		add 0, 0, gapFromFloor
	endif

	if lod3D = 1 | lod3D = 5 then 			! --------- SIMPLE 3D, Simple CHI
		for i = 1 to -1 step -2
			mulx i
			addx -a/2
			rotx 90

			material gs_leaf_mat

			if lod3D <> 5 then addz -gs_leaf_thk

			cprism_ gs_leaf_matin, gs_leaf_mat, gs_leaf_mat,
				5, gs_leaf_thk,
				0, 0, 15,
				amod, 0, 15,
				amod, b-gapFromFloor, 15,
				0,  b-gapFromFloor, 15,
				0, 0, -1

			if lod3D <> 5 then del 1
			del 3
		next i
	endif

	if lod3D = 2 then 			! --------- DETAILED 3D
		if gs_cust_on then
			for i = 1 to -1 step -2
				mulx i
				add -a/2, gs_leaf_thk, 0
				rotx 90
				call "Door_Panel_Collection" parameters a			= amod,
														b			= b,
														zzyzx		= gs_leaf_thk,
														gs_ptype	= gs_cust_panel
				del 3
			next i
		else
			if gs_glass_section then
				for i = 1 to -1 step -2
					pen gs_leaf_pen

					mulx i
					addx -a/2
					rotx 90
					roty 90

					material gs_bottom_part_mat
					prism_ 5, amod,
						0,					0,					15,
						0,					bottomPartHeight,	15,
						bottomPartThick,	bottomPartHeight,	15,
						bottomPartThick,	0,					15,
						0,					0,					-1

					if gs_glass_section_elev-bottomPartHeight > EPS then
						addy bottomPartHeight

						material gs_leaf_matin
						sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

						prism_ 5, amod,
							0,				0,										15,
							0,				gs_glass_section_elev-bottomPartHeight,	15,
							gs_leaf_thk/2,	gs_glass_section_elev-bottomPartHeight,	15,
							gs_leaf_thk/2,	0,										15,
							0,				0,										-1

						addx gs_leaf_thk/2

						material gs_leaf_mat
						sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

						prism_ 5, amod,
							0,				0,										15,
							0,				gs_glass_section_elev-bottomPartHeight,	15,
							gs_leaf_thk/2,	gs_glass_section_elev-bottomPartHeight,	15,
							gs_leaf_thk/2,	0,										15,
							0,				0,										-1

						del 2
					endif

					addy gs_glass_section_elev

					pen gs_glass_pen
					material gs_glass_mat
					sect_fill gs_fillGlass, gs_penGlass_bg, gs_penGlass_fg, gs_glass_pen

					prism_ 5, amod,
						0,				0,						15,
						0,				gs_glass_section_width,	15,
						gs_leaf_thk,	gs_glass_section_width,	15,
						gs_leaf_thk,	0,						15,
						0,				0,						-1

					addy gs_glass_section_width

					pen gs_leaf_pen
					material gs_leaf_matin
					sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

					prism_ 5, amod,
						0,				0,																15,
						0,				b-gs_glass_section_width-gs_glass_section_elev-gapFromFloor,	8,
						gs_leaf_thk/2,	b-gs_glass_section_width-gs_glass_section_elev-gapFromFloor,	13,
						gs_leaf_thk/2,	0,																15,
						0,				0,																-1

					addx gs_leaf_thk/2

					material gs_leaf_mat
					sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

					prism_ 5, amod,
						0,				0,																15,
						0,				b-gs_glass_section_width-gs_glass_section_elev-gapFromFloor,	8,
						gs_leaf_thk/2,	b-gs_glass_section_width-gs_glass_section_elev-gapFromFloor,	13,
						gs_leaf_thk/2,	0,																15,
						0,				0,																-1

					del 7
				next i
			else
				sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

				for i = 1 to -1 step -2
					mulx i
					addx -a/2
					rotx 90
					roty 90

					material gs_bottom_part_mat
					prism_ 5, amod,
						0,					0,					15,
						0,					bottomPartHeight,	15,
						bottomPartThick,	bottomPartHeight,	15,
						bottomPartThick,	0,					15,
						0,					0,					-1

					addy bottomPartHeight

					material gs_leaf_matin
					prism_ 5, amod,
						0,				0,									15,
						0,				b-bottomPartHeight-gapFromFloor,	8,
						gs_leaf_thk/2,	b-bottomPartHeight-gapFromFloor,	13,
						gs_leaf_thk/2,	0,									15,
						0,				0,									-1

					addx gs_leaf_thk/2

					material gs_leaf_mat
					prism_ 5, amod,
						0,				0,									15,
						0,				b-bottomPartHeight-gapFromFloor,	8,
						gs_leaf_thk/2,	b-bottomPartHeight-gapFromFloor,	13,
						gs_leaf_thk/2,	0,									15,
						0,				0,									-1

					del 6
				next i
			endif
		endif
	endif
	del 1
endif


if iHighSpeedDoorType = HIGHSPEED_ROLL_UP then

	if gs_full_edit then
		unID = 400
		hotspot 0, -gs_frame_thk/2, 0, unID, gs_open3D, 1+128 : unID=unID+1
		hotspot 0, -gs_frame_thk/2, gs_open3D, unID, gs_open3D, 2 : unID=unID+1
		hotspot 0, -gs_frame_thk/2, -1, unID, gs_open3D, 3 : unID=unID+1
	endif


	! --------- Opening Lines

	if bOplines3d then
		add -a/2, -gs_frame_thk/2-gs_leaf_thk+0.001, gs_open3D/2
		rotx 90
		mulz -1
		call "OpeningLines" parameters a = a,
					b					= b,
					frameThickness		= gs_leaf_thk+0.002,
					iOpeningType		= 14,			! SASH_UP_SLIDING
					opLineType3dIn		= opLineType3dIn,
					opLineType3dOut		= opLineType3dOut,
					gs_opLinePen		= gs_opLinePen,
					gs_opline_style_m	= gs_opline_style_m
		del 3
	endif


	! --------- Frame

	pen gs_frame_pen
	material gs_frame_mat
	sect_fill gs_fillFr, gs_penFr_bg, gs_penFr_fg, gs_frame_pen_cont

	if lod3D = 1 then 			! --------- SIMPLE 3D
		for i = 1 to -1 step -2
			mulx i
			addx a/2
			prism_ 5, b,
				0,					-gs_frame_thk,	15,
				0,	 				0,				15,
				gs_frame_width,		0,				15,
				gs_frame_width,		-gs_frame_thk,	15,
				0,					-gs_frame_thk,	-1
			del 2
		next i

		add -a/2-gs_frame_width-offsetUpperBoxLength, 0, b
		rotx 90
		roty 90
		prism_ 5, a+gs_frame_width*2+offsetUpperBoxLength,
			0,		upperBoxHeight,		15,
			-upperBoxThick,	upperBoxHeight,		15,
			-upperBoxThick,	0,		15,
			0,		0,		15,
			0,		upperBoxHeight,		-1
		del 3

		add a/2+gs_frame_width+sideBoxWidth, 0, b+upperBoxHeight-sideBoxHeight
		rotz 90
		prism_ 5, sideBoxHeight,
			0,				sideBoxWidth,	15,
			-sideBoxDepth,	sideBoxWidth,	15,
			-sideBoxDepth,	0,				15,
			0,				0,				15,
			0,				sideBoxWidth,	-1
		del 2
	endif

	if lod3D = 2 then 			! --------- DETAILED 3D
		for i = 1 to -1 step -2
			mulx i
			addx a/2
			prism_ 13, b,
				0,								-gs_frame_thk/2+CFrameOpeningSize/2,	15,
				0,	 							0,										15,
				gs_frame_width,					0,										15,
				gs_frame_width,					-gs_frame_thk,							15,
				0,								-gs_frame_thk,							15,
				0,								-gs_frame_thk/2-CFrameOpeningSize/2,	15,
				CFrameWallThk,					-gs_frame_thk/2-CFrameOpeningSize/2,	15,
				CFrameWallThk,					-gs_frame_thk+CFrameWallThk,			15,
				gs_frame_width-CFrameWallThk,	-gs_frame_thk+CFrameWallThk,			15,
				gs_frame_width-CFrameWallThk,	-CFrameWallThk,							15,
				CFrameWallThk,					-CFrameWallThk,							15,
				CFrameWallThk,					-gs_frame_thk/2+CFrameOpeningSize/2,	15,
				0,								-gs_frame_thk/2+CFrameOpeningSize/2,	-1
			del 2
		next i

		add -a/2-gs_frame_width-offsetUpperBoxLength, 0, b
		rotx 90
		roty 90
		prism_ 7, a+gs_frame_width*2+offsetUpperBoxLength,
			0,										upperBoxHeight,							15,
			-upperBoxThick+upperBoxInclPartSize,	upperBoxHeight,							15,
			-upperBoxThick,							upperBoxHeight-upperBoxInclPartSize,	15,
			-upperBoxThick,							upperBoxInclPartSize,					15,
			-upperBoxThick+upperBoxInclPartSize,	0,										15,
			0,										0,										15,
			0,										upperBoxHeight,							-1
		del 3

		add a/2+gs_frame_width+sideBoxWidth, 0, b+upperBoxHeight-sideBoxHeight
		rotz 90
		prism_ 7, sideBoxHeight,
			0,									sideBoxWidth,						15,
			-sideBoxDepth+sideBoxInclPartSize,	sideBoxWidth,						15,
			-sideBoxDepth,						sideBoxWidth-sideBoxInclPartSize,	15,
			-sideBoxDepth,						sideBoxInclPartSize,				15,
			-sideBoxDepth+sideBoxInclPartSize,	0,									15,
			0,									0,									15,
			0,									sideBoxWidth,						-1
		del 2
	endif


	! --------- Leaf

	pen gs_leaf_pen
	sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

	if lod3D <> 5 then
		add -a/2, -gs_frame_thk/2, 0
	else
		add -a/2, 0, 0
	endif
	rotx 90
	roty 90
	addy gs_open3D

	if lod3D = 1 | lod3D = 5 then 			! --------- SIMPLE 3D, Simple CHI
		material gs_leaf_matin
			roty -90
			if lod3D <> 5 then addz -gs_leaf_thk

			cprism_ gs_leaf_matin, gs_leaf_mat, gs_leaf_mat,
				5, gs_leaf_thk,
				0, 0, 15,
				a, 0, 15,
				a, b-gs_open3D, 15,
				0,  b-gs_open3D, 15,
				0, 0, -1

			if lod3D <> 5 then del 1
			del 1
	endif

	if lod3D = 2 then 			! --------- DETAILED 3D
		if gs_glass_section then
			delnum  = 0

			material gs_bottom_part_mat
			prism_ 5,	a,
				0,					0,					15,
				0,					bottomPartHeight,	15,
				bottomPartThick,	bottomPartHeight,	15,
				bottomPartThick,	0,					15,
				0,					0,					-1

			addy bottomPartHeight

			CFrameShorterThick = bottomPartHeight+gs_open3D
			h1 = gs_glass_section_elev-bottomPartHeight

			if gs_glass_section_elev > bottomPartHeight then
				CFrameShorterThick = CFrameShorterThick + h1
				
				if h1+gs_open3D < b then
					material gs_leaf_matin
					prism_ 5,	a,
						0,				0,	15,
						0,				h1,	8,
						gs_leaf_thk/2,	h1,	13,
						gs_leaf_thk/2,	0,	15,
						0,				0,	-1
	
					addx gs_leaf_thk/2
	
					material gs_leaf_mat
					prism_ 5,	a,
						0,				0,	15,
						0,				h1,	8,
						gs_leaf_thk/2,	h1,	13,
						gs_leaf_thk/2,	0,	15,
						0,				0,	-1
	
					del 1
				else
					bottomLeafHeight = b - bottomPartHeight - gs_open3D
					material gs_leaf_matin
					prism_ 5,	a,
						0,				0,	15,
						0,				bottomLeafHeight,	8,
						gs_leaf_thk/2,	bottomLeafHeight,	13,
						gs_leaf_thk/2,	0,	15,
						0,				0,	-1
	
					addx gs_leaf_thk/2
	
					material gs_leaf_mat
					prism_ 5,	a,
						0,				0,	15,
						0,				bottomLeafHeight,	8,
						gs_leaf_thk/2,	bottomLeafHeight,	13,
						gs_leaf_thk/2,	0,	15,
						0,				0,	-1
	
					del 1
				endif
				addy h1
				delnum = delnum + 1
			endif

			if (gs_glass_section_width + CFrameShorterThick) < b then
				pen gs_glass_pen
				material gs_glass_mat
				sect_fill gs_fillGlass, gs_penGlass_bg, gs_penGlass_fg, gs_glass_pen

				prism_ 5,	a,
					0,				0,						15,
					0,				gs_glass_section_width,	15,
					gs_leaf_thk,	gs_glass_section_width,	15,
					gs_leaf_thk,	0,						15,
					0,				0,						-1

				CFrameShorterThick = CFrameShorterThick + gs_glass_section_width
				addy gs_glass_section_width

				pen gs_leaf_pen
				material gs_leaf_matin
				prism_ 5,	a,
					0,				0,						15,
					0,				b-CFrameShorterThick,	15,
					gs_leaf_thk/2,	b-CFrameShorterThick,	15,
					gs_leaf_thk/2,	0,						15,
					0,				0,						-1

				addx gs_leaf_thk/2

				material gs_leaf_mat
				prism_ 5,	a,
					0,				0,						15,
					0,				b-CFrameShorterThick,	15,
					gs_leaf_thk/2,	b-CFrameShorterThick,	15,
					gs_leaf_thk/2,	0,						15,
					0,				0,						-1

				del 2
			else
				if b > gs_glass_section_elev+gs_open3D then
					pen gs_glass_pen
					material gs_glass_mat
					sect_fill gs_fillGlass, gs_penGlass_bg, gs_penGlass_fg, gs_glass_pen
	
					prism_ 5,	a,
						0,			0,						15,
						0,			b-CFrameShorterThick,	15,
						gs_leaf_thk,	b-CFrameShorterThick,	15,
						gs_leaf_thk,	0,						15,
						0,			0,						-1
				endif
			endif

			del delnum + 1
		else
			sect_fill gs_fillLeaf, gs_penLeaf_bg, gs_penLeaf_fg, gs_leaf_pen_2D

			material gs_bottom_part_mat
			prism_ 5,	a,
				0,					0,					15,
				0,					bottomPartHeight,	15,
				bottomPartThick,	bottomPartHeight,	15,
				bottomPartThick,	0,					15,
				0,					0,					-1

			addy bottomPartHeight

			material gs_leaf_matin
			if b-gs_open3D-bottomPartHeight > EPS then
				prism_ 5,	a,
					0,				0,								15,
					0,				b-gs_open3D-bottomPartHeight,	8,
					gs_leaf_thk/2,	b-gs_open3D-bottomPartHeight,	13,
					gs_leaf_thk/2,	0,								15,
					0,				0,								-1

				addx gs_leaf_thk/2

				material gs_leaf_mat
				prism_ 5,	a,
					0,				0,								15,
					0,				b-gs_open3D-bottomPartHeight,	8,
					gs_leaf_thk/2,	b-gs_open3D-bottomPartHeight,	13,
					gs_leaf_thk/2,	0,								15,
					0,				0,								-1

				del 1
			endif
			del 1
		endif
	endif

	del 4
endif
del 1
if lod3D <> 5 then del 1
