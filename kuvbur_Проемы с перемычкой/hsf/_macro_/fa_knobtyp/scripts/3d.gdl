
if not(bifoldDoor)then
	reqBKnobs = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
	if success > 0 and reqBKnobs then
		end
	endif
else
	reqBHandles = 0
	success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBDoorHandles", reqBHandles)
	if success > 0 and reqBHandles then
		end
	endif
endif


if not(gs_shadow) then SHADOW OFF


MATERIAL gs_knob_mat
PEN gs_cont_pen

if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 or gs_detlevel_3D_m = 1 then 		!! for Simple Model and in 3D Windoow
	if GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40 then
		gs_resol = 3
	else
		if gs_knob_type_m=5 or gs_knob_type_m=7 or gs_knob_type_m=8 then gs_resol=8
		if gs_knob_type_m=1 or gs_knob_type_m=2 or gs_knob_type_m=3 or gs_knob_type_m=6 then gs_resol=12
		if gs_knob_type_m=4 then gs_resol=25
	endif
else
	gs_resol = gs_resol
endif

ROTX 90

if gs_knob_type_m=1 then gosub 10
if gs_knob_type_m=2 then gosub 20
if gs_knob_type_m=3 then gosub 30
if gs_knob_type_m=4 then gosub 40
if gs_knob_type_m=5 then gosub 50
if gs_knob_type_m=6 then gosub 60
if gs_knob_type_m=7 then gosub 70
if gs_knob_type_m=8 then gosub 80
if gs_knob_type_m=9 then gosub 90
if gs_knob_type_m=10 then gosub 100

DEL 1
END
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_1 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
10:

RESOL gs_resol
	CYLIND 0.025, 0.012
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_2 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
20:

RESOL gs_resol
	CYLIND 0.015, 0.01
ADDZ 0.015
	CYLIND 0.01, 0.02
DEL 1

RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_3 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
30:
RESOL gs_resol
	CYLIND 0.015, 0.01

ADDZ 0.015
	ELLIPS 0.015, 0.02
DEL 1
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_4 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
40:

!!*************************************************** Parameters

!!R1=0.12
dist1=sqr(0.03*0.21)  	!! x direction
angle1=atn(dist1/0.09)

!!r2=0.085	(inner radius)
dist2=sqr(0.025*0.145)	!! x direction
angle2=atn(dist2/0.06)

!!********************************************************* Knob

RESOL max(2*gs_resol,8)

if gs_knob_p_m = 0  then
muly knob_size/0.127
ROTX 90
ROTY 90
ADDZ -0.005
addx -0.0635

MUL 0.8,0.8,1
CUTPOLYA 4,1,0,
	dist2,0,79,
	0,-0.06,900,
	0,2*angle2,4000,
	-dist2,0,79


	PRISM_ 4,0.01,
		dist1,0,79,
		0,-0.09,900,	!!circle centerpoint
		0,2*angle1,4000,
		-dist1,0,79
CUTEND
del 1
DEL 4

else
mulx knob_size/0.127
ROTX 90
addx -0.0635
ADDZ -0.005

MUL 0.8,0.8,1
CUTPOLYA 4,1,0,
	dist2,0,79,
	0,-0.06,900,
	0,2*angle2,4000,
	-dist2,0,79


	PRISM_ 4,0.01,
		dist1,0,79,
		0,-0.09,900,	!!circle centerpoint
		0,2*angle1,4000,
		-dist1,0,79
CUTEND
del 1
DEL 3
endif

del 1
RETURN


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_5 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
50:

if gs_knob_p_m = 0  then

ADDx 0.006
addy -0.060!0.015+knob_size-0.045
ROTY 90
ROTZ 90
roty 180

RESOL gs_resol

	EXTRUDE  21,     0,     0,      0.012,       1+2+4+16+32,
		knob_size-0.03/2-0.06,     0.018,      1,
		knob_size-0.03/2-0.06,     0.01,    900,
		knob_size-0.03/2-0.105,     -90.0,   4001,
		knob_size-0.03/2-0.052,     0.01,      1,
		knob_size-0.03/2-0.052,     0.0,      1,
		knob_size-0.03/2-0.045,     0.0,      0,
		knob_size-0.03/2-0.045,     0.01,      1,
		knob_size-0.03/2-0.06,     0.01,    900,
		knob_size-0.03/2-0.105,      90.0,   4001,
		knob_size-0.03/2-0.06,     0.025,      1,
	   -0.045,     0.025,      1,
	   -0.045,     0.01,    900,
		0.0,      90.0,   4001,
	   -0.06,     0.01,      1,
	   -0.06,     0.0,      1,
	   -0.053,     0.0,      0,
	   -0.053,     0.01,      1,
	   -0.045,     0.01,    900,
		0.0,     -90.0,   4001,
	   -0.045,     0.018,      1,
		0.045,     0.018,     -1

DEL 4
else
rotz 90
ADDX -0.006
addy 0.06
ROTY 90
ROTZ 90

RESOL gs_resol

	EXTRUDE  21,     0,     0,      0.012,       1+2+4+16+32,
		knob_size-0.03/2-0.06,     0.018,      1,
		knob_size-0.03/2-0.06,     0.01,    900,
		knob_size-0.03/2-0.105,     -90.0,   4001,
		knob_size-0.03/2-0.052,     0.01,      1,
		knob_size-0.03/2-0.052,     0.0,      1,
		knob_size-0.03/2-0.045,     0.0,      0,
		knob_size-0.03/2-0.045,     0.01,      1,
		knob_size-0.03/2-0.06,     0.01,    900,
		knob_size-0.03/2-0.105,      90.0,   4001,
		knob_size-0.03/2-0.06,     0.025,      1,
	   -0.045,     0.025,      1,
	   -0.045,     0.01,    900,
		0.0,      90,   4001,
	   -0.06,     0.01,      1,
	   -0.06,     0.0,      1,
	   -0.053,     0.0,      0,
	   -0.053,     0.01,      1,
	   -0.045,     0.01,    900,
		0.0,     -90,   4001,
	   -0.045,     0.018,      1,
		0.045,     0.018,     -1

DEL 4
endif
RETURN


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_6 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
60:

RESOL gs_resol

if gs_knob_p_m = 0  then

addy -knob_size+0.0025
ROTZ 90
	ELBOW 0.02, 90, 0.0025
ADD 0.02, 0, 0.02
ROTY 90
		CYLIND knob_size-0.045, 0.005
DEL 2
	ADDX knob_size-0.005
ROTZ 180
	ELBOW 0.02, 90, 0.0025
DEL 2

else
	ADDX -knob_size+0.0025
		ELBOW 0.02, 90, 0.0025
	ADD 0.02, 0, 0.02
	ROTY 90
		CYLIND knob_size-0.045, 0.005
	DEL 2
	ADDX knob_size-0.005
	ROTZ 180
		ELBOW 0.02, 90, 0.0025
DEL 2
	del 1
endif
RETURN


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_7 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
70:

if gs_knob_p_m = 0  then

RESOL gs_resol
	ADD 0,-knob_size, 0.027
	ROTX -90
	CYLIND knob_size, 0.003
	DEL 2

	ADD 0,-knob_size+0.025,0.027
	ROTZ 90
	ROTY 90
		ARMC 0.003, 0.0025,0.027,0, 0,90
	ADDZ knob_size-0.05
		ARMC 0.003, 0.0025,0.027,0, 0,90
	DEL 3
DEL 1

else
	addx -0.185
	addx -knob_size+0.015
	ADD 0.17,0.000, 0.027
	ROTX -90
	roty 90
	CYLIND knob_size, 0.003
	DEL 5

	ADD -0.1,-0.002,0.027
	addx -knob_size+0.03
	ROTY 90
	add 0,0.002,0.095
		ARMC 0.003, 0.0025,0.027,0, 0,90
	addz knob_size-0.05
		ARMC 0.003, 0.0025,0.027,0, 0,90
	DEL 2
DEL 2
endif
RETURN

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!! *** Type_8 *** !!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
80:

RESOL gs_resol

if gs_knob_p_m = 0  then

ROTX -90
ROTZ -90
	ADD 0,-0.01,-knob_size!+0.045
		PRISM_ 10, knob_size,
		0,0.02,15,
		0.02,0.02,79,
		1,0,800,
		0.03,0.01,1079,
		0.03,0,15,
		0.025,0,15,
		0.025,0.01,79,
		0,1,800,
		0.02,0.015,1079,
		0,0.015,15
DEL 3
else
	rotz 90
	ROTX -90
	ROTZ -90
	ADD 0,-0.01,0
		PRISM_ 10, knob_size,
			0,0.02,15,
			0.02,0.02,79,
			1,0,800,
			0.03,0.01,1079,
			0.03,0,15,
			0.025,0,15,
			 0.025,0.01,79,
			 0,1,800,
			0.02,0.015,1079,
			0,0.015,15
	DEL 4
endif
RETURN


! ==============================================================================
! Type 9 - Griff Rund
! ==============================================================================

90:

resol gs_resol

knobR = 0.015
knobLength = 0.024
roty -90
revolve 7, 360, 1 + 2 + 16 + 32,
	0, knobR - 0.005, 0,
	knobLength/5, knobR - 0.005, 0,
	knobLength/5, knobR - 0.01, 0,
	knobLength/2, knobR - 0.01, 0,
	knobLength/2, knobR - 0.005, 1,
	knobR/4, -125, 2001,
	knobR, -55, 2001
del 1

return


! ==============================================================================
! Type 9 - Griff Stab
! ==============================================================================

100:

resol gs_resol

handleR = 0.006
handleY = 0.04
add 0, -knob_size/2, handleY
rotx -90
cylind knob_size, handleR
del 2

for hi = 1 to 2
	add 0, -knob_size * 2/3 - 0.01 + knob_size/2, handleY
	rotz 90
	roty 90
	armc handleR, handleR - 0.002, handleY, 0, 0, 90
	del 3
addy knob_size/3 + 0.02
next hi
del 2

return

