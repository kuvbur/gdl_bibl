
reqBMinSpace = 0
reqMSFillType = 28
reqMSFillPen = 4
reqMSFillBackPen = 19
reqMSContLineType = 25
reqMSContLinePen = 1

success = LIBRARYGLOBAL ("LibraryGlobals13", "showBMinSpace", reqBMinSpace)
if success > 0 and reqBMinSpace then
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSFillType", reqMSFillType)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSFillPen", reqMSFillPen)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSFillBackPen", reqMSFillBackPen)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSContLineType", reqMSContLineType)
	success = LIBRARYGLOBAL ("LibraryGlobals13", "MSContLinePen", reqMSContLinePen)
	line_type reqMSContLineType
	pen reqMSContLinePen
	fill reqMSFillType
	if MSType = MINSPACE_NONE 						then goto 1000		!only attributes requests
	if MSType = MINSPACE_FRONT 						then gosub 10		!draw only front minimal space
	if MSType = MINSPACE_SIMPLE_BED 				then gosub 20		!simple bed, bed bunk, designer couch
	if MSType = MINSPACE_DETCHAIR_NO_HOLECUT 		then gosub 30		!detailed chairs and sofas (no hole cut)
	if MSType = MINSPACE_SIMPCHAIR					then gosub 40		!rectangle: simple chairs, couches and tables
	if MSType = MINSPACE_DETSOFA_WITH_HOLECUT 		then gosub 50		!detailed sofas (with hole cut)
	if MSType = MINSPACE_SOFASET_DIFF_SIDES 		then gosub 60		!sofa sets with different MSSides
	if MSType = MINSPACE_ROUND_TABLES 				then gosub 70		!round tables
	if MSType = MINSPACE_PLUMBING_FIXTURES 		then gosub 80		!plumbing fixtures
	if MSType = MINSPACE_PLUMBING_FIXT_CORNER 		then gosub 90		!For plumbing fixtures Corner Objects
	if MSType = MINSPACE_NORM_SHOWER_TRAY 			then gosub 100		!Normal Shower Tray
	if MSType = MINSPACE_NORM_BATH_TUB 			then gosub 110		!Normal bath-tub, Trapezoid Bath-tub
	if MSType = MINSPACE_OFFICE_DESK_TABLE 		then gosub 120		!Office desk tables
	if MSType = MINSPACE_PENTA_CURVED_BATH_TUB 	then gosub 130		!Pentagonal and curved bathtubs with min. space drawn under their shape
	if MSType = MINSPACE_DOUBLE_BED 				then gosub 140		!double bed, bed canopy
	if MSType = MINSPACE_CRIB					 	then gosub 150		!crib
	if MSType = MINSPACE_DESIGNER_SOFA_01		 	then gosub 160		!designer sofa 01
	if MSType = MINSPACE_RECT_APPLIANCES			then gosub 200		!rectangular appliances, kitchen cabinets different sides
	if MSType = MINSPACE_L_SHAPED					then gosub 300		!L-shaped objects
	if MSType = MINSPACE_L_SHAPED_RECT_MS			then gosub 310		!L-shaped objects with rectangular MS
endif
1000:
end  reqBMinSpace, reqMSFillType, reqMSFillPen, reqMSFillBackPen, reqMSContLineType, reqMSContLinePen

10:
!with front parameter
	if MSFront > EPS then
		POLY2_B 5, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			0,0,1,
			0,-MSFront,1,
			a,-MSFront,1,
			a,0+diff,0,
			0,0,-1

		HOTSPOT2 0,-MSFront , unID : unID = unID+1
		HOTSPOT2 a,-MSFront , unID : unID = unID+1
	endif
return

20:
!beds
	if MSSide > EPS or MSFront > EPS or MSLength > EPS or MSSide > EPS or MSSide2 > EPS then
		PUT -MSSide,		-MSFront, 	1
		PUT A + MSSide2, 	-MSFront, 	1
		if MSSide2 > EPS then
			PUT A + MSSide2, 	MSLength + diff2, 	1
			PUT A,				MSLength + diff2, 	0

			HOTSPOT2 A, 			MSLength + diff2, 	unID : unID = unID+1
		endif
		PUT A,				0, 			0
		PUT 0, 				0, 			0
		PUT 0, 				MSLength + diff2, 	1
		if ABS(diff2) > EPS and ABS(diff3) > EPS then
			PUT -MSSide + diff3, 	MSLength + diff2, 	1
			PUT -MSSide + diff3, 	MSLength, 			1
			PUT -MSSide, 			MSLength, 			1
		else
			PUT -MSSide, MSLength, 1
		endif
		PUT -MSSide, -MSFront, -1

		POLY2_B NSP/3, 1+2+4,reqMSFillPen,reqMSFillBackPen,
				USE(NSP)

		while NSP > 2 do
			xHotspot = GET(1)
			yHotspot = GET(1)
			status = GET(1)
			if status = 1 then
				HOTSPOT2 xHotspot, yHotspot, unID : unID = unID+1
			endif
		endwhile
	endif
return

30:
!detailed chairs and sofas (no hole cut)
	POLY2_B 5, 1+2+4,reqMSFillPen,reqMSFillBackPen,
			-MSSide, 	-MSFront,	1,
			A+MSSide, 	-MSFront,	1,
			A+MSSide, 	B+MSRear,	1,
			-MSSide, 	B+MSRear,	1,
			-MSSide, 	-MSFront,	-1

	HOTSPOT2 -MSSide, -MSFront, 	unID: unID=unID+1
	HOTSPOT2 -MSSide, B+MSRear, 	unID: unID=unID+1
	HOTSPOT2  A+MSSide, -MSFront, 	unID: unID=unID+1
	HOTSPOT2  A+MSSide, B+MSRear, 	unID: unID=unID+1
return

40:
!rectangle: simple chairs, couches and tables
	MSSide2 = MSSide
	gosub "rectangleMS"
return

50:
!detailed sofas (with hole cut)
	if MSFront > EPS or MSSide > EPS or MSRear > EPS then
		POLY2_B 14, 1+2+4, reqMSFillPen, reqMSFillBackPen,
				-MSSide, 	-MSFront, 	1,
				-MSSide, 	B+MSRear, 	1,
				A+MSSide, 	B+MSRear, 	1,
				A+MSSide, 	-MSFront, 	1,
				-MSSide, 	-MSFront, 	-1,
				0, 			diff, 		0,
				0, 			B, 			0,
				A, 			B, 			0,
				A, 			diff, 		0,
				A-diff2, 	diff, 		0,
				A-diff2, 	0-diff3, 	0,
				diff2, 		0-diff3, 	0,
				diff2, 		diff, 		0,
				0, 			diff, 		-1

		HOTSPOT2 -MSSide, -MSFront, unID: unID=unID+1
		HOTSPOT2 -MSSide, B+MSRear, unID: unID=unID+1
		HOTSPOT2  A+MSSide, -MSFront, unID: unID=unID+1
		HOTSPOT2  A+MSSide, B+MSRear, unID: unID=unID+1
		HOTSPOT2  A/2, -MSFront, unID: unID=unID+1
	endif
return

60:
!different sides for sofa set
	gosub "rectangleMS"
return

70:
!round tables
	if MSFront > EPS then
		POLY2_B 4, 1+2+4,reqMSFillPen,reqMSFillBackPen,
				0,				0,		900,
				a/2+MSFront,	360,	4001,
				0,				0,		900,
				a/2,			360,	4000


		hotspot2 0,				a/2+MSFront, 	unID: unID=unID+1
		hotspot2 -a/2-MSFront,	0, 				unID: unID=unID+1
		hotspot2 0,				-a/2-MSFront,	unID: unID=unID+1
		hotspot2 a/2+MSFront,	0,				unID
	endif
return

80:
!Plumbing fixtures

!MSRear = 0
mul2 -1, 1
add2 -a/2, -b
gosub "rectangleMS"
del 2

return

90:
!Plumbing fixtures for corner objects
	if MSFront > EPS or MSSide > EPS or MSSide2 > EPS then
		POLY2_B 9, 1+2+4,reqMSFillPen,reqMSFillBackPen,
				-a/2-MSSide,	-b-MSFront,1,
				-a/2-MSSide,	-a/2-MSSide,1,
				-a/2,			-a/2,0,
				-a/2,			-b,0,
				a/2,			-b,0,
				a/2,			-a/2,1,
				a/2+MSSide2,		-a/2-MSSide2,1,
				a/2+MSSide2,		-b-MSFront,1,
				-a/2-MSSide,	-b-MSFront,-1

		HOTSPOT2 a/2+MSSide2, -a/2-MSSide2, unID: unID=unID+1
		HOTSPOT2 -a/2-MSSide, -b-MSFront, unID: unID=unID+1
		HOTSPOT2  a/2+MSSide2, -b-MSFront, unID: unID=unID+1
		HOTSPOT2  -a/2-MSSide, -a/2-MSSide, unID: unID=unID+1
	endif
return

100:
! Normal Shower tray
	if MSFront > EPS then
		poly2_b 6, 1+2+4, reqMSFillPen, reqMSFillBackPen,
				-a/2,						b,				0,
				 a/2,						b,				1,
				 a/2 + MSAdditionalLength,	b,				1,
				 a/2 + MSAdditionalLength,	b + MSFront,	1,
				-a/2,						b + MSFront,	1,
				-a/2,						b, 				-1

		hotspot2 -a/2,						b,				unID: unID = unID + 1
		hotspot2  a/2 + MSAdditionalLength,	b,				unID: unID = unID + 1
		hotspot2  a/2 + MSAdditionalLength,	b + MSFront,	unID: unID = unID + 1
		hotspot2 -a/2,						b + MSFront,	unID: unID = unID + 1
	endif
return

110:
!Normal Bath-Tub, Trapezoid Bath-tub
	if MSFront > EPS or (MSFront2 > EPS and MSLength > EPS)then
		poly2_b 7, 1+2+4, reqMSFillPen, reqMSFillBackPen,
				0,				 0,			0,
				a,				 0,			1,
				a,				-MSFront2,	1,
				a - MSLength,	-MSFront2,	1,
				a - MSLength,	-MSFront,	1,
				diff,			-MSFront,	1,
				0,				 0,			-1

		hotspot2 a,				-MSFront2,	unID: unID = unID + 1
		hotspot2 a - MSLength,	-MSFront2,	unID: unID = unID + 1
		hotspot2 a - MSLength,	-MSFront,	unID: unID = unID + 1
		hotspot2 diff,			-MSFront,	unID: unID = unID + 1
	endif
return

120:
!office desk tables
	if MSFront > EPS and MSLength > EPS then
		if MSLength > A then
			put MSLength,0, 1,
				A, 		 0, 0
		else
			put MSLength,0, 0
		endif

		POLY2_B 4+nsp/3, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			0,0,1,
			0,-MSFront,1,
			MSLength,-MSFront,1,
			use (nsp),
			0,0,-1

		HOTSPOT2 0,-MSFront , unID : unID = unID+1
		HOTSPOT2 MSLength,-MSFront , unID : unID = unID+1
		if MSLength > A then
			HOTSPOT2 MSLength,0 , unID : unID = unID+1
		endif
	endif
return


130:
!Pentagonal and curved bathtubs with min. space drawn under their shape

	if MSFront > EPS and MSSide > EPS then

		poly2_B 8, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			MSSide,		0,			1,
			MSSide,		MSFront,	1,
			-MSSide,	MSFront,	1,
			-MSSide,	0,			1,
			-MSSide2,	0,			1,
			-MSSide2,	-MSRear,	1,
			MSSide2,	-MSRear,	1,
			MSSide2,	0,			1

		hotspot2 MSSide,	MSFront,	unID: unID=unID+1
		hotspot2 -MSSide,	MSFront,	unID: unID=unID+1

		if abs(MSSide - MSSide2) > EPS then
			hotspot2 MSSide,	0,			unID: unID=unID+1
			hotspot2 -MSSide,	0,			unID: unID=unID+1
			hotspot2 MSSide2,	0,			unID: unID=unID+1
			hotspot2 -MSSide2,	0,			unID: unID=unID+1
		endif

	endif
return

140:
!double bed, bed canopy
	if MSSide > EPS or MSFront > EPS or MSLength > EPS or MSSide > EPS or MSSide2 > EPS then
		PUT -MSSide,		-MSFront, 	1
		PUT A + MSSide2, 	-MSFront, 	1
		if MSSide2 > EPS then
			PUT A + MSSide2, 	MSLength, 	1
			PUT A,				MSLength, 	0

			HOTSPOT2 A, 		MSLength, 	unID : unID = unID+1
		endif
		PUT A,				0, 			0
		PUT 0, 				0, 			0
		PUT 0, 				MSLength, 	1
		PUT -MSSide, 		MSLength, 	1
		PUT -MSSide, 		-MSFront, 	-1

		POLY2_B NSP/3, 1+2+4,reqMSFillPen,reqMSFillBackPen,
				USE(NSP)

		while NSP > 2 do
			xHotspot = GET(1)
			yHotspot = GET(1)
			status = GET(1)
			if status = 1 then
				HOTSPOT2 xHotspot, yHotspot, unID : unID = unID+1
			endif
		endwhile
	endif
return

150:
!crib
	gosub "rectangleMS"
return

160:
!detailed designer sofa 01
	if MSFront > EPS or MSSide > EPS or MSRear > EPS then
		POLY2_B 14, 1+2+4, reqMSFillPen, reqMSFillBackPen,
				-MSSide, 	-MSFront, 	1,
				-MSSide, 	B+MSRear, 	1,
				A+MSSide, 	B+MSRear, 	1,
				A+MSSide, 	-MSFront, 	1,
				-MSSide, 	-MSFront, 	-1,
				0, 			-0.03, 		0,
				0, 			B, 			0,
				A, 			B, 			0,
				A, 			-0.03, 		0,
				A-0.105, 	-0.03, 		0,
				A-0.105, 	0-diff3, 	0,
				0.105, 		0-diff3, 	0,
				0.105, 		-0.03, 		0,
				0, 			-0.03, 		-1

		HOTSPOT2 -MSSide, -MSFront, unID: unID=unID+1
		HOTSPOT2 -MSSide, B+MSRear, unID: unID=unID+1
		HOTSPOT2  A+MSSide, -MSFront, unID: unID=unID+1
		HOTSPOT2  A+MSSide, B+MSRear, unID: unID=unID+1
		HOTSPOT2  A/2, -MSFront, unID: unID=unID+1
	endif
return

200:
!rectangular appliances, kitchen cabinets different sides
	if MSFront > EPS then
		POLY2_B 5, 1+2+4,reqMSFillPen,reqMSFillBackPen,
				-MSSide2, 	-MSFront, 	1,
				-MSSide2, 	0, 	1,
				A+MSSide, 	0, 	1,
				A+MSSide, 	-MSFront, 	1,
				-MSSide2, 	-MSFront, 	-1

		HOTSPOT2 -MSSide2, 	-MSFront, 	unID: unID=unID+2
		HOTSPOT2  A+MSSide, -MSFront, 	unID: unID=unID+2

		if dob_face then
			gosub "dobface"
		endif
	endif
return

300:
!L-shaped objects
!diff: depth of object Y
!diff2:depth of object X
	if MSFront > EPS then
		POLY2_B 7, 1+2+4, reqMSFillPen, reqMSFillBackPen,
				diff2, (b-diff < MSFront)*(b-diff-MSFront),1,
				diff2, b-diff,1,
				a+(a-diff2 < MSFront)*(diff2+MSFront-a), b-diff,1,
				a+(a-diff2 < MSFront)*(diff2+MSFront-a),b-diff-MSFront,1,
				diff2+MSFront,b-diff-MSFront,1,
				diff2+MSFront,(B-diff < MSFront)*(b-diff-MSFront),1,
				diff2, (b-diff < MSFront)*(b-diff-MSFront),-1

		if a-diff2 < MSFront then 	HOTSPOT2 a+(diff2+MSFront-a), b-diff+0.01,		unID: unID=unID+1
		if a-diff2 >= MSFront then 	HOTSPOT2 a, b-diff-MSFront,						unID: unID=unID+1
		if b-diff < MSFront then 	HOTSPOT2 diff2+0.01, (b-diff-MSFront),			unID: unID=unID+1
		if b-diff >= MSFront then 	HOTSPOT2 diff2+MSFront, 0,						unID: unID=unID+1
		if	(a-diff2 >= MSFront and b-diff >= MSFront) or (a-diff2 < MSFront and b-diff < MSFront) then
			HOTSPOT2 diff2+MSFront, b-diff-MSFront,	unID: unID=unID+1
		endif
		if dob_face then
			gosub "dobface"
		endif
	endif
return

310:
!L-shaped objects with rectangular MS
!diff: depth of object Y
!diff2:depth of object X
	if MSFront > EPS then
		POLY2_B 5, 1+2+4, reqMSFillPen, reqMSFillBackPen,
				diff2, b-diff-MSFront, 1,
				diff2, b-diff, 1,
				a, b-diff, 1,
				a, b-diff-MSFront, 1,
				diff2, b-diff-MSFront, -1

		HOTSPOT2 diff2, b-diff-MSFront, unID : unID = unID + 1
		HOTSPOT2 a, b-diff-MSFront, unID : unID = unID + 1
		if dob_face then
			gosub "dobface"
		endif
	endif
return

"dobface":
	if dob_face_type = 0 then
		POLY2_B 5, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			-RMSSide, B, 1,
			-RMSSide, B + MSRear, 1,
			A + RMSSide2, B + MSRear, 1,
			A + RMSSide2, B, 1,
			-RMSSide, B, -1
			
		HOTSPOT2 -RMSSide,b+MSRear, unID : unID=unID+1
		HOTSPOT2 a+RMSSide2,b+MSRear, unID : unID=unID+1
	endif
	
	if dob_face_type = 1 then
		POLY2_B 5, 1+2+4, reqMSFillPen, reqMSFillBackPen,
			0, 0, 1,
			0, b, 1,
			-MSRear, b, 1,
			-MSRear, 0,1,
			0,0,-1

		HOTSPOT2 -MSRear, b,	unID: unID=unID+1
		HOTSPOT2 -MSRear, 0,	unID: unID=unID+1
	endif
return

"rectangleMS":
	if MSFront > EPS or MSSide > EPS or MSSide2 > EPS or MSRear > EPS then
		if MSFront > EPS then
			POLY2_B 5, 1+2+4,reqMSFillPen,reqMSFillBackPen,
					0,	-MSFront,	1,
					A, 	-MSFront, 	(MSSide < EPS),
					A, 	0, 			0,
					0,	0,			(MSSide2 < EPS),
					0,	-MSFront, 	-1
		endif

		if MSSide2 > EPS then			! MSSide2 for sofa sets
			POLY2_B 5, 1+2+4,reqMSFillPen,reqMSFillBackPen,
					0, 		 -MSFront,  0,
					0, 		 B+MSRear,  1,
					-MSSide2, B+MSRear,  1,
					-MSSide2, -MSFront,  1,
					0,		 -MSFront, -1
		endif

		if MSSide > EPS then
			POLY2_B 5, 1+2+4,reqMSFillPen,reqMSFillBackPen,
					A, 		  -MSFront, 1,
					A+MSSide, -MSFront, 1,
					A+MSSide, B+MSRear, 1,
					A,		  B+MSRear, 0,
					A, 		  -MSFront, -1
		endif

		if MSRear > EPS then
			POLY2_B 5, 1+2+4,reqMSFillPen,reqMSFillBackPen,
					0,	B,			(MSSide2 < EPS),
					0,	B+MSRear, 	1,
					A, 	B+MSRear, 	(MSSide < EPS),
					A,	B,			0,
					0,	B, 			-1
		endif

		HOTSPOT2 -MSSide2, -MSFront, 	unID: unID=unID+1
		HOTSPOT2 -MSSide2, B+MSRear, 	unID: unID=unID+1
		HOTSPOT2  A+MSSide, -MSFront, 	unID: unID=unID+1
		HOTSPOT2  A+MSSide, B+MSRear, 	unID: unID=unID+1
		HOTSPOT2  A/2, -MSFront, 		unID: unID=unID+1
	endif
return
