
gs_detlevel_3D_m = 2
success = LIBRARYGLOBAL ("Libraryflobals-as", "gs_detlevel_3D_m", gs_detlevel_3D_m)
if gs_detlevel_3D_m = 2 then 
	gs_shadow=1
	res = 20
else
	gs_shadow=0
	res = 6
endif

IF lod3D = 0 THEN END
IF GLOB_CONTEXT>20 THEN lod3D = 1

PEN gs_leaf_pen
MATERIAL gs_leaf_mat
RESOL res


!! PANEL 1-2-3 !!
IF gs_mot_m<4 THEN

	IF lod3D = 2 THEN
		MATERIAL gs_li_mat1

		FOR GM=1 TO gs_section_num
			FOR GG=1 TO 2
				ADD a/2+gs_pv, gs_gm/2+0.01, gs_leaf_thk-gs_gtav-0.0075
				ROTY 90
				CYLIND gs_gm-0.01-gs_pv, 0.0075
				ADDZ gs_gm-0.01-gs_pv
				CYLIND 0.01, gs_gm/2
				DEL 1
				ADDZ -gs_pv
				add -0.0075, -0.01, 0
				PRISM_ 8, gs_pv,
						-gs_gtav, gs_gm/2, 15,
						gs_gm/2+0.01, gs_gm/2, 15,
						gs_gm/2+0.01, -gs_gm/2, 15,
						-gs_gtav+gs_leaf_thk, -gs_gm/2, 15,
						-gs_gtav+gs_leaf_thk, 0, 15,
						-gs_gtav+gs_bv, 0, 15,
						-gs_gtav, gs_bv, 15,
						-gs_gtav, gs_gm/2, -1
				DEL 2
				DEL 2
				MULX -1
			NEXT GG
			DEL 2
			ADDY gs_szekc_m
		NEXT GM
		DEL gs_section_num

		FOR GG=1 TO 2
			ADD a/2+gs_pv, b+gs_gm/2, gs_leaf_thk-0.0055
			ROTY 90
			CYLIND gs_gm-0.01-gs_pv, 0.0075
			ADDZ gs_gm-0.01-gs_pv
			CYLIND 0.01, 0.015
			DEL 1

			ADDZ -gs_pv
			PRISM_ 7, gs_pv,
					-0.01, 0.01, 15,
					gs_leaf_thk-0.0055, 0.01, 15,
					gs_leaf_thk-0.0055, -gs_gm, 15,
					-0.0055+gs_bv, -gs_gm, 15,
					-0.0055+gs_bv, -0.01, 15,
					-0.01, -0.01, 15,
					-0.01, 0.01, -1
			DEL 1

			DEL 2
		MULX -1
		NEXT GG
		DEL 2

	ENDIF


	!!! Alsó tömítés !!!
	MATERIAL gs_leaf_matin
	CPRISM_ gs_leaf_mat, gs_li_mat1, gs_li_mat1, 5, gs_leaf_thk-gs_bv,
			-a/2, 0, 15,
			a/2, 0, 15,
			a/2, gs_gm/2, 15,
			-a/2, gs_gm/2, 15,
			-a/2, 0, -1

	!!! Alsó tömítés vége!!!


	!! PANEL !!
	IF gs_passdoor=1 THEN
		MATERIAL gs_li_mat1
		CUTPOLYA 5, 2, 0,
				a/2-gs_bkx/2-gs_passdoorWidth, 	gs_gm/2+gs_bky/2, 	7,
				a/2-gs_bkx/2, 		gs_gm/2+gs_bky/2, 	7,
				a/2-gs_bkx/2, 		b-gs_bky/2, 		7,
				a/2-gs_bkx/2-gs_passdoorWidth, 	b-gs_bky/2, 		7,
				a/2-gs_bkx/2-gs_passdoorWidth, 	gs_gm/2+gs_bky/2, 	-1
	ENDIF


	ADDY gs_gm/2
	hah = 0
	DIM he[5]
	FOR hi=1 TO 5
		he[hi] = 0
	NEXT hi

	FOR KAZ=1 TO gs_section_num

		IF gs_main_han_height<gs_szekc_m AND KAZ=1 THEN
			he[1]=1
			hah=gs_main_han_height-gs_gm/2
		ENDIF
		IF gs_main_han_height>gs_szekc_m AND gs_main_han_height<2*gs_szekc_m AND KAZ=2 THEN
			he[2]=1
			hah=gs_main_han_height-gs_szekc_m-gs_gm/2
		ENDIF
		IF gs_main_han_height>2*gs_szekc_m AND gs_main_han_height<3*gs_szekc_m AND KAZ=3 THEN
			he[3]=1
			hah=gs_main_han_height-2*gs_szekc_m-gs_gm/2
		ENDIF
		IF gs_main_han_height>3*gs_szekc_m AND gs_main_han_height<4*gs_szekc_m AND KAZ=4 THEN
			he[4]=1
			hah=gs_main_han_height-3*gs_szekc_m-gs_gm/2
		ENDIF
		IF gs_main_han_height>4*gs_szekc_m AND gs_main_han_height<5*gs_szekc_m AND KAZ=5 THEN
			he[5]=1
			hah=gs_main_han_height-4*gs_szekc_m-gs_gm/2
		ENDIF

		bGlazedSection = gs_bGlazedDoor * (KAZ = gs_section_num)
		bDoBottomCut = (KAZ <> 1)

		gosub "sectGarInlay"

		ADDY gs_szekc_m
	NEXT KAZ
	DEL gs_section_num
	DEL 1

	! =============================================================================
	! Kick Board
	! =============================================================================
	if bEnableKickBoard then
		addz gs_leaf_thk / 2

		gosub "KickBoard"

		del 1
	endif

	IF gs_passdoor=1 THEN CUTEND
	!! END OF PANEL !!
ENDIF
!! END OF PANEL 1-2-3 !!



!! CUSTOM PANEL !!
IF gs_mot_m=4 THEN
	PEN gs_leaf_pen
	SECT_FILL gs_psect, gs_sfbpen, gs_sfpen, gs_leaf_pen

	IF gs_passdoor=1 THEN
		MATERIAL gs_li_mat1
		CUTPOLYA 5, 2, 0,
				a/2-gs_bkx/2-gs_passdoorWidth,	gs_gm/2+gs_bky/2, 	7,
				a/2-gs_bkx/2,					gs_gm/2+gs_bky/2, 	7,
				a/2-gs_bkx/2,					b-gs_bky/2, 		7,
				a/2-gs_bkx/2-gs_passdoorWidth,	b-gs_bky/2, 		7,
				a/2-gs_bkx/2-gs_passdoorWidth,	gs_gm/2+gs_bky/2, 	-1
	ENDIF

	IF gs_cust_p<>"" THEN
		ADDX -a/2
		CALL "Door_Panel_Collection" PARAMETERS a=a, b=b, zzyzx=gs_leaf_thk, gs_ptype=gs_cust_p
		DEL 1
	ENDIF

	! =============================================================================
	! Kick Board
	! =============================================================================
	if bEnableKickBoard then
		addz gs_leaf_thk / 2

		gosub "KickBoard"

		del 1
	endif

	IF gs_passdoor=1 THEN CUTEND
ENDIF
!! END OF CUSTOM PANEL !!


! ------------------------------------------------------------------------------
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end
! ------------------------------------------------------------------------------


"KickBoard":
	_kickBoardThickness	= 0.002
	_kickBoardWidth		= a - 0.02

	addz -gs_leaf_thk/2 - _kickBoardThickness

	if bKickBoardIn then		! Inside
		material matKickBoardIn

		prism_ 5, _kickBoardThickness,
			-_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	0.01,				-1
	endif

	addz gs_leaf_thk + _kickBoardThickness

	if bKickBoardOut then		! Outside
		material matKickBoardOut

		prism_ 5, _kickBoardThickness,
			-_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	0.01,				15,
			_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	kickBoardHeight,	15,
			-_kickBoardWidth/2,	0.01,				-1
	endif

	del 2
return


"sectGarInlay":
	gs_hor_m=0.05
	gs_hor_sz=0.02
	gs_pv=0.004

	IF GLOB_CONTEXT>20 THEN lod3D = 1

	PEN gs_leaf_pen
	MATERIAL gs_leaf_mat
	RESOL res

	if lod3D = 1 then
		gosub "simple3D"
	else
		gosub "detailed3D"
	endif

return

! ------------------------------------------------------------------------------
! Simple 3D Inlay
! ------------------------------------------------------------------------------
"simple3D":

	numCuts = 0
	if not(_bSimplePanel) then
		ADD -a/2+gs_bet_sz/2+gs_bkx, gs_bky, 0
		IF gs_mot_m=1 OR (BU=gs_ver_pnum AND bGlazedSection=1 AND (gs_mot_m=2 OR gs_mot_m=3)) THEN
			FOR CT=1 TO gs_hor_pnum
				FOR BU=1 TO gs_ver_pnum
					CUTPOLYA 5, 2, 0,
						-gs_bet_sz/2, 	0, 			6+1*(BU=gs_ver_pnum AND bGlazedSection=1),
						gs_bet_sz/2, 	0, 			6+1*(BU=gs_ver_pnum AND bGlazedSection=1),
						gs_bet_sz/2, 	gs_bet_m, 	6+1*(BU=gs_ver_pnum AND bGlazedSection=1),
						-gs_bet_sz/2, 	gs_bet_m, 	6+1*(BU=gs_ver_pnum AND bGlazedSection=1),
						-gs_bet_sz/2, 	0, 			-1
	
					numCuts = numCuts + 1
					ADDY gs_bet_m+gs_bky
				NEXT BU
				DEL gs_ver_pnum
	
				ADDX gs_bet_sz+gs_bkx
			NEXT CT
			DEL gs_hor_pnum
		ENDIF
		DEL 1
	endif
	
	ADD -a/2, gs_szekc_m/2, 0
	ROTY 90
	ROTZ 90

	SPRISM_{2} gs_li_mat1, gs_li_mat1, gs_li_mat1, 7,
			0, 0, 1, 0, a, 0,
			0, 0, 1, 0, 0, 0,
		  -gs_szekc_m/2, 0, 15, gs_li_mat1,
		  gs_szekc_m/2, 0, 15, gs_li_mat1,
		  gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_leaf_mat,
		  gs_szekc_m/2-gs_bv, gs_leaf_thk, 15, gs_leaf_mat,
		  -gs_szekc_m/2+gs_bv, gs_leaf_thk, 15, gs_leaf_mat,
		  -gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_li_mat1,
		  -gs_szekc_m/2, 0, -1, gs_li_mat1
	DEL 3

	for i = 1 to numCuts
		cutend
	next i

	if not(_bSimplePanel) then
		ADD -a/2+gs_bet_sz/2+gs_bkx, gs_bky, 0
	
		FOR BT=1 TO gs_hor_pnum
			FOR BU=1 TO gs_ver_pnum
				IF (bGlazedSection=0 OR (BU<gs_ver_pnum AND bGlazedSection>0)) AND gs_mot_m=1 THEN
					CPRISM_ gs_leaf_mat, gs_li_mat1, gs_leaf_mat, 5, gs_leaf_thk-0.005,
					   -gs_bet_sz/2, 	0, 			14,
					   gs_bet_sz/2, 	0, 			14,
					   gs_bet_sz/2, 	gs_bet_m, 	14,
					   -gs_bet_sz/2, 	gs_bet_m, 	14,
					   -gs_bet_sz/2, 	0, 			-1
				ENDIF
	
				ADDY gs_bet_m+gs_bky
			NEXT BU
			DEL gs_ver_pnum
	
			ADDX gs_bet_sz+gs_bkx
		NEXT BT
		DEL gs_hor_pnum
	
		DEL 1
	endif

	IF not(_bSimplePanel) and bGlazedSection=1 THEN
		ADD -a/2+gs_bet_sz/2+gs_bkx, rah, -0.01

		FOR iHorPanel = 1 TO gs_hor_pnum
			gosub "topPanelGlazing"

			ADDX gs_bet_sz+gs_bkx
		NEXT iHorPanel
		DEL gs_hor_pnum
		DEL 1
	ENDIF
return


! ------------------------------------------------------------------------------
! Detailed 3D Inlay
! ------------------------------------------------------------------------------

"detailed3D":

	! --- Handle ---------------------------------------------------------------
	if lod3D = 2 & KAZ = 1 then
		if bINTHandles then
			add 0, hah, gs_leaf_thk
			mulx -1
			call "DoorHandle_kmod" parameters A=A,
				bDifferentHandles		= 0,
				bSecondHandle			= 0,
				gs_handle_type_m		= gs_handle_typ_m,
				gs_cust_handle			= gs_cust_handle,
				gs_leaf_thk				= gs_leaf_thk,
				gs_handle_mat			= gs_handle_mat,
				gs_handle_ac_mat		= gs_handle_ac_mat,
				gs_han_pos				= 0,
				gs_han_height			= gs_han_height,
				horizontalBarLength		= 0.3,
				horizontalBarPos		= -0.3/2,
				verticalBarLength		= 0.3,
				verticalBarHeight		= gs_han_height - 0.3/2
			del 2
		else
			add -0, hah, gs_leaf_thk
			rotz -90
			if gs_handle_typ_m = 99 then
				if gs_cust_handle<>"" then
					reqBHandles = 0
					success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBDoorHandles", reqBHandles)
					if success > 0 and not(reqBHandles) then
						call "DW_Hardware_Collection" PARAMETERS gs_ptype = gs_cust_handle
					endif
				endif
			else
				call "da_handletyp_kmod" parameters	gs_handle_typ_m		= gs_handle_typ_m,
												gs_detlevel_3D_m	= lod3D,
												res					= res,
												gs_shadow			= gs_shadow,
												gs_handle_mat		= gs_handle_mat,
												gs_handle_ac_mat	= gs_handle_ac_mat
			endif
			del 2
		endif
	endif

	! --- Cutting on Bottom ----------------------------------------------------

	if bDoBottomCut and lod3D = 2 then
		addz gs_leaf_thk/2
		roty 90
		cutpolya 5, 1, 0,
			-gs_leaf_thk/2+gs_bv, 0, 15,
			gs_hor_sz/2, 0, 15,
			gs_hor_sz/4, gs_hor_m, 15,
			-gs_hor_sz/4, gs_hor_m, 15,
			-gs_leaf_thk/2+gs_bv, 0, -1
		del 2
	endif

	! --- Door panel type selecton ---------------------------------------------

	if gs_mot_m = 1 then
		gosub "casettedType"
	else
		if gs_mot_m = 2 then
			gosub "verticalRibbed"
		else
			if gs_mot_m = 3 then
				gosub "horizontalRibbed"
			else
				gosub "customPanel"
			endif
		endif
	endif

	if bDoBottomCut and lod3D = 2 then cutend
return

! ------------------------------------------------------------------------------
! Casetted Door Type
! ------------------------------------------------------------------------------
"casettedType":

	numCuts = 0
	if not(_bSimplePanel) then
		ADD -a/2+gs_bet_sz/2+gs_bkx, gs_bky, 0
		FOR CT=1 TO gs_hor_pnum
			FOR BU=1 TO gs_ver_pnum
	
				CUTPOLYA 5, 2, 0,
						-gs_bet_sz/2, 	0, 			11,
						gs_bet_sz/2, 	0, 			11,
						gs_bet_sz/2, 	gs_bet_m, 	11,
						-gs_bet_sz/2, 	gs_bet_m, 	11,
						-gs_bet_sz/2, 	0, 			-1
	
				numCuts = numCuts + 1
	
				ADDY gs_bet_m+gs_bky
			NEXT BU
			DEL gs_ver_pnum
	
			ADDX gs_bet_sz+gs_bkx
		NEXT CT
		DEL gs_hor_pnum
		DEL 1
	endif

	ADD -a/2, gs_szekc_m/2, 0
	ROTY 90
	ROTZ 90

	SPRISM_{2} gs_li_mat1, gs_li_mat1, gs_li_mat1, 7,
			0, 0, 1, 0, a, 0,
			0, 0, 1, 0, 0, 0,
		  -gs_szekc_m/2, 0, 15, gs_li_mat1,
		  gs_szekc_m/2, 0, 15, gs_li_mat1,
		  gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_leaf_mat,
		  gs_szekc_m/2-gs_bv, gs_leaf_thk, 15, gs_leaf_mat,
		  -gs_szekc_m/2+gs_bv, gs_leaf_thk, 15, gs_leaf_mat,
		  -gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_li_mat1,
		  -gs_szekc_m/2, 0, -1, gs_li_mat1
	DEL 3

	FOR CE=1 TO numCuts 		!gs_hor_pnum*gs_ver_pnum
	   CUTEND
	NEXT CE

	if not(_bSimplePanel) then
		ADD -a/2+gs_bet_sz/2+gs_bkx, gs_bky, 0

		FOR BT=1 TO gs_hor_pnum
			FOR BU=1 TO gs_ver_pnum
				IF bGlazedSection=0 OR (BU<gs_ver_pnum AND bGlazedSection>0) THEN
					CPRISM_ gs_leaf_mat, gs_li_mat1, gs_leaf_mat, 10, gs_leaf_thk-0.005,
					   -gs_bet_sz/2, 	0, 			14,
					   gs_bet_sz/2, 	0, 			14,
					   gs_bet_sz/2, 	gs_bet_m, 	14,
					   -gs_bet_sz/2, 	gs_bet_m, 	14,
					   -gs_bet_sz/2, 	0, 			-1,
	
					   -gs_bet_sz/2+0.01, 	0.01, 			14,
					   gs_bet_sz/2-0.01, 	0.01, 			14,
					   gs_bet_sz/2-0.01, 	gs_bet_m-0.01, 	14,
					   -gs_bet_sz/2+0.01, 	gs_bet_m-0.01, 	14,
					   -gs_bet_sz/2+0.01, 	0.01, 			-1
	
					FPRISM_ gs_leaf_mat, gs_li_mat1, gs_leaf_mat, gs_leaf_mat,
					   5, gs_leaf_thk-0.005, ATN(0.005/0.06), 0.005,
					   -gs_bet_sz/2+0.01, 0.01, 14,
					   gs_bet_sz/2-0.01, 0.01, 14,
					   gs_bet_sz/2-0.01, gs_bet_m-0.01, 14,
					   -gs_bet_sz/2+0.01, gs_bet_m-0.01, 14,
					   -gs_bet_sz/2+0.01, 0.01, -1
				ENDIF
	
				ADDY gs_bet_m+gs_bky
			NEXT BU
			DEL gs_ver_pnum
	
			ADDX gs_bet_sz+gs_bkx
		NEXT BT
		DEL gs_hor_pnum

		DEL 1
	endif

	IF bGlazedSection and not(_bSimplePanel) THEN
		ADD -a/2+gs_bet_sz/2+gs_bkx, (gs_bky+gs_bet_m)*(gs_ver_pnum-1)+gs_bky, -0.01

		FOR iHorPanel=1 TO gs_hor_pnum
			gosub "topPanelGlazing"

			ADDX gs_bet_sz+gs_bkx
		NEXT iHorPanel
		DEL 1 + gs_hor_pnum
	ENDIF
return


! ------------------------------------------------------------------------------
! Vertical Ribbed Door Type
! ------------------------------------------------------------------------------
"verticalRibbed":
	IF bGlazedSection=1 THEN
		ADD -a/2+gs_bet_sz/2+gs_bkx, rah, 0
		FOR CT=1 TO gs_hor_pnum
			CUTPOLYA 5, 2, 0,
			   -gs_bet_sz/2, 	0, 			15,
			   gs_bet_sz/2, 	0, 			15,
			   gs_bet_sz/2, 	gs_bet_m, 	15,
			   -gs_bet_sz/2,	gs_bet_m, 	15,
			   -gs_bet_sz/2, 	0, 			-1
			ADDX gs_bet_sz+gs_bkx
		NEXT CT
		DEL gs_hor_pnum
		DEL 1
	ENDIF

	ADD -a/2, gs_szekc_m/2, 0
	ROTY 90
	ROTZ 90

	bn=int((a+2*gs_bv)/0.1)
	dx=(a+2*gs_bv)/bn
	FOR HI=1 to bn-1

		ADDZ a/2-gs_bv
		ROTY 90
		CUTPOLY 3,
			gs_bv+a/2-HI*dx, gs_leaf_thk,
			a/2-HI*dx, gs_leaf_thk-gs_bv,
		   -gs_bv+a/2-HI*dx, gs_leaf_thk
		DEL 2
	NEXT HI

	SPRISM_{2} gs_li_mat1, gs_li_mat1, gs_li_mat1, 7+NSP/4*0,
		0, 0, 1, 0, a, 0,
		0, 0, 1, 0, 0, 0,
		-gs_szekc_m/2, 0, 15, gs_li_mat1,
		gs_szekc_m/2, 0, 15, gs_li_mat1,
		gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_leaf_mat,
		gs_szekc_m/2-gs_bv, gs_leaf_thk, 15, gs_leaf_mat,
		-gs_szekc_m/2+gs_bv, gs_leaf_thk, 15, gs_leaf_mat,
		-gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_li_mat1,
		-gs_szekc_m/2, 0, -1, gs_leaf_mat

	FOR HI=1 to bn-1
		CUTEND
	NEXT HI
	DEL 3

	IF bGlazedSection THEN
		FOR CE=1 TO gs_hor_pnum
			CUTEND
		NEXT CE

		ADD -a/2+gs_bet_sz/2+gs_bkx, rah, -0.01
		FOR iHorPanel=1 TO gs_hor_pnum
			gosub "topPanelGlazing"

			ADDX gs_bet_sz+gs_bkx
		NEXT iHorPanel
		DEL 1 + gs_hor_pnum
	ENDIF

return

! ------------------------------------------------------------------------------
! Horizontal Ribbed Door Type
! ------------------------------------------------------------------------------
"horizontalRibbed":

	IF bGlazedSection=1 THEN
		ADD -a/2+gs_bet_sz/2+gs_bkx, rah, 0
		FOR CT=1 TO gs_hor_pnum
			CUTPOLYA 5, 2, 0,
			   -gs_bet_sz/2, 0, 15,
			   gs_bet_sz/2, 0, 15,
			   gs_bet_sz/2, gs_bet_m, 15,
			   -gs_bet_sz/2, gs_bet_m, 15,
			   -gs_bet_sz/2, 0, -1
			ADDX gs_bet_sz+gs_bkx
		NEXT CT
		DEL gs_hor_pnum
		DEL 1
	ENDIF

	ADD -a/2, gs_szekc_m/2, 0
	ROTY 90
	ROTZ 90

	bn=int(gs_szekc_m/0.1)
	dx=gs_szekc_m/bn
	FOR HI=1 to bn-1
		PUT gs_bv+gs_szekc_m/2-HI*dx, gs_leaf_thk, 15, gs_leaf_mat,
			gs_szekc_m/2-HI*dx, gs_leaf_thk-gs_bv, 15, gs_leaf_mat,
		   -gs_bv+gs_szekc_m/2-HI*dx, gs_leaf_thk, 15, gs_leaf_mat
	NEXT HI

	SPRISM_{2} gs_li_mat1, gs_li_mat1, gs_li_mat1, 7+NSP/4,
		0, 0, 1, 0, a, 0,
		0, 0, 1, 0, 0, 0,
		-gs_szekc_m/2, 0, 15, gs_li_mat1,
		gs_szekc_m/2, 0, 15, gs_li_mat1,
		gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_leaf_mat,
		gs_szekc_m/2-gs_bv, gs_leaf_thk, 15, gs_leaf_mat,

		GET (NSP),

		-gs_szekc_m/2+gs_bv, gs_leaf_thk, 15, gs_leaf_mat,
		-gs_szekc_m/2, gs_leaf_thk-gs_bv, 15, gs_li_mat1,
		-gs_szekc_m/2, 0, -1, gs_leaf_mat
	DEL 3

	IF bGlazedSection THEN
		FOR CE=1 TO gs_hor_pnum
			CUTEND
		NEXT CE

		ADD -a/2+gs_bet_sz/2+gs_bkx, rah, -0.01
		FOR iHorPanel=1 TO gs_hor_pnum
			gosub "topPanelGlazing"

			ADDX gs_bet_sz+gs_bkx
		NEXT iHorPanel
		DEL 1 + gs_hor_pnum
	ENDIF
return

! ------------------------------------------------------------------------------
! Custom Door Type
! ------------------------------------------------------------------------------
"customPanel":

	ADD -a/2, gs_szekc_m/2, 0
	ROTY 90
	ROTZ 90
	PRISM_ 7, a,
	  -gs_szekc_m/2, 0, 15,
	  gs_szekc_m/2, 0, 15,
	  gs_szekc_m/2, gs_leaf_thk-gs_bv, 15,
	  gs_szekc_m/2-gs_bv, gs_leaf_thk, 15,
	  -gs_szekc_m/2+gs_bv, gs_leaf_thk, 15,
	  -gs_szekc_m/2, gs_leaf_thk-gs_bv, 15,
	  -gs_szekc_m/2, 0, -1
	DEL 3
return


! ------------------------------------------------------------------------------
! Top Panel Glazing
! ------------------------------------------------------------------------------
"topPanelGlazing":
	PEN gs_leaf_pen
	MATERIAL gs_leaf_mat
	RESOL res

	if lod3D = 1 then		! --- Simple 3D ---
		PEN gs_glass_pen
		MATERIAL gs_glass_mat
		sect_fill gs_fillGlass, gs_penGlass_bg, gs_penGlass_fg, gs_glass_pen

		ADDZ (gs_leaf_thk+0.02)/2-gs_glass_thk/2
		PRISM_ 5, gs_glass_thk,
			-gs_bet_sz/2, 	0, 			15,
			gs_bet_sz/2, 	0, 			15,
			gs_bet_sz/2, 	gs_bet_m, 	15,
			-gs_bet_sz/2, 	gs_bet_m, 	15,
			-gs_bet_sz/2, 	0, 			-1
		DEL 1

	else						! --- Detailed 3D ---

		MATERIAL gs_leaf_mat
		FOR HI=1 TO 2
			CPRISM_ gs_li_mat1, gs_li_mat1, gs_li_mat1, 5, (gs_leaf_thk+0.02)/2,
				-gs_bet_sz/2, 			0, 					10,
				-gs_bet_sz/2+gs_ufw, 	gs_ufw, 			11,
				-gs_bet_sz/2+gs_ufw, 	gs_bet_m-gs_ufw, 	10,
				-gs_bet_sz/2, 			gs_bet_m, 			11,
				-gs_bet_sz/2, 			0, 					-1

			ADDZ (gs_leaf_thk+0.02)/2
			CPRISM_ gs_leaf_mat, gs_leaf_mat, gs_leaf_mat, 5, (gs_leaf_thk+0.02)/2,
				-gs_bet_sz/2, 			0, 					10,
				-gs_bet_sz/2+gs_ufw, 	gs_ufw, 			14,
				-gs_bet_sz/2+gs_ufw, 	gs_bet_m-gs_ufw, 	10,
				-gs_bet_sz/2, 			gs_bet_m, 			14,
				-gs_bet_sz/2, 			0, 					-1
			DEL 1
			MULX -1
		NEXT HI
		DEL 2

		teta = 90
		GOSUB "textureDirection"

		FOR HI=1 TO 2
			CPRISM_ gs_li_mat1, gs_li_mat1, gs_li_mat1, 5, (gs_leaf_thk+0.02)/2,
				-gs_bet_sz/2, 			0, 			11,
				 gs_bet_sz/2, 			0, 			10,
				 gs_bet_sz/2-gs_ufw, 	gs_ufw, 	11,
				-gs_bet_sz/2+gs_ufw, 	gs_ufw, 	10,
				-gs_bet_sz/2, 			0, 			-1

			ADDZ (gs_leaf_thk+0.02)/2
			CPRISM_ gs_leaf_mat, gs_leaf_mat, gs_leaf_mat, 5, (gs_leaf_thk+0.02)/2,
				-gs_bet_sz/2, 			0, 			14,
				 gs_bet_sz/2, 			0, 			10,
				 gs_bet_sz/2-gs_ufw, 	gs_ufw, 	14,
				-gs_bet_sz/2+gs_ufw, 	gs_ufw, 	10,
				-gs_bet_sz/2, 			0, 			-1
			DEL 1
			ADDY gs_bet_m
			MULY -1
		NEXT HI
		DEL 4

		teta = 0
		GOSUB "textureDirection"


		PEN gs_glass_pen
		MATERIAL gs_glass_mat
		sect_fill gs_fillGlass, gs_penGlass_bg, gs_penGlass_fg, gs_glass_pen

		ADDZ (gs_leaf_thk+0.02)/2-gs_glass_thk/2
		PRISM_ 5, gs_glass_thk,
			-gs_bet_sz/2+gs_ufw, gs_ufw, 7,
			gs_bet_sz/2-gs_ufw, gs_ufw, 7,
			gs_bet_sz/2-gs_ufw, gs_bet_m-gs_ufw, 7,
			-gs_bet_sz/2+gs_ufw, gs_bet_m-gs_ufw, 7,
			-gs_bet_sz/2+gs_ufw, gs_ufw, -1
		DEL 1

	endif
return

! ------------------------------------------------------------------------------
! Texture direction
! ------------------------------------------------------------------------------
"textureDirection":

	base
	rotz teta
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1
	coor 2+256, -1, -2, -3, -4
	body 1
	del 1

return
