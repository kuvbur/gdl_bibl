
line_type gs_linetype

unID_Opening = 1000

!****** Scale settings *******

if lod2D = DET2D_OFF then end

if lod2D = DET2D_DETAILED_CHI then lod2D = DET2D_DETAILED
if lod2D = DET2D_SIMPLE_CHI then lod2D = DET2D_MIDDLE



!********************* coordinates according to size types

unitleft = -ac_unit_width / 2
unitright = ac_unit_width / 2
leafleft = -ac_leaf_width / 2
leafright = ac_leaf_width / 2
egressleft = -ac_egress_width / 2
egressright = ac_egress_width / 2

frameleftout	= unitleft  - gs_frame_ovhg_left_out
framerightout	= unitright + gs_frame_ovhg_right_out
frameleftin		= unitleft  - gs_frame_ovhg_left_in
framerightin	= unitright + gs_frame_ovhg_right_in
frameDiff		= (gs_frw1 - gs_frw2) / 2

if lod2D = DET2D_MIDDLE then
	egressright=egressright+gs_ds_w*(gs_frame_style_m=2)
	egressleft=egressleft-gs_ds_w*(gs_frame_style_m=2)
endif


!************ Hotspots on Unit Size

if bShowUnitHotspots then
	add2 (gs_frame_width_right - gs_frame_width_left) / 2, WIDO_SILL-WALL_THICKNESS
	hotspot2 unitleft,	 0, 100
	hotspot2 unitright,	 0, 101
	hotspot2 unitleft,	-gs_fthk, 102
	hotspot2 unitright,	-gs_fthk, 103
	del 1
endif


!************ Hotspots on Leaf Size

if bShowLeafHotspots then
	add2 0, WIDO_SILL-WALL_THICKNESS
	hotspot2 leafleft,	 0, 300
	hotspot2 leafright,	 0, 301
	hotspot2 leafleft,	-gs_fthk, 302
	hotspot2 leafright,	-gs_fthk, 303
	del 1
endif


!**************************** Simple frame without sidelight
!***********************************************************


!************ Hotspots on Egress Size

if lod2D = DET2D_MIDDLE then
	egressthk1=0
	egressthk2=-gs_fthk
else
	egressthk1=-gs_leaf_thk
	egressthk2=-gs_leaf_thk-gs_ds_thk
endif

if bShowEgressHotspots then
	add2 0, WIDO_SILL-WALL_THICKNESS
	if gs_frame_style_m = 1 then
		hotspot2 egressleft  + frameDiff, 0
		hotspot2 egressright + frameDiff, 0
	else
		hotspot2 egressleft  + frameDiff, egressthk1
		hotspot2 egressright + frameDiff, egressthk1
	endif

	if gs_frame_style_m = 2 then
		hotspot2 egressleft  + frameDiff, egressthk2
		hotspot2 egressright + frameDiff, egressthk2
	else
		hotspot2 egressleft  + frameDiff, -gs_fthk
		hotspot2 egressright + frameDiff, -gs_fthk
	endif
	del 1
endif

!************* Hotspots on Leaf Size

if lod2D = DET2D_MIDDLE then
	leafthk1=-gs_fthk
	leafleft1=leafleft+gs_rebate_width
	leafright1=leafright-gs_rebate_width
else
	leafthk1=-gs_leaf_thk
	leafleft1=leafleft
	leafright1=leafright
endif

if bShowAddHotspots & (lod2D = DET2D_MIDDLE | lod2D = DET2D_DETAILED) then
	add2 0, WIDO_SILL-WALL_THICKNESS
	if gs_frame_style_m = 3 then
		hotspot2 leafleft1  + frameDiff, 0
		hotspot2 leafright1 + frameDiff, 0
		hotspot2 leafleft1  + frameDiff, leafthk1
		hotspot2 leafright1 + frameDiff, leafthk1
	else
		hotspot2 leafleft  + frameDiff, 0
		hotspot2 leafright + frameDiff, 0
		hotspot2 leafleft  + frameDiff, -gs_fthk
		hotspot2 leafright + frameDiff, -gs_fthk
	endif
	del 1
endif


! FRAME !!
pen gs_frame_pen_cont
if lod2D = DET2D_MIDDLE then

	!*********************** Simple or 1:100

	fill gs_fillFr

	add2 (gs_frame_width_right - gs_frame_width_left) / 2, WIDO_SILL-WALL_THICKNESS
	poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
		unitleft  + gs_frame_width_left,			 0,			1+32,
		unitleft,	 0,			1+32,
		unitleft,	-gs_fthk,	1+32,
		unitleft  + gs_frame_width_left,			-gs_fthk,	1+32

	poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
		 unitright - gs_frame_width_right,			 0,			1+32,
		 unitright, 0,			1+32,
		 unitright, -gs_fthk,	1+32,
		 unitright - gs_frame_width_right,			-gs_fthk,	1+32
	del 1
endif


if lod2D = DET2D_DETAILED then

	!*********************** Detailed or 1:50

	if gs_frame_style_m=1 then
		leafleft2=-ac_egress_width/2
		leafright2=ac_egress_width/2
	else
		leafleft2=-ac_leaf_width/2
		leafright2=ac_leaf_width/2
	endif

	FILL gs_fillFr

	add2 (gs_frame_width_right - gs_frame_width_left) / 2, WIDO_SILL-WALL_THICKNESS
	poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
		unitleft  + gs_frame_width_left,			 0,			1+32,
		unitleft,	 0,			1+32,
		unitleft,	-gs_fthk,	1+32,
		unitleft  + gs_frame_width_left,			-gs_fthk,	1+32

	poly2_b 4, 1+2+4+32, gs_penFr_fg, gs_penFr_bg,
		 unitright - gs_frame_width_right,			 0,			1+32,
		 unitright, 0,			1+32,
		 unitright, -gs_fthk,	1+32,
		 unitright - gs_frame_width_right,			-gs_fthk,	1+32
	del 1
endif
!! END OF FRAME !!


!! LEAF !!
pen gs_leaf_pen_2D
if gs_mot_m = 0 then   ! PANEL TYPE = EMPTY

	if gs_iSwingType = LINE_SHAPE_SWING_SIMPLE_LINE | gs_iSwingType = LINE_SHAPE_SWING_LEAF_LINE then
		drawindex 20

		pen gs_opLinePen
		line_type gs_swingLineType

		add2 0, WIDO_SILL - WALL_THICKNESS - (gs_frame_thk/2) * (lod2D <> DET2D_SIMPLE)
		hotspot2 0, -0.50, unID_Opening
		hotspot2 0, 0.50, unID_Opening + 1
		line2 0, -0.50, 0, 0.50
		del 1
	endif

	end
else

	add2 a/2, WIDO_SILL-WALL_THICKNESS

	!!!! HOTSPOT EDITING !!!!
	if gs_full_edit & gs_passdoor & (gs_iSwingType <> LINE_SHAPE_SWING_SIMPLE_LINE) & (gs_iSwingType <> LINE_SHAPE_SWING_LEAF_LINE) then
		unID=unID+1
		hotspot2 -gs_bkx/2+0.02, 0, unID, gs_open_2D, 6+512
		unID=unID+1
		hotspot2 -gs_bkx/2+0.02-gs_passdoorWidth, 0, unID, gs_open_2D, 4+128
		unID=unID+1
		hotspot2 -gs_bkx/2+0.02-gs_passdoorWidth*cos(gs_open_2D), gs_passdoorWidth*sin(gs_open_2D), unID, gs_open_2D, 5
	endif
	!!!! END OF HOTSPOT EDITING !!!!

	if lod2D = DET2D_SIMPLE | lod2D = DET2D_MIDDLE then
		line_type gs_linetype
		if not(gs_passdoor) then
			line2 -a, 0, 0, 0
			if gs_iSwingType = LINE_SHAPE_SWING_SIMPLE_LINE | gs_iSwingType = LINE_SHAPE_SWING_LEAF_LINE then
				drawindex 20

				pen gs_opLinePen
				line_type gs_swingLineType

				add2 -a/2, (-gs_frame_thk/2) * (lod2D = DET2D_MIDDLE)
				hotspot2 0, -0.50, unID_Opening
				hotspot2 0, 0.50, unID_Opening + 1
				line2 0, -0.50, 0, 0.50
				del 1
			endif
		else
			line2 -a, 0, -gs_bkx/2+0.02-gs_passdoorWidth, 0
			line2 -gs_bkx/2+0.02, 0, 0, 0

			if gs_iSwingType = LINE_SHAPE_SWING_SIMPLE_LINE | gs_iSwingType = LINE_SHAPE_SWING_LEAF_LINE then
				drawindex 20

				pen gs_opLinePen
				line_type gs_swingLineType

				add2 -gs_bkx/2+0.02-gs_passdoorWidth/2, (-gs_frame_thk/2) * (lod2D = DET2D_MIDDLE)
				hotspot2 0, -0.50, unID_Opening
				hotspot2 0, 0.50, unID_Opening + 1
				line2 0, -0.50, 0, 0.50
				del 1
			else
				line2 -gs_bkx/2+0.02-gs_passdoorWidth*cos(gs_open_2D), gs_passdoorWidth*sin(gs_open_2D),
					  -gs_bkx/2+0.02, 0
				pen gs_opLinepen
				line_type gs_swingLineType
				if abs(gs_open_2D)>eps then
					if gs_iSwingType = LINE_SHAPE_SWING_CURVED then
						 arc2 -gs_bkx/2+0.02, 0, gs_passdoorWidth, 180-gs_open_2D, 180
					else
						line2 -gs_bkx/2+0.02-gs_passdoorWidth*cos(gs_open_2D), gs_passdoorWidth*sin(gs_open_2D), -gs_passdoorWidth-gs_bkx/2+0.02, 0
					endif
				endif
			endif
		endif
		line_type gs_linetype
	endif
	if lod2D = DET2D_DETAILED then
		line_type gs_linetype

		if not(gs_passdoor) then
			poly2_b 5, 1+2*gs_bFills+4+64, gs_penLeaf_fg, gs_penLeaf_bg,
				-a, 0, 1+32,
				-a, -gs_leaf_thk, 1+32,
				0, -gs_leaf_thk, 1+32,
				0, 0, 1+32,
				-a, 0, -1

			if gs_iSwingType = LINE_SHAPE_SWING_SIMPLE_LINE | gs_iSwingType = LINE_SHAPE_SWING_LEAF_LINE then
				drawindex 20

				pen gs_opLinePen
				line_type gs_swingLineType

				add2 -a/2, -gs_frame_thk/2
				hotspot2 0, -0.50, unID_Opening
				hotspot2 0, 0.50, unID_Opening + 1
				line2 0, -0.50, 0, 0.50
				del 1
			endif
		else
			poly2_b 5, 1+2*gs_bFills+4+64, gs_penLeaf_fg, gs_penLeaf_bg,
				-a, 				0, 				1+32,
				-a, 				-gs_leaf_thk, 	1+32,
				-gs_bkx/2+0.02-gs_passdoorWidth, 	-gs_leaf_thk, 	1+32,
				-gs_bkx/2+0.02-gs_passdoorWidth, 	0, 				1+32,
				-a, 				0,		 		-1

			poly2_b 5, 1+2*gs_bFills+4+64, gs_penLeaf_fg, gs_penLeaf_bg,
				 0, 				0, 				1+32,
				 0, 				-gs_leaf_thk, 	1+32,
				-gs_bkx/2+0.02, 			-gs_leaf_thk, 	1+32,
				-gs_bkx/2+0.02, 			0, 				1+32,
				 0, 				0,		 		-1

			if gs_iSwingType = LINE_SHAPE_SWING_SIMPLE_LINE | gs_iSwingType = LINE_SHAPE_SWING_LEAF_LINE then
				drawindex 20

				pen gs_opLinePen
				line_type gs_swingLineType

				add2 -gs_bkx/2+0.02-gs_passdoorWidth/2, -gs_frame_thk/2
				hotspot2 0, -0.50, unID_Opening
				hotspot2 0, 0.50, unID_Opening + 1
				line2 0, -0.50, 0, 0.50
				del 1
			else
				add2 -gs_bkx/2+0.02, 0
				rot2 -gs_open_2D
					poly2_b 5, 1+2*gs_bFills+4+64, gs_penLeaf_fg, gs_penLeaf_bg,
						0, 0, 1+32,
						0, -gs_leaf_thk, 1+32,
						-gs_passdoorWidth, -gs_leaf_thk, 1+32,
						-gs_passdoorWidth, 0, 1+32,
						0, 0, -1
				del 2

				pen gs_opLinepen
				line_type gs_swingLineType
				if abs(gs_open_2D)>eps then
					if gs_iSwingType = LINE_SHAPE_SWING_CURVED then
						arc2 -gs_bkx/2+0.02, 0, gs_passdoorWidth, 180-gs_open_2D+ASN(gs_leaf_thk/gs_passdoorWidth), 180
					else
						line2 -gs_bkx/2+0.02-gs_passdoorWidth*cos(gs_open_2D), gs_passdoorWidth*sin(gs_open_2D), -gs_passdoorWidth-gs_bkx/2+0.02, 0
					endif
				endif
			endif
		endif
		line_type gs_linetype
	endif
	if gs_opline_2D then
		line_type gs_opline_type_2D
		pen gs_opLinepen
		poly2_ 4, 1,
			0, -gs_leaf_thk, 1,
			0, -b, 1,
			-a, -b, 1,
			-a, -gs_leaf_thk, 1
		line2 -a, -gs_leaf_thk, 0, -b
		line2 0, -gs_leaf_thk, -a, -b
		line_type gs_linetype
	endif

	del 1
endif
!! END OF LEAF !!



end
