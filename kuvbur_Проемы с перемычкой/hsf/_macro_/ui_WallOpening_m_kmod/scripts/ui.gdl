
pageStartY 	= 2
dy			= 23

x1			= 5
x2			= 120
x3 			= 225

! ======================================================================
! Reveal
! ======================================================================

y = pageStartY
if gs_RevealTypeEnable <= 1 then
	ui_style 0,1
	ui_outfield stRevealTypes[1], x1, y, x3-x1-1, 15	! No Reveal - Edge
	ui_style 0,0

	y = y + 35

	ui_pict 5, x1, y+1, 121, 33, 1			!no_reveal_edge_door.png

	ui_pict 6, x1+121, y-4, 13, 9, 1		!ui_arrow_v_0.png
	ui_infield "AC_wido_sill", x1+121+14, y-2, 72, 19

else
	ui_style 0,1
	ui_outfield st_uiWallOpening[1], x1, y, x3-x1-1, 15	! Reveal
	ui_style 0,0

	y = y + 20

	ui_pict 1, x1, y+1, 35, 15, 1			!ui_reveal_depth.png
	if iRevealType = REVEAL_EDGE then
		ui_infield "AC_wido_sill", x1+42, y, x2-x1-45+7, 16
	else
		ui_infield "gs_wido_sill", x1+42, y, x2-x1-45+7, 16
	endif

	y = y + 26

	px = 90
	py = 45
	cx = px + 15
	cy = py + 4
	ui_infield{3} "gs_reveal_type_int", x1, y, x3 - x1 - 49, 75,
		2, 2, 10, 1,		!ui_reveal_selection_new.png
		cx, cy, px, py,
		1, stRevealTypes[ 1],	1,
		2, stRevealTypes[ 2],	2,
		3, stRevealTypes[ 3],	3,
		4, stRevealTypes[ 4],	4,
		5, stRevealTypes[ 5],	5,
		6, stRevealTypes[ 6],	6,
		7, stRevealTypes[ 7],	7,
		9, stRevealTypes[ 9],	9,
		10, stRevealTypes[10],	10,
		8, stRevealTypes[ 8],	8

	if not(bPocketDoor) then
		ui_pict_button ui_function, "...", "uiFunctionReveal_flat(1)", x3 - 45 , y + 73 - 20, 36, 22, -iUiID_Reveal
	else
		stCenteredInWall				= `По Центру Стены`
		stWarningPocketCenteredTooltip	= `Дверь будет расположена по центру толщины стены. Последующие изменения толщины стены или коробки двери могут привести к смещению двери с оси стены.`

		bDefaultElem = (GLOB_INTGUID = "" | GLOB_INTGUID = "{00000000-0000-0000-0000-000000000000}")

		if bDefaultElem then
			ui_pict_button ui_function, "", 20, x3-96, y-29, 47, 26, 0		ui_tooltip stCenteredInWall
			_iFlagAutoButton = 4
		else
			ui_pict_button ui_function, "", 19, x3-96, y-29, 47, 26, 9999	ui_tooltip stCenteredInWall
			_iFlagAutoButton = 0
		endif

		if GLOB_UI_BUTTON_ID = 9999 then
			ui_pict 18, x3-45, y-22, 12, 12, 1 ui_tooltip stWarningPocketCenteredTooltip	! ui_warningsign.png
		endif
	endif
endif

ui_separator x1, pageStartY + 125, 444 - x1, pageStartY + 125

! ======================================================================
! Wall Closure
! ======================================================================

y = pageStartY

ui_style 0,1
ui_outfield st_uiWallOpening[2], x3 + 10, y, x3-x1-15, 16	!Wall Closure
ui_style 0,0

y = y + 20

if bPopupControlForClosureType then				!!! for USA DW-s
	ui_style 0, 1

	px = 40
	py = 23
	cx = px + 15
	cy = py + 4

	ui_infield{3} "gs_closure_type_int", x3 + 10, y, 444 - x3 - 13 - 43, 30,
		2, 13, 15, 1,	! ui_closure_selection.png
		cx, cy, px, py,
		 1, stClosureTypes[ 1],	CLOSURE_NO + 1,
		 2, stClosureTypes[ 2],	CLOSURE_TRADITIONAL + 1,
		 3, stClosureTypes[ 3],	CLOSURE_PREFABRICATED + 1,
		 9, stClosureTypes[ 9],	CLOSURE_VARIABLE + 1,
		15, stClosureTypes[15],	CLOSURE_DUTCH_VARIABLE + 1,
		 5, stClosureTypes[ 5],	CLOSURE_SOLID_WALL + 1,
		 6, stClosureTypes[ 6],	CLOSURE_STUD_WALL + 1,
		 7, stClosureTypes[ 7],	CLOSURE_STUD_WALL_SIDING + 1,
		 8, stClosureTypes[ 8],	CLOSURE_BRICK_VENEER + 1,
		10, stClosureTypes[10],	CLOSURE_PLASTER_REVEAL + 1,
		11, stClosureTypes[11],	CLOSURE_INSULATED_REVEAL + 1,
		12, stClosureTypes[12],	CLOSURE_INSULATED_FOR_RENOVATION + 1,
		13, stClosureTypes[13],	CLOSURE_FINISH + 1,
		14, stClosureTypes[14],	CLOSURE_AIR_SPACE + 1,
		 4, stClosureTypes[ 4],	CLOSURE_CUSTOM + 1

	ui_style 0, 0
else
	ui_infield{3}	"gs_closure_type_int", x3 + 10, y, 444 - x3 - 13 - 45, 20,
		8, "", 15, 1,
		0, 0, 0, 0,
		 1, stClosureTypes[ 1], CLOSURE_NO + 1,
		 2, stClosureTypes[ 2], CLOSURE_TRADITIONAL + 1,
		 3, stClosureTypes[ 3], CLOSURE_PREFABRICATED + 1,
		 9, stClosureTypes[ 9], CLOSURE_VARIABLE + 1,
		15, stClosureTypes[15], CLOSURE_DUTCH_VARIABLE + 1,
		 5, stClosureTypes[ 5], CLOSURE_SOLID_WALL + 1,
		 6, stClosureTypes[ 6], CLOSURE_STUD_WALL + 1,
		 7, stClosureTypes[ 7], CLOSURE_STUD_WALL_SIDING + 1,
		 8, stClosureTypes[ 8], CLOSURE_BRICK_VENEER + 1,
		10, stClosureTypes[10], CLOSURE_PLASTER_REVEAL + 1,
		11, stClosureTypes[11], CLOSURE_INSULATED_REVEAL + 1,
		12, stClosureTypes[12], CLOSURE_INSULATED_FOR_RENOVATION + 1,
		13, stClosureTypes[13], CLOSURE_FINISH + 1,
		14, stClosureTypes[14], CLOSURE_AIR_SPACE + 1,
		 4, stClosureTypes[ 4], CLOSURE_CUSTOM + 1
endif

ui_pict_button ui_function, "...", "uiFunctionClosure_flat(1)", 444-5-36 , y-1, 36, 22, -iUiID_Closure

y = y + 30 + 5 * bPopupControlForClosureType

! ======================================================================
! Wall Inset
! ======================================================================

if bEnableParapetInset then
	ui_style 0,1
	ui_infield{3} "gs_parapet_wall_inset", x3 + 10, y-8, x3 - x1 - 53, 16+16,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, st_uiWallOpening[13], 0,		! Wall Inset
		1, st_uiWallOpening[13], 1		! Wall Inset
	ui_style 0,0

	ui_pict_button ui_function, "...", "uiFunctionWallInset_flat(1)", 444-5-36 , y-1, 36, 22, -iUiID_WallInset

	y = y + 25
endif

! ======================================================================
! Masonry Arch
! ======================================================================

if bEnableMasonryArch then
	ui_style 0,1
	ui_infield{3} "bMasonryArch", x3 + 10, y, x3-x1-15, 16,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, st_uiWallOpening[3], 0,	! Masonry Arch
		1, st_uiWallOpening[3], 1	! Masonry Arch
	ui_style 0,0
	
	y = y + 20
	ui_infield{3} "gs_masonry_arch_int", x3 + 10, y, 444 - x3 - 13 - 45, 20,
		8, "",
		1, 1,
		x2+65-x1-30, 18, x2+65-x1-30, 18,
		1,	stMasonryArchTypes[2],	MASONRY_ARCH,
		1,	stMasonryArchTypes[3],	MASONRY_JACK_ARCH,
		1,	stMasonryArchTypes[4],	MASONRY_JACK_ARCH_KEYSTONE,
		1,	stMasonryArchTypes[5],	MASONRY_SEGMENT_ARCH,
		1,	stMasonryArchTypes[6],	MASONRY_SEGMENT_ARCH_KEYSTONE,
		1,	stMasonryArchTypes[7],	MASONRY_BULLSEYE_ARCH
	
	ui_pict_button ui_function, "...", "uiFunctionMasonry_flat(1)", 444-5-36 , y-1, 36, 22, -iUiID_MasonryArch
endif

! ======================================================================
! Opening Oversizes
! ======================================================================

if bShowOversizes then

	y = pageStartY+135
	dy = 22

	ui_style 0,1

	if bHasOversizeBool then
		ui_infield{3} "gs_oversize", x1, y-4, x3-x1-5, 20,
			7, "", 2, 2,
			0, 0, 0, 0,
			1, st_uiWallOpening[4], 0,
			2, st_uiWallOpening[4], 1
	else
		ui_outfield st_uiWallOpening[4], x1, y, x3-x1-5, 18	! Opening Oversize
	endif

	y = y + dy

	ui_style 0,0

	_iFlag = 4 * bHasOversizeBool * not(gs_oversize)

	ui_outfield st_uiWallOpening[5], x1, y+3, x2-x1-1, 18, _iFlag	! Left
	ui_infield "gs_left_oversize", x2,y, x3-x2-5,dy
	y = y + dy

	ui_outfield st_uiWallOpening[7], x1, y+3, x2-x1-1, 18, _iFlag	! Upper
	ui_infield "gs_upper_oversize", x2,y, x3-x2-5,dy
	y = y + dy

	ui_outfield st_uiWallOpening[6], x1, y+3, x2-x1-1, 18, _iFlag	! Right
	ui_infield "gs_right_oversize", x2,y, x3-x2-5,dy
	y = y + dy

	ui_outfield st_uiWallOpening[8], x1, y+3, x2-x1-1, 18, _iFlag	! Lower
	ui_infield "gs_lower_oversize", x2,y, x3-x2-5,dy
	y = y + dy

	ui_outfield st_uiWallOpening[9], x1, y+3, 444 - x1 - 3, 15, _iFlag		! Note: Opening Oversizes don't modify the nominal wall hole sizes.

	ui_separator x3, 133, x3, 245
endif

! ======================================================================
! Empty Openings
! ======================================================================

if isEmptyOpening then

	y = pageStartY+135
	_bHasShapePic = 0

	if	iWindowShape = SHAPE_ARCHED |\
		iWindowShape = SHAPE_HALFARCH |\
		iWindowShape = SHAPE_PENTAGON |\
		iWindowShape = SHAPE_TRAPEZOID |\
		iWindowShape = SHAPE_GOTHICARCH |\
		iWindowShape = SHAPE_OGEECENTERED |\
		iWindowShape = SHAPE_SARACENIC then

		ui_infield "openingArchHeight", x1+100, y, 75, 18 ui_tooltip st_uiWallOpening[15]		! Opening Arch Height
		ui_infield "gs_shoulder_height", x1+100, y+30, 75, 18 ui_tooltip st_uiWallOpening[23]	! Shoulder Height
	endif

	if iWindowShape = SHAPE_SARACENIC then
		ui_pict 17, x1+50, y, 40, 60, 1						!ui_opening_saracenic.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_OGEECENTERED then
		ui_pict 16, x1+50, y, 40, 60, 1						!ui_opening_ogee.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_HORSESHOE then
		ui_pict 15, x1+75, y, 58, 60, 1						!ui_opening_horseshoe.png
		ui_infield "gs_lowerArchHeight", x1, y+12, 75, 18 ui_tooltip st_uiWallOpening[17]		! Bottom Height of Arch
		ui_infield "openingArchHeight", x1+140, y, 75, 18 ui_tooltip st_uiWallOpening[15]		! Opening Arch Height
		ui_infield "gs_shoulder_height", x1+140, y+25, 75, 18 ui_tooltip st_uiWallOpening[23]	! Shoulder Height
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_GOTHICARCH then
		ui_pict 14, x1+50, y, 40, 60, 1						!ui_opening_gothic.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_ARCHED then						!1
		ui_pict 7, x1+50, y, 40, 60, 1						!ui_opening_arch.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_HALFARCH then					!7
		ui_pict 10, x1+50, y, 40, 60, 1						!ui_opening_halfarch.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_PENTAGON then					!8
		ui_pict 12, x1+50, y, 40, 60, 1						!ui_opening_segmented.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_TRAPEZOID then					!11
		ui_pict 11, x1+50, y, 40, 60, 1						!ui_opening_inclined.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_GOTHICARCH then					!13
		ui_pict 8, x1+50, y, 40, 60, 1						!ui_opening_brokenarch.png
		_bHasShapePic = 1
	endif

	if iWindowShape = SHAPE_ELLIPSEARCH then				!14
		ui_pict 9, x1+50, y, 40, 60, 1						!ui_opening_ellipsearch.png

		ui_infield "openingArchHeight", x1+100, y-5, 75, 18 ui_tooltip st_uiWallOpening[15]	!Opening Arch Height
		ui_infield "shr", x1+100, y+18, 75, 18 ui_tooltip st_uiWallOpening[16]	!Shorter Radius
		ui_infield "gs_shoulder_height", x1+100, y+40, 75, 18 ui_tooltip st_uiWallOpening[23]	! Shoulder Height
		_bHasShapePic = 1
	endif

	if	(iWindowShape = SHAPE_RECTANGULAR |\
		iWindowShape = SHAPE_ARCHED |\
		iWindowShape = SHAPE_ROUND |\
		iWindowShape = SHAPE_MULTISIDE |\
		iWindowShape = SHAPE_OCTAGONAL |\
		iWindowShape = SHAPE_HALFARCH |\
		iWindowShape = SHAPE_PENTAGON |\
		iWindowShape = SHAPE_HALFROUND |\
		iWindowShape = SHAPE_TRAPEZOID |\
		iWindowShape = SHAPE_GOTHICARCH |\
		iWindowShape = SHAPE_ELLIPSEARCH |\
		iWindowShape = SHAPE_ROMBUS) & not(isCurtainWallOpeningSWE) then

		if _bHasShapePic then
			y = 266 - 2*21
		else
			y = pageStartY + 143
		endif

		if _bShowFrameThk then
			ui_style 0,1
			ui_outfield st_uiWallOpening[21], x1, y, 139, 16		!Frame Thickness
			ui_style 0,0
			ui_infield "gs_frame_thk", x1+140, y-4, 75, 16
		endif

		if iWindowShape = SHAPE_MULTISIDE then
			ui_style 0,1
			ui_outfield st_uiWallOpening[14], x1, y, 139, 16		!Number of Sides
			ui_style 0,0
			ui_infield "gs_numOfSides", x1+140, y-4, 75, 16
		endif

		if	iWindowShape = SHAPE_ARCHED |\
			iWindowShape = SHAPE_ROUND |\
			iWindowShape = SHAPE_HALFARCH |\
			iWindowShape = SHAPE_HALFROUND |\
			iWindowShape = SHAPE_GOTHICARCH |\
			iWindowShape = SHAPE_ELLIPSEARCH then

			y = y + 21
			ui_outfield st_uiWallOpening[22], x1, y, 140, 16		!Curve Resolution
			ui_style 0,0
			ui_infield "gs_res", x1+140, y-4, 75, 16
		endif
	endif

	ui_separator x3, 133, x3, 265
endif

! ======================================================================
! Wall Opening Surface
! ======================================================================

y0 = pageStartY
if isEmptyOpening then
	y = y0 + 132
	dy = 25
else
	y = y0 + 135
	dy = 40
endif

x3	= 5 + 220 * (bShowOversizes | isEmptyOpening)

ui_style 0,1
ui_outfield st_uiWallOpening[10], x3 + 7 , y, 224-x3-10 + 220 * (bShowOversizes | isEmptyOpening), 18	! Wall Opening Surface
y = y + dy

ui_style 0,0

ui_pict 3, x3 + 15, y-5, 27, 27, 1		!ui_same_as_walledge.png
ui_radiobutton "AC_HoleSideMaterial", 0, st_uiWallOpening[11], x3+10+43, y-6, 224-10-x3-48 + 220 * (bShowOversizes | isEmptyOpening), 29	!Same as Wall Edge
y = y + 33

ui_pict 4, x3 + 15, y-5, 27, 27, 1		!ui_same_as_wallside.png
ui_radiobutton "AC_HoleSideMaterial", 1, st_uiWallOpening[12], x3+10+43, y-6, 224-10-x3-48 + 220 * (bShowOversizes | isEmptyOpening), 29	!Same as Wall Side
y = y + 24

if isEmptyOpening then
	ui_separator x3 + 5, y, 444, y
	y = y + 5

	ui_style 0,1
	ui_outfield st_uiWallOpening[18], x3 + 7, y, 444-x3-7, 15		!Opening Side in Curved Walls
	ui_style 0,0

	ui_infield{3} "iTypeCurvedWallCut", x3+80, y+20, 444-x3-80, 19,
		8, "", 2, 1,
		0,0, 0, 0,
		1, st_uiWallOpening[19],	CUT_PARALLEL,	!Parallel
		2, st_uiWallOpening[20],	CUT_RADIAL		!Radial
endif


