

! =============================================================================
! Panel Settings
! =============================================================================

pageStartY = 4

! --- for panel selection icons

picWdt = 130
picHgt = 266

outFldWdt	= 100
inFldWdt	= 50

outFldHgt	= 15
inFldHgt	= 19

	
xPos0	= 2
xPos1	= xPos0 + picWdt + 26
xPos2	= 230

xPosStart	= xPos1

fullWidth	= 442 - xPos1

y1	= pageStartY
dy	= 22


gosub "panelSelection"


if bDoorPanelPage2 then
	gosub "2DSymbolSpecialOptions"
	end
endif


if gs_ui_dtype = 14 then
	!******** only high-speed door

	y1	= y1 + 30

	inFldWdt	= 65

	picWdt	= 120 + 22 * gs_bGlazedDoor
	picHgt	= 110

	xPos0	= 2
	xPos1	= xPos0 + picWdt + 6
	xPos2	= 230

	fullWidth	= 442 - xPos2

	ui_style 0, 0
	ui_infield{3} "gs_bGlazedDoor",				xPos0,	y1-4,	picWdt,		inFldHgt,
		7, "", 2, 1,
		20, 20, 20, 20,
		1, `С Остеклением`,	1,
		2, `С Остеклением`,	2

	y1 = y1 + 36

	if gs_bGlazedDoor then
		ui_pict "ui_door_high_speed_g(1)",		xPos0,	y1-4,	picWdt,		picHgt,	1

		ui_infield "gs_glass_section_width",	xPos1,	y1+38,	inFldWdt,	inFldHgt	ui_tooltip `Ширина Стеклянной Секции`
		ui_infield "gs_glass_section_elev",		xPos1,	y1+71,	inFldWdt,	inFldHgt	ui_tooltip `Возвышение Стеклянной Секции`
	else
		ui_pict "ui_door_high_speed(1)",		xPos0,	y1-4,	picWdt,		picHgt,	1
	endif

	y1 = y1 - 36

	ui_infield{3} "gs_cust_on",					xPos2,	y1-4,	fullWidth,	inFldHgt,
		7, "", 2, 1,
		20, 20, 20, 20,
		1, `Специальное Полотно Двери`,	1,
		2, `Специальное Полотно Двери`,	2

	y1 = y1 + dy

	xPosStart	= xPos2
	gosub "drawCustomPanel"
endif


if gs_ui_dtype = 10 then
	! --- only garage doors

	ui_style 0, 1

	if iDoorPanelType <> 31 then
		xPos2	= xPos1 + outFldWdt + 2

		fullWidth	= outFldWdt + inFldWdt + 2

		ui_outfield `Стекло`,	xPos1,	y1,		fullWidth,	outFldHgt

		y1	= y1 + dy

		ui_style 0, 0

		if iDoorPanelType < 31 and iDoorPanelType > 9 then
			garmask = 0
		else
			garmask = 4
		endif

		ui_outfield `Горизонт.`,	xPos1,	y1,		outFldWdt,	outFldHgt, garmask
		ui_infield "gs_hor_pnum",	xPos2,	y1-4,	inFldWdt,	inFldHgt

		y1	= y1 + dy

		ui_outfield `Вертик.`,		xPos1,	y1,		outFldWdt,	outFldHgt, garmask
		ui_infield "gs_ver_pnum",	xPos2,	y1-4,	inFldWdt,	inFldHgt
	else
		gosub "drawCustomPanel"
	endif
endif


if gs_ui_dtype = 12 then
	!******** only sectional garage doors

	outFldWdt	= 210

	xPos2	= xPos1 + outFldWdt + 2

	inFldWdt	= 442 - xPos2

	ui_style 0, 1

	if iDoorPanelType = 0 then
		bFlag = 4
	else
		bFlag = 0
	endif

	ui_outfield `Количество Секций`,	xPos1,	y1,		outFldWdt,	outFldHgt, bFlag

	ui_style 0, 0
	ui_infield "gs_section_num",		xPos2,	y1-4,	inFldWdt,	inFldHgt

	y1 = y1 + dy

	ui_style 0, 1
	ui_infield{3} "gs_passdoor",		xPos1,	y1-4,	fullWidth,	inFldHgt,
		7, "", 2, 1,
		20, 20, 20, 20,
		1, `Дверь`,	1,
		2, `Дверь`,	2

	y1 = y1 + dy

	if gs_passdoor & (iDoorPanelType = 31 | (iDoorPanelType = 1 | iDoorPanelType = 2) & not(gs_bGlazedDoor)) then
		bFlag_passdoorwidth = 0
	else
		bFlag_passdoorwidth = 4
	endif

	ui_style 0, 0

	ui_outfield `Ширина Двери`,		xPos1,	y1,		outFldWdt,	outFldHgt, bFlag_passdoorwidth
	ui_infield "gs_passdoorWidth",		xPos2,	y1-4,	inFldWdt,	inFldHgt

	y1 = y1 + dy

	if iDoorPanelType <> 31 then
		ui_infield{3} "gs_bGlazedDoor",		xPos1,	y1-4,	fullWidth,	inFldHgt,
			7, "", 2, 1,
			20, 20, 20, 20,
			1, `Остекление Панели`,	1,
			2, `Остекление Панели`,	2

		y1 = y1 + dy

		ui_style 0, 1
		ui_outfield `Стекол`,			xPos1,	y1,		outFldWdt,	outFldHgt, bFlag
	
		ui_style 0, 0

		ui_infield "gs_hor_pnum",			xPos2,	y1-4,	inFldWdt,	inFldHgt
	else
		gosub "drawCustomPanel"
	endif
endif


! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


"drawCustomPanel":
	! --- Custom Panel

	y1 = y1

	if not(gs_ui_dtype = 14) then
		ui_style 0, 1
		ui_outfield `Имя Полотна Двери`,	xPosStart,		y1,		fullWidth,		outFldHgt

		y1 = y1 + dy
	endif

	ui_style 0, 0
	ui_infield "gs_cust_panel",			xPosStart+20,	y1-4,	fullWidth-20,	inFldHgt

!	if gs_ui_dtype = 12 then
!		y1 = y1 + 16
!	else
		y1 = y1 + 30
!	endif

	call "ui_CreateCustomPanelInfo_m" parameters	bWindow		= 0,
													xPosition	= xPosStart,
													yPosition	= y1,
													blockWidth	= 442 - xPosStart,
													blockHeight	= 265 - y1
return


"panelSelection":

	if gs_ui_dtype = 12 then
		! --- Sectional Garage doors
	
		ui_infield{3} "iDoorPanelType", xPos0, y1, picWdt, picHgt,
			1,"Garage_Panel_Types(13)", 15, 5,
			95, 110, 75, 60,
			1,`Стиль 1`,1,
			2,`Стиль 2`,2,
			8,`Стиль 3`,3,
			14,`Специальное Полотно Двери`,31,
			9,`Пусто`,0
	
	else
		if gs_ui_dtype = 10 then
			! --- Garage doors
	
			ui_infield{3} "iDoorPanelType", xPos0, y1, picWdt, picHgt,
				1,"Garage_Panel_Types(13)", 15, 5,
				95, 110, 75, 60,
				1,`Стиль 1`,1,
				2,`Стиль 2`,2,
				3,`Стиль 3`,3,
				4,`Стиль 4`,4,
				5,`Стиль 5`,5,
				6,`Стиль 6`,10,
				7,`Стиль 7`,11,
				8,`Стиль 8`,12,
				15,`Стиль 9`,14,
				14,`Специальное Полотно Двери`,31,
				9,`Пусто`,0
	
		endif
	endif
return



"2DSymbolSpecialOptions":
	dy	= 26

	penHgt		= 22
	fillHgt		= penHgt

	outFldWdt	= 150
	penWdt		= 40
	fillWdt		= 2 * penWdt

	xPos2	= xPos1 + outFldWdt + 2

	
	ui_style 0, 1
	ui_outfield `Параметры 2D-символа`, xPos1, y1, fullWidth, outFldHgt
	ui_style 0, 0
	
	y1 = y1 + dy

	if gs_doorcode<>"Gar1" and gs_doorcode<>"Gar2" and bEnableShowHandle then
		ui_infield{3} "bShowHandleOnSymbol",	xPos1,	y1,	fullWidth,	inFldHgt,
			7, "",
			2,
			1, 20, 20, 20, 20,
			0, `Показ Ручки в Символе`, 0,
			1, `Показ Ручки в Символе`, 1

		y1 = y1 + dy
	endif

	if bEnableShowGlassPanel then
		ui_infield{3} "bShowGlassPanelOnSymbol",	xPos1,	y1,	fullWidth,	inFldHgt,
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, `Показать в Символе как Остекление`, 0,
		1, `Показать в Символе как Остекление`, 1
	
		y1 = y1 + dy
	endif

	if not(slidingcode) and gs_doorcode<>"Gar2" and gs_doorcode<>"Gar3" and bEnableShowAirtightPanel then
		ui_infield{3} "bShowAirtightPanel",	xPos1,	y1,	fullWidth,	inFldHgt,
			7, "",
			2,
			1, 20, 20, 20, 20,
			0, `Показать Символ Герметичной Панели`, 0,
			1, `Показать Символ Герметичной Панели`, 1

		y1 = y1 + dy
	
		if gs_ui_dtype <> 13 then
			ui_outfield `Тип Штриховки Символа Герметизации`,			xPos1,			y1,		outFldWdt,			outFldHgt, not(bShowAirtightPanel)*4
			ui_infield "gs_fillAirtight",						xPos2,			y1-5,	fillWdt,			fillHgt
	
			y1 = y1 + dy

			ui_outfield `Перо Штриховки Символа Герметизации`,				xPos1,			y1,		outFldWdt+penWdt,	outFldHgt, not(bShowAirtightPanel)*4
			ui_infield "gs_penAirtight_fg",						xPos2+penWdt,	y1-5,	penWdt,				penHgt
	
			y1 = y1 + dy

			ui_outfield `Перо Фона Штриховки Символа Герметизации`,	xPos1,			y1,		outFldWdt+penWdt,	outFldHgt, not(bShowAirtightPanel)*4
			ui_infield "gs_penAirtight_bg",						xPos2+penWdt,	y1-5,	penWdt,				penHgt

			y1 = y1 + dy
		endif
	endif
return


