<?xml version="1.0" encoding="UTF-8"?>
<Symbol IsArchivable="false" IsPlaceable="true" MainGUID="9BB68FD8-2783-451A-A682-F372B3B7652E" MigrationValue="Normal" Owner="1196638531" Signature="1196644685" Version="43">
<Ancestry SectVersion="1" SectionFlags="0" SubIdent="0" Template="true">
	<MainGUID>F938E33A-329D-4A36-BE3E-85E126820996</MainGUID>
	<MainGUID>103E8D2C-8230-42E1-9597-46F84CCE28C0</MainGUID>
	<MainGUID>AD0A9673-1F68-45E0-89E1-9D72C32BC402</MainGUID>
	<MainGUID>789E3B5C-1381-4EE3-AC54-DC2DDDCAAB87</MainGUID>
	<MainGUID>7DF18395-B97C-41EE-9038-35D8D212CABD</MainGUID>
</Ancestry>

<CalledMacros SectVersion="2" SectionFlags="0" SubIdent="0">
	<Macro>
		<MName><![CDATA["Показ_по_сборкам"]]></MName>
		<MainGUID>8CE26AA8-652F-4D4A-95D8-E7E763E145D4</MainGUID>
	</Macro>
	<Macro>
		<MName><![CDATA["Libraryflobals-kzh"]]></MName>
		<MainGUID>F72542D8-E777-4AC3-B709-60D3307436E1</MainGUID>
	</Macro>
</CalledMacros>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_3D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
gosub "set_3d"
gosub "clear_buff_3d"
if GLOB_PREVIEW_MODE<>0 or GLOB_FEEDBACK_MODE=1 or GLOB_VIEW_TYPE=3 then show_3d_hotspot_t = 0
if not(is_call) then
	hotspot 0,0,0
	hotspot A,0,0
	hotspot 0,B,0
	hotspot A,B,0
	hotspot 0,0,ZZYZX
	hotspot A,0,ZZYZX
	hotspot 0,B,ZZYZX
	hotspot A,B,ZZYZX
endif

if show_type_3d=SHOW_GOST and (arm_array_1_dir>0 or arm_array_2_dir>0) then
	if arm_array_1_dir>0 then add sx_1, sy_1, sz_1
	if arm_array_2_dir>0 then add sx_2, sy_2, sz_2
	add sx, sy, sz
	put 0, 0, 0
	put ex, ey, ez
	put ex+ex_1+ex_2, ey+ey_1+ey_2, ez+ez_1+ez_2
	put ex_1+ex_2, ey_1+ey_2, ez_1+ez_2
	put 0, 0, 0
	PLANE NSP/3, get(NSP)
	end
endif


group "body"
	gosub "set_form"
	gosub "set_path"
	mask = 1+2+32+16
	add sx, sy, sz : n_trans = n_trans + 1
	TUBE n_form, n_path, mask, USE(n_form*3+n_path*4)
endgroup
group_name_out = "body"
gosub "clear_buff_3d"

if arm_array_1_dir>0 then
	group "array_1"
		add sx_1, sy_1, sz_1 : n_trans = n_trans + 1
		for i=1 to qty_1
			show_3d_hotspot_t = 0
			if i=1 then show_3d_hotspot_t = show_3d_hotspot
			if i=2 and abs(step_1_s-dx_1-dy_1)>EPS then show_3d_hotspot_t = show_3d_hotspot
			if i=qty_1-1 and abs(step_1_e-dx_1-dy_1)>EPS then show_3d_hotspot_t = show_3d_hotspot
			if i=qty_1 then show_3d_hotspot_t = show_3d_hotspot
			gosub "hotspot_3d"

			placegroup "body"
			if i=1 then 
				add step_1_s*(dx_1>EPS), step_1_s*(dy_1>EPS), step_1_s*(dz_1>EPS)
				n_trans = n_trans + 1
			endif
			if i=qty_1-1 then
				add step_1_e*(dx_1>EPS), step_1_e*(dy_1>EPS), step_1_e*(dz_1>EPS)
				n_trans = n_trans + 1
			endif
			if i<>1 and i<>qty_1-1 then
				add dx_1, dy_1, dz_1
				n_trans = n_trans + 1
			endif
		next i
	endgroup
	gosub "clear_buff_3d"
	group_name_out = "array_1"
	killgroup "body"
endif

if arm_array_2_dir>0 then
	group_name = "body"
	if arm_array_1_dir>0 then group_name = "array_1"
	group "array_2"
		add sx_2, sy_2, sz_2 : n_trans = n_trans + 1
		for i=1 to qty_2
			placegroup group_name
			if i=1 then 
				add step_2_s*(dx_2>EPS), step_2_s*(dy_2>EPS), step_2_s*(dz_2>EPS)
				n_trans = n_trans + 1
			endif
			if i=qty_2-1 then
				add step_2_e*(dx_2>EPS), step_2_e*(dy_2>EPS), step_2_e*(dz_2>EPS)
				n_trans = n_trans + 1
			endif
			if i<>1 and i<>qty_2-1 then
				add dx_2, dy_2, dz_2
				n_trans = n_trans + 1
			endif
		next i
	endgroup
	gosub "clear_buff_3d"
	group_name_out = "array_2"
	killgroup group_name
endif

placegroup group_name_out
killgroup group_name_out

end
"set_3d":
	show_subpos=1 : show_marker=1
	call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
	if not(show_subpos) then end
	
	show_all_fon = 1
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_all_fon", show_all_fon)
	if not(show_all_fon) and arm_fon then end
	
	show_all_plit = 1
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_all_plit", show_all_plit)
	if not(show_all_plit) and arm_plit then end
	
	show_direction = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_direction", show_direction)
	if show_direction<>arm_direction and show_direction then end
	
	show_constr = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_constr", show_constr)
	if show_constr<>arm_constr and show_constr then end
	
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "arm_mat", arm_mat)
	overr_area = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "overr_area", overr_area)
	if overr_area then
		area_lvl = 1
		success = LIBRARYGLOBAL ("Libraryflobals-kzh", "area_lvl", area_lvl)
		dim area_arr[]
		success = LIBRARYGLOBAL ("Libraryflobals-kzh", "area_arr", area_arr)
		dim area_mat[]
		success = LIBRARYGLOBAL ("Libraryflobals-kzh", "area_mat", area_mat)
		flag = 0
		if arm_area<=area_arr[1] then flag = 1
		if arm_area>=area_arr[area_lvl] then flag = area_lvl
		if not(flag) then
			for i=2 to area_lvl
				if arm_area>area_arr[i-1] and arm_area<=area_arr[i] then
					flag = i
					i = area_lvl
				endif
			next i
		endif
		if flag then 
			arm_mat = area_mat[flag]
		else
			arm_mat = area_mat[area_lvl]
		endif
	endif
	building_material arm_mat
	if overr_area then gs_cont_pen = gs_fill_pen
	pen gs_cont_pen
	sect_attrs{2} gs_cont_pen

	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_type_3d", show_type_3d)
	res = 24
	det_level_r1 = 6 : res_r1 = 24
	det_level_r2 = 12 : res_r2 = 12
	det_level_r3 = 24 : res_r3 = 6
	det_level_r4 = 36 : res_r4 = 3
	if GLOB_VIEW_TYPE=3 then
		r_cam=sqr((GLOB_EYEPOS_X-SYMB_POS_X)^2+(GLOB_EYEPOS_Y-SYMB_POS_Y)^2+(GLOB_EYEPOS_Z-SYMB_POS_Z)^2)
		if r_cam<=det_level_r1 then res = res_r1
		if r_cam<=det_level_r2 and r_cam>det_level_r1  then res = res_r2
		if r_cam<=det_level_r3 and r_cam>det_level_r2 then 
			res = res_r3
			show_type_3d = SHOW_LINE
			show_3d_hotspot_t = 0
		endif
		if r_cam>det_level_r4 then
			res = res_r4
			show_type_3d = SHOW_LINE
			show_3d_hotspot_t = 0
		endif
	endif
	if show_type_3d = SHOW_LINE then res = res_r4
	RESOL res
return

"set_form":
	n_form = 0
	tstart = NSP
	put 0,0,900+15
	if show_type_3d = SHOW_LINE then 
		put 0.5/1000,360,4000+15
	else
		put arm_diam/2,360,4000+15
	endif
	tend = NSP
	n_form = (tend - tstart)/3
return

"set_path":
	n_path = 0
	tstart = NSP
	gosub "linear"
	tend = NSP
	n_path = (tend - tstart)/4
return

"linear":
	PUT -1*(abs(ex)>EPS), -1*(abs(ey)>EPS), -1*(abs(ez)>EPS), 0
	PUT 0, 0, 0, 0
	PUT ex, ey, ez, 0
	PUT ex+(abs(ex)>EPS), ey+(abs(ey)>EPS), ez+(abs(ez)>EPS), 0
return

"clear_buff_3d":
	if NSP>0 then tt = min(get(NSP))
	if n_trans>1 then
		del n_trans
		n_trans = 0
	endif
return

"hotspot_3d":
	if show_3d_hotspot_t then
		add sx, sy, sz
		hotspot 0, 0, 0, unID_arm : unID_arm = unID_arm + 1
		hotspot ex, ey, ez, unID_arm : unID_arm = unID_arm + 1

		hotspot 0, arm_r*(ey<EPS), arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot arm_r*(ex<EPS), 0, arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot arm_r*(ex<EPS), arm_r*(ey<EPS), 0, unID_arm : unID_arm = unID_arm + 1

		hotspot 0, -arm_r*(ey<EPS), -arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot -arm_r*(ex<EPS), 0, -arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot -arm_r*(ex<EPS), -arm_r*(ey<EPS), 0, unID_arm : unID_arm = unID_arm + 1

		hotspot ex, arm_r*(ey<EPS), arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot arm_r*(ex<EPS), ey, arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot arm_r*(ex<EPS), arm_r*(ey<EPS), ez, unID_arm : unID_arm = unID_arm + 1

		hotspot ex, -arm_r*(ey<EPS), -arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot -arm_r*(ex<EPS), ey, -arm_r*(ez<EPS), unID_arm : unID_arm = unID_arm + 1
		hotspot -arm_r*(ex<EPS), -arm_r*(ey<EPS), ez, unID_arm : unID_arm = unID_arm + 1
		del 1
	endif
return
]]>
</Script_3D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_2D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
PAPER_TO_MODEL = GLOB_SCALE / 1000
if GLOB_PREVIEW_MODE<>0 or GLOB_FEEDBACK_MODE=1 then show_2d_hotspot = 0
gosub "set_2d"

if not(is_call) then
	hotspot2 0,0,0
	hotspot2 A,0,0
	hotspot2 0,B,0
	hotspot2 A,B,0
endif
!hotspot2 0, 0, unID_arm : unID_arm = unID_arm + 1
!hotspot2 0, 0, unID_el : unID_el = unID_el + 1

if show_2d_hotspot then
	if arm_array_1_dir>0 then add2 sx_1, sy_1
	if arm_array_2_dir>0 then add2 sx_2, sy_2
	add2 sx, sy
	hotspot2 ex+ex_1+ex_2, ey+ey_1+ey_2, unID_el : unID_el = unID_el + 1
	hotspot2 ex, ey+ey_1+ey_2, unID_el : unID_el = unID_el + 1
	hotspot2 ex+ex_1+ex_2, ey, unID_el : unID_el = unID_el + 1
	hotspot2 ex_1+ex_2, ey+ey_1+ey_2, unID_el : unID_el = unID_el + 1
	hotspot2 ex+ex_1+ex_2, ey_1+ey_2, unID_el : unID_el = unID_el + 1
	hotspot2 ex_1+ex_2, ey_1+ey_2, unID_el : unID_el = unID_el + 1
	hotspot2 0, 0, unID_el : unID_el = unID_el + 1
	del 1
	if arm_array_1_dir>0 then del 1
	if arm_array_2_dir>0 then del 1
endif

if show_type_2d=SHOW_GOST and arm_direction<>DIR_Z and ((arm_array_1_dir>0 and arm_array_1_dir<>DIR_Z) or (arm_array_2_dir>0 and arm_array_2_dir<>DIR_Z)) then
	if arm_array_1_dir>0 then add2 sx_1, sy_1
	if arm_array_2_dir>0 then add2 sx_2, sy_2
	add2 sx, sy
	put 0, 0, 1+32
	put ex+ex_1+ex_2, 0, 1+32
	put ex+ex_1+ex_2, ey+ey_1+ey_2, 1+32
	put 0, ey+ey_1+ey_2, 1+32

	fstr_vin = arm_pos + " " + str("%.0mm", arm_dlin_st)
	sstr_vin = "d" + str("%.0mm", arm_diam) + arm_class + " шаг " + str("%.0mm", arm_step)
	call "macro_vin" parameters all, inx_vin = arm_inx, fstr_vin = fstr_vin, sstr_vin = sstr_vin, use_hotspot = 0
	
	circle2 coord_vin[arm_inx][1], coord_vin[arm_inx][2], 0.5 * PAPER_TO_MODEL
	
	!Линия от-до
	sx_zone = coord_vin[arm_inx][1]*(ex>EPS)
	sy_zone = coord_vin[arm_inx][2]*(ey>EPS)
	ex_zone = (ex+ex_1+ex_2)*(ex<EPS)+coord_vin[arm_inx][1]*(ex>EPS)
	ey_zone = (ey+ey_1+ey_2)*(ey<EPS)+coord_vin[arm_inx][2]*(ey>EPS)
	pen gs_cont_pen
	line2 sx_zone, sy_zone, coord_vin[arm_inx][1], coord_vin[arm_inx][2]
	line2 ex_zone, ey_zone, coord_vin[arm_inx][1], coord_vin[arm_inx][2]

	!Стрелка
	str_ = 3 * PAPER_TO_MODEL
	strx_1 = str_
	strx_2 = str_-2*str_*(ex>EPS)
	stry_1 = str_
	stry_2 = str_-2*str_*(ey>EPS)
	line2 sx_zone+strx_1, sy_zone+stry_1, sx_zone, sy_zone
	line2 ex_zone-strx_1, ey_zone-stry_1, ex_zone, ey_zone
	line2 sx_zone+strx_2, sy_zone+stry_2, sx_zone, sy_zone
	line2 ex_zone-strx_2, ey_zone-stry_2, ex_zone, ey_zone

	!Линия стержня
	PEN 41
	sx_sterj = coord_vin[arm_inx][1]*(ex<EPS)
	sy_sterj = coord_vin[arm_inx][2]*(ey<EPS)
	ex_sterj = (ex+ex_1+ex_2)*(ex>EPS)+coord_vin[arm_inx][1]*(ex<EPS)
	ey_sterj = (ey+ey_1+ey_2)*(ey>EPS)+coord_vin[arm_inx][2]*(ey<EPS)
	gosub "gost_2d"
	pen gs_cont_pen
	fill gs_fill_type
	POLY2_B NSP/3, 1+2+4+64, gs_fill_pen, gs_back_pen, get(NSP)
else
	if arm_array_1_dir>0 then
		gosub "array_1"
	else
		if arm_array_2_dir>0 then
			gosub "array_2"
		else
			gosub "body"
		endif
	endif
endif

end

"gost_2d":
	gosub "linear_2d"
return

"linear_2d":
	line2 sx_sterj, sy_sterj, coord_vin[arm_inx][1], coord_vin[arm_inx][2]
	line2 ex_sterj, ey_sterj, coord_vin[arm_inx][1], coord_vin[arm_inx][2]
return

"set_2d":
	show_all_fon = 1
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_all_fon", show_all_fon)
	if not(show_all_fon) and arm_fon then end
	
	show_all_plit = 1
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_all_plit", show_all_plit)
	if not(show_all_plit) and arm_plit then end
	
	show_direction = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_direction", show_direction)
	if show_direction<>arm_direction and show_direction then end
	
	show_constr = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_constr", show_constr)
	if show_constr<>arm_constr and show_constr then end


	show_constr = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_constr", show_constr)
	if show_constr<>arm_constr and show_constr then end

	show_layer = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "show_layer", show_layer)
	if show_layer<>arm_layer and show_layer then end

	call "Показ_по_сборкам" PARAMETERS ALL subpos=subpos, pos=GLOB_ID RETURNED_PARAMETERS show_subpos, show_marker
	if not(show_subpos) then end

	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "arm_mat", arm_mat)

	overr_area = 0
	success = LIBRARYGLOBAL ("Libraryflobals-kzh", "overr_area", overr_area)
	if overr_area then
		area_lvl = 1
		success = LIBRARYGLOBAL ("Libraryflobals-kzh", "area_lvl", area_lvl)
		dim area_arr[]
		success = LIBRARYGLOBAL ("Libraryflobals-kzh", "area_arr", area_arr)
		dim area_mat[]
		success = LIBRARYGLOBAL ("Libraryflobals-kzh", "area_mat", area_mat)
		flag = 0
		if arm_area<=area_arr[1] then flag = 1
		if arm_area>=area_arr[area_lvl] then flag = area_lvl
		if not(flag) then
			for i=2 to area_lvl
				if arm_area>area_arr[i-1] and arm_area<=area_arr[i] then
					flag = i
					i = area_lvl
				endif
			next i
		endif
		if flag then arm_mat = area_mat[flag]
	endif
	n = REQUEST{2} ("Building_Material_info", arm_mat, "gs_bmat_cutfill_properties", gs_fill_type, gs_fill_pen, gs_back_pen)
	if overr_area then gs_cont_pen = gs_fill_pen
	pen gs_cont_pen
return

"body":
	add2 sx, sy
	if arm_direction = DIR_Z then 
		CIRCLE2 0,0,arm_r
		if show_2d_hotspot_t then
			hotspot2 0,0, unID_arm : unID_arm = unID_arm + 1
			hotspot2 arm_r,0, unID_arm : unID_arm = unID_arm + 1
			hotspot2 0,arm_r, unID_arm : unID_arm = unID_arm + 1
			hotspot2 arm_r,arm_r, unID_arm : unID_arm = unID_arm + 1
			hotspot2 -arm_r,0, unID_arm : unID_arm = unID_arm + 1
			hotspot2 0,-arm_r, unID_arm : unID_arm = unID_arm + 1
			hotspot2 -arm_r,-arm_r, unID_arm : unID_arm = unID_arm + 1
		endif
	else
		if show_type_2d = SHOW_LINE then 
			line2 0,0, ex, ey
			if show_2d_hotspot_t then
				hotspot2 0, 0, unID_arm : unID_arm = unID_arm + 1
				hotspot2 ex, ey, unID_arm : unID_arm = unID_arm + 1
			endif
		endif
		if show_type_2d = SHOW_SOLID or show_type_2d=SHOW_GOST then
			line2 -arm_r*(ex<EPS),-arm_r*(ey<EPS), ex-arm_r*(ex<EPS), ey-arm_r*(ey<EPS)
			line2 arm_r*(ex<EPS),arm_r*(ey<EPS), ex+arm_r*(ex<EPS), ey+arm_r*(ey<EPS)
			line2 arm_r*(ex<EPS),arm_r*(ey<EPS), -arm_r*(ex<EPS), -arm_r*(ey<EPS)
			line2 ex+arm_r*(ex<EPS),ey+arm_r*(ey<EPS), ex-arm_r*(ex<EPS), ey-arm_r*(ey<EPS)
			if show_2d_hotspot_t then
				hotspot2 0, 0, unID_arm : unID_arm = unID_arm + 1
				hotspot2 ex, ey, unID_arm : unID_arm = unID_arm + 1
				hotspot2 ex+arm_r*(ex<EPS),ey+arm_r*(ey<EPS), unID_arm : unID_arm = unID_arm + 1
				hotspot2 ex-arm_r*(ex<EPS),ey-arm_r*(ey<EPS), unID_arm : unID_arm = unID_arm + 1
				hotspot2 arm_r*(ex<EPS),arm_r*(ey<EPS), unID_arm : unID_arm = unID_arm + 1
				hotspot2 -arm_r*(ex<EPS),-arm_r*(ey<EPS), unID_arm : unID_arm = unID_arm + 1
			endif
		endif
	endif
	del 1
return

"array_1":
	dxe_1 = sx_1 : dye_1 = sy_1
	add2 sx_1, sy_1
	for i1=1 to qty_1
		show_2d_hotspot_t = 0
		if i1=1 then show_2d_hotspot_t = show_2d_hotspot
		if i1=2 and abs(step_1_s-dx_1-dy_1)>EPS then show_2d_hotspot_t = show_2d_hotspot
		if i1=qty_1-1 and abs(step_1_e-dx_1-dy_1)>EPS then show_2d_hotspot_t = show_2d_hotspot
		if i1=qty_1 then show_2d_hotspot_t = show_2d_hotspot
		if arm_array_2_dir>0 then
			gosub "array_2"
			add2 -dxe_2, -dye_2
		else
			gosub "body"
		endif
		if i1=1 then 
			add2 step_1_s*(dx_1>EPS), step_1_s*(dy_1>EPS)
			dxe_1 = dxe_1 + step_1_s*(dx_1>EPS) : dye_1 = dye_1 + step_1_s*(dy_1>EPS)
		endif
		if i1=qty_1-1 then
			add2 step_1_e*(dx_1>EPS), step_1_e*(dy_1>EPS)
			dxe_1 = dxe_1 + step_1_e*(dx_1>EPS) : dye_1 = dye_1 + step_1_e*(dy_1>EPS)
		endif
		if i1<>1 and i1<>qty_1-1 then
			add2 dx_1, dy_1
			dxe_1 = dxe_1 + dx_1 : dye_1 = dye_1 + dy_1
		endif
	next i1
return

"array_2":
	dxe_2 = sx_2 : dye_2 = sx_2
	add2 sx_2, sy_2
	for i2=1 to qty_2
		show_2d_hotspot_t = 0
		if i2=1 then show_2d_hotspot_t = show_2d_hotspot
		if i2=2 and abs(step_2_s-dx_2-dy_2)>EPS then show_2d_hotspot_t = show_2d_hotspot
		if i2=qty_2-1 and abs(step_2_e-dx_2-dy_2)>EPS then show_2d_hotspot_t = show_2d_hotspot
		if i2=qty_2 then show_2d_hotspot_t = show_2d_hotspot
		gosub "body"
		if i2=1 then 
			add2 step_2_s*(dx_2>EPS), step_2_s*(dy_2>EPS)
			dxe_2 = dxe_2 + step_2_s*(dx_2>EPS) : dye_2 = dye_2 + step_2_s*(dy_2>EPS)
		endif
		if i2=qty_2-1 then
			add2 step_2_e*(dx_2>EPS), step_2_e*(dy_2>EPS)
			dxe_2 = dxe_2 + step_2_e*(dx_2>EPS) : dye_2 = dye_2 + step_2_e*(dy_2>EPS)
		endif
		if i2<>1 and i2<>qty_2-1 then
			add2 dx_2, dy_2
			dxe_2 = dxe_2 + dx_2 : dye_2 = dye_2 + dy_2
		endif
	next i2
return

"clear_buff_2d":
	if NSP>0 then tt = min(get(NSP))
	if n_trans>0 then
		del n_trans
		n_trans = 0
	endif
return



]]>
</Script_2D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_1D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
unID_arm=arm_inx*100
unID_el=arm_inx*5000
EPS=0.0001
n_trans = 0

if arm_inx<EPS then arm_inx=1
! -------- Типы отображения ------------
SHOW_GOST = 1
SHOW_LINE = 2
SHOW_SOLID = 3

! -------- Типы конструкций -------------
TYPE_SLAB = 1
TYPE_WALL = 2
TYPE_BEAM = 3
TYPE_COLUMN = 4

! --------- Направления -----------------
DIR_X = 1
DIR_Y = 2
DIR_Z = 3

! --------- Форма стержня ---------------
LIN_START = 0						!---- Начало линейных стержней ----
	LINN	=	LIN_START + 1			!Простой прямой
	LIN_P	=	LIN_START + 2		!С П-образным загибом
	LIN_G	=	LIN_START + 3		!С Г-образным загибом

LIN_END = 19						!---- Конец линейных стержней ----

KR_START = 20						!---- Начало поддерживающей/поперечной арматуры ----
	KZ	=		KR_START + 1		!Каркас змейкой
	KP3	=		KR_START + 2		!Каркас пространственный с 3 стержнями (козлик)
	FROG =		KR_START + 3		!Лягушка
	KP4	=		KR_START + 4		!Каркас пространственный
	KSHP =		KR_START + 5		!Шпилька (стены)
	KPR =		KR_START + 6		!П-шка (стены)

KR_END = 29							!---- Конец поддерживающей/поперечной арматуры ----

GNUT_START = 30						!---- Начало гнутых стержней ----
	GNUT_P	=	GNUT_START + 1		!П-шка
GNUT_END = 60						!---- Конец гнутых стержней ----

dict armconst_t
if not(haskey(armconst.flag)) then
	armconst_t.flag = 1
	armconst_t.form.LIN_START = LIN_START
	armconst_t.form.LINN = LINN
	armconst_t.form.LIN_P = LIN_P
	armconst_t.form.LIN_G = LIN_G
	armconst_t.form.LIN_END = LIN_END
	armconst_t.form.KR_START = KR_START
	armconst_t.form.KZ = KZ
	armconst_t.form.KP3 = KP3
	armconst_t.form.FROG = FROG
	armconst_t.form.KP4 = KP4
	armconst_t.form.KSHP = KSHP
	armconst_t.form.KPR = KPR
	armconst_t.form.KR_END = KR_END
	armconst_t.form.GNUT_START = GNUT_START
	armconst_t.form.GNUT_P = GNUT_P
	armconst_t.form.GNUT_END = GNUT_END

	armconst_t.show.GOST = SHOW_GOST
	armconst_t.show.LINE = SHOW_LINE
	armconst_t.show.SOLID = SHOW_SOLID

	armconst_t.type.SLAB = TYPE_SLAB
	armconst_t.type.WALL = TYPE_WALL
	armconst_t.type.BEAM = TYPE_BEAM
	armconst_t.type.COLUMN = TYPE_COLUMN

	armconst_t.dir.X = DIR_X
	armconst_t.dir.Y = DIR_Y
	armconst_t.dir.Z = DIR_Z
	
	armconst = armconst_t
	parameters armconst = armconst_t
endif

!!Общие параметры
!armd.el[arm_inx].constr            !Тип конструкции
!armd.el[arm_inx].dir               !Направление оси стержня
!armd.el[arm_inx].length            !Длина стержня в модели
!armd.el[arm_inx].layer             !Номер слоя
!armd.el[arm_inx].area              !Площадь армирования
!armd.el[arm_inx].flag_recal        !Нужно ли пересчитывать расположение стержней (0/1)
!armd.el[arm_inx].maxrad            !Максимальный радиус изгиба
!armd.el[arm_inx].form.type         !Тип формы стержня
!armd.el[arm_inx].form.coord[]      !Параметры формы стержня

!!Отступ начала элемента
!armd.el[arm_inx].offset.x
!armd.el[arm_inx].offset.y
!armd.el[arm_inx].offset.z

!!Направление 1
!!Если задано направление 2, но не задано направление 1 - копируем всё в 1
!armd.el[arm_inx].array[1].dir         !Направление массива
!armd.el[arm_inx].array[1].step        !Шаг массива
!armd.el[arm_inx].array[1].step_offset !Смещение шага при наличии внторого направления (для шахматного порядка)
!armd.el[arm_inx].array[1].offset      !Смещение начала массива
!armd.el[arm_inx].array[1].length      !Длина массива
!armd.el[arm_inx].array[1].step_start  !Шаг первого стержня
!armd.el[arm_inx].array[1].step_end    !Шаг последнего стержня
!armd.el[arm_inx].array[1].qty         !Количество стержней в направлении 1


!!Направление 2 - массив стерней из направления 1
!armd.el[arm_inx].array[2].dir         !Направление массива
!armd.el[arm_inx].array[2].step        !Шаг массива
!armd.el[arm_inx].array[2].offset      !Смещение начала массива
!armd.el[arm_inx].array[2].length      !Длина массива
!armd.el[arm_inx].array[2].step_start  !Шаг первого стержня
!armd.el[arm_inx].array[2].step_end    !Шаг последнего стержня
!armd.el[arm_inx].array[2].qty         !Количество стержней в направлении 2

!!Спецификация
!armd.el[arm_inx].spec.pos             !Позиция стержня
!armd.el[arm_inx].spec.subpos          !Марка сборки
!armd.el[arm_inx].spec.diam            !Диаметр
!armd.el[arm_inx].spec.r               !Радиус

!armd.el[arm_inx].spec.class           !Класс
!armd.el[arm_inx].spec.length          !Длина в спецификацию
!armd.el[arm_inx].spec.fon             !Выводить в п.м. (0/1)
!armd.el[arm_inx].spec.plit            !Фоновое армирование (0/1)
!armd.el[arm_inx].spec.gnut            !Гнутый элемент (0/1)
!armd.el[arm_inx].spec.otm_elem        !Отметка начала элемента
!armd.el[arm_inx].spec.gost            !ГОСТ
!armd.el[arm_inx].spec.ves             !Масса элемента
!armd.el[arm_inx].spec.ves_p           !Масса п.м. элемента
!armd.el[arm_inx].spec.qty             !Количество

arm_diam = armd.el[arm_inx].spec.diam
arm_class = armd.el[arm_inx].spec.class
arm_constr = armd.el[arm_inx].constr
arm_direction = armd.el[arm_inx].dir
arm_length = armd.el[arm_inx].length
arm_array_1_dir = armd.el[arm_inx].array[1].dir
arm_array_1_step = armd.el[arm_inx].array[1].step
arm_array_1_dir_chess = armd.el[arm_inx].array[1].step_offset
arm_array_1_length = armd.el[arm_inx].array[1].length
arm_array_2_dir = armd.el[arm_inx].array[1].dir
arm_array_2_step = armd.el[arm_inx].array[1].step
arm_array_2_length = armd.el[arm_inx].array[1].length
ss_x = armd.el[arm_inx].offset.x
ss_y = armd.el[arm_inx].offset.y
ss_z = armd.el[arm_inx].offset.z
s_1 = armd.el[arm_inx].array[1].offset
s_2 = armd.el[arm_inx].array[2].offset
arm_layer = armd.el[arm_inx].layer

if arm_array_2_length<arm_array_2_step then arm_array_2_step = arm_array_2_length
if arm_array_1_length<arm_array_1_step then arm_array_1_step = arm_array_1_length
if arm_array_1_step<EPS or arm_array_1_length<EPS then arm_array_1_dir = 0
if arm_array_2_step<EPS or arm_array_2_length<EPS then arm_array_2_dir = 0

arm_r = arm_diam/2
s_1 = s_1 + arm_r : s_2 = s_2 + arm_r
arm_array_1_length = arm_array_1_length - arm_diam
arm_array_2_length = arm_array_2_length - arm_diam

qty_1 = 0
if arm_array_1_dir>0 then
	step_1_s = arm_array_1_step : step_1_e = arm_array_1_step
	qty_1 = int(arm_array_1_length / arm_array_1_step)
	if (arm_array_1_length - qty_1 * arm_array_1_step) > EPS then
		step_1_s = (arm_array_1_length - (qty_1 - 1) * arm_array_1_step) / 2
		step_1_e = step_1_s
		qty_1 = qty_1 + 1
	endif
	qty_1 = qty_1 + 1
endif

qty_2 = 0
if arm_array_2_dir>0 then
	step_2_s = arm_array_2_step : step_2_e = arm_array_2_step
	qty_2 = int(arm_array_2_length / arm_array_2_step)
	if (arm_array_2_length - qty_2 * arm_array_2_step) > EPS then
		step_2_s = (arm_array_2_length - (qty_2 - 1) * arm_array_2_step) / 2
		step_2_e = step_2_s
		qty_2 = qty_2 + 1
	endif
	qty_2 = qty_2 + 1
endif

ex=0 : ey=0 : ez=0
sx=0 : sy=0 : sz=0
if arm_direction = DIR_X then
	ex = arm_length
	sx = ss_x
	sy = ss_y
	sz = ss_z
endif
if arm_direction = DIR_Y then
	ey = arm_length
	sy = ss_x
	sx = ss_y
	sz = ss_z
endif
if arm_direction = DIR_Z then
	ez = arm_length
	sz = ss_x
	sy = ss_y
	sx = ss_z
endif

!if show_type_2d=SHOW_GOST and show_type_3d=SHOW_GOST then
!	s_1 = s_1 - arm_r : s_2 = s_2 - arm_r
!	arm_array_1_length = arm_array_1_length + arm_diam
!	arm_array_2_length = arm_array_2_length + arm_diam
!endif

dx_1=0 : dy_1=0 : dz_1=0
sx_1=0 : sy_1=0 : sz_1=0
ex_1=0 : ey_1=0 : ez_1=0
if arm_array_1_dir = DIR_X then 
	dx_1 = arm_array_1_step
	ex_1 = arm_array_1_length
	sx_1 = s_1
endif
if arm_array_1_dir = DIR_Y then 
	dy_1 = arm_array_1_step
	ey_1 = arm_array_1_length
	sy_1 = s_1
endif
if arm_array_1_dir = DIR_Z then 
	dz_1 = arm_array_1_step
	ez_1 = arm_array_1_length
	sz_1 = s_1
endif

dx_2=0 : dy_2=0 : dz_2=0
sx_2=0 : sy_2=0 : sz_2=0
ex_2=0 : ey_2=0 : ez_2=0
if arm_array_2_dir = DIR_X then
	dx_2 = arm_array_2_step
	ex_2 = arm_array_2_length
	sx_2 = s_2
endif
if arm_array_2_dir = DIR_Y then
	dy_2 = arm_array_2_step
	ey_2 = arm_array_2_length
	sy_2 = s_2
endif
if arm_array_2_dir = DIR_Z then
	dz_2 = arm_array_2_step
	ez_2 = arm_array_2_length
	sz_2 = s_2
endif

arm_kol_vo_spec = max(1, qty_1, qty_2, qty_1 * qty_2)
dlin_ed_st = 11.7
l_nahl = 0.6
arm_dlin_st = arm_length
if arm_length > dlin_ed_st then
	arm_fon = 1
	n_st = round_int(arm_length / dlin_ed_st)
	arm_dlin_st = round_int((n_st * l_nahl + arm_length)*10)/10
else
	arm_dlin_st = round_int(arm_length*10)/10
endif

arm_step = arm_array_1_step
if qty_2 > qty_1 then arm_step = arm_array_2_step
arm_area = 0
if arm_step>EPS then arm_area = int(100*100*PI*arm_r^2*(1/arm_step)*100)/100

]]>
</Script_1D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_PR SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_PR>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_UI SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_UI>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_VL SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_VL>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_FWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_FWM>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_BWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_BWM>

<ParamSection SectVersion="27" SectionFlags="0" SubIdent="0">
	<ParamSectHeader>
		<AutoHotspots>false</AutoHotspots>
		<StatBits>
			<STBit_FixSize/>
		</StatBits>
		<WDLeftFrame>0</WDLeftFrame>
		<WDRightFrame>0</WDRightFrame>
		<WDTopFrame>0</WDTopFrame>
		<WDBotFrame>0</WDBotFrame>
		<LayFlags>65535</LayFlags>
		<WDMirrorThickness>0</WDMirrorThickness>
		<WDWallInset>0</WDWallInset>
	</ParamSectHeader>
	<Parameters>
		<Length Name="A">
			<Description><![CDATA["Размер 1"]]></Description>
			<Fix/>
			<Value>1</Value>
		</Length>
		<Length Name="B">
			<Description><![CDATA["Размер 2"]]></Description>
			<Fix/>
			<Value>1</Value>
		</Length>
		<Length Name="ZZYZX">
			<Description><![CDATA["Высота"]]></Description>
			<Fix/>
			<Value>1</Value>
		</Length>
		<Boolean Name="AC_show2DHotspotsIn3D">
			<Description><![CDATA["Показ Узловых 2D Точек в 3D"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Boolean>
		<Length Name="ac_bottomlevel">
			<Description><![CDATA["Отметка Низа"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="ac_toplevel">
			<Description><![CDATA["Отметка Верха"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Length>

		<!-- gs_2D_representation: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_2D_representation">
			<Description><![CDATA["2D-отображение"]]></Description>
			<Fix/>
		</Title>
		<PenColor Name="gs_cont_pen">
			<Description><![CDATA["Перо Контура"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>2</Value>
		</PenColor>
		<FillPattern Name="gs_fill_type">
			<Description><![CDATA["Тип Штриховки"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>65</Value>
		</FillPattern>
		<PenColor Name="gs_fill_pen">
			<Description><![CDATA["Перо Штриховки"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>19</Value>
		</PenColor>
		<PenColor Name="gs_back_pen">
			<Description><![CDATA["Перо Фона Штриховки"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>19</Value>
		</PenColor>

		<!-- arm_sp_api: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="arm_sp_api">
			<Description><![CDATA["Параметры для вызова"]]></Description>
			<Fix/>
		</Title>
		<Boolean Name="show_2d_hotspot">
			<Description><![CDATA[""]]></Description>
			<Fix/>
			<Value>0</Value>
		</Boolean>
		<Boolean Name="show_3d_hotspot">
			<Description><![CDATA[""]]></Description>
			<Fix/>
			<Value>0</Value>
		</Boolean>
		<Integer Name="arm_inx">
			<Description><![CDATA["Индекс элемента"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Integer>
		<Dictionary Name="armd">
			<Description><![CDATA["Словарь с данными о стержнях"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>
				<Array Name="INX">
					<Dictionary Index="1">
						<Integer Name="PARAM1">10</Integer>
						<Integer Name="PARAM2">20</Integer>
						<Integer Name="PARAM3">30</Integer>
					</Dictionary>
				</Array>
			</Value>
		</Dictionary>
		<Dictionary Name="armconst">
			<Description><![CDATA["Словарь с константами"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>
				<Integer Name="FLAG">1</Integer>
				<Dictionary Name="FORM">
					<Integer Name="LIN_START">0</Integer>
					<Integer Name="LINN">1</Integer>
					<Integer Name="LIN_P">2</Integer>
					<Integer Name="LIN_G">3</Integer>
					<Integer Name="LIN_END">19</Integer>
					<Integer Name="KR_START">20</Integer>
					<Integer Name="KZ">21</Integer>
					<Integer Name="KP3">22</Integer>
					<Integer Name="FROG">23</Integer>
					<Integer Name="KP4">24</Integer>
					<Integer Name="KSHP">25</Integer>
					<Integer Name="KPR">26</Integer>
					<Integer Name="KR_END">29</Integer>
					<Integer Name="GNUT_START">30</Integer>
					<Integer Name="GNUT_P">31</Integer>
					<Integer Name="GNUT_END">60</Integer>
				</Dictionary>
				<Dictionary Name="SHOW">
					<Integer Name="GOST">1</Integer>
					<Integer Name="LINE">2</Integer>
					<Integer Name="SOLID">3</Integer>
				</Dictionary>
				<Dictionary Name="TYPE">
					<Integer Name="SLAB">1</Integer>
					<Integer Name="WALL">2</Integer>
					<Integer Name="BEAM">3</Integer>
					<Integer Name="COLUMN">4</Integer>
				</Dictionary>
				<Dictionary Name="DIR">
					<Integer Name="X">1</Integer>
					<Integer Name="Y">2</Integer>
					<Integer Name="Z">3</Integer>
				</Dictionary>
			</Value>
		</Dictionary>
		<Length Name="coord_vin">
			<Description><![CDATA[""]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<ArrayValues FirstDimension="1" SecondDimension="0">
				<AVal Row="1">0</AVal>
			</ArrayValues>
		</Length>
		<RealNum Name="coord_vin_s">
			<Description><![CDATA[""]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<ArrayValues FirstDimension="1" SecondDimension="0">
				<AVal Row="1">0</AVal>
			</ArrayValues>
		</RealNum>
		<String Name="arm_name_arr">
			<Description><![CDATA[""]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<ArrayValues FirstDimension="1" SecondDimension="0">
				<AVal Row="1"><![CDATA[""]]></AVal>
			</ArrayValues>
		</String>
	</Parameters>
</ParamSection>

<Copyright SectVersion="1" SectionFlags="0" SubIdent="0">
	<Author>kuvbur</Author>
	<License>
		<Type>CC0</Type>
		<Version>1.0</Version>
	</License>
</Copyright>

<Keywords SectVersion="1" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Keywords>

<Comment SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Comment>

</Symbol>




