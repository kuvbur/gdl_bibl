<?xml version="1.0" encoding="UTF-8"?>
<Symbol IsArchivable="false" IsPlaceable="true" MainGUID="80F8FBE9-ED18-4FFF-B34F-81C834F67B9D" MigrationValue="Normal" Owner="1196638531" Signature="1196644685" Version="43">
<Ancestry SectVersion="1" SectionFlags="0" SubIdent="0" Template="false">
	<MainGUID>F938E33A-329D-4A36-BE3E-85E126820996</MainGUID>
	<MainGUID>B176ABF1-5813-478F-926B-28EE7C5DC1F7</MainGUID>
	<MainGUID>4FD10D67-2F29-4844-A65A-6597589B0CB5</MainGUID>
</Ancestry>

<CalledMacros SectVersion="2" SectionFlags="0" SubIdent="0">
	<Macro>
		<MName><![CDATA["Подпись"]]></MName>
		<MainGUID>37D5A2DA-1C7F-4C0B-BF58-60B4DB467A96</MainGUID>
	</Macro>
</CalledMacros>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_3D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_3D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_2D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
L=0.01*GLOB_SCALE
MUL2   L,L
hotspot2 0,0
DEFINE STYLE "текст25-5"  fontType, r255, stype, r255type
SET STYLE "текст25-5"
PEN pen_text
!Пааанеслась

if data_type="Подпись" or data_type="Фамилия" then
	surname = ""
	flag_error = 1
	!Основная надпись
	for i=1 to n_position
		if data_subtype=position_info[i][1] then
			surname = position_info[i][2]
			flag_error = 0
			i=n_position
		endif
	next i
	if surname = "" then
		!Согласовано
		for i=1 to n_agreed
			if data_subtype=agreed_info[i][1] then
				flag_error = 0
				surname = agreed_info[i][2]
				i=n_agreed
			endif
		next i
	endif
	if flag_error then
		PEN pen_error
		text2 0,0,"Должность не найдена"
	endif
	if data_type="Подпись" and flag_error = 0 then
		add2 -0.4,0
		CALL "Подпись" parameters surname = surname, pen_podp=pen_podp
	endif
	if tis_fam and data_type="Фамилия" and flag_error = 0 then
		text2 0,0,surname
	endif
endif

if data_type="Раздел" then
	text2 0,0,current_part
endif

if data_type="Стадия" then
	text2 0,0,current_phase
endif

if data_type="Дата изм. 0" and tis_date then
	text2 0,0,current_date
endif

if data_type="Дата последнего изм."then
	text2 0,0,change_info[1][5]
endif
if data_type="Номер последнего изм." then
	text2 0,0,change_info[1][1]
endif
if data_type="Номер разрешения на последний изм." then
	text2 0,0,change_info[1][3]
endif
]]>
</Script_2D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_1D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
EPS=0.0001

dim isorttype[] : dim tsorttype[] : i=0
i=i+1 : tsorttype[i] = "Изменения->Макеты" : isorttype[i] = 0
i=i+1 : tsorttype[i] = "Макеты->Изменения" : isorttype[i] = 1
i=i+1 : tsorttype[i] = "Порядок из выпуска" : isorttype[i] = 2

dim irtype[] : dim trtype[] : i=0
i=i+1 : trtype[i] = "Обычный" : irtype[i] = 0
i=i+1 : trtype[i] = "Полужирный" : irtype[i] = 1
i=i+1 : trtype[i] = "Курсив" : irtype[i] = 2
i=i+1 : trtype[i] = "Полужирный курсив" : irtype[i] = 3

dim istype[] : dim tstype[] : i=0
i=i+1 : tstype[i] = "Слева" : istype[i] = 7
i=i+1 : tstype[i] = "Центр" : istype[i] = 8
i=i+1 : tstype[i] = "Справа" : istype[i] = 9

ROW_PHASE_PART = 2 !Стадия
ROW_DATE = 8 !Дата изма
ROW_IZM_N_DOC = 6 !Номер документа
ROW_IZM_SUR = 7 !Фамилия изменяющего

n_change = -1
n_position = -1
n_agreed = -1
dim error[ ]
dim position_info[ ][ ]
dim agreed_info[ ][ ]
dim change_info[ ][ ]
dim arr1[ ]
dim arr2[ ][ ]
!------------------------------------------------------------------------------------------------
! Check global existence
!------------------------------------------------------------------------------------------------
dim GlobIssueScheme[2][]
dim GlobchangeScheme[2][]
dim LayoutRevHistory[][]
dim LayoutchangeHistory[][]


_bEmptyGlobIssues = 0
_bEmptyGlobchanges = 0
_bEmptyLayoutRevHistory = 0
_bEmptyLayoutChHistory = 0
_bfirstchange = 0
_bEmptychange = 0
n_izm = 0
n_err = 0

current_phase = " "
current_part = " "
current_date = " "
surname = " " 
delim = " "
tis_position = 1
tis_autosign = 1
tis_fam = 1
tis_date = 1

if vardim2(GLOB_ISSUE_SCHEME) > 0 then
	! global exists
	GlobIssueScheme	= GLOB_ISSUE_SCHEME
else
	_bEmptyGlobIssues = 1
endif

if vardim2(GLOB_change_SCHEME) > 0 then
	! global exists
	GlobchangeScheme = GLOB_change_SCHEME
else
	_bEmptyGlobchanges = 1
endif

if GLOB_SCRIPT_TYPE = 2 then
	_bLayoutContext = (GLOB_CONTEXT = 8 | GLOB_CONTEXT = 28)
	if _bLayoutContext then
		! Layout context or Layout Editing feedback: use globals from Issue Manager
		LayoutRevHistory = LAYOUT_REVISION_HISTORY
		LayoutchangeHistory	= LAYOUT_change_HISTORY
		last_change_inx = vardim1(LayoutchangeHistory)
		last_rev_inx = vardim1(LayoutRevHistory)
	endif
endif
if vardim2(LayoutRevHistory) <> vardim2(GlobIssueScheme) then _bEmptyLayoutRevHistory = 1
if vardim2(LayoutchangeHistory) <> vardim2(GlobchangeScheme) then _bEmptyLayoutChHistory = 1
if not(_bEmptyLayoutRevHistory) & _bEmptyLayoutChHistory then
	! if has revision data but no change attached, create 1 dummy empty change row
	for _ichangeCol = 1 to vardim2(GLOB_change_SCHEME)
		if _ichangeCol = 1 then
			LayoutchangeHistory[1][1] = LayoutRevHistory[1][1]
		else
			LayoutchangeHistory[1][_ichangeCol] = ""
		endif
	next _ichangeCol
endif

if not(_bEmptyLayoutRevHistory) then
	ROW_FAM_START_ISSUE = 0 !Начало штампа выпуска
	ROW_FAM_END_ISSUE = 0 !Конец штампа выпуска
	ROW_FAM_START_CHANGE = 0 !Начало штампа изменений
	ROW_FAM_END_CHANGE = 0 !Конец штампа изменений
	ROW_FAM_START_LAYUOT = 0 !Начало штампа макета
	ROW_FAM_END_LAYUOT = 0 !Конец штампа макета
	ROW_FAM_START_AGREED = 0 !Начало согласований
	ROW_FAM_END_AGREED = 0 !Конец согласований

	for i = 1 to vardim2(GlobIssueScheme)
		if STRSTR(GlobIssueScheme[1][i], "!!НАЧАЛО ШТАМПА!!") then ROW_FAM_START_ISSUE = i+1
		if STRSTR(GlobIssueScheme[1][i], "!!КОНЕЦ ШТАМПА!!") then ROW_FAM_END_ISSUE = i-1
		if STRSTR(GlobIssueScheme[1][i], "!!СОГЛАСОВАНО!!") then ROW_FAM_START_AGREED = i+1
		if STRSTR(GlobIssueScheme[1][i], "!!КОНЕЦ СОГЛАСОВАНО!!") then ROW_FAM_END_AGREED = i-1
		if STRSTR(GlobIssueScheme[1][i], "Должности (ДА/НЕТ)")>0 then
			if STRSTR(LayoutRevHistory[1][i], "НЕТ", 1)>0 then tis_position = 0
		endif
		if STRSTR(GlobIssueScheme[1][i], "Подписи (ДА/НЕТ)")>0 then
			if STRSTR(LayoutRevHistory[1][i], "НЕТ", 1)>0 then tis_autosign = 0
		endif
		if STRSTR(GlobIssueScheme[1][i], "Фамилии (ДА/НЕТ)")>0 then
			if STRSTR(LayoutRevHistory[1][i], "НЕТ", 1)>0 then tis_fam = 0
		endif
		if STRSTR(GlobIssueScheme[1][i], "Дата (ДА/НЕТ)")>0 then
			if STRSTR(LayoutRevHistory[1][i], "НЕТ", 1)>0 then tis_date = 0
		endif
	next i
	!Изменения
	for i = 1 to vardim2(GlobchangeScheme)
		if STRSTR(GlobchangeScheme[1][i], "!!НАЧАЛО ИЗМ. 0!!") then ROW_FAM_START_CHANGE = i+2
		if STRSTR(GlobchangeScheme[1][i], "!!КОНЕЦ ИЗМ. 0!!") then ROW_FAM_END_CHANGE = i-1
		if STRSTR(GlobchangeScheme[1][i], "Должности (ДА/НЕТ)")>0 then
			if STRSTR(LayoutchangeHistory[1][i], "НЕТ", 1)>0 then tis_position = 0
		endif
		if STRSTR(GlobchangeScheme[1][i], "Подписи (ДА/НЕТ)")>0 then
			if STRSTR(LayoutchangeHistory[1][i], "НЕТ", 1)>0 then tis_autosign = 0
		endif
		if STRSTR(GlobchangeScheme[1][i], "Фамилии (ДА/НЕТ)")>0 then
			if STRSTR(LayoutchangeHistory[1][i], "НЕТ", 1)>0 then tis_fam = 0
		endif
		if STRSTR(GlobchangeScheme[1][i], "Дата (ДА/НЕТ)")>0 then
			if STRSTR(LayoutchangeHistory[1][i], "НЕТ", 1)>0 then tis_date = 0
		endif
	next i

	ROW_FAM_START_LAYUOT = ROW_FAM_END_CHANGE-2
	ROW_FAM_END_LAYUOT = ROW_FAM_START_AGREED-1

	ROW_DATE = ROW_FAM_START_CHANGE-3 !Дата изма
	ROW_IZM_SUR = ROW_FAM_START_CHANGE-4 !Фамилия изменяющего
	ROW_IZM_N_DOC = ROW_FAM_START_CHANGE-5 !Номер документа

	!---------------------- Поиск изм. 0 ------------------------	
	zero_change_inx = 0
	for i = 1 to last_change_inx
		if STRSTR(LayoutchangeHistory[i][ROW_PHASE_PART], "Изм 0") then zero_change_inx = i
	next i
	!------------------------------------------------------------
	if zero_change_inx then
		phase_part = LayoutchangeHistory[zero_change_inx][ROW_PHASE_PART]
		current_phase = "" : current_part = "" : current_type = "" : current_nizm = 0	
		phase_part_tmp = phase_part
		pos = STRSTR (phase_part_tmp, " ")-1
		current_phase = STRSUB(phase_part_tmp,1,pos)
		phase_part_tmp = STRSUB(phase_part_tmp,pos+2,STRLEN(phase_part_tmp))
		pos = STRSTR (phase_part_tmp, " ")-1
		current_part = STRSUB(phase_part_tmp,1,pos)
		phase_part_tmp = STRSUB(phase_part_tmp,pos+2,STRLEN(phase_part_tmp))
		pos = STRSTR (phase_part_tmp, " ")-1
		current_type = STRSUB(phase_part_tmp,1,pos)
		phase_part_tmp = STRSUB(phase_part_tmp,pos+2,STRLEN(phase_part_tmp))
		current_nizm = phase_part_tmp
		current_date = LayoutchangeHistory[zero_change_inx][ROW_DATE]
	endif
	if zero_change_inx>0 then
		!----- Переписываем должности, фамилии, даты в штамп --------
		!ГИПа, РукОтдела и Нормоконтроля берём из нулевого изма (LayoutchangeHistory)
		!При этом отдельно выделяем последнюю позицию - её положение настраивается в штампе
		n_position = 0
		if sort=1 then
			gosub "from_layout"
			gosub "from_change"
			gosub "from_issue"
		endif
		if sort=0 then
			gosub "from_change"
			gosub "from_layout"
			gosub "from_issue"
		endif
		if sort=2 then
			n_position = 0
			flag = 0
			for i = ROW_FAM_START_ISSUE to ROW_FAM_END_ISSUE
				n_position = n_position + 1
				position_info[n_position][1] = GlobIssueScheme[1][i]
				position_info[n_position][2] = ""
				position_info[n_position][3] = ""
			next i
			for i = ROW_FAM_START_ISSUE to ROW_FAM_END_ISSUE
				if STRLEN(LayoutRevHistory[last_rev_inx][i])>1 then
					for j=1 to n_position
						if position_info[j][1] = GlobIssueScheme[1][i] then
							position_info[j][2] = LayoutRevHistory[last_rev_inx][i]
							position_info[j][3] = current_date
						endif
					next j
				endif
			next i
			for i = ROW_FAM_START_CHANGE to ROW_FAM_END_CHANGE
				if STRLEN(LayoutchangeHistory[zero_change_inx][i])>1 then  !Если указана фамилия - записываем
					for j=1 to n_position
						if position_info[j][1] = GlobchangeScheme[1][i] then
							position_info[j][2] = LayoutchangeHistory[zero_change_inx][i]
							position_info[j][3] = current_date
						endif
					next j
				endif
			next i
			for i = ROW_FAM_START_LAYUOT to ROW_FAM_END_LAYUOT
				if STRLEN(LayoutRevHistory[last_rev_inx][i])>1 then
					for j=1 to n_position
						if position_info[j][1] = GlobIssueScheme[1][i] then
							position_info[j][2] = LayoutRevHistory[last_rev_inx][i]
							position_info[j][3] = current_date
						endif
					next j
				endif
			next i
		endif
		!------------------------------------------------------------
		!-------- Заполнение граф согласований -------------------------
		n_position_agreed = 0
		for i = ROW_FAM_START_AGREED to ROW_FAM_END_AGREED
			if STRLEN(LayoutRevHistory[last_rev_inx][i])>1 then !Если указана фамилия - записываем
				n_position_agreed = n_position_agreed + 1
				agreed_info[n_position_agreed][1] = GlobIssueScheme[1][i]
				agreed_info[n_position_agreed][2] = LayoutRevHistory[last_rev_inx][i]
				agreed_info[n_position_agreed][3] = current_date
			endif
		next i
		!------------------------------------------------------------
		!-------- Заполнение граф изменений -------------------------
		start = 8
		for i = 1 to last_change_inx
			not_zero = (STRSTR(LayoutchangeHistory[i][ROW_PHASE_PART], "Изм 0") = 0)
			comp_phase = (STRSTR(LayoutchangeHistory[i][ROW_PHASE_PART], current_phase) > 0)
			comp_part = (STRSTR(LayoutchangeHistory[i][ROW_PHASE_PART], current_part) > 0)
			not_sys = (STRSTR(LayoutchangeHistory[i][ROW_PHASE_PART], "!") = 0)
			if not_zero and comp_phase and comp_part and not_sys then
				phase_part = LayoutchangeHistory[i][ROW_PHASE_PART]
				t_phase = "" : t_part = "" : t_type = "" : t_nizm = ""
				phase_part_tmp = phase_part
				pos = STRSTR (phase_part_tmp, " ")-1
				t_phase = STRSUB(phase_part_tmp,1,pos)
				phase_part_tmp = STRSUB(phase_part_tmp,pos+2,STRLEN(phase_part_tmp))
				pos = STRSTR (phase_part_tmp, " ")-1
				t_part = STRSUB(phase_part_tmp,1,pos)
				phase_part_tmp = STRSUB(phase_part_tmp,pos+2,STRLEN(phase_part_tmp))
				pos = STRSTR (phase_part_tmp, " ")-1
				t_type = STRSUB(phase_part_tmp,1,pos)
				phase_part_tmp = STRSUB(phase_part_tmp,pos+2,STRLEN(phase_part_tmp))
				t_nizm = phase_part_tmp
				n_izm = n_izm + 1
				change_info[n_izm][1] = t_nizm
				change_info[n_izm][2] = t_type
				change_info[n_izm][3] = LayoutchangeHistory[i][ROW_IZM_N_DOC]
				change_info[n_izm][4] = LayoutchangeHistory[i][ROW_IZM_SUR]
				change_info[n_izm][5] = LayoutchangeHistory[i][ROW_DATE]
				if STRSTR(t_type, "Зам") or STRSTR(t_type, "Нов") then 
					change_info[n_izm][6] = "-"
				else
					change_info[n_izm][6] = " "
				endif
				if STRSTR(t_type, "Изм") then change_info[n_izm][2] = "-"
			endif
		next i
	endif
	n_change = vardim1(change_info)
	n_position = vardim1(position_info)
	n_agreed = vardim1(agreed_info)
endif

if data_type="Дата последнего изм." or data_type="Номер последнего изм." or data_type="Номер разрешения на последний изм." then
	n_col = 1
	dim sort_arr[]
	for i=1 to n_izm
		var = change_info[i][n_col]
		if vartype(var)=2 then
			rez = SPLIT(var,"%n",var_num)
			var = var_num
		endif
		if vartype(var)=1 then
			sort_arr[i] = var
		else
			sort_arr[i] = 0
		endif
	next i
	for j=1 to n_izm
		flag_end=1
		for i=1 TO n_izm-j
			flag_swap = 0
			flag_swap = (sort_arr[i]<sort_arr[i+1])
			if flag_swap then
				kk = sort_arr[i]
				sort_arr[i] = sort_arr[i+1]
				sort_arr[i+1] = kk
				flag_end=0
			endif
		next i
		if flag_end then j=n_izm
	next j
	dim sort_arr_t[][]
	for j=1 to n_izm
		izm = sort_arr[j]
		for i=1 to n_izm
			var = change_info[i][n_col]
			if vartype(var)=2 then
				rez = SPLIT(var,"%n",var_num)
				var = var_num
			endif
			if vartype(var)=1 then
				if var = izm then
					for k=1 to vardim2(change_info)
						sort_arr_t[j][k] = change_info[i][k]
					next k
					i=n_izm
				endif
			endif
		next i
	next j
	change_info = sort_arr_t
endif
goto 20

"from_layout":
	for i = ROW_FAM_START_LAYUOT to ROW_FAM_END_LAYUOT
		if STRLEN(LayoutRevHistory[last_rev_inx][i])>1 then
			flag = 1
			for j = 1 to n_position
				if GlobIssueScheme[1][i]= position_info[j][1] then flag = 0
			next j
			if flag then
				n_position = n_position + 1
				position_info[n_position][1] = GlobIssueScheme[1][i]
				position_info[n_position][2] = LayoutRevHistory[last_rev_inx][i]
				position_info[n_position][3] = current_date
			endif
		endif
	next i
return

"from_issue":
	for i = ROW_FAM_START_ISSUE to ROW_FAM_END_ISSUE
		if STRLEN(LayoutRevHistory[last_rev_inx][i])>1 then
			flag = 1
			for j = 1 to n_position
				if GlobIssueScheme[1][i]= position_info[j][1] then flag = 0
			next j
			if flag then
				n_position = n_position + 1
				position_info[n_position][1] = GlobIssueScheme[1][i]
				position_info[n_position][2] = LayoutRevHistory[last_rev_inx][i]
				position_info[n_position][3] = current_date
			endif
		endif
	next i
return

"from_change":
	for i = ROW_FAM_START_CHANGE to ROW_FAM_END_CHANGE
		if STRLEN(LayoutchangeHistory[zero_change_inx][i])>1 then
			flag = 1
			for j = 1 to n_position
				if GlobchangeScheme[1][i]= position_info[j][1] then flag = 0
			next j
			if flag then
				n_position = n_position + 1
				position_info[n_position][1] = GlobchangeScheme[1][i]
				position_info[n_position][2] = LayoutchangeHistory[zero_change_inx][i]
				position_info[n_position][3] = current_date
			endif
		endif
	next i
return
20:
]]>
</Script_1D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_PR SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_PR>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_UI SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_UI>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_VL SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
values{2} "sort" isorttype, tsorttype
values{2} "r255type" irtype, trtype
values{2} "stype" istype, tstype

values "data_type" "Подпись", "Фамилия", "Раздел", "Стадия", "Дата изм. 0", "Дата последнего изм.", "Номер последнего изм.", "Номер разрешения на последний изм."

dim tposition_val[] : npos = 0
npos = npos + 1 : tposition_val[npos]= "ГИП"
npos = npos + 1 : tposition_val[npos]= "Н. контр."
npos = npos + 1 : tposition_val[npos]= "Разраб."
npos = npos + 1 : tposition_val[npos]= "Рук. отдела"
npos = npos + 1 : tposition_val[npos]= "Пров."
npos = npos + 1 : tposition_val[npos]= "Разраб."

for k=1 to vardim1(custom_position)
	if strlen(custom_position[k])>2 then
		npos = npos + 1
		tposition_val[npos] = custom_position[k]
	endif
next k

if data_type="Подпись" or data_type="Фамилия" then
	values "data_subtype" tposition_val, CUSTOM
else
	LOCK "data_subtype"
endif
]]>
</Script_VL>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_FWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_FWM>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_BWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_BWM>

<ParamSection SectVersion="26" SectionFlags="0" SubIdent="0">
	<ParamSectHeader>
		<AutoHotspots>false</AutoHotspots>
		<StatBits>
			<STBit_FixSize/>
		</StatBits>
		<WDLeftFrame>0</WDLeftFrame>
		<WDRightFrame>0</WDRightFrame>
		<WDTopFrame>0</WDTopFrame>
		<WDBotFrame>0</WDBotFrame>
		<LayFlags>65535</LayFlags>
		<WDMirrorThickness>0</WDMirrorThickness>
		<WDWallInset>0</WDWallInset>
	</ParamSectHeader>
	<Parameters>
		<Length Name="A">
			<Description><![CDATA["Размер 1"]]></Description>
			<Fix/>
			<Value>1</Value>
		</Length>
		<Length Name="B">
			<Description><![CDATA["Размер 2"]]></Description>
			<Fix/>
			<Value>1</Value>
		</Length>
		<String Name="data_type">
			<Description><![CDATA["Тип данных"]]></Description>
			<Value><![CDATA["Подпись"]]></Value>
		</String>
		<String Name="data_subtype">
			<Description><![CDATA["Должность"]]></Description>
			<Value><![CDATA["ГИП"]]></Value>
		</String>
		<String Name="custom_position">
			<Description><![CDATA["Дополнительные должности"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<ArrayValues FirstDimension="20" SecondDimension="0">
				<AVal Row="1"><![CDATA[""]]></AVal>
				<AVal Row="2"><![CDATA[""]]></AVal>
				<AVal Row="3"><![CDATA[""]]></AVal>
				<AVal Row="4"><![CDATA[""]]></AVal>
				<AVal Row="5"><![CDATA[""]]></AVal>
				<AVal Row="6"><![CDATA[""]]></AVal>
				<AVal Row="7"><![CDATA[""]]></AVal>
				<AVal Row="8"><![CDATA[""]]></AVal>
				<AVal Row="9"><![CDATA[""]]></AVal>
				<AVal Row="10"><![CDATA[""]]></AVal>
				<AVal Row="11"><![CDATA[""]]></AVal>
				<AVal Row="12"><![CDATA[""]]></AVal>
				<AVal Row="13"><![CDATA[""]]></AVal>
				<AVal Row="14"><![CDATA[""]]></AVal>
				<AVal Row="15"><![CDATA[""]]></AVal>
				<AVal Row="16"><![CDATA[""]]></AVal>
				<AVal Row="17"><![CDATA[""]]></AVal>
				<AVal Row="18"><![CDATA[""]]></AVal>
				<AVal Row="19"><![CDATA[""]]></AVal>
				<AVal Row="20"><![CDATA[""]]></AVal>
			</ArrayValues>
		</String>

		<!-- _sp0_A: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="_sp0_A">
			<Description><![CDATA["Отображение"]]></Description>
		</Title>
		<String Name="fontType">
			<Description><![CDATA["Шрифт"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["ISOCPEUR Кириллический"]]></Value>
		</String>
		<Integer Name="r255type">
			<Description><![CDATA["Начертание"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Integer>
		<Integer Name="stype">
			<Description><![CDATA["Выравнивание"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>7</Value>
		</Integer>
		<RealNum Name="r255">
			<Description><![CDATA["Размер шрифта"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>3.5</Value>
		</RealNum>
		<PenColor Name="pen_text">
			<Description><![CDATA["Перо текста"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>1</Value>
		</PenColor>
		<PenColor Name="pen_podp">
			<Description><![CDATA["Перо подписи"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>6</Value>
		</PenColor>
		<PenColor Name="pen_error">
			<Description><![CDATA["Перо ошибки"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>20</Value>
		</PenColor>
		<Integer Name="sort">
			<Description><![CDATA["Порядок чтения данных"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>2</Value>
		</Integer>
	</Parameters>
</ParamSection>

<Copyright SectVersion="1" SectionFlags="0" SubIdent="0">
	<Author></Author>
	<License>
		<Type>CC0</Type>
		<Version>1.0</Version>
	</License>
</Copyright>

<Keywords SectVersion="1" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Keywords>

<Comment SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Comment>

</Symbol>
