<?xml version="1.0" encoding="UTF-8"?>
<Symbol IsArchivable="false" IsPlaceable="true" MainGUID="9DAF3E4C-CBD6-4AB3-96B1-96156EC2C4CA" MigrationValue="Normal" Owner="1196638531" Signature="1196644685" Version="38">
<Ancestry SectVersion="1" SectionFlags="0" SubIdent="0" Template="false">
	<MainGUID>F938E33A-329D-4A36-BE3E-85E126820996</MainGUID>
	<MainGUID>103E8D2C-8230-42E1-9597-46F84CCE28C0</MainGUID>
	<MainGUID>84E50B69-6FB6-4909-AC3D-E01EECBE3328</MainGUID>
	<MainGUID>BA0D5AC6-9C7F-4276-9719-E4DFD138DF96</MainGUID>
</Ancestry>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_3D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
IF gs_detlevel_3D_m = DLEV3D_OFF THEN end
IF gs_detlevel_3D_m = DLEV3D_SIMPLE THEN gs_resol = 4

IF (GLOB_CONTEXT > 20 and GLOB_CONTEXT < 40) THEN gs_detlevel_3D_m = DLEV3D_SIMPLE	!!Simple

IF gs_shadow=0 THEN SHADOW OFF

PEN gs_cont_pen
MATERIAL mat_ladder

! ------------------------------------------------------------------------------
! HOTSPOTS
! ------------------------------------------------------------------------------
unID=1
HOTSPOT 0,0,0,unID    		:unID=unID+1


! ------------------------------------------------------------------------------
! EDITABLE HOTSPOTS
! ------------------------------------------------------------------------------

HOTSPOT 0,0,0,unID,b_temp,1+128,b    :unID=unID+1
HOTSPOT 0,-0.1,0,unID,b_temp,3,b     :unID=unID+1
HOTSPOT 0,b_temp,0,unID,b_temp,2,b   :unID=unID+1

HOTSPOT 0,0,0,unID,b_temp,1+128,b    :unID=unID+1
HOTSPOT 0,0.1,0,unID,b_temp,3,b     :unID=unID+1
HOTSPOT 0,-b_temp,0,unID,b_temp,2,b :unID=unID+1

HOTSPOT 0,0,zzyzx,unID,a,1+128      :unID=unID+1
HOTSPOT -0.1,0,zzyzx,unID,a,3       :unID=unID+1
HOTSPOT a,0,zzyzx,unID,a,2          :unID=unID+1

HOTSPOT a-st_w/2,0,0,unID,zzyzx,1+128      :unID=unID+1
HOTSPOT a-st_w/2,0,-0.1,unID,zzyzx,3       :unID=unID+1
HOTSPOT a-st_w/2,0,zzyzx,unID,zzyzx,2          :unID=unID+1

HOTSPOT a,0,zzyzx,unID,b_temp,1+128,b    :unID=unID+1
HOTSPOT a,-0.1,zzyzx,unID,b_temp,3,b     :unID=unID+1
HOTSPOT a,b_temp,zzyzx,unID,b_temp,2,b   :unID=unID+1

HOTSPOT a,0,zzyzx,unID,b_temp,1+128,b    :unID=unID+1
HOTSPOT a,0.1,zzyzx,unID,b_temp,3,b     :unID=unID+1
HOTSPOT a,-b_temp,zzyzx,unID,b_temp,2,b   :unID=unID+1

HOTSPOT 2",b/2+hr_d/2,0,unID,hr_h,1+128    :unID=unID+1
HOTSPOT 2",b/2+hr_d/2,-0.1,unID,hr_h,3     :unID=unID+1
HOTSPOT 2",b/2+hr_d/2,hr_h,unID,hr_h,2   :unID=unID+1

HOTSPOT 2",-b/2-hr_d/2,0,unID,hr_h,1+128    :unID=unID+1
HOTSPOT 2",-b/2-hr_d/2,-0.1,unID,hr_h,3     :unID=unID+1
HOTSPOT 2",-b/2-hr_d/2,hr_h,unID,hr_h,2   :unID=unID+1

! ------------------------------------------------------------------------------
! SIDE RAILs
! ------------------------------------------------------------------------------
ADDy -b/2
ROTz 90
FOR i=1 to 2

	! --- ACCESSORIES ---
	IF gs_detlevel_3D_m = DLEV3D_DETAILED THEN
	 MULx -1
	 ROTx 90
	 ADDz 1"
	 PRISM_ 6,4",
		0,0,15,
		f_w,0,15,
		f_w,f_t,15,
		f_t,f_t,15,
		f_t,f_d,15,
		0,f_d,15
	 DEL 3
	ENDIF

	 CUTPLANE 180
	ROTx 90
	 CUTPLANE 180
	DEL 1

	ADD 0,-a+st_w+1",zzyzx
	ROTx 90-alfa/2
	 CUTPLANE
	DEL 2

	ADDy -6"
	ROTx 90-alfa
	ADDz -6"*sin(90-alfa)
	 PRISM_ 8,zzyzx/sin(alfa),
		0,0,15,
		sr_d,0,15,
		sr_d,sr_t,15,
		sr_t,sr_t,15,
		sr_t,sr_w-sr_t,15,
		sr_d,sr_w-sr_t,15,
		sr_d,sr_w,15,
		0,sr_w,15
	DEL 3
	 CUTEND
	 CUTEND
	 CUTEND

	ADD 0,-a+st_w+1",zzyzx
	ROTx 90-alfa/2
	 CUTPLANE 180
	DEL 2

	ADD 0,-a+st_w+1",zzyzx-sr_w
	ROTx 90
	 PRISM_ 8,st_w+1",
		0,0,15,
		sr_d,0,15,
		sr_d,sr_t,15,
		sr_t,sr_t,15,
		sr_t,sr_w-sr_t,15,
		sr_d,sr_w-sr_t,15,
		sr_d,sr_w,15,
		0,sr_w,15
	DEL 2
	 CUTEND
	ADDx b
	MULx -1
NEXT i
DEL 4
DEL 2

! ------------------------------------------------------------------------------
! STEPS
! ------------------------------------------------------------------------------

ROTz -90
ADDy a-st_w-(n_st-1)*dX
FOR s=1 TO n_st
  ADD 0,dX*(s-1),dY*s-st_h
  ! --- SIMPLE STEPS ---
  IF gs_detlevel_3D_m = DLEV3D_SIMPLE THEN
	  PRISM_ 5,st_h,
		-b/2+sr_t,0,15,
		-b/2+sr_t,st_w,15,
		b/2-sr_t,st_w,15,
		b/2-sr_t,0,15,
		-b/2+sr_t,0,-1
  ENDIF

  ! --- DETAILED STEPS ---
  IF gs_detlevel_3D_m = DLEV3D_DETAILED THEN
	  PRISM_ 10,st_h,
		-b/2+sr_t,0,15,
		-b/2+sr_t,st_w,15,
		b/2-sr_t,st_w,15,
		b/2-sr_t,0,15,
		-b/2+sr_t,0,-1,

		-b/2+sr_t+st_t,st_t,15,
		-b/2+sr_t+st_t,st_w-st_t,15,
		b/2-sr_t-st_t,st_w-st_t,15,
		b/2-sr_t-st_t,st_t,15,
		-b/2+sr_t+st_t,st_t,-1

	  ROTx 90
	  FOR p=1 to npY-1
		ADDz -st_t-p*dpY
		 POLY 4,
			-b/2+sr_t+st_t,0,
			-b/2+sr_t+st_t,st_h,
			b/2-sr_t-st_t,st_h,
			b/2-sr_t-st_t,0
		DEL 1
	  NEXT p
	  DEL 1

	  ROTz 90
	  ROTx 90
	  ADDz -b/2+sr_t
	  FOR p=1 to npX-1
		ADDz st_t+p*dpX
		 POLY 4,
			st_t,0,
			st_t,st_h,
			st_w-st_t,st_h,
			st_w-st_t,0
		DEL 1
	  NEXT p
	  DEL 3
  ENDIF

  DEL 1
NEXT s
DEL 2

! ------------------------------------------------------------------------------
! HANDRAIL
! ------------------------------------------------------------------------------

RESOL gs_resol

! --- Accessories (only in detailed mode) ---
IF gs_detlevel_3D_m = DLEV3D_DETAILED THEN
	IF iHandrailPos = HANDRAILPOS_LEFT or iHandrailPos = HANDRAILPOS_BOTH THEN
		ADD 2",b/2,2"
		PRISM_ 12,2",
			-2",0,15,
			-2",f_t,15,
			-hr_d/2-f_t,f_t,15,
			-hr_d/2-f_t,hr_d/2,79,
			hr_d/2+f_t,hr_d/2,1079,
			hr_d/2+f_t,f_t,15,
			2",f_t,15,
			2",0,15,
			hr_d/2,0,15,
			hr_d/2,hr_d/2,79,
			-hr_d/2,hr_d/2,1079,
			-hr_d/2,0,15
		DEL 1
	ENDIF

	IF iHandrailPos = HANDRAILPOS_RIGHT or iHandrailPos = HANDRAILPOS_BOTH THEN
		ADD 2",-b/2,2"
		MULy -1
		PRISM_ 12,2",
			-2",0,15,
			-2",f_t,15,
			-hr_d/2-f_t,f_t,15,
			-hr_d/2-f_t,hr_d/2,79,
			hr_d/2+f_t,hr_d/2,1079,
			hr_d/2+f_t,f_t,15,
			2",f_t,15,
			2",0,15,
			hr_d/2,0,15,
			hr_d/2,hr_d/2,79,
			-hr_d/2,hr_d/2,1079,
			-hr_d/2,0,15
		DEL 2
	ENDIF
ENDIF

! --- To Wall ---
IF iTopConnType = TOPCONNTYPE_WALL THEN
   IF iHandrailPos = HANDRAILPOS_LEFT or iHandrailPos = HANDRAILPOS_BOTH THEN
	ADD 2",b/2+hr_d/2,0
	TUBE 2,8,1+2+16+32,
		0,0,901,
		hr_d/2,360,4001,

		0,0,0,0,
		0,0,2",0,
		0,0,min(hr_h, zzyzx - 2"),0,
		(zzyzx-hr_h-2")/tan(alfa),0,zzyzx-2",0,
		(zzyzx-hr_h-2")/tan(alfa),max(-b/2+door_w/2+6",0),zzyzx-2",0,
		a-sr_w+2",max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
		a-2",max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
		a+0.1,max(-b/2+door_w/2+6",0),zzyzx+hr_h,0
	DEL 1
   ENDIF

   IF iHandrailPos = HANDRAILPOS_RIGHT or iHandrailPos = HANDRAILPOS_BOTH THEN
	ADD 2",-b/2-hr_d/2,0
	TUBE 2,8,1+2+16+32,
		0,0,901,
		hr_d/2,360,4001,

		0,0,0,0,
		0,0,2",0,
		0,0,min(hr_h, zzyzx - 2"),0,
		(zzyzx-hr_h-2")/tan(alfa),0,zzyzx-2",0,
		(zzyzx-hr_h-2")/tan(alfa),-max(-b/2+door_w/2+6",0),zzyzx-2",0,
		a-sr_w+2",-max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
		a-2",-max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
		a+0.1,-max(-b/2+door_w/2+6",0),zzyzx+hr_h,0
	DEL 1
   ENDIF
ENDIF


! --- Back to stringer ---
IF iTopConnType = TOPCONNTYPE_STRINGER THEN
   IF iHandrailPos = HANDRAILPOS_LEFT or iHandrailPos = HANDRAILPOS_BOTH THEN
	ADD 2",b/2+hr_d/2,0
	muly -1
	TUBE 2,9,1+2+16+32,
		0,0,901,
		hr_d/2,360,4001,

		0,0,0,0,
		0,0,2",0,
		0,0,min(hr_h, zzyzx - 2"),0,
		(zzyzx-hr_h-2")/tan(alfa),0,zzyzx-2",0,
		(zzyzx-hr_h-2")/tan(alfa),-max(-b/2+door_w/2+6",0),zzyzx-2",0,
		a-sr_w+2",-max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
		a-4",-max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
!		a-4",-max(-b/2+door_w/2+6",0),zzyzx-2",0,
		a-4",hr_d/2,zzyzx-2",0,
		a-4",0.1,zzyzx-2",0
	del 1

	DEL 1
   ENDIF

   IF iHandrailPos = HANDRAILPOS_RIGHT or iHandrailPos = HANDRAILPOS_BOTH THEN
	ADD 2",-b/2-hr_d/2,0

	TUBE 2,9,1+2+16+32,
		0,0,901,
		hr_d/2,360,4001,

		0,0,0,0,
		0,0,2",0,
		0,0,min(hr_h, zzyzx - 2"),0,
		(zzyzx-hr_h-2")/tan(alfa),0,zzyzx-2",0,
		(zzyzx-hr_h-2")/tan(alfa),-max(-b/2+door_w/2+6",0),zzyzx-2",0,
		a-sr_w+2",-max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
		a-4",-max(-b/2+door_w/2+6",0),zzyzx+hr_h,0,
!		a-4",-max(-b/2+door_w/2+6",0),zzyzx-2",0,
		a-4",hr_d/2,zzyzx-2",0,
		a-4",0.1,zzyzx-2",0

	DEL 1
   ENDIF
ENDIF

! --- Accessories ---

IF gs_detlevel_3D_m = DLEV3D_DETAILED THEN


ENDIF
]]>
</Script_3D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_2D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[

cutplaneHeight	= GLOB_CSTORY_ELEV - GLOB_HSTORY_ELEV + GLOB_CUTPLANES_INFO[1]


PEN gs_cont_pen
FILL gs_fill_type

! ------------------------------------------------------------------------------
! HOTSPOTS
! ------------------------------------------------------------------------------

unID=1
HOTSPOT2 0,0,unID   : unID=unID+1

! ------------------------------------------------------------------------------
! STORY SENSITIVE
! ------------------------------------------------------------------------------
bCutted		= 0
lineType	= gs_LineType
cuttingPos	= 0
holeWidth	= 0.05
startOffset = a-st_w-(n_st-1)*dX
if bStorySensitive then
	if cutplaneHeight > ac_toplevel then
		!!No cutting all draw symbol line
	else
		if cutplaneHeight < ac_bottomlevel then		!All draw hidden, No cutting
			lineType = gs_LineTypeStUp
		else
			! Cutting
			cutHeigth = cutplaneHeight-ac_bottomlevel

			nCutPosY = int(cutHeigth / dY)
			if nCutPosY < 1 then nCutPosY = 1
			if nCutPosY > n_st-1 then nCutPosY = n_st

			cutPosX = nCutPosY * dX + 0.5 * dX  + EPS
			cutAlpha =  atn((b/2-sr_d) / (dX*0.5))
			bCutted	= 1
		endif
	endif
endif

! ------------------------------------------------------------------------------
! Scale Sensitive
! ------------------------------------------------------------------------------
call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16,
		returned_parameters	det2D


! ------------------------------------------------------------------------------
! EDITABLE HOTSPOTS
! ------------------------------------------------------------------------------

HOTSPOT2 0,0,unID,a,1+256   : unID=unID+1
HOTSPOT2 -0.1,0,unID,a,3    : unID=unID+1
HOTSPOT2 a,0,unID,a,2   	: unID=unID+1

HOTSPOT2 0,0,unID,b_temp,1+128,b    : unID=unID+1
HOTSPOT2 0,-0.1,unID,b_temp,3,b     : unID=unID+1
HOTSPOT2 0,b_temp,unID,b_temp,2,b   : unID=unID+1

HOTSPOT2 0,0,unID,b_temp,1+128,b    : unID=unID+1
HOTSPOT2 0,0.1,unID,b_temp,3,b    	: unID=unID+1
HOTSPOT2 0,-b_temp,unID,b_temp,2,b  : unID=unID+1

HOTSPOT2 a,0,unID,b_temp,1+128,b    : unID=unID+1
HOTSPOT2 a,-0.1,unID,b_temp,3,b     : unID=unID+1
HOTSPOT2 a,b_temp,unID,b_temp,2,b   : unID=unID+1

HOTSPOT2 a,0,unID,b_temp,1+128,b    : unID=unID+1
HOTSPOT2 a,0.1,unID,b_temp,3,b    	: unID=unID+1
HOTSPOT2 a,-b_temp,unID,b_temp,2,b  : unID=unID+1



! ------------------------------------------------------------------------------
! 2D Content
! ------------------------------------------------------------------------------
ROT2 -90
ch = INITADDONSCOPE ("PolyOperations", "", "")

nContainer = 1
prevSourceContainer	= "mySourceContainer"
lastDestContainer	= "myDestinationContainer"

PREPAREFUNCTION ch, "CreateContainer", prevSourceContainer, ""
PREPAREFUNCTION ch, "CreateContainer", lastDestContainer, ""

PREPAREFUNCTION ch, "SetSourceContainer",		prevSourceContainer, ""
PREPAREFUNCTION ch, "SetDestinationContainer",	lastDestContainer, ""

gosub "putBasePolygons"

if not(bCutted) then
	if lastDestContainer <> "" then
		PREPAREFUNCTION ch, "DeleteContainer",		lastDestContainer, ""
	endif
	lastDestContainer = prevSourceContainer
	prevSourceContainer = "mySourceContainer" + STR(nContainer,1,0)
	PREPAREFUNCTION ch, "CreateContainer",			prevSourceContainer, ""
	PREPAREFUNCTION ch, "SetSourceContainer",		prevSourceContainer, ""
	PREPAREFUNCTION ch, "SetDestinationContainer",	lastDestContainer, ""

	gosub "drawDestinationPolygons"
else
	dim sourceIDArray[]
	numOrigPoly = CALLFUNCTION (ch, "GetSourcePolygons", "", sourceIDArray)

	!----Below---------
	lineType = gs_LineType
	gosub "cutpolyAbove"

	dim resPolyIDArray[]
	for i = 1 to numOrigPoly
		nPgon = CALLFUNCTION (ch, sourceIDArray[i] + " - cutAbove", "", resPolyIDArray)
		gosub "drawDestinationPolygons"
	next i

	!----Above---------
	lineType = gs_LineTypeStUp
	gosub "cutpolyBelow"

	dim resPolyIDArray[]
	for i = 1 to numOrigPoly
		nPgon = CALLFUNCTION (ch, sourceIDArray[i] + " - cutBelow", "", resPolyIDArray)
		gosub "drawDestinationPolygons"
	next i
endif

CLOSEADDONSCOPE ch

DEL 1


! ------------------------------------------------------------------------------
! Walking Line
! ------------------------------------------------------------------------------
if walkl then
	hotspot2 A-st_w-(n_st-1)*dX,0,unID   : unID=unID+1

	if det2D = DETLEVEL2D_50 then
		line_type gs_LineType
		circle2 A-st_w-(n_st-1)*dX,0, 0.05
	endif

	line_type gs_wLineType1
	if not(bCutted) then
		line2 startOffset,0, A,0
	else
		line2 startOffset,0, startOffset + cutPosX -holeWidth, 0
		line_type gs_LineTypeStUp
		line2 startOffset + cutPosX,0, A, 0
	endif
	line2 A,0, A-dX, dX
	line2 A,0, A-dX,-dX
endif


! ------------------------------------------------------------------------------
END
! ------------------------------------------------------------------------------


"putBasePolygons":
	!------- SIDE RAILs 1 ----------------
	DIM vertArr[][]
	DIM contArr[]
	DIM inhEdgeInfos[]

	nVertices	= 4
	contArr[1]	= 5
	nContour	= 1
	defaultInhEdgeInfo	= 1

	vertArr[1][1] = -b/2
	vertArr[1][2] = 0
	vertArr[1][3] = 0.0
	vertArr[2][1] = -b/2+sr_d
	vertArr[2][2] = 0
	vertArr[2][3] = 0.0
	vertArr[3][1] = -b/2+sr_d
	vertArr[3][3] = 0.0
	vertArr[4][1] = -b/2
	vertArr[4][3] = 0.0
	if det2D = DETLEVEL2D_50 then
		vertArr[3][2] = A-st_w-2"
		vertArr[4][2] = A-st_w-2"
	else
		vertArr[3][2] = A
		vertArr[4][2] = A
	endif

	inhEdgeInfos[1] = 1
	inhEdgeInfos[2] = 1
	inhEdgeInfos[3] = 1
	inhEdgeInfos[4] = 1

	PREPAREFUNCTION ch, "Store", "sideRail_11", nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos

	if (bCutted & det2D = DETLEVEL2D_50) | (not(bCutted) & det2D <> DETLEVEL2D_200) then
		vertArr[1][1] = -b/2
		vertArr[1][2] = a-st_w-2"
		vertArr[1][3] = 0.0
		vertArr[2][1] = -b/2+sr_d
		vertArr[2][2] = a-st_w-2"
		vertArr[2][3] = 0.0
		vertArr[3][1] = -b/2+sr_d
		vertArr[3][2] = a
		vertArr[3][3] = 0.0
		vertArr[4][1] = -b/2
		vertArr[4][2] = a
		vertArr[4][3] = 0.0

		inhEdgeInfos[1] = 0
		inhEdgeInfos[2] = 1
		inhEdgeInfos[3] = 1
		inhEdgeInfos[4] = 1

		PREPAREFUNCTION ch, "Store", "sideRail_12", nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos
	endif


	!------- SIDE RAILs 2 ----------------
	DIM vertArr[][]
	DIM contArr[]
	DIM inhEdgeInfos[]

	nVertices	= 4
	contArr[1]	= 5
	nContour	= 1
	defaultInhEdgeInfo	= 1

	vertArr[1][1] = b/2
	vertArr[1][2] = 0
	vertArr[1][3] = 0.0
	vertArr[2][1] = b/2-sr_d
	vertArr[2][2] = 0
	vertArr[2][3] = 0.0
	vertArr[3][1] = b/2-sr_d
	vertArr[3][3] = 0.0
	vertArr[4][1] = b/2
	vertArr[4][3] = 0.0
	if det2D = DETLEVEL2D_50 then
		vertArr[3][2] = A-st_w-2"
		vertArr[4][2] = A-st_w-2"
	else
		vertArr[3][2] = A
		vertArr[4][2] = A
	endif

	inhEdgeInfos[1] = 1
	inhEdgeInfos[2] = 1
	inhEdgeInfos[3] = 1
	inhEdgeInfos[4] = 1

	PREPAREFUNCTION ch, "Store", "sideRail_21", nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos

	if (bCutted & det2D = DETLEVEL2D_50) | (not(bCutted) & det2D <> DETLEVEL2D_200) then
		vertArr[1][1] = b/2
		vertArr[1][2] = a-st_w-2"
		vertArr[1][3] = 0.0
		vertArr[2][1] = b/2-sr_d
		vertArr[2][2] = a-st_w-2"
		vertArr[2][3] = 0.0
		vertArr[3][1] = b/2-sr_d
		vertArr[3][2] = a
		vertArr[3][3] = 0.0
		vertArr[4][1] = b/2
		vertArr[4][2] = a
		vertArr[4][3] = 0.0

		inhEdgeInfos[1] = 0
		inhEdgeInfos[2] = 1
		inhEdgeInfos[3] = 1
		inhEdgeInfos[4] = 1

		PREPAREFUNCTION ch, "Store", "sideRail_22", nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos
	endif

	!------- STEPS ----------------
	DIM vertArr[][]
	DIM contArr[]
	DIM inhEdgeInfos[]

	nVertices	= 4
	contArr[1]	= 5
	nContour	= 1
	defaultInhEdgeInfo	= 1

	inhEdgeInfos[1] = 0
	inhEdgeInfos[2] = 0
	inhEdgeInfos[3] = 0
	inhEdgeInfos[4] = 1

	if det2D = DETLEVEL2D_200 then
		vertArr[1][1] = -b/2+sr_d
		vertArr[1][2] = startOffset
		vertArr[1][3] = 0.0
		vertArr[2][1] = -b/2+sr_d
		vertArr[2][2] = A
		vertArr[2][3] = 0.0
		vertArr[3][1] = b/2-sr_d
		vertArr[3][2] = A
		vertArr[3][3] = 0.0
		vertArr[4][1] = b/2-sr_d
		vertArr[4][2] = startOffset
		vertArr[4][3] = 0.0
		inhEdgeInfos[2] = 1

		PREPAREFUNCTION ch, "Store", "currentStep", nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos
	else
		for s=1 to n_st
			offset = startOffset + dX*(s-1)

			if s = n_st then
				stepWidth = st_w
				inhEdgeInfos[2] = 1
			else
				if (st_w) > dX then
					stepWidth = dX
				else
					stepWidth = st_w
					inhEdgeInfos[2] = 1
				endif
			endif

			vertArr[1][1] = -b/2+sr_d
			vertArr[1][2] = offset
			vertArr[1][3] = 0.0
			vertArr[2][1] = -b/2+sr_d
			vertArr[2][2] = stepWidth+offset
			vertArr[2][3] = 0.0
			vertArr[3][1] = b/2-sr_d
			vertArr[3][2] = stepWidth+offset
			vertArr[3][3] = 0.0
			vertArr[4][1] = b/2-sr_d
			vertArr[4][2] = offset
			vertArr[4][3] = 0.0

			currentStep = "step" + STR(s,1,0)
			PREPAREFUNCTION ch, "Store", currentStep, nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos
		next s
	endif


	!------- Handrails ----------------
	if det2D = DETLEVEL2D_50 then
		if iHandrailPos = HANDRAILPOS_LEFT | iHandrailPos = HANDRAILPOS_BOTH then
			DIM vertArr[][]
			DIM contArr[]
			DIM inhEdgeInfos[]

			nVertices	= 4
			contArr[1]	= 5
			nContour	= 1
			defaultInhEdgeInfo	= 1

			vertArr[1][1] = -b/2
			vertArr[1][2] = 2"
			vertArr[1][3] = 0.0
			vertArr[2][1] = -b/2-hr_d
			vertArr[2][2] = 2"
			vertArr[2][3] = 0.0
			vertArr[3][1] = -b/2-hr_d
			vertArr[3][2] = a
			vertArr[3][3] = 0.0
			vertArr[4][1] = -b/2
			vertArr[4][2] = a
			vertArr[4][3] = 0.0

			inhEdgeInfos[1] = 1
			inhEdgeInfos[2] = 1
			inhEdgeInfos[3] = 1
			inhEdgeInfos[4] = 0

			PREPAREFUNCTION ch, "Store", "handRail_1", nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos
		endif

		if iHandrailPos = HANDRAILPOS_RIGHT | iHandrailPos = HANDRAILPOS_BOTH then
			DIM vertArr[][]
			DIM contArr[]
			DIM inhEdgeInfos[]

			nVertices	= 4
			contArr[1]	= 5
			nContour	= 1
			defaultInhEdgeInfo	= 1

			vertArr[1][1] = b/2
			vertArr[1][2] = 2"
			vertArr[1][3] = 0.0
			vertArr[2][1] = b/2+hr_d
			vertArr[2][2] = 2"
			vertArr[2][3] = 0.0
			vertArr[3][1] = b/2+hr_d
			vertArr[3][2] = a
			vertArr[3][3] = 0.0
			vertArr[4][1] = b/2
			vertArr[4][2] = a
			vertArr[4][3] = 0.0

			inhEdgeInfos[1] = 1
			inhEdgeInfos[2] = 1
			inhEdgeInfos[3] = 1
			inhEdgeInfos[4] = 0

			PREPAREFUNCTION ch, "Store", "handRail_2", nVertices, nContour, vertArr, contArr, defaultInhEdgeInfo, inhEdgeInfos
		endif
	endif
return


"cutpolyAbove":
	DIM vertArr[][]
	DIM contArr[]

	cutWidth = b/2 + 1

	nVertices	= 4
	contArr[1]	= 5
	nContour	= 1

	vertArr[1][1] = -cutWidth
	vertArr[1][2] = startOffset + cutPosX + cutWidth/tan(cutAlpha) - holeWidth
	vertArr[1][3] = 0.0
	vertArr[2][1] = cutWidth
	vertArr[2][2] = startOffset + cutPosX - cutWidth/tan(cutAlpha) - holeWidth
	vertArr[2][3] = 0.0
	vertArr[3][1] = cutWidth
	vertArr[3][2] = a+1
	vertArr[3][3] = 0.0
	vertArr[4][1] = -cutWidth
	vertArr[4][2] = a+1
	vertArr[4][3] = 0.0

	PREPAREFUNCTION ch, "Store", "cutAbove", nVertices, nContour, vertArr, contArr
return

"cutpolyBelow":
	DIM vertArr[][]
	DIM contArr[]

	cutWidth = b/2 + 1

	nVertices	= 4
	contArr[1]	= 5
	nContour	= 1

	vertArr[1][1] = -cutWidth
	vertArr[1][2] = startOffset + cutPosX + cutWidth/tan(cutAlpha)
	vertArr[1][3] = 0.0
	vertArr[2][1] = cutWidth
	vertArr[2][2] = startOffset + cutPosX - cutWidth/tan(cutAlpha)
	vertArr[2][3] = 0.0
	vertArr[3][1] = cutWidth
	vertArr[3][2] = -1
	vertArr[3][3] = 0.0
	vertArr[4][1] = -cutWidth
	vertArr[4][2] = -1
	vertArr[4][3] = 0.0

	PREPAREFUNCTION ch, "Store", "cutBelow", nVertices, nContour, vertArr, contArr
return


"drawDestinationPolygons":
	line_type lineType
	dim resPolyIDArr[]
	numPoly = CALLFUNCTION(ch, "GetDestinationPolygons", "", resPolyIDArr)
	for cP = 1 to numPoly
		polygonID = resPolyIDArr[cP]
		mask = 1
		gosub "getGDLpolygonFromAPI"
		poly2_b NSP/3,1+2+4+64,gs_fill_pen,gs_back_pen,
			get(NSP)

		PREPAREFUNCTION ch, "Dispose", polygonID, lastDestContainer
	next cP
return


! ==============================================================================
! GET GDL Polygon
! ------------------------------------------------------------------------------
! Input variables:
!	ch:				Channel ID
!	polygonID:		Polygon ID
!
! Returned variables:
!	GDL stack:			GDL polygon
! ==============================================================================
"getGDLpolygonFromAPI":
	DIM resVertices[]
	numVertices = CALLFUNCTION (ch, "GetVertices", polygonID, resVertices)
	numVertices = numVertices / 3

	DIM contArr[]
	numContours = CALLFUNCTION (ch, "GetContourEnds", polygonID, contArr)

	DIM inhEdgeInfos[]
	numEdges = CALLFUNCTION (ch, "GetInhEdgeInfos", polygonID, inhEdgeInfos)

	FOR contIndex = 1 TO numContours
		IF contIndex = 1 THEN
			begIdx = 0
		ELSE
			begIdx = endIdx +1
		ENDIF
		IF contIndex = numContours THEN
			endIdx = numVertices - 1
		ELSE
			endIdx = contArr[contIndex + 1] - 2 -(contIndex - 1)
		ENDIF

		bClosed = 0
		bStartWithLast = 0

		index = endIdx * 3
		lastVertX = resVertices[index + 1]
		lastVertY = resVertices[index + 2]
		lastVertA = resVertices[index + 3]
		edgeInfo  = inhEdgeInfos[endIdx + 1]

		IF ABS(lastVertA) > EPS THEN
			PUT lastVertX, lastVertY, 0
			bStartWithLast = 1
		ENDIF

		FOR vertIndex = begIdx TO endIdx
			index = vertIndex * 3
			actVertX = resVertices[index + 1]
			actVertY = resVertices[index + 2]
			actVertA = resVertices[index + 3]

			! ------------------------------------------------------------------------------
			! Show inherited edge information number
			! ------------------------------------------------------------------------------
			!
			!			text2	(lastVertX + actVertX) / 2,
			!					(lastVertY + actVertY) / 2,
			!					edgeInfo
			! ------------------------------------------------------------------------------

			IF vertIndex = begIdx THEN
				IF bStartWithLast THEN
					ac2gdl_spx = lastVertX
					ac2gdl_spy = lastVertY
				ELSE
					ac2gdl_spx = actVertX
					ac2gdl_spy = actVertY
				ENDIF
			ENDIF

			if abs(lastVertA) > EPS then
			!	ac2gdl_mx = (actVertX + lastVertX) / 2
			!	ac2gdl_my = (actVertY + lastVertY) / 2
			!	ac2gdl_ratio = 1 / 2 / tan (lastVertA / 2)
			!
			!	ac2gdl_cx = ac2gdl_mx - ac2gdl_ratio * (actVertY - lastVertY)	! Arc center
			!	ac2gdl_cy = ac2gdl_my + ac2gdl_ratio * (actVertX - lastVertX)
			!
			!	put ac2gdl_cx,	ac2gdl_cy,	900,
			!		0,			lastVertA,	4000
			else
				if edgeInfo then
					line2 lastVertX, lastVertY, actVertX, actVertY
				endif
			endif

			IF vertIndex > begIdx & ABS(ac2gdl_spx - actVertX) < EPS & ABS(ac2gdl_spy - actVertY) < EPS THEN
				PUT actVertX, actVertY, -1
				bClosed = 1
			ELSE
				IF vertIndex < endIdx | NOT(bStartWithLast) THEN
					edgeInfo  = inhEdgeInfos[vertIndex + 1]
					PUT actVertX, actVertY, 0
				ENDIF
			ENDIF

			lastVertX = actVertX
			lastVertY = actVertY
			lastVertA = actVertA

		NEXT vertIndex

		IF NOT(bClosed) THEN	! Close the polygon IF it has holes
			PUT ac2gdl_spx, ac2gdl_spy, -1
		ENDIF

	NEXT contIndex
RETURN
]]>
</Script_2D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_1D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
! const values for parameter: gs_detlevel_3D_m
DLEV3D_OFF      = 0
DLEV3D_SIMPLE   = 1
DLEV3D_DETAILED = 2


! const values for parameter: iTopConnType
TOPCONNTYPE_WALL     = 1
TOPCONNTYPE_STRINGER = 2


! const values for parameter: iHandrailPos
HANDRAILPOS_LEFT  = 1
HANDRAILPOS_RIGHT = 2
HANDRAILPOS_BOTH  = 3
HANDRAILPOS_NONE  = 4

! Ship_Ladder

EPS	= 0.0001

dim treed_mtr[3]
	treed_mtr[1]=`Простой`
	treed_mtr[2]=`Детальный`
	treed_mtr[3]=`Откл.`

dim stTopConnDesc[2]
	stTopConnDesc[1]=`К Стене`
	stTopConnDesc[2]=`К Тетиве`

dim stHandRailPosValues[4]
	stHandRailPosValues[1]=`Слева`
	stHandRailPosValues[2]=`Справа`
	stHandRailPosValues[3]=`Обе Стороны`
	stHandRailPosValues[4]=`Нет`

DETLEVEL2D_SCALE_SENSITIVE	= 0
DETLEVEL2D_50				= 3
DETLEVEL2D_100				= 2
DETLEVEL2D_200				= 1


! --- Basic Variables ---
f_w  =2"			! foot width
f_d  =2"			! foot depth
f_t  =1/4"			! foot thickness

st_w =7"			! depth of the steps
st_h =1"			! height of the steps
st_t =1/2"			! thickness of the steps

sr_w =(st_w+2")*sin(alfa)	! side rail width - calculated from stw & alfa
sr_d =2"			! side rail depth
sr_t =1/4"			! side rail thickness

! --- Calculated Variables ---

npY  = INT((st_w-2*st_t)/1")
npX  = INT((b-2*sr_t-2*st_t)/1")
dpY  = (st_w-2*st_t)/npY
dpX  = (b-2*sr_t-2*st_t)/npX

dx_0 = (st_w+1")-sr_w*tan(alfa/2)  !!!correction x value of A regarding to last step_width

s_corr=4"			!!! side correction - rail overhang

stepp_dis=vd_st
n_st=INT((zzyzx)/stepp_dis)+1
dY = (zzyzx-sr_t)/n_st
dX=dY/tan(alfa)

]]>
</Script_1D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_PR SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_PR>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_UI SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
TABID_ROOT				= -1
TABID_DIMENSIONS		= 1
TABID_REPRESENTATION	= 2

dim stRepresentation[15]
	stRepresentation[ 1]	= `2D-отображение`
	stRepresentation[ 2]	= `Перо Контура`
	stRepresentation[ 3]	= `Тип Штриховки`
	stRepresentation[ 4]	= `Перо Штриховки`
	stRepresentation[ 5]	= `Перо Фона`
	stRepresentation[ 6]	= `3D-отображение`
	stRepresentation[ 7]	= `3D-детализация`
	stRepresentation[ 8]	= `Аппроксимация`
	stRepresentation[ 9]	= `Отбрасывание Теней`
	stRepresentation[10]	= `2D-символ`
	stRepresentation[11]	= `2D-детализация`
	stRepresentation[12]	= `Линия Всхода`
	stRepresentation[13]	= `Тип Линии Всхода`
	stRepresentation[14]	= `Зависит от Этажа`
	stRepresentation[15]	= `Тип Линии Проекции`

dim stDimension[5]
	stDimension[1]	= `Крепеж Верха`
	stDimension[2]	= `Перила`
	stDimension[3]	= `Расположение`
	stDimension[4]	= `Диаметр`
	stDimension[5]	= `Конструкция`

dim stTooltip[7]
	stTooltip[1]	= `Длина Лестницы`
	stTooltip[2]	= `Ширина Лестницы`
	stTooltip[3]	= `Высота Лестницы`
	stTooltip[4]	= `Высота Перил`
	stTooltip[5]	= `Ширина Двери (0=Нет)`
	stTooltip[6]	= `Высота Подступенка`
	stTooltip[7]	= `Наклон Лестницы`


! ======================================================================

ui_dialog `Параметры Лестницы`,444,266
ui_current_page gs_ui_current_page




pageStartY	= 5
yStart		= pageStartY + 2
xHalfLevel 	= 222
xEndLevel	= 444
hRow		= 16
lineSpace	= 25
hBox		= 20

! ======================================================================

if TABID_DIMENSIONS > 0 then
	ui_page TABID_DIMENSIONS, TABID_ROOT, `Размеры`, "uiTab_Dimension_flat(1)"
	gosub "dimensionPage"
endif

if TABID_REPRESENTATION > 0 then
	ui_page TABID_REPRESENTATION, TABID_ROOT, `Отображение`, "uiTab_2DRepr_flat(1)"
	gosub "representationPage"
endif

! ======================================================================
end
! ======================================================================

"dimensionPage":
! ======================================================================
! Page 1
! ======================================================================

	pageStartY	= 5
	yStart		= pageStartY + 2
	x1			= 5
	x2			= 216
	x5			= 320
	wBox1		= 94
	wBox2		= 90


	ui_style 0, 1
	ui_outfield stDimension[5], x1, yStart, 100,	hRow			! Structure
	ui_style 0, 0

	ui_pict "ship_ladder_graphic_b(1)", 100, pageStartY + 48, 112, 148, 1
	ui_infield "hr_h", x2, pageStartY + 69, wBox2, hRow								ui_tooltip stTooltip[4]		!Handrail Height
	ui_infield "ZZYZX", x2, pageStartY + 132, wBox2, hRow							ui_tooltip stTooltip[3]		!Ladder Height
	ui_infield "alfa", x2, pageStartY + 156, wBox2, hRow							ui_tooltip stTooltip[7]		!Ladder Angle
	ui_infield "B", 47, pageStartY + 202, wBox2, hRow								ui_tooltip stTooltip[2]		!Ladder Width
	ui_infield "A", 143, pageStartY + 202, wBox2, hRow								ui_tooltip stTooltip[1]		!Ladder Length
	ui_infield "vd_st", x1, pageStartY + 156, wBox2, hRow							ui_tooltip stTooltip[6]		!Riser Height
	ui_infield "door_w", 80, pageStartY + 25, wBox2, hRow							ui_tooltip stTooltip[5]		!Door Width (0=No)


	! ------ Top Connection -------
	ui_separator 313, pageStartY - 4, 313, pageStartY + 231

	ui_style 0, 1
	ui_outfield stDimension[1], x5, yStart, xEndLevel-x5,	hRow		! Top Connection
	ui_style 0, 0
	y1 = yStart + 20

	ui_infield{3} "iTopConnType", x5, y1, 120, 77,
			2, "connection_ladder(1)", 2, 1,
			25, 57, 25, 57,
			2, stTopConnDesc[1], TOPCONNTYPE_WALL,
			1, stTopConnDesc[2], TOPCONNTYPE_STRINGER	ui_tooltip stDimension[1]
	y1 = y1 + 82

	ui_separator 316, y1, 444, y1
	y1 = y1 + 9

	ui_style 0, 1
	ui_outfield stDimension[2], x5, y1, xEndLevel-x5,	hRow			! Handrail
	ui_style 0, 0
	y1 = y1 + lineSpace


	ui_outfield stDimension[3], x5+5, y1, xEndLevel-x5-5,	hRow		! Position
	y1 = y1 + lineSpace - 2
	ui_infield{3} "iHandrailPos", 440-wBox1, y1-1, wBox1, hBox,
		8, "", 4, 1,
		0, 0, 0, 0,
		1, stHandRailPosValues[1], HANDRAILPOS_LEFT,
		2, stHandRailPosValues[2], HANDRAILPOS_RIGHT,
		3, stHandRailPosValues[3], HANDRAILPOS_BOTH,
		4, stHandRailPosValues[4], HANDRAILPOS_NONE	ui_tooltip stDimension[3]
	y1 = y1 + lineSpace - 1

	iFlag = 4 * ( iHandrailPos = HANDRAILPOS_NONE)
	ui_outfield stDimension[4], x5+5, y1, xEndLevel-x5-5, hRow, iFlag	! Diameter
	y1 = y1 + lineSpace - 1
	ui_infield{3} "hr_d", 440-wBox1, y1-1, wBox2, hRow					ui_tooltip stDimension[4]
return

"representationPage":
! ======================================================================
! Page 2
! ======================================================================

	pageStartY 	= 5
	x1			= 10
	wDescript1	= 200
	wBox1		= 120
	wBox2		= 90
	wPenBox		= 40
	hPenBox		= 22
	hLineBox	= 27
	wFillBox	= 120
	hFillBox	= 22

	! ------ 1. Column - 2D Symbol -------
	wColumn = xHalfLevel - x1 - 13
	xEnd = xHalfLevel - 13

	ui_style 0, 1
	ui_outfield stRepresentation[10], x1, yStart, wColumn,	hRow					! 2D Symbol
	ui_style 0, 0
	y1 = yStart + lineSpace

	ui_outfield stRepresentation[11], x1, y1, wColumn,	hRow						! 2D Detail Level
	y1 = y1 + lineSpace
	dim stDetail2d[]
	call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
								gs_detlevel_2D_m	= gs_detlevel_2D_m,
								enable2dDetLevels	= 2+4+8+16,
			returned_parameters	stDetail2d
	ui_infield{3} "gs_detlevel_2D_m", xEnd-wBox1, y1-3, wBox1, hBox,
			8, "", 4, 1, 0, 0, 0, 0,
			1, stDetail2d[1], DETLEVEL2D_SCALE_SENSITIVE,
			2, stDetail2d[2], DETLEVEL2D_50,
			3, stDetail2d[3], DETLEVEL2D_100,
			4, stDetail2d[4], DETLEVEL2D_200 									ui_tooltip stRepresentation[11]
	y1 = y1 + lineSpace

	ui_infield{3} "walkl", x1, y1-1, wColumn, hRow,									! Walking Line
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stRepresentation[12], 0,
		1, stRepresentation[12], 1													ui_tooltip stRepresentation[12]
	y1 = y1 + lineSpace

	iFlag = 4*not(walkl)
	ui_outfield stRepresentation[13], x1, y1, wColumn,	hRow, iFlag					! Walking Line Type
	y1 = y1 + lineSpace
	ui_infield "gs_wLineType1",	xEnd-wBox1, y1-4, wBox1, hLineBox				ui_tooltip stRepresentation[13]
	y1 = y1 + lineSpace + 6

	ui_infield{3} "bStorySensitive", x1, y1-1, wColumn, hRow,						! Story Sensitive
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stRepresentation[14], 0,
		1, stRepresentation[14], 1													ui_tooltip stRepresentation[14]
	y1 = y1 + lineSpace

	iFlag = 4*not(bStorySensitive)
	ui_outfield stRepresentation[15], x1, y1, wColumn,	hRow, iFlag					! Overhead Line Type
	y1 = y1 + lineSpace
	ui_infield "gs_LineTypeStUp",	xEnd-wBox1, y1-4, wBox1, hLineBox			ui_tooltip stRepresentation[15]
	y1 = y1 + lineSpace


	! ------ 2. Column - 2D Representation -------
	ui_separator xHalfLevel-1, pageStartY - 4, xHalfLevel-1, pageStartY + 231

	wColumn = xEndLevel - xHalfLevel - x1 - 5
	xEnd = xEndLevel - 5
	x1 = xHalfLevel + x1

	ui_style 0, 1
	ui_outfield stRepresentation[1], x1, yStart, wColumn,	hRow					! 2D Representation
	ui_style 0, 0
	y1 = yStart + lineSpace

	ui_outfield stRepresentation[2], x1, y1, wColumn-wPenBox-2,	hRow				! Contour Pen
	ui_infield "gs_cont_pen",	xEnd-wPenBox, y1-4, wPenBox, hPenBox 			 ui_tooltip stRepresentation[2]
	y1 = y1 + lineSpace

	ui_outfield stRepresentation[3], x1, y1, wColumn-wFillBox-2,	hRow			! Fill Type
	ui_infield "gs_fill_type",	xEnd-wFillBox, y1-4, wFillBox, hFillBox 		ui_tooltip stRepresentation[3]
	y1 = y1 + lineSpace

	ui_outfield stRepresentation[4], x1, y1, wColumn-wPenBox-2,	hRow				! Fill Pen
	ui_infield "gs_fill_pen",	xEnd-wPenBox, y1-4, wPenBox, hPenBox 			ui_tooltip stRepresentation[4]
	y1 = y1 + lineSpace

	ui_outfield stRepresentation[5], x1, y1, wColumn-wPenBox-2,	hRow				! Fill Background Pen
	ui_infield "gs_back_pen",	xEnd-wPenBox, y1-4, wPenBox, hPenBox 			ui_tooltip stRepresentation[5]
	y1 = y1 + lineSpace

	ui_separator xHalfLevel+2, y1-3, xEndLevel, y1-3


	! ------ 2. Column - 3D Representation -------

	y1 = y1 + 6
	ui_style 0,1
	ui_outfield stRepresentation[6], x1, y1,	wDescript1,	hRow					!3D Representation
	ui_style 0,0
	y1 = y1 + lineSpace

	ui_outfield stRepresentation[7], x1 , y1, wColumn-wBox2-2, hRow					! 3D Detail Level
	ui_infield{3} "gs_detlevel_3D_m", xEnd-wBox2, y1-3, wBox2, hBox,
			8, "", 4, 1, 0, 0, 0, 0,
			1, treed_mtr[1], DLEV3D_SIMPLE,
			2, treed_mtr[2], DLEV3D_DETAILED,
			3, treed_mtr[3], DLEV3D_OFF	ui_tooltip stRepresentation[7]
	y1 = y1 + lineSpace

	iFlag = (gs_detlevel_3D_m = DLEV3D_OFF) * 4

	ui_outfield stRepresentation[8], x1 , y1, wColumn-wBox2-2, hRow, iFlag			! Resolution
	ui_infield "gs_resol", xEnd-wBox2, y1-2, wBox2,	hRow						ui_tooltip stRepresentation[8]
	y1 = y1 + lineSpace

	ui_infield{3} "gs_shadow", x1+1, y1-1, wDescript1, hRow,							! Shadow
		7, "",
		2,
		1, 20, 20, 20, 20,
		0, stRepresentation[9], 0,
		1, stRepresentation[9], 1													ui_tooltip stRepresentation[9]
	y1 = y1 + lineSpace
return

]]>
</Script_UI>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_VL SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
call "FM_types" parameters all

VALUES "gs_ui_current_page" 1, 2

values{2} "gs_detlevel_3D_m" 	DLEV3D_SIMPLE, `Простой`,
								DLEV3D_DETAILED, `Детальный`,
								DLEV3D_OFF, `Откл.`

values{2} "iTopConnType" 	TOPCONNTYPE_WALL, stTopConnDesc[1],
							TOPCONNTYPE_STRINGER, stTopConnDesc[2]

values{2} "iHandrailPos" 	HANDRAILPOS_LEFT, stHandRailPosValues[1],
							HANDRAILPOS_RIGHT, stHandRailPosValues[2],
							HANDRAILPOS_BOTH, stHandRailPosValues[3],
							HANDRAILPOS_NONE, stHandRailPosValues[4]
!!!!values "A" Range[0.7,)
values "A" range [zzyzx / tan(75) + 5", zzyzx / tan(60) + 5"]
VALUES "B" RANGE (0,]
VALUES "ZZYZX" RANGE (0.992,]
values "alfa" range [60, 75]

VALUES "hr_h" RANGE (0,]
VALUES "hr_d" RANGE (0,]
VALUES "vd_st" RANGE (0,zzyzx]
VALUES "door_w" 0, RANGE [B/2,]

IF GLOB_MODPAR_NAME="b_temp" THEN
  PARAMETERS b=2*b_temp
  b=2*b_temp
ELSE
  PARAMETERS b_temp=b/2
  b_temp=b/2
ENDIF

if GLOB_MODPAR_NAME = "ZZYZX" | GLOB_MODPAR_NAME = "alfa" then
	a = zzyzx / tan(alfa) + 5"
	parameters a = a
else if GLOB_MODPAR_NAME = "A" then	
	alfa = atn(zzyzx / (a - 5"))
	parameters alfa = alfa
endif


IF iHandrailPos = HANDRAILPOS_NONE THEN lock	"iTopConnType",
												"hr_d",
												"hr_h",
												"door_w",
												"gs_resol"

if gs_cont_pen<=0 then parameters gs_cont_pen=2
if gs_fill_pen<=0 then parameters gs_fill_pen=91


! ----- 3D Representation -----
if gs_detlevel_3D_m = DLEV3D_OFF then
	hideparameter "gs_resol", "gs_shadow"
	lock "gs_resol", "gs_shadow"
endif
values "gs_resol" range [3,)


! ----- 2D Representation -----
call "2dDetailLevel" parameters gs_detlevel_2D	= gs_detlevel_2D,
							gs_detlevel_2D_m	= gs_detlevel_2D_m,
							enable2dDetLevels	= 2+4+8+16

if not(walkl) then
	hideparameter "gs_wLineType1"
	lock "gs_wLineType1"
endif

if not(bStorySensitive) then
	hideparameter "gs_LineTypeStUp"
	lock "gs_LineTypeStUp"
endif

ac_bottomlevel = 0
ac_toplevel = ZZYZX + EPS

parameters ac_toplevel = ac_toplevel, ac_bottomlevel = ac_bottomlevel

]]>
</Script_VL>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Comment SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[Корабельная лестница с перилами и зависимым от этажа 2D-символом.]]>
</Comment>

<Keywords SectVersion="1" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Keywords>

<ParamSection SectVersion="26" SectionFlags="0" SubIdent="0">
	<ParamSectHeader>
		<Version>26</Version>
		<AutoHotspots>false</AutoHotspots>
		<StatBits>
			<STBit_UIDefault/>
			<STBit_FixSize/>
			<STBit_UIUseHierarchicalPages/>
		</StatBits>
		<WDLeftFrame>0</WDLeftFrame>
		<WDRightFrame>0</WDRightFrame>
		<WDTopFrame>0</WDTopFrame>
		<WDBotFrame>0</WDBotFrame>
		<LayFlags>65535</LayFlags>
		<WDMirrorThickness>0</WDMirrorThickness>
		<WDWallInset>0</WDWallInset>
		<SymbolColor>0</SymbolColor>
	</ParamSectHeader>
	<Parameters>
		<Length Name="A">
			<Description><![CDATA["Длина Лестницы"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Unique/>
			</Flags>
			<Value>0.993025403784</Value>
		</Length>
		<Length Name="B">
			<Description><![CDATA["Ширина Лестницы"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Unique/>
			</Flags>
			<Value>0.7</Value>
		</Length>
		<Length Name="ZZYZX">
			<Description><![CDATA["Высота Лестницы"]]></Description>
			<Fix/>
			<Value>1.5</Value>
		</Length>
		<Boolean Name="AC_show2DHotspotsIn3D">
			<Description><![CDATA["Показ Узловых 2D Точек в 3D"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<Length Name="ac_bottomlevel">
			<Description><![CDATA["Отметка Низа"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="ac_toplevel">
			<Description><![CDATA["Отметка Верха"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Length>
		<Length Name="vd_st">
			<Description><![CDATA["Высота Подступенка"]]></Description>
			<Value>0.3</Value>
		</Length>
		<Angle Name="alfa">
			<Description><![CDATA["Наклон Лестницы"]]></Description>
			<Value>60</Value>
		</Angle>

		<!-- iHandrailPos: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Integer Name="iHandrailPos">
			<Description><![CDATA["Перила"]]></Description>
			<Flags>
				<ParFlg_BoldName/>
			</Flags>
			<Value>3</Value>
		</Integer>
		<Integer Name="iTopConnType">
			<Description><![CDATA["Соединение Вверху"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>2</Value>
		</Integer>
		<Length Name="hr_d">
			<Description><![CDATA["Диаметр"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.03</Value>
		</Length>
		<Length Name="hr_h">
			<Description><![CDATA["Высота"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.9</Value>
		</Length>
		<Integer Name="gs_resol">
			<Description><![CDATA["Разрешение"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>6</Value>
		</Integer>
		<Length Name="door_w">
			<Description><![CDATA["Ширина Двери (0=Нет)"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Length>

		<!-- gs_3D_representation: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_3D_representation">
			<Description><![CDATA["3D-отображение"]]></Description>
		</Title>
		<Integer Name="gs_detlevel_3D_m">
			<Description><![CDATA["3D-детализация"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>1</Value>
		</Integer>
		<Boolean Name="gs_shadow">
			<Description><![CDATA["Отбрасывание Теней"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>1</Value>
		</Boolean>

		<!-- gs_2D_representation: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_2D_representation">
			<Description><![CDATA["2D-отображение"]]></Description>
			<Fix/>
		</Title>
		<String Name="gs_detlevel_2D">
			<Description><![CDATA["2D-детализация"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["Зависит от Масштаба"]]></Value>
		</String>
		<Integer Name="gs_detlevel_2D_m">
			<Description><![CDATA["2D-детализация"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Integer>
		<PenColor Name="gs_cont_pen">
			<Description><![CDATA["Перо Контура"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>4</Value>
		</PenColor>
		<FillPattern Name="gs_fill_type">
			<Description><![CDATA["Тип Штриховки"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>65</Value>
		</FillPattern>
		<PenColor Name="gs_fill_pen">
			<Description><![CDATA["Перо Штриховки"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>19</Value>
		</PenColor>
		<PenColor Name="gs_back_pen">
			<Description><![CDATA["Перо Фона Штриховки"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>19</Value>
		</PenColor>
		<LineType Name="gs_LineType">
			<Description><![CDATA["Тип Линии"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>1</Value>
		</LineType>
		<Boolean Name="bStorySensitive">
			<Description><![CDATA["Зависит от Этажа"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_BoldName/>
			</Flags>
			<Value>1</Value>
		</Boolean>
		<LineType Name="gs_LineTypeStUp">
			<Description><![CDATA["Тип Линии Проекции"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>4</Value>
		</LineType>
		<Boolean Name="walkl">
			<Description><![CDATA["Показ Линии Всхода"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_BoldName/>
			</Flags>
			<Value>1</Value>
		</Boolean>
		<LineType Name="gs_wLineType1">
			<Description><![CDATA["Тип Линии Всхода"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>4</Value>
		</LineType>

		<!-- gs_material: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_material">
			<Description><![CDATA["Покрытия"]]></Description>
		</Title>
		<Material Name="mat_ladder">
			<Description><![CDATA["Лестница"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>11</Value>
		</Material>
		<Length Name="b_temp">
			<Description><![CDATA[""]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.35</Value>
		</Length>

		<!-- gs_list: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_list">
			<Description><![CDATA["Параметры Спецификаций"]]></Description>
			<Fix/>
		</Title>
		<RealNum Name="gs_list_cost">
			<Description><![CDATA["Цена"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</RealNum>
		<String Name="gs_list_manufacturer">
			<Description><![CDATA["Производитель"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_note">
			<Description><![CDATA["Примечания"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_location">
			<Description><![CDATA["Расположение"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_accessories">
			<Description><![CDATA["Аксессуары"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="FM_Type">
			<Description><![CDATA["Тип Группы"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["Другой"]]></Value>
		</String>
		<Integer Name="iFMType">
			<Description><![CDATA["Тип Группы"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>25</Value>
		</Integer>
		<String Name="FM_InventoryNumber">
			<Description><![CDATA["Инвентарный Номер"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="FM_SerialNumber">
			<Description><![CDATA["Серийный Номер"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="FM_ProductionYear">
			<Description><![CDATA["Год Производства"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<RealNum Name="FM_ObjectWeight">
			<Description><![CDATA["Вес Изделия"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</RealNum>
		<String Name="FM_ObjectWeightUnit">
			<Description><![CDATA["Единица Веса"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["кг"]]></Value>
		</String>
		<String Name="gs_list_custom1">
			<Description><![CDATA["Пользовательское Значение 1"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom2">
			<Description><![CDATA["Пользовательское Значение 2"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom3">
			<Description><![CDATA["Пользовательское Значение 3"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom4">
			<Description><![CDATA["Пользовательское Значение 4"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<String Name="gs_list_custom5">
			<Description><![CDATA["Пользовательское Значение 5"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA[""]]></Value>
		</String>
		<Integer Name="gs_ui_current_page">
			<Description><![CDATA[""]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Integer>
		<String Name="LibraryLangCode">
			<Description><![CDATA[""]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value><![CDATA["RUS"]]></Value>
		</String>
	</Parameters>
</ParamSection>

<CalledMacros SectVersion="2" SectionFlags="0" SubIdent="0">
	<Macro>
		<MName><![CDATA["2dDetailLevel"]]></MName>
		<MainGUID>2FD16305-CA53-4505-BFA7-4E98A8BA9399</MainGUID>
	</Macro>
	<Macro>
		<MName><![CDATA["FM_types"]]></MName>
		<MainGUID>2759D2DF-AA8E-44D1-AD1D-261581266B34</MainGUID>
	</Macro>
</CalledMacros>

<Picture MIME="image/png" SectVersion="19" SectionFlags="0" SubIdent="0" length_in_bytes="1507" platform="Win">
	<![CDATA[
	696D6167652F706E670089504E470D0A1A0A0000000D49484452000000800000
	00800803000000F4E091F900000060504C5445FFFFFF757369656359848375A3
	A09155534B3C3B35C3C2BB4C4B43A4A4A0CDCDCB5C5A5143423BB3B2AD9D9D9A
	D1D0C8BCBBB46E6C629492837C7A71E9E8E5706E63B0AFAADDDCD98D8C84A09F
	9C807E72605E55E1E0DDC0BFB8504F47D0CFC75AA0DA9B000005344944415478
	5EED99D98EDB3A104459DDCD9DDA3D7B92FBFF7F7925D98A91912D3F358D00A9
	C1808F3C28154B4DD9FC9DE2F6B9002792E702F4213E172095273F82A63C70E0
	5DD9211B3F8E1DD2CEC8108F37E894337292188F01AC6E46FA36B60F2220AA00
	BE7DE000A26E08393D02687401AC397E0449941D2023871BFCD7F6AA19E87A73
	9C8194741D786DCD711191E9A3EE2178E0008CAE036C1E38C0CA0EFC32C70EA4
	A8EC407EE0406A751D481F0F1CE039A71F7A006B091D76FD60741D784D0F1C20
	A39B0132C7003FA2B203BF1E00A476066815A791690338CA88224027E6788377
	A3E8C0B677FC38CE48A308309A0700507600C61CF6C0CF787E61AA2998E310FA
	0F5D074ED11C8730A67969F500BAF8C0015A1669350FC13180DD1CD2CBE03100
	2A001C75BD8FCA00D3BABCCE006F7D7FF7117DA80124D91CF801587C36FE1BC6
	67D275A08B6707A616942D330144E0DC34ED196430BA00315D2EE88CCC2F4403
	D14499108833E873F0AE9FA50880CB2A82009E051A301008C4B42C5F0C04C589
	68FA0D60DDE284EFA3F02A0290195F1882537C17C8B64A71E6AA93F76901195F
	08CE95D8B6BA19349D887366AF379617E75C94A40EC08B037B15C10C60D4F47E
	599B759F610F60C5A902B0A4743904AE600FD8F306A026BA64D051887B80E685
	B501C6730F890BC1ECE5797D34EA00DD3D00C8A0BCFFA9DF0E412851FE6C5C3F
	BC58B1CEB59A006F712BE21266596B3937CDCF15A46F4FB26430690234DDA588
	4BB094C33B882C1159004333362B80518F8011099999A76C81000C0458C0120B
	B401866B064FDEFB09BC88084CB079142847E0C45B06AF87E047F23E32F30816
	21ED0CBE5E8AB88469D782931729AA00DB954B1680B803904E3F83722D62D995
	D05A0FCA003828623B0340B9078DBD5FC4273622D0CEA09C0150F600BD18512F
	627F90C158A38863DA26E23049FBE7566DAA7008789B88CFAFA24281FBBE7F5B
	01E89C4157258325846C0102E86559284CDEF946BD074FC3198003780200A2C9
	660B4BC89385FE21F0F152C4218FEB45048119CC003108224E19A0D98AD86D45
	FCD67B2FBC88ECA89FC1CFBB13F1C9C337C2EE89454C358AB81C14312A147177
	99887103C05F8A582A647044D817718A158A58DA6D22DE03A04611E37A083A73
	55E522FE6F2DE236EDE01AFD8978DC26E212161578DFBFFDFE7CA83F11C7AD88
	97CDC9120F446C017A6FBC44A33F11A7AD88C304C244964194E7C582C5B27E06
	5FD2360C0C63E259990066106009AC3F1187EF45DC343EF159C0A85FC45FD769
	E45B0BFA08D62F62F3757F224E35266243F727E2AE46119B7CBF88D3762DACE1
	800BFB224EB142119B7CFFF35817B78958537429E29B0022A802D0085CC08D3B
	A3885307C8DB2198FF0A62D3BF5D3380F54D60F41D1059F70F3913C112A6F135
	CE201D845D0D809348B104063113285346004023B622D6543E17711E7856B02B
	04812D8118153268E852C465A189BDF7BC0A0033C45601D817B19FC53C622B62
	4DD1BE8837752B805107386D45BC07400580BC16B10D662F011527EA0E5C6F65
	7B800A193474FE468C9B00AE06C0FD0CAE00A6020095DB00235501E838CC00F3
	7F8CF20746B3B4C0A00FD0DB1256041B062202A2FFD96F3FA53BD10788844244
	16B06022E28C9583B661405944230278D127C0B48A2D316FC380B26894BC66D0
	FBFE7A231A6047195D0D00AC3DD87D7F11318F951C1845EEF4E00220350060EF
	0070716D158012E24D80C1B9A40F000AC5DE2C62A1E28CBEC8B910E41600977A
	0066AF4650CA500500F11680F754258326E32E5A2935009AFB68A598A78A9C7B
	36407A2EC07B67FE62FDD33FFDD3FFB7C3382766B6B8410000000049454E44AE
	426082
	]]>
</Picture>

<GDLPict MIME="image/png" SectVersion="19" SectionFlags="0" SubIdent="1" length_in_bytes="638" platform="Win">
	<![CDATA[
	696D6167652F706E670089504E470D0A1A0A0000000D49484452000000640000
	00900203000000838F26C80000000C504C5445FF3121000000737373FFFFFF00
	7B76D10000000174524E530040E6D8660000021649444154785EA5D731AADB40
	10C6F1B1B01AABF183E4122AF4AEE0420770A1779F256D2EE1528CC16917A7D0
	55444A1BE27649C08AA5B5F2699895F0265FE157FC90FC67E015A6F9951E565B
	259F9EB253F2F1DC3EE2197CCFEB1BDF82B6FF59ED21394C21376549F629ED54
	1AB3DA87E5BB49ABA0245743BB48C9BEC64B91C58BD532D6C44B66E2A5A078B1
	5AC69878C94CBC14142F3646D0220569F15250BCD828418A14A4F592563332FC
	67E5DCEF4B8B34E2635D9674663F0825DD6FFB78A6F939BCA319C5F6F2CBA615
	B197F30192759DA31D255E2CA217E45D0AA29520BAD1324607C50E72A9A520DA
	D04490A605D177F2C28F1D2DA2994F42107DE69AE4DB10ED948CD1AD16444B41
	B416444BF1D1CD82382988D682682D8896D7F169391F67A460265D80E8B03825
	88568268298856E2EFB92077D282E8B03825880E09A2E5DD1EBAE1A35132DED3
	A802442B4174589C12440725F3D152706925B8B412444B41B41644CBEB0CD1CC
	362C19F38174818F6E43826829880E49B3202E2C493723488B10442B419A1444
	8BBB3DA4BF67394ECA856BF25BED4501D228AD94A06C2A88D6823429CD9CFCF0
	F78C92AB4F8B921BA229ADA6D7B9F08908D711C2EBF2EFE4DB1C612FC9E6D6B5
	B152DC3A3327777A594E83D4C537F7B250318879976969A50497A3BC978DC959
	449725FDD3B63E652B604D8B7BEB61BD7BD3BFCB52FF910AD9F7F2F9A3DFAEFF
	8355F3CF2C7CCFC2FE004B867411FAA0212A0000000049454E44AE426082
	]]>
</GDLPict>

<GDLPict MIME="image/png" SectVersion="19" SectionFlags="0" SubIdent="2" length_in_bytes="273" platform="Win">
	<![CDATA[
	696D6167652F706E670089504E470D0A1A0A0000000D49484452000000780000
	004B08030000008C54B3E300000006504C5445FFFFFF75757575DF71C5000000
	BC4944415478DAEDD4318A04410C0441D5FF3FBD70DC22672D15643A5DCE5849
	20A367DE7E2DDFDDDBCECDA16DE0CDAE6D22B832CCBB368CBB3A4CBB3E4CBB3E
	4CBB3E4CBB3E4CBB3E4CBB3E4CBB3ECCBA3D9CFFE1ED7E6F6D321E5CB893A2ED
	60A99D146D072B6D9214ED156E9E83F1944EF0F4ED3BF81DFC0E06FE1DFEC11B
	D3AD0FD3AD0FD3AD0F0BEDC6DB846937C6DA8DE9D687E9D687E9D687E176E3CC
	1DCE34B0D20EDFFA30DAFA7012B2F5E14CC2B63E3C6CEBC37F035A711FB80302
	333EF166510000000049454E44AE426082
	]]>
</GDLPict>

<MigrationTable SectVersion="1" SectionFlags="0" SubIdent="0">
	<MigrationTableElement>
		<MainGUID>36F03404-A1D5-4B91-B373-B0CAF4F8CEC3</MainGUID>
		<Version>19</Version>
		<Name><![CDATA["Ship Ladder 60"]]></Name>
		<AutoMigration>false</AutoMigration>
	</MigrationTableElement>
</MigrationTable>

<Copyright SectVersion="1" SectionFlags="0" SubIdent="0">
	<Author>GRAPHISOFT</Author>
	<License>
		<Type>CC BY-ND</Type>
		<Version>3.0</Version>
	</License>
</Copyright>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_FWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
actualGUID = FROM_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

	_startID	= "36F03404-A1D5-4B91-B373-B0CAF4F8CEC3"	!19 version
	_endID		= "9DAF3E4C-CBD6-4AB3-96B1-96156EC2C4CA"	!new version
gosub "mig20_FWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid actualGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! New alfa parameter
! ==============================================================================
"mig20_FWM":
	if actualGUID = _startID then
		!fixed value in old version
		alfa = 60
		parameters alfa = alfa
		
		actualGUID = _endID
	endif
return
]]>
</Script_FWM>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_BWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
targetGUID = TO_GUID

! ==============================================================================
! Subroutines
! ==============================================================================

gosub "mig20_BWM"

! ==============================================================================
! Set migration GUID
! ==============================================================================

setmigrationguid targetGUID

! ==============================================================================
end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! end ! en
! ==============================================================================


! ==============================================================================
! New alfa parameter
! ==============================================================================
"mig20_BWM":
	if targetGUID # "" then
		if abs(alfa - 60) > EPS then
			targetGUID = ""
		endif
	endif
return
]]>
</Script_BWM>

</Symbol>
