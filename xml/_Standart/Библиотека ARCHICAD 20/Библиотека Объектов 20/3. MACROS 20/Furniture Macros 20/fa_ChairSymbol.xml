<?xml version="1.0" encoding="UTF-8"?>
<Symbol IsArchivable="false" IsPlaceable="false" MainGUID="2D55D509-D5A7-47A1-9332-9153404CC05C" MigrationValue="Normal" Owner="1196638531" Signature="1196644685" Version="38">
<Ancestry SectVersion="1" SectionFlags="0" SubIdent="0" Template="false">
	<MainGUID>F938E33A-329D-4A36-BE3E-85E126820996</MainGUID>
	<MainGUID>103E8D2C-8230-42E1-9597-46F84CCE28C0</MainGUID>
	<MainGUID>B2970E4C-8680-4061-8670-2F4B638F3953</MainGUID>
</Ancestry>

<CalledMacros SectVersion="2" SectionFlags="0" SubIdent="0">
	<Macro>
		<MName><![CDATA["Resize_A_B_ZZYZX"]]></MName>
		<MainGUID>33A17ABE-882E-11D6-95A3-00039318F8B6</MainGUID>
	</Macro>
	<Macro>
		<MName><![CDATA["fa_minimalspace"]]></MName>
		<MainGUID>948022D5-BA0C-4407-8518-A3B5922DF84C</MainGUID>
	</Macro>
</CalledMacros>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_3D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_3D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_2D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
PEN gs_cont_pen
FILL gs_fill_type

! === Circle & Circle with Backrest ===

if gs_SymbolType_m = 1 or gs_SymbolType_m = 2 or gs_SymbolType_m = 11 or gs_SymbolType_m = 12 then
	if gs_OrigoCentered then add2 -A/2,-A/2

	if gs_symbolType_m = 1 or gs_symbolType_m = 2 or gs_symbolType_m = 11 then
		MSType = 4
	endif

	gosub "minimalSpaceChair"

	add2 A/2,A/2

	if gs_SymbolType_m = 2 then		! Circle with Backrest
		arc2 0,0,A/2+0.05,0,180
	endif

	if gs_SymbolType_m = 11 then	! Circle with Backrest 2
		R = A/2+0.10
		arc2 0,0,R,12,180-12
		px1 = R * cos(12)
		py = R * sin(12)
		px2 = sqr((A/2)^2 - py^2)
		line2  px1,py, px2,py
		line2 -px1,py,-px2,py
	endif

	if gs_SymbolType_m = 12 then	! Circle with Backrest 3
		R = A/2+0.075

		py1 = sqr((A/2)^2 - 0.019^2)
		py2 = sqr(R^2 - 0.019^2)
		for i=1 to 2
			rot2 38
			poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
				0,0,900,
				-0.019,py1,1,
				-0.019,py2,0,
				 0.019,py2,3001,
				 0.019,py1,0,
				-0.019,py1,3000
			del 1
			mul2 -1,1
		next i
		del 2

		poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
			 0,0,901,
			 R*cos(45), R*sin(45), 1,
			-R*cos(45), R*sin(45), 3001,
			-R*cos(45)-0.016*sin(45), R*sin(45)+0.016*cos(45), 1,
			-R*cos(45)-0.016*sin(45), R*sin(45)+0.016*cos(45)+0.044*sqr(2), 1001,
			 R*cos(45)+0.016*sin(45), R*sin(45)+0.016*cos(45)+0.044*sqr(2), 3001,
			 R*cos(45)+0.016*sin(45)-0.044*sin(45), R*sin(45)+0.016*cos(45)+0.044*cos(45), 901,
			 R*cos(45)+0.016*sin(45), R*sin(45)+0.016*cos(45), 3001,
			 R*cos(45), R*sin(45), 1
	endif

	poly2_b 2, 1+2+64, gs_fill_pen, gs_back_pen,
		0,0,901,
		A/2,360,4001

	del 1
	if gs_OrigoCentered then del 1

	if not(gs_OrigoCentered) then
		gosub 1001
	else
		gosub 1002
	endif

	gs_hotspotUnIdBegin = unID
	end gs_hotspotUnIdBegin
endif


! === Rectangular chairs ===

IF gs_OrigoCentered THEN ADD2 -A/2,-B/2

if gs_SymbolType_m = 3 or gs_SymbolType_m = 4 or gs_SymbolType_m = 10 then			!rectangle
	MSType = 4
endif
if gs_SymbolType_m = 5 or gs_SymbolType_m = 6 then									!trapezoid
	MSType = 4
endif
if gs_SymbolType_m = 7 or gs_SymbolType_m = 8 then									!curved
	MSType = 4
endif

gosub "minimalSpaceChair"

IF gs_SymbolType_m=0 GOSUB 1		! Min. Space and Hotspots for detailed symbol
IF gs_SymbolType_m=3 GOSUB 30		! Rectangle
IF gs_SymbolType_m=4 GOSUB 40		! Rectangle with Backrest 1
IF gs_SymbolType_m=5 GOSUB 50		! Trapezoid
IF gs_SymbolType_m=6 GOSUB 60		! Trapezoid with Backrest
IF gs_SymbolType_m=7 GOSUB 70		! Curved
IF gs_SymbolType_m=8 GOSUB 80		! Curved with Backrest
IF gs_SymbolType_m=9 GOSUB 90		! Rectangle with Backrest 2/ Office Chair
IF gs_SymbolType_m=10 GOSUB 100		! Rectangle with Backrest 3
IF gs_SymbolType_m=13 GOSUB 130		! Curved with Backrest 2
IF gs_SymbolType_m=14 GOSUB 140		! Curved with Backrest 3
IF gs_SymbolType_m=15 GOSUB 150		! Detailed 1
IF gs_SymbolType_m=16 GOSUB 160		! Detailed 2


IF gs_OrigoCentered THEN DEL 1

IF gs_OrigoCentered=0 THEN
	GOSUB 1003:
ELSE
	GOSUB 1004:
ENDIF

gs_hotspotUnIdBegin = unID
end gs_hotspotUnIdBegin



! ==============================================================================
! Chair symbol types
! ==============================================================================

1:

! only minimal space for custom shaped chairs

RETURN


30:

	FILL gs_fill_type
	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,b,1,
			0,b,1,
			0,0,-1

RETURN


40:

LINE2 0,b-0.05,a,b-0.05

	FILL gs_fill_type
	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,b,1,
			0,b,1,
			0,0,-1

RETURN


50:

	FILL gs_fill_type
	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A-0.05,b,1,
			0.05,b,1,
			0,0,-1

RETURN


60:

	x=(b-0.05)*0.05/b

	LINE2 x,b-0.05,a-x,b-0.05

	FILL gs_fill_type
	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A-0.05,b,1,
			0.05,b,1,
			0,0,-1

RETURN


70:

	R=A/2

	FILL gs_fill_type
	POLY2_B 6, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,B-R,1,
			A-R,B-R,901,
			R,180,4001,
			0,0,-1

RETURN


80:

	R=A/2

	FILL gs_fill_type
	POLY2_B 6, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,B-R,1,
			A-R,B-R,901,
			R,180,4001,
			0,0,-1

	tt=0.05
	ARC2 A/2,B-R,A/2+tt,0,180

RETURN

90:

	tt=0.075

	FILL gs_fill_type
	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,b-tt*2,1,
			0,b-tt*2,1,
			0,0,-1

	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			0+tt,B,1,
			A-tt,B,1,
			A-tt,b-tt,1,
			0+tt,b-tt,1,
			0+tt,B,-1

	POLY2_B 5, 1+2+64, gs_fill_pen, gs_back_pen,
			A/2+tt/2,B-tt*2,1,
			A/2-tt/2,B-tt*2,1,
			A/2-tt/2,b-tt,1,
			A/2+tt/2,b-tt,1,
			A/2+tt/2,B-tt*2,-1

RETURN


100:

	tt=b/6
	dd=SQR((A/2)^2+tt^2)
	RAD=(dd*dd/2)/tt

	ALFA=ABS(ATN((A/2+tt/2)/(RAD-2*tt)))

	ARC2 A/2,B-RAD+tt+B/20,RAD,90-alfa,90+alfa

	FILL gs_fill_type
	POLY2_B 5, 1+2+64,gs_fill_pen, gs_back_pen,
			0,0,1,
			A,0,1,
			A,b,1,
			0,b,1,
			0,0,-1

RETURN


130:
	armW = 0.15
	R = A/2

	fill gs_fill_type
	poly2_b 8, 1+2+4+64, gs_fill_pen, gs_back_pen,
		 0,		armW,	1,
		-armW,	armW,	1,
		-armW,	B-R,	1,
		 A+armW,B-R,	1001,
		 A+armW,armW,	1,
		 A,		armW,	1,
		 A,		B-R,	0,
		 0,		B-R,	1000
	poly2_b 9, 1+2+64, gs_fill_pen, gs_back_pen,
		0,0,1,
		A,0,1,
		A,B-R,1,
		A-R,B-R,901,
		R,90,4001,
		R,B-R,901,
		R,90,4001,
		0,B-R,1,
		0,0,-1

return

140:
	armW = 0.18
	RR	 = 0.12
	R = 0.55*A
	py1 = sqr(R^2 - (A/2)^2)

	fill gs_fill_type
	poly2_b 9, 1+2+4+64, gs_fill_pen, gs_back_pen,
		A/2,B-R,901,
		 0,		RR,		1,
		-armW,	RR,		1,
		-armW,	B-R+py1,	1,
		 A+armW,B-R+py1,	3001,
		 A+armW,RR,		1,
		 A,		RR,		1,
		 A,		B-R+py1,	0,
		 0,		B-R+py1,	3000

	poly2_b 9, 1+2+64, gs_fill_pen, gs_back_pen,
		0,RR,1,
		0,-1,801,
		RR,0,1001,
		A-RR,0,1,
		A,RR,1001,
		A,B-R+py1,1,
		A/2,B-R,901,
		0,B-R+py1,3001,
		0,0,-1
	line2 0,B-R+py1,A,B-R+py1
return

150:
	add2 A/2, 0
	mul2 A, B/0.7589857878381
	fill gs_fill_type

	for i=1 to 2
		poly2_b 8, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.412472310872, 0.5042872575346,      1,
			-0.4454789561129, 0.4891311203095,    900,
			0, 130.4395594118,   4001,
			-0.4784237636223, 0.5044212080728,      1,
			-0.4783902044188, 0.4220186814645,      0,
			-0.4459418515027, 0.3223778518928,    900,
			0, -36.6596484042,   4001,
			-0.4124802142925, 0.4216829844888,      1
		poly2_b 10, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.4999896252663, 0.4121555143479,      1,
			-0.4999921861635, 0.1419262161206,      1,
			-0.4459430122221, 0.2317059334238,    900,
			0, 62.09739999996,   4001,
			-0.3918940777391, 0.1419260719623,      1,
			-0.3501058248966, 0.1419260719623,      1,
			-0.3501058248966, 0.4121555426471,      1,
			-0.3918941247464, 0.4121555426471,      1,
			-0.4459418515027, 0.3223778518928,    900,
			0, 62.09737000004,   4001
		line2    -0.3918940777391, 0.1419260719623, -0.3918941247464, 0.4121555426471
		mul2 -1,1
	next i
	del 2

	poly2_b 6, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-0.075054218316, 0.6691290901383,      1,
		-0.075054218316, 0.6255355490925,      0,
		0.07507401621766, 0.6255355490925,      1,
		0.07507401621766, 0.6691290901383,      0,
		0, 2.159481211012,    900,
		0, -5.766725549621,   4001
	poly2_b 18, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-0.3501058248966, 0.06029010319873,      1,
		-0.3266482882507, 0.06029010319873,    900,
		0, 77.1406285826,   4001,
		-0.3318689707315, 0.0374208990165,      1,
		0.0001933890152809, 1.492023173939,    900,
		0, 25.70384186485,   4001,
		0.3318786602217, 0.03733486717531,      1,
		0.3266639159237, 0.06020542614092,    900,
		0, 77.15550547655,   4001,
		0.3501214525696, 0.06020542614092,      1,
		0.3501214525696, 0.6020780124466,      1,
		0.3266639159237, 0.6020780124466,    900,
		0,           90,   4001,
		0.3266639159237, 0.6255355490925,      1,
		-0.3266482882507, 0.6255355490925,      1,
		-0.3266482882507, 0.6020780124466,    900,
		0,           90,   4001,
		-0.3501058248966, 0.6020780124466,      1
	poly2_b 18, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-0.3320516758034, 0.7046550439166,      1,
		9.898950844489E-006, 2.159481211012,    900,
		0, 25.71480945931,   4001,
		0.332071473705, 0.7046550439166,      1,
		0.3268515762168, 0.7275244272862,    900,
		0, 77.14259527027,   4001,
		0.3503091128627, 0.7275244272862,      1,
		0.3503091128627, 0.7355282511922,      1,
		0.3268515762168, 0.7355282511922,    900,
		0,           90,   4001,
		0.3268515762168, 0.7589857878381,      1,
		-0.3268317783152, 0.7589857878381,      1,
		-0.3268317783152, 0.7355282511922,    900,
		0,           90,   4001,
		-0.350289314961, 0.7355282511922,      1,
		-0.350289314961, 0.7275244272862,      1,
		-0.3268317783151, 0.7275244272862,    900,
		0, 77.14259527028,   4001
	del 2
return

160:
	add2 A/2, 0
	mul2 A, B/1.000573805672
	fill gs_fill_type

	for i=1 to 2
		poly2_b 8, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-0.4913602514585, 0.5932312007,      1,
			-0.4999999870194, 0.2559180445856,      1,
			-0.4481481223429, 0.2734851839796,    900,
			0, 135.2408619479,   4001,
			-0.3989599944684, 0.2494487879702,      1,
			-0.3730636287232, 0.5836196417518,      1,
			-0.4337282441606, 0.5697631364227,    900,
			0, 144.9772801618,   4001
		mul2 -1,1
	next i
	del 2

	poly2_b 34, 1+2+4+64, gs_fill_pen, gs_back_pen,
		-0.4160154614959, 0.2173610833541,      1,
		-0.2426647168595, 0.2021957793166,    900,
		0, 78.89103792634,   4001,
		-0.2909462953108, 0.03501516951152,      1,
		0, 1.226266044023,    900,
		0, 27.4499914213,   4001,
		0.2909462970719, 0.03501515487816,      1,
		0.2426644901561, 0.2021959511117,    900,
		0, 78.89122141068,   4001,
		0.4160154131803, 0.2173619985528,      1,
		0.4150453351224, 0.2298798294306,      1,
		0.4481489330328, 0.2734857626027,    900,
		0, -26.75271134489,   4001,
		0.3989600170767, 0.2494487879702,      1,
		0.3730636513315, 0.5836196417518,      1,
		0.4337282667687, 0.5697631364228,    900,
		0, -23.78523116949,   4001,
		0.3838047412901, 0.6069093328616,      1,
		0.03834844775459, 0.565045281238,    900,
		0, 19.30252764503,   4001,
		0.3505469742921, 0.7187486630792,      1,
		0, -0.2045072821347,    900,
		0, 41.58209444127,   4001,
		-0.3505469516839, 0.7187486630792,      1,
		-0.0388052466752, 0.5641208553603,    900,
		0, 19.25379281453,   4001,
		-0.3840993685039, 0.6073020896202,      1,
		-0.4337282441606, 0.5697631364227,    900,
		0, -24.2373184648,   4001,
		-0.3730636287232, 0.5836196417518,      1,
		-0.3989599944684, 0.2494487879702,      1,
		-0.4481481223429, 0.2734851839796,    900,
		0, -26.75316106862,   4001,
		-0.4150453099584, 0.2298798624104,      1
	poly2_b 12, 1+2+4+64, gs_fill_pen, gs_back_pen,
		0.3505469906775, 0.9778764755544,      1,
		0, -1.717771323557,    900,
		0, 14.81853820856,   4001,
		-0.3505469680693, 0.9778764755544,      1,
		-0.2745544832082, 0.848312574122,    900,
		0, 119.2146181258,   4001,
		-0.3505469516839, 0.7187486630792,      1,
		1.130413776809E-008, -0.2045072821347,    900,
		0, -41.58209444127,   4001,
		0.3505469742921, 0.7187486630792,      1,
		0.2745545058164, 0.848312574122,    900,
		0, 119.2146181258,   4001
	del 2
return


!!!!!!!!!!!!!!!!!!!! Hotspots !!!!!!!!!!!!!!!!!!!!


1001:
	unID = gs_hotspotUnIdBegin + 20
	IF EditHotspots = 1 THEN

		HOTSPOT2 A/2,0,unID: unID=unID+1
		HOTSPOT2 A/2,A/2,unID: unID=unID+1

		! Make Length stretchable

		HOTSPOT2 0, A/2, unID, A, 1+256
		unID=unID+1
		HOTSPOT2 -0.001, A/2, unID, A, 3+128
		unID=unID+1
		HOTSPOT2 A, A/2, unID, A, 2
		unID=unID+1

		! Make Width stretchable

		HOTSPOT2 A/2, 0, unID, A, 1+256
		unID=unID+1
		HOTSPOT2 A/2, -0.001, unID, A, 3+128
		unID=unID+1
		HOTSPOT2 A/2, A, unID, A, 2
		unID=unID+1

	ELSE

		if NOT(bHideSingleHotspot) then HOTSPOT2 A/2,A,unID: unID=unID+1

	ENDIF

RETURN


1002:
	unID = gs_hotspotUnIdBegin + 20
	IF EditHotspots = 1 THEN

		HOTSPOT2 0,-A/2,unID :	unID=unID+1
		HOTSPOT2 0,0,unID :	unID=unID+1

		FOR k=1 to 4
			HOTSPOT2 0, 0, unID, temp_AA, 1+128, A:	unID=unID+1
			HOTSPOT2 -1, 0, unID, temp_AA, 3+128, A: unID=unID+1
			HOTSPOT2 temp_AA, 0, unID, temp_AA, 2, A: unID=unID+1
			ROT2 90
		NEXT k
		DEL 4
	ELSE

		if NOT(bHideSingleHotspot) then HOTSPOT2 A/2,A,unID: unID=unID+1

	ENDIF

RETURN


1003:
	unID = gs_hotspotUnIdBegin + 20
	IF EditHotspots = 1 THEN

		HOTSPOT2 A/2,0,unID: unID=unID+1
		HOTSPOT2 A/2,B/2,unID: unID=unID+1


		CALL "Resize_A_B_ZZYZX" Parameters ALL GS_HotspotUnIDBegin = gs_hotspotUnIdBegin+20000

	ELSE

		if NOT(bHideSingleHotspot) then HOTSPOT2 A/2,B,unID: unID=unID+1

	ENDIF

RETURN

1004:
	unID = gs_hotspotUnIdBegin + 20
	IF EditHotspots = 1 THEN

		HOTSPOT2 0,-B/2,unID: unID=unID+1
		HOTSPOT2 0,0,unID: unID=unID+1


		HOTSPOT2 0, -B/2, unID, temp_AA, 1+128, A:	unID=unID+1
		HOTSPOT2 -1, -B/2, unID, temp_AA, 3+128, A: unID=unID+1
		HOTSPOT2 temp_AA, -B/2, unID, temp_AA, 2, A: unID=unID+1

		HOTSPOT2 0, -B/2, unID, temp_AA, 1+128, A:	unID=unID+1
		HOTSPOT2 1, -B/2, unID, temp_AA, 3+128, A: unID=unID+1
		HOTSPOT2 -temp_AA, -B/2, unID, temp_AA, 2, A: unID=unID+1

		HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A:	unID=unID+1
		HOTSPOT2 -1, B/2, unID, temp_AA, 3+128, A: unID=unID+1
		HOTSPOT2 temp_AA, B/2, unID, temp_AA, 2, A: unID=unID+1

		HOTSPOT2 0, B/2, unID, temp_AA, 1+128, A:	unID=unID+1
		HOTSPOT2 1, B/2, unID, temp_AA, 3+128, A: unID=unID+1
		HOTSPOT2 -temp_AA, B/2, unID, temp_AA, 2, A: unID=unID+1


		HOTSPOT2 -A/2, 0, unID, temp_BB, 1+128, B:	unID=unID+1
		HOTSPOT2 -A/2, -1, unID, temp_BB, 3+128, B: unID=unID+1
		HOTSPOT2 -A/2, temp_BB, unID, temp_BB, 2, B: unID=unID+1

		HOTSPOT2 A/2, 0, unID, temp_BB, 1+128, B:	unID=unID+1
		HOTSPOT2 A/2, -1, unID, temp_BB, 3+128, B: unID=unID+1
		HOTSPOT2 A/2, temp_BB, unID, temp_BB, 2, B: unID=unID+1

		HOTSPOT2 -A/2, 0, unID, temp_BB, 1+128, B:	unID=unID+1
		HOTSPOT2 -A/2, 1, unID, temp_BB, 3+128, B: unID=unID+1
		HOTSPOT2 -A/2, -temp_BB, unID, temp_BB, 2, B: unID=unID+1

		HOTSPOT2 A/2, 0, unID, temp_BB, 1+128, B:	unID=unID+1
		HOTSPOT2 A/2, 1, unID, temp_BB, 3+128, B: unID=unID+1
		HOTSPOT2 A/2, -temp_BB, unID, temp_BB, 2, B: unID=unID+1

	ELSE

		if NOT(bHideSingleHotspot) then HOTSPOT2 A/2,B,unID: unID=unID+1

	ENDIF

RETURN


"minimalSpaceChair":
	if not(calledAsMacro) then
		DRAWINDEX 10
		reqBMinSpace = 0
		call "fa_minimalspace" parameters all MSType = MSType, unID = 3000,
								returned_parameters reqBMinSpace,
													MSFillType,
													MSFillPen,
													MSFillBackPen,
													MSContLineType,
													MSContLinePen

		if reqBMinSpace <> 0 and (MSFront > EPS or MSSide > EPS or MSRear > EPS)then
			FILL MSFillType
			PEN MSContLinePen
			line_type  MSContLineType

			if gs_symbolType_m = 1 or gs_symbolType_m = 2 or gs_symbolType_m = 11 then
				dim contours[5]
					contours[1] = (MSSide < EPS)
					contours[2] = (MSFront < EPS)
					contours[3] = (MSSide < EPS)
					contours[4] = ((MSRear < EPS) AND (B < A+EPS))
					contours[5] = (MSSide < EPS)
				add2 A/2, A/2
				for i = 1 to 4
					POLY2_B 5, 1+2+4+64, MSFillPen, MSFillBackPen,
							-A/2, 0, 0,
							0,    0, 900,
							0, 90, 4000+contours[i+1],
							-A/2, -A/2, contours[i],
							-A/2, 0, -1
					rot2 90
				next i
				del 5
				if B > A + EPS then
					POLY2_B 5, 1+2+4+64, MSFillPen, MSFillBackPen,
							0, 		A, 		(MSSide < EPS),
							0, 		B,  	(MSRear < EPS),
							A,		B,		(MSSide < EPS),
							A,		A,	0,
							0,		A,	-1
				endif
			endif
			if gs_SymbolType_m = 5 or gs_SymbolType_m = 6 then
				if MSSide > EPS or MSRear > EPS then
					POLY2_B 4, 1+2+4+64, MSFillPen, MSFillBackPen,
						0, 		0, 	(MSSide < EPS),
						0, 		B, 	(MSRear < EPS),
						0.05,	B,	0,
						0,		0,	-1

					POLY2_B 4, 1+2+4+64, MSFillPen, MSFillBackPen,
						A,		0,	0,
						A-0.05,	B,	(MSRear < EPS),
						A, 		B, 	(MSSide < EPS),
						A, 		0, 	-1
				endif
			endif
			if gs_SymbolType_m = 7 or gs_SymbolType_m = 8 then
				if MSSide > EPS or MSRear > EPS then
					add2 A/2, B-A/2
					rot2 180
					POLY2_B 5, 1+2+4+64, MSFillPen, MSFillBackPen,
							-A/2, 0, 0,
							0,    0, 900,
							0, 90, 4000+(MSRear < EPS),
							-A/2, -A/2, (MSSide < EPS),
							-A/2, 0, -1
					rot2 90
					POLY2_B 5, 1+2+4+64, MSFillPen, MSFillBackPen,
							-A/2, 0, 0,
							0,    0, 900,
							0, 90, 4000+(MSSide < EPS),
							-A/2, -A/2, (MSRear < EPS),
							-A/2, 0, -1
					del 3
				endif
			endif
		endif
		PEN gs_cont_pen
		FILL gs_fill_type
		line_type 1
		DRAWINDEX 30
	endif
return
]]>
</Script_2D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_1D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
EPS = 0.0001
!!!gs_SymbolType_Mask = 65535

dim idxSymbolTypes[16]
	idxSymbolTypes[ 1] = 1
	idxSymbolTypes[ 2] = 2
	idxSymbolTypes[ 3] = 11
	idxSymbolTypes[ 4] = 12
	idxSymbolTypes[ 5] = 3
	idxSymbolTypes[ 6] = 4
	idxSymbolTypes[ 7] = 9
	idxSymbolTypes[ 8] = 10
	idxSymbolTypes[ 9] = 5
	idxSymbolTypes[10] = 6
	idxSymbolTypes[11] = 7
	idxSymbolTypes[12] = 8
	idxSymbolTypes[13] = 13
	idxSymbolTypes[14] = 14
	idxSymbolTypes[15] = 15
	idxSymbolTypes[16] = 16

dim stSymbolTypesArray[]
dim iSymbolTypesArray[]

j = 1

for i=1 to vardim1(stSymbolTypes)
	if bittest(gs_SymbolType_Mask, i - 1) then
		idx = idxSymbolTypes[i]
		stSymbolTypesArray[j] = stSymbolTypes[idx]
		iSymbolTypesArray[j] = idx
		j = j + 1
	endif
next i
]]>
</Script_1D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_PR SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_PR>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_UI SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[

end	iSymbolTypesArray
]]>
</Script_UI>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_VL SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
if vardim1(stSymbolTypesArray) > 0 then
	values "gs_SymbolType" stSymbolTypesArray
	values "gs_SymbolType_m" iSymbolTypesArray
endif


if GLOB_MODPAR_NAME = "gs_SymbolType" then
	gs_SymbolType_m = 1
	for i = 2 to vardim1(stSymbolTypes)
		if gs_SymbolType = stSymbolTypes[i] then
			gs_SymbolType_m = i
			i = vardim1(stSymbolTypes)		! break
		endif
	next i
	parameters gs_SymbolType_m = gs_SymbolType_m
else
	if gs_SymbolType_m > 0 & gs_SymbolType_m <= vardim1(stSymbolTypes) then
		parameters gs_SymbolType = stSymbolTypes[gs_SymbolType_m]
	endif
endif


if gs_OrigoCentered then
	if GLOB_MODPAR_NAME = "temp_AA" then
		parameters A=temp_AA*2
	else
		parameters temp_AA=A/2
	endif
	if GLOB_MODPAR_NAME = "temp_BB" then
		parameters B=temp_BB*2
	else
		parameters temp_BB=B/2
	endif
endif
]]>
</Script_VL>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Comment SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Comment>

<ParamSection SectVersion="26" SectionFlags="0" SubIdent="0">
	<ParamSectHeader>
		<Version>26</Version>
		<AutoHotspots>false</AutoHotspots>
		<StatBits>
			<STBit_FixSize/>
		</StatBits>
		<WDLeftFrame>0</WDLeftFrame>
		<WDRightFrame>0</WDRightFrame>
		<WDTopFrame>0</WDTopFrame>
		<WDBotFrame>0</WDBotFrame>
		<LayFlags>65535</LayFlags>
		<WDMirrorThickness>0</WDMirrorThickness>
		<WDWallInset>0</WDWallInset>
		<SymbolColor>0</SymbolColor>
	</ParamSectHeader>
	<Parameters>
		<Length Name="A">
			<Description><![CDATA["Dimension 1"]]></Description>
			<Fix/>
			<Value>0.4</Value>
		</Length>
		<Length Name="B">
			<Description><![CDATA["Dimension 2"]]></Description>
			<Fix/>
			<Value>0.4</Value>
		</Length>
		<Length Name="ZZYZX">
			<Description><![CDATA["Height"]]></Description>
			<Fix/>
			<Value>1</Value>
		</Length>
		<Length Name="temp_AA">
			<Description><![CDATA["Dimension 1/2"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.2</Value>
		</Length>
		<Length Name="temp_BB">
			<Description><![CDATA["Dimension 2/2"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.2</Value>
		</Length>
		<Boolean Name="AC_show2DHotspotsIn3D">
			<Description><![CDATA["Show 2D Hotspots in 3D"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<Length Name="ac_bottomlevel">
			<Description><![CDATA["Bottom Level"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="ac_toplevel">
			<Description><![CDATA["Top Level"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Length>
		<Boolean Name="gs_OrigoCentered">
			<Description><![CDATA["Origin Centered"]]></Description>
			<Value>1</Value>
		</Boolean>
		<String Name="gs_SymbolType">
			<Description><![CDATA["Symbol Type"]]></Description>
			<Value><![CDATA["Окружность"]]></Value>
		</String>
		<Integer Name="gs_SymbolType_Mask">
			<Description><![CDATA["Symbol Type Enable Mask"]]></Description>
			<Value>4019</Value>
		</Integer>
		<Integer Name="gs_SymbolType_m">
			<Description><![CDATA["Symbol Type"]]></Description>
			<Value>4</Value>
		</Integer>
		<Boolean Name="EditHotspots">
			<Description><![CDATA["Editable Hotspots"]]></Description>
			<Value>1</Value>
		</Boolean>
		<PenColor Name="gs_cont_pen">
			<Description><![CDATA["Contour Pen"]]></Description>
			<Value>4</Value>
		</PenColor>
		<FillPattern Name="gs_fill_type">
			<Description><![CDATA["Fill Type"]]></Description>
			<Value>65</Value>
		</FillPattern>
		<PenColor Name="gs_fill_pen">
			<Description><![CDATA["Fill Pen"]]></Description>
			<Value>19</Value>
		</PenColor>
		<PenColor Name="gs_back_pen">
			<Description><![CDATA["Fill Background Pen"]]></Description>
			<Value>19</Value>
		</PenColor>

		<!-- gs_min_space: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_min_space">
			<Description><![CDATA["Minimal Space"]]></Description>
		</Title>
		<Length Name="MSFront">
			<Description><![CDATA["Minimal Space in Front"]]></Description>
			<Value>0.03</Value>
		</Length>
		<Length Name="MSSide">
			<Description><![CDATA["Minimal Space on Sides"]]></Description>
			<Value>0.15</Value>
		</Length>
		<Length Name="MSRear">
			<Description><![CDATA["Minimal Space Behind"]]></Description>
			<Value>0.3</Value>
		</Length>
		<Integer Name="MSType">
			<Description><![CDATA["MinSpace Type"]]></Description>
			<Value>3</Value>
		</Integer>
		<Integer Name="gs_hotspotUnIdBegin">
			<Description><![CDATA[""]]></Description>
			<Value>10000</Value>
		</Integer>
		<String Name="stSymbolTypes">
			<Description><![CDATA["Symbol Type"]]></Description>
			<ArrayValues FirstDimension="16" SecondDimension="0">
				<AVal Row="1"><![CDATA["Окружность"]]></AVal>
				<AVal Row="2"><![CDATA["Круглый со Спинкой"]]></AVal>
				<AVal Row="3"><![CDATA["Прямоугольный"]]></AVal>
				<AVal Row="4"><![CDATA["Прямоугольный со Спинкой 1"]]></AVal>
				<AVal Row="5"><![CDATA["Трапециевидный"]]></AVal>
				<AVal Row="6"><![CDATA["Трапециевидный со Спинкой"]]></AVal>
				<AVal Row="7"><![CDATA["Изогнутая"]]></AVal>
				<AVal Row="8"><![CDATA["Криволинейный со Спинкой"]]></AVal>
				<AVal Row="9"><![CDATA["Прямоугольный со Спинкой 3"]]></AVal>
				<AVal Row="10"><![CDATA["Прямоугольный со Спинкой 2"]]></AVal>
				<AVal Row="11"><![CDATA["Круглый со Спинкой 2"]]></AVal>
				<AVal Row="12"><![CDATA["Круглый со Спинкой 3"]]></AVal>
				<AVal Row="13"><![CDATA["Криволинейный со Спинкой 2"]]></AVal>
				<AVal Row="14"><![CDATA["Криволинейный со Спинкой 3"]]></AVal>
				<AVal Row="15"><![CDATA["Детальный 1"]]></AVal>
				<AVal Row="16"><![CDATA["Детальный 2"]]></AVal>
			</ArrayValues>
		</String>
		<Boolean Name="calledAsMacro">
			<Description><![CDATA["called from layouts"]]></Description>
			<Value>0</Value>
		</Boolean>
		<Boolean Name="bHideSingleHotspot">
			<Description><![CDATA["Hide Hotspot at Backrest (Always zero)"]]></Description>
			<Value>0</Value>
		</Boolean>
	</Parameters>
</ParamSection>

<Picture MIME="image/png" SectVersion="19" SectionFlags="0" SubIdent="0" length_in_bytes="130" platform="Win">
	<![CDATA[
	696D6167652F706E670089504E470D0A1A0A0000000D49484452000000800000
	00800103000000F9F0F38800000006504C5445FFFFFF00000055C2D37E000000
	2D4944415448C7636018053400FCFFE1E00376011E8462033A0A0C5A878D0A8C
	0A8C0A8C0A0CEEC271145019000044EE54F756BEADBD0000000049454E44AE42
	6082
	]]>
</Picture>

<Copyright SectVersion="1" SectionFlags="0" SubIdent="0">
	<Author>GRAPHISOFT</Author>
	<License>
		<Type>CC BY-ND</Type>
		<Version>3.0</Version>
	</License>
</Copyright>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_FWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_FWM>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_BWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_BWM>

<Keywords SectVersion="1" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Keywords>

</Symbol>
