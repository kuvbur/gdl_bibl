<?xml version="1.0" encoding="UTF-8"?>
<Symbol IsArchivable="true" IsPlaceable="false" MainGUID="C3FB387D-CB3E-4713-9BA9-0876E02B5378" MigrationValue="Normal" Owner="1196638531" Signature="1297307981" Version="38">
<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_3D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
! ==============================================================================
! Definitions
! ==============================================================================

EPS=0.0001

reducedDoorWidth	= doorWidth -0.001*(1-bSliding)
reducedDoorHeight	= doorHeight -0.001*(1-bSliding)
frameHillHeight		= 0.01
mindistSmall		= 0.01
mindistLarge		= 0.03
arcSegmentHeight	= 0.08
thickness			= 0.02
bSmallSize			= 0
minDrawerKnob		= 0.05

if not(bKnob) then knob_d=0
if gs_knob_type_m=1 then knob_d=0.025
if gs_knob_type_m=2 then knob_d=0.025
if gs_knob_type_m=3 then knob_d=0.03
if gs_knob_type_m=4 then knob_d=0.024
if gs_knob_type_m=5 then knob_d=0.025
if gs_knob_type_m=6 then knob_d=0.025
if gs_knob_type_m=7 then knob_d=0.03
if gs_knob_type_m=8 then knob_d=0.03

if iDoorStyle > 14 or (iDoorStyle = 13 and bDrawer) and gs_detlevel_3D_m=2 then
	knobOffsetZ = doorThickness/4
else
	knobOffsetZ = 0
endif


! ==============================================================================
! Style Dimensions
! ==============================================================================

if iDoorStyle=18 then		! -> Rajzi Style 11
	minPanelHeight		= 0.08
	middlePanelHeight	= 0.45

	if reducedDoorHeight < doorFrameWidth*4+middlePanelHeight+2*minPanelHeight then
		middlePanelHeight = reducedDoorHeight-doorFrameWidth*4-2*minPanelHeight
		if middlePanelHeight < minPanelHeight then middlePanelHeight = minPanelHeight
	endif
endif

if iDoorStyle=17 then		! -> Rajzi Style 10
	minPanelHeight		= 0.08
	topPanelHeight		= 0.45
	decorPanelHeight	= 0.12

	if reducedDoorHeight < doorFrameWidth*4+topPanelHeight+decorPanelHeight+minPanelHeight then
		topPanelHeight = reducedDoorHeight-doorFrameWidth*4-decorPanelHeight-minPanelHeight
		if topPanelHeight < minPanelHeight then topPanelHeight = minPanelHeight
	endif
	if reducedDoorHeight < doorFrameWidth*4+decorPanelHeight+2*minPanelHeight then
		decorPanelHeight = reducedDoorHeight-doorFrameWidth*4-2*minPanelHeight
		if decorPanelHeight < minPanelHeight then decorPanelHeight = minPanelHeight
	endif
endif

if iDoorStyle=16 then		! -> Rajzi Style 9
	minPanelWidth		= 0.08
	leftPanelWidth		= 0.25
	middlePanelWidth	= 0.12

	if reducedDoorWidth < doorFrameWidth*4+leftPanelWidth+middlePanelWidth+minPanelWidth then
		leftPanelWidth = reducedDoorWidth-doorFrameWidth*4-middlePanelWidth-minPanelWidth
		if leftPanelWidth < minPanelWidth then leftPanelWidth = minPanelWidth
	endif
	if reducedDoorWidth < doorFrameWidth*4+middlePanelWidth+2*minPanelWidth then
		middlePanelWidth = reducedDoorWidth-doorFrameWidth*4-2*minPanelWidth
		if middlePanelWidth < minPanelWidth then middlePanelWidth = minPanelWidth
	endif
endif


! =============================================================================
! Default Settings
! ==============================================================================
if abs(cabdoortyp_unID-1)<EPS then
	unID=200
else
	unID=cabdoortyp_unID
endif

if gs_shadow=0 then SHADOW OFF
pen gs_cont_pen

if reducedDoorHeight < 2 * doorFrameWidth + 0.1 then
	if iDoorStyle = 7 or iDoorStyle = 5 then iDoorStyle = 12		! Style 10, 8 --> 5
	if iDoorStyle = 6 or iDoorStyle = 4 then iDoorStyle = 11		! Style 9, 7 --> 4
endif
if reducedDoorHeight < 2 * doorFrameWidth then
	if iDoorStyle <> 99 and iDoorStyle <> 6 then iDoorStyle = 1
endif

if bMirrored then addx doorWidth
if bDrawer then bAngleEdit = 0
if iDoorStyle=8 then
	gs_full_edit = 0
	gs_knob_type_m = 0
endif


! ==============================================================================
! 3D Hotspot Editing
! ==============================================================================
if gs_full_edit then
	if bMirrored then mulx -1

	!*** knob placing
	if bKnob then

		if (gs_detlevel_3D_m=2 or GLOB_CONTEXT>20 and GLOB_CONTEXT<40) and bKnob then !!! HTPS in 3D feedback and in Detailed Model
			rotz -doorAngle3D

			if not(bUpperKnob) then
				if bDrawer then
					if nKnobDrawers = 1 then
						hotspot doorWidth/2+xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2, 				 unID, zDrawerKnobPos,1+128 : unID=unID+1
						hotspot doorWidth/2+xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, zDrawerKnobPos,2 : unID=unID+1
						hotspot doorWidth/2+xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2-1, 			 unID, zDrawerKnobPos,3 : unID=unID+1

						hotspot doorWidth/2,			    -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,1+128 : unID=unID+1
						hotspot doorWidth/2+xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,2 : unID=unID+1
						hotspot doorWidth/2-1,			    -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,3 : unID=unID+1
					else
						hotspot xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2, 				 unID, zDrawerKnobPos,1+128 : unID=unID+1
						hotspot xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, zDrawerKnobPos,2 : unID=unID+1
						hotspot xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2-1, 			 unID, zDrawerKnobPos,3 : unID=unID+1

						hotspot 0,			    -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,1+128 : unID=unID+1
						hotspot xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,2 : unID=unID+1
						hotspot -1,			    -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,3 : unID=unID+1

						hotspot doorWidth-xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2, 			   unID, zDrawerKnobPos,1+128 : unID=unID+1
						hotspot doorWidth-xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, zDrawerKnobPos,2 : unID=unID+1
						hotspot doorWidth-xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2-1, 			   unID, zDrawerKnobPos,3 : unID=unID+1

						hotspot doorWidth,			      -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,1+128 : unID=unID+1
						hotspot doorWidth-xDrawerKnobPos, -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,2 : unID=unID+1
						hotspot doorWidth+1,			  -doorThickness+knobOffsetZ, doorHeight/2+zDrawerKnobPos, unID, xDrawerKnobPos,3 : unID=unID+1
					endif
				else
					addz -toe_h+.09
					hotspot reducedDoorWidth-knob_pl_x, -doorThickness+knobOffsetZ, 0, unID, knob_pl_y,1+128 : unID=unID+1
					hotspot reducedDoorWidth-knob_pl_x, -doorThickness+knobOffsetZ, knob_pl_y, unID, knob_pl_y,2 : unID=unID+1
					hotspot reducedDoorWidth-knob_pl_x, -doorThickness+knobOffsetZ, -1, unID, knob_pl_y,3 : unID=unID+1

					hotspot reducedDoorWidth,           -doorThickness+knobOffsetZ, knob_pl_y, unID, knob_pl_x,1+128 : unID=unID+1
					hotspot reducedDoorWidth-knob_pl_x, -doorThickness+knobOffsetZ, knob_pl_y, unID, knob_pl_x,2 : unID=unID+1
					hotspot reducedDoorWidth+1,         -doorThickness+knobOffsetZ, knob_pl_y, unID, knob_pl_x,3 : unID=unID+1
					del 1
				endif
			else
				hotspot reducedDoorWidth-knob_pl_x_up, -doorThickness+knobOffsetZ, 0, 			unID, knob_pl_y_up,1+128 : unID=unID+1
				hotspot reducedDoorWidth-knob_pl_x_up, -doorThickness+knobOffsetZ, knob_pl_y_up,unID, knob_pl_y_up,2 : unID=unID+1
				hotspot reducedDoorWidth-knob_pl_x_up, -doorThickness+knobOffsetZ, -1, 			unID, knob_pl_y_up,3 : unID=unID+1

				hotspot reducedDoorWidth,			  -doorThickness+knobOffsetZ, knob_pl_y_up, unID, knob_pl_x_up,1+128 : unID=unID+1
				hotspot reducedDoorWidth-knob_pl_x_up, -doorThickness+knobOffsetZ, knob_pl_y_up, unID, knob_pl_x_up,2 : unID=unID+1
				hotspot reducedDoorWidth+1,			  -doorThickness+knobOffsetZ, -1,			 unID, knob_pl_x_up,3 : unID=unID+1
			endif

			del 1
		endif
	endif

	if not(bUpperKnob) and bAngleEdit then
		!! Opening !!
		addz -toe_h+.09
		hotspot 0, 0, knob_pl_y, unID, doorAngle3D, 6+512 : unID=unID+1
		hotspot 0, 0, knob_pl_y, unID, doorAngle3D, 7		: unID=unID+1
		hotspot reducedDoorWidth-knob_pl_x, 0, knob_pl_y, unID, doorAngle3D,4+128 : unID=unID+1
		rotz -doorAngle3D
		hotspot reducedDoorWidth-knob_pl_x,-doorThickness-knob_d, knob_pl_y,unID,doorAngle3D,5 : unID=unID+1
		del 2
	endif

	!*** End of knob placing

	if bMirrored then del 1
endif


! ==============================================================================
! Drawing Door
! ==============================================================================
if reducedDoorWidth<=0 or reducedDoorHeight<=0 then
	end unID
endif

if not(bMirrored) then
	rotz -doorAngle3D
else
	rotz doorAngle3D
	addx -reducedDoorWidth
endif

if reducedDoorWidth<0.02 or reducedDoorHeight<0.02 then
	gs_detlevel_3D_m = 1
endif
if GLOB_CONTEXT>20 and GLOB_CONTEXT<40 then
	gs_detlevel_3D_m = 1
endif

if gs_detlevel_3D_m=1 or iDoorStyle=0 then
	gosub "Door_Style_Simple"
else
	bDrawStandardKnob = 1
	tubeMask = 1+2!+16+32
	add (doorWidth-reducedDoorWidth)/2, 0, (doorHeight-reducedDoorHeight)/2

	if not(bBevel) & not(iDoorStyle=10 or iDoorStyle=99) then
		lin_ 0, -doorThickness, 0, 0, -doorThickness, reducedDoorHeight
		lin_ reducedDoorWidth, -doorThickness, 0, reducedDoorWidth, -doorThickness, reducedDoorHeight
	endif

	if iDoorStyle=1 then gosub "Door_Style_01"
	if iDoorStyle=2 then gosub "Door_Style_02"
	if iDoorStyle=3 then gosub "Door_Style_03"
	if iDoorStyle=4 then gosub "Door_Style_04"
	if iDoorStyle=5 then gosub "Door_Style_05"
	if iDoorStyle=6 then gosub "Door_Style_06"
	if iDoorStyle=7 then gosub "Door_Style_07"
	if iDoorStyle=8 then gosub "Door_Style_08"		!! door type only for furniture
	if iDoorStyle=9 then gosub "Door_Style_09"
	if iDoorStyle=11 then gosub "Door_Style_11"
	if iDoorStyle=12 then gosub "Door_Style_12"
	if iDoorStyle=13 then gosub "Door_Style_13"
	if iDoorStyle=14 then gosub "Door_Style_14"
	if iDoorStyle=15 then gosub "Door_Style_15"
	if iDoorStyle=16 then gosub "Door_Style_16"
	if iDoorStyle=17 then gosub "Door_Style_17"
	if iDoorStyle=18 then gosub "Door_Style_18"
	if iDoorStyle=10 or iDoorStyle=99 then gosub "DoorCustomPanel"

	del 1

	if bDrawer then gosub "Drawer"

	if bDrawStandardKnob then
		if bMirrored then
			mulx -1
			addx -reducedDoorWidth
		endif
		if bKnob then
			if not(bUpperKnob) then
				addz -toe_h+0.09
				if bDrawer then
					gs_knob_p_m = iDrawerKnobPos
					_bVarLengthKnob = (gs_knob_type_m > 3 and gs_knob_type_m <> 9)

					if nKnobDrawers = 1 then
						add doorWidth/2+xDrawerKnobPos+knob_size/2*(iDrawerKnobPos = 1)*_bVarLengthKnob,-doorThickness,doorHeight/2+zDrawerKnobPos+knob_size/2*(iDrawerKnobPos = 0)*_bVarLengthKnob
						gosub "Knobs"
						del 1
					else
						add xDrawerKnobPos+knob_size/2*(iDrawerKnobPos = 1)*_bVarLengthKnob,-doorThickness,doorHeight/2+zDrawerKnobPos+knob_size/2*(iDrawerKnobPos = 0)*_bVarLengthKnob
						gosub "Knobs"
						del 1

						add doorWidth-xDrawerKnobPos+knob_size/2*(iDrawerKnobPos = 1)*_bVarLengthKnob,-doorThickness,doorHeight/2+zDrawerKnobPos+knob_size/2*(iDrawerKnobPos = 0)*_bVarLengthKnob
						gosub "Knobs"
						del 1
					endif
				else
					add reducedDoorWidth-knob_pl_x,-doorThickness,knob_pl_y
					gosub "Knobs"
					del 1
				endif
				del 1
			else
				add reducedDoorWidth-knob_pl_x_up,-doorThickness,knob_pl_y_up
				gosub "Knobs"
				del 1
			endif
		endif
		if bMirrored then del 2
	endif
endif

del 1+bMirrored*2

! ==============================================================================
! END
! ==============================================================================

cabdoortyp_unID = unID
END cabdoortyp_unID


! ==============================================================================
! SubRoutines
! ==============================================================================

! ------------------------------------------------------------------------------
! Type_Simple/Simple
! ------------------------------------------------------------------------------
"Door_Style_Simple":

	material matDoorFrame
	if iDoorStyle=13 | iDoorStyle=14 | iDoorStyle=15 | iDoorStyle=16 | iDoorStyle=17 | iDoorStyle=18 then		!! New door type default material
		material matDoorDecor1
	endif

	if iDoorStyle<>3 and iDoorStyle<>5 and iDoorStyle<>7 then
		!***** door panel without glass
		addy -doorThickness
		block reducedDoorWidth, doorThickness, reducedDoorHeight
		del 1
		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
	else
		!***** door panel with glass
		lin_ 0, -doorThickness, 0, 0, -doorThickness, reducedDoorHeight
		lin_ reducedDoorWidth, -doorThickness, 0, reducedDoorWidth, -doorThickness, reducedDoorHeight

		addx reducedDoorWidth/2
		rotx 90
		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,0,15,
					reducedDoorWidth/2-doorFrameWidth,0,10,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
					reducedDoorWidth/2-doorFrameWidth,0,15
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,10,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
					-reducedDoorWidth/2+doorFrameWidth,0,15
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		material matDoorGlass

		prism_ 5,doorThickness,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,15,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,15,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1

		del 2

	endif

return


! ------------------------------------------------------------------------------
! Door_Style_1
! ------------------------------------------------------------------------------
"Door_Style_01":

	if reducedDoorWidth<mindistLarge or reducedDoorHeight<mindistLarge then  goto "Door_Style_Simple"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	material matDoorDecor1
	if bBevel then
		fprism_ matDoorFrame, matDoorFrame, matDoorFrame, matDoorFrame,
			4,doorThickness,45,frameHillHeight,
			-reducedDoorWidth/2,0,15,
			reducedDoorWidth/2,0,15,
			reducedDoorWidth/2,reducedDoorHeight,15,
			-reducedDoorWidth/2,reducedDoorHeight,15
	else
		prism_ 	4,doorThickness,
				-reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,reducedDoorHeight,15,
				-reducedDoorWidth/2,reducedDoorHeight,15
	endif

	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_2
! ------------------------------------------------------------------------------
"Door_Style_02":

	if reducedDoorWidth<doorFrameWidth*2+mindistLarge or reducedDoorHeight<doorFrameWidth*2+mindistLarge then  goto "Door_Style_01"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	!! Frame
	backMask = 0
	material matDoorFrame
	tempBevel = bBevel
	gosub "Frame_45_Vertical"
	gosub "Frame_45_Horizontal"

	!! Panel
	tempBevel = 1
	tempHeight = reducedDoorHeight-2*doorFrameWidth
	matInsert = matDoorDecor1
	addy doorFrameWidth
	gosub "Door_Insert_Panel"
	del 1

	del 2
return


! ------------------------------------------------------------------------------
! Door_Style_3
! ------------------------------------------------------------------------------
"Door_Style_03":

	if reducedDoorWidth < doorFrameWidth*2+mindistSmall+nVerticalMullion*mullionWidth or reducedDoorHeight<doorFrameWidth*2+mindistSmall+nHorizontalMullion*mullionWidth then goto "Door_Style_01"        ! simple door

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** Door Panel
	addx reducedDoorWidth/2
	rotx 90

	if bBevel then
		material matDoorFrame
		group "frame_typ2"					!! Door Frame
		base
		tube 6, 4, tubeMask,
			0, 0, 0,
			doorFrameWidth, 0, 0,
			doorFrameWidth, doorThickness-frameHillHeight, 0,
			doorFrameWidth-frameHillHeight, doorThickness, 0,
			frameHillHeight, doorThickness, 0,
			0, doorThickness-frameHillHeight, 0,
			-reducedDoorWidth/2, 1, 0, 0,
			-reducedDoorWidth/2, 0, 0, 0,
			 reducedDoorWidth/2, 0, 0, 0,
			 reducedDoorWidth/2, 1, 0, 0

		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			tube 6, 4, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				reducedDoorWidth/2-1, 0, 0, 0,
				reducedDoorWidth/2, 0, 0, 0,
				reducedDoorWidth/2, reducedDoorHeight, 0, 0,
				reducedDoorWidth/2-1, reducedDoorHeight, 0, 0

		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			tube 6, 4, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth-1, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
				 reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
				 reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-doorFrameWidth-1, 0, 0

		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			tube 6, 4, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2+doorFrameWidth+1, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth+1, reducedDoorHeight-doorFrameWidth, 0, 0

		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
		endgroup
		placegroup "frame_typ2"

		group "grill_typ2"
			gosub "Grill"
		endgroup

		grills_typ1=subgroup("grill_typ2","frame_typ2")
		placegroup grills_typ1

		killgroup "frame_typ2"
		killgroup "grill_typ2"
		killgroup grills_typ1
	else
		group "frame_typ1"					!! Door Frame

		material matDoorFrame

		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,0,10,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	4,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,10,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

		endgroup

		placegroup "frame_typ1"

		group "grill_typ1"
			gosub "Grill"
		endgroup

		grills_typ1=subgroup("grill_typ1","frame_typ1")
		placegroup grills_typ1

		killgroup "frame_typ1"
		killgroup "grill_typ1"
		killgroup grills_typ1

		body -1
	endif

	addz doorThickness-frameHillHeight
	material matDoorGlass
	poly_ 4,						!! Window Glass
		-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,1,
		reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,1,
		reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,1,
		-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,1
	body -1
	del 1
	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_4
! ------------------------------------------------------------------------------
"Door_Style_04":

	seg_alfa=atn(arcSegmentHeight/(reducedDoorWidth/2-doorFrameWidth))
	seg_beta=90-2*seg_alfa
	segh2=(reducedDoorWidth/2-doorFrameWidth)*tan(seg_beta)
	seg_r=arcSegmentHeight+segh2					!! radius of segment
	!!seg_gamma=180-2*(seg_alfa+seg_beta)
	seg_gamma=4*seg_alfa

	if reducedDoorWidth<doorFrameWidth*2+mindistLarge or reducedDoorHeight<doorFrameWidth*2+mindistLarge	then  goto "Door_Style_01"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	material matDoorFrame

	if bBevel then
		fprism_ matDoorFrame, matDoorFrame, matDoorFrame, matDoorFrame,
				12,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,reducedDoorHeight,15,
				-reducedDoorWidth/2,reducedDoorHeight,15,
				-reducedDoorWidth/2,0,-1,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				0,reducedDoorHeight-doorFrameWidth-seg_r,900,
				0,seg_gamma,4000,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1

		fprism_ matDoorDecor1, matDoorFrame, matDoorFrame, matDoorDecor1,
				6,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				0,reducedDoorHeight-doorFrameWidth-seg_r,912,
				0,seg_gamma,4012,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12
	else
		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,0,13,
					reducedDoorWidth/2-doorFrameWidth,0,8,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
					reducedDoorWidth/2-doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			resol 36
			prism_	6,doorThickness,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,!1
					0,reducedDoorHeight-doorFrameWidth-seg_r,900,
					0,seg_gamma,4000,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,10,!2
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,13
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
					-reducedDoorWidth/2+doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

		base
			fprism_ matDoorDecor1, matDoorFrame, matDoorFrame, matDoorDecor1,
				6,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				0,reducedDoorHeight-doorFrameWidth-seg_r,912,
				0,seg_gamma,4012,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
	endif
	material matDoorFrame
	base
	prism_ 6,doorThickness-frameHillHeight,
			-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
			reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
			reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
			0,reducedDoorHeight-doorFrameWidth-seg_r,900,
			0,seg_gamma,4000,
			-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12
	vert 0, 0, 0
	vert 0, 1, 0
	vert -1, 0, 0
	vert 0, 0, -1

	coor 2, -1, -2, -3, -4
	body 1

	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_5
! ------------------------------------------------------------------------------
"Door_Style_05":

	seg_alfa=atn(arcSegmentHeight/(reducedDoorWidth/2-doorFrameWidth))
	seg_beta=90-2*seg_alfa
	segh2=(reducedDoorWidth/2-doorFrameWidth)*tan(seg_beta)
	seg_r=arcSegmentHeight+segh2		!! radius of segment
	seg_gamma=180-2*(seg_alfa+seg_beta)

	if reducedDoorWidth<doorFrameWidth*2+mindistSmall+nVerticalMullion*mullionWidth or reducedDoorHeight<doorFrameWidth*2+mindistSmall+nHorizontalMullion*mullionWidth then goto "Door_Style_01"

	addx reducedDoorWidth/2
	rotx 90

	material matDoorFrame

	if bBevel then
		group "frame_typ6"					!! Door Frame

		fprism_ matDoorFrame, matDoorFrame, matDoorFrame, matDoorFrame,
				12,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,reducedDoorHeight,15,
				-reducedDoorWidth/2,reducedDoorHeight,15,
				-reducedDoorWidth/2,0,-1,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,13,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,13,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,13,
				0,reducedDoorHeight-doorFrameWidth-seg_r,900,
				0,2*seg_gamma,4000,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,13,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
		endgroup
		placegroup "frame_typ6"

		group "grill_typ6"
			gosub "Grill"
		endgroup

		grills_typ5=subgroup("grill_typ6","frame_typ6")
		placegroup grills_typ5

		killgroup "frame_typ6"
		killgroup "grill_typ6"
		killgroup grills_typ5
	else
		group "frame_typ5"					!! Door Frame
		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,0,13,
					reducedDoorWidth/2-doorFrameWidth,0,8,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,15,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
					reducedDoorWidth/2-doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			resol 36
			prism_	6,doorThickness,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,15,!1
					0,reducedDoorHeight-doorFrameWidth-seg_r,900,
					0,2*seg_gamma,4000,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,10,!2
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,13
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
					-reducedDoorWidth/2+doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

		endgroup
		placegroup "frame_typ5"

		group "grill_typ5"
			gosub "Grill"
		endgroup

		grills_typ5=subgroup("grill_typ5","frame_typ5")
		placegroup grills_typ5

		killgroup "frame_typ5"
		killgroup "grill_typ5"
		killgroup grills_typ5

		body -1
	endif

	addz doorThickness-frameHillHeight
	material matDoorGlass
	poly_ 7,						!! Window Glass
		-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,1,
		reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,1,
		reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,1,
		0,reducedDoorHeight-doorFrameWidth-seg_r,900,
		0,2*seg_gamma,4000,
		-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,1,
		-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
	body -1
	del 1
	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_6
! ------------------------------------------------------------------------------
"Door_Style_06":

	seg_alfa=atn((reducedDoorWidth/2-doorFrameWidth)/arcSegmentHeight)
	segh2=sqr((arcSegmentHeight*arcSegmentHeight)+(reducedDoorWidth/2-doorFrameWidth)*(reducedDoorWidth/2-doorFrameWidth))
	seg_r=(segh2/cos(seg_alfa))/4
	seg_beta=90-seg_alfa

	if reducedDoorWidth<doorFrameWidth*2+mindistLarge or reducedDoorHeight<doorFrameWidth*2+mindistLarge 				then  goto "Door_Style_01"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	!! 1.center point of radius of segment: reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r
	!! 2.center point of radius of segment: 0, reducedDoorHeight-doorFrameWidth-seg_r
	!! 3.center point of radius of segment: -reducedDoorWidth/2+doorFrameWidth, -reducedDoorHeight+doorFrameWidth-arcSegmentHeight+seg_r

	!! Frame
	material matDoorFrame

	if bBevel then
		fprism_ matDoorFrame, matDoorFrame, matDoorFrame, matDoorFrame,
				16,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,0,15,
				reducedDoorWidth/2,reducedDoorHeight,15,
				-reducedDoorWidth/2,reducedDoorHeight,15,
				-reducedDoorWidth/2,0,-1,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 1.centerpoint
				0,2*seg_beta-0.5,4012,							!! 0.01 correction
				0, reducedDoorHeight-doorFrameWidth-seg_r, 912,					!! 2.centerpoint
				0,-(4*seg_beta),4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 3.centerpoint
				0,2*seg_beta-0.5,4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
		gosub "textureHorizontal"

		!! Middle Panel
		fprism_ matDoorDecor1, matDoorFrame, matDoorFrame, matDoorDecor1,
				11,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 1.centerpoint
				0,2*seg_beta-0.5,4012,							!! 0.01 correction
				0, reducedDoorHeight-doorFrameWidth-seg_r, 912,					!! 2.centerpoint
				0,-(4*seg_beta),4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 3.centerpoint
				0,2*seg_beta-0.5,4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

		material matDoorFrame
		!! Back Panel
		prism_ 13,doorThickness-frameHillHeight,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,900,		!! 1.centerpoint
				0,2*seg_beta,4000,
				-(reducedDoorWidth/2-doorFrameWidth)/2,reducedDoorHeight-doorFrameWidth-arcSegmentHeight/2,12,
				0, reducedDoorHeight-doorFrameWidth-seg_r, 900,				!! 2.centerpoint
				0,-(4*seg_beta),4000,
				(reducedDoorWidth/2-doorFrameWidth)/2,reducedDoorHeight-doorFrameWidth-arcSegmentHeight/2,12,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,900,		!! 3.centerpoint
				0,2*seg_beta,4000,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
	else
		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,0,13,
					reducedDoorWidth/2-doorFrameWidth,0,8,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
					reducedDoorWidth/2-doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			resol 36

			prism_	10,doorThickness,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,	!1
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 1.centerpoint
				0,2*seg_beta-0.5,4012,							!! 0.01 correction
				0, reducedDoorHeight-doorFrameWidth-seg_r, 912,					!! 2.centerpoint
				0,-(4*seg_beta),4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 3.centerpoint
				0,2*seg_beta-0.5,4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,10,	!2
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,13,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
					-reducedDoorWidth/2+doorFrameWidth,0,13

		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

		!! Middle Panel
		base
			fprism_ matDoorDecor1, matDoorFrame, matDoorFrame, matDoorDecor1,
				11,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 1.centerpoint
				0,2*seg_beta-0.5,4012,							!! 0.01 correction
				0, reducedDoorHeight-doorFrameWidth-seg_r, 912,					!! 2.centerpoint
				0,-(4*seg_beta),4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,912,		!! 3.centerpoint
				0,2*seg_beta-0.5,4012,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
				reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
				-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
		if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
	endif

	material matDoorFrame
	!! Back Panel
	base
		prism_ 13,doorThickness-frameHillHeight,
			-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,12,
			-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
			-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,900,		!! 1.centerpoint
			0,2*seg_beta,4000,
			-(reducedDoorWidth/2-doorFrameWidth)/2,reducedDoorHeight-doorFrameWidth-arcSegmentHeight/2,12,
			0, reducedDoorHeight-doorFrameWidth-seg_r, 900,				!! 2.centerpoint
			0,-(4*seg_beta),4000,
			(reducedDoorWidth/2-doorFrameWidth)/2,reducedDoorHeight-doorFrameWidth-arcSegmentHeight/2,12,
			reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,900,		!! 3.centerpoint
			0,2*seg_beta,4000,
			reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,12,
			reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
			-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
	del 2

return


! ------------------------------------------------------------------------------
!  Door_Style_7
! ------------------------------------------------------------------------------
"Door_Style_07":

	seg_alfa=atn((reducedDoorWidth/2-doorFrameWidth)/arcSegmentHeight)
	segh2=sqr((arcSegmentHeight*arcSegmentHeight)+(reducedDoorWidth/2-doorFrameWidth)*(reducedDoorWidth/2-doorFrameWidth))
	seg_r=(segh2/cos(seg_alfa))/4
	seg_beta=90-seg_alfa


	if reducedDoorWidth<doorFrameWidth*2+mindistSmall+nVerticalMullion*mullionWidth or reducedDoorHeight<doorFrameWidth*2+mindistSmall+nHorizontalMullion*mullionWidth then goto "Door_Style_01"        ! simple door

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** Door Panel
	addx reducedDoorWidth/2
	rotx 90
	material matDoorFrame

	if bBevel then
		group "frame_typ8"
		fprism_ matDoorFrame, matDoorFrame, matDoorFrame, matDoorFrame,
			15,doorThickness,45,frameHillHeight,
			-reducedDoorWidth/2,0,15,
			reducedDoorWidth/2,0,15,
			reducedDoorWidth/2,reducedDoorHeight,15,
			-reducedDoorWidth/2,reducedDoorHeight,15,
			-reducedDoorWidth/2,0,-1,
			-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,13,
			-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,13,
			-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,913,		!! 1.centerpoint
			0,2*seg_beta-0.05,4013,					!! 0.01 correction
			0, reducedDoorHeight-doorFrameWidth-seg_r, 913,				!! 2.centerpoint
			0,-(4*seg_beta),4013,
			reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,913,		!! 3.centerpoint
			0,2*seg_beta-0.05,4013,
			reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,13,
			-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
		endgroup
		placegroup "frame_typ8"

		group "grill_typ8"
			gosub "Grill"
		endgroup

		grills_typ7=subgroup("grill_typ8","frame_typ8")
		placegroup grills_typ7

		killgroup "frame_typ8"
		killgroup "grill_typ8"
		killgroup grills_typ7
	else
		group "frame_typ7"					!! Door Frame

			base
					prism_	4,doorThickness,
							-reducedDoorWidth/2+doorFrameWidth,0,13,
							reducedDoorWidth/2-doorFrameWidth,0,8,
							reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,15,
							-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
			vert 0, 0, 0
			vert 1, 0, 0
			vert 0, 1, 0
			vert 0, 0, 1

			coor 2, -1, -2, -3, -4
			body 1

			base
					prism_	6,doorThickness,
							reducedDoorWidth/2,0,15,
							reducedDoorWidth/2,reducedDoorHeight,15,
							reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
							reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,15,
							reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
							reducedDoorWidth/2-doorFrameWidth,0,13
			vert 0, 0, 0
			vert 0, 1, 0
			vert -1, 0, 0
			vert 0, 0, -1

			coor 2, -1, -2, -3, -4
			body 1

			base
				resol 36
				prism_	10,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,13,	!1
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,913,		!! 1.centerpoint
					0,2*seg_beta-0.5,4013,							!! 0.01 correction
					0, reducedDoorHeight-doorFrameWidth-seg_r, 913,					!! 2.centerpoint
					0,-(4*seg_beta),4013,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,913,		!! 3.centerpoint
					0,2*seg_beta-0.5,4013,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,10,	!2
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,13,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8
			vert 0, 0, 0
			vert 1, 0, 0
			vert 0, 1, 0
			vert 0, 0, 1

			coor 2, -1, -2, -3, -4
			body 1

			base
					prism_	6,doorThickness,
							-reducedDoorWidth/2,0,15,
							-reducedDoorWidth/2,reducedDoorHeight,15,
							-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8,
							-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,15,
							-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
							-reducedDoorWidth/2+doorFrameWidth,0,13

			vert 0, 0, 0
			vert 0, 1, 0
			vert -1, 0, 0
			vert 0, 0, -1

			coor 2, -1, -2, -3, -4
			body 1

			if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
		endgroup
		placegroup "frame_typ7"

		group "grill_typ7"
			gosub "Grill"
		endgroup

		grills_typ7=subgroup("grill_typ7","frame_typ7")
		placegroup grills_typ7

		killgroup "frame_typ7"
		killgroup "grill_typ7"
		killgroup grills_typ7

		body -1
	endif

	addz doorThickness-frameHillHeight
	material matDoorGlass
	poly_ 13,						!! Window Glass
		-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,0,
		-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,0,
		-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,900,		!! 1.centerpoint
		0,2*seg_beta,4000,
		-(reducedDoorWidth/2-doorFrameWidth)/2,reducedDoorHeight-doorFrameWidth-arcSegmentHeight/2,0,
		0, reducedDoorHeight-doorFrameWidth-seg_r, 900,				!! 2.centerpoint
		0,-(4*seg_beta),4000,
		(reducedDoorWidth/2-doorFrameWidth)/2,reducedDoorHeight-doorFrameWidth-arcSegmentHeight/2,0,
		reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight+seg_r,900,		!! 3.centerpoint
		0,2*seg_beta,4000,
		reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth-arcSegmentHeight,0,
		reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,0,
		-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,-1
	body -1
	del 1
	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_8
! ------------------------------------------------------------------------------
"Door_Style_08":
	if reducedDoorWidth<mindistLarge+0.14 or reducedDoorHeight<mindistLarge	then  goto "Door_Style_01"        ! simple door

	gap_bor = 0
	addx reducedDoorWidth/2
	rotx 90
	base
	if bBevel then
		fprism_ matDoorDecor1, matDoorDecor1, matDoorDecor1, matDoorDecor1,
			8,doorThickness,45,frameHillHeight,
			-reducedDoorWidth/2,0,15,
			reducedDoorWidth/2,0,15,
			reducedDoorWidth/2,reducedDoorHeight,15,
			0.06,reducedDoorHeight,15,
			0.06,reducedDoorHeight-0.05,15,
			-0.06,reducedDoorHeight-0.05,15,
			-.06,reducedDoorHeight,15,
			-reducedDoorWidth/2,reducedDoorHeight,15
	else
		cprism_ matDoorDecor1, matDoorDecor1, matDoorDecor1,
			8,doorThickness,
			-reducedDoorWidth/2+gap_bor,gap_bor,15,
			reducedDoorWidth/2-gap_bor,gap_bor,15,
			reducedDoorWidth/2-gap_bor,reducedDoorHeight-gap_bor,15,
			0.06-gap_bor,reducedDoorHeight-gap_bor,15,
			0.06-gap_bor,reducedDoorHeight-0.05+gap_bor,15,
			-0.06+gap_bor,reducedDoorHeight-0.05+gap_bor,15,
			-0.06+gap_bor,reducedDoorHeight-gap_bor,15,
			-reducedDoorWidth/2+gap_bor,reducedDoorHeight-gap_bor,15

	endif
	vert 0, 0, 0
	vert 0, 1, 0
	vert -1, 0, 0
	vert 0, 0, -1

	coor 2, -1, -2, -3, -4
	body 1

	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_9
! ------------------------------------------------------------------------------
"Door_Style_09":

	if reducedDoorWidth<doorFrameWidth*2+mindistSmall or reducedDoorHeight<doorFrameWidth*2+mindistSmall	then  goto "Door_Style_01"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	material matDoorFrame

	if bBevel then
		base
			fprism_ matDoorFrame, matDoorFrame, matDoorFrame, matDoorFrame,
				4,doorThickness,45,frameHillHeight,
						reducedDoorWidth/2,0,15,
						reducedDoorWidth/2,reducedDoorHeight,15,
						reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,15,
						reducedDoorWidth/2-doorFrameWidth,0,15
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			fprism_ matDoorFrame, matDoorFrame, matDoorFrame, matDoorFrame,
				4,doorThickness,45,frameHillHeight,
						-reducedDoorWidth/2,0,15,
						-reducedDoorWidth/2,reducedDoorHeight,15,
						-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,15,
						-reducedDoorWidth/2+doorFrameWidth,0,15
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		else
		base
			prism_	4,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,0,15
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,0,15
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
	endif

	material matDoorGlass

	addz doorThickness/2
		base
			poly_ 4,
				-reducedDoorWidth/2+doorFrameWidth,0,1,
				reducedDoorWidth/2-doorFrameWidth,0,1,
				reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,1,
				-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,1
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
	del 1

	del 2

	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

return


! ------------------------------------------------------------------------------
! Door_Style_11
! ------------------------------------------------------------------------------
"Door_Style_11":

	if reducedDoorWidth<doorFrameWidth*2+mindistLarge or reducedDoorHeight<doorFrameWidth*2+mindistLarge then  goto "Door_Style_01"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	material matDoorFrame

	if bBevel then

		group "frame_typ11_hor"					!! Door Frame
		base
			tube 6, 4, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2, 0, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth-frameHillHeight, 0, 0, 0,
				 reducedDoorWidth/2-doorFrameWidth+frameHillHeight, 0, 0, 0,
				 reducedDoorWidth/2, 0, 0, 0

		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			tube 6, 4, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2, reducedDoorHeight-doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth-frameHillHeight, reducedDoorHeight-doorFrameWidth, 0, 0,
				 reducedDoorWidth/2-doorFrameWidth+frameHillHeight, reducedDoorHeight-doorFrameWidth, 0, 0,
				 reducedDoorWidth/2, reducedDoorHeight-doorFrameWidth, 0, 0

		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1
		endgroup

		group "frame_typ11_vert"					!! Door Frame
		base
			tube 6, 6, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				reducedDoorWidth/2-4*doorFrameWidth, 0, 0, 0,
				reducedDoorWidth/2-2*doorFrameWidth, 0, 0, 0,
				reducedDoorWidth/2, 0, 0, 0,
				reducedDoorWidth/2, reducedDoorHeight, 0, 0,
				reducedDoorWidth/2-2*doorFrameWidth, reducedDoorHeight, 0, 0,
				reducedDoorWidth/2-4*doorFrameWidth, reducedDoorHeight, 0, 0

		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			tube 6, 6, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2+4*doorFrameWidth, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+2*doorFrameWidth, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+2*doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+4*doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0

		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
		endgroup

		frame_typ11_v=subgroup("frame_typ11_vert","frame_typ11_hor")
		placegroup frame_typ11_v
		placegroup "frame_typ11_hor"

		killgroup "frame_typ11_vert"
		killgroup "frame_typ11_hor"
		killgroup frame_typ11_v

	else
		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,0,13,
					reducedDoorWidth/2-doorFrameWidth,0,8,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,14,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,14,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
					reducedDoorWidth/2-doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,13,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,14,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,14,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
					-reducedDoorWidth/2+doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
	endif

	!! Panel
	backMask = 0
	tempBevel = 1
	tempHeight = reducedDoorHeight-2*doorFrameWidth
	matInsert = matDoorDecor1
	addy doorFrameWidth
	gosub "Door_Insert_Panel"
	del 1

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_12
! ------------------------------------------------------------------------------
"Door_Style_12":

	if reducedDoorWidth<doorFrameWidth*2+mindistSmall+nVerticalMullion*mullionWidth or reducedDoorHeight<doorFrameWidth*2+mindistSmall+nHorizontalMullion*mullionWidth then goto "Door_Style_01"        ! simple door

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! *** Door Panel
	addx reducedDoorWidth/2
	rotx 90

	material matDoorFrame

	if bBevel then

		group "frame_typ12_hor"					!! Door Frame
		base
			tube 6, 4, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2, 0, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth-frameHillHeight, 0, 0, 0,
				 reducedDoorWidth/2-doorFrameWidth+frameHillHeight, 0, 0, 0,
				 reducedDoorWidth/2, 0, 0, 0
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			tube 6, 4, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2, reducedDoorHeight-doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth-frameHillHeight, reducedDoorHeight-doorFrameWidth, 0, 0,
				 reducedDoorWidth/2-doorFrameWidth+frameHillHeight, reducedDoorHeight-doorFrameWidth, 0, 0,
				 reducedDoorWidth/2, reducedDoorHeight-doorFrameWidth, 0, 0

		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1
		endgroup

		group "frame_typ12_vert"					!! Door Frame
		base
			tube 6, 6, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				reducedDoorWidth/2-4*doorFrameWidth, 0, 0, 0,
				reducedDoorWidth/2-2*doorFrameWidth, 0, 0, 0,
				reducedDoorWidth/2, 0, 0, 0,
				reducedDoorWidth/2, reducedDoorHeight, 0, 0,
				reducedDoorWidth/2-2*doorFrameWidth, reducedDoorHeight, 0, 0,
				reducedDoorWidth/2-4*doorFrameWidth, reducedDoorHeight, 0, 0

		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			tube 6, 6, tubeMask,
				0, 0, 0,
				doorFrameWidth, 0, 0,
				doorFrameWidth, doorThickness-frameHillHeight, 0,
				doorFrameWidth-frameHillHeight, doorThickness, 0,
				frameHillHeight, doorThickness, 0,
				0, doorThickness-frameHillHeight, 0,
				-reducedDoorWidth/2+4*doorFrameWidth, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+2*doorFrameWidth, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth, doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+2*doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
				-reducedDoorWidth/2+4*doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0

		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
		endgroup

		frame_typ12_v=subgroup("frame_typ12_vert","frame_typ12_hor")
		frame_typ12_vv = addgroup(frame_typ12_v,"frame_typ12_hor")
		placegroup frame_typ12_v
		placegroup "frame_typ12_hor"
		killgroup "frame_typ12_vert"
		killgroup "frame_typ12_hor"
		killgroup frame_typ12_v

	else
		group "frame_typ12"					!! Door Frame
		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,0,13,
					reducedDoorWidth/2-doorFrameWidth,0,8,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10,
					reducedDoorWidth/2-doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	4,doorThickness,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,13,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,10
		vert 0, 0, 0
		vert 1, 0, 0
		vert 0, 1, 0
		vert 0, 0, 1

		coor 2, -1, -2, -3, -4
		body 1

		base
			prism_	6,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10,
					-reducedDoorWidth/2+doorFrameWidth,0,13
		vert 0, 0, 0
		vert 0, 1, 0
		vert -1, 0, 0
		vert 0, 0, -1

		coor 2, -1, -2, -3, -4
		body 1
		endgroup
	endif
	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"

	group "grill_typ12"
		gosub "Grill"
	endgroup

	if bBevel then
		grills_typ2=subgroup("grill_typ12",frame_typ12_vv)
		placegroup grills_typ2
		killgroup frame_typ12_vv
		killgroup "grill_typ12"
		killgroup grills_typ2
	else
		grills_typ2=subgroup("grill_typ12","frame_typ12")
		placegroup "frame_typ12"
		placegroup grills_typ2
		killgroup "frame_typ12"
		killgroup "grill_typ12"
		killgroup grills_typ2
	endif

	body -1

	addz doorThickness-frameHillHeight
	material matDoorGlass
	poly_ 4,						!! Window Glass
		-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,1,
		reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,1,
		reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,1,
		-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,1
	body -1
	del 1
	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_13
! ------------------------------------------------------------------------------
"Door_Style_13":

	if reducedDoorWidth < doorFrameWidth*2+mindistSmall+nVerticalMullion*mullionWidth or reducedDoorHeight<doorFrameWidth*2+mindistSmall+nHorizontalMullion*mullionWidth then goto "Door_Style_01"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	!! Frame
	if doorFrameWidth > eps then
		material matDoorFrame
		tempBevel = bBevel
		backMask = 1
		group "frame_13_hor"
			gosub "Frame_45_Horizontal"
		endgroup
		group "frame_13_vert"
			gosub "Frame_45_Vertical"
		endgroup
		group "grill_typ13"
			gosub "GrillFrame"
		endgroup

		grills_typ13_v=subgroup("grill_typ13","frame_13_vert")
		grills_typ13_h=subgroup(grills_typ13_v,"frame_13_hor")
		placegroup grills_typ13_h
		placegroup "frame_13_vert"
		placegroup "frame_13_hor"

		killgroup grills_typ13_h
		killgroup grills_typ13_v
		killgroup "grill_typ13"
		killgroup "frame_13_vert"
		killgroup "frame_13_hor"
	else
		frameHillHeight = 0
		gosub "GrillFrame"
	endif

	!! Panel
	add doorFrameWidth-reducedDoorWidth/2, doorFrameWidth, 0
	tempWidth = xx
	tempHeight = yy
	material matDoorDecor1
	for i=1 to nVerticalMullion+1
		for ii=1 to nHorizontalMullion+1
			add (xx+mullionWidth)*(i-1), (yy+mullionWidth)*(ii-1), 0
			gosub "Door_Insert_Panel_Sliding"
			del 1
		next ii
	next i
	del 1

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_14
! ------------------------------------------------------------------------------
"Door_Style_14":

	if reducedDoorWidth<doorFrameWidth*2+mindistLarge or reducedDoorHeight<doorFrameWidth*2+mindistLarge then goto "Door_Style_01"        ! simple door

	middleDivide = 0.7
	backMask = 0

	addx reducedDoorWidth/2
	rotx 90

	material matDoorFrame
	tempBevel = bBevel
	matInsert = matDoorDecor1
	if doorFrameWidth > eps then
		if reducedDoorHeight > doorFrameWidth*3 + middleDivide +mindistSmall then
			if bBevel then
				group "frame_typ13_vert"		!! Door Frame
					gosub "Frame_45_Vertical"
				endgroup
				gosub "Frame_45_Horizontal"

				group "divide_typ13"
					addy middleDivide+doorFrameWidth
					tube 6, 4, tubeMask,
						0, 0, 0,
						doorFrameWidth, 0, 0,
						doorFrameWidth, doorThickness-frameHillHeight, 0,
						doorFrameWidth-frameHillHeight, doorThickness, 0,
						frameHillHeight, doorThickness, 0,
						0, doorThickness-frameHillHeight, 0,

						-reducedDoorWidth/2, 0, 0, 0,
						-reducedDoorWidth/2+doorFrameWidth-frameHillHeight, 0, 0, 0,
						 reducedDoorWidth/2-doorFrameWidth+frameHillHeight, 0, 0, 0,
						 reducedDoorWidth/2, 0, 0, 0
					gosub "textureHorizontal"
					del 1
				endgroup

				frame_typ13_v = subgroup("frame_typ13_vert","divide_typ13")
				placegroup frame_typ13_v
				gosub "textureVertical2"

				frame_typ13_h = subgroup("divide_typ13",frame_typ13_v)
				placegroup frame_typ13_h
				gosub "textureHorizontal2"

				killgroup frame_typ13_v
				killgroup frame_typ13_h
				killgroup "divide_typ13"
				killgroup "frame_typ13_vert"
			else
				gosub "Frame_45_Horizontal"

				prism_	6,doorThickness,
							reducedDoorWidth/2,0,15,
							reducedDoorWidth/2,reducedDoorHeight,10,
							reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,12,
							reducedDoorWidth/2-doorFrameWidth,middleDivide+2*doorFrameWidth,10,
							reducedDoorWidth/2-doorFrameWidth,middleDivide+doorFrameWidth,12,
							reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10
				gosub "textureVertical2"

				prism_	6,doorThickness,
							-reducedDoorWidth/2,0,15,
							-reducedDoorWidth/2,reducedDoorHeight,10,
							-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,12,
							-reducedDoorWidth/2+doorFrameWidth,middleDivide+2*doorFrameWidth,10,
							-reducedDoorWidth/2+doorFrameWidth,middleDivide+doorFrameWidth,12,
							-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
				gosub "textureVertical2"

				addy middleDivide+doorFrameWidth
				prism_	4,doorThickness,
							-reducedDoorWidth/2+doorFrameWidth,0,12,
							reducedDoorWidth/2-doorFrameWidth,0,10,
							reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12,
							-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
				gosub "textureHorizontal2"
				del 1
			endif
		endif

		!!Panels
		addy doorFrameWidth
		tempHeight = middleDivide
		tempBevel = 1
		gosub "Door_Insert_Panel"
		del 1

		addy doorFrameWidth*2+middleDivide
		tempHeight = reducedDoorHeight-3*doorFrameWidth-middleDivide
		gosub "Door_Insert_Mirror"
		del 1
	else
		if doorFrameWidth > eps then
			gosub "Frame_45_Vertical"
			gosub "Frame_45_Horizontal"
		endif

		addy doorFrameWidth
		tempHeight = reducedDoorHeight-2*doorFrameWidth
		tempBevel = 1
		gosub "Door_Insert_Panel"
		del 1
	endif

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_15
! ------------------------------------------------------------------------------
"Door_Style_15":

	if reducedDoorWidth<doorFrameWidth*2+mindistSmall or reducedDoorHeight<doorFrameWidth*2+mindistSmall then goto "Door_Style_Simple"        ! simple door

	addx reducedDoorWidth/2
	rotx 90

	!! Frame
	if doorFrameWidth > eps then
		material matDoorFrame
		tempBevel = 0
		backMask = 1
		gosub "Frame_45_Vertical"
		gosub "Frame_45_Horizontal"
	endif

	!! Panel
	add doorFrameWidth-reducedDoorWidth/2, doorFrameWidth, 0
	tempWidth = reducedDoorWidth - doorFrameWidth*2
	tempHeight = reducedDoorHeight-2*doorFrameWidth
	material matDoorDecor1
	if not(bFormMirror) or bSmallSize then
		gosub "Door_Insert_Panel_Sliding"
	else
		gosub "Door_Insert_Mirror_Sliding"
	endif
	del 1

	del 2

return

! ------------------------------------------------------------------------------
! Door_Style_16
! ------------------------------------------------------------------------------
"Door_Style_16":

	if reducedDoorWidth < doorFrameWidth*4+3*minPanelWidth then
		bSmallSize = 1
		goto "Door_Style_15"
	endif

	addx reducedDoorWidth/2
	rotx 90

	!! Frame
	if doorFrameWidth > eps then
		material matDoorFrame
		tempBevel = 0
		backMask = 1
		gosub "Frame_45_Vertical"
		addx -reducedDoorWidth/2
		if bFormMirror then
			mulx -1
			addx -reducedDoorWidth
		endif
		prism_	8,doorThickness,
					0,0,15,
					reducedDoorWidth,0,10,
					reducedDoorWidth-doorFrameWidth,doorFrameWidth,15,
					3*doorFrameWidth+leftPanelWidth+middlePanelWidth,doorFrameWidth,8,
					2*doorFrameWidth+leftPanelWidth+middlePanelWidth,doorFrameWidth,15,
					2*doorFrameWidth+leftPanelWidth,doorFrameWidth,8,
					doorFrameWidth+leftPanelWidth,doorFrameWidth,15,
					doorFrameWidth,doorFrameWidth,10
		gosub "textureHorizontal2"

		prism_	8,doorThickness,
					0,reducedDoorHeight,15,
					reducedDoorWidth,reducedDoorHeight,10,
					reducedDoorWidth-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					3*doorFrameWidth+leftPanelWidth+middlePanelWidth,reducedDoorHeight-doorFrameWidth,8,
					2*doorFrameWidth+leftPanelWidth+middlePanelWidth,reducedDoorHeight-doorFrameWidth,15,
					2*doorFrameWidth+leftPanelWidth,reducedDoorHeight-doorFrameWidth,8,
					doorFrameWidth+leftPanelWidth,reducedDoorHeight-doorFrameWidth,15,
					doorFrameWidth,reducedDoorHeight-doorFrameWidth,10
		gosub "textureHorizontal2"

		cprism_ matDoorFrame, matDoorFrame, matDoorFrame,
				4, doorThickness,
				doorFrameWidth+leftPanelWidth, doorFrameWidth, 10,
				2*doorFrameWidth+leftPanelWidth, doorFrameWidth, 15,
				2*doorFrameWidth+leftPanelWidth, reducedDoorHeight-doorFrameWidth, 10,
				doorFrameWidth+leftPanelWidth, reducedDoorHeight-doorFrameWidth, 15
		gosub "textureVertical2"

		cprism_ matDoorFrame, matDoorFrame, matDoorFrame,
				4, doorThickness,
				2*doorFrameWidth+leftPanelWidth+middlePanelWidth, doorFrameWidth, 10,
				3*doorFrameWidth+leftPanelWidth+middlePanelWidth, doorFrameWidth, 15,
				3*doorFrameWidth+leftPanelWidth+middlePanelWidth, reducedDoorHeight-doorFrameWidth, 10,
				2*doorFrameWidth+leftPanelWidth+middlePanelWidth, reducedDoorHeight-doorFrameWidth, 15
		gosub "textureVertical2"
		if bFormMirror then del 2
		del 1
	endif

	!! Panel
	if bFormMirror then mulx -1
	add doorFrameWidth-reducedDoorWidth/2, doorFrameWidth, 0
	tempWidth = leftPanelWidth
	tempHeight = reducedDoorHeight-2*doorFrameWidth
	material matDoorDecor1
	gosub "Door_Insert_Panel_Sliding"
	addx doorFrameWidth+leftPanelWidth
	tempWidth = middlePanelWidth
	material matDoorDecor2
	gosub "Door_Insert_Panel_Sliding"
	addx doorFrameWidth+middlePanelWidth
	tempWidth = reducedDoorWidth-doorFrameWidth*4-leftPanelWidth-middlePanelWidth
	material matDoorDecor1
	gosub "Door_Insert_Panel_Sliding"
	del 3

	if bFormMirror then del 1

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_17
! ------------------------------------------------------------------------------
"Door_Style_17":
	if reducedDoorHeight < doorFrameWidth*4+3*minPanelHeight then
		bSmallSize = 1
		goto "Door_Style_15"        ! simple door
	endif

	addx reducedDoorWidth/2
	rotx 90

	!! Frame
	if doorFrameWidth > eps then
		material matDoorFrame
		tempBevel = 0
		backMask = 1
		gosub "Frame_45_Horizontal"
		if (bFormMirror) then
			muly -1
			addy -reducedDoorHeight
		endif
		prism_	8,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-topPanelHeight-doorFrameWidth,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-topPanelHeight-2*doorFrameWidth,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-topPanelHeight-2*doorFrameWidth-decorPanelHeight,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-topPanelHeight-3*doorFrameWidth-decorPanelHeight,15,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10
		gosub "textureVertical2"

		prism_	8,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,10,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-topPanelHeight-doorFrameWidth,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-topPanelHeight-2*doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-topPanelHeight-2*doorFrameWidth-decorPanelHeight,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-topPanelHeight-3*doorFrameWidth-decorPanelHeight,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		gosub "textureVertical2"

		cprism_ matDoorFrame, matDoorFrame, matDoorFrame,
				4, doorThickness,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth-topPanelHeight, 10,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-2*doorFrameWidth-topPanelHeight, 15,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-2*doorFrameWidth-topPanelHeight, 10,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-doorFrameWidth-topPanelHeight, 15
		gosub "textureHorizontal2"

		cprism_ matDoorFrame, matDoorFrame, matDoorFrame,
				4, doorThickness,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-2*doorFrameWidth-topPanelHeight-decorPanelHeight, 10,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-3*doorFrameWidth-topPanelHeight-decorPanelHeight, 15,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-3*doorFrameWidth-topPanelHeight-decorPanelHeight, 10,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-2*doorFrameWidth-topPanelHeight-decorPanelHeight, 15
		gosub "textureHorizontal2"
	endif


	!! Panel
	add doorFrameWidth-reducedDoorWidth/2, doorFrameWidth, 0
	tempWidth = reducedDoorWidth-2*doorFrameWidth
	tempHeight = reducedDoorHeight-4*doorFrameWidth-topPanelHeight-decorPanelHeight
	material matDoorDecor1
	gosub "Door_Insert_Panel_Sliding"
	addy doorFrameWidth+tempHeight
	tempHeight = decorPanelHeight
	material matDoorDecor2
	gosub "Door_Insert_Panel_Sliding"
	addy doorFrameWidth+decorPanelHeight
	tempHeight = topPanelHeight
	material matDoorDecor1
	gosub "Door_Insert_Panel_Sliding"
	del 3

	if (bFormMirror) then del 2

	del 2

return


! ------------------------------------------------------------------------------
! Door_Style_18
! ------------------------------------------------------------------------------
"Door_Style_18":
	if reducedDoorHeight < doorFrameWidth*4+3*minPanelHeight then
		bSmallSize = 1
		goto "Door_Style_15"        ! simple door
	endif

	addx reducedDoorWidth/2
	rotx 90

	!! Frame
	if doorFrameWidth > eps then
		material matDoorFrame
		tempBevel = 0
		backMask = 1
		gosub "Frame_45_Horizontal"
		prism_	8,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight/2+middlePanelHeight/2+doorFrameWidth,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight/2+middlePanelHeight/2,15,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight/2-middlePanelHeight/2,8,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight/2-middlePanelHeight/2-doorFrameWidth,15,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10
		gosub "textureVertical2"

		prism_	8,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,10,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight/2+middlePanelHeight/2+doorFrameWidth,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight/2+middlePanelHeight/2,15,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight/2-middlePanelHeight/2,8,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight/2-middlePanelHeight/2-doorFrameWidth,15,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		gosub "textureVertical2"

		cprism_ matDoorFrame, matDoorFrame, matDoorFrame,
				4, doorThickness,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight/2-middlePanelHeight/2, 10,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight/2-middlePanelHeight/2-doorFrameWidth, 15,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight/2-middlePanelHeight/2-doorFrameWidth, 10,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight/2-middlePanelHeight/2, 15
		gosub "textureHorizontal2"

		cprism_ matDoorFrame, matDoorFrame, matDoorFrame,
				4, doorThickness,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight/2+middlePanelHeight/2, 10,
				-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight/2+middlePanelHeight/2+doorFrameWidth, 15,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight/2+middlePanelHeight/2+doorFrameWidth, 10,
				reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight/2+middlePanelHeight/2, 15
		gosub "textureHorizontal2"
	endif


	!! Panel
	add doorFrameWidth-reducedDoorWidth/2, doorFrameWidth, 0
	tempWidth = reducedDoorWidth-2*doorFrameWidth
	tempHeight = (reducedDoorHeight-4*doorFrameWidth-middlePanelHeight)/2
	if not(bFormMirror) then
		material matDoorDecor1
	else
		material matDoorDecor2
	endif
	gosub "Door_Insert_Panel_Sliding"
	addy doorFrameWidth+tempHeight
	tempHeight = middlePanelHeight
	if not(bFormMirror) then
		material matDoorDecor2
	else
		material matDoorDecor1
	endif
	gosub "Door_Insert_Panel_Sliding"
	addy doorFrameWidth+tempHeight
	tempHeight = (reducedDoorHeight-4*doorFrameWidth-middlePanelHeight)/2
	if not(bFormMirror) then
		material matDoorDecor1
	else
		material matDoorDecor2
	endif
	gosub "Door_Insert_Panel_Sliding"
	del 3

	del 2

return


! ==============================================================================
! Custom Panel
! ==============================================================================
"DoorCustomPanel":

	addx reducedDoorWidth/2
	rotx 90
	addx -reducedDoorWidth/2

	call "Object_Panel_Collection" parameters a=reducedDoorWidth, b=reducedDoorHeight, zzyzx=doorThickness, gs_ptype=doorCustomPanelName

	del 3
return


! ==============================================================================
! Vertical and Horizontal Grills of Leaf
! ==============================================================================
"Grill":

	xx=(reducedDoorWidth-2*doorFrameWidth-nVerticalMullion*mullionWidth)/(nVerticalMullion+1)	!! dist. of hor. panes
	yy=(reducedDoorHeight-2*doorFrameWidth-nHorizontalMullion*mullionWidth)/(nHorizontalMullion+1)	!! dist. of vert. panes
	!if xx < mullionWidth or yy < mullionWidth then return

	material matDoorFrame

	add -reducedDoorWidth/2+doorFrameWidth+xx, doorFrameWidth-frameHillHeight, doorThickness-frameHillHeight-0.001
		for i=1 to nVerticalMullion
			block mullionWidth, reducedDoorHeight-2*(doorFrameWidth-frameHillHeight), 0.005		!! Vert. Grill
			gosub "textureVertical2"
			addx xx+mullionWidth
		next i
		del nVerticalMullion
	del 1

	addy doorFrameWidth

	for ii=1 to nHorizontalMullion

		add -reducedDoorWidth/2+doorFrameWidth-frameHillHeight,yy,doorThickness-frameHillHeight-0.001
		block xx+frameHillHeight, mullionWidth, 0.005		!! hor. Right End Grills
		gosub "textureHorizontal2"
		del 1

		if nVerticalMullion<>0 and nHorizontalMullion<>0 then
			add reducedDoorWidth/2-doorFrameWidth-xx,yy,doorThickness-frameHillHeight-0.001
			block xx+frameHillHeight, mullionWidth, 0.005		!! hor. Left End Grills
			gosub "textureHorizontal2"
			del 1


			add -reducedDoorWidth/2+doorFrameWidth+xx+mullionWidth,yy,doorThickness-frameHillHeight-0.001
			for p=1 to nVerticalMullion-1
				block xx, mullionWidth, 0.005			!! hor. Middle Grills
				gosub "textureHorizontal2"
				addx xx+mullionWidth
			next p
			del nVerticalMullion-1
			del 1
		endif

		addy yy+mullionWidth
	next ii
	del nHorizontalMullion
	del 1
	del top
	body -1
return


"GrillFrame":

	xx=(reducedDoorWidth-2*doorFrameWidth-nVerticalMullion*mullionWidth)/(nVerticalMullion+1)	!! dist. of hor. panes
	yy=(reducedDoorHeight-2*doorFrameWidth-nHorizontalMullion*mullionWidth)/(nHorizontalMullion+1)	!! dist. of vert. panes
	grillX = reducedDoorWidth-2*doorFrameWidth+2*frameHillHeight
	grillY = reducedDoorHeight-2*doorFrameWidth+2*frameHillHeight

	material matDoorFrame

	add -reducedDoorWidth/2+doorFrameWidth+xx, doorFrameWidth-frameHillHeight, 0
	for i=1 to nVerticalMullion
		prism_ 4, doorThickness,
				0, 0, 13,
				mullionWidth, 0, 13,
				mullionWidth, grillY , 13,
				0, grillY , 13
		gosub "textureVertical2"
		addx xx+mullionWidth
	next i
	del nVerticalMullion


	for i=1 to nVerticalMullion
		cutpolya 4, 0, 0,
				0, 0, 15,
				mullionWidth, 0, 15,
				mullionWidth, grillY , 15,
				0, grillY , 15
		addx xx+mullionWidth
	next i
	del nVerticalMullion
	del 1


	addy doorFrameWidth

	add -reducedDoorWidth/2+doorFrameWidth-frameHillHeight,yy, 0
	for ii=1 to nHorizontalMullion
		prism_ 4, doorThickness,
				0, 0, 13,
				grillX, 0, 13,
				grillX, mullionWidth, 13,
				0, mullionWidth, 13
		gosub "textureHorizontal2"
		addy yy+mullionWidth
	next ii
	del nHorizontalMullion
	del 1

	for i=1 to nVerticalMullion
		cutend
	next i

	del 1

	body -1
return


! ==============================================================================
! Frame 45 degree corner
! ==============================================================================
"Frame_45_Vertical":

	if tempBevel then

		tube 6, 4, tubeMask,
			0, 0, 0,
			doorFrameWidth, 0, 0,
			doorFrameWidth, doorThickness-frameHillHeight, 0,
			doorFrameWidth-frameHillHeight, doorThickness, 0,
			frameHillHeight, doorThickness, 0,
			0, doorThickness-frameHillHeight, 0,
			reducedDoorWidth/2-1, 0, 0, 0,
			reducedDoorWidth/2, 0, 0, 0,
			reducedDoorWidth/2, reducedDoorHeight, 0, 0,
			reducedDoorWidth/2-1, reducedDoorHeight, 0, 0
		gosub "textureVertical2"

		tube 6, 4, tubeMask,
			0, 0, 0,
			doorFrameWidth, 0, 0,
			doorFrameWidth, doorThickness-frameHillHeight, 0,
			doorFrameWidth-frameHillHeight, doorThickness, 0,
			frameHillHeight, doorThickness, 0,
			0, doorThickness-frameHillHeight, 0,
			-reducedDoorWidth/2+doorFrameWidth+1, doorFrameWidth, 0, 0,
			-reducedDoorWidth/2+doorFrameWidth, doorFrameWidth, 0, 0,
			-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
			-reducedDoorWidth/2+doorFrameWidth+1, reducedDoorHeight-doorFrameWidth, 0, 0
		gosub "textureVertical2"

	else
		prism_	4,doorThickness,
					reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,12+backMask,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,10
		gosub "textureVertical2"

		prism_	4,doorThickness,
					-reducedDoorWidth/2,0,15,
					-reducedDoorWidth/2,reducedDoorHeight,10,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,12+backMask,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		gosub "textureVertical2"

	endif

return


"Frame_45_Horizontal":

	if tempBevel then
		tube 6, 4, tubeMask,
			0, 0, 0,
			doorFrameWidth, 0, 0,
			doorFrameWidth, doorThickness-frameHillHeight, 0,
			doorFrameWidth-frameHillHeight, doorThickness, 0,
			frameHillHeight, doorThickness, 0,
			0, doorThickness-frameHillHeight, 0,
			-reducedDoorWidth/2, 1, 0, 0,
			-reducedDoorWidth/2, 0, 0, 0,
			 reducedDoorWidth/2, 0, 0, 0,
			 reducedDoorWidth/2, 1, 0, 0
		gosub "textureHorizontal2"

		tube 6, 4, tubeMask,
			0, 0, 0,
			doorFrameWidth, 0, 0,
			doorFrameWidth, doorThickness-frameHillHeight, 0,
			doorFrameWidth-frameHillHeight, doorThickness, 0,
			frameHillHeight, doorThickness, 0,
			0, doorThickness-frameHillHeight, 0,
			-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth-1, 0, 0,
			-reducedDoorWidth/2+doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
			 reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-doorFrameWidth, 0, 0,
			 reducedDoorWidth/2-doorFrameWidth, reducedDoorHeight-doorFrameWidth-1, 0, 0
		gosub "textureHorizontal2"

	else
		prism_	4,doorThickness,
					-reducedDoorWidth/2,0,15,
					reducedDoorWidth/2,0,10,
					reducedDoorWidth/2-doorFrameWidth,doorFrameWidth,12+backMask,
					-reducedDoorWidth/2+doorFrameWidth,doorFrameWidth,10
		gosub "textureHorizontal2"

		prism_	4,doorThickness,
					-reducedDoorWidth/2,reducedDoorHeight,15,
					reducedDoorWidth/2,reducedDoorHeight,10,
					reducedDoorWidth/2-doorFrameWidth,reducedDoorHeight-doorFrameWidth,12+backMask,
					-reducedDoorWidth/2+doorFrameWidth,reducedDoorHeight-doorFrameWidth,10
		gosub "textureHorizontal2"

	endif

return


! ==============================================================================
! Panels
! ==============================================================================
"Door_Insert_Panel":
	if tempBevel then
		fprism_ matInsert, matDoorFrame, matDoorFrame, matInsert,
				4,doorThickness,45,frameHillHeight,
				-reducedDoorWidth/2+doorFrameWidth,0,14,
				reducedDoorWidth/2-doorFrameWidth,0,14,
				reducedDoorWidth/2-doorFrameWidth,tempHeight,14,
				-reducedDoorWidth/2+doorFrameWidth,tempHeight,14
	else
		cprism_	matInsert, matDoorFrame, matDoorFrame,
				4,doorThickness,
				reducedDoorWidth/2-doorFrameWidth,0,12,
				reducedDoorWidth/2-doorFrameWidth,tempHeight,12,
				-reducedDoorWidth/2+doorFrameWidth,tempHeight,12,
				-reducedDoorWidth/2+doorFrameWidth,0,12
	endif
	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
return


"Door_Insert_Panel_Sliding":
	addz doorThickness/4
	prism_	4,doorThickness/2,
			0,0,15,
			0,tempHeight,15,
			tempWidth,tempHeight,15,
			tempWidth,0,15

	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
	del 1
return


"Door_Insert_Mirror":

	cprism_	matDoorFrame, matDoorFrame, matDoorFrame,
			4,doorThickness-frameHillHeight,
			reducedDoorWidth/2-doorFrameWidth,0,12,
			reducedDoorWidth/2-doorFrameWidth,tempHeight,12,
			-reducedDoorWidth/2+doorFrameWidth,tempHeight,12,
			-reducedDoorWidth/2+doorFrameWidth,0,12

	addz doorThickness-frameHillHeight
	if tempBevel then
		fprism_ matDoorMirror, matDoorMirror, matDoorMirror, matDoorMirror,
				4,doorThickness-frameHillHeight,45,frameHillHeight,
				-reducedDoorWidth/2+doorFrameWidth,0,14,
				reducedDoorWidth/2-doorFrameWidth,0,14,
				reducedDoorWidth/2-doorFrameWidth,tempHeight,14,
				-reducedDoorWidth/2+doorFrameWidth,tempHeight,14
	else
		cprism_	matDoorMirror, matDoorMirror, matDoorMirror,
				4,frameHillHeight,
				reducedDoorWidth/2-doorFrameWidth,0,15,
				reducedDoorWidth/2-doorFrameWidth,tempHeight,15,
				-reducedDoorWidth/2+doorFrameWidth,tempHeight,15,
				-reducedDoorWidth/2+doorFrameWidth,0,15
	endif
	del 1

	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
return


"Door_Insert_Mirror_Sliding":
	addz doorThickness/4
	prism_	4,doorThickness/4,
			0,0,15,
			0,tempHeight,15,
			tempWidth,tempHeight,15,
			tempWidth,0,15

	addz doorThickness/4
	cprism_	matDoorMirror, matDoorMirror, matDoorMirror,
			4,doorThickness/4,
			0,0,15,
			0,tempHeight,15,
			tempWidth,tempHeight,15,
			tempWidth,0,15
	del 2

	if reducedDoorHeight>reducedDoorWidth then gosub "textureVertical" else gosub "textureHorizontal"
return


! ==============================================================================
! Drawer
! ==============================================================================
"Drawer":
	drawerWidth = doorWidth - thickness
	drawerHeight= doorHeight - thickness*1.5
	drawerDepth = drawerDepth - thickness - doorThickness - mindistSmall

	if drawerWidth<2*thickness+mindistSmall or drawerHeight<2*thickness+mindistSmall or drawerDepth<thickness+mindistSmall then return !!Not draw Drawer

	material matDoorFrame
	add thickness/2, 0, thickness/2

	!!Bottom
	prism_ 4, thickness,
		thickness, 0, 15,
		drawerWidth-thickness, 0, 15,
		drawerWidth-thickness, drawerDepth-thickness, 15,
		thickness, drawerDepth-thickness, 15
	gosub "textureHorizontal2"

	!!Left Side
	prism_ 4, drawerHeight,
		0, 0, 15,
		thickness, 0, 15,
		thickness, drawerDepth, 15,
		0, drawerDepth, 15
	gosub "textureHorizontal"

	!!Right Side
	prism_ 4, drawerHeight,
		drawerWidth, 0, 15,
		drawerWidth-thickness, 0, 15,
		drawerWidth-thickness, drawerDepth, 15,
		drawerWidth, drawerDepth, 15
	gosub "textureHorizontal"

	!!Back Side
	prism_ 4, drawerHeight,
		thickness, drawerDepth-thickness, 15,
		drawerWidth-thickness, drawerDepth-thickness, 15,
		drawerWidth-thickness, drawerDepth, 15,
		thickness, drawerDepth, 15
	gosub "textureHorizontal"

	del 1
return


! ==============================================================================
! Knobs
! ==============================================================================
"Knobs":
	if gs_knob_type_m = 9 then
		reqBKnobs = 0
		success = LIBRARYGLOBAL ("LibraryGlobals13", "hideBKnobs", reqBKnobs)
		if (success = 0 or not(reqBKnobs)) and gs_cust_knob<>"" then
			addx -gs_customHandleHeight/2
			roty 90
			rotx 90
			addz -knobOffsetZ
			call "Knob_Collection" parameters gs_ptype	= gs_cust_knob,
									a		= gs_customHandleLength,
									b		= gs_customHandleHeight,
									zzyzx	= gs_customHandleDepth
			del 4
		endif
	else
		addy knobOffsetZ
		call "fa_knobtyp" parameters ALL
		del 1
	endif
return


! ==============================================================================
! Textures
! ==============================================================================
"textureVertical":
	roty 90
	gosub "mainTexture"
	del 1
return

"textureHorizontal":
	gosub "mainTexture"
return

"textureVertical2":
	rotz 90
	gosub "mainTexture"
	del 1
return

"textureHorizontal2":
	rotx 90
	gosub "mainTexture"
	del 1
return

"mainTexture":
	base
	vert 0, 0, 0
	vert 1, 0, 0
	vert 0, 1, 0
	vert 0, 0, 1

	coor 2, -1, -2, -3, -4
	body -1
return


]]>
</Script_3D>

<ParamSection SectVersion="26" SectionFlags="0" SubIdent="0">
	<ParamSectHeader>
		<Version>26</Version>
		<AutoHotspots>false</AutoHotspots>
		<WDLeftFrame>0</WDLeftFrame>
		<WDRightFrame>0</WDRightFrame>
		<WDTopFrame>0</WDTopFrame>
		<WDBotFrame>0</WDBotFrame>
		<LayFlags>65535</LayFlags>
		<WDMirrorThickness>0</WDMirrorThickness>
		<WDWallInset>0</WDWallInset>
		<SymbolColor>0</SymbolColor>
	</ParamSectHeader>
	<Parameters>
		<Length Name="A">
			<Description><![CDATA["Dimension 1"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="B">
			<Description><![CDATA["Dimension 2"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="ZZYZX">
			<Description><![CDATA["Height"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Boolean Name="AC_show2DHotspotsIn3D">
			<Description><![CDATA["Show 2D Hotspots in 3D"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Boolean>
		<Length Name="ac_bottomlevel">
			<Description><![CDATA["Bottom Level"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="ac_toplevel">
			<Description><![CDATA["Top Level"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Length>
		<Boolean Name="bNotMacro">
			<Description><![CDATA["Not Called Macro"]]></Description>
			<Value>1</Value>
		</Boolean>
		<Integer Name="gs_detlevel_3D_m">
			<Description><![CDATA["3D"]]></Description>
			<Value>2</Value>
		</Integer>
		<Integer Name="gs_resol">
			<Description><![CDATA["Resolution"]]></Description>
			<Value>24</Value>
		</Integer>
		<PenColor Name="gs_cont_pen">
			<Description><![CDATA["Contour Pen"]]></Description>
			<Value>7</Value>
		</PenColor>
		<Boolean Name="gs_shadow">
			<Description><![CDATA["Shadow"]]></Description>
			<Value>1</Value>
		</Boolean>
		<Boolean Name="gs_full_edit">
			<Description><![CDATA["Extra Hotspot Editing"]]></Description>
			<Value>0</Value>
		</Boolean>
		<Boolean Name="bAngleEdit">
			<Description><![CDATA["Angle Editing"]]></Description>
			<Value>1</Value>
		</Boolean>
		<Integer Name="cabdoortyp_unID">
			<Description><![CDATA["unID"]]></Description>
			<Value>0</Value>
		</Integer>

		<!-- gs_door: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_door">
			<Description><![CDATA["Door"]]></Description>
		</Title>
		<Boolean Name="bBevel">
			<Description><![CDATA["Edge Bevel"]]></Description>
			<Value>0</Value>
		</Boolean>
		<Boolean Name="bMirrored">
			<Description><![CDATA["Mirrored Vertical"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<Boolean Name="bFormMirror">
			<Description><![CDATA["Mirror Panel Form"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<Boolean Name="bSliding">
			<Description><![CDATA["Sliding Door"]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<Angle Name="doorAngle3D">
			<Description><![CDATA["Door Opening Angle 3D"]]></Description>
			<Value>0</Value>
		</Angle>
		<Integer Name="iDoorStyle">
			<Description><![CDATA["Door Styles"]]></Description>
			<Value>17</Value>
		</Integer>
		<Integer Name="iTempDoorStyle">
			<Description><![CDATA[""]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>17</Value>
		</Integer>
		<String Name="doorCustomPanelName">
			<Description><![CDATA["Custom Panel Name"]]></Description>
			<Value><![CDATA[""]]></Value>
		</String>
		<Length Name="doorWidth">
			<Description><![CDATA["Door Width"]]></Description>
			<Value>0.6</Value>
		</Length>
		<Length Name="doorHeight">
			<Description><![CDATA["Door Height"]]></Description>
			<Value>0.8</Value>
		</Length>
		<Length Name="doorThickness">
			<Description><![CDATA["Door Thickness"]]></Description>
			<Value>0.02</Value>
		</Length>
		<Length Name="doorFrameWidth">
			<Description><![CDATA["Door Frame Width"]]></Description>
			<Value>0.02</Value>
		</Length>
		<Length Name="mullionWidth">
			<Description><![CDATA["Grid Width"]]></Description>
			<Value>0.02</Value>
		</Length>
		<Integer Name="nVerticalMullion">
			<Description><![CDATA["No. of Vert. Door Grids"]]></Description>
			<Value>1</Value>
		</Integer>
		<Integer Name="nHorizontalMullion">
			<Description><![CDATA["No. of Hor. Door Grids"]]></Description>
			<Value>3</Value>
		</Integer>
		<Material Name="matDoorFrame">
			<Description><![CDATA["Door (Frame)"]]></Description>
			<Value>13</Value>
		</Material>
		<Material Name="matDoorGlass">
			<Description><![CDATA["Door (Glazing)"]]></Description>
			<Value>24</Value>
		</Material>
		<Material Name="matDoorMirror">
			<Description><![CDATA["Door (Mirror)"]]></Description>
			<Value>91</Value>
		</Material>
		<Material Name="matDoorDecor1">
			<Description><![CDATA["Door 1"]]></Description>
			<Value>18</Value>
		</Material>
		<Material Name="matDoorDecor2">
			<Description><![CDATA["Door 2"]]></Description>
			<Value>19</Value>
		</Material>

		<!-- wardrobeKnob: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="wardrobeKnob">
			<Description><![CDATA["Knobs"]]></Description>
		</Title>
		<Boolean Name="bKnob">
			<Description><![CDATA["Knob"]]></Description>
			<Value>0</Value>
		</Boolean>
		<Integer Name="gs_knob_type_m">
			<Description><![CDATA["Knob Style"]]></Description>
			<Value>1</Value>
		</Integer>
		<Length Name="knob_pl_x">
			<Description><![CDATA["(Lower) Knob Position Hor."]]></Description>
			<Value>0.05</Value>
		</Length>
		<Length Name="knob_pl_y">
			<Description><![CDATA["(Lower) Knob Position Vert."]]></Description>
			<Value>0.52</Value>
		</Length>
		<Integer Name="bUpperKnob">
			<Description><![CDATA["Upper Knob"]]></Description>
			<Value>0</Value>
		</Integer>
		<Length Name="knob_pl_x_up">
			<Description><![CDATA["Upper Knob Position Hor."]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.045</Value>
		</Length>
		<Length Name="knob_pl_y_up">
			<Description><![CDATA["Upper Knob Position Vert."]]></Description>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.3</Value>
		</Length>
		<Integer Name="gs_knob_p_m">
			<Description><![CDATA["Knob Position"]]></Description>
			<Value>1</Value>
		</Integer>
		<Length Name="knob_size">
			<Description><![CDATA["Knob Size"]]></Description>
			<Value>0.1</Value>
		</Length>
		<Material Name="gs_knob_mat">
			<Description><![CDATA["Knob"]]></Description>
			<Value>40</Value>
		</Material>
		<String Name="gs_cust_knob">
			<Description><![CDATA["Custom Knob Name"]]></Description>
			<Value><![CDATA[""]]></Value>
		</String>
		<Length Name="toe_h">
			<Description><![CDATA["Toe Height"]]></Description>
			<Value>0.09</Value>
		</Length>
		<Length Name="gs_customHandleLength">
			<Description><![CDATA[""]]></Description>
			<Value>0.1</Value>
		</Length>
		<Length Name="gs_customHandleDepth">
			<Description><![CDATA[""]]></Description>
			<Value>0.02</Value>
		</Length>
		<Length Name="gs_customHandleHeight">
			<Description><![CDATA[""]]></Description>
			<Value>0.02</Value>
		</Length>

		<!-- drawer: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="drawer">
			<Description><![CDATA["Drawers"]]></Description>
		</Title>
		<Boolean Name="bDrawer">
			<Description><![CDATA["Drawer"]]></Description>
			<Value>0</Value>
		</Boolean>
		<Integer Name="nKnobDrawers">
			<Description><![CDATA["No. of Knobs by Drawer"]]></Description>
			<Value>1</Value>
		</Integer>
		<Length Name="drawerDepth">
			<Description><![CDATA["Drawer Depth"]]></Description>
			<Value>0.5</Value>
		</Length>
		<Length Name="drawerKnobSize">
			<Description><![CDATA["Drawer Knob Size"]]></Description>
			<Value>0.1</Value>
		</Length>
		<Integer Name="iDrawerKnobPos">
			<Description><![CDATA["Drawer Knob Position"]]></Description>
			<Value>1</Value>
		</Integer>
		<Length Name="xDrawerKnobPos">
			<Description><![CDATA["Drawer Knob Position Hor."]]></Description>
			<Value>0</Value>
		</Length>
		<Length Name="zDrawerKnobPos">
			<Description><![CDATA["Drawer Knob Position Vert."]]></Description>
			<Value>0</Value>
		</Length>
	</Parameters>
</ParamSection>

<Picture MIME="image/png" SectVersion="19" SectionFlags="0" SubIdent="0" length_in_bytes="559" platform="Win">
	<![CDATA[
	696D6167652F706E670089504E470D0A1A0A0000000D49484452000000800000
	00800803000000F4E091F9000000EA504C5445FFFFFFF8F3EAF4EDDFF4EEE1FB
	F8F3ECDFC8E1CFABE3D2B0F4EDE0EDE1CCE3D2B1E5D5B6F5EEE2E6D6B6E6D6B7
	E6D5B6E8D8B8D9CBAF95928B918E8992908A93928DACA69AE7D6B7E8D7B7D9CC
	B2A2A2A3A1A1A3A1A2A3A2A2A484858997958FE5D4B4E7D9BFF1F0F0F2F2F3F1
	F1F1F3F3F3F5F5F59E9EA096938CE6D8BEECEBEBECECEDECECECEDEDEDF0F0F0
	ECECEFECEEF1ECEEF0EDEFF1F1F2F4999A9E908E89E5D4B5E6D6BAE8DFCFE8E0
	D0E8E0CFEAE2D1BFB8A9BEB4A1E5D5B5E4D3B2EAD9B8EBDAB9EDE0CBE2D1B0E4
	D4B5F5EEE1EDE2CDE4D3B3E6D6B8F5EEE3FDFBF8FBF9F5FCFAF6FEFDFBE518EA
	B4000000F64944415478DAEDDB5952C2501445510D182031010111314827BD62
	0336F46AD028D1F94FC77F7E5E995BAF8A54ED3D81B326700EF6AF4323A12F23
	A9061C99297D9969352063D9FAB28E01000000E09F00C795E7080059C7962700
	E44EF285A2B4D3D2991315503EAF5C78B2AADE65CD8D0CA8379AAD2B59ED4EB7
	2700F407C36B5937230000000000000000000000000000000000000000000000
	000000000000000000000000000000000000000000000000000000000040CC01
	B777F7B21EC693C7E880A7E797E94CD87CB174A3BF6C566B79AF6F3CAD000000
	1037C0BBBFD197FFA1067C065FFA0ABED5806DF8A3AFF07777EE0FFB41E0F5F1
	33716E0000000049454E44AE426082
	]]>
</Picture>

<Ancestry SectVersion="1" SectionFlags="0" SubIdent="0" Template="false">
	<MainGUID>F938E33A-329D-4A36-BE3E-85E126820996</MainGUID>
	<MainGUID>103E8D2C-8230-42E1-9597-46F84CCE28C0</MainGUID>
	<MainGUID>B2970E4C-8680-4061-8670-2F4B638F3953</MainGUID>
</Ancestry>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_VL SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
if bNotMacro then
	values "gs_detlevel_3D_m" 2,1
	values "iDoorStyle" 1,2,3,4,5,6,7,8,9,10,11,12, 13,14,15,16,17,18	!!Custom Panel(10)	1-12->Old styles, 13-18->new styles
	values "gs_knob_type_m" 1,2,3,4,5,6,7,8
	values "gs_knob_p_m" 0,1 	!!!(0-Vertical, 1-Horizontal)
	values "iDrawerKnobPos" 0,1 	!!!(0-Vertical, 1-Horizontal)
endif

! ==============================================================================
! Door Style Change
! ==============================================================================
if iDoorStyle <> iTempDoorStyle then
	if (iDoorStyle < 13 | iDoorStyle = 14) and (iTempDoorStyle > 12 & iTempDoorStyle <> 14) then
		doorFrameWidth = 0.1
		parameters doorFrameWidth = doorFrameWidth
	endif

	if (iDoorStyle > 12 & iDoorStyle <> 14) and (iTempDoorStyle < 13 | iTempDoorStyle = 14) then
		doorFrameWidth = 0.02
		parameters doorFrameWidth = doorFrameWidth
	endif

	if (iDoorStyle < 15 & iDoorStyle <> 13) and (iTempDoorStyle >= 15 | iTempDoorStyle = 13) then
		matDoorFrame = 15		! Wd-Pine Horizontal
		parameters matDoorFrame = matDoorFrame
	endif

	if (iDoorStyle >= 15 | iDoorStyle = 13) and (iTempDoorStyle < 15 & iTempDoorStyle <> 13) then
		matDoorFrame = 13		! Mtl-Chrome
		parameters matDoorFrame = matDoorFrame
	endif

	if iDoorStyle < 16 and iTempDoorStyle >= 16 then
		matDoorDecor1 = 15		! Wd-Pine Horizontal
		parameters matDoorDecor1 = matDoorDecor1
	endif

	if iDoorStyle >= 16 and iTempDoorStyle < 16 then
		matDoorDecor1 = 18		! Paint-1
		parameters matDoorDecor1 = matDoorDecor1
	endif

	if iDoorStyle = 13 and iTempDoorStyle <> 13 then
		nVerticalMullion = 0
		nHorizontalMullion = 3
		parameters nVerticalMullion = nVerticalMullion, nHorizontalMullion = nHorizontalMullion
	endif

	if iDoorStyle <> 13 and iTempDoorStyle = 13 then
		nVerticalMullion = 1
		nHorizontalMullion = 3
		parameters nVerticalMullion = nVerticalMullion, nHorizontalMullion = nHorizontalMullion
	endif

	iTempDoorStyle = iDoorStyle
	parameters iTempDoorStyle = iTempDoorStyle
endif

! ==============================================================================
! Door Parameters
! ==============================================================================
values "doorThickness" range [0.02,]
if iDoorStyle <= 12 then
	values "doorFrameWidth" range [0.03,]
else
	values "doorFrameWidth" range [0,]
endif
values "mullionWidth" range (0, )
values "nVerticalMullion" range [0,]
values "nHorizontalMullion" range [0,]
values "doorAngle3D" range [0, 90]

if iDoorStyle = 1 or iDoorStyle = 8 then
	lock "doorFrameWidth", "matDoorFrame"
	hideparameter "doorFrameWidth", "matDoorFrame"
endif

if iDoorStyle=10 or iDoorStyle>14 then
	lock "bBevel"
	hideparameter "bBevel"
endif

if iDoorStyle < 16 then
	lock "matDoorDecor2"
	hideparameter "matDoorDecor2"
endif

if iDoorStyle <> 3 and iDoorStyle <> 5 and iDoorStyle <> 7 and iDoorStyle <> 12 and iDoorStyle <> 13 then
	lock "mullionWidth", "nVerticalMullion", "nHorizontalMullion"
	hideparameter "mullionWidth", "nVerticalMullion", "nHorizontalMullion"
endif

if iDoorStyle <> 3 and iDoorStyle <> 5 and iDoorStyle <> 7 and iDoorStyle <> 9 and iDoorStyle <> 12 then
	lock "matDoorGlass"
	hideparameter "matDoorGlass"
else
	lock "matDoorDecor1"
	hideparameter "matDoorDecor1"
endif

if iDoorStyle <> 14 and iDoorStyle <> 15 then
	lock "matDoorMirror"
	hideparameter "matDoorMirror"
endif

if iDoorStyle = 10 or iDoorStyle = 99 then
	lock "doorFrameWidth", "matDoorFrame", "matDoorGlass", "matDoorMirror", "matDoorDecor1", "matDoorDecor2"
	hideparameter "doorFrameWidth", "matDoorFrame", "matDoorGlass", "matDoorMirror", "matDoorDecor1", "matDoorDecor2"

	dim gs_pt[]
	call "Object_Panel_Collection" parameters returned_parameters gs_pt
	values "doorCustomPanelName" gs_pt
else
	lock "doorCustomPanelName"
	hideparameter "doorCustomPanelName"
endif

! ==============================================================================
! Knob Parameters
! ==============================================================================
! ----- Knob Parameters-----
if not(bKnob) then
	hideparameter	"gs_knob_type_m", "knob_pl_x", "knob_pl_y", "knob_size", "gs_knob_mat",
					"xDrawerKnobPos", "zDrawerKnobPos", "nKnobDrawers"
	lock			"gs_knob_type_m", "knob_pl_x", "knob_pl_y", "knob_size", "gs_knob_mat",
					"xDrawerKnobPos", "zDrawerKnobPos", "nKnobDrawers"
endif

xKnobPosMod = 0
if gs_knob_type_m > 3 & gs_knob_p_m = 1 then xKnobPosMod = knob_size
values "knob_pl_x" range [0.02, doorWidth - 0.02 - xKnobPosMod]
yKnobPosMod = 0
if gs_knob_type_m > 3 & gs_knob_p_m = 0 then yKnobPosMod = knob_size
values "knob_pl_y" range [0.02 + yKnobPosMod, doorHeight - 0.02]
values "knob_size" range [0.045, )

]]>
</Script_VL>

<CalledMacros SectVersion="2" SectionFlags="0" SubIdent="0">
	<Macro>
		<MName><![CDATA["Object_Panel_Collection"]]></MName>
		<MainGUID>F846F2AE-D5D0-4EAA-894F-F4CAAF3737B6</MainGUID>
	</Macro>
	<Macro>
		<MName><![CDATA["Knob_Collection"]]></MName>
		<MainGUID>792251AA-3310-4E1F-B787-19075AC87358</MainGUID>
	</Macro>
	<Macro>
		<MName><![CDATA["fa_knobtyp"]]></MName>
		<MainGUID>29F83AA1-B564-436E-9586-290736E65631</MainGUID>
	</Macro>
	<Macro>
		<MName><![CDATA["LibraryGlobals13"]]></MName>
		<MainGUID>9137124A-DDF2-4A06-A532-5FC0CE873258</MainGUID>
	</Macro>
</CalledMacros>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_1D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_1D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_2D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
!
!! ======================================================================
!! Only Testing
!! ======================================================================
!
!! ----- Project -----
!unID = 1
!project2{3} 4, 270, 3+8192, 15
!
!! ----- DoorHeight, DoorWidth Hotspots -----
!hotspot2  0, 0, unID, doorWidth, 1+256		: unID=unID+1
!hotspot2 -1, 0, unID, doorWidth, 3			: unID=unID+1
!hotspot2  doorWidth, 0, unID, doorWidth, 2			: unID=unID+1
!
!hotspot2  0, doorHeight, unID, doorWidth, 1+256		: unID=unID+1
!hotspot2 -1, doorHeight, unID, doorWidth, 3			: unID=unID+1
!hotspot2  doorWidth, doorHeight, unID, doorWidth, 2			: unID=unID+1
!
!hotspot2  doorWidth,  0, unID, doorHeight, 1+256		: unID=unID+1
!hotspot2  doorWidth, -1, unID, doorHeight, 3			: unID=unID+1
!hotspot2  doorWidth,  doorHeight, unID, doorHeight, 2			: unID=unID+1
!
!hotspot2  0,  0, unID, doorHeight, 1+256		: unID=unID+1
!hotspot2  0, -1, unID, doorHeight, 3			: unID=unID+1
!hotspot2  0,  doorHeight, unID, doorHeight, 2			: unID=unID+1
!
!
!! ----- Knob Hotspots -----
!hotspot2 doorWidth-knob_pl_x, 0, unID, knob_pl_y,1+128 : unID=unID+1
!hotspot2 doorWidth-knob_pl_x, knob_pl_y, unID, knob_pl_y,2 : unID=unID+1
!hotspot2 doorWidth-knob_pl_x, -1, unID, knob_pl_y,3 : unID=unID+1
!
!hotspot2 doorWidth,            knob_pl_y, unID, knob_pl_x,1+128 : unID=unID+1
!hotspot2 doorWidth-knob_pl_x,  knob_pl_y, unID, knob_pl_x,2 : unID=unID+1
!hotspot2 doorWidth+1,          knob_pl_y, unID, knob_pl_x,3 : unID=unID+1
]]>
</Script_2D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_PR SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_PR>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_UI SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_UI>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Comment SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Comment>

<Copyright SectVersion="1" SectionFlags="0" SubIdent="0">
	<Author>GRAPHISOFT</Author>
	<License>
		<Type>CC BY-ND</Type>
		<Version>3.0</Version>
	</License>
</Copyright>

</Symbol>
