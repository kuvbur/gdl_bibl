<?xml version="1.0" encoding="UTF-8"?>
<Symbol IsArchivable="false" IsPlaceable="false" MainGUID="D2E3DAC2-596A-4F0A-AB35-0E3D6C36DB85" MigrationValue="Normal" Owner="1196638531" Signature="1196644685" Version="43">
<Ancestry SectVersion="1" SectionFlags="0" SubIdent="0" Template="false">
	<MainGUID>F938E33A-329D-4A36-BE3E-85E126820996</MainGUID>
	<MainGUID>103E8D2C-8230-42E1-9597-46F84CCE28C0</MainGUID>
	<MainGUID>56A4A77E-2509-450D-94D0-A816189B7DF4</MainGUID>
	<MainGUID>06BA412F-7262-4221-A9B3-1A3E410E9170</MainGUID>
</Ancestry>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_3D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
if gs_detlevel_3D_m=1 or (GLOB_CONTEXT>20 and GLOB_CONTEXT<40) then gs_resol=4	! Simple
if gs_detlevel_3D_m=0 then END													! Off

resol gs_resol
pen gs_cont_pen
material gs_gutter_ds_mat

if gs_shadow=0 then SHADOW OFF
if gs_shadow=1 then SHADOW ON

if gs_gutter_ds_profile_num = 1 then
	addy - (dis_wall - gs_gutter_dd)
else
	addy - (dis_wall - gs_gutter_bb)
endif

! -----------------------------------------------------------------------------
! 3D HOTSPOTS
! -----------------------------------------------------------------------------
unID=gs_unID

if gs_HtpsMacro=0 then									! Hotspots not used in  Downspout Complex
	! --- ZZYZX ---
	! --- Upper Part / Length ---
	addy -gs_gutter_os_off-gs_gutter_os_u_lgth*TAN(gs_gutter_os_u_ang)
		HOTSPOT 0,	0,	0,		unID,	ZZYZX,	1+128	:unID=unID+1
		HOTSPOT 0,	0,	-1,		unID,	ZZYZX,	3		:unID=unID+1
		HOTSPOT 0,	0,	ZZYZX,	unID,	ZZYZX,	2		:unID=unID+1
	del 1
	! --- Lower Part / Length ---
	addy -gs_gutter_os_l_lgth*TAN(gs_gutter_os_l_ang)
		HOTSPOT 0,	0,	0,		unID,	ZZYZX,	1+256	:unID=unID+1
		HOTSPOT 0,	0,	-1,		unID,	ZZYZX,	3		:unID=unID+1
		HOTSPOT 0,	0,	ZZYZX,	unID,	ZZYZX,	2+128	:unID=unID+1
	del 1
endif

! --- Upper Part Vertical Length ---
add 0, -gs_gutter_os_off, ZZYZX
	HOTSPOT 0,	0,	0,						unID,	gs_gutter_os_u_lgth,	1+128	:unID=unID+1
	HOTSPOT 0,	0,	gs_gutter_os_u_lgth,	unID,	gs_gutter_os_u_lgth,	3		:unID=unID+1
	HOTSPOT 0,	0,	-gs_gutter_os_u_lgth,	unID,	gs_gutter_os_u_lgth,	2		:unID=unID+1
del 1

! --- Lower Part Vertical Length ---
	HOTSPOT 0,	0,	0,						unID,	gs_gutter_os_l_lgth,	1+128	:unID=unID+1
	HOTSPOT 0,	0,	-1,						unID,	gs_gutter_os_l_lgth,	3		:unID=unID+1
	HOTSPOT 0,	0,	gs_gutter_os_l_lgth,	unID,	gs_gutter_os_l_lgth,	2		:unID=unID+1

! --- Middle Part / Horizontal Offset ---
addz gs_gutter_os_l_lgth+gs_gutter_os_m_lgth
	HOTSPOT 0,	0,					0,	unID,	gs_gutter_os_off,	1+128	:unID=unID+1
	HOTSPOT 0,	1,					0,	unID,	gs_gutter_os_off,	3		:unID=unID+1
	HOTSPOT 0,	-gs_gutter_os_off,	0,	unID,	gs_gutter_os_off,	2		:unID=unID+1
del 1

add	 0,-gs_gutter_os_off,(gs_gutter_os_m_lgth+gs_gutter_os_l_lgth)		! Position of Origo

! -----------------------------------------------------------------------------
! 3D MOdelL
! -----------------------------------------------------------------------------
call "m_DSProfiles" PARAMETERS gs_gutter_ds_profile_num=gs_gutter_ds_profile_num, gs_gutter_aa=gs_gutter_aa, gs_gutter_bb=gs_gutter_bb, gs_gutter_dd=gs_gutter_dd, gs_mask=1

rotz 180

tube nsp/3,6,4+8+16+32,
	get(nsp),
	0,	 gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang) - gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang-2*gs_gutter_u_end_ang),	2*gs_gutter_os_u_lgth,	0,
	0,	 gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang),	gs_gutter_os_u_lgth,	0,
	0,	 0,		0,		0,
	0,	-gs_gutter_os_off,	-gs_gutter_os_m_lgth,	0,
	0,	-gs_gutter_os_off+gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang),	-gs_gutter_os_m_lgth - gs_gutter_os_l_lgth,	0,
	0,	-gs_gutter_os_off+(gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang)) - gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang-2*gs_gutter_l_end_ang),	-gs_gutter_os_m_lgth-2*gs_gutter_os_l_lgth,	0

! -----------------------------------------------------------------------------
! 3D HOTSPOTS
! -----------------------------------------------------------------------------
if gs_HtpsMacro=0 then 										! Hotspots not used in  Downspout Complex
	! --- Upper Part / Angle ---
	HOTSPOT  0,	0,												0,												unID,	gs_gutter_os_u_ang,	6	: unID=unID+1
	HOTSPOT  0,	0,												gs_gutter_os_u_lgth/2,							unID,	gs_gutter_os_u_ang,	4+128	: unID=unID+1
	HOTSPOT  0,	gs_gutter_os_u_lgth/2*SIN(gs_gutter_os_u_ang),	gs_gutter_os_u_lgth/2*COS(gs_gutter_os_u_ang),	unID,	gs_gutter_os_u_ang,	5	: unID=unID+1
	HOTSPOT  -1, 0,												0,												unID, 	gs_gutter_os_u_ang,	7	: unID=unID+1

	! --- Lower Part / Angle ---
	add 0, -gs_gutter_os_off, -gs_gutter_os_off / TAN(gs_gutter_os_ang)
		HOTSPOT  0,	0,												 0,												unID,	gs_gutter_os_l_ang,	6	:unID=unID+1
		HOTSPOT  0,	0,												-gs_gutter_os_l_lgth/2,							unID,	gs_gutter_os_l_ang,	4+128	:unID=unID+1
		HOTSPOT  0,-gs_gutter_os_l_lgth/2*SIN(-gs_gutter_os_l_ang),	-gs_gutter_os_l_lgth/2*COS(gs_gutter_os_l_ang),	unID,	gs_gutter_os_l_ang,	5	:unID=unID+1
		HOTSPOT  1,	0,												 0,												unID,	gs_gutter_os_l_ang,	7	:unID=unID+1
	del 1
endif

! -----------------------------------------------------------------------------
! Upper and LowerPart / END Angle
! -----------------------------------------------------------------------------
		! --- Calculation for End Angle hotspots ---
		if gs_gutter_ds_profile_num=1 then						! Round Profile
			atvitu = gs_gutter_dd / COS(gs_gutter_os_u_ang-gs_gutter_u_end_ang)
		else
			atvitu = gs_gutter_bb / COS(gs_gutter_os_u_ang-gs_gutter_u_end_ang)
		endif
		zeltu = atvitu * SIN(up_end_ang_temp)
		yeltu = atvitu * COS(up_end_ang_temp)

		! --- Lower End ---
		if gs_gutter_ds_profile_num=1 then						! Round Profile
			atvitl = gs_gutter_dd / COS(gs_gutter_os_l_ang-gs_gutter_l_end_ang)
		else
			atvitl = gs_gutter_bb / COS(gs_gutter_os_l_ang-gs_gutter_l_end_ang)
		endif
		zeltl = atvitl * SIN(low_end_ang_temp)
		yeltl = atvitl * COS(low_end_ang_temp)

		if gs_gutter_ds_profile_num=1 then						! HOTSPOTs ROUND Profile
			if GLOB_CONTEXT <> 4 then
				XX= 0			 : YY= yeltu 		: gosub "htsp_upper_end_ang"
				XX= 0			 : YY=-yeltu 		: gosub "htsp_upper_end_ang2"
				XX= gs_gutter_dd : YY= 0	 : HOTSPOT XX, gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang), gs_gutter_os_u_lgth	:unID=unID+1
				XX=-gs_gutter_dd : YY= 0	 : HOTSPOT XX, gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang), gs_gutter_os_u_lgth	:unID=unID+1
				XX=-gs_gutter_dd : YY= -dis_wall 	: gosub "htsp_dis_wall"
			endif

			if gs_HtpsMacro=0 then								! Hotspots not used in  Downspout Complex
				add 0, -gs_gutter_os_off, -gs_gutter_os_off / TAN(gs_gutter_os_ang)
					XX= 0			 : YY= yeltl	 : GOSUB "htsp_lower_end_ang"
					XX= 0			 : YY=-yeltl	 : GOSUB "htsp_lower_end_ang2"
					XX= gs_gutter_dd : YY= 0		 : HOTSPOT XX, gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang), - gs_gutter_os_l_lgth	:unID=unID+1
					XX=-gs_gutter_dd : YY= 0		 : HOTSPOT XX, gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang), - gs_gutter_os_l_lgth	:unID=unID+1
				del 1
			endif
		else													! HOTSPOTs NOT_ROUND profile

			XX= gs_gutter_aa : YY= yeltu		: gosub "htsp_upper_end_ang"
			XX= gs_gutter_aa : YY=-yeltu		: gosub "htsp_upper_end_ang2"
			XX=-gs_gutter_aa : YY= yeltu		: gosub "htsp_upper_end_ang"
			XX=-gs_gutter_aa : YY=-yeltu		: gosub "htsp_upper_end_ang2"
			XX=-gs_gutter_aa : YY= -dis_wall 	: gosub "htsp_dis_wall"

			if gs_HtpsMacro=0 then 								! (Hotspots not used in  Downspout Complex
				add 0, -gs_gutter_os_off, -gs_gutter_os_off / TAN(gs_gutter_os_ang)
					XX= gs_gutter_aa : YY= yeltl: GOSUB "htsp_lower_end_ang"
					XX= gs_gutter_aa : YY=-yeltl: GOSUB "htsp_lower_end_ang2"
					XX=-gs_gutter_aa : YY= yeltl: GOSUB "htsp_lower_end_ang"
					XX=-gs_gutter_aa : YY=-yeltl: GOSUB "htsp_lower_end_ang2"
				del 1
			endif
		endif

del 1
del 1	
end
! =============================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
! =============================================================================

! -----------------------------------------------------------------------------
"htsp_upper_end_ang":						! 3D Hotspots for Upper End Angle
! -----------------------------------------------------------------------------
Uppmxang = (90-gs_gutter_os_u_ang)/2
	add XX,gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang), gs_gutter_os_u_lgth
	rotX (Uppmxang)

	HOTSPOT 0,	atvitu,		0,		UnID,	up_end_ang_temp,	4+128,	gs_gutter_u_end_ang	:unID=unID+1
	HOTSPOT -1,	0,			0,		UnID,	up_end_ang_temp,	7,		gs_gutter_u_end_ang	:unID=unID+1
	HOTSPOT 0,	0,			0,		UnId,	up_end_ang_temp,	6,		gs_gutter_u_end_ang	:unID=unID+1
	HOTSPOT 0,	YY, 		-zeltu,	UnID,	up_end_ang_temp, 	5,		gs_gutter_u_end_ang	:unID=unID+1

	del 2
return


! -----------------------------------------------------------------------------
"htsp_upper_end_ang2":						! 3D Hotspots for Upper End Angle2
! -----------------------------------------------------------------------------
Uppmxang = (90-gs_gutter_os_u_ang)/2
	add XX,gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang), gs_gutter_os_u_lgth
	rotX (Uppmxang)

	HOTSPOT 0,	-atvitu,	0,		UnID,	up_end_ang_temp,	4+128,	gs_gutter_u_end_ang	:unID=unID+1
	HOTSPOT -1,	0,			0,		UnID,	up_end_ang_temp,	7,		gs_gutter_u_end_ang	:unID=unID+1
	HOTSPOT 0,	0,			0,		UnId,	up_end_ang_temp,	6,		gs_gutter_u_end_ang	:unID=unID+1
	HOTSPOT 0,	YY, 		zeltu,	UnID,	up_end_ang_temp, 	5,		gs_gutter_u_end_ang	:unID=unID+1

	del 2
return


! -----------------------------------------------------------------------------
"htsp_lower_end_ang":						! 3D Hotspots for Lower End Angle
! -----------------------------------------------------------------------------
Lowmxang = (90-gs_gutter_os_l_ang)/2
	add XX, gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang), -gs_gutter_os_l_lgth
	rotX (-Lowmxang)

	HOTSPOT 0,	atvitl,		0,		UnID,	low_end_ang_temp,	4+128,	gs_gutter_l_end_ang	:unID=unID+1
	HOTSPOT 1,	0,			0,		UnID,	low_end_ang_temp,	7,		gs_gutter_l_end_ang	:unID=unID+1
	HOTSPOT 0,	0,			0,		UnId,	low_end_ang_temp,	6,		gs_gutter_l_end_ang	:unID=unID+1
	HOTSPOT 0,	YY, 		zeltl,	UnID,	low_end_ang_temp, 	5,		gs_gutter_l_end_ang	:unID=unID+1

	del 2
return


! -----------------------------------------------------------------------------
"htsp_lower_end_ang2":						! 3D Hotspots for Lower End Angle2
! -----------------------------------------------------------------------------
Lowmxang = (90-gs_gutter_os_l_ang)/2
	add XX, gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang), -gs_gutter_os_l_lgth
	rotX (-Lowmxang)

	HOTSPOT 0,	-atvitl,	0,		UnID,	low_end_ang_temp,	4+128,	gs_gutter_l_end_ang	:unID=unID+1
	HOTSPOT 1,	0,			0,		UnID,	low_end_ang_temp,	7,		gs_gutter_l_end_ang	:unID=unID+1
	HOTSPOT 0,	0,			0,		UnId,	low_end_ang_temp,	6,		gs_gutter_l_end_ang	:unID=unID+1
	HOTSPOT 0,	YY, 		-zeltl,	UnID,	low_end_ang_temp, 	5,		gs_gutter_l_end_ang	:unID=unID+1

	del 2
return

! -----------------------------------------------------------------------------
"htsp_dis_wall":			!3D Hotsports for Dis. Between Downspout Axes and Wall
! -----------------------------------------------------------------------------
	addy - (gs_gutter_os_off)
	!new editable hotspots, using fix unID because of migration
	hotspot XX, YY, 		- (gs_gutter_os_m_lgth + gs_gutter_os_l_lgth), 40001, dis_wall, 1+128
	hotspot XX, 0, 			- (gs_gutter_os_m_lgth + gs_gutter_os_l_lgth), 40002, dis_wall, 2
	hotspot XX, YY-1, 		- (gs_gutter_os_m_lgth + gs_gutter_os_l_lgth), 40003, dis_wall, 3

	hotspot -XX, YY, 		- (gs_gutter_os_m_lgth + gs_gutter_os_l_lgth), 40004, dis_wall, 1+128
	hotspot -XX, 0, 		- (gs_gutter_os_m_lgth + gs_gutter_os_l_lgth), 40005, dis_wall, 2
	hotspot -XX, YY-1, 		- (gs_gutter_os_m_lgth + gs_gutter_os_l_lgth), 40006, dis_wall, 3
	del 1
return

]]>
</Script_3D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_2D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[

PEN gs_cont_pen
LINE_TYPE gs_line_type
FILL gs_fill_type
unID=gs_unID

if gs_gutter_ds_profile_num = 1 then
	add2 0, - (dis_wall - gs_gutter_dd)
else
	add2 0, - (dis_wall - gs_gutter_bb)
endif

ACCUTPLANEHEIGHT = (GLOB_CSTORY_ELEV + GLOB_CUTPLANES_INFO [1]) - GLOB_HSTORY_ELEV - vertical_offset

if gs_gutter_ds_profile_num=1 then										! Round Profile
	temp_aa=gs_gutter_dd
	_tempDepth = gs_gutter_dd
else
	temp_aa=gs_gutter_aa
	_tempDepth = gs_gutter_bb
endif

! ------ OFFSET 2D Contour ------
call "m_DSProfiles" parameters 	gs_gutter_ds_profile_num	= gs_gutter_ds_profile_num, 
								gs_gutter_aa				= gs_gutter_aa,
								gs_gutter_bb				= gs_gutter_bb,
								gs_gutter_dd				= gs_gutter_dd

add2 0, -gs_gutter_os_off
rot2 180

if ACCUTPLANEHEIGHT < -EPS and vertical_offset < EPS then
		! ------ Above the cut plane ------
	if iFloorPlanDisplayType = DISPLAY_OVERHEAD_PROJ then

		gs_line_type = overhead_Line_Type
		cut_pen = gs_cont_pen
		gs_gutter_os_l_ang = 0

		gosub "Cut_lower_tube_O"
		gosub "Cut_lower_tube_PR"
	endif

else
	_lowerCornerAngle = (gs_gutter_os_ang - gs_gutter_os_l_ang) / 2
	_lowerCorner_HH = _tempDepth * sin(_lowerCornerAngle) / cos(gs_gutter_os_ang - _lowerCornerAngle)

	_upperCornerAngle = (gs_gutter_os_ang + gs_gutter_os_u_ang) / 2
	_upperCorner_HH = _tempDepth * sin(_upperCornerAngle) / cos(gs_gutter_os_ang - _upperCornerAngle)

	! -----------------------------------------------------------------------------
	! Projected
	! -----------------------------------------------------------------------------
	if iFloorPlanDisplayType = DISPLAY_PROJECTED then

		! ------ Cut the LOWER tube ------
		if  ACCUTPLANEHEIGHT <= gs_gutter_os_l_lgth - _lowerCorner_HH then
			gosub "Cut_lower_tube_PR"
		endif

		! ------ Cut the LOWER Corner ------
		if  ACCUTPLANEHEIGHT > gs_gutter_os_l_lgth - _lowerCorner_HH and \
			ACCUTPLANEHEIGHT < gs_gutter_os_l_lgth + _lowerCorner_HH then
			gosub "Cut_lower_corner_PR"
		endif

		! ------ Cut the MIDDLE tube ------
		if  ACCUTPLANEHEIGHT >= gs_gutter_os_l_lgth + _lowerCorner_HH and \
			ACCUTPLANEHEIGHT <= zzyzx - gs_gutter_os_u_lgth - _upperCorner_HH then
			gosub "Cut_middle_tube_PR"
		endif

		! ------ Cut the UPPER Corner ------
		if  ACCUTPLANEHEIGHT > zzyzx - gs_gutter_os_u_lgth - _upperCorner_HH and \
			ACCUTPLANEHEIGHT < zzyzx - gs_gutter_os_u_lgth + _upperCorner_HH then
			gosub "Cut_upper_corner_PR"
		endif

		! ------ Cut the UPPER tube ------
		if  ACCUTPLANEHEIGHT >= zzyzx - gs_gutter_os_u_lgth + _upperCorner_HH and \
			ACCUTPLANEHEIGHT < zzyzx  then
			gosub "Cut_upper_tube_PR"
		endif

		! ------ OVERHEAD ------
		if  ACCUTPLANEHEIGHT >= zzyzx then
			gosub "hor_offset_hotspots"
			gosub "Overhead_PR"
		endif
	endif

	! -----------------------------------------------------------------------------
	! Projected With Overhead
	! -----------------------------------------------------------------------------
	if iFloorPlanDisplayType = DISPLAY_OVERHEAD_PROJ then
		gosub "hor_offset_hotspots"

		! ------ Cut the LOWER tube ------
		if  ACCUTPLANEHEIGHT <= gs_gutter_os_l_lgth - _lowerCorner_HH then
			gosub "Cut_lower_tube_O"
			gosub "Cut_lower_tube_PR"
		endif

		! ------ Cut the LOWER Corner ------
		if  ACCUTPLANEHEIGHT > gs_gutter_os_l_lgth - _lowerCorner_HH and \
			ACCUTPLANEHEIGHT < gs_gutter_os_l_lgth + _lowerCorner_HH then
			gosub "Cut_lower_tube_O"
			gosub "Cut_lower_corner_PR"
		endif

		! ------ Cut the MIDDLE tube ------
		if  ACCUTPLANEHEIGHT >= gs_gutter_os_l_lgth + _lowerCorner_HH and \
			ACCUTPLANEHEIGHT <= zzyzx - gs_gutter_os_u_lgth - _upperCorner_HH then
			gosub "Cut_middle_tube_O"
			gosub "Cut_middle_tube_PR"
		endif

		! ------ Cut the UPPER Corner ------
		if  ACCUTPLANEHEIGHT > zzyzx - gs_gutter_os_u_lgth - _upperCorner_HH and \
			ACCUTPLANEHEIGHT < zzyzx - gs_gutter_os_u_lgth + _upperCorner_HH then
			gosub "Cut_upper_tube_O"
			gosub "Cut_upper_corner_PR"
		endif

		! ------ Cut the UPPER tube ------
		if  ACCUTPLANEHEIGHT >= zzyzx - gs_gutter_os_u_lgth + _upperCorner_HH and \
			ACCUTPLANEHEIGHT < zzyzx  then
			gosub "Cut_upper_tube_O"
			gosub "Cut_upper_tube_PR"
		endif

		! ------ OVERHEAD ------
		if  ACCUTPLANEHEIGHT >= zzyzx then
			gosub "Overhead_PR"
		endif
	endif
endif

! -----------------------------------------------------------------------------
! Symbolic
! -----------------------------------------------------------------------------
if iFloorPlanDisplayType = DISPLAY_SYMBOLIC then
	gs_gutter_os_l_ang = 0
	_frame_fill = 1+2+4+64
	gosub "lower_end"
endif

! -----------------------------------------------------------------------------
! Outlines Only
! -----------------------------------------------------------------------------
if iFloorPlanDisplayType = DISPLAY_OUTLINES then
	gosub "hor_offset_hotspots"
	gosub "Overhead_PR"
endif

! -----------------------------------------------------------------------------
! Overhead All
! -----------------------------------------------------------------------------
if iFloorPlanDisplayType = DISPLAY_OVERHEAD_ALL then

	gosub "hor_offset_hotspots"

	gs_line_type = overhead_Line_Type
	cut_pen = gs_cont_pen
	gs_gutter_os_l_ang = 0

	gosub "Cut_lower_tube_O"
	gosub "Cut_lower_tube_PR"
endif

del 2
del 1
end
! =============================================================================
!END	!END	!END	!END	!END	!END	!END	!END	!END	!END
! =============================================================================

! -----------------------------------------------------------------------------
"lower_end":
! -----------------------------------------------------------------------------
	ADD2 0, -gs_gutter_os_off + (gs_gutter_os_l_lgth * TAN(gs_gutter_os_l_ang))
		atfog = 1/ COS(gs_gutter_os_l_ang - gs_gutter_l_end_ang)
		IF gs_gutter_os_l_ang > 0 THEN MUL2 1, atfog * COS(gs_gutter_l_end_ang)

	! ------ Lower End Hotspots ------
	IF gs_HtpsMacro=0 THEN !!!***(Hotspots not used in  Downspout Complex)
		IF gs_gutter_ds_profile_num=1 THEN						! Round Profile
			HOTSPOT2 gs_gutter_dd, 0,  unID : unID = unID+1
			HOTSPOT2 0, gs_gutter_dd,  unID : unID = unID+1
			HOTSPOT2 0, -gs_gutter_dd,  unID : unID = unID+1
			HOTSPOT2 -gs_gutter_dd, 0,  unID : unID = unID+1

		unID_ = unID
		unID = 2000

			FOR kk=1 to 4
				!!! *** RADIUS
				HOTSPOT2 0, 0, unID, gs_gutter_dd, 1+128, gs_gutter_dd2 : unID=unID+1
				HOTSPOT2 -1, 0, unID, gs_gutter_dd, 3, gs_gutter_dd2 : unID=unID+1
				HOTSPOT2 gs_gutter_dd, 0, unID, gs_gutter_dd, 2, gs_gutter_dd2 : unID=unID+1
				ROT2 90
				NEXT kk
			DEL 4

			add2 0, -dis_wall
				HOTSPOT2  0,	 0,			unID, dis_wall, 1+128, 	dis_wall	:	unID=unID+1
				HOTSPOT2  0,	 dis_wall,	unID, dis_wall, 2, 		dis_wall	:	unID=unID+1
				HOTSPOT2  0,	 -1,		unID, dis_wall, 3, 		dis_wall	:	unID=unID+1
			del 1
		
			unID = unID_

		ELSE
			HOTSPOT2 gs_gutter_aa, gs_gutter_bb,  unID : unID = unID+1
			HOTSPOT2 -gs_gutter_aa, gs_gutter_bb,  unID : unID = unID+1
			HOTSPOT2 -gs_gutter_aa, -gs_gutter_bb,  unID : unID = unID+1
			HOTSPOT2 gs_gutter_aa, -gs_gutter_bb,  unID : unID = unID+1

			unID_ = unID
			unID = 4000

			!!! *** Width
			ADD2 0,gs_gutter_bb
				HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 -1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1

				HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 -gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1
			DEL 1
			ADD2 0,-gs_gutter_bb
				HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 -1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1

				HOTSPOT2 0, 0, unID, gs_gutter_aa, 1+128, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 1, 0, unID, gs_gutter_aa, 3, gs_gutter_aa2 : unID=unID+1
				HOTSPOT2 -gs_gutter_aa, 0, unID, gs_gutter_aa, 2, gs_gutter_aa2 : unID=unID+1
			DEL 1

			!!! *** Length
			ADD2 gs_gutter_aa,0
				HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, -1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1

				HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, 1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, -gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1
			DEL 1
			ADD2 -gs_gutter_aa,0
				HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, -1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1

				HOTSPOT2 0, 0, unID, gs_gutter_bb, 1+128, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, 1, unID, gs_gutter_bb, 3, gs_gutter_bb2 : unID=unID+1
				HOTSPOT2 0, -gs_gutter_bb,  unID, gs_gutter_bb, 2, gs_gutter_bb2 : unID=unID+1
			DEL 1

			add2 0, -dis_wall
				HOTSPOT2  0,	 0,			unID, dis_wall, 1+128, 	dis_wall	:	unID=unID+1
				HOTSPOT2  0,	 dis_wall,	unID, dis_wall, 2, 		dis_wall	:	unID=unID+1
				HOTSPOT2  0,	 -1,		unID, dis_wall, 3, 		dis_wall	:	unID=unID+1
			del 1
	
			unID = unID_

		ENDIF
	ENDIF

		POLY2_B nsp/3, _frame_fill, gs_fill_pen, gs_back_pen,
			use(nsp)

		IF gs_gutter_os_l_ang > 0 THEN DEL 1
	DEL 1
return


! -----------------------------------------------------------------------------
"lower_tube":
! -----------------------------------------------------------------------------
	if sin (gs_gutter_os_l_ang) > EPS then
		POLY2_B 4, _frame_fill, gs_fill_pen, gs_back_pen,
			-temp_aa,	-gs_gutter_os_off + (gs_gutter_os_l_lgth * TAN (gs_gutter_os_l_ang)), 0,
			 temp_aa,	-gs_gutter_os_off + (gs_gutter_os_l_lgth * TAN (gs_gutter_os_l_ang)), 1,
			temp_aa, -gs_gutter_os_off , 0,
			-temp_aa,	-gs_gutter_os_off , 1
	endif
return


! -----------------------------------------------------------------------------
"lower_break":
! -----------------------------------------------------------------------------
	add2 0, -gs_gutter_os_off
	if ABS(gs_gutter_os_ang + gs_gutter_os_l_ang) - 180 < EPS then
		atfog2 = 1 / COS( ((gs_gutter_os_ang+gs_gutter_os_l_ang)/2) )
		mul2 1, atfog2 * COS( - gs_gutter_os_ang + (gs_gutter_os_ang+gs_gutter_os_l_ang)/2)

			POLY2_B nsp/3, _frame_fill, gs_fill_pen, gs_back_pen, use(nsp)
		del 1
	endif
	del 1
return


! -----------------------------------------------------------------------------
"middle_tube":
! -----------------------------------------------------------------------------
	POLY2_B 4, _frame_fill, gs_fill_pen, gs_back_pen,
		-temp_aa, 0, 0,
		temp_aa, 0, 1,
		temp_aa, -gs_gutter_os_off, 0,
		-temp_aa,-gs_gutter_os_off, 1
return


! -----------------------------------------------------------------------------
"upper_break":
! -----------------------------------------------------------------------------
	if ABS(gs_gutter_os_ang + gs_gutter_os_u_ang) - 180 < EPS then
		atfog3 = 1 / COS( gs_gutter_os_u_ang - ((gs_gutter_os_ang+gs_gutter_os_u_ang)/2) )
		mul2 1, atfog3 * COS( (gs_gutter_os_ang+gs_gutter_os_u_ang)/2)

			POLY2_B nsp/3, _frame_fill, gs_fill_pen, gs_back_pen, use(nsp)
		del 1
	endif
return


! -----------------------------------------------------------------------------
"upper_tube":
! -----------------------------------------------------------------------------
	if sin (gs_gutter_os_u_ang) > EPS then
		POLY2_B 4, _frame_fill, gs_fill_pen, gs_back_pen,
			-temp_aa, 0, 0,
			temp_aa, 0, 1,
			temp_aa, gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang) , 0,
			-temp_aa,gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang) , 1
	endif
return


! -----------------------------------------------------------------------------
"upper_end":
! -----------------------------------------------------------------------------
	add2 0, gs_gutter_os_u_lgth * TAN(gs_gutter_os_u_ang)
		atfog = 1/ COS(gs_gutter_os_u_ang - gs_gutter_u_end_ang)
		if gs_gutter_os_u_ang > 0 then mul2 1, atfog * COS(gs_gutter_u_end_ang)

		! --- Upper End Hotspots ---
		HOTSPOT2 0,0, unID : unID = unID+1 	!!Centerpoint
		if gs_gutter_ds_profile_num=1 then						! Round Profile
			HOTSPOT2 gs_gutter_dd, 0,  unID : unID = unID+1
			HOTSPOT2 0, gs_gutter_dd,  unID : unID = unID+1
			HOTSPOT2 0, -gs_gutter_dd,  unID : unID = unID+1
			HOTSPOT2 -gs_gutter_dd, 0,  unID : unID = unID+1
		else
			HOTSPOT2 gs_gutter_aa, gs_gutter_bb,  unID : unID = unID+1
			HOTSPOT2 -gs_gutter_aa, gs_gutter_bb,  unID : unID = unID+1
			HOTSPOT2 -gs_gutter_aa, -gs_gutter_bb,  unID : unID = unID+1
			HOTSPOT2 gs_gutter_aa, -gs_gutter_bb,  unID : unID = unID+1
		endif

		POLY2_B nsp/3, _frame_fill, gs_fill_pen, gs_back_pen,
			use(nsp)

		if gs_gutter_os_u_ang > 0 then del 1
	del 1
return


! -----------------------------------------------------------------------------
"Cut_lower_tube_PR":
! -----------------------------------------------------------------------------
	if sin (gs_gutter_os_l_ang) > EPS then
		line_type gs_line_type
		pen gs_cont_pen

		drawindex 10
		_frame_fill = 1+2+4+64
		gosub "lower_end"

		add2 0, -gs_gutter_os_off + (gs_gutter_os_l_lgth-ACCUTPLANEHEIGHT) * tan (gs_gutter_os_l_ang)

		drawindex 10
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-temp_aa,	ACCUTPLANEHEIGHT * tan (gs_gutter_os_l_ang), 0,
			 temp_aa,	ACCUTPLANEHEIGHT * tan (gs_gutter_os_l_ang), 1,
			 temp_aa,	0 , 0,
			-temp_aa,	0 , 1

		pen cut_pen
		mul2 1, 1 / cos(gs_gutter_os_l_ang)
		drawindex 30
			POLY2_B nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen,
				use(nsp)
		del 2
	else
		line_type gs_line_type
		pen cut_pen

		drawindex 30
		_frame_fill = 1+2+4+64
		gosub "lower_end"
		gosub "lower_break"
	endif
return


! -----------------------------------------------------------------------------
"Cut_lower_corner_PR":
! -----------------------------------------------------------------------------
	line_type gs_line_type
	pen gs_cont_pen
	drawindex 10
		_frame_fill = 1+2+4+64
		gosub "lower_end"
		gosub "lower_tube"

	add2 0, -gs_gutter_os_off

	add2 0, (gs_gutter_os_l_lgth - ACCUTPLANEHEIGHT) * tan (gs_gutter_os_l_ang) + (ACCUTPLANEHEIGHT - gs_gutter_os_l_lgth + _lowerCorner_HH) * (tan(gs_gutter_os_ang) + tan(gs_gutter_os_l_ang)) / 2

	line_type gs_line_type
	pen cut_pen
	mul2 1, (1 / cos(gs_gutter_os_l_ang)) + (ACCUTPLANEHEIGHT - (gs_gutter_os_l_lgth - _lowerCorner_HH)) * (tan(gs_gutter_os_ang) + tan(gs_gutter_os_l_ang)) / (2 * _tempDepth)
	DRAWINDEX 30
	POLY2_B nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen,
		use(nsp)
	del 2
return


! -----------------------------------------------------------------------------
"Cut_middle_tube_PR":
! -----------------------------------------------------------------------------
	line_type gs_line_type
	pen gs_cont_pen
	drawindex 10
		_frame_fill = 1+2+4+64
		gosub "lower_end"
		gosub "lower_break"
		gosub "lower_tube"

	add2 0, -gs_gutter_os_off

	line_type gs_line_type
	pen gs_cont_pen
	drawindex 10
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-temp_aa,	(ACCUTPLANEHEIGHT-gs_gutter_os_l_lgth) * tan (gs_gutter_os_ang), 0,
			 temp_aa,	(ACCUTPLANEHEIGHT-gs_gutter_os_l_lgth) * tan (gs_gutter_os_ang), 1,
			 temp_aa,	0 , 0,
			-temp_aa,	0 , 1

	add2 0, (ACCUTPLANEHEIGHT-gs_gutter_os_l_lgth) * tan (gs_gutter_os_ang)

	line_type gs_line_type
	pen cut_pen
	mul2 1, 1 / cos(gs_gutter_os_ang)
	DRAWINDEX 30
	POLY2_B nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen,
		use(nsp)
	del 2
return


! -----------------------------------------------------------------------------
"Cut_upper_corner_PR":
! -----------------------------------------------------------------------------
	line_type gs_line_type
	pen gs_cont_pen
	drawindex 10
		_frame_fill = 1+2+4+64
		gosub "lower_end"
		gosub "lower_tube"
		gosub "lower_break"
		gosub "middle_tube"

	add2 0, -(zzyzx - gs_gutter_os_u_lgth - ACCUTPLANEHEIGHT) * tan (gs_gutter_os_u_ang) - (zzyzx - gs_gutter_os_u_lgth + _upperCorner_HH - ACCUTPLANEHEIGHT) * (tan(gs_gutter_os_ang) - tan(gs_gutter_os_u_ang)) / 2

	line_type gs_line_type
	pen cut_pen
	mul2 1, (1 / cos(gs_gutter_os_u_ang)) + (zzyzx - gs_gutter_os_u_lgth + _upperCorner_HH - ACCUTPLANEHEIGHT) * (tan(gs_gutter_os_ang) - tan(gs_gutter_os_u_ang)) / (2 * _tempDepth)
	DRAWINDEX 30
	POLY2_B nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen,
		use(nsp)
	del 2
return


! -----------------------------------------------------------------------------
"Cut_upper_tube_PR":
! -----------------------------------------------------------------------------
	line_type gs_line_type
	pen gs_cont_pen
		_frame_fill = 1+2+4+64
		drawindex 10
		gosub "lower_end"
		gosub "lower_tube"
		gosub "lower_break"
		gosub "middle_tube"
		gosub "upper_break"

	if sin (gs_gutter_os_u_ang) > EPS then
		add2 0, (ACCUTPLANEHEIGHT-(zzyzx-gs_gutter_os_u_lgth)) * tan (gs_gutter_os_u_ang)
		drawindex 10
		POLY2_B 4, 1+2+4+64, gs_fill_pen, gs_back_pen,
			-temp_aa,	-(ACCUTPLANEHEIGHT-(zzyzx-gs_gutter_os_u_lgth)) * tan (gs_gutter_os_u_ang), 0,
			 temp_aa,	-(ACCUTPLANEHEIGHT-(zzyzx-gs_gutter_os_u_lgth)) * tan (gs_gutter_os_u_ang), 1,
			 temp_aa,	0 , 0,
			-temp_aa,	0 , 1

		pen cut_pen
		mul2 1, 1 / cos(gs_gutter_os_u_ang)
		drawindex 30
		POLY2_B nsp/3, 1+2+4+64, gs_fill_pen, gs_back_pen,
			use(nsp)
		del 2
	else
		line_type gs_line_type
		pen cut_pen
		drawindex 30
		_frame_fill = 1+2+4+64
		gosub "upper_end"
	endif
return


! -----------------------------------------------------------------------------
"Overhead_PR":
! -----------------------------------------------------------------------------
	line_type gs_line_type
	pen gs_cont_pen
		_frame_fill = 1+2+4+64
		gosub "lower_tube"
		gosub "lower_break"
		gosub "lower_end"
		gosub "middle_tube"
		gosub "upper_break"
		gosub "middle_tube"
		gosub "upper_break"
		gosub "upper_tube"
		gosub "upper_end"
return


! -----------------------------------------------------------------------------
"Cut_lower_tube_O":
! -----------------------------------------------------------------------------
	line_type overhead_Line_Type
	pen gs_cont_pen
	drawindex 20
	_frame_fill = 1+4
	gosub "lower_break"
	gosub "lower_tube"
	gosub "upper_end"
	gosub "upper_break"
	gosub "upper_tube"
	gosub "middle_tube"
return


! -----------------------------------------------------------------------------
"Cut_middle_tube_O":
! -----------------------------------------------------------------------------
	line_type overhead_Line_Type
	pen gs_cont_pen

	_frame_fill = 1+4
	gosub "upper_break"
	gosub "upper_end"
	gosub "upper_tube"

	drawindex 20
	POLY2_B 5, 1, gs_fill_pen, gs_back_pen,
		-temp_aa,	-gs_gutter_os_off+(ACCUTPLANEHEIGHT-gs_gutter_os_l_lgth) * tan (gs_gutter_os_ang), 0,
		 temp_aa,	-gs_gutter_os_off+(ACCUTPLANEHEIGHT-gs_gutter_os_l_lgth) * tan (gs_gutter_os_ang), 1,
		 temp_aa,	0 , 0,
		-temp_aa,	0 , 1,
		-temp_aa,	-gs_gutter_os_off+(ACCUTPLANEHEIGHT-gs_gutter_os_l_lgth) * tan (gs_gutter_os_ang), 1

return


! -----------------------------------------------------------------------------
"Cut_upper_tube_O":
! -----------------------------------------------------------------------------
	if sin (gs_gutter_os_u_ang) > EPS then
		line_type overhead_Line_Type

		_frame_fill = 1+4
		drawindex 10
		gosub "upper_end"
		gosub "upper_break"

		add2 0, (ACCUTPLANEHEIGHT-(zzyzx-gs_gutter_os_u_lgth)) * tan (gs_gutter_os_u_ang)

		POLY2_B 4, 1+4, gs_fill_pen, gs_back_pen,
			-temp_aa,	(zzyzx-ACCUTPLANEHEIGHT) * tan (gs_gutter_os_u_ang), 0,
			 temp_aa,	(zzyzx-ACCUTPLANEHEIGHT) * tan (gs_gutter_os_u_ang), 1,
			 temp_aa,	0 , 0,
			-temp_aa,	0 , 1
		del 1
	endif
return


! -----------------------------------------------------------------------------
"hor_offset_hotspots":
! -----------------------------------------------------------------------------
!add2 0, +gs_gutter_os_off
	rot2 -180
	add2 0, gs_gutter_os_off

	HOTSPOT2 0, 0,					unID, gs_gutter_os_off, 1+256	: unID = unID+1		! Base
	HOTSPOT2 0, -gs_gutter_os_off,	unID, gs_gutter_os_off, 2		: unID = unID+1		! Move
	HOTSPOT2 0, 1, unID, gs_gutter_os_off, 3	: unID = unID+1
	del 2
return
]]>
</Script_2D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_1D SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
EPS = 0.0001  												! precision of ZERO

!------------ Floor Plan Display type integer definitions ----------------------
DISPLAY_PROJECTED			= 1
DISPLAY_OVERHEAD_PROJ		= 2
DISPLAY_SYMBOLIC			= 3
DISPLAY_OUTLINES			= 4
DISPLAY_OVERHEAD_ALL		= 5

! -----------------------------------------------------------------------------
! USA profiles
! -----------------------------------------------------------------------------
if gs_gutter_ds_profile=`Круглый 3"` OR gs_gutter_ds_profile=`Круглый 4"` OR gs_gutter_ds_profile=`Специальный - Круглый` then
	parameters gs_gutter_ds_profile_num=1
endif

if gs_gutter_ds_profile=`Прямоугольный 2"*3"` OR gs_gutter_ds_profile=`Прямоугольный 3"*2"` OR gs_gutter_ds_profile=`Прямоугольный 3"*4"` OR gs_gutter_ds_profile=`Прямоугольный 4"*3"` OR gs_gutter_ds_profile=`Специальный - Прямоугольный` then
	parameters gs_gutter_ds_profile_num=2
endif

if gs_gutter_ds_profile=`Закругленный 2"*3"` OR gs_gutter_ds_profile=`Закругленный 3"*2"` OR gs_gutter_ds_profile=`Закругленный 3"*4"` OR gs_gutter_ds_profile=`Закругленный 4"*3"` OR gs_gutter_ds_profile=`Специальный - Закругленный` then
	parameters gs_gutter_ds_profile_num=3
endif


if gs_gutter_ds_profile=`Круглый 3"` then
	parameters gs_gutter_dd=3*0.5*0.0254
				gs_gutter_dd=3*0.5*0.0254
	parameters gs_gutter_dd2=gs_gutter_dd*2
				gs_gutter_dd2=gs_gutter_dd*2
	lock "gs_gutter_dd2"
endif
if gs_gutter_ds_profile=`Круглый 4"` then
	parameters gs_gutter_dd=4*0.5*0.0254
				gs_gutter_dd=4*0.5*0.0254
	parameters gs_gutter_dd2=gs_gutter_dd*2
				gs_gutter_dd2=gs_gutter_dd*2
	lock "gs_gutter_dd2"
endif

if gs_gutter_ds_profile=`Прямоугольный 2"*3"` OR gs_gutter_ds_profile=`Закругленный 2"*3"` then
	parameters gs_gutter_aa=0.0254
				gs_gutter_aa=0.0254
	parameters gs_gutter_aa2=gs_gutter_aa*2
				gs_gutter_aa2=gs_gutter_aa*2
	parameters gs_gutter_bb=0.0381
				gs_gutter_bb=0.0381
	parameters gs_gutter_bb2=gs_gutter_bb*2
				gs_gutter_bb2=gs_gutter_bb*2
	lock "gs_gutter_aa2"
	lock "gs_gutter_bb2"
endif

if gs_gutter_ds_profile=`Прямоугольный 3"*2"` OR gs_gutter_ds_profile=`Закругленный 3"*2"` then
	parameters gs_gutter_aa=0.0381
				gs_gutter_aa=0.0381
	parameters gs_gutter_aa2=gs_gutter_aa*2
				gs_gutter_aa2=gs_gutter_aa*2
	parameters gs_gutter_bb=0.0254
				gs_gutter_bb=0.0254
	parameters gs_gutter_bb2=gs_gutter_bb*2
				gs_gutter_bb2=gs_gutter_bb*2
	lock "gs_gutter_aa2"
	lock "gs_gutter_bb2"
endif

if gs_gutter_ds_profile=`Прямоугольный 3"*4"` OR gs_gutter_ds_profile=`Закругленный 3"*4"` then
	parameters gs_gutter_aa=0.0381
				gs_gutter_aa=0.0381
	parameters gs_gutter_aa2=gs_gutter_aa*2
				gs_gutter_aa2=gs_gutter_aa*2
	parameters gs_gutter_bb=0.0508
				gs_gutter_bb=0.0508
	parameters gs_gutter_bb2=gs_gutter_bb*2
				gs_gutter_bb2=gs_gutter_bb*2
	lock "gs_gutter_aa2"
	lock "gs_gutter_bb2"
endif

if gs_gutter_ds_profile=`Прямоугольный 4"*3"` OR gs_gutter_ds_profile=`Закругленный 4"*3"` then
	parameters gs_gutter_aa=0.0508
				gs_gutter_aa=0.0508
	parameters gs_gutter_aa2=gs_gutter_aa*2
				gs_gutter_aa2=gs_gutter_aa*2
	parameters gs_gutter_bb=0.0381
				gs_gutter_bb=0.0381
	parameters gs_gutter_bb2=gs_gutter_bb*2
				gs_gutter_bb2=gs_gutter_bb*2
	lock "gs_gutter_aa2"
	lock "gs_gutter_bb2"
endif
]]>
</Script_1D>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_PR SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_PR>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_UI SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_UI>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_VL SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[
parameters ac_bottomlevel = 0
parameters ac_toplevel = ZZYZX

! -------------------------------------------------------------------------------
! A, B, ZZYZX
! -------------------------------------------------------------------------------
if gs_gutter_ds_profile_num=1 then 								! Round Profile
	PARAMETERS A=gs_gutter_dd2
	PARAMETERS B=gs_gutter_dd2
else
	PARAMETERS A=gs_gutter_aa2
	PARAMETERS B=gs_gutter_bb2
endif
LOCK "A","B"

! -------------------------------------------------------------------------------
!  Downspout Profile
! -------------------------------------------------------------------------------
if GLOB_MODPAR_NAME = "gs_gutter_aa" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	PARAMETERS 	gs_gutter_aa2 = gs_gutter_aa*2
				gs_gutter_aa2 = gs_gutter_aa*2
endif

if GLOB_MODPAR_NAME = "gs_gutter_aa2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	PARAMETERS 	gs_gutter_aa = gs_gutter_aa2/2
				gs_gutter_aa = gs_gutter_aa2/2
endif

if GLOB_MODPAR_NAME = "gs_gutter_bb" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	PARAMETERS 	gs_gutter_bb2 = gs_gutter_bb*2
				gs_gutter_bb2 = gs_gutter_bb*2
endif

if GLOB_MODPAR_NAME = "gs_gutter_bb2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	PARAMETERS 	gs_gutter_bb = gs_gutter_bb2/2
				gs_gutter_bb = gs_gutter_bb2/2
endif

if GLOB_MODPAR_NAME = "gs_gutter_dd" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	PARAMETERS 	gs_gutter_dd2 = gs_gutter_dd*2
				gs_gutter_dd2 = gs_gutter_dd*2
endif

if GLOB_MODPAR_NAME = "gs_gutter_dd2" or GLOB_MODPAR_NAME = "gs_gutter_ds_profile" then
	PARAMETERS 	gs_gutter_dd = gs_gutter_dd2/2
				gs_gutter_dd = gs_gutter_dd2/2
endif

VALUES "gs_gutter_aa" RANGE (0,)
VALUES "gs_gutter_aa2" RANGE (0,)
VALUES "gs_gutter_bb" RANGE (0,)
VALUES "gs_gutter_bb2" RANGE (0,)
VALUES "gs_gutter_dd" RANGE (0,)
VALUES "gs_gutter_dd2" RANGE (0,)

if gs_gutter_ds_profile_num = 1 then
	values "dis_wall" range [gs_gutter_dd, )
	hideparameter "gs_gutter_aa2"
	hideparameter "gs_gutter_bb2"
else
	values "dis_wall" range [gs_gutter_bb, )
	hideparameter "gs_gutter_dd2"
endif

! -------------------------------------------------------------------------------
!  Setting of Geometry
! -------------------------------------------------------------------------------

IF  GLOB_MODPAR_NAME = "gs_gutter_os_l_lgth" OR GLOB_MODPAR_NAME = "gs_gutter_os_u_lgth" THEN
	gs_gutter_os_m_lgth = ZZYZX - gs_gutter_os_l_lgth - gs_gutter_os_u_lgth
	PARAMETERS gs_gutter_os_m_lgth = gs_gutter_os_m_lgth
ELSE
	gs_gutter_os_l_lgth = ZZYZX-gs_gutter_os_u_lgth-gs_gutter_os_m_lgth
	PARAMETERS gs_gutter_os_l_lgth = gs_gutter_os_l_lgth
ENDIF

gs_gutter_os_ang=ATN(gs_gutter_os_off/gs_gutter_os_m_lgth)
PARAMETERS gs_gutter_os_ang = gs_gutter_os_ang

VALUES "ZZYZX" RANGE [gs_gutter_os_u_lgth+gs_gutter_os_m_lgth+2*gs_gutter_bb,)
VALUES "gs_gutter_os_u_lgth" RANGE [2*gs_gutter_bb, ZZYZX - gs_gutter_os_l_lgth - gs_gutter_os_off/TAN(89) ]
VALUES "gs_gutter_os_l_lgth" RANGE [2*gs_gutter_bb, ZZYZX - gs_gutter_os_u_lgth - gs_gutter_os_off/TAN(89)]
VALUES "gs_gutter_os_off" RANGE [gs_gutter_bb,gs_gutter_os_m_lgth*TAN(89)]
VALUES "gs_gutter_os_ang" RANGE [ATN(gs_gutter_os_off/((gs_gutter_os_m_lgth+gs_gutter_os_l_lgth)-2*gs_gutter_bb)),89]
VALUES "gs_gutter_os_m_lgth" RANGE [gs_gutter_os_off/TAN(89),)

! ---  gs_gutter_os_m_lgth_temp -------------------------------------------------
	! if the Horizontal Offset is changed the Lower Part Vertical Length is also changed because
	! the summa of Middle Part Vertical Length and Lower Part Vertical Length is get the same
PARAMETERS gs_gutter_os_m_lgth_temp = gs_gutter_os_m_lgth

! --- End Angles ----------------------------------------------------------------
Uppmxang = (90-gs_gutter_os_u_ang)/2
values "gs_gutter_u_end_ang" RANGE [-Uppmxang, 90-Uppmxang]

Lowmxang = (90-gs_gutter_os_l_ang)/2
values "gs_gutter_l_end_ang" RANGE [-Lowmxang, 90-Lowmxang]

! --- End Angles(synchronised temporary variables) ------------------------------
if up_end_ang_temp < ((360-90)/2)+90 and up_end_ang_temp > 90 then PARAMETERS up_end_ang_temp=90
if up_end_ang_temp >= ((360-90)/2)+90 and up_end_ang_temp < 360 then PARAMETERS up_end_ang_temp=0

if low_end_ang_temp < ((360-90)/2)+90 and low_end_ang_temp > 90 then PARAMETERS low_end_ang_temp=90
if low_end_ang_temp >= ((360-90)/2)+90 and low_end_ang_temp < 360 then PARAMETERS low_end_ang_temp=0

if GLOB_MODPAR_NAME = "up_end_ang_temp" then
	PARAMETERS gs_gutter_u_end_ang = up_end_ang_temp - (Uppmxang)
else
	PARAMETERS up_end_ang_temp = gs_gutter_u_end_ang + (Uppmxang)
endif

if GLOB_MODPAR_NAME = "low_end_ang_temp" then
	PARAMETERS gs_gutter_l_end_ang = low_end_ang_temp - (Lowmxang)
else
	PARAMETERS low_end_ang_temp = gs_gutter_l_end_ang + (Lowmxang)
endif

! --- Upper and Lower Angles(synchronised temporary variables) ------------------
if gs_gutter_os_u_ang < 225 and gs_gutter_os_u_ang > 89 then parameters gs_gutter_os_u_ang = 89
if gs_gutter_os_u_ang >= 225 and gs_gutter_os_u_ang < 360 then parameters gs_gutter_os_u_ang = 0

if gs_gutter_os_l_ang < 225 and gs_gutter_os_l_ang > 89 then parameters gs_gutter_os_l_ang = 89
if gs_gutter_os_l_ang >= 225 and gs_gutter_os_l_ang < 360 then parameters gs_gutter_os_l_ang = 0

if gs_gutter_os_u_ang < 0 then PARAMETERS gs_gutter_os_u_ang=0
if gs_gutter_os_l_ang < 0 then PARAMETERS gs_gutter_os_l_ang=0

! -------------------------------------------------------------------------------
! 3D Representation
! -------------------------------------------------------------------------------
VALUES "gs_detlevel_3D_m" 2,1,0												! 2-Detailed, 1-Simple, 0-Off

VALUES "gs_resol" RANGE [3,)
if gs_detlevel_3D_m=1 OR gs_gutter_ds_profile_num=2 then LOCK "gs_resol"	! 1-Simple, 2-Rectangular

! -------------------------------------------------------------------------------
! 2D Representation
! -------------------------------------------------------------------------------
if gs_cont_pen= 0 then PARAMETERS gs_cont_pen=2
if gs_fill_pen=0 then PARAMETERS gs_fill_pen=91

VALUES "floorPlanDisplayType" `Сечение и Низ`, `Сечение и Верх`, `Символический`, `Только Контуры`, `Вся Проекция`
VALUES "iFloorPlanDisplayType" 	DISPLAY_PROJECTED, \
								DISPLAY_OVERHEAD_PROJ, \
								DISPLAY_SYMBOLIC, \
								DISPLAY_OUTLINES, \
								DISPLAY_OVERHEAD_ALL

if GLOB_MODPAR_NAME = "floorPlanDisplayType" then
	iFloorPlanDisplayType = DISPLAY_OVERHEAD_PROJ
	if floorPlanDisplayType = `Сечение и Низ`		then iFloorPlanDisplayType = DISPLAY_PROJECTED
	if floorPlanDisplayType = `Символический`		then iFloorPlanDisplayType = DISPLAY_SYMBOLIC
	if floorPlanDisplayType = `Только Контуры`	then iFloorPlanDisplayType = DISPLAY_OUTLINES
	if floorPlanDisplayType = `Вся Проекция`	then iFloorPlanDisplayType = DISPLAY_OVERHEAD_ALL
	parameters iFloorPlanDisplayType = iFloorPlanDisplayType
else
	floorPlanDisplayType = `Сечение и Верх`
	if iFloorPlanDisplayType = DISPLAY_PROJECTED		then floorPlanDisplayType = `Сечение и Низ`
	if iFloorPlanDisplayType = DISPLAY_SYMBOLIC			then floorPlanDisplayType = `Символический`
	if iFloorPlanDisplayType = DISPLAY_OUTLINES			then floorPlanDisplayType = `Только Контуры`
	if iFloorPlanDisplayType = DISPLAY_OVERHEAD_ALL		then floorPlanDisplayType = `Вся Проекция`
	parameters floorPlanDisplayType = floorPlanDisplayType
endif

if iFloorPlanDisplayType = DISPLAY_PROJECTED then
	HIDEPARAMETER "overhead_Line_Type"
endif
if iFloorPlanDisplayType = DISPLAY_SYMBOLIC or iFloorPlanDisplayType = DISPLAY_OUTLINES then
	HIDEPARAMETER "cut_pen", "overhead_Line_Type"
endif
if iFloorPlanDisplayType = DISPLAY_OVERHEAD_ALL then
	HIDEPARAMETER "cut_pen", "gs_line_type"
endif
]]>
</Script_VL>

<Comment SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Comment>

<ParamSection SectVersion="22" SectionFlags="0" SubIdent="0">
	<ParamSectHeader>
		<AutoHotspots>false</AutoHotspots>
		<StatBits>
			<STBit_FixSize/>
		</StatBits>
		<WDLeftFrame>0</WDLeftFrame>
		<WDRightFrame>0</WDRightFrame>
		<WDTopFrame>0</WDTopFrame>
		<WDBotFrame>0</WDBotFrame>
		<LayFlags>65535</LayFlags>
		<WDMirrorThickness>0</WDMirrorThickness>
		<WDWallInset>0</WDWallInset>
	</ParamSectHeader>
	<Parameters>
		<Length Name="A">
			<Description><![CDATA["Dimension 1"]]></Description>
			<Fix/>
			<Value>0.1</Value>
		</Length>
		<Length Name="B">
			<Description><![CDATA["Dimension 2"]]></Description>
			<Fix/>
			<Value>0.1</Value>
		</Length>
		<Length Name="ZZYZX">
			<Description><![CDATA["Height"]]></Description>
			<Fix/>
			<Value>0.8</Value>
		</Length>
		<Boolean Name="AC_show2DHotspotsIn3D">
			<Description><![CDATA["Show 2D Hotspots in 3D"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<Length Name="ac_bottomlevel">
			<Description><![CDATA["Bottom Level"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>1</Value>
		</Length>
		<Length Name="ac_toplevel">
			<Description><![CDATA["Top Level"]]></Description>
			<Fix/>
			<Flags>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Length>
		<Length Name="dis_wall">
			<Description><![CDATA["Distance Between Downspout Axes and Wall"]]></Description>
			<Value>0.05</Value>
		</Length>

		<!-- Gutter_DS__sp0: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="Gutter_DS__sp0">
			<Description><![CDATA["Downspout"]]></Description>
		</Title>
		<String Name="gs_gutter_ds_profile">
			<Description><![CDATA["Downspout Profile"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["Круглый"]]></Value>
		</String>
		<Integer Name="gs_gutter_ds_profile_num">
			<Description><![CDATA["Downspout Profile"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>2</Value>
		</Integer>
		<Length Name="gs_gutter_aa">
			<Description><![CDATA["Profile Width"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.05</Value>
		</Length>
		<Length Name="gs_gutter_aa2">
			<Description><![CDATA["Profile Width"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.1</Value>
		</Length>
		<Length Name="gs_gutter_bb">
			<Description><![CDATA["Profile Depth"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.05</Value>
		</Length>
		<Length Name="gs_gutter_bb2">
			<Description><![CDATA["Profile Depth"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.1</Value>
		</Length>
		<Length Name="gs_gutter_dd">
			<Description><![CDATA["Profile Radius"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.05</Value>
		</Length>
		<Length Name="gs_gutter_dd2">
			<Description><![CDATA["Profile Diameter"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.1</Value>
		</Length>

		<!-- gs_geometry: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_geometry">
			<Description><![CDATA["Setting of Geometry"]]></Description>
		</Title>
		<Length Name="gs_gutter_os_u_lgth">
			<Description><![CDATA["Upper Part Vert. Length"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.3</Value>
		</Length>
		<Angle Name="gs_gutter_os_u_ang">
			<Description><![CDATA["Upper Part Angle"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Angle>
		<Angle Name="gs_gutter_u_end_ang">
			<Description><![CDATA["Upper End Angle"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Angle>
		<Angle Name="up_end_ang_temp">
			<Description><![CDATA["Upper End Angle"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>45</Value>
		</Angle>
		<Length Name="gs_gutter_os_off">
			<Description><![CDATA["Horizontal Offset"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.3</Value>
		</Length>
		<Length Name="gs_gutter_os_m_lgth">
			<Description><![CDATA["Middle Part Vert. Length"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.3</Value>
		</Length>
		<Length Name="gs_gutter_os_m_lgth_temp">
			<Description><![CDATA["Middle Part Vert. Length"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0.3</Value>
		</Length>
		<Angle Name="gs_gutter_os_ang">
			<Description><![CDATA["Middle Part Angle"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>45</Value>
		</Angle>
		<Length Name="gs_gutter_os_l_lgth">
			<Description><![CDATA["Lower Part Vert. Length"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0.2</Value>
		</Length>
		<Angle Name="gs_gutter_os_l_ang">
			<Description><![CDATA["Lower Part Angle"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Angle>
		<Angle Name="gs_gutter_l_end_ang">
			<Description><![CDATA["Lower End Angle"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Angle>
		<Angle Name="low_end_ang_temp">
			<Description><![CDATA["Lower End Angle"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>45</Value>
		</Angle>
		<Length Name="vertical_offset">
			<Description><![CDATA["Vertical offset"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Length>

		<!-- gs_3d_representation: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_3d_representation">
			<Description><![CDATA["3D Representation"]]></Description>
		</Title>
		<Integer Name="gs_detlevel_3D_m">
			<Description><![CDATA["3D Detail Level (0-Off,1-Simple,2-Detailed)"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>2</Value>
		</Integer>
		<Integer Name="gs_resol">
			<Description><![CDATA["Resolution"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>12</Value>
		</Integer>
		<Boolean Name="gs_shadow">
			<Description><![CDATA["Shadow"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>0</Value>
		</Boolean>

		<!-- gs_2d_representation: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_2d_representation">
			<Description><![CDATA["2D Representation"]]></Description>
		</Title>
		<String Name="floorPlanDisplayType">
			<Description><![CDATA["Floor Plan Display"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value><![CDATA["Сечение и Верх"]]></Value>
		</String>
		<Integer Name="iFloorPlanDisplayType">
			<Description><![CDATA["Floor Plan Display"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>2</Value>
		</Integer>
		<PenColor Name="cut_pen">
			<Description><![CDATA["Cut Pen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>24</Value>
		</PenColor>
		<PenColor Name="gs_cont_pen">
			<Description><![CDATA["Contour Pen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>84</Value>
		</PenColor>
		<LineType Name="gs_line_type">
			<Description><![CDATA["Line Type"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>1</Value>
		</LineType>
		<LineType Name="overhead_Line_Type">
			<Description><![CDATA["Overhead Line Type"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>4</Value>
		</LineType>
		<FillPattern Name="gs_fill_type">
			<Description><![CDATA["Fill Type"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>65</Value>
		</FillPattern>
		<PenColor Name="gs_fill_pen">
			<Description><![CDATA["Fill Pen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>91</Value>
		</PenColor>
		<PenColor Name="gs_back_pen">
			<Description><![CDATA["Fill Background Pen"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>91</Value>
		</PenColor>

		<!-- gs_material: PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK ===== PARAMETER BLOCK -->

		<Title Name="gs_material">
			<Description><![CDATA["Surfaces"]]></Description>
		</Title>
		<Material Name="gs_gutter_ds_mat">
			<Description><![CDATA["Offset"]]></Description>
			<Flags>
				<ParFlg_Child/>
			</Flags>
			<Value>47</Value>
		</Material>
		<Boolean Name="gs_HtpsMacro">
			<Description><![CDATA["Hotspots not called in Macro"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>0</Value>
		</Boolean>
		<Integer Name="gs_unID">
			<Description><![CDATA["Start unID Num. of Hotspots"]]></Description>
			<Flags>
				<ParFlg_Child/>
				<ParFlg_Hidden/>
			</Flags>
			<Value>2000</Value>
		</Integer>
	</Parameters>
</ParamSection>

<CalledMacros SectVersion="2" SectionFlags="0" SubIdent="0">
	<Macro>
		<MName><![CDATA["m_DSProfiles"]]></MName>
		<MainGUID>2AA24506-3E95-489A-A896-6C35F18C006F</MainGUID>
	</Macro>
</CalledMacros>

<Picture MIME="image/png" SectVersion="19" SectionFlags="0" SubIdent="0" length_in_bytes="1411" platform="Win">
	<![CDATA[
	696D6167652F706E670089504E470D0A1A0A0000000D49484452000000800000
	00800803000000F4E091F90000005D504C5445FFFFFF88868CD5D2DB8D8A91DA
	D6E0CDCAD27D7B81817E85BDBAC2E5E3E94646484E4E50D2CED8535255C4C1CA
	B5B2BAADABB1C0BDC6A2A1A46D6C705E5D603E3D40E1DEE5EEEDF19493986A68
	6D9D9BA1F0EEF2B2AFB8908E9476747A2C986682000004D749444154785ED55B
	697AE34A0814D09B16AF4BB69979F73FE613D0B89D3940E59BB22CFF04175540
	ACCEF4CF619ED3DBF43EFF5CFC8F6BCE9763E6F9EDEDFD7DFE858ECF44A7E325
	E7BCED97829991F1335139EDB135BA7D1CF944C0F89742B5FECE395FB6DC7126
	6402850A69028ACD38B812328137DE2EBFCF791B097C1151053A8077A46D4B9B
	2590F25715280373526C7ACB798FDF48A812FD8226F096061A9152002D414A89
	B76702A2148013501518D812A852D11A60E3206F8953932A55E81D97802B8087
	06343E96012F815E6C2204DB900D91053711C226E0DF3CCAA00914AA500D18D4
	0B5E02292252C17D80531422E72252D11AD01A78FC6000AC81E43A5402D48622
	55A0B3205AA1DE7353136035A0789A404B40042E41A8D0BCA036C46AC043BB10
	438405D807BC070C6465006EC3C4297CD8483328580D308F3A3431005BB185ED
	CD3827561B0A9481509FA5E14B6911C16A2012604DC05C089D8631099D02FC3E
	90D2AB09D85C80B56164F0320D8B4C38B8FA38E07D00DD0939C550761BE297D2
	D80A6D1A823B6174A14E4223AD80BCA3C77154203722F99912B8129D01F8307A
	96C0668148A11F59C97A27340A80368CF89140414FC328FF60405D806EC54180
	6F4424053D8C8615B29010C137A2BE94C75A0E75417CFF2E032F01BE11A5A0A1
	1574238AF8BD14DE074A02EF84A31534AB809CC17D60FC81D8C891902EE806E0
	F895AC10C915E882881D1A285605868A309498D918500E08BA11750BD85A4E6A
	44226A400DBCCCE35648C41753F452EA6268229D013A63FF3C770AFACF748554
	05F304001FBD04FCD2883405AA44102B7E2CCBE193E4DA9A3764DF8AB5044218
	2B1E96C0E14672FD2A2A4117017D4C005C2DF61258AD09084955128E13009F83
	02BDDD5500DD0702A1E0B8BC605D964E80F782D304C07F5D02A3089682DF1156
	E4EF0CAC375581DB0044017DCB603D7833042E06F3E15B11D65B113216D40957
	98150756F1BD4C33A8C4202B1E9E1A78AC779320518575A3D35F1414B26757AE
	830BB01B050B777B68103A8474A343AFC168C8DE8DC0565CFFCA801C906E1422
	0C2B8A1801B886FCB104060595446014CCA182418139810468C5C368058FB5F8
	38520077A3B5BFF70C6C3108A017036560298301E06210581FBA1B511502AE87
	BC7CC7CD9B612595E215DA8D566741CC8B4EC19F09B818444BFAB46EE4435126
	D862B086121F8B59D157039920388CEFFFD833B8F76F4FB0044E3DBAC5DFAFE2
	0CE012983E23FE7EED58C944004C809D8247C772330A2AF0077472FD05D662D1
	B513012978BCE056456B007CA02E167F40C88E38E21298E9F3F09AC05DAA6B00
	06CE89F3E94BCAFD6E3A2C224248063865CE29A7B4BFF3F9DC6722908197733D
	96462B4258069847F89CFD50055403899345CE9E44138296200EB665B62CFC5C
	0B948114300EF0254803398E9BA335A0E0C801EF82E783CCB0A110613BA1FBD0
	EF2642826B8079BF32770D1429580692BD789C6D122922E827894389780DA401
	F683B6D84E1822083136016BC0C25B029977F463A61FD03E3014C06CC308DD07
	9223CE3816826A80870F1519BF0FB8F60C71BE0D3F0B5C0296412B82D6404A43
	86995584D059100A0834A2829F86FC74416E54C00C7411C4A78A508A4CF079EC
	9F310B1AD205C92F853340D42624034383318E33F6FFC0A7385C150CF0044652
	1E940A459379FA29247DA5E99FC3FF2C504131FFA09AF30000000049454E44AE
	426082
	]]>
</Picture>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_FWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_FWM>

<!-- GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT ===== GDL SCRIPT -->

<Script_BWM SectVersion="20" SectionFlags="0" SubIdent="0">
<![CDATA[]]>
</Script_BWM>

<Copyright SectVersion="1" SectionFlags="0" SubIdent="0">
	<Author>GRAPHISOFT</Author>
	<License>
		<Type>CC BY-ND</Type>
		<Version>3.0</Version>
	</License>
</Copyright>

</Symbol>
